
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		Если ТипЗнч(Параметры.ПараметрКоманды) = Тип("Массив") Тогда
			МассивСсылок = Параметры.ПараметрКоманды;
		Иначе
			МассивСсылок = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Параметры.ПараметрКоманды);
		КонецЕсли;
		
		ОтборДокументы.Очистить();
		Для каждого Ссылка Из МассивСсылок Цикл
			
			Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
				ОтборДоговор = Ссылка;
			Иначе
				ОтборДокументы.Добавить(Ссылка);
			КонецЕсли;
			
		КонецЦикла;
		
		ФормироватьПриОткрытии = Истина;
		Элементы.Документы.Видимость = Ложь;
		Элементы.Договор.Видимость = Ложь;
		
	КонецЕсли;
	
	ДанныеОтчета = 1;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ФормироватьПриОткрытии Тогда
		СформироватьОтчет();
	Иначе
		УстановитьСостояниеОтчетНеСформирован();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДоговорПриИзменении(Элемент)
	УстановитьСостояниеОтчетНеСформирован();
КонецПроцедуры

&НаКлиенте
Процедура ДокументыПриИзменении(Элемент)
	УдалитьПустыеЗначенияИзСписка(ОтборДокументы);
	УстановитьСостояниеОтчетНеСформирован();
КонецПроцедуры

&НаКлиенте
Процедура ДанныеОтчетаПриИзменении(Элемент)
	УстановитьСостояниеОтчетНеСформирован();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьОтчет()
	
	ОбъектОтчет = РеквизитФормыВЗначение("Отчет");
	ЕстьДанныеДляОтображения = Ложь;
	ОбъектОтчет.СформироватьОтчет(Результат, ОтборДоговор, ОтборДокументы.ВыгрузитьЗначения(), ДанныеОтчета, ЕстьДанныеДляОтображения);
	Если ЕстьДанныеДляОтображения Тогда
		Элементы.Результат.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.НеИспользовать;
		Элементы.Результат.ОтображениеСостояния.Видимость = Ложь;
	Иначе
		Элементы.Результат.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
		Элементы.Результат.ОтображениеСостояния.Текст = НСтр("ru = 'Нет данных для отображения.'");
		Элементы.Результат.ОтображениеСостояния.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Процедура устанавливает отображение состояния "Отчет не сформирован" при изменении полей отчета.
//
&НаКлиенте
Процедура УстановитьСостояниеОтчетНеСформирован()
	
	Элементы.Результат.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
	Элементы.Результат.ОтображениеСостояния.Текст = НСтр("ru = 'Отчет не сформирован. Нажмите ""Сформировать"" для получения отчета.'");
	Элементы.Результат.ОтображениеСостояния.Видимость = Истина;
	
КонецПроцедуры // УстановитьСостояниеОтчетНеСформирован()

&НаКлиенте
Процедура Сформировать(Команда)
	
	Если ОтборДоговор.Пустая() И ОтборДокументы.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru='Не указан отбор по документу или договору'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,, "ОтборДоговор");
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	СформироватьОтчет();
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПустыеЗначенияИзСписка(СписокЗначений)
	Индекс = СписокЗначений.Количество();
	Пока Индекс > 0 Цикл
		Индекс = Индекс - 1;
		Если Не ЗначениеЗаполнено(СписокЗначений[Индекс].Значение) Тогда
			СписокЗначений.Удалить(Индекс);
		КонецЕсли;
	КонецЦикла
КонецПроцедуры

#КонецОбласти
