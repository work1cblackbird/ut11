#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПериодОтбора = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(
		КомпоновщикНастроек.ПользовательскиеНастройки, "ПериодОтбора").Значение; // СтандартныйПериод -
	Организация = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(
		КомпоновщикНастроек.ПользовательскиеНастройки, "Организация").Значение; // СправочникСсылка.Организации -
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Организация",   Организация);
	СтруктураПараметров.Вставить("ДатаНачала",    ПериодОтбора.ДатаНачала);
	СтруктураПараметров.Вставить("ДатаОкончания", ОбработаннаяДатаОкончания(ПериодОтбора.ДатаОкончания));
	
	СообщениеОЗакрытииМесяца = "";
	
	УстановитьПривилегированныйРежим(Истина);
	Если УчетНДСУПСлужебный.ПроверитьНаличиеЗаданий_ФормированияДвиженийПоНДС(СтруктураПараметров) Тогда
		СообщениеОЗакрытииМесяца = НСтр("ru = 'Необходимо выполнить регл. операцию
		|""Формирование движений по НДС"".'");
	КонецЕсли;
	
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"СообщениеОЗакрытииМесяца",
		СообщениеОЗакрытииМесяца);
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);

	ВнешниеНаборыДанных = ПолучитьВнешниеНаборыДанных();
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

Функция ОбработаннаяДатаОкончания(знач ДатаОкончания)
	
	Если НЕ ЗначениеЗаполнено(ДатаОкончания) Тогда
		ДатаОкончания        = Дата(3000, 1, 1);
	КонецЕсли;
	
	Возврат ДатаОкончания;
	
КонецФункции

Функция ПолучитьВнешниеНаборыДанных()
	
	СтруктураНаборовДанных = Новый Структура;
	
	ПериодОтбора = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(
		КомпоновщикНастроек.ПользовательскиеНастройки, "ПериодОтбора").Значение; // СтандартныйПериод -
	Организация = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(
		КомпоновщикНастроек.ПользовательскиеНастройки, "Организация").Значение; // СправочникСсылка.Организации -
	
	МВТ = Новый МенеджерВременныхТаблиц;
	
	ПараметрыРасчета = УчетНДСУПСлужебный.ИнициализироватьПараметрыПодготовкиРасчетовАвансовВЦеляхНДС();
	
	ПараметрыРасчета.ДатаНачала                    = ПериодОтбора.ДатаНачала;
	ПараметрыРасчета.ДатаОкончания                 = ПериодОтбора.ДатаОкончания;
	ПараметрыРасчета.Организации                   = Организация;
	
	УчетНДСУПСлужебный.ПодготовитьВТ_АвансыПолученные_Погашения(МВТ, ПараметрыРасчета);
	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	
	Запрос.УстановитьПараметр("ДатаНачала",    ПериодОтбора.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ПериодОтбора.ДатаОкончания);
	Запрос.УстановитьПараметр("ДатаОкончанияВключая",
		?(ЗначениеЗаполнено(ПериодОтбора.ДатаОкончания), Новый Граница(ПериодОтбора.ДатаОкончания, ВидГраницы.Включая), Неопределено));
	Запрос.УстановитьПараметр("Организация",   Организация);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НДСАвансы.Организация КАК Организация,
	|	НДСАвансы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НДСАвансы.Контрагент КАК Контрагент,
	|	НДСАвансы.ОбъектРасчетов КАК ОбъектРасчетов,
	|	НДСАвансы.ДокументОплаты КАК ДокументОплаты,
	|	НДСАвансы.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
	|	НДСАвансы.СтавкаНДС.Ставка КАК СтавкаНДС,
	|	НДСАвансы.ДокументЗачетаАванса КАК ДокументЗачетаАванса,
	|	НДСАвансы.СуммаАванса КАК СуммаАванса,
	|	НДСАвансы.НачислениеНДС КАК НачислениеНДС,
	|	НДСАвансы.СписаниеНДССАванса КАК СписаниеНДССАванса,
	|	НДСАвансы.СуммаЗачета КАК СуммаЗачета,
	|	НДСАвансы.СуммаВычетаНДС КАК СуммаВычетаНДС,
	|	НДСАвансы.ОстатокНДСНачало КАК ОстатокНДСНачало,
	|	НДСАвансы.ОстатокНДСКонец КАК ОстатокНДСКонец
	|ПОМЕСТИТЬ втОстаткиИОборотыНДССАвансов
	|ИЗ
	|	(ВЫБРАТЬ
	|		НДСАвансы.Организация КАК Организация,
	|		НДСАвансы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		НДСАвансы.Контрагент КАК Контрагент,
	|		НДСАвансы.ОбъектРасчетов КАК ОбъектРасчетов,
	|		НДСАвансы.ДокументОплаты КАК ДокументОплаты,
	|		НДСАвансы.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
	|		НДСАвансы.СтавкаНДС КАК СтавкаНДС,
	|		НДСАвансы.ДокументЗачетаАванса КАК ДокументЗачетаАванса,
	|		ВЫБОР
	|			КОГДА НДСАвансы.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияНДСАвансы.НачислениеСПолученногоАванса)
	|				ТОГДА НДСАвансы.СуммаБезНДС + НДСАвансы.НДС
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СуммаАванса,
	|		ВЫБОР
	|			КОГДА НДСАвансы.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияНДСАвансы.НачислениеСПолученногоАванса)
	|				ТОГДА НДСАвансы.НДС
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК НачислениеНДС,
	|		ВЫБОР
	|			КОГДА НДСАвансы.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияНДСАвансы.СписаниеНДССПолученногоАванса)
	|				ТОГДА НДСАвансы.НДС
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СписаниеНДССАванса,
	|		ВЫБОР
	|			КОГДА НДСАвансы.Событие В (ЗНАЧЕНИЕ(Перечисление.СобытияНДСАвансы.ВычетПриЗачетеАванса), ЗНАЧЕНИЕ(Перечисление.СобытияНДСАвансы.ВычетПриВозвратеАванса))
	|				ТОГДА НДСАвансы.СуммаБезНДС + НДСАвансы.НДС
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СуммаЗачета,
	|		ВЫБОР
	|			КОГДА НДСАвансы.Событие В (ЗНАЧЕНИЕ(Перечисление.СобытияНДСАвансы.ВычетПриЗачетеАванса), ЗНАЧЕНИЕ(Перечисление.СобытияНДСАвансы.ВычетПриВозвратеАванса))
	|				ТОГДА НДСАвансы.НДС
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СуммаВычетаНДС,
	|		0 КАК ОстатокНДСНачало,
	|		0 КАК ОстатокНДСКонец
	|	ИЗ
	|		РегистрНакопления.НДСАвансыПолученные КАК НДСАвансы
	|	ГДЕ
	|		НДСАвансы.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|		И НДСАвансы.Организация = &Организация
	|		И НДСАвансы.Активность = ИСТИНА
	|		И НДСАвансы.Событие В (ЗНАЧЕНИЕ(Перечисление.СобытияНДСАвансы.НачислениеСПолученногоАванса), ЗНАЧЕНИЕ(Перечисление.СобытияНДСАвансы.СписаниеНДССПолученногоАванса), ЗНАЧЕНИЕ(Перечисление.СобытияНДСАвансы.ВычетПриЗачетеАванса), ЗНАЧЕНИЕ(Перечисление.СобытияНДСАвансы.ВычетПриВозвратеАванса))
	|		И НДСАвансы.НДС <> 0
	|		И НДСАвансы.НастройкаХозяйственнойОперации <> ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПереносАванса)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОстаткиАвансов.Организация,
	|		ОстаткиАвансов.НаправлениеДеятельности,
	|		ОстаткиАвансов.Контрагент,
	|		ОстаткиАвансов.ОбъектРасчетов,
	|		ОстаткиАвансов.ДокументОплаты,
	|		ОстаткиАвансов.ИсправленныйСчетФактура,
	|		ОстаткиАвансов.СтавкаНДС,
	|		НЕОПРЕДЕЛЕНО,
	|		ОстаткиАвансов.СуммаБезНДСОстаток + ОстаткиАвансов.НДСОстаток,
	|		0,
	|		0,
	|		0,
	|		0,
	|		ОстаткиАвансов.НДСОстаток,
	|		0
	|	ИЗ
	|		РегистрНакопления.НДСАвансыПолученные.Остатки(&ДатаНачала, Организация = &Организация) КАК ОстаткиАвансов
	|	ГДЕ
	|		ОстаткиАвансов.НДСОстаток <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОстаткиАвансов.Организация,
	|		ОстаткиАвансов.НаправлениеДеятельности,
	|		ОстаткиАвансов.Контрагент,
	|		ОстаткиАвансов.ОбъектРасчетов,
	|		ОстаткиАвансов.ДокументОплаты,
	|		ОстаткиАвансов.ИсправленныйСчетФактура,
	|		ОстаткиАвансов.СтавкаНДС,
	|		НЕОПРЕДЕЛЕНО,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		ОстаткиАвансов.НДСОстаток
	|	ИЗ
	|		РегистрНакопления.НДСАвансыПолученные.Остатки(&ДатаОкончанияВключая, Организация = &Организация) КАК ОстаткиАвансов
	|	ГДЕ
	|		ОстаткиАвансов.НДСОстаток <> 0) КАК НДСАвансы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОсновнаяТаблица.Организация КАК Организация,
	|	ОсновнаяТаблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ОсновнаяТаблица.Контрагент КАК Контрагент,
	|	ОсновнаяТаблица.ДокументОплаты КАК ДокументОплаты,
	|	ОсновнаяТаблица.ОбъектРасчетов КАК ОбъектРасчетов
	|ПОМЕСТИТЬ втОсновнаяТаблица
	|ИЗ
	|	(ВЫБРАТЬ
	|		НДСАвансы.Организация КАК Организация,
	|		НДСАвансы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		НДСАвансы.Контрагент КАК Контрагент,
	|		НДСАвансы.ДокументОплаты КАК ДокументОплаты,
	|		НДСАвансы.ОбъектРасчетов КАК ОбъектРасчетов
	|	ИЗ
	|		втОстаткиИОборотыНДССАвансов КАК НДСАвансы
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗачетыПоВзаиморасчетам.Организация,
	|		ЗачетыПоВзаиморасчетам.НаправлениеДеятельности,
	|		ЗачетыПоВзаиморасчетам.Контрагент,
	|		ЗачетыПоВзаиморасчетам.РасчетныйДокумент,
	|		ЗачетыПоВзаиморасчетам.ОбъектРасчетов
	|	ИЗ
	|		АвансыПолученные_Погашения КАК ЗачетыПоВзаиморасчетам
	|	ГДЕ
	|		(ЗачетыПоВзаиморасчетам.РасчетныйДокумент, ЗачетыПоВзаиморасчетам.Организация) В
	|				(ВЫБРАТЬ
	|					Т.ДокументОснование,
	|					Т.Организация
	|				ИЗ
	|					Документ.СчетФактураВыданныйАванс КАК Т
	|				ГДЕ
	|					Т.Проведен)) КАК ОсновнаяТаблица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактура.ДокументОснование КАК ДокументОснование,
	|	СчетФактура.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(ПОДСТРОКА(СчетФактура.НомерИсправления, 2, 1) КАК СТРОКА(1))) = """"
	|			ТОГДА ВЫРАЗИТЬ(""0"" + СчетФактура.НомерИсправления КАК СТРОКА(3))
	|		ИНАЧЕ ВЫРАЗИТЬ(СчетФактура.НомерИсправления КАК СТРОКА(3))
	|	КОНЕЦ КАК НомерИсправленияДляСортировки
	|ПОМЕСТИТЬ втСчетаФактуры
	|ИЗ
	|	Документ.СчетФактураВыданныйАванс КАК СчетФактура
	|ГДЕ
	|	(СчетФактура.ДокументОснование, СчетФактура.Организация) В
	|			(ВЫБРАТЬ
	|				Т.ДокументОплаты,
	|				Т.Организация
	|			ИЗ
	|				втОсновнаяТаблица КАК Т)
	|	И СчетФактура.Дата <= &ДатаОкончания
	|	И СчетФактура.Проведен
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаФактурыПоследние.ДокументОснование КАК ДокументОснование,
	|	СчетФактура.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА СчетФактура.НомерИсправленияДляСортировки = """"
	|				ИЛИ СчетФактура.НомерИсправленияДляСортировки = ""0""
	|			ТОГДА ЗНАЧЕНИЕ(Документ.СчетФактураВыданныйАванс.ПустаяСсылка)
	|		ИНАЧЕ СчетФактура.Ссылка
	|	КОНЕЦ КАК ИсправленныйСчетФактура
	|ПОМЕСТИТЬ втСчетаФактурыПоследние
	|ИЗ
	|	(ВЫБРАТЬ
	|		Т.ДокументОснование КАК ДокументОснование,
	|		МАКСИМУМ(Т.НомерИсправленияДляСортировки) КАК НомерИсправленияДляСортировки
	|	ИЗ
	|		втСчетаФактуры КАК Т
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Т.ДокументОснование) КАК СчетаФактурыПоследние
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетаФактуры КАК СчетФактура
	|		ПО СчетаФактурыПоследние.ДокументОснование = СчетФактура.ДокументОснование
	|			И СчетаФактурыПоследние.НомерИсправленияДляСортировки = СчетФактура.НомерИсправленияДляСортировки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втСчетаФактуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОсновнаяТаблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ОсновнаяТаблица.Контрагент КАК Контрагент,
	|	ОсновнаяТаблица.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ОсновнаяТаблица.ДокументОплаты КАК ДокументОплаты,
	|	СчетаФактуры.Ссылка КАК СчетФактура
	|ИЗ
	|	втОсновнаяТаблица КАК ОсновнаяТаблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСчетаФактурыПоследние КАК СчетаФактуры
	|		ПО ОсновнаяТаблица.ДокументОплаты = СчетаФактуры.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСАвансы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НДСАвансы.Контрагент КАК Контрагент,
	|	НДСАвансы.ОбъектРасчетов КАК ОбъектРасчетов,
	|	НДСАвансы.ДокументОплаты КАК ДокументОплаты,
	|	НДСАвансы.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(НДСАвансы.СуммаАванса) КАК СуммаАванса,
	|	СУММА(НДСАвансы.НачислениеНДС) КАК НачислениеНДС,
	|	СУММА(НДСАвансы.СписаниеНДССАванса) КАК СписаниеНДССАванса,
	|	СУММА(НДСАвансы.ОстатокНДСНачало) КАК ОстатокНДСНачало,
	|	СУММА(НДСАвансы.ОстатокНДСКонец) КАК ОстатокНДСКонец
	|ИЗ
	|	втОстаткиИОборотыНДССАвансов КАК НДСАвансы
	|ГДЕ
	|	(НДСАвансы.ДокументОплаты, НДСАвансы.ИсправленныйСчетФактура) В
	|			(ВЫБРАТЬ
	|				СчетаФактуры.ДокументОснование,
	|				СчетаФактуры.ИсправленныйСчетФактура
	|			ИЗ
	|				втСчетаФактурыПоследние КАК СчетаФактуры)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСАвансы.НаправлениеДеятельности,
	|	НДСАвансы.Контрагент,
	|	НДСАвансы.ДокументОплаты,
	|	НДСАвансы.СтавкаНДС,
	|	НДСАвансы.ОбъектРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСАвансы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НДСАвансы.Контрагент КАК Контрагент,
	|	НДСАвансы.ОбъектРасчетов КАК ОбъектРасчетов,
	|	НДСАвансы.ДокументОплаты КАК ДокументОплаты,
	|	НДСАвансы.ДокументЗачетаАванса КАК ДокументЗачетаАванса,
	|	НДСАвансы.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(НДСАвансы.СуммаЗачета) КАК СуммаЗачета_Детализированная,
	|	СУММА(НДСАвансы.СуммаВычетаНДС) КАК СуммаВычетаНДС
	|ИЗ
	|	втОстаткиИОборотыНДССАвансов КАК НДСАвансы
	|ГДЕ
	|	(НДСАвансы.ДокументОплаты, НДСАвансы.ИсправленныйСчетФактура) В
	|			(ВЫБРАТЬ
	|				СчетаФактуры.ДокументОснование,
	|				СчетаФактуры.ИсправленныйСчетФактура
	|			ИЗ
	|				втСчетаФактурыПоследние КАК СчетаФактуры)
	|	И НДСАвансы.СуммаВычетаНДС <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСАвансы.НаправлениеДеятельности,
	|	НДСАвансы.Контрагент,
	|	НДСАвансы.ДокументОплаты,
	|	НДСАвансы.ДокументЗачетаАванса,
	|	НДСАвансы.СтавкаНДС,
	|	НДСАвансы.ОбъектРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗачетыАвансов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗачетыАвансов.Контрагент КАК Контрагент,
	|	ЗачетыАвансов.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ЗачетыАвансов.РасчетныйДокумент КАК ДокументОплаты,
	|	СУММА(ЗачетыАвансов.СуммаПогашения) КАК СуммаЗачета_Общая
	|ИЗ
	|	АвансыПолученные_Погашения КАК ЗачетыАвансов
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗачетыАвансов.НаправлениеДеятельности,
	|	ЗачетыАвансов.Контрагент,
	|	ЗачетыАвансов.РасчетныйДокумент,
	|	ЗачетыАвансов.ОбъектРасчетов";
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	
	Выборка1 =  ПакетРезультатов[ПакетРезультатов.Количество() - 4].Выбрать();
	Выборка2 =  ПакетРезультатов[ПакетРезультатов.Количество() - 3].Выбрать();
	Выборка3 =  ПакетРезультатов[ПакетРезультатов.Количество() - 2].Выбрать();
	Выборка4 =  ПакетРезультатов[ПакетРезультатов.Количество() - 1].Выбрать();
	
	СтруктураНаборовДанных.Вставить("ОсновнаяТаблица",               Выборка1);
	СтруктураНаборовДанных.Вставить("ОстаткиИОборотыНДССАвансов",    Выборка2);
	СтруктураНаборовДанных.Вставить("Зачеты_НДССАвансов",            Выборка3);
	СтруктураНаборовДанных.Вставить("Зачеты_ПоДаннымВзаиморасчетов", Выборка4);
	
	
	Возврат СтруктураНаборовДанных;
	
КонецФункции

#КонецОбласти

#КонецЕсли
