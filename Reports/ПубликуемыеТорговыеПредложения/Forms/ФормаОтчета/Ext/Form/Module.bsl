
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("Отбор") Тогда
		ФормаПараметры = Новый Структура;
		ФормаПараметры.Вставить("Отбор");
		ФормаПараметры.Отбор = Новый Структура;
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ФормаПараметры.Отбор, Параметры.Отбор, Истина);
		Параметры.Отбор.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеВариантаНаСервере(Настройки)
	
	Если ТипЗнч(ФормаПараметры.Отбор) = Тип("Структура") Тогда
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов") Тогда
			Модуль = ОбщегоНазначения.ОбщийМодуль("ОтчетыСервер");
			Модуль.УстановитьФиксированныеОтборы(ФормаПараметры.Отбор, Отчет.КомпоновщикНастроек.Настройки, НастройкиОтчета);
		Иначе
			УстановитьФиксированныеОтборы(ФормаПараметры.Отбор, Отчет.КомпоновщикНастроек.Настройки, НастройкиОтчета);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	ЗначенияПолей = РезультатОбработкаРасшифровкиНаСервере(Расшифровка);
	
	Если ТипЗнч(ЗначенияПолей) <> Тип("Структура") Или ЗначенияПолей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Для Каждого КлючИЗначение Из ЗначенияПолей Цикл
		
		Если КлючИЗначение.Значение.Количество() = 1 Тогда
			
			ПоказатьЗначение(, КлючИЗначение.Значение[0]);
			Возврат;
			
		КонецЕсли;
		
		СписокВыбора = Новый СписокЗначений;
		СписокВыбора.ЗагрузитьЗначения(КлючИЗначение.Значение);
		
		Оповещение = Новый ОписаниеОповещения("ВыборИзСпискаЗавершение", ЭтотОбъект);
		ПоказатьВыборИзСписка(Оповещение, СписокВыбора, Элемент);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьФиксированныеОтборы(СтруктураОтборов, НастройкиКД, НастройкиОтчета)
	
	Если ТипЗнч(НастройкиКД) <> Тип("НастройкиКомпоновкиДанных")
		Или СтруктураОтборов = Неопределено
		Или СтруктураОтборов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	ПараметрыКД = НастройкиКД.ПараметрыДанных;
	ОтборыКД = НастройкиКД.Отбор;
	Недоступный = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	Для Каждого КлючИЗначение Из СтруктураОтборов Цикл
		Имя = КлючИЗначение.Ключ;
		Значение = КлючИЗначение.Значение;
		Если ТипЗнч(Значение) = Тип("ФиксированныйМассив") Тогда
			Значение = Новый Массив(Значение);
		КонецЕсли;
		Если ТипЗнч(Значение) = Тип("Массив") Тогда
			Список = Новый СписокЗначений;
			Список.ЗагрузитьЗначения(Значение);
			Значение = Список;
		КонецЕсли;
		ПараметрКД = ПараметрыКД.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(Имя));
		Если ТипЗнч(ПараметрКД) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
			ПараметрКД.ИдентификаторПользовательскойНастройки = "";
			ПараметрКД.Использование    = Истина;
			ПараметрКД.РежимОтображения = Недоступный;
			ПараметрКД.Значение         = Значение;
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(Значение) = Тип("СписокЗначений") Тогда
			ВидСравненияКД = ВидСравненияКомпоновкиДанных.ВСписке;
		Иначе
			ВидСравненияКД = ВидСравненияКомпоновкиДанных.Равно;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ОтборыКД, Имя, Значение, ВидСравненияКД,,
			Истина, Недоступный, "");
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция РезультатОбработкаРасшифровкиНаСервере(Расшифровка)
	
	СтруктураЗначенийПолей = Новый Структура;
	Если Не ЭтоАдресВременногоХранилища(ДанныеРасшифровки) Тогда
		Возврат СтруктураЗначенийПолей;
	КонецЕсли;
	
	ДанныеРасшифровкиЗначение = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	
	ЭлементРасшифровки = ДанныеРасшифровкиЗначение.Элементы[Расшифровка];
	ЗначенияПолейРасшифровки = ЭлементРасшифровки.ПолучитьПоля();
	
	Если ЗначенияПолейРасшифровки.Количество() = 1 
		И ТипЗнч(ЗначенияПолейРасшифровки[0].Значение) <> Тип("Массив") 
		И ТипЗнч(ЗначенияПолейРасшифровки[0].Значение) <> Тип("ТаблицаЗначений") Тогда
		
		Возврат ЗначенияПолейРасшифровки[0].Значение;
		
	КонецЕсли;
	
	Для Каждого ЗначениеПоля Из ЗначенияПолейРасшифровки Цикл
		
		СтруктураЗначенийПолей.Вставить(ЗначениеПоля.Поле, ЗначениеПоля.Значение);
		
	КонецЦикла;
	
	Возврат СтруктураЗначенийПолей;
	
КонецФункции

&НаКлиенте
Процедура ВыборИзСпискаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, Результат.Значение);
	
КонецПроцедуры

#КонецОбласти