// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//   НастройкиОтчета - см. ВариантыОтчетов.ОписаниеОтчета.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	ВыводитьВариантыОтчетов = Ложь;
	
	НастройкиОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ВедомостьПоРетроБонусамКлиентов);
	НастройкиОтчета.ФункциональныеОпции.Добавить(Метаданные.ФункциональныеОпции.ИспользоватьРетроБонусыКлиентов.Имя);
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ВедомостьПоРетроБонусамКлиентов");
	НастройкиВарианта.Описание = НСтр("ru = 'Сколько бонусов начислено и выдано клиентам за указанный период?
									  |Какие суммы бонусов ожидают подписания актов финансовой премии или согласования документов списания?
									  |Какой остаток начисленных ретро-бонусов по клиентам?'");
	НастройкиВарианта.Размещение.Вставить(Метаданные.Подсистемы.CRMИМаркетинг.Подсистемы.РетроБонусыКлиентов);
	
	ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, ВыводитьВариантыОтчетов);
	
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(
		Настройки,
		НастройкиОтчета,
		"ВедомостьПоРетроБонусамКлиентовКонтекст");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//   КлючВарианта - Строка - 
//   
//	Возвращаемое значение:
//		СтрокаТаблицыЗначений - описание созданной команды.
//
Функция ДобавитьКомандуОтчета(КомандыОтчетов, Параметры, КлючВарианта = "") Экспорт
	
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ВедомостьПоРетроБонусамКлиентов) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		Представление = НСтр("ru = 'Ведомость по начисленным ретро-бонусам'");
		КомандаОтчет.Менеджер = Метаданные.Отчеты.ВедомостьПоРетроБонусамКлиентов.ПолноеИмя();
		КомандаОтчет.Представление = Представление;
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность = "Обычное";
		
		Если ЗначениеЗаполнено(КлючВарианта) Тогда
			
			КомандаОтчет.КлючВарианта = КлючВарианта;
			
		КонецЕсли;
		
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

#КонецОбласти

#КонецЕсли