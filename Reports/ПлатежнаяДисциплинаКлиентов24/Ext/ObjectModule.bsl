#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - См. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.События.ПередЗагрузкойВариантаНаСервере = Истина;
КонецПроцедуры

// Вызывается в одноименном обработчике формы отчета после выполнения кода формы.
//
// Подробнее - см. ОтчетыПереопределяемый.ПередЗагрузкойВариантаНаСервере
//
Процедура ПередЗагрузкойВариантаНаСервере(ЭтаФорма, НовыеНастройкиКД) Экспорт
	Отчет = ЭтаФорма.Отчет;
	КомпоновщикНастроекФормы = Отчет.КомпоновщикНастроек;
	
	// Изменение настроек по функциональным опциям
	НастроитьПараметрыОтборыПоФункциональнымОпциям(КомпоновщикНастроекФормы);
	
	// Установка значений по умолчанию
	УстановитьОбязательныеНастройки(КомпоновщикНастроекФормы, Истина);
	
	НовыеНастройкиКД = КомпоновщикНастроекФормы.Настройки;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПользовательскиеНастройкиМодифицированы = Ложь;
	
	УстановитьОбязательныеНастройки(КомпоновщикНастроек, ПользовательскиеНастройкиМодифицированы);
	
	// Сформируем отчет
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	СхемаКомпоновкиДанных.НаборыДанных.НаборДанных.Запрос = ТекстЗапроса();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	// Сообщим форме отчета, что настройки модифицированы
	Если ПользовательскиеНастройкиМодифицированы Тогда
		КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПользовательскиеНастройкиМодифицированы", Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьОбязательныеНастройки(КомпоновщикНастроек, ПользовательскиеНастройкиМодифицированы)
	
	СегментыСервер.ВключитьОтборПоСегментуПартнеровВСКД(КомпоновщикНастроек);
	УстановитьДатуОтчета(КомпоновщикНастроек, ПользовательскиеНастройкиМодифицированы);
	
КонецПроцедуры

Процедура УстановитьДатуОтчета(КомпоновщикНастроек, ПользовательскиеНастройкиМодифицированы)
	
	ПараметрДатаОтчетаГраница = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "КонецПериодаГраница");
	ПараметрДатаОтчетаГраница.Использование = Истина;

	ПараметрПериод = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "Период");
	Период = ПараметрПериод.Значение; // СтандартныйПериод
	Если ПараметрПериод.Использование И ЗначениеЗаполнено(Период.ДатаНачала) Тогда
		ПараметрДатаОтчетаГраница.Значение = Новый Граница(КонецДня(Период.ДатаОкончания), ВидГраницы.Включая);
	Иначе
		ПараметрДатаОтчетаГраница.Значение = Дата(1,1,1)
	КонецЕсли;
	
КонецПроцедуры

Процедура НастроитьПараметрыОтборыПоФункциональнымОпциям(КомпоновщикНастроекФормы)
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
		КомпоновкаДанныхСервер.УдалитьЭлементОтбораИзВсехНастроекОтчета(КомпоновщикНастроекФормы, "Контрагент");
	КонецЕсли;
КонецПроцедуры

Функция ТекстЗапроса()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Сегменты.Партнер КАК Партнер,
	|	ИСТИНА КАК ИспользуетсяОтборПоСегментуПартнеров
	|ПОМЕСТИТЬ ОтборПоСегментуПартнеров
	|ИЗ
	|	РегистрСведений.ПартнерыСегмента КАК Сегменты
	|{ГДЕ
	|	Сегменты.Сегмент.* КАК СегментПартнеров,
	|	Сегменты.Партнер.* КАК Партнер}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Партнер,
	|	ИспользуетсяОтборПоСегментуПартнеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	АналитикаУчета.Организация КАК Организация,
	|	АналитикаУчета.Партнер КАК Партнер,
	|	АналитикаУчета.Контрагент КАК Контрагент,
	|	АналитикаУчета.Договор КАК Договор,
	|	АналитикаУчета.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	АналитикаУчета.КлючАналитики КАК КлючАналитики
	|ПОМЕСТИТЬ ВТ_АналитикаУчетаПоПартнерам
	|ИЗ
	|	РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчета
	|ГДЕ
	|		АналитикаУчета.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|{ГДЕ
	|		АналитикаУчета.Организация.* КАК Организация,
	|		АналитикаУчета.Партнер.* КАК Партнер,
	|		АналитикаУчета.Контрагент.* КАК Контрагент,
	|		АналитикаУчета.Договор.* КАК Договор,
	|		АналитикаУчета.НаправлениеДеятельности.* КАК НаправлениеДеятельности,
	|		(АналитикаУчета.Партнер В
	|				(ВЫБРАТЬ
	|					ОтборПоСегментуПартнеров.Партнер
	|				ИЗ
	|					ОтборПоСегментуПартнеров
	|				ГДЕ
	|					ОтборПоСегментуПартнеров.ИспользуетсяОтборПоСегментуПартнеров = &ИспользуетсяОтборПоСегментуПартнеров))}
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючАналитики
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка КАК Организация,
	|	КурсВалюты.Валюта КАК Валюта,
	|	ВЫБОР &ДанныеОтчета
	|		КОГДА 1
	|			ТОГДА ЕСТЬNULL(КурсВалюты.КурсЧислитель, 1) * ЕСТЬNULL(КурсВалютыУпр.КурсЗнаменатель, 1) / (ЕСТЬNULL(КурсВалюты.КурсЗнаменатель, 1) * ЕСТЬNULL(КурсВалютыУпр.КурсЧислитель, 1))
	|		ИНАЧЕ ЕСТЬNULL(КурсВалюты.КурсЧислитель, 1) / ЕСТЬNULL(КурсВалюты.КурсЗнаменатель, 1)
	|	КОНЕЦ КАК Коэффициент
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних({(&КонецПериода)}) КАК КурсВалюты
	|			ПО Организации.ВалютаРегламентированногоУчета = КурсВалюты.БазоваяВалюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних({(&КонецПериода)}, Валюта = &ВалютаУправленческогоУчета) КАК КурсВалютыУпр
	|			ПО Организации.ВалютаРегламентированногоУчета = КурсВалютыУпр.БазоваяВалюта
	|{ГДЕ
	|	Организации.Ссылка.* КАК Организация}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Валюта
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|//Остатки по фактической задолженности на конец периода отчета
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	АналитикаУчета.Организация                                     КАК Организация,
	|	АналитикаУчета.Партнер                                         КАК Партнер,
	|	АналитикаУчета.Контрагент                                      КАК Контрагент,
	|	АналитикаУчета.Договор                                         КАК Договор,
	|	АналитикаУчета.НаправлениеДеятельности                         КАК НаправлениеДеятельности,
	|	РасчетыПоСрокам.ОбъектРасчетов                                 КАК ОбъектРасчетов,
	|	РасчетыПоСрокам.РасчетныйДокумент                              КАК РасчетныйДокумент,
	|	ВЫБОР &ДанныеОтчета
	|		КОГДА 1
	|			ТОГДА &ВалютаУправленческогоУчета
	|		ИНАЧЕ АналитикаУчета.Организация.ВалютаРегламентированногоУчета
	|	КОНЕЦ                                                          КАК Валюта,
	|	
	|	ВЫБОР
	|		КОГДА РАЗНОСТЬДАТ(РасчетыПоСрокам.ДатаПлановогоПогашения, &КонецПериода, ДЕНЬ) < 0
	|			ИЛИ РасчетыПоСрокам.ДатаПлановогоПогашения < &НачалоПериода
	|			ИЛИ РасчетыПоСрокам.ДатаПлановогоПогашения > &КонецПериода
	|			ТОГДА 0
	|		ИНАЧЕ РАЗНОСТЬДАТ(РасчетыПоСрокам.ДатаПлановогоПогашения, &КонецПериода, ДЕНЬ)
	|	КОНЕЦ                                                          КАК ДлительностьПросрочки,
	|	ВЫБОР
	|		КОГДА РасчетыПоСрокам.ДатаПлановогоПогашения >= &НачалоПериода
	|				И РасчетыПоСрокам.ДатаПлановогоПогашения <= &КонецПериода
	|				И РАЗНОСТЬДАТ(РасчетыПоСрокам.ДатаПлановогоПогашения, &КонецПериода, ДЕНЬ) > 0 
	|				И НЕ РасчетыПоСрокам.РасчетныйДокумент ССЫЛКА Документ.КорректировкаЗадолженности
	|			ТОГДА
	|				ВЫБОР &ДанныеОтчета
	|					КОГДА 1
	|						ТОГДА РасчетыПоСрокам.ДолгУпрОстаток
	|					ИНАЧЕ РасчетыПоСрокам.ДолгРеглОстаток
	|				КОНЕЦ 
	|		ИНАЧЕ 0
	|	КОНЕЦ                                                          КАК ДолгКлиентаПросроченоВПериоде,
	|	ВЫБОР &ДанныеОтчета
	|		КОГДА 1
	|			ТОГДА РасчетыПоСрокам.ДолгУпрОстаток
	|		ИНАЧЕ РасчетыПоСрокам.ДолгРеглОстаток
	|	КОНЕЦ                                                          КАК ДолгКлиента,
	|	ВЫБОР 
	|		КОГДА РасчетыПоСрокам.ДатаПлановогоПогашения < &КонецПериода
	|			ТОГДА ВЫБОР &ДанныеОтчета
	|					КОГДА 1
	|						ТОГДА РасчетыПоСрокам.ДолгУпрОстаток
	|					ИНАЧЕ РасчетыПоСрокам.ДолгРеглОстаток
	|				КОНЕЦ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                                          КАК ДолгКлиентаПросрочено
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам.Остатки({&КонецПериодаГраница},
	|		АналитикаУчетаПоПартнерам В
	|				(ВЫБРАТЬ
	|					ВТ_АналитикаУчетаПоПартнерам.КлючАналитики
	|				ИЗ
	|					ВТ_АналитикаУчетаПоПартнерам)) КАК РасчетыПоСрокам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АналитикаУчетаПоПартнерам КАК АналитикаУчета
	|			ПО АналитикаУчета.КлючАналитики = РасчетыПоСрокам.АналитикаУчетаПоПартнерам
	|ГДЕ
	|	РасчетыПоСрокам.ДолгРеглОстаток > 0
	|	И (&ВключатьЗадолженность = 0 ИЛИ &ВключатьЗадолженность = 1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Остатки по плановой задолженности на конец периода отчета
	|ВЫБРАТЬ
	|	АналитикаУчета.Организация               КАК Организация,
	|	АналитикаУчета.Партнер                   КАК Партнер,
	|	АналитикаУчета.Контрагент                КАК Контрагент,
	|	АналитикаУчета.Договор                   КАК Договор,
	|	АналитикаУчета.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	РасчетыПланОплат.ОбъектРасчетов          КАК ОбъектРасчетов,
	|	РасчетыПланОплат.ДокументПлан            КАК РасчетныйДокумент,
	|	ВЫБОР &ДанныеОтчета
	|		КОГДА 1
	|			ТОГДА &ВалютаУправленческогоУчета
	|		ИНАЧЕ АналитикаУчета.Организация.ВалютаРегламентированногоУчета
	|	КОНЕЦ                                    КАК Валюта,
	|	
	|	ВЫБОР
	|		КОГДА РАЗНОСТЬДАТ(РасчетыПланОплат.ДатаПлановогоПогашения, &КонецПериода, ДЕНЬ) < 0
	|			ИЛИ РасчетыПланОплат.ДатаПлановогоПогашения < &НачалоПериода
	|			ИЛИ РасчетыПланОплат.ДатаПлановогоПогашения > &КонецПериода
	|			ТОГДА 0
	|		ИНАЧЕ РАЗНОСТЬДАТ(РасчетыПланОплат.ДатаПлановогоПогашения, &КонецПериода, ДЕНЬ)
	|	КОНЕЦ                                    КАК ДлительностьПросрочки,
	|	ВЫБОР
	|		КОГДА РасчетыПланОплат.ДатаПлановогоПогашения >= &НачалоПериода
	|				И РасчетыПланОплат.ДатаПлановогоПогашения <= &КонецПериода
	|				И РАЗНОСТЬДАТ(РасчетыПланОплат.ДатаПлановогоПогашения, &КонецПериода, ДЕНЬ) > 0 
	|			ТОГДА РасчетыПланОплат.КОплатеОстаток * КурсВалюты.Коэффициент
	|		ИНАЧЕ 0
	|	КОНЕЦ                                    КАК ДолгКлиентаПросроченоВПериоде,
	|	РасчетыПланОплат.КОплатеОстаток
	|		* КурсВалюты.Коэффициент             КАК ДолгКлиента,
	|	ВЫБОР 
	|		КОГДА РасчетыПланОплат.ДатаПлановогоПогашения < &КонецПериода
	|			ТОГДА РасчетыПланОплат.КОплатеОстаток * КурсВалюты.Коэффициент
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                    КАК ДолгКлиентаПросрочено
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПланОплат.Остатки({&КонецПериодаГраница},
	|		АналитикаУчетаПоПартнерам В
	|				(ВЫБРАТЬ
	|					ВТ_АналитикаУчетаПоПартнерам.КлючАналитики
	|				ИЗ
	|					ВТ_АналитикаУчетаПоПартнерам)
	|		И ТИПЗНАЧЕНИЯ(ДокументПлан) В (&ТипыДокументовПлана)) КАК РасчетыПланОплат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АналитикаУчетаПоПартнерам КАК АналитикаУчета
	|			ПО АналитикаУчета.КлючАналитики = РасчетыПланОплат.АналитикаУчетаПоПартнерам
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалюты
	|			ПО (КурсВалюты.Валюта = РасчетыПланОплат.Валюта)
	|				И (КурсВалюты.Организация = АналитикаУчета.Организация)
	|ГДЕ
	|	РасчетыПланОплат.КОплатеОстаток > 0
	|	И (&ВключатьЗадолженность = 0 ИЛИ &ВключатьЗадолженность = 2)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Обороты по фактической задолженности
	|ВЫБРАТЬ
	|	Аналитика.Организация                  КАК Организация,
	|	Аналитика.Партнер                      КАК Партнер,
	|	Аналитика.Контрагент                   КАК Контрагент,
	|	Аналитика.Договор                      КАК Договор,
	|	Аналитика.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	РасчетыОбороты.ОбъектРасчетов          КАК ОбъектРасчетов,
	|	РасчетыОбороты.РасчетныйДокумент       КАК РасчетныйДокумент,
	|	ВЫБОР &ДанныеОтчета
	|		КОГДА 1
	|			ТОГДА &ВалютаУправленческогоУчета
	|		ИНАЧЕ Аналитика.Организация.ВалютаРегламентированногоУчета
	|	КОНЕЦ                                  КАК Валюта,
	|	
	|	ВЫБОР
	|		КОГДА РАЗНОСТЬДАТ(РасчетыОбороты.ДатаПлановогоПогашения, РасчетыОбороты.Период, ДЕНЬ) < 0
	|			ТОГДА 0
	|		ИНАЧЕ РАЗНОСТЬДАТ(РасчетыОбороты.ДатаПлановогоПогашения, РасчетыОбороты.Период, ДЕНЬ)
	|	КОНЕЦ                                  КАК ДлительностьПросрочки,
	|	
	|	ВЫБОР
	|		КОГДА РасчетыОбороты.ДатаПлановогоПогашения >= &НачалоПериода
	|				И РасчетыОбороты.ДатаПлановогоПогашения <= &КонецПериода
	|				И НЕ РасчетыОбороты.РасчетныйДокумент ССЫЛКА Документ.КорректировкаЗадолженности
	|			ТОГДА ВЫБОР &ДанныеОтчета
	|					КОГДА 1
	|						ТОГДА РасчетыОбороты.ДолгУпр
	|					ИНАЧЕ РасчетыОбороты.ДолгРегл
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                  КАК ДолгКлиентаПросроченоВПериоде,
	|	0                                      КАК ДолгКлиента,
	|	0                                      КАК ДолгКлиентаПросрочено
	|	
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|			ПО Аналитика.КлючАналитики = РасчетыОбороты.АналитикаУчетаПоПартнерам
	|ГДЕ
	|	РасчетыОбороты.ДолгРегл > 0
	|	И РасчетыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И РасчетыОбороты.Период >= &НачалоПериода
	|	И РасчетыОбороты.Период <= &КонецПериода
	|	И (&ВключатьЗадолженность = 0 ИЛИ &ВключатьЗадолженность = 1)
	|{ГДЕ
	|	Аналитика.Организация.* КАК Организация,
	|	Аналитика.Партнер.* КАК Партнер,
	|	Аналитика.Контрагент.* КАК Контрагент,
	|	Аналитика.Договор.* КАК Договор,
	|	Аналитика.НаправлениеДеятельности.* КАК НаправлениеДеятельности,
	|	(Аналитика.Партнер В
	|			(ВЫБРАТЬ
	|				ОтборПоСегментуПартнеров.Партнер
	|			ИЗ
	|				ОтборПоСегментуПартнеров
	|			ГДЕ
	|				ОтборПоСегментуПартнеров.ИспользуетсяОтборПоСегментуПартнеров = &ИспользуетсяОтборПоСегментуПартнеров))}
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Аналитика.Организация                                   КАК Организация,
	|	Аналитика.Партнер                                       КАК Партнер,
	|	Аналитика.Контрагент                                    КАК Контрагент,
	|	Аналитика.Договор                                       КАК Договор,
	|	Аналитика.НаправлениеДеятельности                       КАК НаправлениеДеятельности,
	|	РасчетыОбороты.ОбъектРасчетов                           КАК ОбъектРасчетов,
	|	РасчетыОбороты.ДокументПлан                             КАК РасчетныйДокумент,
	|	ВЫБОР &ДанныеОтчета
	|		КОГДА 1
	|			ТОГДА &ВалютаУправленческогоУчета
	|		ИНАЧЕ РасчетыОбороты.АналитикаУчетаПоПартнерам.Организация.ВалютаРегламентированногоУчета
	|	КОНЕЦ                                                   КАК Валюта,
	|	
	|	ВЫБОР
	|		КОГДА РАЗНОСТЬДАТ(РасчетыОбороты.ДатаПлановогоПогашения,
	|				РасчетыОбороты.Период, ДЕНЬ) < 0
	|			ТОГДА 0
	|		ИНАЧЕ РАЗНОСТЬДАТ(РасчетыОбороты.ДатаПлановогоПогашения,
	|				РасчетыОбороты.Период, ДЕНЬ)
	|	КОНЕЦ                                                   КАК ДлительностьПросрочки,
	|	
	|	ВЫБОР
	|		КОГДА РасчетыОбороты.ДатаПлановогоПогашения >= &НачалоПериода
	|				И РасчетыОбороты.ДатаПлановогоПогашения <= &КонецПериода
	|			ТОГДА РасчетыОбороты.КОплате * КурсВалюты.Коэффициент
	|		ИНАЧЕ 0
	|	КОНЕЦ                                                   КАК ДолгКлиентаПросроченоВПериоде,
	|	0                                                       КАК ДолгКлиента,
	|	0                                                       КАК ДолгКлиентаПросрочено
	|	
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПланОплат КАК РасчетыОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалюты
	|			ПО (КурсВалюты.Валюта = РасчетыОбороты.Валюта)
	|				И (КурсВалюты.Организация = РасчетыОбороты.АналитикаУчетаПоПартнерам.Организация)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|			ПО Аналитика.КлючАналитики = РасчетыОбороты.АналитикаУчетаПоПартнерам
	|ГДЕ
	|	РасчетыОбороты.КОплате > 0
	|	И РасчетыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И РасчетыОбороты.Период >= &НачалоПериода
	|	И РасчетыОбороты.Период <= &КонецПериода
	|	И (&ВключатьЗадолженность = 0 ИЛИ &ВключатьЗадолженность = 2)
	|{ГДЕ
	|	Аналитика.Организация.* КАК Организация,
	|	Аналитика.Партнер.* КАК Партнер,
	|	Аналитика.Контрагент.* КАК Контрагент,
	|	Аналитика.Договор.* КАК Договор,
	|	Аналитика.НаправлениеДеятельности.* КАК НаправлениеДеятельности,
	|	(Аналитика.Партнер В
	|			(ВЫБРАТЬ
	|				ОтборПоСегментуПартнеров.Партнер
	|			ИЗ
	|				ОтборПоСегментуПартнеров
	|			ГДЕ
	|				ОтборПоСегментуПартнеров.ИспользуетсяОтборПоСегментуПартнеров = &ИспользуетсяОтборПоСегментуПартнеров))}";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ТипыДокументовПлана", ПолучитьТипыДокументовПлана());
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ПолучитьТипыДокументовПлана()
	
	ТекстТиповДокументов = ""; 
	
	
	ТекстТиповДокументов = ТекстТиповДокументов + "ТИП(Документ.ЗаказКлиента), ";
	ТекстТиповДокументов = ТекстТиповДокументов + "ТИП(Документ.ЗаявкаНаВозвратТоваровОтКлиента), ";
	ТекстТиповДокументов = ТекстТиповДокументов + "ТИП(Документ.ГрафикИсполненияДоговора)";
	
	Возврат ТекстТиповДокументов;
КонецФункции

#КонецОбласти

#КонецЕсли