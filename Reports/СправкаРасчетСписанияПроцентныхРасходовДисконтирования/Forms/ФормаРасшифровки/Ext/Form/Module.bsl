
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СформироватьПриОткрытии") Тогда
		СформироватьПриОткрытии = Параметры.СформироватьПриОткрытии;
	КонецЕсли;
	
	Если Параметры.Свойство("Отбор") Тогда
		Если Параметры.Отбор.Свойство("ДанныеОтчета") Тогда
			ДанныеОтчета = Параметры.Отбор.ДанныеОтчета;
		КонецЕсли;
		Если Параметры.Отбор.Свойство("ПериодОтчета") Тогда
			Если ТипЗнч(Параметры.Отбор.ПериодОтчета) = Тип("СтандартнаяДатаНачала") Тогда
				ПериодОтчета = Параметры.Отбор.ПериодОтчета.Дата;
			Иначе
				ПериодОтчета = Параметры.Отбор.ПериодОтчета;
			КонецЕсли;
			Если Параметры.Отбор.Свойство("ОбъектРасчетов") Тогда
				ОтборОбъектРасчетов =  Параметры.Отбор.ОбъектРасчетов;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		Если ТипЗнч(Параметры.ПараметрКоманды) = Тип("Массив") Тогда
			МассивСсылок = Параметры.ПараметрКоманды;
		Иначе
			МассивСсылок = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Параметры.ПараметрКоманды);
		КонецЕсли;
		ОтборДокументы.Очистить();
		Для Каждого Ссылка Из МассивСсылок Цикл
			ОтборДокументы.Добавить(Ссылка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если СформироватьПриОткрытии Тогда
		СформироватьОтчет();
	Иначе
		УстановитьСостояниеОтчетНеСформирован();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьОтчет()
	
	ОбъектОтчет = РеквизитФормыВЗначение("Отчет");
	ЕстьДанныеДляОтображения = Ложь;
	ОбъектОтчет.СформироватьОтчет(Результат, ПериодОтчета, ОтборДокументы.ВыгрузитьЗначения(), ДанныеОтчета, ОтборОбъектРасчетов, ЕстьДанныеДляОтображения);
	Если ЕстьДанныеДляОтображения Тогда
		Элементы.Результат.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.НеИспользовать;
		Элементы.Результат.ОтображениеСостояния.Видимость = Ложь;
	Иначе
		Элементы.Результат.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
		Элементы.Результат.ОтображениеСостояния.Текст = НСтр("ru = 'Нет данных для отображения.'");
		Элементы.Результат.ОтображениеСостояния.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьОтчет();
КонецПроцедуры

// Процедура устанавливает отображение состояния "Отчет не сформирован" при изменении полей отчета.
//
&НаКлиенте
Процедура УстановитьСостояниеОтчетНеСформирован()
	
	Элементы.Результат.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
	Элементы.Результат.ОтображениеСостояния.Текст = НСтр("ru = 'Отчет не сформирован. Нажмите ""Сформировать"" для получения отчета.'");
	Элементы.Результат.ОтображениеСостояния.Видимость = Истина;
	
КонецПроцедуры // УстановитьСостояниеОтчетНеСформирован()

#КонецОбласти
