#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	МодульВариантыОтчетов = ОбщегоНазначения.ОбщийМодуль("ВариантыОтчетов");
	МодульВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Ложь);
	
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	
	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "Основной");
	НастройкиВарианта.Включен = Ложь;
	НастройкиВарианта.Описание = НСтр("ru = 'Справка расчет начисления процентных расходов дисконтирования.'");
КонецПроцедуры

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//
// Возвращаемое значение:
//   СтрокаТаблицыЗначений - новая команда отчета.
Функция ДобавитьКомандуОтчета(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.СправкаРасчетСписанияПроцентныхРасходовДисконтирования)
		И ЕстьПравоЧтенияТаблицДокументов() Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.СправкаРасчетСписанияПроцентныхРасходовДисконтирования.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Справка-расчет списания процентных расходов дисконтирования'");
		КомандаОтчет.МножественныйВыбор = Истина;
		КомандаОтчет.Важность = "Обычное";
		КомандаОтчет.КлючВарианта = "КонтекстныйВариант";
	
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЕстьПравоЧтенияТаблицДокументов() 
	
	МассивТиповДокументов = РегистрыНакопления.ПроцентныеРасходыДисконтирования.ПолучитьТипыДокументовУчаствующихВДисконтировании();
	
	Для Каждого ТипДокумента Из МассивТиповДокументов Цикл
		
		ДокументСсылка = Новый(ТипДокумента);
		МетаданныеДокумента = ДокументСсылка.Метаданные();
		
		Если МетаданныеДокумента = Неопределено 
			Или Не ПравоДоступа("Чтение", МетаданныеДокумента) Тогда
			
			Возврат Ложь;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#КонецЕсли