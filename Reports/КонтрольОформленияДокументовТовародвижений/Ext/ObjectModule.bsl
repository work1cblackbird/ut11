#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает список распоряжений для оформления документов.
// 
// Возвращаемое значение:
//  СписокЗначений - список распоряжений для оформления документов.
//
Функция РаспоряженияДляОформленияДокументов() Экспорт
	
	ТаблицаЗначенийРезультат = СкомпоноватьРезультатВКоллекциюЗначений();
	
	СписокРаспоряжений = Новый СписокЗначений;
	Для Каждого Стр Из ТаблицаЗначенийРезультат Цикл
		Если СписокРаспоряжений.НайтиПоЗначению(Стр.Распоряжение) = Неопределено Тогда
			СписокРаспоряжений.Добавить(Стр.Распоряжение);
		КонецЕсли;
	КонецЦикла;
	Возврат СписокРаспоряжений;
	
КонецФункции

// Возвращает списки номенклатуры и характеристик для оформления складских актов.
// 
// Возвращаемое значение:
//  Структура - структура с полями:
//		* СписокНоменклатуры	- СписокЗначений - список номенклатуры для оформления складских актов.
//		* СписокХарактеристик	- СписокЗначений - список характеристик для оформления складских актов.
//
Функция НоменклатураИХарактеристикиДляОформленияДокументов() Экспорт
	
	ТаблицаЗначенийРезультат = СкомпоноватьРезультатВКоллекциюЗначений();
	
	ИспользоватьХарактеристикиНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	Структура = Новый Структура("СписокНоменклатуры, СписокХарактеристик");
	Структура.СписокНоменклатуры = Новый СписокЗначений;
	Структура.СписокХарактеристик = Новый СписокЗначений;
	Для Каждого Стр Из ТаблицаЗначенийРезультат Цикл
		Если Структура.СписокНоменклатуры.НайтиПоЗначению(Стр.Номенклатура) = Неопределено Тогда
			Структура.СписокНоменклатуры.Добавить(Стр.Номенклатура);
		КонецЕсли;
		Если ИспользоватьХарактеристикиНоменклатуры Тогда
			Если Структура.СписокХарактеристик.НайтиПоЗначению(Стр.Характеристика) = Неопределено Тогда
				Структура.СписокХарактеристик.Добавить(Стр.Характеристика);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат Структура;

КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ПользовательскиеНастройкиМодифицированы = Ложь;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	АдресТаблицыОтборов = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "АдресТаблицыОтборов").Значение;
	
	УстановитьОбязательныеНастройки(ПользовательскиеНастройкиМодифицированы);
	ИнициализироватьМенеджерВременныхТаблицДаннымиОтбора(МенеджерВременныхТаблиц, АдресТаблицыОтборов);
	
	СхемаКомпоновкиДанных.НаборыДанных.КонтрольОформленияДокументов.Запрос =
		ТекстЗапросаКонтрольОформленияДокументовТовародвижений(ЗначениеЗаполнено(АдресТаблицыОтборов));
	
	// Сформируем отчет
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);

	// Удалим отборы по пустым полям из запроса по излишкам / недостачам
	ИсходныйЗапросМакета = МакетКомпоновки.НаборыДанных.КонтрольОформленияДокументов.Запрос;
	МакетКомпоновки.НаборыДанных.КонтрольОформленияДокументов.Запрос = 
		ТекстЗапросаОформлениеИзлишковНедостачБезОтборов(ИсходныйЗапросМакета);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина, , МенеджерВременныхТаблиц);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	// Сообщим форме отчета, что настройки модифицированы
	Если ПользовательскиеНастройкиМодифицированы Тогда
		КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПользовательскиеНастройкиМодифицированы", Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ИнициализироватьМенеджерВременныхТаблицДаннымиОтбора(МенеджерВременныхТаблиц, АдресТаблицыОтборов)
	
	Если ЗначениеЗаполнено(АдресТаблицыОтборов) Тогда
		ТаблицаОтборов = ПолучитьИзВременногоХранилища(АдресТаблицыОтборов);
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаОтборов.Номенклатура		КАК Номенклатура,
		|	ТаблицаОтборов.Характеристика	КАК Характеристика,
		|	ТаблицаОтборов.Назначение		КАК Назначение,
		|	ТаблицаОтборов.Серия			КАК Серия,
		|	ТаблицаОтборов.Склад			КАК Склад
		|ПОМЕСТИТЬ ТаблицаОтборов
		|ИЗ
		|	&ТаблицаОтборов КАК ТаблицаОтборов";
		
		Запрос.УстановитьПараметр("ТаблицаОтборов", ТаблицаОтборов);
		
		Запрос.Выполнить();
	КонецЕсли;
	
КонецПроцедуры

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - см. ОтчетыКлиентСервер.НастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.События.ПриСозданииНаСервере = Истина;
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   ЭтаФорма - ФормаКлиентскогоПриложения - Форма отчета.
//   Отказ - Булево - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Булево - Передается из параметров обработчика "как есть".
//
// См. также:
//   "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	Параметры = ЭтаФорма.Параметры;
	КомпоновщикНастроекФормы = ЭтаФорма.Отчет.КомпоновщикНастроек;
	НастройкиОтчета = ЭтаФорма.НастройкиОтчета;
	
	Схема = ПолучитьИзВременногоХранилища(ЭтаФорма.НастройкиОтчета.АдресСхемы);
	Поля = Схема.НаборыДанных.КонтрольОформленияДокументов.Поля; // ПоляНабораДанныхСхемыКомпоновкиДанных
	ПолеСклад = Поля.Найти("Склад");
	Если ПолеСклад <> Неопределено Тогда
		ПараметрыРедактирования = ПолеСклад.ПараметрыРедактирования.Элементы;
	КонецЕсли;
КонецПроцедуры

// Вызывается перед загрузкой новых настроек. Используется для изменения схемы компоновки.
//   Например, если схема отчета зависит от ключа варианта или параметров отчета.
//   Чтобы изменения схемы вступили в силу следует вызывать метод ОтчетыСервер.ПодключитьСхему().
//
// Параметры:
//   Контекст - Произвольный - 
//       Параметры контекста, в котором используется отчет.
//       Используется для передачи в параметрах метода ОтчетыСервер.ПодключитьСхему().
//   КлючСхемы - Строка -
//       Идентификатор текущей схемы компоновщика настроек.
//       По умолчанию не заполнен (это означает что компоновщик инициализирован на основании основной схемы).
//       Используется для оптимизации, чтобы переинициализировать компоновщик как можно реже).
//       Может не использоваться если переинициализация выполняется безусловно.
//   КлючВарианта - Строка, Неопределено -
//       Имя предопределенного или уникальный идентификатор пользовательского варианта отчета.
//       Неопределено когда вызов для варианта расшифровки или без контекста.
//   НовыеНастройкиКД - НастройкиКомпоновкиДанных, Неопределено -
//       Настройки варианта отчета, которые будут загружены в компоновщик настроек после его инициализации.
//       Неопределено когда настройки варианта не надо загружать (уже загружены ранее).
//   НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных, Неопределено -
//       Пользовательские настройки, которые будут загружены в компоновщик настроек после его инициализации.
//       Неопределено когда пользовательские настройки не надо загружать (уже загружены ранее).
//
// Примеры:
// 1. Компоновщик отчета инициализируется на основании схемы из общих макетов:
//	Если КлючСхемы <> "1" Тогда
//		КлючСхемы = "1";
//		СхемаКД = ПолучитьОбщийМакет("МояОбщаяСхемаКомпоновки");
//		ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКД, КлючСхемы);
//	КонецЕсли;
//
// 2. Схема зависит от значения параметра, выведенного в пользовательские настройки отчета:
//	Если ТипЗнч(НовыеПользовательскиеНастройкиКД) = Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда
//		ПолноеИмяОбъектаМетаданных = "";
//		Для Каждого ЭлементКД Из НовыеПользовательскиеНастройкиКД.Элементы Цикл
//			Если ТипЗнч(ЭлементКД) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
//				ИмяПараметра = Строка(ЭлементКД.Параметр);
//				Если ИмяПараметра = "ОбъектМетаданных" Тогда
//					ПолноеИмяОбъектаМетаданных = ЭлементКД.Значение;
//				КонецЕсли;
//			КонецЕсли;
//		КонецЦикла;
//		Если КлючСхемы <> ПолноеИмяОбъектаМетаданных Тогда
//			КлючСхемы = ПолноеИмяОбъектаМетаданных;
//			СхемаКД = Новый СхемаКомпоновкиДанных;
//			// Наполнение схемы...
//			ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКД, КлючСхемы);
//		КонецЕсли;
//	КонецЕсли;
//
Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	// Локализация списка значений
	ДоступныеЗначения = ДоступныеЗначенияПоляВидОперации();
	
	ВидОперации = СхемаКомпоновкиДанных.НаборыДанных.КонтрольОформленияДокументов.Поля.Найти("ВидОперации");
	ВидОперации.УстановитьДоступныеЗначения(ДоступныеЗначения);
	
	ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКомпоновкиДанных, КлючСхемы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьОбязательныеНастройки(ПользовательскиеНастройкиМодифицированы)
	
	ВидыОперацийИСинонимы = ВидыОперацийИСинонимы();
	
	// Строковые литералы
	ПредставленияРекомендаций = Отчеты.КонтрольОформленияДокументовТовародвижений.ПредставленияРекомендаций();
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияЗавершитеОформлениеРасходныхОрдеровНаТовары", ПредставленияРекомендаций["ЗавершитеОформлениеРасходныхОрдеровНаТовары"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияОформитеВозвратТоваровОтКлиента", ПредставленияРекомендаций["ОформитеВозвратТоваровОтКлиента"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияОформитеПриходныеОрдераНаТовары", ПредставленияРекомендаций["ОформитеПриходныеОрдераНаТовары"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияОформитеПриобретениеТоваровИУслуг", ПредставленияРекомендаций["ОформитеПриобретениеТоваровИУслуг"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияОформитеПоступлениеТоваров", ПредставленияРекомендаций["ОформитеПоступлениеТоваров"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияОформитеПеремещениеТоваровВСтатусеПринято", ПредставленияРекомендаций["ОформитеПеремещениеТоваровВСтатусеПринято"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияОформитеДокументСборкиВСтатусеСобраноРазобрано", ПредставленияРекомендаций["ОформитеДокументСборкиВСтатусеСобраноРазобрано"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияОформитеДокументРазборкиВСтатусеСобраноРазобрано", ПредставленияРекомендаций["ОформитеДокументРазборкиВСтатусеСобраноРазобрано"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияОформитеПрочееОприходованиеТоваров", ПредставленияРекомендаций["ОформитеПрочееОприходованиеТоваров"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияОформитеСкладскиеАктыДляОтраженияИзлишков", ПредставленияРекомендаций["ОформитеСкладскиеАктыДляОтраженияИзлишков"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияОформитеСкладскиеАктыДляОтраженияНедостач", ПредставленияРекомендаций["ОформитеСкладскиеАктыДляОтраженияНедостач"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияОформитеПередачуМатериаловВПроизводствоВСтатусеПринято", ПредставленияРекомендаций["ОформитеПередачуМатериаловВПроизводствоВСтатусеПринято"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияОформитеВозвратМатериаловИзПроизводства", ПредставленияРекомендаций["ОформитеВозвратМатериаловИзПроизводства"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияОформитеВыпускПродукции", ПредставленияРекомендаций["ОформитеВыпускПродукции"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияОформитеВозвратСырьяОтПереработчика", ПредставленияРекомендаций["ОформитеВозвратСырьяОтПереработчика"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияОформитеПоступлениеОтПереработчика", ПредставленияРекомендаций["ОформитеПоступлениеОтПереработчика"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияОформитеПоступлениеСырьяОтДавальца", ПредставленияРекомендаций["ОформитеПоступлениеСырьяОтДавальца"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияОформитеРасходныеОрдераНаТовары", ПредставленияРекомендаций["ОформитеРасходныеОрдераНаТовары"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияОформитеРеализацииТоваровИУслуг", ПредставленияРекомендаций["ОформитеРеализацииТоваровИУслуг"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияОформитеОтгрузкиТоваровСХранения", ПредставленияРекомендаций["ОформитеОтгрузкиТоваровСХранения"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаРекомендацияОформитеПриемкиТоваровНаХранение", ПредставленияРекомендаций["ОформитеПриемкиТоваровНаХранение"]);
	
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаТипОтклоненияОформлениеФинансовыхДокументов", НСтр("ru='Оформление финансовых документов'"));
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаТипОтклоненияОформлениеСкладскихДокументов", НСтр("ru='Оформление складских документов'"));
	
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииОтгрузкаКлиентам", ВидыОперацийИСинонимы["ОтгрузкаКлиентам"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииВозвратыТоваровОтКлиентов", ВидыОперацийИСинонимы["ВозвратыТоваровОтКлиентов"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииПриемкаОтПоставщиков", ВидыОперацийИСинонимы["ПриемкаОтПоставщиков"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииВозвратыТоваровПоставщикам", ВидыОперацийИСинонимы["ВозвратыТоваровПоставщикам"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииВнутреннееПотребление", ВидыОперацийИСинонимы["ВнутреннееПотребление"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииПеремещениеТоваров", ВидыОперацийИСинонимы["ПеремещениеТоваров"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииСборкаРазборкаТоваров", ВидыОперацийИСинонимы["СборкаРазборкаТоваров"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииПрочееОприходованиеТоваров", ВидыОперацийИСинонимы["ПрочееОприходованиеТоваров"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииПередачаМатериаловВПроизводство", ВидыОперацийИСинонимы["ПередачаМатериаловВПроизводство"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииВозвратыМатериаловИзПроизводства", ВидыОперацийИСинонимы["ВозвратыМатериаловИзПроизводства"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииПриемкаГотовойПродукции", ВидыОперацийИСинонимы["ПриемкаГотовойПродукции"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииПереработкаСырьяДавальца", ВидыОперацийИСинонимы["ПереработкаСырьяДавальца"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииВозвратыСырьяДавальцам", ВидыОперацийИСинонимы["ВозвратыСырьяДавальцам"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииПередачиПродукцииДавальцам", ВидыОперацийИСинонимы["ПередачиПродукцииДавальцам"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииПереработкаСырьяУПереработчика", ВидыОперацийИСинонимы["ПереработкаСырьяУПереработчика"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииВозвратыСырьяОтПереработчиков", ВидыОперацийИСинонимы["ВозвратыСырьяОтПереработчиков"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииПоступленияПродукцииОтПереработчиков", ВидыОперацийИСинонимы["ПоступленияПродукцииОтПереработчиков"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииОформлениеИзлишковИНедостач", ВидыОперацийИСинонимы["ОформлениеИзлишковИНедостач"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииОтгрузкаПринятыхТоваровСХранения", ВидыОперацийИСинонимы["ОтгрузкаПринятыхТоваровСХранения"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииПриемкаТоваровНаХранение", ВидыОперацийИСинонимы["ПриемкаТоваровНаХранение"]);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СтрокаВидОперацииПередачаТоваровХранителю", ВидыОперацийИСинонимы["ПередачаТоваровХранителю"]);
	
	// Адаптируем локализацию для отборов
	Поля = СхемаКомпоновкиДанных.НаборыДанных.КонтрольОформленияДокументов.Поля;
	
	ПолеРекомендация = Поля.Найти("Рекомендация");
	ЛокализоватьДоступныеЗначенияПоля(ПолеРекомендация);
	
	ПолеТипОтклонения = Поля.Найти("ТипОтклонения");
	ЛокализоватьДоступныеЗначенияПоля(ПолеТипОтклонения);
	
	ПолеВидОперации = Поля.Найти("ВидОперации");
	ЛокализоватьДоступныеЗначенияПоля(ПолеВидОперации);
КонецПроцедуры

Процедура ЛокализоватьДоступныеЗначенияПоля(Поле)
	ДоступныеЗначения = Поле.ПолучитьДоступныеЗначения();
	
	Для Каждого ДоступноеЗначение Из ДоступныеЗначения Цикл
		
		ПараметрПоИмениДоступногоЗначения = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, ДоступноеЗначение.Значение);
		
		Если Не ПараметрПоИмениДоступногоЗначения = Неопределено Тогда
			
			ДоступноеЗначение.Значение = ПараметрПоИмениДоступногоЗначения.Значение;
			ДоступноеЗначение.Представление = ПараметрПоИмениДоступногоЗначения.Значение;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Поле.УстановитьДоступныеЗначения(ДоступныеЗначения);
КонецПроцедуры

Функция ТекстЗапросаПродажи()
	// Строковые литералы локализуются в настройках СКД отчета
	// Наборы данных - Поля - Доступные значения.
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&СтрокаВидОперацииОтгрузкаКлиентам КАК ВидОперации,
	|	&СтрокаТипОтклоненияОформлениеСкладскихДокументов КАК ТипОтклонения,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|			ТОГДА &СтрокаРекомендацияОформитеРеализацииТоваровИУслуг
	|		КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|			ТОГДА &СтрокаРекомендацияЗавершитеОформлениеРасходныхОрдеровНаТовары
	|	КОНЕЦ КАК Рекомендация,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|			ТОГДА -ТоварыКОтгрузке.КОтгрузкеОстаток
	|	КОНЕЦ КАК НезавершеннаяСкладскаяОперация,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.КОформлениюОстаток > 0
	|			ТОГДА ТоварыКОтгрузке.КОформлениюОстаток
	|	КОНЕЦ КАК НезавершеннаяФинансоваяОперация,
	|	ЕСТЬNULL(РеквизитыРаспоряжения.ДатаДокументаИБ, ДАТАВРЕМЯ(1,1,1)) КАК ДатаОперации,
	|	ТоварыКОтгрузке.ДокументОтгрузки КАК Распоряжение,
	|	ТоварыКОтгрузке.Склад КАК Склад,
	|	ТоварыКОтгрузке.Номенклатура КАК Номенклатура,
	|	ТоварыКОтгрузке.Характеристика КАК Характеристика,
	|	ТоварыКОтгрузке.Серия,
	|	ТоварыКОтгрузке.Назначение
	|{ВЫБРАТЬ
	|	Склад.*,
	|	Номенклатура.*,
	|	Характеристика.*,
	|	Серия.*,
	|	Назначение.*}
	|ИЗ
	|	РегистрНакопления.ТоварыКОтгрузке.Остатки(
	|		&ПериодГраница,
	|		ДокументОтгрузки В(
	|			ВЫБРАТЬ
	|				РаспоряженияНаОтгрузку.Ссылка
	|			ИЗ
	|				РаспоряженияНаОтгрузкуПоПродажам КАК РаспоряженияНаОтгрузку)
	|		И &ОтборТоваровПоРегистру
	|		{(ДокументОтгрузки).* КАК Распоряжение, (Склад).*, (Номенклатура).*, (Характеристика).*, (Серия).*, (Назначение).*}
	|	) КАК ТоварыКОтгрузке
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеквизитыРаспоряжения
	|		ПО ТоварыКОтгрузке.ДокументОтгрузки = РеквизитыРаспоряжения.Ссылка
	|			И НЕ РеквизитыРаспоряжения.ДополнительнаяЗапись
	|ГДЕ
	|	ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|	ИЛИ ТоварыКОтгрузке.КОформлениюОстаток > 0
	|{ГДЕ
	|	(&СтрокаВидОперацииОтгрузкаКлиентам) КАК ВидОперации,
	|	(&СтрокаТипОтклоненияОформлениеСкладскихДокументов) КАК ТипОтклонения,
	|	(ВЫБОР
	|			КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|				ТОГДА &СтрокаРекомендацияЗавершитеОформлениеРасходныхОрдеровНаТовары
	|		КОНЕЦ) КАК Рекомендация,
	|	(ВЫБОР
	|			КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|				ТОГДА -ТоварыКОтгрузке.КОтгрузкеОстаток
	|		КОНЕЦ) КАК НезавершеннаяСкладскаяОперация}";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаВозвратыТоваровОтКлиентов()
	// Строковые литералы локализуются в настройках СКД отчета
	// Наборы данных - Поля - Доступные значения.
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&СтрокаВидОперацииВозвратыТоваровОтКлиентов КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА &СтрокаТипОтклоненияОформлениеФинансовыхДокументов
	|		ИНАЧЕ &СтрокаТипОтклоненияОформлениеСкладскихДокументов
	|	КОНЕЦ КАК ТипОтклонения,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА &СтрокаРекомендацияОформитеВозвратТоваровОтКлиента
	|		ИНАЧЕ &СтрокаРекомендацияОформитеПриходныеОрдераНаТовары
	|	КОНЕЦ КАК Рекомендация,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток
	|	КОНЕЦ КАК НезавершеннаяСкладскаяОперация,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НезавершеннаяФинансоваяОперация,
	|	ЕСТЬNULL(РеквизитыРаспоряжения.ДатаДокументаИБ, ДАТАВРЕМЯ(1,1,1)) КАК ДатаОперации,
	|	ТоварыКОформлениюПоступления.ДокументПоступления КАК Распоряжение,
	|	ТоварыКОформлениюПоступления.Склад КАК Склад,
	|	ТоварыКОформлениюПоступления.Номенклатура КАК Номенклатура,
	|	ТоварыКОформлениюПоступления.Характеристика КАК Характеристика,
	|	ТоварыКОформлениюПоступления.Серия КАК Серия,
	|	ТоварыКОформлениюПоступления.Назначение КАК Назначение
	|{ВЫБРАТЬ
	|	Склад.*,
	|	Номенклатура.*,
	|	Характеристика.*,
	|	Серия.*,
	|	Назначение.*}
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Остатки(
	|		&ПериодГраница, 
	|		ТИПЗНАЧЕНИЯ(ДокументПоступления) В (ТИП(Документ.ЗаявкаНаВозвратТоваровОтКлиента), 
	|			ТИП(Документ.ВозвратТоваровОтКлиента)) 
	|		И &ОтборТоваровПоРегистру
	|		{(ДокументПоступления).* КАК Распоряжение, (Склад).*, (Номенклатура).*, (Характеристика).*, (Серия).*, (Назначение).*}
	|	) КАК ТоварыКОформлениюПоступления
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеквизитыРаспоряжения
	|		ПО ТоварыКОформлениюПоступления.ДокументПоступления = РеквизитыРаспоряжения.Ссылка
	|			И НЕ РеквизитыРаспоряжения.ДополнительнаяЗапись
	|{ГДЕ
	|	(&СтрокаВидОперацииВозвратыТоваровОтКлиентов) КАК ВидОперации,
	|	(ВЫБОР
	|			КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА &СтрокаТипОтклоненияОформлениеФинансовыхДокументов
	|			ИНАЧЕ &СтрокаТипОтклоненияОформлениеСкладскихДокументов
	|		КОНЕЦ) КАК ТипОтклонения,
	|	(ВЫБОР
	|			КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА &СтрокаРекомендацияОформитеВозвратТоваровОтКлиента
	|			ИНАЧЕ &СтрокаРекомендацияОформитеПриходныеОрдераНаТовары
	|		КОНЕЦ) КАК Рекомендация,
	|	(ВЫБОР
	|			КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА 0
	|			ИНАЧЕ -ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток
	|		КОНЕЦ) КАК НезавершеннаяСкладскаяОперация,
	|	(ВЫБОР
	|			КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НезавершеннаяФинансоваяОперация}";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаЗакупки()
	// Строковые литералы локализуются в настройках СКД отчета
	// Наборы данных - Поля - Доступные значения.
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&СтрокаВидОперацииПриемкаОтПоставщиков КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА &СтрокаТипОтклоненияОформлениеФинансовыхДокументов
	|		ИНАЧЕ &СтрокаТипОтклоненияОформлениеСкладскихДокументов
	|	КОНЕЦ КАК ТипОтклонения,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА ВЫБОР
	|					КОГДА ТоварыКОформлениюПоступления.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|							ИЛИ ТоварыКОформлениюПоступления.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаКомиссию)
	|						ТОГДА &СтрокаРекомендацияОформитеПриобретениеТоваровИУслуг
	|					ИНАЧЕ &СтрокаРекомендацияОформитеПоступлениеТоваров
	|				КОНЕЦ
	|		ИНАЧЕ &СтрокаРекомендацияОформитеПриходныеОрдераНаТовары
	|	КОНЕЦ КАК Рекомендация,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток
	|	КОНЕЦ КАК НезавершеннаяСкладскаяОперация,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НезавершеннаяФинансоваяОперация,
	|	ЕСТЬNULL(РеквизитыРаспоряжения.ДатаДокументаИБ, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаОперации,
	|	ТоварыКОформлениюПоступления.ДокументПоступления КАК Распоряжение,
	|	ТоварыКОформлениюПоступления.Склад КАК Склад,
	|	ТоварыКОформлениюПоступления.Номенклатура КАК Номенклатура,
	|	ТоварыКОформлениюПоступления.Характеристика КАК Характеристика,
	|	ТоварыКОформлениюПоступления.Серия КАК Серия,
	|	ТоварыКОформлениюПоступления.Назначение КАК Назначение
	|{ВЫБРАТЬ
	|	Склад.*,
	|	Номенклатура.*,
	|	Характеристика.*,
	|	Серия.*,
	|	Назначение.*}
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Остатки(
	|			&ПериодГраница,
	|			ДокументПоступления В
	|				(ВЫБРАТЬ
	|					РаспоряженияНаПоступление.Ссылка
	|				ИЗ
	|					РаспоряженияНаПоступлениеПоЗакупке КАК РаспоряженияНаПоступление)
	|		И &ОтборТоваровПоРегистру
	|			{(ДокументПоступления).* КАК Распоряжение, (Склад).*, (Номенклатура).*, (Характеристика).*, (Серия).*, (Назначение).*}
	|) КАК ТоварыКОформлениюПоступления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеквизитыРаспоряжения
	|		ПО ТоварыКОформлениюПоступления.ДокументПоступления = РеквизитыРаспоряжения.Ссылка
	|			И (НЕ РеквизитыРаспоряжения.ДополнительнаяЗапись)
	|{ГДЕ
	|	(&СтрокаВидОперацииПриемкаОтПоставщиков) КАК ВидОперации,
	|	(ВЫБОР
	|			КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА &СтрокаТипОтклоненияОформлениеФинансовыхДокументов
	|			ИНАЧЕ &СтрокаТипОтклоненияОформлениеСкладскихДокументов
	|		КОНЕЦ) КАК ТипОтклонения,
	|	(ВЫБОР
	|			КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА ВЫБОР
	|						КОГДА ТоварыКОформлениюПоступления.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|								ИЛИ ТоварыКОформлениюПоступления.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаКомиссию)
	|							ТОГДА &СтрокаРекомендацияОформитеПриобретениеТоваровИУслуг
	|						ИНАЧЕ &СтрокаРекомендацияОформитеПоступлениеТоваров
	|					КОНЕЦ
	|			ИНАЧЕ &СтрокаРекомендацияОформитеПриходныеОрдераНаТовары
	|		КОНЕЦ) КАК Рекомендация,
	|	(ВЫБОР
	|			КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА 0
	|			ИНАЧЕ -ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток
	|		КОНЕЦ) КАК НезавершеннаяСкладскаяОперация,
	|	(ВЫБОР
	|			КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НезавершеннаяФинансоваяОперация}";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаВременныхТаблицЗакупки()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументПоступления.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ РаспоряженияНаПоступлениеПоЗакупке
	|ИЗ
	|	Справочник.СоглашенияСПоставщиками КАК ДокументПоступления
	|ГДЕ
	|	НЕ ДокументПоступления.ПометкаУдаления
	|	И ДокументПоступления.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаКомиссию))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументПоступления.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДокументПоступления
	|ГДЕ
	|	НЕ ДокументПоступления.ПометкаУдаления
	|	И ДокументПоступления.ВариантПриемкиТоваров В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.ПоДоговорамПослеНакладных),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.ПоДоговорамПослеЗаказовИлиНакладных),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.ПоДоговорамБезЗаказовИНакладных))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументПоступления.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗаказПоставщику КАК ДокументПоступления
	|ГДЕ
	|	НЕ ДокументПоступления.ПометкаУдаления
	|	И ДокументПоступления.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаКомиссию))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументПоступления.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ДокументПоступления
	|ГДЕ
	|	НЕ ДокументПоступления.ПометкаУдаления
	|	И ДокументПоступления.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаКомиссию))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВозвратыТоваровПоставщикам()
	// Строковые литералы локализуются в настройках СКД отчета
	// Наборы данных - Поля - Доступные значения.
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&СтрокаВидОперацииВозвратыТоваровПоставщикам КАК ВидОперации,
	|	&СтрокаТипОтклоненияОформлениеСкладскихДокументов КАК ТипОтклонения,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|			ТОГДА &СтрокаРекомендацияЗавершитеОформлениеРасходныхОрдеровНаТовары
	|	КОНЕЦ КАК Рекомендация,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|			ТОГДА -ТоварыКОтгрузке.КОтгрузкеОстаток
	|	КОНЕЦ КАК НезавершеннаяСкладскаяОперация,
	|	0 КАК НезавершеннаяФинансоваяОперация,
	|	ЕСТЬNULL(РеквизитыРаспоряжения.ДатаДокументаИБ, ДАТАВРЕМЯ(1,1,1)) КАК ДатаОперации,
	|	ТоварыКОтгрузке.ДокументОтгрузки КАК Распоряжение,
	|	ТоварыКОтгрузке.Склад КАК Склад,
	|	ТоварыКОтгрузке.Номенклатура КАК Номенклатура,
	|	ТоварыКОтгрузке.Характеристика КАК Характеристика,
	|	ТоварыКОтгрузке.Серия,
	|	ТоварыКОтгрузке.Назначение
	|{ВЫБРАТЬ
	|	Склад.*,
	|	Номенклатура.*,
	|	Характеристика.*,
	|	Серия.*,
	|	Назначение.*}
	|ИЗ
	|	РегистрНакопления.ТоварыКОтгрузке.Остатки(
	|		&ПериодГраница, 
	|		ТИПЗНАЧЕНИЯ(ДокументОтгрузки) В (ТИП(Документ.ВозвратТоваровПоставщику)) 
	|		И &ОтборТоваровПоРегистру
	|		{(ДокументОтгрузки).* КАК Распоряжение, (Склад).*, (Номенклатура).*, (Характеристика).*, (Серия).*, (Назначение).*}
	|	) КАК ТоварыКОтгрузке
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеквизитыРаспоряжения
	|		ПО ТоварыКОтгрузке.ДокументОтгрузки = РеквизитыРаспоряжения.Ссылка
	|			И НЕ РеквизитыРаспоряжения.ДополнительнаяЗапись
	|ГДЕ
	|	ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|{ГДЕ
	|	(&СтрокаВидОперацииВозвратыТоваровПоставщикам) КАК ВидОперации,
	|	(&СтрокаТипОтклоненияОформлениеСкладскихДокументов) КАК ТипОтклонения,
	|	(ВЫБОР
	|			КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|				ТОГДА &СтрокаРекомендацияЗавершитеОформлениеРасходныхОрдеровНаТовары
	|		КОНЕЦ) КАК Рекомендация,
	|	(ВЫБОР
	|			КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|				ТОГДА -ТоварыКОтгрузке.КОтгрузкеОстаток
	|		КОНЕЦ) КАК НезавершеннаяСкладскаяОперация}";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаВнутреннееПотребление()
	// Строковые литералы локализуются в настройках СКД отчета
	// Наборы данных - Поля - Доступные значения.
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&СтрокаВидОперацииВнутреннееПотребление КАК ВидОперации,
	|	&СтрокаТипОтклоненияОформлениеСкладскихДокументов КАК ТипОтклонения,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|			ТОГДА &СтрокаРекомендацияЗавершитеОформлениеРасходныхОрдеровНаТовары
	|	КОНЕЦ КАК Рекомендация,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|			ТОГДА -ТоварыКОтгрузке.КОтгрузкеОстаток
	|	КОНЕЦ КАК НезавершеннаяСкладскаяОперация,
	|	0 КАК НезавершеннаяФинансоваяОперация,
	|	ЕСТЬNULL(РеквизитыРаспоряжения.ДатаДокументаИБ, ДАТАВРЕМЯ(1,1,1)) КАК ДатаОперации,
	|	ТоварыКОтгрузке.ДокументОтгрузки КАК Распоряжение,
	|	ТоварыКОтгрузке.Склад КАК Склад,
	|	ТоварыКОтгрузке.Номенклатура КАК Номенклатура,
	|	ТоварыКОтгрузке.Характеристика КАК Характеристика,
	|	ТоварыКОтгрузке.Серия,
	|	ТоварыКОтгрузке.Назначение
	|{ВЫБРАТЬ
	|	Склад.*,
	|	Номенклатура.*,
	|	Характеристика.*,
	|	Серия.*,
	|	Назначение.*}
	|ИЗ
	|	РегистрНакопления.ТоварыКОтгрузке.Остатки(
	|		&ПериодГраница, 
	|		ТИПЗНАЧЕНИЯ(ДокументОтгрузки) В (ТИП(Документ.ЗаказНаВнутреннееПотребление), 
	|			ТИП(Документ.ВнутреннееПотребление)
	|		) 
	|		И &ОтборТоваровПоРегистру
	|		{(ДокументОтгрузки).* КАК Распоряжение, (Склад).*, (Номенклатура).*, (Характеристика).*, (Серия).*, (Назначение).*}
	|	) КАК ТоварыКОтгрузке
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеквизитыРаспоряжения
	|		ПО ТоварыКОтгрузке.ДокументОтгрузки = РеквизитыРаспоряжения.Ссылка
	|			И НЕ РеквизитыРаспоряжения.ДополнительнаяЗапись
	|ГДЕ
	|	ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|{ГДЕ
	|	(&СтрокаВидОперацииВнутреннееПотребление) КАК ВидОперации,
	|	(&СтрокаТипОтклоненияОформлениеСкладскихДокументов) КАК ТипОтклонения,
	|	(ВЫБОР
	|			КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|				ТОГДА &СтрокаРекомендацияЗавершитеОформлениеРасходныхОрдеровНаТовары
	|		КОНЕЦ) КАК Рекомендация,
	|	(ВЫБОР
	|			КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|				ТОГДА -ТоварыКОтгрузке.КОтгрузкеОстаток
	|		КОНЕЦ) КАК НезавершеннаяСкладскаяОперация}";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаПеремещениеТоваров()
	// Строковые литералы локализуются в настройках СКД отчета
	// Наборы данных - Поля - Доступные значения.
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&СтрокаВидОперацииПеремещениеТоваров КАК ВидОперации,
	|	&СтрокаТипОтклоненияОформлениеСкладскихДокументов КАК ТипОтклонения,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|			ТОГДА &СтрокаРекомендацияЗавершитеОформлениеРасходныхОрдеровНаТовары
	|	КОНЕЦ КАК Рекомендация,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|			ТОГДА -ТоварыКОтгрузке.КОтгрузкеОстаток
	|	КОНЕЦ КАК НезавершеннаяСкладскаяОперация,
	|	0 КАК НезавершеннаяФинансоваяОперация,
	|	ЕСТЬNULL(РеквизитыРаспоряжения.ДатаДокументаИБ, ДАТАВРЕМЯ(1,1,1)) КАК ДатаОперации,
	|	ТоварыКОтгрузке.ДокументОтгрузки КАК Распоряжение,
	|	ТоварыКОтгрузке.Склад КАК Склад,
	|	ТоварыКОтгрузке.Номенклатура КАК Номенклатура,
	|	ТоварыКОтгрузке.Характеристика КАК Характеристика,
	|	ТоварыКОтгрузке.Серия,
	|	ТоварыКОтгрузке.Назначение
	|ИЗ
	|	РегистрНакопления.ТоварыКОтгрузке.Остатки(
	|		&ПериодГраница, 
	|		ТИПЗНАЧЕНИЯ(ДокументОтгрузки) В (ТИП(Документ.ЗаказНаПеремещение), 
	|			ТИП(Документ.ПеремещениеТоваров)) 
	|		И &ОтборТоваровПоРегистру
	|		{(ДокументОтгрузки).* КАК Распоряжение, (Склад).*, (Номенклатура).*, (Характеристика).*, (Серия).*, (Назначение).*}
	|	) КАК ТоварыКОтгрузке
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеквизитыРаспоряжения
	|		ПО ТоварыКОтгрузке.ДокументОтгрузки = РеквизитыРаспоряжения.Ссылка
	|			И НЕ РеквизитыРаспоряжения.ДополнительнаяЗапись
	|ГДЕ
	|	ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|{ГДЕ
	|	(&СтрокаВидОперацииПеремещениеТоваров) КАК ВидОперации,
	|	(&СтрокаТипОтклоненияОформлениеСкладскихДокументов) КАК ТипОтклонения,
	|	(ВЫБОР
	|			КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|				ТОГДА &СтрокаРекомендацияЗавершитеОформлениеРасходныхОрдеровНаТовары
	|		КОНЕЦ) КАК Рекомендация,
	|	(ВЫБОР
	|			КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|				ТОГДА -ТоварыКОтгрузке.КОтгрузкеОстаток
	|		КОНЕЦ) КАК НезавершеннаяСкладскаяОперация}
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&СтрокаВидОперацииПеремещениеТоваров,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА &СтрокаТипОтклоненияОформлениеФинансовыхДокументов
	|		ИНАЧЕ &СтрокаТипОтклоненияОформлениеСкладскихДокументов
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА &СтрокаРекомендацияОформитеПеремещениеТоваровВСтатусеПринято
	|		ИНАЧЕ &СтрокаРекомендацияОформитеПриходныеОрдераНаТовары
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ЕСТЬNULL(РеквизитыРаспоряжения.ДатаДокументаИБ, ДАТАВРЕМЯ(1,1,1)),
	|	ТоварыКОформлениюПоступления.ДокументПоступления,
	|	ТоварыКОформлениюПоступления.Склад,
	|	ТоварыКОформлениюПоступления.Номенклатура,
	|	ТоварыКОформлениюПоступления.Характеристика,
	|	ТоварыКОформлениюПоступления.Серия,
	|	ТоварыКОформлениюПоступления.Назначение
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Остатки(
	|		&ПериодГраница, 
	|		ТИПЗНАЧЕНИЯ(ДокументПоступления) В (ТИП(Документ.ЗаказНаПеремещение), 
	|			ТИП(Документ.ПеремещениеТоваров)) 
	|		И &ОтборТоваровПоРегистру
	|		{(ДокументПоступления).* КАК Распоряжение, (Склад).*, (Номенклатура).*, (Характеристика).*, (Серия).*, (Назначение).*}
	|	) КАК ТоварыКОформлениюПоступления
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеквизитыРаспоряжения
	|		ПО ТоварыКОформлениюПоступления.ДокументПоступления = РеквизитыРаспоряжения.Ссылка
	|			И НЕ РеквизитыРаспоряжения.ДополнительнаяЗапись
	|{ГДЕ
	|	(&СтрокаВидОперацииПеремещениеТоваров) КАК ВидОперации,
	|	(ВЫБОР
	|			КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА &СтрокаТипОтклоненияОформлениеФинансовыхДокументов
	|			ИНАЧЕ &СтрокаТипОтклоненияОформлениеСкладскихДокументов
	|		КОНЕЦ) КАК ТипОтклонения,
	|	(ВЫБОР
	|			КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА &СтрокаРекомендацияОформитеПеремещениеТоваровВСтатусеПринято
	|			ИНАЧЕ &СтрокаРекомендацияОформитеПриходныеОрдераНаТовары
	|		КОНЕЦ) КАК Рекомендация,
	|	(ВЫБОР
	|			КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА 0
	|			ИНАЧЕ -ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток
	|		КОНЕЦ) КАК НезавершеннаяСкладскаяОперация,
	|	(ВЫБОР
	|			КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НезавершеннаяФинансоваяОперация}
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&СтрокаВидОперацииПеремещениеТоваров,
	|	&СтрокаТипОтклоненияОформлениеСкладскихДокументов,
	|	&СтрокаРекомендацияОформитеПеремещениеТоваровВСтатусеПринято,
	|	ПеремещениеТоваров.Количество,
	|	ПеремещениеТоваров.Количество,
	|	ВЫБОР
	|		КОГДА НЕ ПеремещениеТоваров.Ссылка.ЗаказНаПеремещение.Ссылка ЕСТЬ NULL 
	|			ТОГДА ПеремещениеТоваров.Ссылка.ЗаказНаПеремещение.Дата
	|		ИНАЧЕ ПеремещениеТоваров.Ссылка.Дата
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ ПеремещениеТоваров.Ссылка.ЗаказНаПеремещение.Ссылка ЕСТЬ NULL 
	|			ТОГДА ПеремещениеТоваров.Ссылка.ЗаказНаПеремещение
	|		ИНАЧЕ ПеремещениеТоваров.Ссылка
	|	КОНЕЦ,
	|	ПеремещениеТоваров.Ссылка.СкладПолучатель,
	|	ПеремещениеТоваров.Номенклатура,
	|	ПеремещениеТоваров.Характеристика,
	|	ПеремещениеТоваров.Серия,
	|	ПеремещениеТоваров.Назначение
	|ИЗ
	|	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументов
	|		ПО ПеремещениеТоваров.Ссылка = РеестрДокументов.СторнируемыйДокумент
	|			И РеестрДокументов.Проведен
	|			И НЕ РеестрДокументов.ДополнительнаяЗапись
	|ГДЕ
	|	ПеремещениеТоваров.Ссылка.Проведен
	|	И РеестрДокументов.Ссылка ЕСТЬ NULL
	|	И ПеремещениеТоваров.Ссылка.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыПеремещенийТоваров.Принято)
	|	И ПеремещениеТоваров.Ссылка.Дата <= &ДатаКонцаДня
	|	И &ОтборТоваровПоПеремещению
	|{ГДЕ
	|	(&СтрокаВидОперацииПеремещениеТоваров) КАК ВидОперации,
	|	(&СтрокаТипОтклоненияОформлениеСкладскихДокументов) КАК ТипОтклонения,
	|	(&СтрокаРекомендацияОформитеПеремещениеТоваровВСтатусеПринято) КАК Рекомендация,
	|	ПеремещениеТоваров.Количество КАК НезавершеннаяСкладскаяОперация,
	|	ПеремещениеТоваров.Количество КАК НезавершеннаяФинансоваяОперация}";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаСборкаРазборка()
	// Строковые литералы локализуются в настройках СКД отчета
	// Наборы данных - Поля - Доступные значения.
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&СтрокаВидОперацииСборкаРазборкаТоваров КАК ВидОперации,
	|	&СтрокаТипОтклоненияОформлениеСкладскихДокументов КАК ТипОтклонения,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|			ТОГДА &СтрокаРекомендацияЗавершитеОформлениеРасходныхОрдеровНаТовары
	|	КОНЕЦ КАК Рекомендация,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|			ТОГДА -ТоварыКОтгрузке.КОтгрузкеОстаток
	|	КОНЕЦ КАК НезавершеннаяСкладскаяОперация,
	|	0 КАК НезавершеннаяФинансоваяОперация,
	|	ЕСТЬNULL(РеквизитыРаспоряжения.ДатаДокументаИБ, ДАТАВРЕМЯ(1,1,1)) КАК ДатаОперации,
	|	ТоварыКОтгрузке.ДокументОтгрузки КАК Распоряжение,
	|	ТоварыКОтгрузке.Склад КАК Склад,
	|	ТоварыКОтгрузке.Номенклатура КАК Номенклатура,
	|	ТоварыКОтгрузке.Характеристика КАК Характеристика,
	|	ТоварыКОтгрузке.Серия,
	|	ТоварыКОтгрузке.Назначение
	|{ВЫБРАТЬ
	|	Склад.*,
	|	Номенклатура.*,
	|	Характеристика.*,
	|	Серия.*,
	|	Назначение.*}
	|ИЗ
	|	РегистрНакопления.ТоварыКОтгрузке.Остатки(
	|		&ПериодГраница, 
	|		ТИПЗНАЧЕНИЯ(ДокументОтгрузки) В (ТИП(Документ.ЗаказНаСборку), 
	|			ТИП(Документ.СборкаТоваров)) 
	|		И &ОтборТоваровПоРегистру
	|		{(ДокументОтгрузки).* КАК Распоряжение, (Склад).*, (Номенклатура).*, (Характеристика).*, (Серия).*, (Назначение).*}
	|	) КАК ТоварыКОтгрузке
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеквизитыРаспоряжения
	|		ПО ТоварыКОтгрузке.ДокументОтгрузки = РеквизитыРаспоряжения.Ссылка
	|			И НЕ РеквизитыРаспоряжения.ДополнительнаяЗапись
	|ГДЕ
	|	ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|{ГДЕ
	|	(&СтрокаВидОперацииСборкаРазборкаТоваров) КАК ВидОперации,
	|	(&СтрокаТипОтклоненияОформлениеСкладскихДокументов) КАК ТипОтклонения,
	|	(ВЫБОР
	|			КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|				ТОГДА &СтрокаРекомендацияЗавершитеОформлениеРасходныхОрдеровНаТовары
	|		КОНЕЦ) КАК Рекомендация,
	|	(ВЫБОР
	|			КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|				ТОГДА -ТоварыКОтгрузке.КОтгрузкеОстаток
	|		КОНЕЦ) КАК НезавершеннаяСкладскаяОперация}
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&СтрокаВидОперацииСборкаРазборкаТоваров,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА &СтрокаТипОтклоненияОформлениеФинансовыхДокументов
	|		ИНАЧЕ &СтрокаТипОтклоненияОформлениеСкладскихДокументов
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(РеквизитыРаспоряжения.ХозяйственнаяОперация, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазборкаТоваров)
	|						ТОГДА &СтрокаРекомендацияОформитеДокументРазборкиВСтатусеСобраноРазобрано
	|					КОГДА ЕСТЬNULL(РеквизитыРаспоряжения.ХозяйственнаяОперация, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
	|						ТОГДА &СтрокаРекомендацияОформитеДокументСборкиВСтатусеСобраноРазобрано
	|				КОНЕЦ
	|		ИНАЧЕ &СтрокаРекомендацияОформитеПриходныеОрдераНаТовары
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ЕСТЬNULL(РеквизитыРаспоряжения.ДатаДокументаИБ, ДАТАВРЕМЯ(1,1,1)),
	|	ТоварыКОформлениюПоступления.ДокументПоступления,
	|	ТоварыКОформлениюПоступления.Склад,
	|	ТоварыКОформлениюПоступления.Номенклатура,
	|	ТоварыКОформлениюПоступления.Характеристика,
	|	ТоварыКОформлениюПоступления.Серия,
	|	ТоварыКОформлениюПоступления.Назначение
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Остатки(
	|		&ПериодГраница, 
	|		ТИПЗНАЧЕНИЯ(ДокументПоступления) В (ТИП(Документ.ЗаказНаСборку), 
	|			ТИП(Документ.СборкаТоваров)) 
	|		И &ОтборТоваровПоРегистру
	|		{(ДокументПоступления).* КАК Распоряжение, (Склад).*, (Номенклатура).*, (Характеристика).*, (Серия).*, (Назначение).*}
	|	) КАК ТоварыКОформлениюПоступления
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеквизитыРаспоряжения
	|		ПО ТоварыКОформлениюПоступления.ДокументПоступления = РеквизитыРаспоряжения.Ссылка
	|			И НЕ РеквизитыРаспоряжения.ДополнительнаяЗапись
	|{ГДЕ
	|	(&СтрокаВидОперацииСборкаРазборкаТоваров) КАК ВидОперации,
	|	(ВЫБОР
	|			КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА &СтрокаТипОтклоненияОформлениеФинансовыхДокументов
	|			ИНАЧЕ &СтрокаТипОтклоненияОформлениеСкладскихДокументов
	|		КОНЕЦ) КАК ТипОтклонения,
	|	(ВЫБОР
	|			КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА ВЫБОР
	|						КОГДА ЕСТЬNULL(РеквизитыРаспоряжения.ХозяйственнаяОперация, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазборкаТоваров)
	|							ТОГДА &СтрокаРекомендацияОформитеДокументРазборкиВСтатусеСобраноРазобрано
	|						КОГДА ЕСТЬNULL(РеквизитыРаспоряжения.ХозяйственнаяОперация, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
	|							ТОГДА &СтрокаРекомендацияОформитеДокументСборкиВСтатусеСобраноРазобрано
	|					КОНЕЦ
	|			ИНАЧЕ &СтрокаРекомендацияОформитеПриходныеОрдераНаТовары
	|		КОНЕЦ) КАК Рекомендация,
	|	(ВЫБОР
	|			КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА 0
	|			ИНАЧЕ -ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток
	|		КОНЕЦ) КАК НезавершеннаяСкладскаяОперация,
	|	(ВЫБОР
	|			КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НезавершеннаяФинансоваяОперация}
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&СтрокаВидОперацииСборкаРазборкаТоваров,
	|	&СтрокаТипОтклоненияОформлениеФинансовыхДокументов,
	|	&СтрокаРекомендацияОформитеДокументРазборкиВСтатусеСобраноРазобрано,
	|	0,
	|	СборкаРазборкаТоваров.Количество,
	|	ВЫБОР
	|		КОГДА НЕ СборкаРазборкаТоваров.Ссылка.ЗаказНаСборку.Ссылка ЕСТЬ NULL 
	|			ТОГДА СборкаРазборкаТоваров.Ссылка.ЗаказНаСборку.Дата
	|		ИНАЧЕ СборкаРазборкаТоваров.Ссылка.Дата
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ СборкаРазборкаТоваров.Ссылка.ЗаказНаСборку.Ссылка ЕСТЬ NULL 
	|			ТОГДА СборкаРазборкаТоваров.Ссылка.ЗаказНаСборку
	|		ИНАЧЕ СборкаРазборкаТоваров.Ссылка
	|	КОНЕЦ,
	|	СборкаРазборкаТоваров.Ссылка.Склад,
	|	СборкаРазборкаТоваров.Номенклатура,
	|	СборкаРазборкаТоваров.Характеристика,
	|	СборкаРазборкаТоваров.Серия,
	|	СборкаРазборкаТоваров.Назначение
	|ИЗ
	|	Документ.СборкаТоваров.Товары КАК СборкаРазборкаТоваров
	|ГДЕ
	|	СборкаРазборкаТоваров.Ссылка.Проведен
	|	И СборкаРазборкаТоваров.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазборкаТоваров)
	|	И СборкаРазборкаТоваров.Ссылка.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.СобраноРазобрано)
	|	И СборкаРазборкаТоваров.Ссылка.Дата <= &ДатаКонцаДня
	|	И &ОтборТоваровПоРазборке
	|{ГДЕ
	|	(&СтрокаВидОперацииСборкаРазборкаТоваров) КАК ВидОперации,
	|	(&СтрокаТипОтклоненияОформлениеФинансовыхДокументов) КАК ТипОтклонения,
	|	(&СтрокаРекомендацияОформитеДокументРазборкиВСтатусеСобраноРазобрано) КАК Рекомендация,
	|	СборкаРазборкаТоваров.Количество КАК НезавершеннаяСкладскаяОперация,
	|	СборкаРазборкаТоваров.Количество КАК НезавершеннаяФинансоваяОперация}
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&СтрокаВидОперацииСборкаРазборкаТоваров,
	|	&СтрокаТипОтклоненияОформлениеФинансовыхДокументов,
	|	&СтрокаРекомендацияОформитеДокументСборкиВСтатусеСобраноРазобрано,
	|	0,
	|	СборкаРазборкаТоваров.Количество,
	|	ВЫБОР
	|		КОГДА НЕ СборкаРазборкаТоваров.ЗаказНаСборку.Ссылка ЕСТЬ NULL 
	|			ТОГДА СборкаРазборкаТоваров.ЗаказНаСборку.Дата
	|		ИНАЧЕ СборкаРазборкаТоваров.Ссылка.Дата
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ СборкаРазборкаТоваров.ЗаказНаСборку.Ссылка ЕСТЬ NULL 
	|			ТОГДА СборкаРазборкаТоваров.ЗаказНаСборку
	|		ИНАЧЕ СборкаРазборкаТоваров.Ссылка
	|	КОНЕЦ,
	|	СборкаРазборкаТоваров.Склад,
	|	СборкаРазборкаТоваров.Номенклатура,
	|	СборкаРазборкаТоваров.Характеристика,
	|	СборкаРазборкаТоваров.Серия,
	|	СборкаРазборкаТоваров.Назначение
	|ИЗ
	|	Документ.СборкаТоваров КАК СборкаРазборкаТоваров
	|ГДЕ
	|	СборкаРазборкаТоваров.Проведен
	|	И СборкаРазборкаТоваров.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
	|	И СборкаРазборкаТоваров.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.СобраноРазобрано)
	|	И СборкаРазборкаТоваров.Дата <= &ДатаКонцаДня
	|	И &ОтборТоваровПоСборке
	|{ГДЕ
	|	(&СтрокаВидОперацииСборкаРазборкаТоваров) КАК ВидОперации,
	|	(&СтрокаТипОтклоненияОформлениеФинансовыхДокументов) КАК ТипОтклонения,
	|	(&СтрокаРекомендацияОформитеДокументСборкиВСтатусеСобраноРазобрано) КАК Рекомендация,
	|	СборкаРазборкаТоваров.Количество КАК НезавершеннаяФинансоваяОперация}";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаПрочиеОприходования()
	// Строковые литералы локализуются в настройках СКД отчета
	// Наборы данных - Поля - Доступные значения.
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&СтрокаВидОперацииПрочееОприходованиеТоваров КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА &СтрокаТипОтклоненияОформлениеФинансовыхДокументов
	|		ИНАЧЕ &СтрокаТипОтклоненияОформлениеСкладскихДокументов
	|	КОНЕЦ КАК ТипОтклонения,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА &СтрокаРекомендацияОформитеПрочееОприходованиеТоваров
	|		ИНАЧЕ &СтрокаРекомендацияОформитеПриходныеОрдераНаТовары
	|	КОНЕЦ КАК Рекомендация,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток
	|	КОНЕЦ КАК НезавершеннаяСкладскаяОперация,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НезавершеннаяФинансоваяОперация,
	|	ЕСТЬNULL(РеквизитыРаспоряжения.ДатаДокументаИБ, ДАТАВРЕМЯ(1,1,1)) КАК ДатаОперации,
	|	ТоварыКОформлениюПоступления.ДокументПоступления КАК Распоряжение,
	|	ТоварыКОформлениюПоступления.Склад КАК Склад,
	|	ТоварыКОформлениюПоступления.Номенклатура КАК Номенклатура,
	|	ТоварыКОформлениюПоступления.Характеристика КАК Характеристика,
	|	ТоварыКОформлениюПоступления.Серия,
	|	ТоварыКОформлениюПоступления.Назначение
	|{ВЫБРАТЬ
	|	Склад.*,
	|	Номенклатура.*,
	|	Характеристика.*,
	|	Серия.*,
	|	Назначение.*}
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Остатки(
	|		&ПериодГраница, 
	|		ТИПЗНАЧЕНИЯ(ДокументПоступления) В (ТИП(Документ.ПрочееОприходованиеТоваров)) 
	|		И &ОтборТоваровПоРегистру
	|		{(ДокументПоступления).* КАК Распоряжение, (Склад).*, (Номенклатура).*, (Характеристика).*, (Серия).*, (Назначение).*}
	|	) КАК ТоварыКОформлениюПоступления
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеквизитыРаспоряжения
	|		ПО ТоварыКОформлениюПоступления.ДокументПоступления = РеквизитыРаспоряжения.Ссылка
	|			И НЕ РеквизитыРаспоряжения.ДополнительнаяЗапись
	|{ГДЕ
	|	(&СтрокаВидОперацииПрочееОприходованиеТоваров) КАК ВидОперации,
	|	(ВЫБОР
	|			КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА &СтрокаТипОтклоненияОформлениеФинансовыхДокументов
	|			ИНАЧЕ &СтрокаТипОтклоненияОформлениеСкладскихДокументов
	|		КОНЕЦ) КАК ТипОтклонения,
	|	(ВЫБОР
	|			КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА &СтрокаРекомендацияОформитеПрочееОприходованиеТоваров
	|			ИНАЧЕ &СтрокаРекомендацияОформитеПриходныеОрдераНаТовары
	|		КОНЕЦ) КАК Рекомендация,
	|	(ВЫБОР
	|			КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА 0
	|			ИНАЧЕ -ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток
	|		КОНЕЦ) КАК НезавершеннаяСкладскаяОперация,
	|	(ВЫБОР
	|			КОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерамОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НезавершеннаяФинансоваяОперация}";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаОформлениеИзлишковНедостач()
	// Строковые литералы локализуются в настройках СКД отчета
	// Наборы данных - Поля - Доступные значения.
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&СтрокаВидОперацииОформлениеИзлишковИНедостач КАК ВидОперации,
	|	&СтрокаТипОтклоненияОформлениеФинансовыхДокументов КАК ТипОтклонения,
	|	ВЫБОР
	|		КОГДА НедооформленныеИзлишкиИНедостачи.КОформлениюАктовОстаток > 0
	|			ТОГДА &СтрокаРекомендацияОформитеСкладскиеАктыДляОтраженияИзлишков
	|		ИНАЧЕ &СтрокаРекомендацияОформитеСкладскиеАктыДляОтраженияНедостач
	|	КОНЕЦ КАК Рекомендация,
	|	0 КАК НезавершеннаяСкладскаяОперация,
	|	НедооформленныеИзлишкиИНедостачи.КОформлениюАктовОстаток КАК НезавершеннаяФинансоваяОперация,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОперации,
	|	Неопределено КАК Распоряжение,
	|	НедооформленныеИзлишкиИНедостачи.Склад КАК Склад,
	|	НедооформленныеИзлишкиИНедостачи.Номенклатура КАК Номенклатура,
	|	НедооформленныеИзлишкиИНедостачи.Характеристика КАК Характеристика,
	|	НедооформленныеИзлишкиИНедостачи.Серия,
	|	НедооформленныеИзлишкиИНедостачи.Назначение
	|{ВЫБРАТЬ
	|	Склад.*,
	|	Номенклатура.*,
	|	Характеристика.*,
	|	Серия.*,
	|	Назначение.*}
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюИзлишковНедостач.Остатки(
	|		&ПериодГраница, 
	|		&ОтборТоваровПоРегистру
	|		{(Склад).*, (Номенклатура).*, (Характеристика).*, (Серия).*, (Назначение).*}
	|	) КАК НедооформленныеИзлишкиИНедостачи
	|{ГДЕ
	|	(&СтрокаВидОперацииОформлениеИзлишковИНедостач) КАК ВидОперации,
	|	(&СтрокаТипОтклоненияОформлениеФинансовыхДокументов) КАК ТипОтклонения,
	|	(ВЫБОР
	|			КОГДА НедооформленныеИзлишкиИНедостачи.КОформлениюАктовОстаток > 0
	|				ТОГДА &СтрокаРекомендацияОформитеСкладскиеАктыДляОтраженияИзлишков
	|			ИНАЧЕ &СтрокаРекомендацияОформитеСкладскиеАктыДляОтраженияНедостач
	|		КОНЕЦ) КАК Рекомендация,
	|	НедооформленныеИзлишкиИНедостачи.КОформлениюАктовОстаток КАК НезавершеннаяФинансоваяОперация}";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаОтгрузкаПринятыхТоваровСХранения()
	// Строковые литералы локализуются в настройках СКД отчета
	// Наборы данных - Поля - Доступные значения.
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&СтрокаВидОперацииОтгрузкаПринятыхТоваровСХранения КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток > 0
	|			ТОГДА &СтрокаТипОтклоненияОформлениеСкладскихДокументов
	|		ИНАЧЕ &СтрокаТипОтклоненияОформлениеФинансовыхДокументов
	|	КОНЕЦ КАК ТипОтклонения,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток > 0
	|			ТОГДА &СтрокаРекомендацияОформитеРасходныеОрдераНаТовары
	|		ИНАЧЕ &СтрокаРекомендацияОформитеОтгрузкиТоваровСХранения
	|	КОНЕЦ КАК Рекомендация,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток > 0
	|			ТОГДА -ТоварыКОтгрузке.КОтгрузкеОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НезавершеннаяСкладскаяОперация,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток < 0
	|			ТОГДА -ТоварыКОтгрузке.КОтгрузкеОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НезавершеннаяФинансоваяОперация,
	|	ЕСТЬNULL(РеквизитыРаспоряжения.ДатаДокументаИБ, ДАТАВРЕМЯ(1,1,1)) КАК ДатаОперации,
	|	ТоварыКОтгрузке.ДокументОтгрузки КАК Распоряжение,
	|	ТоварыКОтгрузке.Склад КАК Склад,
	|	ТоварыКОтгрузке.Номенклатура КАК Номенклатура,
	|	ТоварыКОтгрузке.Характеристика КАК Характеристика,
	|	ТоварыКОтгрузке.Серия,
	|	ТоварыКОтгрузке.Назначение
	|{ВЫБРАТЬ
	|	Склад.*,
	|	Номенклатура.*,
	|	Характеристика.*,
	|	Серия.*,
	|	Назначение.*}
	|ИЗ
	|	РегистрНакопления.ТоварыКОтгрузке.Остатки(
	|		&ПериодГраница,
	|		ТИПЗНАЧЕНИЯ(ДокументОтгрузки) В (ТИП(Документ.ОтгрузкаТоваровСХранения))
	|		И &ОтборТоваровПоРегистру
	|		{(ДокументОтгрузки).* КАК Распоряжение, (Склад).*, (Номенклатура).*, (Характеристика).*, (Серия).*, (Назначение).*}
	|	) КАК ТоварыКОтгрузке
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеквизитыРаспоряжения
	|		ПО ТоварыКОтгрузке.ДокументОтгрузки = РеквизитыРаспоряжения.Ссылка
	|			И НЕ РеквизитыРаспоряжения.ДополнительнаяЗапись
	|ГДЕ
	|	ТоварыКОтгрузке.КОтгрузкеОстаток <> 0
	|{ГДЕ
	|	(&СтрокаВидОперацииОтгрузкаПринятыхТоваровСХранения) КАК ВидОперации,
	|	(ВЫБОР
	|			КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток > 0
	|				ТОГДА &СтрокаТипОтклоненияОформлениеСкладскихДокументов
	|			ИНАЧЕ &СтрокаТипОтклоненияОформлениеФинансовыхДокументов
	|		КОНЕЦ) КАК ТипОтклонения,
	|	(ВЫБОР
	|			КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток > 0
	|				ТОГДА &СтрокаРекомендацияОформитеРасходныеОрдераНаТовары
	|			ИНАЧЕ &СтрокаРекомендацияОформитеОтгрузкиТоваровСХранения
	|		КОНЕЦ) КАК Рекомендация,
	|	(ВЫБОР
	|			КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток > 0
	|				ТОГДА ТоварыКОтгрузке.КОтгрузкеОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НезавершеннаяСкладскаяОперация,
	|	(ВЫБОР
	|			КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток < 0
	|				ТОГДА ТоварыКОтгрузке.КОтгрузкеОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НезавершеннаяФинансоваяОперация}";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаПриемкаТоваровНаХранение()
	// Строковые литералы локализуются в настройках СКД отчета
	// Наборы данных - Поля - Доступные значения.
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&СтрокаВидОперацииПриемкаТоваровНаХранение КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА ТоварыКПоступлению.КОформлениюПоступленийПоОрдерамОстаток < 0
	|			ТОГДА &СтрокаТипОтклоненияОформлениеСкладскихДокументов
	|		ИНАЧЕ &СтрокаТипОтклоненияОформлениеФинансовыхДокументов
	|	КОНЕЦ КАК ТипОтклонения,
	|	ВЫБОР
	|		КОГДА ТоварыКПоступлению.КОформлениюПоступленийПоОрдерамОстаток < 0
	|			ТОГДА &СтрокаРекомендацияОформитеПриходныеОрдераНаТовары
	|		ИНАЧЕ &СтрокаРекомендацияОформитеПриемкиТоваровНаХранение
	|	КОНЕЦ КАК Рекомендация,
	|	ВЫБОР
	|		КОГДА ТоварыКПоступлению.КОформлениюПоступленийПоОрдерамОстаток < 0
	|			ТОГДА -ТоварыКПоступлению.КОформлениюПоступленийПоОрдерамОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НезавершеннаяСкладскаяОперация,
	|	ВЫБОР
	|		КОГДА ТоварыКПоступлению.КОформлениюПоступленийПоОрдерамОстаток > 0
	|			ТОГДА ТоварыКПоступлению.КОформлениюПоступленийПоОрдерамОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НезавершеннаяФинансоваяОперация,
	|	ЕСТЬNULL(РеквизитыРаспоряжения.ДатаДокументаИБ, ДАТАВРЕМЯ(1,1,1)) КАК ДатаОперации,
	|	ТоварыКПоступлению.ДокументПоступления КАК Распоряжение,
	|	ТоварыКПоступлению.Склад КАК Склад,
	|	ТоварыКПоступлению.Номенклатура КАК Номенклатура,
	|	ТоварыКПоступлению.Характеристика КАК Характеристика,
	|	ТоварыКПоступлению.Серия,
	|	ТоварыКПоступлению.Назначение
	|{ВЫБРАТЬ
	|	Склад.*,
	|	Номенклатура.*,
	|	Характеристика.*,
	|	Серия.*,
	|	Назначение.*}
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Остатки(
	|		&ПериодГраница,
	|		ДокументПоступления В (
	|			ВЫБРАТЬ
	|				РаспоряженияНаПоступление.Ссылка
	|			ИЗ
	|				РаспоряженияНаПоступлениеПоПриемкеНаХранение КАК РаспоряженияНаПоступление)
	|		И &ОтборТоваровПоРегистру
	|		{(ДокументПоступления).* КАК Распоряжение, (Склад).*, (Номенклатура).*, (Характеристика).*, (Серия).*, (Назначение).*}
	|	) КАК ТоварыКПоступлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеквизитыРаспоряжения
	|		ПО ТоварыКПоступлению.ДокументПоступления = РеквизитыРаспоряжения.Ссылка
	|			И НЕ РеквизитыРаспоряжения.ДополнительнаяЗапись
	|ГДЕ
	|	ТоварыКПоступлению.КОформлениюПоступленийПоОрдерамОстаток <> 0
	|{ГДЕ
	|	(&СтрокаВидОперацииПриемкаТоваровНаХранение) КАК ВидОперации,
	|	(ВЫБОР
	|			КОГДА ТоварыКПоступлению.КОформлениюПоступленийПоОрдерамОстаток < 0
	|				ТОГДА &СтрокаТипОтклоненияОформлениеСкладскихДокументов
	|			ИНАЧЕ &СтрокаТипОтклоненияОформлениеФинансовыхДокументов
	|		КОНЕЦ) КАК ТипОтклонения,
	|	(ВЫБОР
	|			КОГДА ТоварыКПоступлению.КОформлениюПоступленийПоОрдерамОстаток < 0
	|				ТОГДА &СтрокаРекомендацияОформитеПриходныеОрдераНаТовары
	|			ИНАЧЕ &СтрокаРекомендацияОформитеПриемкиТоваровНаХранение
	|		КОНЕЦ) КАК Рекомендация,
	|	(ВЫБОР
	|			КОГДА ТоварыКПоступлению.КОформлениюПоступленийПоОрдерамОстаток < 0
	|				ТОГДА -ТоварыКПоступлению.КОформлениюПоступленийПоОрдерамОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НезавершеннаяСкладскаяОперация,
	|	(ВЫБОР
	|			КОГДА ТоварыКПоступлению.КОформлениюПоступленийПоОрдерамОстаток > 0
	|				ТОГДА ТоварыКПоступлению.КОформлениюПоступленийПоОрдерамОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НезавершеннаяФинансоваяОперация}";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаВременныхТаблицПриемкиТоваровНаХранение()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументПоступления.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ РаспоряженияНаПоступлениеПоПриемкеНаХранение
	|ИЗ
	|	Справочник.СоглашенияСПоставщиками КАК ДокументПоступления
	|ГДЕ
	|	НЕ ДокументПоступления.ПометкаУдаления
	|	И ДокументПоступления.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументПоступления.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗаказПоставщику КАК ДокументПоступления
	|ГДЕ
	|	НЕ ДокументПоступления.ПометкаУдаления
	|	И ДокументПоступления.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументПоступления.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПриемкаТоваровНаХранение КАК ДокументПоступления
	|ГДЕ
	|	НЕ ДокументПоступления.ПометкаУдаления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции



Функция ТекстЗапросаКонтрольОформленияДокументовТовародвижений(ИспользуетсяОтборТоваров)
	КоличествоПодключенныхЗапросов = 0;
	ТекстЗапроса = "";
	
	Доступные = ДоступныеВидыОперацийИЗапросы();
	ТекстыЗапроса = Новый Структура; 
	Для Каждого Доступный Из Доступные Цикл 
		ТекстыЗапроса.Вставить(Доступный.ИмяТекстаЗапроса, Истина);
	КонецЦикла;
	
	// Проверим права чтения документов
	ПравоЧтениеЗаказКлиента						= ПравоДоступа("Чтение", Метаданные.Документы.ЗаказКлиента);
	ПравоЧтениеРеализацияТоваровУслуг			= ПравоДоступа("Чтение", Метаданные.Документы.РеализацияТоваровУслуг);
	ПравоЧтениеЗаявкаНаВозвратТоваровОтКлиента	= ПравоДоступа("Чтение", Метаданные.Документы.ЗаявкаНаВозвратТоваровОтКлиента);
	ПравоЧтениеВозвратТоваровОтКлиента			= ПравоДоступа("Чтение", Метаданные.Документы.ВозвратТоваровОтКлиента);
	
	ПравоЧтениеЗаказПоставщику					= ПравоДоступа("Чтение", Метаданные.Документы.ЗаказПоставщику);
	ПравоЧтениеПриобретениеТоваровУслуг			= ПравоДоступа("Чтение", Метаданные.Документы.ПриобретениеТоваровУслуг);
	ПравоЧтениеСоглашенияСПоставщиками			= ПравоДоступа("Чтение", Метаданные.Справочники.СоглашенияСПоставщиками);
	ПравоЧтениеВозвратТоваровПоставщику			= ПравоДоступа("Чтение", Метаданные.Документы.ВозвратТоваровПоставщику);
	
	ПравоЧтениеЗаказНаВнутреннееПотребление		= ПравоДоступа("Чтение", Метаданные.Документы.ЗаказНаВнутреннееПотребление);
	ПравоЧтениеВнутреннееПотребление			= ПравоДоступа("Чтение", Метаданные.Документы.ВнутреннееПотребление);
	
	ПравоЧтениеЗаказНаПеремещение				= ПравоДоступа("Чтение", Метаданные.Документы.ЗаказНаПеремещение);
	ПравоЧтениеПеремещениеТоваров				= ПравоДоступа("Чтение", Метаданные.Документы.ПеремещениеТоваров);
	
	ПравоЧтениеЗаказНаСборку					= ПравоДоступа("Чтение", Метаданные.Документы.ЗаказНаСборку);
	ПравоЧтениеСборкаТоваров					= ПравоДоступа("Чтение", Метаданные.Документы.СборкаТоваров);
	
	ПравоЧтениеПрочееОприходованиеТоваров		= ПравоДоступа("Чтение", Метаданные.Документы.ПрочееОприходованиеТоваров);
	
	ПравоЧтениеОтгрузкаТоваровСХранения			= ПравоДоступа("Чтение", Метаданные.Документы.ОтгрузкаТоваровСХранения);
	ПравоЧтениеПриемкаТоваровНаХранение			= ПравоДоступа("Чтение", Метаданные.Документы.ПриемкаТоваровНаХранение);
	
	ПравоЧтениеПередачаТоваровХранителю			= ПравоДоступа("Чтение", Метаданные.Документы.ПередачаТоваровХранителю);
	ПравоЧтениеПоступлениеТоваровОтХранителя	= ПравоДоступа("Чтение", Метаданные.Документы.ПоступлениеТоваровОтХранителя);
	
	
	// Проверим права чтения регистров
	ПравоЧтениеТоварыКОтгрузке					= ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыКОтгрузке);
	ПравоЧтениеТоварыКОформлениюПоступления		= ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыКПоступлению);
	ПравоЧтениеТоварыКОформлениюИзлишковНедостач= ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыКОформлениюИзлишковНедостач);
	
	// Продажи
	Если ПравоЧтениеТоварыКОтгрузке
		И ПравоЧтениеЗаказКлиента
		И ПравоЧтениеРеализацияТоваровУслуг 
		И ПравоЧтениеЗаявкаНаВозвратТоваровОтКлиента
		И ТекстыЗапроса.Свойство("ТекстЗапросаПродажи") Тогда
		ПодключитьЗапрос(ТекстЗапроса, ТекстЗапросаПродажи(), КоличествоПодключенныхЗапросов,
			ТекстЗапросаВременныхТаблицПродажи());
	КонецЕсли;
	Если ПравоЧтениеТоварыКОформлениюПоступления
		И ПравоЧтениеЗаявкаНаВозвратТоваровОтКлиента
		И ПравоЧтениеВозвратТоваровОтКлиента
		И ТекстыЗапроса.Свойство("ТекстЗапросаВозвратыТоваровОтКлиентов") Тогда
		ПодключитьЗапрос(ТекстЗапроса, ТекстЗапросаВозвратыТоваровОтКлиентов(), КоличествоПодключенныхЗапросов);
	КонецЕсли;
	
	// Закупки
	Если ПравоЧтениеТоварыКОформлениюПоступления
		И ПравоЧтениеЗаказПоставщику
		И ПравоЧтениеПриобретениеТоваровУслуг 
		И ПравоЧтениеСоглашенияСПоставщиками
		И ТекстыЗапроса.Свойство("ТекстЗапросаЗакупки") Тогда
		ПодключитьЗапрос(ТекстЗапроса, ТекстЗапросаЗакупки(), КоличествоПодключенныхЗапросов,
			ТекстЗапросаВременныхТаблицЗакупки());
	КонецЕсли;
	Если ПравоЧтениеТоварыКОтгрузке
		И ПравоЧтениеВозвратТоваровПоставщику
		И ТекстыЗапроса.Свойство("ТекстЗапросаВозвратыТоваровПоставщикам") Тогда
		ПодключитьЗапрос(ТекстЗапроса, ТекстЗапросаВозвратыТоваровПоставщикам(), КоличествоПодключенныхЗапросов);
	КонецЕсли;
	
	// Внутреннее потребление
	Если ПравоЧтениеТоварыКОтгрузке
		И ПравоЧтениеЗаказНаВнутреннееПотребление
		И ПравоЧтениеВнутреннееПотребление
		И ТекстыЗапроса.Свойство("ТекстЗапросаВнутреннееПотребление") Тогда
		ПодключитьЗапрос(ТекстЗапроса, ТекстЗапросаВнутреннееПотребление(), КоличествоПодключенныхЗапросов);
	КонецЕсли;
	
	// Перемещение товаров
	Если ПравоЧтениеТоварыКОтгрузке
		И ПравоЧтениеТоварыКОформлениюПоступления
		И ПравоЧтениеЗаказНаПеремещение
		И ПравоЧтениеПеремещениеТоваров
		И ТекстыЗапроса.Свойство("ТекстЗапросаПеремещениеТоваров") Тогда
		ПодключитьЗапрос(ТекстЗапроса, ТекстЗапросаПеремещениеТоваров(), КоличествоПодключенныхЗапросов);
	КонецЕсли;
	
	// Сборка (разборка)
	Если ПравоЧтениеТоварыКОтгрузке
		И ПравоЧтениеТоварыКОформлениюПоступления
		И ПравоЧтениеЗаказНаСборку
		И ПравоЧтениеСборкаТоваров
		И ТекстыЗапроса.Свойство("ТекстЗапросаСборкаРазборка") Тогда
		ПодключитьЗапрос(ТекстЗапроса, ТекстЗапросаСборкаРазборка(), КоличествоПодключенныхЗапросов);
	КонецЕсли;
	
	// Прочее оприходование
	Если ПравоЧтениеТоварыКОформлениюПоступления
		И ПравоЧтениеПрочееОприходованиеТоваров
		И ТекстыЗапроса.Свойство("ТекстЗапросаПрочиеОприходования") Тогда
		ПодключитьЗапрос(ТекстЗапроса, ТекстЗапросаПрочиеОприходования(), КоличествоПодключенныхЗапросов);
	КонецЕсли;
	
	// Оформление излишков и недостач
	Если ПравоЧтениеТоварыКОформлениюИзлишковНедостач
		И ТекстыЗапроса.Свойство("ТекстЗапросаОформлениеИзлишковНедостач") Тогда
		ПодключитьЗапрос(ТекстЗапроса, ТекстЗапросаОформлениеИзлишковНедостач(), КоличествоПодключенныхЗапросов);
	КонецЕсли;
	
	// Отгрузка товаров с хранения
	Если ПравоЧтениеТоварыКОтгрузке
		И ПравоЧтениеОтгрузкаТоваровСХранения
		И ТекстыЗапроса.Свойство("ТекстЗапросаОтгрузкаПринятыхТоваровСХранения") Тогда
		ПодключитьЗапрос(ТекстЗапроса, ТекстЗапросаОтгрузкаПринятыхТоваровСХранения(), КоличествоПодключенныхЗапросов);
	КонецЕсли;
	
	// Приемка товаров на хранение
	Если ПравоЧтениеТоварыКОформлениюПоступления
		И ПравоЧтениеЗаказПоставщику
		И ПравоЧтениеСоглашенияСПоставщиками
		И ПравоЧтениеПриемкаТоваровНаХранение
		И ТекстыЗапроса.Свойство("ТекстЗапросаПриемкаТоваровНаХранение") Тогда
		ПодключитьЗапрос(ТекстЗапроса, ТекстЗапросаПриемкаТоваровНаХранение(), КоличествоПодключенныхЗапросов,
			ТекстЗапросаВременныхТаблицПриемкиТоваровНаХранение());
	КонецЕсли;
	
	// Передача товаров хранителю
	Если ПравоЧтениеТоварыКОтгрузке
		И ПравоЧтениеЗаказКлиента
		И ПравоЧтениеПередачаТоваровХранителю
		И ПравоЧтениеЗаявкаНаВозвратТоваровОтКлиента
		И ТекстыЗапроса.Свойство("ТекстЗапросаПередачаТоваровХранителю") Тогда
		ПодключитьЗапрос(ТекстЗапроса, ТекстЗапросаПередачаТоваровХранителю(), КоличествоПодключенныхЗапросов,
			ТекстЗапросаВременныхТаблицПередачиТоваровХранителю());
	КонецЕсли;
	
	
	
	ЗаменитьПараметрыОтбораТоваров(ТекстЗапроса, ИспользуетсяОтборТоваров);
	
	Возврат ТекстЗапроса;
КонецФункции

Процедура ЗаменитьПараметрыОтбораТоваров(ТекстЗапроса, ИспользуетсяОтборТоваров)
	
	ТекстЗапросаОтборТоваровПоРегистру = "ИСТИНА";
	ТекстЗапросаОтборТоваровПоПеремещению = "ИСТИНА";
	ТекстЗапросаОтборТоваровПоРазборке = "ИСТИНА";
	ТекстЗапросаОтборТоваровПоСборке = "ИСТИНА";
	
	Если ИспользуетсяОтборТоваров Тогда
		ТекстЗапросаОтборТоваровПоРегистру =
		"(Склад, Номенклатура, Характеристика, Назначение, Серия) В
		|					(ВЫБРАТЬ
		|						ТаблицаОтборов.Склад			КАК Склад,
		|						ТаблицаОтборов.Номенклатура		КАК Номенклатура,
		|						ТаблицаОтборов.Характеристика	КАК Характеристика,
		|						ТаблицаОтборов.Назначение		КАК Назначение,
		|						ТаблицаОтборов.Серия			КАК Серия
		|					ИЗ
		|						ТаблицаОтборов КАК ТаблицаОтборов)";
		ТекстЗапросаОтборТоваровПоПеремещению =
		"(ПеремещениеТоваров.Ссылка.СкладПолучатель, ПеремещениеТоваров.Номенклатура, ПеремещениеТоваров.Характеристика, ПеремещениеТоваров.Назначение, ПеремещениеТоваров.Серия) В
		|					(ВЫБРАТЬ
		|						ТаблицаОтборов.Склад			КАК Склад,
		|						ТаблицаОтборов.Номенклатура		КАК Номенклатура,
		|						ТаблицаОтборов.Характеристика	КАК Характеристика,
		|						ТаблицаОтборов.Назначение		КАК Назначение,
		|						ТаблицаОтборов.Серия			КАК Серия
		|					ИЗ
		|						ТаблицаОтборов КАК ТаблицаОтборов)";
		ТекстЗапросаОтборТоваровПоРазборке =
		"(СборкаРазборкаТоваров.Ссылка.Склад, СборкаРазборкаТоваров.Номенклатура, СборкаРазборкаТоваров.Характеристика, СборкаРазборкаТоваров.Назначение, СборкаРазборкаТоваров.Серия) В
		|					(ВЫБРАТЬ
		|						ТаблицаОтборов.Склад			КАК Склад,
		|						ТаблицаОтборов.Номенклатура		КАК Номенклатура,
		|						ТаблицаОтборов.Характеристика	КАК Характеристика,
		|						ТаблицаОтборов.Назначение		КАК Назначение,
		|						ТаблицаОтборов.Серия			КАК Серия
		|					ИЗ
		|						ТаблицаОтборов КАК ТаблицаОтборов)";
		ТекстЗапросаОтборТоваровПоСборке =
		"(СборкаРазборкаТоваров.Склад, СборкаРазборкаТоваров.Номенклатура, СборкаРазборкаТоваров.Характеристика, СборкаРазборкаТоваров.Назначение, СборкаРазборкаТоваров.Серия) В
		|					(ВЫБРАТЬ
		|						ТаблицаОтборов.Склад			КАК Склад,
		|						ТаблицаОтборов.Номенклатура		КАК Номенклатура,
		|						ТаблицаОтборов.Характеристика	КАК Характеристика,
		|						ТаблицаОтборов.Назначение		КАК Назначение,
		|						ТаблицаОтборов.Серия			КАК Серия
		|					ИЗ
		|						ТаблицаОтборов КАК ТаблицаОтборов)";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборТоваровПоРегистру", ТекстЗапросаОтборТоваровПоРегистру);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборТоваровПоПеремещению", ТекстЗапросаОтборТоваровПоПеремещению);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборТоваровПоРазборке", ТекстЗапросаОтборТоваровПоРазборке);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборТоваровПоСборке", ТекстЗапросаОтборТоваровПоСборке);
	
КонецПроцедуры

Процедура ПодключитьЗапрос(ДинамическийТекстЗапроса, ПодключаемыйЗапрос, КоличествоПодключенныхЗапросов,
	ТекстЗапросаВременнойТаблицы = Неопределено)
	
	ТекстЗапросаВременнойТаблицы = ?(ЗначениеЗаполнено(ТекстЗапросаВременнойТаблицы),
		ТекстЗапросаВременнойТаблицы + ОбщегоНазначения.РазделительПакетаЗапросов(), "");
	
	Если КоличествоПодключенныхЗапросов = 0 Тогда
		ДинамическийТекстЗапроса = ДинамическийТекстЗапроса + ТекстЗапросаВременнойТаблицы + ПодключаемыйЗапрос;
	Иначе
		ДинамическийТекстЗапроса = ДинамическийТекстЗапроса + Символы.ПС + Символы.ПС 
			+ "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС + Символы.ПС;
			
		СтрокаПолейКомпоновки = "{ВЫБРАТЬ
			|	Склад.*,
			|	Номенклатура.*,
			|	Характеристика.*,
			|	Серия.*,
			|	Назначение.*}";
		
		ПодключаемыйЗапросПодготовка = СтрЗаменить(ПодключаемыйЗапрос, "РАЗРЕШЕННЫЕ", "");
		ПодключаемыйЗапросПодготовка = СтрЗаменить(ПодключаемыйЗапросПодготовка, СтрокаПолейКомпоновки, "");
		
		ДинамическийТекстЗапроса = ТекстЗапросаВременнойТаблицы + ДинамическийТекстЗапроса + ПодключаемыйЗапросПодготовка;
	КонецЕсли;
	
	КоличествоПодключенныхЗапросов = КоличествоПодключенныхЗапросов + 1;
	
КонецПроцедуры

Функция ТекстЗапросаОформлениеИзлишковНедостачБезОтборов(ТекстЗапроса)
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	ИндексОператора = Неопределено;
	Для Каждого ОператорСхемыЗапроса Из СхемаЗапроса.ПакетЗапросов[0].Операторы Цикл
		Если Строка(ОператорСхемыЗапроса.ВыбираемыеПоля[0]) = "&СтрокаВидОперацииОформлениеИзлишковИНедостач" Тогда
			ИндексОператора = СхемаЗапроса.ПакетЗапросов[0].Операторы.Индекс(ОператорСхемыЗапроса);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ИндексОператора <> Неопределено Тогда
		ОтборыОператора = СхемаЗапроса.ПакетЗапросов[0].Операторы[ИндексОператора].Отбор; // ВыраженияСхемыЗапроса
		ИндексыУдаляемыхОператоров = Новый Массив;
		
		Для Каждого ОтборОператора Из ОтборыОператора Цикл 
			ОтборОператораСтрока = Строка(ОтборОператора);
			Если СтрНайти(ОтборОператораСтрока, "ДАТАВРЕМЯ(1, 1, 1)") > 0 
				Или СтрНайти(ОтборОператораСтрока, "NULL") > 0 Тогда
				ИндексыУдаляемыхОператоров.Добавить(ОтборыОператора.Индекс(ОтборОператора));
			КонецЕсли;
		КонецЦикла;
		
		КорректировкаИндекса = 0;
		Для Каждого ИндексУдаляемогоОператора Из ИндексыУдаляемыхОператоров Цикл 
			ОтборыОператора.Удалить(ИндексУдаляемогоОператора + КорректировкаИндекса);
			КорректировкаИндекса = КорректировкаИндекса - 1; 
		КонецЦикла;
	КонецЕсли;
	
	Возврат СхемаЗапроса.ПолучитьТекстЗапроса();
КонецФункции

Функция ДоступныеВидыОперацийИЗапросы()
	
	Доступные = Новый ТаблицаЗначений;
	Доступные.Колонки.Добавить("ВидОперации", ОбщегоНазначения.ОписаниеТипаСтрока(200));
	Доступные.Колонки.Добавить("ВидОперацииСиноним", ОбщегоНазначения.ОписаниеТипаСтрока(200));
	Доступные.Колонки.Добавить("ИмяТекстаЗапроса", ОбщегоНазначения.ОписаниеТипаСтрока(200));
	
	ВидыОперацийИСинонимы = ВидыОперацийИСинонимы();
	
	// Не отключаются комбинацией функциональных опций
	ДобавитьВидОперацииИЗапрос(Доступные, ВидыОперацийИСинонимы, "ОтгрузкаКлиентам", "ТекстЗапросаПродажи");
	ДобавитьВидОперацииИЗапрос(Доступные, ВидыОперацийИСинонимы, "ВозвратыТоваровОтКлиентов", "ТекстЗапросаВозвратыТоваровОтКлиентов");
	ДобавитьВидОперацииИЗапрос(Доступные, ВидыОперацийИСинонимы, "ПриемкаОтПоставщиков", "ТекстЗапросаЗакупки");
	ДобавитьВидОперацииИЗапрос(Доступные, ВидыОперацийИСинонимы, "ВозвратыТоваровПоставщикам", "ТекстЗапросаВозвратыТоваровПоставщикам");
	ДобавитьВидОперацииИЗапрос(Доступные, ВидыОперацийИСинонимы, "ОформлениеИзлишковИНедостач", "ТекстЗапросаОформлениеИзлишковНедостач");
	
	// Отключаются комбинацией функциональных опций
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыНаВнутреннееПотребление")
		Или ПолучитьФункциональнуюОпцию("ИспользоватьВнутреннееПотребление") Тогда
		ДобавитьВидОперацииИЗапрос(Доступные, ВидыОперацийИСинонимы, "ВнутреннееПотребление", "ТекстЗапросаВнутреннееПотребление");
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыНаПеремещение")
		Или ПолучитьФункциональнуюОпцию("ИспользоватьПеремещениеТоваров") Тогда
		ДобавитьВидОперацииИЗапрос(Доступные, ВидыОперацийИСинонимы, "ПеремещениеТоваров", "ТекстЗапросаПеремещениеТоваров");
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыНаСборку")
		Или ПолучитьФункциональнуюОпцию("ИспользоватьСборкуРазборку") Тогда
		ДобавитьВидОперацииИЗапрос(Доступные, ВидыОперацийИСинонимы, "СборкаРазборкаТоваров", "ТекстЗапросаСборкаРазборка");
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПрочееОприходованиеТоваров") Тогда
		ДобавитьВидОперацииИЗапрос(Доступные, ВидыОперацийИСинонимы, "ПрочееОприходованиеТоваров", "ТекстЗапросаПрочиеОприходования");
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОтветственноеХранениеВПроцессеЗакупки") Тогда
		ДобавитьВидОперацииИЗапрос(Доступные, ВидыОперацийИСинонимы, "ОтгрузкаПринятыхТоваровСХранения", "ТекстЗапросаОтгрузкаПринятыхТоваровСХранения");
		ДобавитьВидОперацииИЗапрос(Доступные, ВидыОперацийИСинонимы, "ПриемкаТоваровНаХранение", "ТекстЗапросаПриемкаТоваровНаХранение");
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПередачуНаОтветственноеХранениеСПравомПродажи") Тогда
		ДобавитьВидОперацииИЗапрос(Доступные, ВидыОперацийИСинонимы, "ПередачаТоваровХранителю", "ТекстЗапросаПередачаТоваровХранителю");
	КонецЕсли;

	
	Возврат Доступные;
	
КонецФункции

Функция ДоступныеЗначенияПоляВидОперации()
	
	ДоступныеВидыОперацийИЗапросы = ДоступныеВидыОперацийИЗапросы();
	ДоступныеЗначения = Новый СписокЗначений;
	
	ДоступныеЗначения.ЗагрузитьЗначения(ДоступныеВидыОперацийИЗапросы.ВыгрузитьКолонку("ВидОперацииСиноним"));
	Для Каждого ДоступноеЗначение Из ДоступныеЗначения Цикл 
		ДоступноеЗначение.Представление = ДоступноеЗначение.Значение;
	КонецЦикла;
	
	Возврат ДоступныеЗначения;
КонецФункции

Процедура ДобавитьВидОперацииИЗапрос(Таблица, ВидыОперацийИСинонимы, ВидОперации, ИмяТекстаЗапроса)
	
	НоваяСтрока = Таблица.Добавить();
	НоваяСтрока.ВидОперации = ВидОперации;
	НоваяСтрока.ВидОперацииСиноним = ВидыОперацийИСинонимы[ВидОперации];
	НоваяСтрока.ИмяТекстаЗапроса = ИмяТекстаЗапроса;
	
КонецПроцедуры

Функция ВидыОперацийИСинонимы()
	
	ВидыОперацийИСинонимы = Новый Соответствие;
	
	ВидыОперацийИСинонимы.Вставить("ОтгрузкаКлиентам", НСтр("ru='Отгрузка клиентам'"));
	ВидыОперацийИСинонимы.Вставить("ВозвратыТоваровОтКлиентов", НСтр("ru='Возвраты товаров от клиентов'"));
	ВидыОперацийИСинонимы.Вставить("ПриемкаОтПоставщиков", НСтр("ru='Приемка от поставщиков'"));
	ВидыОперацийИСинонимы.Вставить("ВозвратыТоваровПоставщикам", НСтр("ru='Возвраты товаров поставщикам'"));
	ВидыОперацийИСинонимы.Вставить("ОформлениеИзлишковИНедостач", НСтр("ru='Оформление излишков и недостач'"));
	ВидыОперацийИСинонимы.Вставить("ВнутреннееПотребление", НСтр("ru='Внутреннее потребление'"));
	ВидыОперацийИСинонимы.Вставить("ПеремещениеТоваров", НСтр("ru='Перемещение товаров'"));
	ВидыОперацийИСинонимы.Вставить("СборкаРазборкаТоваров", НСтр("ru='Сборка (разборка) товаров'"));
	ВидыОперацийИСинонимы.Вставить("ПрочееОприходованиеТоваров", НСтр("ru='Прочее оприходование товаров'"));
	ВидыОперацийИСинонимы.Вставить("ПередачаМатериаловВПроизводство", НСтр("ru='Передача материалов в производство'"));
	ВидыОперацийИСинонимы.Вставить("ВозвратыМатериаловИзПроизводства", НСтр("ru='Возвраты материалов из производства'"));
	ВидыОперацийИСинонимы.Вставить("ПриемкаГотовойПродукции", НСтр("ru='Приемка готовой продукции'"));
	ВидыОперацийИСинонимы.Вставить("ПереработкаСырьяУПереработчика", НСтр("ru='Переработка сырья у переработчика'"));
	ВидыОперацийИСинонимы.Вставить("ВозвратыСырьяОтПереработчиков", НСтр("ru='Возвраты сырья от переработчиков'"));
	ВидыОперацийИСинонимы.Вставить("ПоступленияПродукцииОтПереработчиков", НСтр("ru='Поступления продукции от переработчиков'"));
	ВидыОперацийИСинонимы.Вставить("ПереработкаСырьяДавальца", НСтр("ru='Переработка сырья давальца'"));
	ВидыОперацийИСинонимы.Вставить("ВозвратыСырьяДавальцам", НСтр("ru='Возвраты сырья давальцам'"));
	ВидыОперацийИСинонимы.Вставить("ПередачиПродукцииДавальцам", НСтр("ru='Передачи продукции давальцам'"));
	ВидыОперацийИСинонимы.Вставить("ОтгрузкаПринятыхТоваровСХранения", НСтр("ru='Отгрузка принятых товаров с хранения'"));
	ВидыОперацийИСинонимы.Вставить("ПриемкаТоваровНаХранение", НСтр("ru='Приемка товаров на хранение'"));
	ВидыОперацийИСинонимы.Вставить("ПередачаТоваровХранителю", НСтр("ru='Передача товаров хранителю'"));
	
	Возврат ВидыОперацийИСинонимы;
	
КонецФункции

Функция ТекстЗапросаВременныхТаблицПродажи()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументПродажи.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ РаспоряженияНаОтгрузкуПоПродажам
	|ИЗ
	|	Документ.ЗаказКлиента КАК ДокументПродажи
	|ГДЕ
	|	НЕ ДокументПродажи.ПометкаУдаления
	|	И ДокументПродажи.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаХранениеСПравомПродажи)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументПродажи.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗаявкаНаВозвратТоваровОтКлиента КАК ДокументПродажи
	|ГДЕ
	|	НЕ ДокументПродажи.ПометкаУдаления
	|	И ДокументПродажи.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтХранителя)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументПродажи.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДокументПродажи
	|ГДЕ
	|	НЕ ДокументПродажи.ПометкаУдаления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВременныхТаблицПередачиТоваровХранителю()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументПродажи.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ РаспоряженияНаОтгрузкуПоПередачам
	|ИЗ
	|	Документ.ЗаказКлиента КАК ДокументПродажи
	|ГДЕ
	|	НЕ ДокументПродажи.ПометкаУдаления
	|	И ДокументПродажи.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаХранениеСПравомПродажи)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументПродажи.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗаявкаНаВозвратТоваровОтКлиента КАК ДокументПродажи
	|ГДЕ
	|	НЕ ДокументПродажи.ПометкаУдаления
	|	И ДокументПродажи.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтХранителя)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументПродажи.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПередачаТоваровХранителю КАК ДокументПродажи
	|ГДЕ
	|	НЕ ДокументПродажи.ПометкаУдаления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПередачаТоваровХранителю()
	// Строковые литералы локализуются в настройках СКД отчета
	// Наборы данных - Поля - Доступные значения
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&СтрокаВидОперацииПередачаТоваровХранителю КАК ВидОперации,
	|	&СтрокаТипОтклоненияОформлениеСкладскихДокументов КАК ТипОтклонения,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|			ТОГДА &СтрокаРекомендацияЗавершитеОформлениеРасходныхОрдеровНаТовары
	|	КОНЕЦ КАК Рекомендация,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|			ТОГДА -ТоварыКОтгрузке.КОтгрузкеОстаток
	|	КОНЕЦ КАК НезавершеннаяСкладскаяОперация,
	|	0 КАК НезавершеннаяФинансоваяОперация,
	|	ЕСТЬNULL(РеквизитыРаспоряжения.ДатаДокументаИБ, ДАТАВРЕМЯ(1,1,1)) КАК ДатаОперации,
	|	ТоварыКОтгрузке.ДокументОтгрузки КАК Распоряжение,
	|	ТоварыКОтгрузке.Склад КАК Склад,
	|	ТоварыКОтгрузке.Номенклатура КАК Номенклатура,
	|	ТоварыКОтгрузке.Характеристика КАК Характеристика,
	|	ТоварыКОтгрузке.Серия,
	|	ТоварыКОтгрузке.Назначение
	|{ВЫБРАТЬ
	|	Склад.*,
	|	Номенклатура.*,
	|	Характеристика.*,
	|	Серия.*,
	|	Назначение.*}
	|ИЗ
	|	РегистрНакопления.ТоварыКОтгрузке.Остатки(
	|		&ПериодГраница, 
	|		ДокументОтгрузки В(
	|			ВЫБРАТЬ
	|				РаспоряженияНаОтгрузку.Ссылка
	|			ИЗ
	|				РаспоряженияНаОтгрузкуПоПередачам КАК РаспоряженияНаОтгрузку)
	|		И &ОтборТоваровПоРегистру
	|		{(ДокументОтгрузки).* КАК Распоряжение, (Склад).*, (Номенклатура).*, (Характеристика).*, (Серия).*, (Назначение).*}
	|	) КАК ТоварыКОтгрузке
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеквизитыРаспоряжения
	|		ПО ТоварыКОтгрузке.ДокументОтгрузки = РеквизитыРаспоряжения.Ссылка
	|			И НЕ РеквизитыРаспоряжения.ДополнительнаяЗапись
	|ГДЕ
	|	ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|{ГДЕ
	|	(&СтрокаВидОперацииПередачаТоваровХранителю) КАК ВидОперации,
	|	(&СтрокаТипОтклоненияОформлениеСкладскихДокументов) КАК ТипОтклонения,
	|	(ВЫБОР
	|			КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|				ТОГДА &СтрокаРекомендацияЗавершитеОформлениеРасходныхОрдеровНаТовары
	|		КОНЕЦ) КАК Рекомендация,
	|	(ВЫБОР
	|			КОГДА ТоварыКОтгрузке.КОтгрузкеОстаток - ТоварыКОтгрузке.КОформлениюОстаток <> 0
	|				ТОГДА -ТоварыКОтгрузке.КОтгрузкеОстаток
	|		КОНЕЦ) КАК НезавершеннаяСкладскаяОперация}";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция СкомпоноватьРезультатВКоллекциюЗначений()
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	АдресТаблицыОтборов = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "АдресТаблицыОтборов").Значение;
	ИнициализироватьМенеджерВременныхТаблицДаннымиОтбора(МенеджерВременныхТаблиц, АдресТаблицыОтборов);
	ТаблицаЗначенийРезультат = Новый ТаблицаЗначений;
	
	УстановитьОбязательныеНастройки(Ложь);
	СхемаКомпоновкиДанных.НаборыДанных.КонтрольОформленияДокументов.Запрос =
		ТекстЗапросаКонтрольОформленияДокументовТовародвижений(ЗначениеЗаполнено(АдресТаблицыОтборов));
	
	// Сформируем отчет
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных,
													НастройкиОтчета,
													,
													,
													Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	// Удалим отборы по пустым полям из запроса по излишкам / недостачам
	ИсходныйЗапросМакета = МакетКомпоновки.НаборыДанных.КонтрольОформленияДокументов.Запрос;
	
	МакетКомпоновки.НаборыДанных.КонтрольОформленияДокументов.Запрос = 
		ТекстЗапросаОформлениеИзлишковНедостачБезОтборов(ИсходныйЗапросМакета);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , , Истина, , МенеджерВременныхТаблиц);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ТаблицаЗначенийРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	Возврат ТаблицаЗначенийРезультат;

КонецФункции

#КонецОбласти

#КонецЕсли