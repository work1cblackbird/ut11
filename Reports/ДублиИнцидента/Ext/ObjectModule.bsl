#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПроверяемыйИнцидент = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ПроверяемыйИнцидент").Значение;
	
	ПройденныеИнциденты = Новый Массив;
	ДублиИнцидента = Справочники.Инциденты.ДублиИнцидента(ПроверяемыйИнцидент, ПройденныеИнциденты);
	
	ТаблицаИнцидентов = Новый ТаблицаЗначений;
	ТаблицаИнцидентов.Колонки.Добавить("Дубли", Новый ОписаниеТипов("СправочникСсылка.Инциденты"));
	Для Инд = 1 По ДублиИнцидента.Количество() Цикл
		ТаблицаИнцидентов.Добавить();
	КонецЦикла;
	ТаблицаИнцидентов.ЗагрузитьКолонку(ДублиИнцидента, "Дубли");
	
	ВнешниеНаборы = Новый Структура("ТаблицаИнцидентов", ТаблицаИнцидентов);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.Настройки, ДанныеРасшифровки);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборы, ДанныеРасшифровки, Истина);
	
	ПроцессорВыводаВТабличныйДокумент = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВыводаВТабличныйДокумент.УстановитьДокумент(ДокументРезультат);
	ПроцессорВыводаВТабличныйДокумент.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
