#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокИменТабличныхЧастейДокументов = Новый СписокЗначений;
	СписокИменТабличныхЧастейДокументов.Добавить("ЗаказКлиента.Товары",            НСтр("ru = 'В заказах'"));
	СписокИменТабличныхЧастейДокументов.Добавить("АктВыполненныхРабот.Услуги",     НСтр("ru = 'В актах выполненных работ'"));
	СписокИменТабличныхЧастейДокументов.Добавить("РеализацияТоваровУслуг.Товары",  НСтр("ru = 'В реализациях'"));
	СписокИменТабличныхЧастейДокументов.Добавить("ВозвратТоваровОтКлиента.Товары", НСтр("ru = 'В возвратах'"));
	
	CRMЛокализация.ДополнитьСписокДокументовФормирующихОтчетСоставПродажи(СписокИменТабличныхЧастейДокументов);
	
	ТекстЗапроса = ТекстЗапросаПоСпискуИменТабличныхЧастей(СписокИменТабличныхЧастейДокументов);
	
	НастройкиОтчета   = КомпоновщикНастроек.ПолучитьНастройки();
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	СхемаКомпоновкиДанных.НаборыДанных.СоставПродажи.Запрос = ТекстЗапроса;
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , , Истина);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - См. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.События.ПриСозданииНаСервере = Истина;
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   ЭтаФорма             - ФормаКлиентскогоПриложения - Форма отчета.
//   Отказ                - Булево - передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Булево - передается из параметров обработчика "как есть".
//
// См. также:
//   "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	КомпоновщикНастроекФормы = ЭтаФорма.Отчет.КомпоновщикНастроек;
	Параметры = ЭтаФорма.Параметры;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		ЭтаФорма.ФормаПараметры.Отбор.Вставить("Сделка", Параметры.ПараметрКоманды);
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаПоСпискуИменТабличныхЧастей(СписокИменТабличныхЧастейДокументов)
	
	ТекстЗапроса = "";
	
	ЭтоПервыйЗапросОбъединения = Истина;
	
	Для Каждого ЭлементСписка Из СписокИменТабличныхЧастейДокументов Цикл
		
		Подстроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ЭлементСписка.Значение, "."); 
		Если Подстроки.Количество() = 2 Тогда
			ИмяДокумента = Подстроки[0];
		Иначе
			Продолжить;
		КонецЕсли;
		
		Если Не ПравоДоступа("Чтение", Метаданные.Документы[ИмяДокумента]) Тогда
			Продолжить;
		КонецЕсли;
			
		ТекстЗапросаПоДокументу = "";
		ИмяТаблицы             = "Документ." + ЭлементСписка.Значение;
		
		ТекстЗапросаПоДокументу = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|&СтроковоеПредставление              КАК ТипДокументов,
		|НоменклатураДокумента.Ссылка         КАК Ссылка,
		|НоменклатураДокумента.Номенклатура   КАК Номенклатура,
		|НоменклатураДокумента.Характеристика КАК Характеристика,
		|НоменклатураДокумента.Количество     КАК Количество
		|ИЗ
		|	&ИмяТаблицы КАК НоменклатураДокумента
		|ГДЕ 
		|	
		|	НоменклатураДокумента.Ссылка.Проведен = ИСТИНА
		|{ГДЕ
		|	НоменклатураДокумента.Ссылка.Сделка.* КАК Сделка}";
		
		ТекстЗапросаПоДокументу = СтрЗаменить(ТекстЗапросаПоДокументу, "&ИмяТаблицы",            ИмяТаблицы);
		ТекстЗапросаПоДокументу = СтрЗаменить(ТекстЗапросаПоДокументу, "&СтроковоеПредставление", """" + ЭлементСписка.Представление + """");
		
		Если НЕ ЭтоПервыйЗапросОбъединения Тогда
			
			ТекстЗапросаПоДокументу = СтрЗаменить(ТекстЗапросаПоДокументу, "РАЗРЕШЕННЫЕ", "");
			
			ТекстЗапросаПоДокументу = "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|" + ТекстЗапросаПоДокументу; 
			
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаПоДокументу;
		
		ЭтоПервыйЗапросОбъединения = Ложь;
		
	КонецЦикла;
	
	Если ТекстЗапроса = "" Тогда
		ВызватьИсключение НСтр("ru = 'Для текущего пользователя нет доступных документов по которым определяется состав продажи'");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецЕсли