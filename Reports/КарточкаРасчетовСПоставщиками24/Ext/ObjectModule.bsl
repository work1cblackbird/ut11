#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - См. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.События.ПриСозданииНаСервере = Истина;
	Настройки.События.ПередЗагрузкойВариантаНаСервере = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   ЭтаФорма - ФормаКлиентскогоПриложения - Форма отчета:
//   	* Параметры - Структура:
//   		** ОписаниеКоманды - Структура:
//   			*** ДополнительныеПараметры - Структура.
//   Отказ - Булево - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Булево - Передается из параметров обработчика "как есть".
//
// См. также:
//   "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	Параметры = ЭтаФорма.Параметры;
	КомпоновщикНастроекФормы = ЭтаФорма.Отчет.КомпоновщикНастроек;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		
		ЭтаФорма.НастройкиОтчета.РазрешеноВыбиратьВарианты = Ложь;
		
		СформироватьПараметрыОтчета(Параметры.ПараметрКоманды, ЭтаФорма, Параметры);
			
	КонецЕсли;
	
	ФормаПараметры = ЭтаФорма.ФормаПараметры;
	
КонецПроцедуры

// Вызывается в одноименном обработчике формы отчета после выполнения кода формы.
//
// Подробнее - см. ОтчетыПереопределяемый.ПередЗагрузкойВариантаНаСервере
//
Процедура ПередЗагрузкойВариантаНаСервере(ЭтаФорма, НовыеНастройкиКД) Экспорт
	Отчет = ЭтаФорма.Отчет;
	КомпоновщикНастроекФормы = Отчет.КомпоновщикНастроек;
	
	// Изменение настроек по функциональным опциям
	НастроитьПараметрыОтборыПоФункциональнымОпциям(КомпоновщикНастроекФормы);
	
	// Установка значений по умолчанию
	УстановитьОбязательныеНастройки(КомпоновщикНастроекФормы, Истина);
	
	НовыеНастройкиКД = КомпоновщикНастроекФормы.Настройки;
КонецПроцедуры

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПользовательскиеНастройкиМодифицированы = Ложь;
	
	УстановитьОбязательныеНастройки(КомпоновщикНастроек, ПользовательскиеНастройкиМодифицированы);
	
	// Сформируем отчет
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	СхемаКомпоновкиДанных.НаборыДанных.НаборДанных.Запрос = ТекстЗапроса();
	
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиОтчета, "ТекстНачальныйОстаток", НСтр("ru='Начальный остаток'"), Истина);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	//Предупреждение о невыполенном распределении взаиморасчетов.
	ВзаиморасчетыСервер.ВывестиПредупреждениеОбОбновлении(ДокументРезультат);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	// Сообщим форме отчета, что настройки модифицированы
	Если ПользовательскиеНастройкиМодифицированы Тогда
		КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПользовательскиеНастройкиМодифицированы", Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьОбязательныеНастройки(КомпоновщикНастроек, ПользовательскиеНастройкиМодифицированы)
	
	СегментыСервер.ВключитьОтборПоСегментуПартнеровВСКД(КомпоновщикНастроек);
	
КонецПроцедуры

Процедура НастроитьПараметрыОтборыПоФункциональнымОпциям(КомпоновщикНастроекФормы)
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
		КомпоновкаДанныхСервер.УдалитьЭлементОтбораИзВсехНастроекОтчета(КомпоновщикНастроекФормы, "Контрагент");
	КонецЕсли;
КонецПроцедуры

Функция ТекстЗапроса()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Сегменты.Партнер КАК Партнер,
	|	ИСТИНА КАК ИспользуетсяОтборПоСегментуПартнеров
	|ПОМЕСТИТЬ ОтборПоСегментуПартнеров
	|ИЗ
	|	РегистрСведений.ПартнерыСегмента КАК Сегменты
	|{ГДЕ
	|	Сегменты.Сегмент.* КАК СегментПартнеров,
	|	Сегменты.Партнер.* КАК Партнер}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Партнер,
	|	ИспользуетсяОтборПоСегментуПартнеров
	|;
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка КАК Организация,
	|	КурсВалюты.Валюта КАК Валюта,
	|	ВЫБОР &ДанныеОтчета
	|		КОГДА 4
	|			ТОГДА 1
	|		КОГДА 2
	|			ТОГДА ЕСТЬNULL(КурсВалюты.КурсЧислитель, 1) * ЕСТЬNULL(КурсВалютыУпр.КурсЗнаменатель, 1) / (ЕСТЬNULL(КурсВалюты.КурсЗнаменатель, 1) * ЕСТЬNULL(КурсВалютыУпр.КурсЧислитель, 1))
	|		ИНАЧЕ ЕСТЬNULL(КурсВалюты.КурсЧислитель, 1) / ЕСТЬNULL(КурсВалюты.КурсЗнаменатель, 1)
	|	КОНЕЦ КАК Коэффициент
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних({(&КонецПериода)}) КАК КурсВалюты
	|			ПО Организации.ВалютаРегламентированногоУчета = КурсВалюты.БазоваяВалюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних({(&КонецПериода)}, Валюта = &ВалютаУправленческогоУчета) КАК КурсВалютыУпр
	|			ПО Организации.ВалютаРегламентированногоУчета = КурсВалютыУпр.БазоваяВалюта
	|{ГДЕ
	|	Организации.Ссылка.* КАК Организация}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Валюта
	|;
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	АналитикаУчета.Организация КАК Организация,
	|	АналитикаУчета.Партнер КАК Партнер,
	|	АналитикаУчета.Контрагент КАК Контрагент,
	|	АналитикаУчета.Договор КАК Договор,
	|	АналитикаУчета.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	АналитикаУчета.КлючАналитики КАК КлючАналитики
	|ПОМЕСТИТЬ ВТ_АналитикаУчетаПоПартнерам
	|ИЗ
	|	РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчета
	|ГДЕ
	|		АналитикаУчета.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|{ГДЕ
	|		АналитикаУчета.Организация.* КАК Организация,
	|		АналитикаУчета.Партнер.* КАК Партнер,
	|		АналитикаУчета.Контрагент.* КАК Контрагент,
	|		АналитикаУчета.Договор.* КАК Договор,
	|		АналитикаУчета.НаправлениеДеятельности.* КАК НаправлениеДеятельности,
	|		(АналитикаУчета.Партнер В
	|				(ВЫБРАТЬ
	|					ОтборПоСегментуПартнеров.Партнер
	|				ИЗ
	|					ОтборПоСегментуПартнеров
	|				ГДЕ
	|					ОтборПоСегментуПартнеров.ИспользуетсяОтборПоСегментуПартнеров = &ИспользуетсяОтборПоСегментуПартнеров))}
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючАналитики
	|;
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Расчеты.Организация                 КАК Организация,
	|	Расчеты.Партнер                     КАК Партнер,
	|	Расчеты.Контрагент                  КАК Контрагент,
	|	Расчеты.Договор                     КАК Договор,
	|	Расчеты.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	Расчеты.ОбъектРасчетов              КАК ОбъектРасчетов,
	|	Расчеты.Валюта                      КАК Валюта,
	|	Расчеты.РасчетныйДокумент           КАК РасчетныйДокумент,
	|	Расчеты.ДокументРегистратор         КАК ДокументРегистратор,
	|	Расчеты.ДатаВозникновения           КАК ДатаВозникновения,
	|	Расчеты.ДатаПлановогоПогашения      КАК ДатаПлановогоПогашения,
	|	Расчеты.Сторно                      КАК Сторно,
	|	НАЧАЛОПЕРИОДА(Расчеты.Период, ДЕНЬ) КАК Период,
	|	МАКСИМУМ(Расчеты.ПорядокОперации)   КАК ПорядокОперации,
	|	
	|	СУММА(Расчеты.ДолгПоставщика)       КАК ДолгПоставщика,
	|	СУММА(Расчеты.НашДолг)              КАК НашДолг,
	|	СУММА(Расчеты.ПлановаяОплата)       КАК ПлановаяОплата,
	|	СУММА(Расчеты.ПлановаяПоставка)     КАК ПлановаяПоставка,
	|	СУММА(Расчеты.СписаниеПереоценка)     КАК СписаниеПереоценка,
	|	СУММА(Расчеты.Оплачено)     КАК Оплачено,
	|	СУММА(Расчеты.Поставлено)     КАК Поставлено,
	|	СУММА(Расчеты.Зачтено)     КАК Зачтено
	|ПОМЕСТИТЬ ВтРасчеты
	|ИЗ (
	|	ВЫБРАТЬ
	|		АналитикаУчета.Организация                                     КАК Организация,
	|		АналитикаУчета.Партнер                                         КАК Партнер,
	|		АналитикаУчета.Контрагент                                      КАК Контрагент,
	|		АналитикаУчета.Договор                                         КАК Договор,
	|		АналитикаУчета.НаправлениеДеятельности                         КАК НаправлениеДеятельности,
	|		РасчетыПоСрокамОстатки.ОбъектРасчетов                          КАК ОбъектРасчетов,
	|		ВЫБОР &ДанныеОтчета
	|			КОГДА 4
	|				ТОГДА РасчетыПоСрокамОстатки.Валюта
	|			КОГДА 2
	|				ТОГДА &ВалютаУправленческогоУчета
	|			ИНАЧЕ АналитикаУчета.Организация.ВалютаРегламентированногоУчета
	|		КОНЕЦ                                                          КАК Валюта,
	|		РасчетыПоСрокамОстатки.РасчетныйДокумент                       КАК РасчетныйДокумент,
	|		ВЫРАЗИТЬ(&ТекстНачальныйОстаток КАК СТРОКА(17))                КАК ДокументРегистратор,
	|		РасчетыПоСрокамОстатки.ДатаВозникновения                       КАК ДатаВозникновения,
	|		РасчетыПоСрокамОстатки.ДатаПлановогоПогашения                  КАК ДатаПлановогоПогашения,
	|		&НачалоПериода                                                 КАК Период,
	|		""""                                                           КАК ПорядокОперации,
	|		ЛОЖЬ                                                           КАК Сторно,
	|		
	|		0                                                              КАК Заказано,
	|		0                                                              КАК Поставлено,
	|		0                                                              КАК Зачтено,
	|		0                                                              КАК Оплачено,
	|		0                                                              КАК СписаниеПереоценка,
	|		ВЫБОР &ДанныеОтчета
	|			КОГДА 4
	|				ТОГДА РасчетыПоСрокамОстатки.ПредоплатаНачальныйОстаток
	|			КОГДА 2
	|				ТОГДА РасчетыПоСрокамОстатки.ПредоплатаУпрНачальныйОстаток
	|			ИНАЧЕ РасчетыПоСрокамОстатки.ПредоплатаРеглНачальныйОстаток
	|		КОНЕЦ                                                          КАК ДолгПоставщика,
	|		ВЫБОР &ДанныеОтчета
	|			КОГДА 4
	|				ТОГДА РасчетыПоСрокамОстатки.ДолгНачальныйОстаток
	|			КОГДА 2
	|				ТОГДА РасчетыПоСрокамОстатки.ДолгУпрНачальныйОстаток
	|			ИНАЧЕ РасчетыПоСрокамОстатки.ДолгРеглНачальныйОстаток
	|		КОНЕЦ                                                          КАК НашДолг,
	|		ВЫБОР &ДанныеОтчета
	|			КОГДА 4
	|				ТОГДА РасчетыПоСрокамОстатки.ДолгНачальныйОстаток
	|			КОГДА 2
	|				ТОГДА РасчетыПоСрокамОстатки.ДолгУпрНачальныйОстаток
	|			ИНАЧЕ РасчетыПоСрокамОстатки.ДолгРеглНачальныйОстаток
	|		КОНЕЦ
	|		- ВЫБОР &ДанныеОтчета
	|			КОГДА 4
	|				ТОГДА РасчетыПоСрокамОстатки.ПредоплатаНачальныйОстаток
	|			КОГДА 2
	|				ТОГДА РасчетыПоСрокамОстатки.ПредоплатаУпрНачальныйОстаток
	|			ИНАЧЕ РасчетыПоСрокамОстатки.ПредоплатаРеглНачальныйОстаток
	|		КОНЕЦ КАК ПлановаяОплата,
	|		0                                                              КАК ПлановаяПоставка
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщикамиПоСрокам.ОстаткиИОбороты({&НачалоПериода},{&КонецПериода}, , ,
	|			АналитикаУчетаПоПартнерам В
	|				(ВЫБРАТЬ
	|					ВТ_АналитикаУчетаПоПартнерам.КлючАналитики
	|				ИЗ
	|					ВТ_АналитикаУчетаПоПартнерам)) КАК РасчетыПоСрокамОстатки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АналитикаУчетаПоПартнерам КАК АналитикаУчета
	|				ПО РасчетыПоСрокамОстатки.АналитикаУчетаПоПартнерам = АналитикаУчета.КлючАналитики
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		АналитикаУчета.Организация                      КАК Организация,
	|		АналитикаУчета.Партнер                          КАК Партнер,
	|		АналитикаУчета.Контрагент                       КАК Контрагент,
	|		АналитикаУчета.Договор                          КАК Договор,
	|		АналитикаУчета.НаправлениеДеятельности          КАК НаправлениеДеятельности,
	|		РасчетыПланОплатОстатки.ОбъектРасчетов          КАК ОбъектРасчетов,
	|		ВЫБОР &ДанныеОтчета
	|			КОГДА 4
	|				ТОГДА РасчетыПланОплатОстатки.Валюта
	|			КОГДА 2
	|				ТОГДА &ВалютаУправленческогоУчета
	|			ИНАЧЕ АналитикаУчета.Организация.ВалютаРегламентированногоУчета
	|		КОНЕЦ                                           КАК Валюта,
	|		РасчетыПланОплатОстатки.ДокументПлан            КАК ДокументПлан,
	|		ВЫРАЗИТЬ(&ТекстНачальныйОстаток КАК СТРОКА(17)) КАК ДокументРегистратор,
	|		РасчетыПланОплатОстатки.ДатаВозникновения       КАК ДатаВозникновения,
	|		РасчетыПланОплатОстатки.ДатаПлановогоПогашения  КАК ДатаПлановогоПогашения,
	|		&НачалоПериода                                  КАК Период,
	|		""""                                            КАК ПорядокОперации,
	|		ЛОЖЬ                                                           КАК Сторно,
	|		
	|		0                                               КАК Заказано,
	|		0                                               КАК Поставлено,
	|		0                                               КАК Зачтено,
	|		0                                               КАК Оплачено,
	|		0                                               КАК СписаниеПереоценка,
	|		
	|		0                                               КАК ДолгПоставщика,
	|		0                                               КАК НашДолг,
	|		(РасчетыПланОплатОстатки.КОплатеНачальныйОстаток)
	|		* КурсВалюты.Коэффициент                        КАК ПлановаяОплата,
	|		0                                               КАК ПлановаяПоставка
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщикамиПланОплат.ОстаткиИОбороты({&НачалоПериода},{&КонецПериода},,,Не НераспределенныйАванс
	|			И АналитикаУчетаПоПартнерам В
	|				(ВЫБРАТЬ
	|					ВТ_АналитикаУчетаПоПартнерам.КлючАналитики
	|				ИЗ
	|					ВТ_АналитикаУчетаПоПартнерам)) КАК РасчетыПланОплатОстатки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АналитикаУчетаПоПартнерам КАК АналитикаУчета
	|				ПО РасчетыПланОплатОстатки.АналитикаУчетаПоПартнерам = АналитикаУчета.КлючАналитики
	|			ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалюты
	|				ПО (КурсВалюты.Валюта = РасчетыПланОплатОстатки.Валюта)
	|					И (КурсВалюты.Организация = АналитикаУчета.Организация)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		АналитикаУчета.Организация                        КАК Организация,
	|		АналитикаУчета.Партнер                            КАК Партнер,
	|		АналитикаУчета.Контрагент                         КАК Контрагент,
	|		АналитикаУчета.Договор                            КАК Договор,
	|		АналитикаУчета.НаправлениеДеятельности            КАК НаправлениеДеятельности,
	|		РасчетыПланПоставокОстатки.ОбъектРасчетов         КАК ОбъектРасчетов,
	|		ВЫБОР &ДанныеОтчета
	|			КОГДА 4
	|				ТОГДА РасчетыПланПоставокОстатки.Валюта
	|			КОГДА 2
	|				ТОГДА &ВалютаУправленческогоУчета
	|			ИНАЧЕ АналитикаУчета.Организация.ВалютаРегламентированногоУчета
	|		КОНЕЦ                                             КАК Валюта,
	|		РасчетыПланПоставокОстатки.ДокументПлан           КАК ДокументПлан,
	|		ВЫРАЗИТЬ(&ТекстНачальныйОстаток КАК СТРОКА(17))   КАК ДокументРегистратор,
	|		РасчетыПланПоставокОстатки.ДатаВозникновения      КАК ДатаВозникновения,
	|		РасчетыПланПоставокОстатки.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|		&НачалоПериода                                    КАК Период,
	|		""""                                              КАК ПорядокОперации,
	|		ЛОЖЬ                                              КАК Сторно,
	|		
	|		0                                                 КАК Заказано,
	|		0                                                 КАК Поставлено,
	|		0                                                 КАК Зачтено,
	|		0                                                 КАК Оплачено,
	|		0                                                 КАК СписаниеПереоценка,
	|		0                                                 КАК ДолгПоставщика,
	|		0                                                 КАК НашДолг,
	|		0                                                 КАК ПлановаяОплата,
	|		РасчетыПланПоставокОстатки.СуммаНачальныйОстаток
	|			* КурсВалюты.Коэффициент                      КАК ПлановаяПоставка
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщикамиПланПоставок.ОстаткиИОбороты({&НачалоПериода},{&КонецПериода},,,НЕ НераспределенныйАванс
	|			И АналитикаУчетаПоПартнерам В
	|				(ВЫБРАТЬ
	|					ВТ_АналитикаУчетаПоПартнерам.КлючАналитики
	|				ИЗ
	|					ВТ_АналитикаУчетаПоПартнерам)) КАК РасчетыПланПоставокОстатки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АналитикаУчетаПоПартнерам КАК АналитикаУчета
	|				ПО РасчетыПланПоставокОстатки.АналитикаУчетаПоПартнерам = АналитикаУчета.КлючАналитики
	|			ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалюты
	|				ПО (КурсВалюты.Валюта = РасчетыПланПоставокОстатки.Валюта)
	|					И (КурсВалюты.Организация = АналитикаУчета.Организация)
	|	ГДЕ
	|		НЕ НераспределенныйАванс
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		АналитикаУчета.Организация                                     КАК Организация,
	|		АналитикаУчета.Партнер                                         КАК Партнер,
	|		АналитикаУчета.Контрагент                                      КАК Контрагент,
	|		АналитикаУчета.Договор                                         КАК Договор,
	|		АналитикаУчета.НаправлениеДеятельности                         КАК НаправлениеДеятельности,
	|		РасчетыПоСрокам.ОбъектРасчетов                                 КАК ОбъектРасчетов,
	|		ВЫБОР &ДанныеОтчета
	|			КОГДА 4
	|				ТОГДА РасчетыПоСрокам.Валюта
	|			КОГДА 2
	|				ТОГДА &ВалютаУправленческогоУчета
	|			ИНАЧЕ АналитикаУчета.Организация.ВалютаРегламентированногоУчета
	|		КОНЕЦ                                                          КАК Валюта,
	|		РасчетыПоСрокам.РасчетныйДокумент                              КАК РасчетныйДокумент,
	|		РасчетыПоСрокам.ДокументРегистратор                            КАК ДокументРегистратор,
	|		РасчетыПоСрокам.ДатаВозникновения                              КАК ДатаВозникновения,
	|		РасчетыПоСрокам.ДатаПлановогоПогашения                         КАК ДатаПлановогоПогашения,
	|		РасчетыПоСрокам.Период                                         КАК Период,
	|		РасчетыПоСрокам.ПорядокОперации                                КАК ПорядокОперации,
	|		РасчетыПоСрокам.Сторно                                         КАК Сторно,
	|		
	|		0                                                              КАК Заказано,
	|		&ПоставленоПоставщиком                                         КАК Поставлено,
	|		&ЗачтеноПоставщиком                                            КАК Зачтено,
	|		&ОплаченоПоставщику                                            КАК Оплачено,
	|		&СписаниеПереоценка                                            КАК СписаниеПереоценка,
	|		
	|		ВЫБОР КОГДА РасчетыПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА
	|					ВЫБОР &ДанныеОтчета
	|						КОГДА 4
	|							ТОГДА РасчетыПоСрокам.Предоплата
	|						КОГДА 2
	|							ТОГДА РасчетыПоСрокам.ПредоплатаУпр
	|						ИНАЧЕ РасчетыПоСрокам.ПредоплатаРегл
	|					КОНЕЦ
	|			ИНАЧЕ 
	|					- ВЫБОР &ДанныеОтчета
	|						КОГДА 4
	|							ТОГДА РасчетыПоСрокам.Предоплата
	|						КОГДА 2
	|							ТОГДА РасчетыПоСрокам.ПредоплатаУпр
	|						ИНАЧЕ РасчетыПоСрокам.ПредоплатаРегл
	|					КОНЕЦ
	|		КОНЕЦ                                                          КАК ДолгПоставщика,
	|		ВЫБОР КОГДА РасчетыПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА
	|					ВЫБОР &ДанныеОтчета
	|						КОГДА 4
	|							ТОГДА РасчетыПоСрокам.Долг
	|						КОГДА 2
	|							ТОГДА РасчетыПоСрокам.ДолгУпр
	|						ИНАЧЕ РасчетыПоСрокам.ДолгРегл
	|					КОНЕЦ
	|			ИНАЧЕ
	|					- ВЫБОР &ДанныеОтчета
	|						КОГДА 4
	|							ТОГДА РасчетыПоСрокам.Долг
	|						КОГДА 2
	|							ТОГДА РасчетыПоСрокам.ДолгУпр
	|						ИНАЧЕ РасчетыПоСрокам.ДолгРегл
	|					КОНЕЦ
	|		КОНЕЦ                                                          КАК НашДолг,
	|		ВЫБОР КОГДА РасчетыПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА
	|					ВЫБОР &ДанныеОтчета
	|						КОГДА 4
	|							ТОГДА РасчетыПоСрокам.Долг
	|						КОГДА 2
	|							ТОГДА РасчетыПоСрокам.ДолгУпр
	|						ИНАЧЕ РасчетыПоСрокам.ДолгРегл
	|					КОНЕЦ
	|			ИНАЧЕ
	|					- ВЫБОР &ДанныеОтчета
	|						КОГДА 4
	|							ТОГДА РасчетыПоСрокам.Долг
	|						КОГДА 2
	|							ТОГДА РасчетыПоСрокам.ДолгУпр
	|						ИНАЧЕ РасчетыПоСрокам.ДолгРегл
	|					КОНЕЦ
	|		КОНЕЦ
	|		- ВЫБОР КОГДА РасчетыПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА
	|					ВЫБОР &ДанныеОтчета
	|						КОГДА 4
	|							ТОГДА РасчетыПоСрокам.Предоплата
	|						КОГДА 2
	|							ТОГДА РасчетыПоСрокам.ПредоплатаУпр
	|						ИНАЧЕ РасчетыПоСрокам.ПредоплатаРегл
	|					КОНЕЦ
	|			ИНАЧЕ 
	|					- ВЫБОР &ДанныеОтчета
	|						КОГДА 4
	|							ТОГДА РасчетыПоСрокам.Предоплата
	|						КОГДА 2
	|							ТОГДА РасчетыПоСрокам.ПредоплатаУпр
	|						ИНАЧЕ РасчетыПоСрокам.ПредоплатаРегл
	|			КОНЕЦ
	|		КОНЕЦ                                                          КАК ПлановаяОплата,
	|		0                                                              КАК ПлановаяПоставка
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыПоСрокам
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АналитикаУчетаПоПартнерам КАК АналитикаУчета
	|				ПО РасчетыПоСрокам.АналитикаУчетаПоПартнерам = АналитикаУчета.КлючАналитики
	|	ГДЕ
	|		(РасчетыПоСрокам.ДолгРегл <> 0 ИЛИ РасчетыПоСрокам.ПредоплатаРегл <> 0)
	|	{ГДЕ
	|		РасчетыПоСрокам.Период МЕЖДУ &НачалоПериода И &КонецПериода}
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		АналитикаУчета.Организация               КАК Организация,
	|		АналитикаУчета.Партнер                   КАК Партнер,
	|		АналитикаУчета.Контрагент                КАК Контрагент,
	|		АналитикаУчета.Договор                   КАК Договор,
	|		АналитикаУчета.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|		РасчетыПланОплат.ОбъектРасчетов          КАК ОбъектРасчетов,
	|		ВЫБОР &ДанныеОтчета
	|			КОГДА 4
	|				ТОГДА РасчетыПланОплат.Валюта
	|			КОГДА 2
	|				ТОГДА &ВалютаУправленческогоУчета
	|			ИНАЧЕ АналитикаУчета.Организация.ВалютаРегламентированногоУчета
	|		КОНЕЦ                                                          КАК Валюта,
	|		РасчетыПланОплат.ДокументПлан            КАК ДокументПлан,
	|		РасчетыПланОплат.ДокументРегистратор     КАК ДокументРегистратор,
	|		РасчетыПланОплат.ДатаВозникновения       КАК ДатаВозникновения,
	|		РасчетыПланОплат.ДатаПлановогоПогашения  КАК ДатаПлановогоПогашения,
	|		МИНИМУМ(РасчетыПланОплат.Период)          КАК Период,
	|		МИНИМУМ(РасчетыПланОплат.ПорядокОперации) КАК ПорядокОперации,
	|		ЛОЖЬ                                     КАК Сторно,
	|		
	|		0                                        КАК Заказано,
	|		0                                        КАК Поставлено,
	|		0                                        КАК Зачтено,
	|		0                                        КАК Оплачено,
	|		0                                        КАК СписаниеПереоценка,
	|		
	|		0                                        КАК ДолгПоставщика,
	|		0                                        КАК НашДолг,
	|		СУММА(ВЫБОР КОГДА РасчетыПланОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА РасчетыПланОплат.КОплате
	|			ИНАЧЕ -РасчетыПланОплат.КОплате
	|		КОНЕЦ * КурсВалюты.Коэффициент)          КАК ПлановаяОплата,
	|		0                                        КАК ПлановаяПоставка
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщикамиПланОплат КАК РасчетыПланОплат
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АналитикаУчетаПоПартнерам КАК АналитикаУчета
	|				ПО РасчетыПланОплат.АналитикаУчетаПоПартнерам = АналитикаУчета.КлючАналитики
	|			ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалюты
	|				ПО (КурсВалюты.Валюта = РасчетыПланОплат.Валюта)
	|					И (КурсВалюты.Организация = АналитикаУчета.Организация)
	|	ГДЕ
	|		НЕ РасчетыПланОплат.НераспределенныйАванс
	|	{ГДЕ
	|		РасчетыПланОплат.Период МЕЖДУ &НачалоПериода И &КонецПериода}
	|
	|	СГРУППИРОВАТЬ ПО
	|		АналитикаУчета.Организация,
	|		АналитикаУчета.Партнер,
	|		АналитикаУчета.Контрагент,
	|		АналитикаУчета.Договор,
	|		АналитикаУчета.НаправлениеДеятельности,
	|		РасчетыПланОплат.ОбъектРасчетов,
	|		ВЫБОР &ДанныеОтчета
	|			КОГДА 4
	|				ТОГДА РасчетыПланОплат.Валюта
	|			КОГДА 2
	|				ТОГДА &ВалютаУправленческогоУчета
	|			ИНАЧЕ АналитикаУчета.Организация.ВалютаРегламентированногоУчета
	|		КОНЕЦ,
	|		РасчетыПланОплат.ДокументПлан,
	|		РасчетыПланОплат.ДокументРегистратор,
	|		РасчетыПланОплат.ДатаВозникновения,
	|		РасчетыПланОплат.ДатаПлановогоПогашения
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		АналитикаУчета.Организация                  КАК Организация,
	|		АналитикаУчета.Партнер                      КАК Партнер,
	|		АналитикаУчета.Контрагент                   КАК Контрагент,
	|		АналитикаУчета.Договор                      КАК Договор,
	|		АналитикаУчета.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|		РасчетыПланПоставок.ОбъектРасчетов          КАК ОбъектРасчетов,
	|		ВЫБОР &ДанныеОтчета
	|			КОГДА 4
	|				ТОГДА РасчетыПланПоставок.Валюта
	|			КОГДА 2
	|				ТОГДА &ВалютаУправленческогоУчета
	|			ИНАЧЕ АналитикаУчета.Организация.ВалютаРегламентированногоУчета
	|		КОНЕЦ                                                          КАК Валюта,
	|		РасчетыПланПоставок.ДокументПлан            КАК ДокументПлан,
	|		РасчетыПланПоставок.ДокументРегистратор     КАК ДокументРегистратор,
	|		РасчетыПланПоставок.ДатаВозникновения       КАК ДатаВозникновения,
	|		РасчетыПланПоставок.ДатаПлановогоПогашения  КАК ДатаПлановогоПогашения,
	|		РасчетыПланПоставок.Период                  КАК Период,
	|		РасчетыПланПоставок.ПорядокОперации         КАК ПорядокОперации,
	|		ЛОЖЬ                                     КАК Сторно,
	|		
	|		ВЫБОР КОГДА РасчетыПланПоставок.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА РасчетыПланПоставок.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ * КурсВалюты.Коэффициент              КАК Заказано,
	|		0                                           КАК Поставлено,
	|		0                                           КАК Зачтено,
	|		0                                           КАК Оплачено,
	|		0                                           КАК СписаниеПереоценка,
	|		0                                           КАК ДолгПоставщика,
	|		0                                           КАК НашДолг,
	|		0                                           КАК ПлановаяОплата,
	|		ВЫБОР КОГДА РасчетыПланПоставок.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА РасчетыПланПоставок.Сумма
	|			ИНАЧЕ -РасчетыПланПоставок.Сумма
	|		КОНЕЦ * КурсВалюты.Коэффициент              КАК ПлановаяПоставка
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщикамиПланПоставок КАК РасчетыПланПоставок
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АналитикаУчетаПоПартнерам КАК АналитикаУчета
	|				ПО РасчетыПланПоставок.АналитикаУчетаПоПартнерам = АналитикаУчета.КлючАналитики
	|			ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалюты
	|				ПО (КурсВалюты.Валюта = РасчетыПланПоставок.Валюта)
	|					И (КурсВалюты.Организация = АналитикаУчета.Организация)
	|	ГДЕ
	|		НЕ НераспределенныйАванс
	|	{ГДЕ
	|		РасчетыПланПоставок.Период МЕЖДУ &НачалоПериода И &КонецПериода}) КАК Расчеты
	|
	|СГРУППИРОВАТЬ ПО
	|	Расчеты.Организация,
	|	Расчеты.Партнер,
	|	Расчеты.Контрагент,
	|	Расчеты.Договор,
	|	Расчеты.НаправлениеДеятельности,
	|	Расчеты.ОбъектРасчетов,
	|	Расчеты.Валюта,
	|	Расчеты.РасчетныйДокумент,
	|	Расчеты.ДокументРегистратор,
	|	Расчеты.ДатаВозникновения,
	|	Расчеты.ДатаПлановогоПогашения,
	|	Расчеты.Сторно,
	|	НАЧАЛОПЕРИОДА(Расчеты.Период, ДЕНЬ)
	|;
	|
	|ВЫБРАТЬ 
	|	Расчеты.ОбъектРасчетов              КАК ОбъектРасчетов,
	|	Расчеты.ДокументРегистратор         КАК ДокументРегистратор,
	|	Расчеты.Организация                 КАК Организация,
	|	Расчеты.Период                      КАК Период,
	|	
	|	ВЫБОР КОГДА СУММА(Расчеты.Оплачено) = 0 
	|				И СУММА(Расчеты.Поставлено) = 0
	|				И СУММА(Расчеты.СписаниеПереоценка) = 0 
	|		ТОГДА ВЫБОР КОГДА СУММА(Расчеты.Зачтено)+СУММА(Расчеты.ПлановаяПоставка) = 0 
	|					ТОГДА СУММА(Расчеты.ПлановаяОплата)
	|				ИНАЧЕ ВЫБОР КОГДА СУММА(Расчеты.ПлановаяПоставка) > СУММА(Расчеты.Зачтено) И СУММА(Расчеты.ПлановаяПоставка) > 0
	|								ИЛИ СУММА(Расчеты.ПлановаяПоставка) < СУММА(Расчеты.Зачтено) И СУММА(Расчеты.ПлановаяПоставка) < 0
	|						ТОГДА СУММА(Расчеты.ПлановаяПоставка)
	|						ИНАЧЕ СУММА(Расчеты.Зачтено)
	|					КОНЕЦ
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР КОГДА СУММА(Расчеты.Оплачено) > СУММА(Расчеты.Поставлено) И СУММА(Расчеты.Оплачено) > 0
	|					ТОГДА СУММА(Расчеты.Оплачено)
	|				КОГДА СУММА(Расчеты.Поставлено) > СУММА(Расчеты.Оплачено) И СУММА(Расчеты.Поставлено) > 0
	|					ТОГДА СУММА(Расчеты.Поставлено)
	|				КОГДА СУММА(Расчеты.Оплачено) < СУММА(Расчеты.Поставлено) И СУММА(Расчеты.Оплачено) < 0
	|					ТОГДА СУММА(Расчеты.Оплачено)
	|				КОГДА СУММА(Расчеты.Поставлено) <= СУММА(Расчеты.Оплачено) И СУММА(Расчеты.Поставлено) < 0
	|					ТОГДА СУММА(Расчеты.Поставлено)
	|				ИНАЧЕ СУММА(Расчеты.СписаниеПереоценка)
	|			КОНЕЦ
	|	КОНЕЦ                               КАК СуммаПоДокументу
	|
	|ПОМЕСТИТЬ ВтСуммыПоДокументам
	|ИЗ 
	|	ВтРасчеты КАК Расчеты
	|СГРУППИРОВАТЬ ПО
	|	Расчеты.ОбъектРасчетов,
	|	Расчеты.ДокументРегистратор,
	|	Расчеты.Организация,
	|	Расчеты.Период
	|;
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Расчеты.Организация                 КАК Организация,
	|	Расчеты.Партнер                     КАК Партнер,
	|	Расчеты.Контрагент                  КАК Контрагент,
	|	Расчеты.Договор                     КАК Договор,
	|	Расчеты.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	Расчеты.ОбъектРасчетов              КАК ОбъектРасчетов,
	|	Расчеты.Валюта                      КАК Валюта,
	|	Расчеты.РасчетныйДокумент           КАК РасчетныйДокумент,
	|	Расчеты.ДокументРегистратор         КАК ДокументРегистратор,
	|	Расчеты.ДатаВозникновения           КАК ДатаВозникновения,
	|	Расчеты.ДатаПлановогоПогашения      КАК ДатаПлановогоПогашения,
	|	Расчеты.Период                      КАК Период,
	|	Расчеты.ПорядокОперации             КАК ПорядокОперации,
	|	Расчеты.Сторно                      КАК Сторно,
	|	
	|	Расчеты.ДолгПоставщика              КАК ДолгПоставщика,
	|	Расчеты.НашДолг                     КАК НашДолг,
	|	Расчеты.ПлановаяОплата              КАК ПлановаяОплата,
	|	Расчеты.ПлановаяПоставка            КАК ПлановаяПоставка,
	|	ВЫБОР КОГДА Суммы.СуммаПоДокументу = 0 И Расчеты.ДолгПоставщика <> 0
	|			ТОГДА Расчеты.ДолгПоставщика
	|		КОГДА Суммы.СуммаПоДокументу = 0 И Расчеты.НашДолг <> 0
	|			ТОГДА Расчеты.НашДолг
	|		ИНАЧЕ Суммы.СуммаПоДокументу
	|	КОНЕЦ                               КАК СуммаПоДокументу
	|ИЗ ВтРасчеты КАК Расчеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСуммыПоДокументам КАК Суммы
	|			ПО Суммы.ОбъектРасчетов = Расчеты.ОбъектРасчетов
	|				И Суммы.ДокументРегистратор = Расчеты.ДокументРегистратор
	|				И Суммы.Организация = Расчеты.Организация
	|				И Суммы.Период = Расчеты.Период
	|ГДЕ
	|	Расчеты.ДолгПоставщика <> 0
	|	ИЛИ Расчеты.НашДолг <> 0
	|	ИЛИ Расчеты.ПлановаяОплата <> 0
	|	ИЛИ Расчеты.ПлановаяПоставка <> 0
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ПоставленоПоставщиком", ВзаиморасчетыСервер.ШаблонПоляПоставленоПоставщиком());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ЗачтеноПоставщиком", ВзаиморасчетыСервер.ШаблонПоляЗачтеноПоставщиком());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ОплаченоПоставщику", ВзаиморасчетыСервер.ШаблонПоляОплаченоПоставщику());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&СписаниеПереоценка", ВзаиморасчетыСервер.ШаблонПоляСписаниеПереоценкаЗадолженности());
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура СформироватьПараметрыОтчета(ПараметрКоманды, ЭтаФорма, Параметры)
	
	ПараметрыФормы = ЭтаФорма.ФормаПараметры;
	
	Если ТипЗнч(ПараметрКоманды) = Тип("Массив") Тогда
		ЭтоМассив = Истина;
		Если ПараметрКоманды.Количество() > 0 Тогда
			ПервыйЭлемент = ПараметрКоманды[0];
		Иначе
			ПервыйЭлемент = Неопределено;
		КонецЕсли;
	Иначе
		ЭтоМассив = Ложь;
		ПервыйЭлемент = ПараметрКоманды;
	КонецЕсли;
	
	Если ТипЗнч(ПервыйЭлемент) = Тип("СправочникСсылка.Партнеры") Тогда
		Если ЭтоМассив Тогда
			ЕстьПодчиненныеПартнеры = Ложь;
			Для Каждого ЭлементПараметраКоманды Из ПараметрКоманды Цикл
				Если ПартнерыИКонтрагенты.ЕстьПодчиненныеПартнеры(ЭлементПараметраКоманды) Тогда
					ЕстьПодчиненныеПартнеры = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		Иначе
			ЕстьПодчиненныеПартнеры = ПартнерыИКонтрагенты.ЕстьПодчиненныеПартнеры(ПараметрКоманды);
		КонецЕсли;
		
		Если ЕстьПодчиненныеПартнеры Тогда
			ПараметрыФормы.КлючНазначенияИспользования = "ГруппаПартнеров";
			Параметры.КлючНазначенияИспользования = "ГруппаПартнеров";
		Иначе
			ПараметрыФормы.КлючНазначенияИспользования = ПараметрКоманды;
			Параметры.КлючНазначенияИспользования = ПараметрКоманды;
		КонецЕсли;
		ЗначениеОтбора = ПараметрКоманды;
		ВидСравненияКД = ВидСравненияКомпоновкиДанных.Равно;
		Если ЭтоМассив Тогда
			ЗначениеОтбора = Новый СписокЗначений;
			ЗначениеОтбора.ЗагрузитьЗначения(ПараметрКоманды);
			Если ЕстьПодчиненныеПартнеры Тогда
				ВидСравненияКД = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии;
			Иначе
				ВидСравненияКД = ВидСравненияКомпоновкиДанных.ВСписке;
			КонецЕсли;
		Иначе
			Если ЕстьПодчиненныеПартнеры Тогда
				ВидСравненияКД = ВидСравненияКомпоновкиДанных.ВИерархии;
			Иначе
				ВидСравненияКД = ВидСравненияКомпоновкиДанных.Равно;
			КонецЕсли;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			КомпоновщикНастроек.ФиксированныеНастройки.Отбор,
			"Партнер",
			ЗначениеОтбора,
			ВидСравненияКД,,
			Истина);
	ИначеЕсли ТипЗнч(ПервыйЭлемент) = Тип("СправочникСсылка.ДоговорыКонтрагентов") 
		ИЛИ ТипЗнч(ПервыйЭлемент) = Тип("СправочникСсылка.ДоговорыМеждуОрганизациями") Тогда
		ПараметрыФормы.КлючНазначенияИспользования = ПараметрКоманды;
		Параметры.КлючНазначенияИспользования = ПараметрКоманды;
		ЗначениеОтбора = ПервыйЭлемент;
		ВидСравненияКД = ВидСравненияКомпоновкиДанных.Равно;
		Если ЭтоМассив Тогда
			ЗначениеОтбора = Новый СписокЗначений;
			ЗначениеОтбора.ЗагрузитьЗначения(ПараметрКоманды);
			ВидСравненияКД = ВидСравненияКомпоновкиДанных.ВСписке;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			КомпоновщикНастроек.ФиксированныеНастройки.Отбор, 
			"Договор",
			ЗначениеОтбора,
			ВидСравненияКД,,
			Истина);
	Иначе
		ОбъектРасчетов = ВзаиморасчетыСервер.ПолучитьОбъектыРасчетовДляОтбораОтчета(ПараметрКоманды);
		ПараметрыФормы.КлючНазначенияИспользования = ОбъектРасчетов;
		Параметры.КлючНазначенияИспользования = ОбъектРасчетов;
		ЗначениеОтбора = ОбъектРасчетов;
		ВидСравненияКД = ВидСравненияКомпоновкиДанных.Равно;
		Если ТипЗнч(ОбъектРасчетов) = Тип("Массив") Тогда
			ЗначениеОтбора = Новый СписокЗначений;
			ЗначениеОтбора.ЗагрузитьЗначения(ОбъектРасчетов);
			ВидСравненияКД = ВидСравненияКомпоновкиДанных.ВСписке;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			КомпоновщикНастроек.ФиксированныеНастройки.Отбор, 
			"ОбъектРасчетов",
			ЗначениеОтбора,
			ВидСравненияКД,,
			Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли