
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокВыбораОрганизаций();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	ОчиститьСообщения();
	СформироватьОтчет();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервал(Команда)
	ОбщегоНазначенияУТКлиент.РедактироватьПериод(Отчет, 
		Новый Структура("ДатаНачала, ДатаОкончания", "ДатаНачала", "ДатаОкончания"));
КонецПроцедуры

&НаКлиенте
Процедура ПечатьМХ1(Команда)
	МассивОбъектов = СписокДокументов.ВыгрузитьЗначения();
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Обработка.ПечатьОбщихФорм", "МХ1", МассивОбъектов, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПечатьМХ3(Команда)
	МассивОбъектов = СписокДокументов.ВыгрузитьЗначения();
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Обработка.ПечатьОбщихФорм", "МХ3", МассивОбъектов, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПечатьМХ1МХ3(Команда)
	МассивОбъектов = СписокДокументов.ВыгрузитьЗначения();
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Обработка.ПечатьОбщихФорм", "МХ1,МХ3", МассивОбъектов, ЭтаФорма);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьОтчет()
	
	ТаблицаОтчета.Очистить();
	Если ПроверитьЗаполнение() Тогда
		Отчеты.ЖурналУчетаТМЦСданныхНаХранение.СформироватьОтчетЖурналУчетаТМЦСданныхНаХранение(
			ТаблицаОтчета,
			Отчет,
			СписокДокументов);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораОрганизаций()
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Склады.Поклажедержатель КАК Ссылка
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	Склады.СкладОтветственногоХранения
	|	И Склады.Поклажедержатель ССЫЛКА Справочник.Организации");
	
	МассивОрганизаций = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	Элементы.Организация.СписокВыбора.ЗагрузитьЗначения(МассивОрганизаций);
	
КонецПроцедуры

#КонецОбласти