#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//
// Возвращаемое значение:
//  Неопределено, СтрокаТаблицыЗначений - команда отчета
Функция ДобавитьКомандуОтчета(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.СостояниеРасчетовСКомитентами) 
			И ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриЗакупках") Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.СостояниеРасчетовСКомитентами.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Состояние расчетов с комитентами'");
		
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность = "Обычное";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//
Процедура НастроитьВариантыОтчета(Настройки) Экспорт
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.СостояниеРасчетовСКомитентами);
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, ОписаниеОтчета, Ложь);
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "");
	ОписаниеВарианта.Описание = НСтр("ru= 'Сколько мы должны комитенту за реализованные товары?
		|Сколько комитент должен нам, например, по вознаграждениям?'");
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "СостояниеРасчетовСКомитентами");
	ОписаниеВарианта.Описание = НСтр("ru= 'Сколько мы должны комитенту за реализованные товары?
		|Сколько комитент должен нам, например, по вознаграждениям?'");
	ОписаниеВарианта.ФункциональныеОпции.Добавить("ИспользоватьКомиссиюПриЗакупкахНеБазовая");
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "СостояниеРасчетовСКомитентамиБазовая");
	ОписаниеВарианта.ФункциональныеОпции.Добавить("ИспользоватьКомиссиюПриЗакупкахБазовая");
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "СостояниеРасчетовСКомитентамиКонтекст");
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "СостояниеРасчетовСКомитентамиКонтекстБазовая");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
		
#КонецЕсли