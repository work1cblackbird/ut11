
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ЗначениеВДанныеФормы(ПолучитьИзВременногоХранилища(Параметры.ВидыСобытий),ВидыСобытий);
	КомпоновщикОтбораПоПользователям = Параметры.НастройкаКомпоновки;
	УИД_ВызвавшейФормы               = Параметры.УИД_ВызывающейФормы;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВидыСобытий
&НаКлиенте
Процедура ВидыСобытийПометкаПриИзменении(Элемент)
	
	ТекущаяСтрока = ВидыСобытий.НайтиПоИдентификатору(Элементы.ВидыСобытий.ТекущаяСтрока);
	Если ТекущаяСтрока.Пометка = 2 Тогда
		ТекущаяСтрока.Пометка = 0;
	КонецЕсли;
	
	Родитель = ТекущаяСтрока.ПолучитьРодителя();
	Если Родитель = Неопределено Тогда
		
		Для каждого ПодчиненныйЭлемент Из ТекущаяСтрока.ПолучитьЭлементы() Цикл
			
			Если ТекущаяСтрока.Пометка <> ПодчиненныйЭлемент.Пометка Тогда
				ПодчиненныйЭлемент.Пометка = ТекущаяСтрока.Пометка;
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		ЗначениеПредыдущегоЭлемента = 2;
		
		Для каждого ПодчиненныйЭлемент Из Родитель.ПолучитьЭлементы() Цикл
			
			Если ЗначениеПредыдущегоЭлемента <> 2 И (ПодчиненныйЭлемент.Пометка <> ЗначениеПредыдущегоЭлемента) Тогда
				Родитель.Пометка = 2;
				Возврат;
			ИначеЕсли Родитель.Пометка <> ПодчиненныйЭлемент.Пометка Тогда
				Родитель.Пометка = ПодчиненныйЭлемент.Пометка;
			КонецЕсли;
			
			ЗначениеПредыдущегоЭлемента =  ПодчиненныйЭлемент.Пометка;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВидыСобытийВключатьВсе(Команда)
	
	УстановитьЗначениеФлажкаВключать(Истина,ВидыСобытий);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыСобытийИсключатьВсе(Команда)
	
	УстановитьЗначениеФлажкаВключать(Ложь,ВидыСобытий);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	Закрыть(СтруктураПараметровВыбора());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьЗначениеФлажкаВключать(ЗначениеФлажка,Родитель)
	
	Для каждого Элемент Из Родитель.ПолучитьЭлементы() Цикл
	
		Элемент.Пометка = ЗначениеФлажка;
		УстановитьЗначениеФлажкаВключать(ЗначениеФлажка,Элемент);
	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СтруктураПараметровВыбора()

	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("КомпоновщикНастроек",КомпоновщикОтбораПоПользователям);
	ПараметрыВыбора.Вставить("ВидыСобытий",ВременноеХранилищеНастроекНаСервере());
	Возврат ПараметрыВыбора;

КонецФункции

&НаСервере
Функция ВременноеХранилищеНастроекНаСервере()
	
	Возврат ПоместитьВоВременноеХранилище(РеквизитФормыВЗначение("ВидыСобытий"),УИД_ВызвавшейФормы);
	
КонецФункции

#КонецОбласти
