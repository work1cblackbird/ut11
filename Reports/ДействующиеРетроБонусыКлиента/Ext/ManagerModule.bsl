// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//   НастройкиОтчета - см. ВариантыОтчетов.ОписаниеОтчета.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	ВыводитьВариантыОтчетов = Ложь;
	
	НастройкиОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ДействующиеРетроБонусыКлиента);
	НастройкиОтчета.ФункциональныеОпции.Добавить(Метаданные.ФункциональныеОпции.ИспользоватьРетроБонусыКлиентов.Имя);
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(
		Настройки, НастройкиОтчета, "ДействующиеРетроБонусыПоКлиенту");
	НастройкиВарианта.Описание = НСтр("ru = 'Какие условия ретро-бонусов действуют для контрагента и/или клиента?'");
	НастройкиВарианта.Размещение.Вставить(Метаданные.Подсистемы.CRMИМаркетинг.Подсистемы.РетроБонусыКлиентов);
	
	ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, ВыводитьВариантыОтчетов);
	
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(
		Настройки,
		НастройкиОтчета,
		"ДействующиеРетроБонусыПоКлиентуКонтекст");
	
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(
		Настройки,
		НастройкиОтчета,
		"ДействующиеРетроБонусыПоКонтрагентуКонтекст");
	
КонецПроцедуры

#КонецОбласти

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//   КлючВарианта - Строка - 
//   
//	Возвращаемое значение:
//		СтрокаТаблицыЗначений - описание созданной команды.
//
Функция ДобавитьКомандуОтчета(КомандыОтчетов, Параметры, КлючВарианта = "") Экспорт
	
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ДействующиеРетроБонусыКлиента)
	   И ПолучитьФункциональнуюОпцию("ИспользоватьРетроБонусыКлиентов") Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		Представление = НСтр("ru = 'Действующие ретро-бонусы по клиенту'");
		КомандаОтчет.Менеджер = Метаданные.Отчеты.ДействующиеРетроБонусыКлиента.ПолноеИмя();
		КомандаОтчет.Представление = Представление;
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность = "Обычное";
		
		Если ЗначениеЗаполнено(КлючВарианта) Тогда
			
			КомандаОтчет.КлючВарианта = КлючВарианта;
			
		КонецЕсли;
		
		Возврат КомандаОтчет;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли