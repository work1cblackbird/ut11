#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - см. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПриСозданииНаСервере = Истина;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   ЭтаФорма - ФормаКлиентскогоПриложения - Форма отчета. Содержит в том числе:
//   	* Параметры - Структура - Структура параметров, содержит в том числе:
//   		** ОписаниеКоманды - Структура:
//   			*** ДополнительныеПараметры - Структура:
//   				**** ИмяКоманды - Строка
//   Отказ - Булево - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Булево - Передается из параметров обработчика "как есть".
//
// См. также:
//   "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	КомпоновщикНастроекФормы = ЭтаФорма.Отчет.КомпоновщикНастроек; // КомпоновщикНастроекКомпоновкиДанных
	
	ДоступныеПоляОтбораТип = КомпоновщикНастроекФормы.Настройки.ДоступныеПоляОтбора.Элементы.Найти("Тип");
	Если ДоступныеПоляОтбораТип <> Неопределено Тогда
		ЗначениеНеопеределено = ДоступныеПоляОтбораТип.ДоступныеЗначения.НайтиПоЗначению(Тип("Неопределено"));
		ДоступныеПоляОтбораТип.ДоступныеЗначения.Удалить(ЗначениеНеопеределено);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	
	ПараметрДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("Период");
	ПериодОтчета = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанных.ИдентификаторПользовательскойНастройки);
	
	Если ПериодОтчета <> Неопределено Тогда
		ЗаголовокОтчета = НСтр("ru='Реестр торговых документов за'") + " " + Формат(ПериодОтчета.Значение.ДатаНачала,"ДЛФ=D") + 
														" - " + Формат(ПериодОтчета.Значение.ДатаОкончания, "ДЛФ=D");
	Иначе
		ЗаголовокОтчета = НСтр("ru='Реестр торговых документов'");
	КонецЕсли;
	
	ПараметрЗаголовок = КомпоновщикНастроек.Настройки.ПараметрыВывода.Элементы.Найти("Заголовок");
	Если ПараметрЗаголовок <> Неопределено Тогда
		ПараметрЗаголовок.Значение = ЗаголовокОтчета;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
