#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - см. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.События.ПриСозданииНаСервере = Истина;
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   ЭтаФорма - ФормаКлиентскогоПриложения - Форма отчета.
//   Отказ - Булево - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Булево - Передается из параметров обработчика "как есть".
//
// См. также:
//   "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	КомпоновщикНастроекФормы = ЭтаФорма.Отчет.КомпоновщикНастроек;
	Параметры = ЭтаФорма.Параметры;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		
		ЭтаФорма.ФормаПараметры.КлючНазначенияИспользования = Параметры.ПараметрКоманды;
		
		ПараметрРегистратор = ПолучитьДокументыРасчетовСПоставщиками(Параметры.ПараметрКоманды);
		ЭтаФорма.ФормаПараметры.Отбор.Вставить("ДокументПоступления", ПараметрРегистратор);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДокументыРасчетовСПоставщиками(Документ)
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Товары.ДокументПоступления КАК  ДокументПоступления
		|ИЗ
		|	Документ.ВозвратТоваровПоставщику.Товары КАК Товары
		|ГДЕ
		|	Товары.Ссылка В (&Документ)
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.ДокументПоступления КАК  ДокументПоступления
		|ИЗ
		|	Документ.ВыкупВозвратнойТарыУПоставщика.Товары КАК Товары
		|ГДЕ
		|	Товары.Ссылка В (&Документ)
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПриобретениеТоваровУслуг.Ссылка КАК  ДокументПоступления
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
		|ГДЕ
		|	Товары.Ссылка В (&Документ)
		|");
		
	Если ТипЗнч(Документ) <> Тип("СписокЗначений") И ТипЗнч(Документ) <> Тип("Массив") Тогда
		СписокДокументов = Новый СписокЗначений();
		СписокДокументов.Добавить(Документ);
	Иначе
		СписокДокументов = Документ;
	КонецЕсли;
		
	Запрос.УстановитьПараметр("Документ", СписокДокументов);
	МассивДокументов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ДокументПоступления");
	СписокОтбора = Новый СписокЗначений();
	СписокОтбора.ЗагрузитьЗначения(МассивДокументов);
	Возврат СписокОтбора;
	
КонецФункции

#КонецОбласти

#КонецЕсли