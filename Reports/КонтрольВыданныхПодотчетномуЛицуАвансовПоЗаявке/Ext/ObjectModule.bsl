#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - См. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПриСозданииНаСервере = Истина;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   Отказ - Булево - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Булево - Передается из параметров обработчика "как есть".
//
// См. также:
//   "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Параметры = Форма.Параметры;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		Форма.ФормаПараметры.Отбор.Вставить("Документ", Параметры.ПараметрКоманды);
		Форма.НастройкиОтчета.РазрешеноВыбиратьВарианты = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрДокумент = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек.Настройки, "Документ");
	
	Если ЗначениеЗаполнено(ПараметрДокумент.Значение) Тогда
		
		Если ТипЗнч(ПараметрДокумент.Значение) = Тип("ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств") Тогда
			
			РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				ПараметрДокумент.Значение, "Организация, ПодотчетноеЛицо");
			
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(КомпоновщикНастроек,"Организация",РеквизитыДокумента["Организация"],ВидСравненияКомпоновкиДанных.Равно);
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(КомпоновщикНастроек,"ПодотчетноеЛицо",РеквизитыДокумента["ПодотчетноеЛицо"],ВидСравненияКомпоновкиДанных.Равно);
				
		ИначеЕсли ТипЗнч(ПараметрДокумент.Значение) = Тип("ДокументСсылка.ЗаявкаНаКомандировку") Тогда
			
			РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				ПараметрДокумент.Значение, "Организация, Сотрудник, СписокФизЛиц");
			
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(КомпоновщикНастроек,"Организация",РеквизитыДокумента["Организация"],ВидСравненияКомпоновкиДанных.Равно);
			
			Если РеквизитыДокумента["СписокФизЛиц"] Тогда
				Запрос = Новый Запрос("ВЫБРАТЬ Сотрудник ИЗ Документ.ЗаявкаНаКомандировку.ВыдачаПодОтчет ГДЕ Ссылка = &Ссылка");
				Запрос.УстановитьПараметр("Ссылка", ПараметрДокумент.Значение);
				СписокСотрудников = Новый СписокЗначений;
				СписокСотрудников.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Сотрудник"));
				КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(КомпоновщикНастроек,"ПодотчетноеЛицо",СписокСотрудников,ВидСравненияКомпоновкиДанных.ВСписке);
			Иначе
				КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(КомпоновщикНастроек,"ПодотчетноеЛицо",РеквизитыДокумента["Сотрудник"],ВидСравненияКомпоновкиДанных.Равно);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли