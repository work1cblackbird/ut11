#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
// Возвращаемое значение:
//   СтрокаТаблицыЗначений - новая команда отчета.
Функция ДобавитьКомандуОтчета(КомандыОтчетов) Экспорт
	
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ВедомостьПоТоварамОрганизаций) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.ВедомостьПоТоварамОрганизаций.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Движения товара в организациях'");
		
		
		КомандаОтчет.Важность = "Обычное";
		КомандаОтчет.МножественныйВыбор = Ложь;
		
		КомандаОтчет.ДополнительныеПараметры.Вставить("ИмяКоманды", "ВедомостьПоНоменклатуреОрганизаций");
		
		КомандаОтчет.КлючВарианта = "ПоНоменклатуреКонтекст";
		
		КомандаОтчет.ФункциональныеОпции = "ИспользоватьНесколькоОрганизаций";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//
Процедура НастроитьВариантыОтчета(Настройки) Экспорт
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ВедомостьПоТоварамОрганизаций);
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "");
	ОписаниеВарианта.Описание = НСтр("ru= 'Контроль остатков и движений товаров по организациям.'");
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ДвиженияТоваровОрганизаций");
	ОписаниеВарианта.Описание = НСтр("ru= 'Анализ движений товаров на складах и организациях.
		|В какой организации и по какому складу есть товар и были движения за период?'");
	ПодсистемаОтчета = Метаданные.НайтиПоПолномуИмени("Подсистема.Продажи.Подсистема.ОптовыеПродажи");
	ОписаниеВарианта.Размещение.Вставить(ПодсистемаОтчета);
	ВариантыОтчетовУТПереопределяемый.УстановитьВажностьВариантаОтчета(ОписаниеВарианта, "Важный");
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ДоступныеДляПродажиТоварыОрганизаций");
	ОписаниеВарианта.Описание = НСтр("ru= 'Какие товары доступны на складах для продажи от имени выбранной организации?'");
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ДвиженияТоваровПереданных");
	ОписаниеВарианта.Описание = НСтр("ru= 'Анализ движений товаров, переданных на хранение.
		|У каких партнеров и по каким договорам есть наш товар и были движения за период?'");
	ОписаниеВарианта.ФункциональныеОпции.Добавить("ИспользоватьПередачуНаОтветственноеХранениеСПравомПродажи");
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ДвиженияТоваровПереданныхНаКомиссию25");
	НаименованиеВарианта = НСтр("ru= 'Ведомость по переданным на комиссию товарам'");
	НаименованиеВарианта = КомиссионнаяТорговляСервер.ДобавитьПостфиксКомиссии25(НаименованиеВарианта);
	ОписаниеВарианта.Наименование = НаименованиеВарианта;
	ОписаниеВарианта.Описание = НСтр("ru= 'Анализ движений товаров, переданных на комиссию.
		|У каких партнеров и по каким договорам есть наш товар и были движения за период?'");
	ОписаниеВарианта.ФункциональныеОпции.Добавить("ИспользоватьКомиссиюПриПродажах");
	ПодсистемаОтчета = Метаданные.НайтиПоПолномуИмени("Подсистема.Продажи.Подсистема.КомиссионныеПродажи");
	ОписаниеВарианта.Размещение.Вставить(ПодсистемаОтчета);
	ОписаниеВарианта.ВидимостьПоУмолчанию = Истина;
	
	ОтключитьВариантДвиженияТоваровПереданныхПереработчикам = Истина;
	Если ОтключитьВариантДвиженияТоваровПереданныхПереработчикам Тогда
		ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "ДвиженияТоваровПереданныхПереработчикам");
	КонецЕсли;
	
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "ОстаткиТоваровОрганизаций");
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "ПоНоменклатуреКонтекст");
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "ПоНоменклатуреКонтекстБазовая");
	//++ Локализация
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ДвиженияТоваровОрганизацийПоРНПТ");
	ОписаниеВарианта.Описание = НСтр("ru= 'Контроль остатков и движений прослеживаемых импортных товаров по организациям.'");
	ОписаниеВарианта.ФункциональныеОпции.Добавить("ИспользоватьУчетПрослеживаемыхИмпортныхТоваров");
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
		
#КонецЕсли