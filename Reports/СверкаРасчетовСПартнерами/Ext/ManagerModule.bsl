#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//
// Возвращаемое значение:
//	- Неопределено
//	- СтрокаТаблицыЗначений.
Функция ДобавитьКомандуОтчета(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.СверкаРасчетовСПартнерами) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Обработчик = "ОтчетыКлиентЛокализация.СверкаРасчетовСПартнерами";
		КомандаОтчет.Менеджер = Метаданные.Отчеты.СверкаРасчетовСПартнерами.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Сверка расчетов с партнерами'");
		
		КомандаОтчет.МножественныйВыбор = Истина;
		КомандаОтчет.Важность = "Обычное";
		КомандаОтчет.РежимЗаписи = "Проводить";
		КомандаОтчет.ИмяПараметраФормы = "Отбор.СверкаВзаиморасчетов";
		Если ПолучитьФункциональнуюОпцию("БазоваяВерсия") Тогда
			КомандаОтчет.КлючВарианта = "СверкаВзаиморасчетовКонтекстБазовая";
		Иначе
			КомандаОтчет.КлючВарианта = "СверкаВзаиморасчетовКонтекст";
		КонецЕсли;
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//
Процедура НастроитьВариантыОтчета(Настройки) Экспорт
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.СверкаРасчетовСПартнерами);
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "СверкаВзаиморасчетов");
	ОписаниеВарианта.Описание = НСтр("ru= 'Анализ задокументированных сверок расчетов с клиентами.
		|Есть ли расхождения между данными от клиента и нашими?'");
	ОписаниеВарианта.ФункциональныеОпции.Добавить("НеБазоваяВерсия");
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "СверкаВзаиморасчетовБазовая");
	ОписаниеВарианта.Описание = НСтр("ru= 'Анализ задокументированных сверок расчетов с клиентами.
		|Есть ли расхождения между данными от клиента и нашими?'");
	ОписаниеВарианта.ФункциональныеОпции.Добавить("БазоваяВерсия");
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "СверкаВзаиморасчетовКонтекст");
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "СверкаВзаиморасчетовКонтекстБазовая");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли