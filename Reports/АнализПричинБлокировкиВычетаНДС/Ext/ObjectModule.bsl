#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НаДату = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек.ПользовательскиеНастройки, "НаДату").Значение;
	НаДату = КонецДня(НаДату);
		
	Организация = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(
		КомпоновщикНастроек.ПользовательскиеНастройки, "Организация").Значение;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Организация", Организация);
	СтруктураПараметров.Вставить("НаДату"     , НаДату);
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();

	ВнешниеНаборыДанных = ПолучитьВнешниеНаборыДанных(Организация, НаДату);
	
	// Скроем блоки основного варианта с пустыми таблицами данных.
	НастройкаВидимостиБлоков = ВнешниеНаборыДанных.ВидимостьБлоков;

	Для Каждого Блок Из НастройкиОтчета.Структура Цикл  

		НастройкаВидимостиТекущегоБлока = НастройкаВидимостиБлоков.Найти(Блок.Имя,"Имя");
		Если НастройкаВидимостиТекущегоБлока <> Неопределено Тогда 
			Блок.Использование = НастройкаВидимостиТекущегоБлока.Видимость;
		КонецЕсли;
		
	КонецЦикла;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьВнешниеНаборыДанных(Организация, НаДату)
	
	СтруктураНаборовДанных = Новый Структура;
			
	МВТ = Новый МенеджерВременныхТаблиц;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;

	Запрос.УстановитьПараметр("ГраницаОстатков", Новый Граница(КонецДня(НаДату), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КонецПериода",    КонецДня(НаДату));
	Запрос.УстановитьПараметр("Организация",     Организация);
	
	#Область ИсходныеДанные
	// Исходные данные.
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
					|	НДСПредъявленныйОстатки.Организация КАК Организация,
					|	НДСПредъявленныйОстатки.СчетФактура КАК СчетФактура,
					|	НДСПредъявленныйОстатки.Поставщик КАК Поставщик,
					|	НДСПредъявленныйОстатки.ВидЦенности КАК ВидЦенности,
					|	НДСПредъявленныйОстатки.СтавкаНДС КАК СтавкаНДС,
					|	НДСПредъявленныйОстатки.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
					|	НДСПредъявленныйОстатки.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
					|	НДСПредъявленныйОстатки.РеализацияЭкспорт КАК РеализацияЭкспорт,
					|	НДСПредъявленныйОстатки.НаправлениеДеятельности КАК НаправлениеДеятельности,
					|	НДСПредъявленныйОстатки.НДСОстаток КАК НДСОстаток
					|ПОМЕСТИТЬ ОстаткиПоНДС
					|ИЗ
					|	РегистрНакопления.НДСПредъявленный.Остатки(&ГраницаОстатков, Организация = &Организация) КАК НДСПредъявленныйОстатки
					|ИНДЕКСИРОВАТЬ ПО
					|	СчетФактура
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	ОстаткиПоНДС.СчетФактура КАК СчетФактура,
					|	ОстаткиПоНДС.Организация КАК Организация,
					|	ОстаткиПоНДС.ВидЦенности КАК ВидЦенности
					|ПОМЕСТИТЬ СписокСФ
					|ИЗ
					|	ОстаткиПоНДС КАК ОстаткиПоНДС
					|
					|ИНДЕКСИРОВАТЬ ПО
					|	СчетФактура,
					|	Организация
					|";
	Запрос.Выполнить();
	#КонецОбласти
	
	#Область РучныеБлокировки_1
	// Первичная выборка и Ручные блокировки (1).
	Запрос.Текст = "ВЫБРАТЬ
					|	БлокировкаВычетаНДССчетаФактуры.СчетФактура КАК СчетФактура,
					|	МАКСИМУМ(БлокировкаВычетаНДССчетаФактуры.Ссылка.Дата) КАК ДатаПоследнегоДокумента
					|ПОМЕСТИТЬ ДатыПоследнихОперацийПоБлокировке
					|ИЗ
					|	Документ.БлокировкаВычетаНДС.СчетаФактуры КАК БлокировкаВычетаНДССчетаФактуры
					|ГДЕ
					|	НЕ БлокировкаВычетаНДССчетаФактуры.Ссылка.ПометкаУдаления
					|	И БлокировкаВычетаНДССчетаФактуры.Ссылка.Дата <= &КонецПериода
					|	И (БлокировкаВычетаНДССчетаФактуры.Ссылка.Организация, БлокировкаВычетаНДССчетаФактуры.СчетФактура) В
					|			(ВЫБРАТЬ
					|				СписокСФ.Организация,
					|				СписокСФ.СчетФактура
					|			ИЗ
					|				СписокСФ КАК СписокСФ)
					|
					|СГРУППИРОВАТЬ ПО
					|	БлокировкаВычетаНДССчетаФактуры.СчетФактура
					|
					|ИНДЕКСИРОВАТЬ ПО
					|	СчетФактура,
					|	ДатаПоследнегоДокумента
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	БлокировкаВычетаНДССчетаФактуры.СчетФактура КАК СчетФактура,
					|	МАКСИМУМ(БлокировкаВычетаНДССчетаФактуры.Ссылка) КАК Ссылка
					|ПОМЕСТИТЬ ВТ_СрезПоследнихДокументовБлокировки
					|ИЗ
					|	Документ.БлокировкаВычетаНДС.СчетаФактуры КАК БлокировкаВычетаНДССчетаФактуры
					|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыПоследнихОперацийПоБлокировке КАК ДатыПоследнихОперацийПоБлокировке
					|		ПО (ДатыПоследнихОперацийПоБлокировке.СчетФактура = БлокировкаВычетаНДССчетаФактуры.СчетФактура)
					|			И (ДатыПоследнихОперацийПоБлокировке.ДатаПоследнегоДокумента = БлокировкаВычетаНДССчетаФактуры.Ссылка.Дата)
					|ГДЕ
					|	НЕ БлокировкаВычетаНДССчетаФактуры.Ссылка.ПометкаУдаления
					|
					|СГРУППИРОВАТЬ ПО
					|	БлокировкаВычетаНДССчетаФактуры.СчетФактура
					|
					|ИНДЕКСИРОВАТЬ ПО
					|	СчетФактура,
					|	Ссылка
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	РучныеБлокировки.Организация КАК Организация,
					|	РучныеБлокировки.СчетФактура КАК СчетФактура,
					|	МАКСИМУМ(РучныеБлокировки.НДС) КАК НДС,
					|	МИНИМУМ(РучныеБлокировки.СрокБлокировки) КАК СрокБлокировки
					|ПОМЕСТИТЬ РучнаяБлокировкаВычетаНДС
					|ИЗ
					|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
					|		ЗаблокированныеСФ.Ссылка.Организация КАК Организация,
					|		ЗаблокированныеСФ.СчетФактура КАК СчетФактура,
					|		СУММА(ЗаблокированныеСФ.НДС) КАК НДС,
					|		МИНИМУМ(ВЫБОР
					|				КОГДА ЗаблокированныеСФ.СрокБлокировки = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
					|					ТОГДА ДАТАВРЕМЯ(3999, 12, 31)
					|				ИНАЧЕ ЗаблокированныеСФ.СрокБлокировки
					|			КОНЕЦ) КАК СрокБлокировки
					|	ИЗ
					|		Документ.БлокировкаВычетаНДС.СчетаФактуры КАК ЗаблокированныеСФ
					|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СрезПоследнихДокументовБлокировки КАК ВТ_СрезПоследнихДокументовБлокировки
					|			ПО ЗаблокированныеСФ.СчетФактура = ВТ_СрезПоследнихДокументовБлокировки.СчетФактура
					|				И ЗаблокированныеСФ.Ссылка = ВТ_СрезПоследнихДокументовБлокировки.Ссылка
					|	ГДЕ
					|		(ЗаблокированныеСФ.СрокБлокировки = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
					|				ИЛИ ЗаблокированныеСФ.СрокБлокировки > &КонецПериода)
					|		И ЗаблокированныеСФ.Ссылка.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБлокировкиВычетаНДС.Установлена)
					|	
					|	СГРУППИРОВАТЬ ПО
					|		ЗаблокированныеСФ.Ссылка.Организация,
					|		ЗаблокированныеСФ.СчетФактура,
					|		ЗаблокированныеСФ.СрокБлокировки) КАК РучныеБлокировки
					|
					|СГРУППИРОВАТЬ ПО
					|	РучныеБлокировки.Организация,
					|	РучныеБлокировки.СчетФактура,
					|	РучныеБлокировки.СрокБлокировки
					|
					|ИНДЕКСИРОВАТЬ ПО
					|	Организация,
					|	СчетФактура
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	ОстаткиПоНДС.Организация КАК Организация,
					|	ОстаткиПоНДС.СчетФактура КАК СчетФактура,
					|	ОстаткиПоНДС.Поставщик КАК Поставщик,
					|	ОстаткиПоНДС.ВидЦенности КАК ВидЦенности,
					|	ОстаткиПоНДС.СтавкаНДС КАК СтавкаНДС,
					|	ОстаткиПоНДС.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
					|	ОстаткиПоНДС.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
					|	ОстаткиПоНДС.РеализацияЭкспорт КАК РеализацияЭкспорт,
					|	ОстаткиПоНДС.НаправлениеДеятельности КАК НаправлениеДеятельности,
					|	ОстаткиПоНДС.НДСОстаток КАК НДСОстаток,
					|	
					|	ВЫБОР
					|		КОГДА РучнаяБлокировкаВычетаНДС.СрокБлокировки = ДАТАВРЕМЯ(3999, 12, 31)
					|			ТОГДА ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(ОстаткиПоНДС.СчетФактура.Дата, КВАРТАЛ), КВАРТАЛ, 11)
					|		ИНАЧЕ РучнаяБлокировкаВычетаНДС.СрокБлокировки
					|	КОНЕЦ КАК СрокБлокировки,
					|	
					|	ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(ОстаткиПоНДС.СчетФактура.Дата, КВАРТАЛ), КВАРТАЛ, 11) КАК ПравоВычетаДо,
					|	
					|	ВЫБОР
					|		КОГДА РучнаяБлокировкаВычетаНДС.НДС ЕСТЬ NULL
					|			ТОГДА 0
					|		ИНАЧЕ ВЫБОР
					|				КОГДА РучнаяБлокировкаВычетаНДС.НДС = 0
					|					ТОГДА ОстаткиПоНДС.НДСОстаток
					|				ИНАЧЕ ВЫБОР
					|						КОГДА РучнаяБлокировкаВычетаНДС.НДС > ОстаткиПоНДС.НДСОстаток
					|							ТОГДА ОстаткиПоНДС.НДСОстаток
					|						ИНАЧЕ РучнаяБлокировкаВычетаНДС.НДС
					|					КОНЕЦ
					|			КОНЕЦ
					|	КОНЕЦ КАК Заблокировано1
					|	
					|ПОМЕСТИТЬ ОстаткиПоНДС1
					|ИЗ
					|	ОстаткиПоНДС КАК ОстаткиПоНДС
					|		ЛЕВОЕ СОЕДИНЕНИЕ РучнаяБлокировкаВычетаНДС КАК РучнаяБлокировкаВычетаНДС
					|		ПО ОстаткиПоНДС.Организация = РучнаяБлокировкаВычетаНДС.Организация
					|			И ОстаткиПоНДС.СчетФактура = РучнаяБлокировкаВычетаНДС.СчетФактура
					|
					|ИНДЕКСИРОВАТЬ ПО
					|	Организация,
					|	СчетФактура
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ ВТ_СрезПоследнихДокументовБлокировки
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ ДатыПоследнихОперацийПоБлокировке
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ ОстаткиПоНДС
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ РучнаяБлокировкаВычетаНДС
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|";
	
	Запрос.Выполнить();
	#КонецОбласти

	#Область ЭлектронныеУслуги_2
	// Электронные услуги (2).
	// Могут отсутствовать. Обрабатываем только при наличии электронных услуг в остатках НДС предъявленного.
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	СписокСФ.СчетФактура КАК СчетФактура,
					|	СписокСФ.Организация КАК Организация
					|ПОМЕСТИТЬ Задания
					|ИЗ
					|	СписокСФ КАК СписокСФ
					|ГДЕ
					|	СписокСФ.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ЭлектронныеУслуги)
					|
					|ИНДЕКСИРОВАТЬ ПО
					|	Организация,
					|	СчетФактура";

	Запрос.Выполнить();
	
	ВТЭлектронныхУслуг = Запрос.МенеджерВременныхТаблиц.Таблицы.Найти("Задания");
	Если НЕ ВТЭлектронныхУслуг.ПолучитьДанные().Пустой() Тогда
		// В остатках есть электронные услуги. 
		
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
						|	НДСПредъявленный.Регистратор КАК СчетФактура,
						|	НДСПредъявленный.Организация КАК Организация,
						|	СУММА(НДСПредъявленный.НДС) КАК НДСПриобретения
						|ПОМЕСТИТЬ НДСПриПриобретенииЭлектронныхУслуг
						|ИЗ
						|	РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
						|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Задания КАК Задания
						|		ПО (Задания.СчетФактура = НДСПредъявленный.Регистратор)
						|ГДЕ
						|	НДСПредъявленный.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
						|	И НДСПредъявленный.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ЭлектронныеУслуги)
						|
						|СГРУППИРОВАТЬ ПО
						|	НДСПредъявленный.Регистратор,
						|	НДСПредъявленный.Организация
						|
						|ИНДЕКСИРОВАТЬ ПО
						|	Организация,
						|	СчетФактура
						|;
						|
						|////////////////////////////////////////////////////////////////////////////////
						|ВЫБРАТЬ РАЗЛИЧНЫЕ
						|	НДСПриПриобретенииЭлектронныхУслуг.Организация КАК Организация,
						|	НДСПриПриобретенииЭлектронныхУслуг.СчетФактура КАК СчетФактура,
						|	НДСПриПриобретенииЭлектронныхУслуг.НДСПриобретения КАК НДСПриобретения,
						|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
						|	Расчеты.Сумма КАК Сумма
						|ПОМЕСТИТЬ ПриобретенияЭлектронныхУслуг
						|ИЗ
						|	РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
						|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НДСПриПриобретенииЭлектронныхУслуг КАК НДСПриПриобретенииЭлектронныхУслуг
						|		ПО (НДСПриПриобретенииЭлектронныхУслуг.СчетФактура = Расчеты.Регистратор)
						|ГДЕ
						|	Расчеты.Сумма <> 0
						|
						|ИНДЕКСИРОВАТЬ ПО
						|	АналитикаУчетаПоПартнерам,
						|	СчетФактура";
		
		Запрос.Выполнить();
		
		МассивОрганизаций = Новый Массив;
		МассивОрганизаций.Добавить(Организация);
		
		ПараметрыПоиска = ВзаиморасчетыСервер.ИнициализироватьПараметрыПоискаОснованийИДокументовОплаты();
		ПараметрыПоиска.ПараметрыОтбораПоТаблицеДокументов.ИмяПоля          = "СчетФактура";
		ПараметрыПоиска.ПараметрыОтбораПоТаблицеДокументов.ИмяТаблицыОтбора = "ПриобретенияЭлектронныхУслуг";
		ПараметрыПоиска.Организации                                         = МассивОрганизаций;
		ПараметрыПоиска.НаДату                                              = НаДату;
		
		ВзаиморасчетыСервер.ПодготовитьВТ_РасчетыСПоставщиками_ОснованияИДокументыОплаты(МВТ,ПараметрыПоиска);
		
		Запрос.Текст = "ВЫБРАТЬ
						|	ПриобретенияЭлектронныхУслуг.Организация КАК Организация,
						|	ПриобретенияЭлектронныхУслуг.СчетФактура КАК СчетФактура,
						|	СУММА(ЕСТЬNULL(Оплаты.СуммаОплаты, 0)) КАК СуммаОплаты,
						|	МАКСИМУМ(ПриобретенияЭлектронныхУслуг.Сумма) КАК СуммаПриобретения,
						|	МАКСИМУМ(ПриобретенияЭлектронныхУслуг.НДСПриобретения) КАК НДСПриобретения
						|ПОМЕСТИТЬ ОплатаЭлектронныхУслуг
						|ИЗ
						|	ПриобретенияЭлектронныхУслуг КАК ПриобретенияЭлектронныхУслуг
						|		ЛЕВОЕ СОЕДИНЕНИЕ РасчетыСПоставщиками_ОснованияИДокументыОплаты КАК Оплаты
						|		ПО (Оплаты.АналитикаУчетаПоПартнерам = ПриобретенияЭлектронныхУслуг.АналитикаУчетаПоПартнерам)
						|			И (Оплаты.ОснованиеОплаты = ПриобретенияЭлектронныхУслуг.СчетФактура)
						|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
						|		ПО (Оплаты.АналитикаУчетаПоПартнерам.Организация = ДанныеПервичныхДокументов.Организация)
						|			И (Оплаты.ДокументОплаты = ДанныеПервичныхДокументов.Документ)
						|ГДЕ
						|	(ПриобретенияЭлектронныхУслуг.СчетФактура <> Оплаты.ДокументОплаты
						|			ИЛИ Оплаты.ДокументОплаты ЕСТЬ NULL)
						|
						|СГРУППИРОВАТЬ ПО
						|	ПриобретенияЭлектронныхУслуг.Организация,
						|	ПриобретенияЭлектронныхУслуг.СчетФактура
						|;
						|
						|////////////////////////////////////////////////////////////////////////////////
						|УНИЧТОЖИТЬ РасчетыСПоставщиками_ОснованияИДокументыОплаты
						|;
						|
						|////////////////////////////////////////////////////////////////////////////////
						|ВЫБРАТЬ
						|	ОстаткиПоНДС1.Организация КАК Организация,
						|	ОстаткиПоНДС1.СчетФактура КАК СчетФактура,
						|	ОстаткиПоНДС1.Поставщик КАК Поставщик,
						|	ОстаткиПоНДС1.ВидЦенности КАК ВидЦенности,
						|	ОстаткиПоНДС1.СтавкаНДС КАК СтавкаНДС,
						|	ОстаткиПоНДС1.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
						|	ОстаткиПоНДС1.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
						|	ОстаткиПоНДС1.РеализацияЭкспорт КАК РеализацияЭкспорт,
						|	ОстаткиПоНДС1.НаправлениеДеятельности КАК НаправлениеДеятельности,
						|	ОстаткиПоНДС1.НДСОстаток КАК НДСОстаток,
						|	ОстаткиПоНДС1.СрокБлокировки КАК СрокБлокировки,
						|	ОстаткиПоНДС1.ПравоВычетаДо КАК ПравоВычетаДо,
						|	ОстаткиПоНДС1.Заблокировано1 КАК Заблокировано1,
						|
						|	ВЫБОР
						|		КОГДА ОстаткиПоНДС1.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ЭлектронныеУслуги)
						|				И ЕСТЬNULL(ОплатаЭлектронныхУслуг.СуммаОплаты, 0) = 0
						|			ТОГДА ОстаткиПоНДС1.НДСОстаток
						|		КОГДА ОстаткиПоНДС1.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ЭлектронныеУслуги)
						|				И ЕСТЬNULL(ОплатаЭлектронныхУслуг.СуммаОплаты, 0) <> 0
						|			ТОГДА ВЫБОР
						|					КОГДА (ВЫРАЗИТЬ(ОплатаЭлектронныхУслуг.НДСПриобретения 
						|										* ((ОплатаЭлектронныхУслуг.СуммаПриобретения - ОплатаЭлектронныхУслуг.СуммаОплаты) 
						|											/ ОплатаЭлектронныхУслуг.СуммаПриобретения) 
						|							 КАК ЧИСЛО(31, 2))) <= ОстаткиПоНДС1.НДСОстаток - ОстаткиПоНДС1.Заблокировано1
						|						ТОГДА ВЫРАЗИТЬ(ОплатаЭлектронныхУслуг.НДСПриобретения 
						|										* ((ОплатаЭлектронныхУслуг.СуммаПриобретения - ОплатаЭлектронныхУслуг.СуммаОплаты) 
						|											/ ОплатаЭлектронныхУслуг.СуммаПриобретения) 
						|								КАК ЧИСЛО(31, 2))
						|					ИНАЧЕ ОстаткиПоНДС1.НДСОстаток
						|				КОНЕЦ
						|		ИНАЧЕ 0
						|	КОНЕЦ КАК Заблокировано2
						|
						|ПОМЕСТИТЬ ОстаткиПоНДС2
						|ИЗ
						|	ОстаткиПоНДС1 КАК ОстаткиПоНДС1
						|		ЛЕВОЕ СОЕДИНЕНИЕ ОплатаЭлектронныхУслуг КАК ОплатаЭлектронныхУслуг
						|		ПО ОстаткиПоНДС1.Организация = ОплатаЭлектронныхУслуг.Организация
						|			И ОстаткиПоНДС1.СчетФактура = ОплатаЭлектронныхУслуг.СчетФактура
						|
						|ИНДЕКСИРОВАТЬ ПО
						|	Организация,
						|	СчетФактура
						|;
						|
						|////////////////////////////////////////////////////////////////////////////////
						|УНИЧТОЖИТЬ ОстаткиПоНДС1
						|;
						|
						|////////////////////////////////////////////////////////////////////////////////
						|УНИЧТОЖИТЬ ОплатаЭлектронныхУслуг
						|;
						|
						|////////////////////////////////////////////////////////////////////////////////
						|УНИЧТОЖИТЬ Задания
						|;
						|
						|////////////////////////////////////////////////////////////////////////////////
						|УНИЧТОЖИТЬ НДСПриПриобретенииЭлектронныхУслуг
						|;
						|
						|////////////////////////////////////////////////////////////////////////////////
						|УНИЧТОЖИТЬ ПриобретенияЭлектронныхУслуг";
		
		Запрос.Выполнить();
		
	Иначе
		// Электронных услуг нет.
		Запрос.Текст = "ВЫБРАТЬ
						|	ОстаткиПоНДС1.Организация КАК Организация,
						|	ОстаткиПоНДС1.СчетФактура КАК СчетФактура,
						|	ОстаткиПоНДС1.Поставщик КАК Поставщик,
						|	ОстаткиПоНДС1.ВидЦенности КАК ВидЦенности,
						|	ОстаткиПоНДС1.СтавкаНДС КАК СтавкаНДС,
						|	ОстаткиПоНДС1.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
						|	ОстаткиПоНДС1.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
						|	ОстаткиПоНДС1.РеализацияЭкспорт КАК РеализацияЭкспорт,
						|	ОстаткиПоНДС1.НаправлениеДеятельности КАК НаправлениеДеятельности,
						|	ОстаткиПоНДС1.НДСОстаток КАК НДСОстаток,
						|	ОстаткиПоНДС1.СрокБлокировки КАК СрокБлокировки,
						|	ОстаткиПоНДС1.ПравоВычетаДо КАК ПравоВычетаДо,
						|	ОстаткиПоНДС1.Заблокировано1 КАК Заблокировано1,
						|	0 КАК Заблокировано2
						|ПОМЕСТИТЬ ОстаткиПоНДС2
						|ИЗ
						|	ОстаткиПоНДС1 КАК ОстаткиПоНДС1
						|
						|ИНДЕКСИРОВАТЬ ПО
						|	Организация,
						|	СчетФактура
						|;
						|
						|////////////////////////////////////////////////////////////////////////////////
						|УНИЧТОЖИТЬ ОстаткиПоНДС1
						|;
						|
						|////////////////////////////////////////////////////////////////////////////////
						|УНИЧТОЖИТЬ Задания";

		Запрос.Выполнить();
	КонецЕсли;
	#КонецОбласти

	#Область ОплатаНДСВБюджет_3_и_ЗаявленияОВвозе_4
	// Нет оплаты НДС в бюджет (3). 
	// Нет отметки налогового органа по заявлениям о ввозе (4).
	Запрос.Текст = "ВЫБРАТЬ
					|	ЗаявленияОВвозеДанныеПоОплате.СчетФактура КАК СчетФактура,
					|	СУММА(ЗаявленияОВвозеДанныеПоОплате.Оплачено) КАК Оплачено,
					|	СУММА(ЗаявленияОВвозеДанныеПоОплате.ОжидаетОплаты) КАК ОжидаетОплаты
					|ПОМЕСТИТЬ ЗаявленияОВвозеОплата
					|ИЗ
					|	(ВЫБРАТЬ
					|		СписокСФ.СчетФактура КАК СчетФактура,
					|		СУММА(ЕСТЬNULL(ПодтверждениеОплатыНДСВБюджетОплачено.Сумма, 0)) КАК Оплачено,
					|		СУММА(0) КАК ОжидаетОплаты
					|	ИЗ
					|		СписокСФ КАК СписокСФ
					|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодтверждениеОплатыНДСВБюджет КАК ПодтверждениеОплатыНДСВБюджетОплачено
					|			ПО СписокСФ.СчетФактура = ПодтверждениеОплатыНДСВБюджетОплачено.СчетФактура
					|				И ПодтверждениеОплатыНДСВБюджетОплачено.ДатаДокументаПеречисленияНалога <= &КонецПериода
					|				И ПодтверждениеОплатыНДСВБюджетОплачено.Состояние = ЗНАЧЕНИЕ(перечисление.СостоянияОплатыНДСВБюджет.Оплачено)
					|	ГДЕ
					|		СписокСФ.СчетФактура ССЫЛКА Документ.ЗаявлениеОВвозеТоваров
					|	
					|	СГРУППИРОВАТЬ ПО
					|		СписокСФ.СчетФактура
					|	
					|	ОБЪЕДИНИТЬ ВСЕ
					|	
					|	ВЫБРАТЬ
					|		СписокСФ.СчетФактура,
					|		СУММА(0),
					|		СУММА(ЕСТЬNULL(ПодтверждениеОплатыНДСВБюджетОжидаетОплаты.Сумма, 0))
					|	ИЗ
					|		СписокСФ КАК СписокСФ
					|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодтверждениеОплатыНДСВБюджет КАК ПодтверждениеОплатыНДСВБюджетОжидаетОплаты
					|			ПО СписокСФ.СчетФактура = ПодтверждениеОплатыНДСВБюджетОжидаетОплаты.СчетФактура
					|				И (ПодтверждениеОплатыНДСВБюджетОжидаетОплаты.Состояние = ЗНАЧЕНИЕ(перечисление.СостоянияОплатыНДСВБюджет.ОжидаетОплаты))
					|	ГДЕ
					|		СписокСФ.СчетФактура ССЫЛКА Документ.ЗаявлениеОВвозеТоваров
					|	
					|	СГРУППИРОВАТЬ ПО
					|		СписокСФ.СчетФактура) КАК ЗаявленияОВвозеДанныеПоОплате
					|
					|СГРУППИРОВАТЬ ПО
					|	ЗаявленияОВвозеДанныеПоОплате.СчетФактура
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	СписокСФ.СчетФактура КАК СчетФактура,
					|	ВЫБОР
					|		КОГДА ПодтверждениеОплатыНДСВБюджетПодтверждение.СчетФактура ЕСТЬ NULL
					|			ТОГДА ЛОЖЬ
					|		ИНАЧЕ ИСТИНА
					|	КОНЕЦ КАК ЕстьПодтверждение
					|ПОМЕСТИТЬ ЗаявленияОВвозеПодтверждения
					|ИЗ
					|	СписокСФ КАК СписокСФ
					|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодтверждениеОплатыНДСВБюджет КАК ПодтверждениеОплатыНДСВБюджетПодтверждение
					|		ПО СписокСФ.СчетФактура = ПодтверждениеОплатыНДСВБюджетПодтверждение.СчетФактура
					|			И (ПодтверждениеОплатыНДСВБюджетПодтверждение.Состояние = ЗНАЧЕНИЕ(перечисление.СостоянияОплатыНДСВБюджет.ПолученоПодтверждение))
					|ГДЕ
					|	СписокСФ.СчетФактура ССЫЛКА Документ.ЗаявлениеОВвозеТоваров
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	ЕСТЬNULL(ЗаявленияОВвозеОплата.СчетФактура, ЗаявленияОВвозеПодтверждения.СчетФактура) КАК СчетФактура,
					|	ЕСТЬNULL(ЗаявленияОВвозеОплата.Оплачено, 0) КАК Оплачено,
					|	ЕСТЬNULL(ЗаявленияОВвозеОплата.ОжидаетОплаты, 0) КАК ОжидаетОплаты,
					|	ЕСТЬNULL(ЗаявленияОВвозеПодтверждения.ЕстьПодтверждение, ЛОЖЬ) КАК ЕстьПодтверждение
					|ПОМЕСТИТЬ СостояниеЗаявленийОВвозе
					|ИЗ
					|	ЗаявленияОВвозеОплата КАК ЗаявленияОВвозеОплата
					|		ПОЛНОЕ СОЕДИНЕНИЕ ЗаявленияОВвозеПодтверждения КАК ЗаявленияОВвозеПодтверждения
					|		ПО ЗаявленияОВвозеОплата.СчетФактура = ЗаявленияОВвозеПодтверждения.СчетФактура
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	СписокСФ.СчетФактура КАК СчетФактура,
					|	ВЫБОР
					|		КОГДА ПодтверждениеОплатыНДСВБюджет.СчетФактура ЕСТЬ NULL
					|			ТОГДА ЛОЖЬ
					|		ИНАЧЕ ИСТИНА
					|	КОНЕЦ КАК ЕстьПодтверждение
					|ПОМЕСТИТЬ ПодтвержденияПоСФНАиТДИ
					|ИЗ
					|	СписокСФ КАК СписокСФ
					|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодтверждениеОплатыНДСВБюджет КАК ПодтверждениеОплатыНДСВБюджет
					|		ПО СписокСФ.СчетФактура = ПодтверждениеОплатыНДСВБюджет.СчетФактура
					|			И (ПодтверждениеОплатыНДСВБюджет.Состояние = ЗНАЧЕНИЕ(перечисление.СостоянияОплатыНДСВБюджет.ПолученоПодтверждение))
					|ГДЕ
					|	ТИПЗНАЧЕНИЯ(СписокСФ.СчетФактура) В (ТИП(Документ.ТаможеннаяДекларацияИмпорт), ТИП(Документ.СчетФактураНалоговыйАгент))
					|
					|ИНДЕКСИРОВАТЬ ПО
					|	СчетФактура
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	ОстаткиПоНДС2.Организация КАК Организация,
					|	ОстаткиПоНДС2.Поставщик КАК Поставщик,
					|	ОстаткиПоНДС2.СчетФактура КАК СчетФактура,
					|	ОстаткиПоНДС2.НаправлениеДеятельности КАК НаправлениеДеятельности,
					|	ОстаткиПоНДС2.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
					|	ОстаткиПоНДС2.ВидЦенности КАК ВидЦенности,
					|	ОстаткиПоНДС2.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
					|	ОстаткиПоНДС2.РеализацияЭкспорт КАК РеализацияЭкспорт,
					|	ОстаткиПоНДС2.СтавкаНДС КАК СтавкаНДС,
					|	ОстаткиПоНДС2.НДСОстаток КАК НДСОстаток,
					|	ОстаткиПоНДС2.Заблокировано1 КАК Заблокировано1,
					|	ОстаткиПоНДС2.СрокБлокировки КАК СрокБлокировки,
					|	ОстаткиПоНДС2.ПравоВычетаДо КАК ПравоВычетаДо,
					|	ОстаткиПоНДС2.Заблокировано2 КАК Заблокировано2,
					|	ВЫБОР
					|		КОГДА ОстаткиПоНДС2.СчетФактура ССЫЛКА Документ.ЗаявлениеОВвозеТоваров
					|			ТОГДА ВЫБОР
					|					КОГДА СостояниеЗаявленийОВвозе.ЕстьПодтверждение = ЛОЖЬ
					|							И СостояниеЗаявленийОВвозе.Оплачено < СостояниеЗаявленийОВвозе.ОжидаетОплаты
					|						ТОГДА ОстаткиПоНДС2.НДСОстаток - ОстаткиПоНДС2.Заблокировано1 - ОстаткиПоНДС2.Заблокировано2
					|					ИНАЧЕ 0
					|				КОНЕЦ
					|		КОГДА ОстаткиПоНДС2.СчетФактура ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
					|				ИЛИ ОстаткиПоНДС2.СчетФактура ССЫЛКА Документ.СчетФактураНалоговыйАгент
					|			ТОГДА ВЫБОР
					|					КОГДА ПодтвержденияПоСФНАиТДИ.ЕстьПодтверждение
					|						ТОГДА 0
					|					ИНАЧЕ ОстаткиПоНДС2.НДСОстаток - ОстаткиПоНДС2.Заблокировано1 - ОстаткиПоНДС2.Заблокировано2
					|				КОНЕЦ
					|		ИНАЧЕ 0
					|	КОНЕЦ КАК Заблокировано3,
					|	ВЫБОР
					|		КОГДА ОстаткиПоНДС2.СчетФактура ССЫЛКА Документ.ЗаявлениеОВвозеТоваров
					|			ТОГДА ВЫБОР
					|					КОГДА СостояниеЗаявленийОВвозе.ЕстьПодтверждение = ЛОЖЬ
					|							И СостояниеЗаявленийОВвозе.Оплачено >= СостояниеЗаявленийОВвозе.ОжидаетОплаты
					|						ТОГДА ОстаткиПоНДС2.НДСОстаток - ОстаткиПоНДС2.Заблокировано1 - ОстаткиПоНДС2.Заблокировано2
					|					ИНАЧЕ 0
					|				КОНЕЦ
					|		ИНАЧЕ 0
					|	КОНЕЦ КАК Заблокировано4
					|ПОМЕСТИТЬ ОстаткиПоНДС4
					|ИЗ
					|	ОстаткиПоНДС2 КАК ОстаткиПоНДС2
					|		ЛЕВОЕ СОЕДИНЕНИЕ СостояниеЗаявленийОВвозе КАК СостояниеЗаявленийОВвозе
					|		ПО ОстаткиПоНДС2.СчетФактура = СостояниеЗаявленийОВвозе.СчетФактура
					|		ЛЕВОЕ СОЕДИНЕНИЕ ПодтвержденияПоСФНАиТДИ КАК ПодтвержденияПоСФНАиТДИ
					|		ПО ОстаткиПоНДС2.СчетФактура = ПодтвержденияПоСФНАиТДИ.СчетФактура
					|
					|ИНДЕКСИРОВАТЬ ПО
					|	СчетФактура
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ ОстаткиПоНДС2
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ ЗаявленияОВвозеОплата
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ ЗаявленияОВвозеПодтверждения
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ СостояниеЗаявленийОВвозе
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ ПодтвержденияПоСФНАиТДИ";
	
	Запрос.Выполнить();
	#КонецОбласти
	
	#Область СФНалоговыйАгент_5
	// Нет поступлений ценностей по счету-фактуре налогового агента (5).
	Запрос.Текст = "ВЫБРАТЬ
					|	СписокСФ.СчетФактура КАК СчетФактура,
					|	СУММА(НДСПредъявленный.НДС) КАК НДС
					|ПОМЕСТИТЬ НДСПоСФНА
					|ИЗ
					|	РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
					|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокСФ КАК СписокСФ
					|		ПО НДСПредъявленный.Регистратор = СписокСФ.СчетФактура
					|			И (СписокСФ.СчетФактура ССЫЛКА Документ.СчетФактураНалоговыйАгент)
					|			И (НДСПредъявленный.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
					|			И (НДСПредъявленный.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка))
					|
					|СГРУППИРОВАТЬ ПО
					|	СписокСФ.СчетФактура
					|
					|ИНДЕКСИРОВАТЬ ПО
					|	СчетФактура
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	СписокСФ.СчетФактура КАК СчетФактура,
					|	СУММА(НДСПредъявленный.НДС) КАК НДС
					|ПОМЕСТИТЬ РасходНДСПоСФНАНеРегламентнаяОперация
					|ИЗ
					|	РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
					|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокСФ КАК СписокСФ
					|		ПО НДСПредъявленный.Регистратор = СписокСФ.СчетФактура
					|			И (СписокСФ.СчетФактура ССЫЛКА Документ.СчетФактураНалоговыйАгент)
					|			И (НДСПредъявленный.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
					|			И (НДСПредъявленный.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка))
					|			И (НДСПредъявленный.РегламентнаяОперация = ЛОЖЬ)
					|
					|СГРУППИРОВАТЬ ПО
					|	СписокСФ.СчетФактура
					|
					|ИНДЕКСИРОВАТЬ ПО
					|	СчетФактура
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	СписокСФ.СчетФактура КАК СчетФактура,
					|	СУММА(ПартииПрочихРасходов.НДСРегл) КАК НДСРегл
					|ПОМЕСТИТЬ НДСПоПартиямПрочихРасходов
					|ИЗ
					|	РегистрНакопления.ПартииПрочихРасходов КАК ПартииПрочихРасходов
					|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокСФ КАК СписокСФ
					|		ПО ПартииПрочихРасходов.Регистратор = СписокСФ.СчетФактура
					|			И (СписокСФ.СчетФактура ССЫЛКА Документ.СчетФактураНалоговыйАгент)
					|			И (ПартииПрочихРасходов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
					|
					|СГРУППИРОВАТЬ ПО
					|	СписокСФ.СчетФактура
					|
					|ИНДЕКСИРОВАТЬ ПО
					|	СчетФактура
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	НДСПоСФНА.СчетФактура КАК СчетФактура,
					|
					|	НДСПоСФНА.НДС - ЕСТЬNULL(РасходНДСПоСФНАНеРегламентнаяОперация.НДС, 0) 
					|		- ЕСТЬNULL(НДСПоПартиямПрочихРасходов.НДСРегл, 0) КАК ЗаблокированоНДС
					|
					|ПОМЕСТИТЬ НетЦенностейПоСФНА
					|ИЗ
					|	НДСПоСФНА КАК НДСПоСФНА
					|		ЛЕВОЕ СОЕДИНЕНИЕ РасходНДСПоСФНАНеРегламентнаяОперация КАК РасходНДСПоСФНАНеРегламентнаяОперация
					|		ПО НДСПоСФНА.СчетФактура = РасходНДСПоСФНАНеРегламентнаяОперация.СчетФактура
					|		ЛЕВОЕ СОЕДИНЕНИЕ НДСПоПартиямПрочихРасходов КАК НДСПоПартиямПрочихРасходов
					|		ПО НДСПоСФНА.СчетФактура = НДСПоПартиямПрочихРасходов.СчетФактура
					|
					|ИНДЕКСИРОВАТЬ ПО
					|	СчетФактура
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	ОстаткиПоНДС4.Организация КАК Организация,
					|	ОстаткиПоНДС4.Поставщик КАК Поставщик,
					|	ОстаткиПоНДС4.СчетФактура КАК СчетФактура,
					|	ОстаткиПоНДС4.НаправлениеДеятельности КАК НаправлениеДеятельности,
					|	ОстаткиПоНДС4.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
					|	ОстаткиПоНДС4.ВидЦенности КАК ВидЦенности,
					|	ОстаткиПоНДС4.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
					|	ОстаткиПоНДС4.РеализацияЭкспорт КАК РеализацияЭкспорт,
					|	ОстаткиПоНДС4.СтавкаНДС КАК СтавкаНДС,
					|	ОстаткиПоНДС4.НДСОстаток КАК НДСОстаток,
					|	ОстаткиПоНДС4.Заблокировано1 КАК Заблокировано1,
					|	ОстаткиПоНДС4.СрокБлокировки КАК СрокБлокировки,
								|	ОстаткиПоНДС4.ПравоВычетаДо КАК ПравоВычетаДо,
					|	ОстаткиПоНДС4.Заблокировано2 КАК Заблокировано2,
					|	ОстаткиПоНДС4.Заблокировано3 КАК Заблокировано3,
					|	ОстаткиПоНДС4.Заблокировано4 КАК Заблокировано4,
					|	ВЫБОР
					|		КОГДА НетЦенностейПоСФНА.СчетФактура ЕСТЬ NULL
					|			ТОГДА 0
					|		ИНАЧЕ ВЫБОР
					|				КОГДА НетЦенностейПоСФНА.ЗаблокированоНДС >= ОстаткиПоНДС4.НДСОстаток 
					|															- ОстаткиПоНДС4.Заблокировано1 
					|															- ОстаткиПоНДС4.Заблокировано2 
					|															- ОстаткиПоНДС4.Заблокировано3 
					|															- ОстаткиПоНДС4.Заблокировано4
					|					ТОГДА ОстаткиПоНДС4.НДСОстаток 
					|							- ОстаткиПоНДС4.Заблокировано1 
					|							- ОстаткиПоНДС4.Заблокировано2 
					|							- ОстаткиПоНДС4.Заблокировано3 
					|							- ОстаткиПоНДС4.Заблокировано4
					|				ИНАЧЕ НетЦенностейПоСФНА.ЗаблокированоНДС
					|			КОНЕЦ
					|	КОНЕЦ КАК Заблокировано5
					|ПОМЕСТИТЬ ОстаткиПоНДС5
					|ИЗ
					|	ОстаткиПоНДС4 КАК ОстаткиПоНДС4
					|		ЛЕВОЕ СОЕДИНЕНИЕ НетЦенностейПоСФНА КАК НетЦенностейПоСФНА
					|		ПО ОстаткиПоНДС4.СчетФактура = НетЦенностейПоСФНА.СчетФактура
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ НДСПоСФНА
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ РасходНДСПоСФНАНеРегламентнаяОперация
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ НДСПоПартиямПрочихРасходов
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ НетЦенностейПоСФНА
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ ОстаткиПоНДС4
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|";
	Запрос.Выполнить();
	#КонецОбласти
	
	#Область НетСчетаФактуры_6
	// Нет счета-фактуры (6).
	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	СписокСФ.СчетФактура КАК СчетФактура
					|ИЗ
					|	СписокСФ КАК СписокСФ
					|ГДЕ
					|	НЕ СписокСФ.СчетФактура ССЫЛКА Документ.СчетФактураНалоговыйАгент
					|	И НЕ СписокСФ.СчетФактура ССЫЛКА Документ.СчетФактураПолученныйНалоговыйАгент
					|	И НЕ СписокСФ.СчетФактура ССЫЛКА Документ.ЗаявлениеОВвозеТоваров
					|	И НЕ СписокСФ.СчетФактура ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
					|	И НЕ СписокСФ.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ЭлектронныеУслуги)";
	
	ДокументыДляПоискаСчетовФактур = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("СчетФактура");
	
	ПараметрыПоискаСчетовФактур = УчетНДС.НовыйПараметрыПоискаСчетовФактур();
	
	ПараметрыПоискаСчетовФактур.КонецПериода        = КонецДня(Запрос.Параметры.КонецПериода);
	ПараметрыПоискаСчетовФактур.Организация         = Запрос.Параметры.Организация;
	ПараметрыПоискаСчетовФактур.Фильтр              = ДокументыДляПоискаСчетовФактур;
	// Искать документы, по которым счета-фактуры отсутствуют.
	ПараметрыПоискаСчетовФактур.НаличиеСчетаФактуры = Ложь;
	// Поиск осуществляется по остаткам регистра накопления НДСПредъявленный.
	ПараметрыПоискаСчетовФактур.ИскатьПоОборотам    = Ложь;
	
	

	ПоискСчетовФактур = УчетНДСПереопределяемый.ОпределитьНаличиеСчетовФактурПолученных(ПараметрыПоискаСчетовФактур);
	
	// Документы из списка ДокументыДляПоискаСчетовФактур, по которым нет счетов-фактур.
	ДокументыБезСчетовФактур = ПоискСчетовФактур.ВыгрузитьКолонку("ДокументОснование");
	
	Запрос.УстановитьПараметр("ДокументыБезСчетовФактур", ДокументыБезСчетовФактур);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	СписокСФ.СчетФактура КАК СчетФактура
					|ПОМЕСТИТЬ НетСчетовФактур
					|ИЗ
					|	СписокСФ КАК СписокСФ
					|ГДЕ
					|	СписокСФ.СчетФактура В(&ДокументыБезСчетовФактур)
					|
					|ИНДЕКСИРОВАТЬ ПО
					|	СчетФактура
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	ОстаткиПоНДС5.Организация КАК Организация,
					|	ОстаткиПоНДС5.Поставщик КАК Поставщик,
					|	ОстаткиПоНДС5.СчетФактура КАК СчетФактура,
					|	ОстаткиПоНДС5.НаправлениеДеятельности КАК НаправлениеДеятельности,
					|	ОстаткиПоНДС5.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
					|	ОстаткиПоНДС5.ВидЦенности КАК ВидЦенности,
					|	ОстаткиПоНДС5.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
					|	ОстаткиПоНДС5.РеализацияЭкспорт КАК РеализацияЭкспорт,
					|	ОстаткиПоНДС5.СтавкаНДС КАК СтавкаНДС,
					|	ОстаткиПоНДС5.НДСОстаток КАК НДСОстаток,
					|	ОстаткиПоНДС5.Заблокировано1 КАК Заблокировано1,
					|	ОстаткиПоНДС5.СрокБлокировки КАК СрокБлокировки,
								|	ОстаткиПоНДС5.ПравоВычетаДо КАК ПравоВычетаДо,
					|	ОстаткиПоНДС5.Заблокировано2 КАК Заблокировано2,
					|	ОстаткиПоНДС5.Заблокировано3 КАК Заблокировано3,
					|	ОстаткиПоНДС5.Заблокировано4 КАК Заблокировано4,
					|	ОстаткиПоНДС5.Заблокировано5 КАК Заблокировано5,
					|	ВЫБОР
					|		КОГДА НетСчетовФактур.СчетФактура = ОстаткиПоНДС5.СчетФактура
					|			ТОГДА ОстаткиПоНДС5.НДСОстаток 
					|					- ОстаткиПоНДС5.Заблокировано1 
					|					- ОстаткиПоНДС5.Заблокировано2 
					|					- ОстаткиПоНДС5.Заблокировано3 
					|					- ОстаткиПоНДС5.Заблокировано4 
					|					- ОстаткиПоНДС5.Заблокировано5
					|		ИНАЧЕ 0
					|	КОНЕЦ КАК Заблокировано6
					|ПОМЕСТИТЬ ОстаткиПоНДС6
					|ИЗ
					|	ОстаткиПоНДС5 КАК ОстаткиПоНДС5
					|		ЛЕВОЕ СОЕДИНЕНИЕ НетСчетовФактур КАК НетСчетовФактур
					|		ПО ОстаткиПоНДС5.СчетФактура = НетСчетовФактур.СчетФактура
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ ОстаткиПоНДС5
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|//УНИЧТОЖИТЬ НетСчетовФактур";
	Запрос.Выполнить();
	#КонецОбласти
	
	#Область Экспорт_7
	
	// Экспорт (7).
	Запрос.Текст = "ВЫБРАТЬ
					|	ОстаткиПоНДС6.Организация КАК Организация,
					|	ОстаткиПоНДС6.Поставщик КАК Поставщик,
					|	ОстаткиПоНДС6.СчетФактура КАК СчетФактура,
					|	ОстаткиПоНДС6.НаправлениеДеятельности КАК НаправлениеДеятельности,
					|	ОстаткиПоНДС6.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
					|	ОстаткиПоНДС6.ВидЦенности КАК ВидЦенности,
					|	ОстаткиПоНДС6.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
					|	ОстаткиПоНДС6.РеализацияЭкспорт КАК РеализацияЭкспорт,
					|	ОстаткиПоНДС6.СтавкаНДС КАК СтавкаНДС,
					|	ОстаткиПоНДС6.НДСОстаток КАК НДСОстаток,
					|	ОстаткиПоНДС6.Заблокировано1 КАК Заблокировано1,
					|	ОстаткиПоНДС6.СрокБлокировки КАК СрокБлокировки,
					|	ОстаткиПоНДС6.ПравоВычетаДо КАК ПравоВычетаДо,
					|	ОстаткиПоНДС6.Заблокировано2 КАК Заблокировано2,
					|	ОстаткиПоНДС6.Заблокировано3 КАК Заблокировано3,
					|	ОстаткиПоНДС6.Заблокировано4 КАК Заблокировано4,
					|	ОстаткиПоНДС6.Заблокировано5 КАК Заблокировано5,
					|	ОстаткиПоНДС6.Заблокировано6 КАК Заблокировано6,
					|	 ВЫБОР
					|				КОГДА ОстаткиПоНДС6.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг)
					|					ТОГДА ОстаткиПоНДС6.НДСОстаток 
					|							- ОстаткиПоНДС6.Заблокировано1 
					|							- ОстаткиПоНДС6.Заблокировано2 
					|							- ОстаткиПоНДС6.Заблокировано3 
					|							- ОстаткиПоНДС6.Заблокировано4 
					|							- ОстаткиПоНДС6.Заблокировано5 
					|							- ОстаткиПоНДС6.Заблокировано6
					|				ИНАЧЕ 0
					|			КОНЕЦ КАК Заблокировано7
					|ПОМЕСТИТЬ ОстаткиПоНДС7
					|
					|ИЗ
					|	ОстаткиПоНДС6 КАК ОстаткиПоНДС6
					|
					|ИНДЕКСИРОВАТЬ ПО
					|	СчетФактура
					|;
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ ОстаткиПоНДС6";
	Запрос.Выполнить();
	#КонецОбласти
	
	#Область ОжидаетРаспределенияВКонцеКвартала_8
	// Ожидает распределения НДС в конце квартала (8).
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
					|	Организации.ГоловнаяОрганизация КАК ГоловнаяОрганизация
					|ПОМЕСТИТЬ ДляУчетнойПолитики
					|ИЗ
					|	Справочник.Организации КАК Организации
					|ГДЕ
					|	Организации.Ссылка = &Организация
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	НастройкиУчетаНДССрезПоследних.ПериодичностьФормированияВычетовИВосстановленийНДС КАК ПериодичностьФормированияВычетовИВосстановленийНДС
					|ПОМЕСТИТЬ ПериодичностьНДС
					|ИЗ
					|	РегистрСведений.НастройкиУчетаНДС.СрезПоследних(
					|			&КонецПериода,
					|			Организация В
					|				(ВЫБРАТЬ
					|					ДУП.ГоловнаяОрганизация
					|				ИЗ
					|					ДляУчетнойПолитики КАК ДУП)) КАК НастройкиУчетаНДССрезПоследних
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	ОстаткиПоНДС7.Организация КАК Организация,
					|	ОстаткиПоНДС7.Поставщик КАК Поставщик,
					|	ОстаткиПоНДС7.СчетФактура КАК СчетФактура,
					|	ОстаткиПоНДС7.НаправлениеДеятельности КАК НаправлениеДеятельности,
					|	ОстаткиПоНДС7.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
					|	ОстаткиПоНДС7.ВидЦенности КАК ВидЦенности,
					|	ОстаткиПоНДС7.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
					|	ОстаткиПоНДС7.РеализацияЭкспорт КАК РеализацияЭкспорт,
					|	ОстаткиПоНДС7.СтавкаНДС КАК СтавкаНДС,
					|	ОстаткиПоНДС7.НДСОстаток КАК НДСОстаток,
					|	ОстаткиПоНДС7.Заблокировано1 КАК Заблокировано1,
					|	ОстаткиПоНДС7.СрокБлокировки КАК СрокБлокировки,
					|	ОстаткиПоНДС7.ПравоВычетаДо КАК ПравоВычетаДо,
					|	ОстаткиПоНДС7.Заблокировано2 КАК Заблокировано2,
					|	ОстаткиПоНДС7.Заблокировано3 КАК Заблокировано3,
					|	ОстаткиПоНДС7.Заблокировано4 КАК Заблокировано4,
					|	ОстаткиПоНДС7.Заблокировано5 КАК Заблокировано5,
					|	ОстаткиПоНДС7.Заблокировано6 КАК Заблокировано6,
					|	ОстаткиПоНДС7.Заблокировано7 КАК Заблокировано7,
					|	ВЫБОР
					|		КОГДА ОстаткиПоНДС7.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
					|			ТОГДА ВЫБОР
					|					КОГДА ПериодичностьНДС.ПериодичностьФормированияВычетовИВосстановленийНДС = ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
					|						ТОГДА ВЫБОР
					|								КОГДА КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ) <> КОНЕЦПЕРИОДА(&КонецПериода, КВАРТАЛ)
					|									ТОГДА ОстаткиПоНДС7.НДСОстаток 
					|											- ОстаткиПоНДС7.Заблокировано1 
					|											- ОстаткиПоНДС7.Заблокировано2 
					|											- ОстаткиПоНДС7.Заблокировано3 
					|											- ОстаткиПоНДС7.Заблокировано4 
					|											- ОстаткиПоНДС7.Заблокировано5 
					|											- ОстаткиПоНДС7.Заблокировано6 
					|											- ОстаткиПоНДС7.Заблокировано7
					|								ИНАЧЕ 0
					|							КОНЕЦ
					|					ИНАЧЕ 0
					|				КОНЕЦ
					|		ИНАЧЕ 0
					|	КОНЕЦ КАК Заблокировано8
					|ПОМЕСТИТЬ ОстаткиПоНДС8
					|ИЗ
					|	ОстаткиПоНДС7 КАК ОстаткиПоНДС7,
					|	ПериодичностьНДС КАК ПериодичностьНДС
					|
					|ИНДЕКСИРОВАТЬ ПО
					|	СчетФактура;
					|УНИЧТОЖИТЬ ОстаткиПоНДС7";
	Запрос.Выполнить();
	#КонецОбласти
	
	#Область ФактическоеИспользование_9
	// Отнесен на вид деятельности "по фактическому использованию" (9).
	
	Запрос.Текст = "ВЫБРАТЬ
					|	ОстаткиПоНДС8.Организация КАК Организация,
					|	ОстаткиПоНДС8.Поставщик КАК Поставщик,
					|	ОстаткиПоНДС8.СчетФактура КАК СчетФактура,
					|	ОстаткиПоНДС8.НаправлениеДеятельности КАК НаправлениеДеятельности,
					|	ОстаткиПоНДС8.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
					|	ОстаткиПоНДС8.ВидЦенности КАК ВидЦенности,
					|	ОстаткиПоНДС8.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
					|	ОстаткиПоНДС8.РеализацияЭкспорт КАК РеализацияЭкспорт,
					|	ОстаткиПоНДС8.СтавкаНДС КАК СтавкаНДС,
					|	ОстаткиПоНДС8.НДСОстаток КАК НДСОстаток,
					|	ОстаткиПоНДС8.Заблокировано1 КАК Заблокировано1,
					|	ОстаткиПоНДС8.СрокБлокировки КАК СрокБлокировки,
					|	ОстаткиПоНДС8.ПравоВычетаДо КАК ПравоВычетаДо,
					|	ОстаткиПоНДС8.Заблокировано2 КАК Заблокировано2,
					|	ОстаткиПоНДС8.Заблокировано3 КАК Заблокировано3,
					|	ОстаткиПоНДС8.Заблокировано4 КАК Заблокировано4,
					|	ОстаткиПоНДС8.Заблокировано5 КАК Заблокировано5,
					|	ОстаткиПоНДС8.Заблокировано6 КАК Заблокировано6,
					|	ОстаткиПоНДС8.Заблокировано7 КАК Заблокировано7,
					|	ОстаткиПоНДС8.Заблокировано8 КАК Заблокировано8,
					|	ВЫБОР
					|		КОГДА ОстаткиПоНДС8.ВидДеятельностиНДС = Значение(Перечисление.ТипыНалогообложенияНДС.ПоФактическомуИспользованию)
					|			ТОГДА ОстаткиПоНДС8.НДСОстаток
					|					- ОстаткиПоНДС8.Заблокировано1 
					|					- ОстаткиПоНДС8.Заблокировано2
					|					- ОстаткиПоНДС8.Заблокировано3
					|					- ОстаткиПоНДС8.Заблокировано4
					|					- ОстаткиПоНДС8.Заблокировано5
					|					- ОстаткиПоНДС8.Заблокировано6
					|					- ОстаткиПоНДС8.Заблокировано7
					|					- ОстаткиПоНДС8.Заблокировано8
					|		ИНАЧЕ 0
					|	КОНЕЦ КАК Заблокировано9
					|ПОМЕСТИТЬ ОстаткиПоНДС9
					|ИЗ
					|	ОстаткиПоНДС8 КАК ОстаткиПоНДС8";
	Запрос.Выполнить();
	#КонецОбласти

	#Область ПроизводствоСДлительнымЦиклом_10
	// Производство с длительным циклом (10)
	Запрос.Текст = "ВЫБРАТЬ
					|	ОстаткиПоНДС9.Организация КАК Организация,
					|	ОстаткиПоНДС9.Поставщик КАК Поставщик,
					|	ОстаткиПоНДС9.СчетФактура КАК СчетФактура,
					|	ОстаткиПоНДС9.НаправлениеДеятельности КАК НаправлениеДеятельности,
					|	ОстаткиПоНДС9.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
					|	ОстаткиПоНДС9.ВидЦенности КАК ВидЦенности,
					|	ОстаткиПоНДС9.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
					|	ОстаткиПоНДС9.РеализацияЭкспорт КАК РеализацияЭкспорт,
					|	ОстаткиПоНДС9.СтавкаНДС КАК СтавкаНДС,
					|	ОстаткиПоНДС9.НДСОстаток КАК НДСОстаток,
					|	ОстаткиПоНДС9.Заблокировано1 КАК Заблокировано1,
					|	ОстаткиПоНДС9.СрокБлокировки КАК СрокБлокировки,
					|	ОстаткиПоНДС9.ПравоВычетаДо КАК ПравоВычетаДо,
					|	ОстаткиПоНДС9.Заблокировано2 КАК Заблокировано2,
					|	ОстаткиПоНДС9.Заблокировано3 КАК Заблокировано3,
					|	ОстаткиПоНДС9.Заблокировано4 КАК Заблокировано4,
					|	ОстаткиПоНДС9.Заблокировано5 КАК Заблокировано5,
					|	ОстаткиПоНДС9.Заблокировано6 КАК Заблокировано6,
					|	ОстаткиПоНДС9.Заблокировано7 КАК Заблокировано7,
					|	ОстаткиПоНДС9.Заблокировано8 КАК Заблокировано8,
					|	ОстаткиПоНДС9.Заблокировано9 КАК Заблокировано9,
					|	ВЫБОР
					|		КОГДА ОстаткиПоНДС9.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПроизводствоСДЦ)
					|			ТОГДА ОстаткиПоНДС9.НДСОстаток 
					|					- ОстаткиПоНДС9.Заблокировано1 
					|					- ОстаткиПоНДС9.Заблокировано2 
					|					- ОстаткиПоНДС9.Заблокировано3 
					|					- ОстаткиПоНДС9.Заблокировано4 
					|					- ОстаткиПоНДС9.Заблокировано5 
					|					- ОстаткиПоНДС9.Заблокировано6 
					|					- ОстаткиПоНДС9.Заблокировано7 
					|					- ОстаткиПоНДС9.Заблокировано8 
					|					- ОстаткиПоНДС9.Заблокировано9
					|		ИНАЧЕ 0
					|	КОНЕЦ КАК Заблокировано10
					|ПОМЕСТИТЬ ОстаткиПоНДС10
					|ИЗ
					|	ОстаткиПоНДС9 КАК ОстаткиПоНДС9
					|
					|ИНДЕКСИРОВАТЬ ПО
					|	СчетФактура";
	Запрос.Выполнить();
	#КонецОбласти
	
	#Область Космос
	// Космос
	Запрос.Текст = "ВЫБРАТЬ
					|	ОстаткиПоНДС10.Организация КАК Организация,
					|	ОстаткиПоНДС10.Поставщик КАК Поставщик,
					|	ОстаткиПоНДС10.СчетФактура КАК СчетФактура,
					|	ОстаткиПоНДС10.НаправлениеДеятельности КАК НаправлениеДеятельности,
					|	ОстаткиПоНДС10.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
					|	ОстаткиПоНДС10.ВидЦенности КАК ВидЦенности,
					|	ОстаткиПоНДС10.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
					|	ОстаткиПоНДС10.РеализацияЭкспорт КАК РеализацияЭкспорт,
					|	ОстаткиПоНДС10.СтавкаНДС КАК СтавкаНДС,
					|	ОстаткиПоНДС10.НДСОстаток КАК НДСОстаток,
					|	ОстаткиПоНДС10.Заблокировано1 КАК Заблокировано1,
					|	ОстаткиПоНДС10.СрокБлокировки КАК СрокБлокировки,
					|	ОстаткиПоНДС10.ПравоВычетаДо КАК ПравоВычетаДо,
					|	ОстаткиПоНДС10.Заблокировано2 КАК Заблокировано2,
					|	ОстаткиПоНДС10.Заблокировано3 КАК Заблокировано3,
					|	ОстаткиПоНДС10.Заблокировано4 КАК Заблокировано4,
					|	ОстаткиПоНДС10.Заблокировано5 КАК Заблокировано5,
					|	ОстаткиПоНДС10.Заблокировано6 КАК Заблокировано6,
					|	ОстаткиПоНДС10.Заблокировано7 КАК Заблокировано7,
					|	ОстаткиПоНДС10.Заблокировано8 КАК Заблокировано8,
					|	ОстаткиПоНДС10.Заблокировано9 КАК Заблокировано9,
					|	ОстаткиПоНДС10.Заблокировано10 КАК Заблокировано10,
					|	ВЫБОР
					|		КОГДА ОстаткиПоНДС10.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.Космос)
					|			ТОГДА ОстаткиПоНДС10.НДСОстаток 
					|					- ОстаткиПоНДС10.Заблокировано1 
					|					- ОстаткиПоНДС10.Заблокировано2 
					|					- ОстаткиПоНДС10.Заблокировано3 
					|					- ОстаткиПоНДС10.Заблокировано4 
					|					- ОстаткиПоНДС10.Заблокировано5 
					|					- ОстаткиПоНДС10.Заблокировано6 
					|					- ОстаткиПоНДС10.Заблокировано7 
					|					- ОстаткиПоНДС10.Заблокировано8 
					|					- ОстаткиПоНДС10.Заблокировано9
					|					- ОстаткиПоНДС10.Заблокировано10
					|		ИНАЧЕ 0
					|	КОНЕЦ КАК Заблокировано11
					|ПОМЕСТИТЬ ОстаткиПоНДС11
					|ИЗ
					|	ОстаткиПоНДС10 КАК ОстаткиПоНДС10
					|
					|ИНДЕКСИРОВАТЬ ПО
					|	СчетФактура";
	Запрос.Выполнить();
	#КонецОбласти

	#Область ОтложенДоКонцаКвартала_и_Доступно_0
	// Отложен до конца квартала по учетной политике
	Запрос.Текст = "ВЫБРАТЬ
					|	ОстаткиПоНДС11.Организация КАК Организация,
					|	ОстаткиПоНДС11.Поставщик КАК Поставщик,
					|	ОстаткиПоНДС11.СчетФактура КАК СчетФактура,
					|	ОстаткиПоНДС11.НаправлениеДеятельности КАК НаправлениеДеятельности,
					|	ОстаткиПоНДС11.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
					|	ОстаткиПоНДС11.ВидЦенности КАК ВидЦенности,
					|	ОстаткиПоНДС11.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
					|	ОстаткиПоНДС11.РеализацияЭкспорт КАК РеализацияЭкспорт,
					|	ОстаткиПоНДС11.СтавкаНДС КАК СтавкаНДС,
					|	ОстаткиПоНДС11.НДСОстаток КАК НДСОстаток,
					|	ОстаткиПоНДС11.Заблокировано1 КАК Заблокировано1,
					|	ОстаткиПоНДС11.СрокБлокировки КАК СрокБлокировки,
					|	ОстаткиПоНДС11.ПравоВычетаДо КАК ПравоВычетаДо,
					|	ОстаткиПоНДС11.Заблокировано2 КАК Заблокировано2,
					|	ОстаткиПоНДС11.Заблокировано3 КАК Заблокировано3,
					|	ОстаткиПоНДС11.Заблокировано4 КАК Заблокировано4,
					|	ОстаткиПоНДС11.Заблокировано5 КАК Заблокировано5,
					|	ОстаткиПоНДС11.Заблокировано6 КАК Заблокировано6,
					|	ОстаткиПоНДС11.Заблокировано7 КАК Заблокировано7,
					|	ОстаткиПоНДС11.Заблокировано8 КАК Заблокировано8,
					|	ОстаткиПоНДС11.Заблокировано9 КАК Заблокировано9,
					|	ОстаткиПоНДС11.Заблокировано10 КАК Заблокировано10,
					|	ОстаткиПоНДС11.Заблокировано11 КАК Заблокировано11,
					|	ВЫБОР
					|		КОГДА ПериодичностьНДС.ПериодичностьФормированияВычетовИВосстановленийНДС = ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
					|			ТОГДА ВЫБОР
					|					КОГДА КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ) <> КОНЕЦПЕРИОДА(&КонецПериода, КВАРТАЛ)
					|						ТОГДА ОстаткиПоНДС11.НДСОстаток 
					|								- ОстаткиПоНДС11.Заблокировано1 
					|								- ОстаткиПоНДС11.Заблокировано2 
					|								- ОстаткиПоНДС11.Заблокировано3 
					|								- ОстаткиПоНДС11.Заблокировано4 
					|								- ОстаткиПоНДС11.Заблокировано5 
					|								- ОстаткиПоНДС11.Заблокировано6 
					|								- ОстаткиПоНДС11.Заблокировано7 
					|								- ОстаткиПоНДС11.Заблокировано8 
					|								- ОстаткиПоНДС11.Заблокировано9 
					|								- ОстаткиПоНДС11.Заблокировано10
					|								- ОстаткиПоНДС11.Заблокировано11
					|					ИНАЧЕ 0
					|				КОНЕЦ
					|		ИНАЧЕ 0
					|	КОНЕЦ КАК Заблокировано12
					|ПОМЕСТИТЬ ОстаткиПоНДС12
					|ИЗ
					|	ОстаткиПоНДС11 КАК ОстаткиПоНДС11,
					|	ПериодичностьНДС КАК ПериодичностьНДС
					|
					|ИНДЕКСИРОВАТЬ ПО
					|	СчетФактура
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	ОстаткиПоНДС12.Организация КАК Организация,
					|	ОстаткиПоНДС12.Поставщик КАК Поставщик,
					|	ОстаткиПоНДС12.СчетФактура КАК СчетФактура,
					|	ОстаткиПоНДС12.НаправлениеДеятельности КАК НаправлениеДеятельности,
					|	ОстаткиПоНДС12.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
					|	ОстаткиПоНДС12.ВидЦенности КАК ВидЦенности,
					|	ОстаткиПоНДС12.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
					|	ОстаткиПоНДС12.РеализацияЭкспорт КАК РеализацияЭкспорт,
					|	ОстаткиПоНДС12.СтавкаНДС КАК СтавкаНДС,
					|	ОстаткиПоНДС12.НДСОстаток КАК НДСОстаток,
					|	ОстаткиПоНДС12.Заблокировано1 КАК Заблокировано1,
					|	ОстаткиПоНДС12.СрокБлокировки КАК СрокБлокировки,
					|	ОстаткиПоНДС12.ПравоВычетаДо КАК ПравоВычетаДо,
					|	ОстаткиПоНДС12.Заблокировано2 КАК Заблокировано2,
					|	ОстаткиПоНДС12.Заблокировано3 КАК Заблокировано3,
					|	ОстаткиПоНДС12.Заблокировано4 КАК Заблокировано4,
					|	ОстаткиПоНДС12.Заблокировано5 КАК Заблокировано5,
					|	ОстаткиПоНДС12.Заблокировано6 КАК Заблокировано6,
					|	ОстаткиПоНДС12.Заблокировано7 КАК Заблокировано7,
					|	ОстаткиПоНДС12.Заблокировано8 КАК Заблокировано8,
					|	ОстаткиПоНДС12.Заблокировано9 КАК Заблокировано9,
					|	ОстаткиПоНДС12.Заблокировано10 КАК Заблокировано10,
					|	ОстаткиПоНДС12.Заблокировано11 КАК Заблокировано11,
					|	ОстаткиПоНДС12.Заблокировано12 КАК Заблокировано12,
					|
					|	ОстаткиПоНДС12.НДСОстаток 
					|	- ОстаткиПоНДС12.Заблокировано1 
					|	- ОстаткиПоНДС12.Заблокировано2 
					|	- ОстаткиПоНДС12.Заблокировано3 
					|	- ОстаткиПоНДС12.Заблокировано4 
					|	- ОстаткиПоНДС12.Заблокировано5 
					|	- ОстаткиПоНДС12.Заблокировано6 
					|	- ОстаткиПоНДС12.Заблокировано7 
					|	- ОстаткиПоНДС12.Заблокировано8 
					|	- ОстаткиПоНДС12.Заблокировано9 
					|	- ОстаткиПоНДС12.Заблокировано10 
					|	- ОстаткиПоНДС12.Заблокировано11
					|	- ОстаткиПоНДС12.Заблокировано12 КАК Заблокировано0
					|
					|ПОМЕСТИТЬ АнализПричинБлокировкиВычетаНДС
					|ИЗ
					|	ОстаткиПоНДС12 КАК ОстаткиПоНДС12
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ ОстаткиПоНДС12
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	АнализПричинБлокировкиВычетаНДС.Организация КАК Организация,
					|	АнализПричинБлокировкиВычетаНДС.Поставщик КАК Поставщик,
					|	АнализПричинБлокировкиВычетаНДС.СчетФактура КАК СчетФактура,
					|	АнализПричинБлокировкиВычетаНДС.НаправлениеДеятельности КАК НаправлениеДеятельности,
					|	АнализПричинБлокировкиВычетаНДС.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
					|	АнализПричинБлокировкиВычетаНДС.ВидЦенности КАК ВидЦенности,
					|	АнализПричинБлокировкиВычетаНДС.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
					|	АнализПричинБлокировкиВычетаНДС.РеализацияЭкспорт КАК РеализацияЭкспорт,
					|	АнализПричинБлокировкиВычетаНДС.СтавкаНДС КАК СтавкаНДС,
					|	АнализПричинБлокировкиВычетаНДС.НДСОстаток КАК НДСОстаток,
					|	АнализПричинБлокировкиВычетаНДС.Заблокировано1 КАК Заблокировано1,
					|	АнализПричинБлокировкиВычетаНДС.СрокБлокировки КАК СрокБлокировки,
					|	АнализПричинБлокировкиВычетаНДС.ПравоВычетаДо КАК ПравоВычетаДо,
					|	АнализПричинБлокировкиВычетаНДС.Заблокировано2 КАК Заблокировано2,
					|	АнализПричинБлокировкиВычетаНДС.Заблокировано3 КАК Заблокировано3,
					|	АнализПричинБлокировкиВычетаНДС.Заблокировано4 КАК Заблокировано4,
					|	АнализПричинБлокировкиВычетаНДС.Заблокировано5 КАК Заблокировано5,
					|	АнализПричинБлокировкиВычетаНДС.Заблокировано6 КАК Заблокировано6,
					|	АнализПричинБлокировкиВычетаНДС.Заблокировано7 КАК Заблокировано7,
					|	АнализПричинБлокировкиВычетаНДС.Заблокировано8 КАК Заблокировано8,
					|	АнализПричинБлокировкиВычетаНДС.Заблокировано9 КАК Заблокировано9,
					|	АнализПричинБлокировкиВычетаНДС.Заблокировано10 КАК Заблокировано10,
					|	АнализПричинБлокировкиВычетаНДС.Заблокировано11 КАК Заблокировано11,
					|	АнализПричинБлокировкиВычетаНДС.Заблокировано12 КАК Заблокировано12,
					|	АнализПричинБлокировкиВычетаНДС.Заблокировано0 КАК Заблокировано0
					|ИЗ
					|	АнализПричинБлокировкиВычетаНДС КАК АнализПричинБлокировкиВычетаНДС";
	
	#КонецОбласти
	
	НаборИсходныхДанных = Запрос.Выполнить().Выбрать();
	
	Запрос.Текст = "ВЫБРАТЬ
					|	""НДС заблокирован пользователем"" КАК Имя,
					|	МАКСИМУМ(АнализПричинБлокировкиВычетаНДС.Заблокировано1 <> 0) КАК Видимость
					|ПОМЕСТИТЬ втВидимостьБлоковИсходная
					|ИЗ
					|	АнализПричинБлокировкиВычетаНДС КАК АнализПричинБлокировкиВычетаНДС
					|
					|ОБЪЕДИНИТЬ ВСЕ
					|
					|ВЫБРАТЬ
					|	""Нет оплаты по поступлению электронных услуг"",
					|	МАКСИМУМ(АнализПричинБлокировкиВычетаНДС.Заблокировано2 <> 0)
					|ИЗ
					|	АнализПричинБлокировкиВычетаНДС КАК АнализПричинБлокировкиВычетаНДС
					|
					|ОБЪЕДИНИТЬ ВСЕ
					|
					|ВЫБРАТЬ
					|	""Нет оплаты НДС в бюджет"",
					|	МАКСИМУМ(АнализПричинБлокировкиВычетаНДС.Заблокировано3 <> 0)
					|ИЗ
					|	АнализПричинБлокировкиВычетаНДС КАК АнализПричинБлокировкиВычетаНДС
					|
					|ОБЪЕДИНИТЬ ВСЕ
					|
					|ВЫБРАТЬ
					|	""Нет отметки налогового органа по заявлениям о ввозе"",
					|	МАКСИМУМ(АнализПричинБлокировкиВычетаНДС.Заблокировано4 <> 0)
					|ИЗ
					|	АнализПричинБлокировкиВычетаНДС КАК АнализПричинБлокировкиВычетаНДС
					|
					|ОБЪЕДИНИТЬ ВСЕ
					|
					|ВЫБРАТЬ
					|	""Нет поступлений ценностей по счету-фактуре налогового агента"",
					|	МАКСИМУМ(АнализПричинБлокировкиВычетаНДС.Заблокировано5 <> 0)
					|ИЗ
					|	АнализПричинБлокировкиВычетаНДС КАК АнализПричинБлокировкиВычетаНДС
					|
					|ОБЪЕДИНИТЬ ВСЕ
					|
					|ВЫБРАТЬ
					|	""Нет счета-фактуры"",
					|	МАКСИМУМ(АнализПричинБлокировкиВычетаНДС.Заблокировано6 <> 0)
					|ИЗ
					|	АнализПричинБлокировкиВычетаНДС КАК АнализПричинБлокировкиВычетаНДС
					|
					|ОБЪЕДИНИТЬ ВСЕ
					|
					|ВЫБРАТЬ
					|	""Экспорт"",
					|	МАКСИМУМ(АнализПричинБлокировкиВычетаНДС.Заблокировано7 <> 0)
					|ИЗ
					|	АнализПричинБлокировкиВычетаНДС КАК АнализПричинБлокировкиВычетаНДС
					|
					|ОБЪЕДИНИТЬ ВСЕ
					|
					|ВЫБРАТЬ
					|	""Ожидает распределения НДС"",
					|	МАКСИМУМ(АнализПричинБлокировкиВычетаНДС.Заблокировано8 <> 0)
					|ИЗ
					|	АнализПричинБлокировкиВычетаНДС КАК АнализПричинБлокировкиВычетаНДС
					|
					|ОБЪЕДИНИТЬ ВСЕ
					|
					|ВЫБРАТЬ
					|	""Отнесен на вид деятельности """"по фактическому использованию"""""",
					|	МАКСИМУМ(АнализПричинБлокировкиВычетаНДС.Заблокировано9 <> 0)
					|ИЗ
					|	АнализПричинБлокировкиВычетаНДС КАК АнализПричинБлокировкиВычетаНДС
					|
					|ОБЪЕДИНИТЬ ВСЕ
					|
					|ВЫБРАТЬ
					|	""Производство с длительным циклом"",
					|	МАКСИМУМ(АнализПричинБлокировкиВычетаНДС.Заблокировано10 <> 0)
					|ИЗ
					|	АнализПричинБлокировкиВычетаНДС КАК АнализПричинБлокировкиВычетаНДС
					|
					|ОБЪЕДИНИТЬ ВСЕ
					|
					|ВЫБРАТЬ
					|	""Космическая деятельность"",
					|	МАКСИМУМ(АнализПричинБлокировкиВычетаНДС.Заблокировано11 <> 0)
					|ИЗ
					|	АнализПричинБлокировкиВычетаНДС КАК АнализПричинБлокировкиВычетаНДС
					|
					|ОБЪЕДИНИТЬ ВСЕ
					|
					|ВЫБРАТЬ
					|	""Отложен до конца квартала по учетной политике"",
					|	МАКСИМУМ(АнализПричинБлокировкиВычетаНДС.Заблокировано12 <> 0)
					|ИЗ
					|	АнализПричинБлокировкиВычетаНДС КАК АнализПричинБлокировкиВычетаНДС
					|
					|ОБЪЕДИНИТЬ ВСЕ
					|
					|ВЫБРАТЬ
					|	""Блокировок нет, НДС может быть принят к вычету"",
					|	МАКСИМУМ(АнализПричинБлокировкиВычетаНДС.Заблокировано0 <> 0)
					|ИЗ
					|	АнализПричинБлокировкиВычетаНДС КАК АнализПричинБлокировкиВычетаНДС
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	втВидимостьБлоковИсходная.Имя,
					|	ЕСТЬNULL(втВидимостьБлоковИсходная.Видимость, Ложь) КАК Видимость
					|ИЗ
					|	втВидимостьБлоковИсходная КАК втВидимостьБлоковИсходная";

	ВидимостьБлоков = Запрос.Выполнить().Выгрузить();
	
	СтруктураНаборовДанных = Новый Структура;
	СтруктураНаборовДанных.Вставить("НаборИсходныхДанных", НаборИсходныхДанных);
	СтруктураНаборовДанных.Вставить("ВидимостьБлоков", ВидимостьБлоков);
		
	Возврат СтруктураНаборовДанных;
	
КонецФункции

#КонецОбласти

#КонецЕсли
