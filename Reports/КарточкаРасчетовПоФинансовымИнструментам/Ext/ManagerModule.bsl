//@strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//
Процедура НастроитьВариантыОтчета(Настройки) Экспорт
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.КарточкаРасчетовПоФинансовымИнструментам);
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	КраткоеОписаниеОтчета = НСтр("ru= 'Расшифровка начислений и оплат до документов регистраторов.
		|Какие документы сформировали текущее сальдо расчетов?'");
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "КарточкаРасчетовПоФинансовымИнструментам");
	ОписаниеВарианта.Описание = КраткоеОписаниеОтчета;
	ОписаниеВарианта.ФункциональныеОпции.Добавить("УправлениеПредприятием");
	ОписаниеВарианта.ФункциональныеОпции.Добавить("КомплекснаяАвтоматизация");
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "КарточкаРасчетовПоФинансовымИнструментамУТ");
	ОписаниеВарианта.Описание = КраткоеОписаниеОтчета;
	ОписаниеВарианта.ФункциональныеОпции.Добавить("УправлениеТорговлей");
	ОписаниеВарианта.ФункциональныеОпции.Добавить("БазоваяВерсия");
	
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "КарточкаРасчетовПоФинансовымИнструментамКонтекст");
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "КарточкаРасчетовПоФинансовымИнструментамУТКонтекст");
	
КонецПроцедуры

// Добавляет команду отчета "Карточка расчетов по финансовым инструментам" в список команд.
// Группировка по партнеру/контрагенту
// 
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//
// Возвращаемое значение:
// 	- СтрокаТаблицыЗначений: см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов.
// 	- Неопределено - Если нет прав на просмотр отчета.
//
Функция ДобавитьКомандуОтчета(КомандыОтчетов) Экспорт
	Если Константы.НоваяАрхитектураВзаиморасчетов.Получить() Тогда
		Если ПравоДоступа("Просмотр", Метаданные.Отчеты.КарточкаРасчетовПоФинансовымИнструментам) Тогда
			
			КомандаОтчет = КомандыОтчетов.Добавить();
			
			КомандаОтчет.Менеджер = Метаданные.Отчеты.КарточкаРасчетовПоФинансовымИнструментам.ПолноеИмя();
			КомандаОтчет.Представление = Метаданные.Отчеты.КарточкаРасчетовПоФинансовымИнструментам.Синоним;
			
			КомандаОтчет.МножественныйВыбор = Истина;
			КомандаОтчет.Важность = "Обычное";
			КомандаОтчет.ДополнительныеПараметры.Вставить("ИмяКоманды", "КарточкаПоФинИнструментам");
			КомандаОтчет.КлючВарианта = "КарточкаРасчетовПоФинансовымИнструментамКонтекст";
			
			Возврат КомандаОтчет;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
