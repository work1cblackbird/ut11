#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	НоменклатураЛокализация.УстановитьТекстЗапросаОтчетаТоварыСИстекающимиСертификатами(СхемаКомпоновкиДанных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - См. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
	
КонецПроцедуры

// Вызывается перед загрузкой новых настроек. Используется для изменения схемы компоновки.
//
// Параметры:
//	Контекст - Произвольный - параметры контекста, в котором используется отчет,
//	КлючСхемы - Строка - идентификатор текущей схемы компоновщика настроек,
//	КлючВарианта - Строка, Неопределено - имя предопределенного или уникальный идентификатор пользовательского 
//											варианта отчета. Неопределено когда вызов для варианта расшифровки
//											или без контекста.
//	НовыеНастройкиКД - НастройкиКомпоновкиДанных, Неопределено - настройки варианта отчета, которые будут загружены 
//		в компоновщик настроек после его инициализации.
//		Неопределено когда настройки варианта не надо загружать (уже загружены ранее).
//	НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных, Неопределено -
//		пользовательские настройки, которые будут загружены в компоновщик настроек после его инициализации.
//		Неопределено когда пользовательские настройки не надо загружать (уже загружены ранее).
//
Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если КлючСхемы <> "СхемаИнициализирована" Тогда
		
		КлючСхемы        = "СхемаИнициализирована";
		ПоляНабораДанных = СхемаКомпоновкиДанных.НаборыДанных.СертификатыНоменклатуры.Поля;
		
		ЗначенияПоляТипСертификата = Новый СписокЗначений;
		МассивТиповСертификатов    = Справочники.СертификатыНоменклатуры.СформироватьСписокВыбораТиповСертификатов();
		
		Для Каждого ТипСертификата Из МассивТиповСертификатов Цикл
			ЗначенияПоляТипСертификата.Добавить(ТипСертификата, ТипСертификата);
		КонецЦикла;
		
		ПолеТипСертификата = ПоляНабораДанных.Найти("ТипСертификата");
		ПолеТипСертификата.УстановитьДоступныеЗначения(ЗначенияПоляТипСертификата);
		
		ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКомпоновкиДанных, КлючСхемы);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли