#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получает ссылку на справочник виды налогов взносов по перечислению типу налогов.
// 
// Параметры:
//  ТипНалога - ПеречислениеСсылка.УдалитьТипыНалогов
// 
// Возвращаемое значение:
//  СправочникСсылка.ВидыНалоговВзносов - Ссылка виды налогов взносов по типу налогов
Функция СсылкаВидыНалоговВзносовПоТипуНалогов(ТипНалога) Экспорт
	Если НЕ ТипЗнч(ТипНалога) = Тип("ПеречислениеСсылка.УдалитьТипыНалогов") Тогда
		Возврат ТипНалога;
	КонецЕсли;
	Если ТипНалога = Перечисления.УдалитьТипыНалогов.ПустаяСсылка() Тогда
		Возврат Справочники.ВидыНалоговВзносов.ПустаяСсылка();
	КонецЕсли;
	Индекс = Перечисления.УдалитьТипыНалогов.Индекс(ТипНалога);
	МетаданныеПеречисления = ТипНалога.Метаданные().ЗначенияПеречисления[Индекс];
	Ссылка = Справочники.ВидыНалоговВзносов.ПолучитьСсылкуПредопределенногоЭлемента(МетаданныеПеречисления.Имя);
	Возврат Ссылка;
КонецФункции

// Заполняет значение нового реквизита Объекта с типом Справочник.ВидыНалоговВзносов 
// на основании старого удаляемого реквизита с типом Перечисление.УдалитьТипыНалогов.
// 
// Параметры:
//  Объект - ДокументОбъект, СправочникОбъект
//	СтарыйРеквизит - Строка
//	НовыйРеквизит - Строка
Процедура ЗаполнитьРеквизитТипНалога(Объект, СтарыйРеквизит, НовыйРеквизит = Неопределено) Экспорт
	
	МассивПути = СтрРазделить(СтарыйРеквизит, ".");
	ЭтоТабличнаяЧасть = МассивПути.Количество() = 2;
	
	Если ЭтоТабличнаяЧасть Тогда
		ИмяТабличнойЧасти = МассивПути[0];
		СтарыйРеквизит = МассивПути[1];
		Для Каждого Строка Из Объект[ИмяТабличнойЧасти] Цикл
			Если НовыйРеквизит = Неопределено Тогда
				Строка[СтарыйРеквизит] = СсылкаВидыНалоговВзносовПоТипуНалогов(Строка[СтарыйРеквизит]);
			Иначе 
				Строка[НовыйРеквизит] = СсылкаВидыНалоговВзносовПоТипуНалогов(Строка[СтарыйРеквизит]);
				Строка[СтарыйРеквизит] = Неопределено;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Если НовыйРеквизит = Неопределено Тогда
			Объект[СтарыйРеквизит]= СсылкаВидыНалоговВзносовПоТипуНалогов(Объект[СтарыйРеквизит]);
		Иначе
			Объект[НовыйРеквизит]= СсылкаВидыНалоговВзносовПоТипуНалогов(Объект[СтарыйРеквизит]);
			Объект[СтарыйРеквизит] = Неопределено;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли