#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Приводит переданное значение перечисления к двум: ПродажаОблагаетсяНДС или ПродажаНеОблагаетсяНДС
//
// Параметры:
//  НалогообложениеНДС	 - ПеречислениеСсылка.ТипыНалогообложенияНДС 
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыНалогообложенияНДС 
//
Функция СНдсБезНдс(НалогообложениеНДС) Экспорт
	
	Если НалогообложениеНДС = ЛьготноеНалогообложениеНДСНаУСН Тогда
		Возврат НалогообложениеНДС;
	ИначеЕсли ТипыСНДС().Найти(НалогообложениеНДС) <> Неопределено Тогда
		Возврат Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС;
	ИначеЕсли ТипыБезНДС().Найти(НалогообложениеНДС) <> Неопределено Тогда
		Возврат Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС;
	Иначе
		
		//ОпределяетсяРаспределением
		//ВводОСВЭксплуатацию
		//НалоговыйАгентПоНДС
		//РеверсивноеОбложениеНДС
		ТекстИсключения = НСтр("ru = 'Неподдерживаемый тип налогообложения НДС %НалогообложенияНДС%'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%НалогообложенияНДС%", НалогообложениеНДС);
		
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
КонецФункции

// Возвращает массив типов налогообложения НДС, предусматривающих учет НДС
// 
// Возвращаемое значение:
//  Массив 
//
Функция ТипыСНДС() Экспорт
	
	Массив = Новый Массив;
	Массив.Добавить(ПоФактическомуИспользованию);
	Массив.Добавить(ПродажаНаЭкспорт);
	Массив.Добавить(ПродажаОблагаетсяНДС);
	Массив.Добавить(ЭкспортСырьевыхТоваровУслуг);
	Массив.Добавить(ЭкспортНесырьевыхТоваров);
	Массив.Добавить(ОблагаетсяНДСУПокупателя);
	Массив.Добавить(ПроизводствоСДЦ);
	Массив.Добавить(РеализацияРаботУслугНаЭкспорт);
	Массив.Добавить(ПродажаОблагаетсяНДСВСтранеЕАЭС);
	Массив.Добавить(ОблагаетсяНДССМежценовойРазницы);
	Массив.Добавить(Космос);
	Массив.Добавить(ЛьготноеНалогообложениеНДСНаУСН);
	
	Возврат Массив;
	
КонецФункции

// Возвращает массив типов налогообложения НДС, не предусматривающих учет НДС
// 
// Возвращаемое значение:
//  Массив 
//
Функция ТипыБезНДС() Экспорт
	
	Массив = Новый Массив;
	Массив.Добавить(ПродажаНеОблагаетсяНДС);
	//++ Локализация
	Массив.Добавить(ПродажаОблагаетсяЕНВД);
	Массив.Добавить(ПродажаПоПатенту);
	//-- Локализация
	
	Возврат Массив;
	
КонецФункции

// Получает представление значения перечисления
//
// Параметры:
//  Значение - ПеречислениеСсылка.ТипыНалогообложенияНДС 
//  КодЯзыка - Строка - необязательный
// 
// Возвращаемое значение:
//  Строка 
//
Функция ПредставлениеНаЯзыке(Значение, КодЯзыка = Неопределено) Экспорт
	
	Представление = "";
	
	Если Значение = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС Тогда
		Представление = НСтр("ru='Облагается НДС'", КодЯзыка);
	ИначеЕсли Значение = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС Тогда
		Представление = НСтр("ru='Не облагается НДС'", КодЯзыка);
	ИначеЕсли Значение = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД Тогда
		Представление = НСтр("ru='Облагается ЕНВД'", КодЯзыка);
	ИначеЕсли Значение = Перечисления.ТипыНалогообложенияНДС.ПродажаНаЭкспорт Тогда
		Представление = НСтр("ru='Экспорт'", КодЯзыка);
	ИначеЕсли Значение = Перечисления.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров Тогда
		Представление = НСтр("ru='Экспорт несырьевых товаров'", КодЯзыка);
	ИначеЕсли Значение = Перечисления.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг Тогда
		Представление = НСтр("ru='Экспорт сырьевых товаров, услуг (работ)'", КодЯзыка);
	ИначеЕсли Значение = Перечисления.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию Тогда
		Представление = НСтр("ru='Ввод ОС в эксплуатацию'", КодЯзыка);
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений();
	ДоступныеТипыНалогообложения = УчетНДСУПКлиентСервер.ПрименяемыеТипыНалогообложенияНДС(Истина, Истина, Истина);
	Для каждого Элемент Из ДоступныеТипыНалогообложения Цикл
		ДанныеВыбора.Добавить(Элемент.Значение, Элемент.Представление);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
