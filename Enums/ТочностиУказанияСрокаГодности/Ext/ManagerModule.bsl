#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает форматную строку представления срока годности.
//
// Параметры:
//  ТочностьУказанияСрокаГодности - ПеречислениеСсылка.ТочностиУказанияСрокаГодности - точность указания срока годности вида номенклатуры.
// 
// Возвращаемое значение:
//  Строка - форматная строка срока годности вида номенклатуры.
//
Функция ФорматнаяСтрока(ТочностьУказанияСрокаГодности)Экспорт
	
	ФорматнаяСтрока = "";
	
	ШаблонФорматнойСтроки = НСтр("ru = 'ДФ=%ТочностьУказания%'");
	Если ТочностьУказанияСрокаГодности = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоЧасов Тогда
		ФорматнаяСтрока = СтрЗаменить(ШаблонФорматнойСтроки, "%ТочностьУказания%", "'dd.MM.yy HH:00'");
	ИначеЕсли ТочностьУказанияСрокаГодности = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоМесяцев Тогда
		ФорматнаяСтрока = СтрЗаменить(ШаблонФорматнойСтроки, "%ТочностьУказания%", "MM.yy");
	ИначеЕсли ТочностьУказанияСрокаГодности = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоДней Тогда
		ФорматнаяСтрока = СтрЗаменить(ШаблонФорматнойСтроки, "%ТочностьУказания%", "dd.MM.yy");
	КонецЕсли;
	
	Возврат ФорматнаяСтрока;
	
КонецФункции

// Возвращает представление форматной строки для вывода в элементах управления
//
// Параметры:
//  ТочностьУказанияСрокаГодности - ПеречислениеСсылка.ТочностиУказанияСрокаГодности - точность указания срока годности вида номенклатуры.
// 
// Возвращаемое значение:
//  Строка
//
Функция ПредставлениеФорматнойСтроки(ТочностьУказанияСрокаГодности)Экспорт
	
	ПредставлениеФорматнойСтроки = "";
	
	Если ТочностьУказанияСрокаГодности = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоЧасов Тогда
		ПредставлениеФорматнойСтроки = НСтр("ru = 'дата, час'");
	ИначеЕсли ТочностьУказанияСрокаГодности = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоМесяцев Тогда
		ПредставлениеФорматнойСтроки = НСтр("ru = 'месяц, год'");
	ИначеЕсли ТочностьУказанияСрокаГодности = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоДней Тогда
		ПредставлениеФорматнойСтроки = НСтр("ru = 'дата'");
	КонецЕсли;
	
	Возврат ПредставлениеФорматнойСтроки;
	
КонецФункции

#КонецОбласти

#КонецЕсли


