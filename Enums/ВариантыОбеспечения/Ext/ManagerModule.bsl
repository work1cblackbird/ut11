#Если НЕ МобильныйАвтономныйСервер Тогда
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ОтгружатьЕслиДоступно") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Результат = ОбеспечениеВДокументахВызовСервера.ОбработкаПолученияДанныхВыбораВариантаОбеспечения(Параметры);
		
		Упаковка = Параметры.Упаковка;
		Если Не ЗначениеЗаполнено(Упаковка) Тогда
			Если ЗначениеЗаполнено(Результат.ЕдиницаИзмерения) Тогда
				Упаковка = Результат.ЕдиницаИзмерения;
			КонецЕсли;
		КонецЕсли;
		
		ТребуетсяУказаниеСерийВСтроке = Результат.ТребуетсяУказаниеСерийВСтроке;
		ТребуетсяВыборСклада = Параметры.Свойство("НесколькоСкладов")
			И Параметры.НесколькоСкладов
			И (Параметры.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар")
				Или Параметры.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.МногооборотнаяТара"));
		
		РезервИлиОтгрузка = Параметры.ВариантОбеспечения
				= ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.СоСклада")
			Или Параметры.ВариантОбеспечения
				= ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить");
		
		РазрешеноДробитьСтроки = Не Параметры.Свойство("ИспользоватьЧастичнуюОтгрузку")
			Или Параметры.ИспользоватьЧастичнуюОтгрузку;
		
		ДанныеВыбора = Новый СписокЗначений();
		
		Значение = ОбеспечениеВДокументахКлиентСервер.ЗначениеВыбораВариантаОбеспечения();
		Значение.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.НеТребуется");
		ДанныеВыбора.Добавить(Значение, НСтр("ru = 'Не обеспечивать'"));
		
		Значение = ОбеспечениеВДокументахКлиентСервер.ЗначениеВыбораВариантаОбеспечения();
		Значение.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.КОбеспечению");
		ДанныеВыбора.Добавить(Значение, НСтр("ru = 'К обеспечению'"));
		
		Значение = ОбеспечениеВДокументахКлиентСервер.ЗначениеВыбораВариантаОбеспечения();
		Значение.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.РезервироватьПоМереПоступления");
		ДанныеВыбора.Добавить(Значение, НСтр("ru = 'Резервировать по мере поступления'"));
		
		Если Параметры.КоличествоУпаковок > 0 Тогда
			
			Если Параметры.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.ПереданРанее")
					Или РезервИлиОтгрузка
					Или Не Параметры.ОтгружатьЕслиДоступно
					Или Результат.ПроводитьБезКонтроляРезервов
						И Не РазрешеноДробитьСтроки
					Или Параметры.КоличествоУпаковок <= Параметры.Доступно
					Или Результат.ДопустимоеОтклонение > 0
						И Параметры.Количество * Результат.КоэффициентУпаковки <= Параметры.Доступно Тогда
				
				Значение = ОбеспечениеВДокументахКлиентСервер.ЗначениеВыбораВариантаОбеспечения();
				Значение.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.СоСклада");
				ДанныеВыбора.Добавить(Значение, НСтр("ru = 'Резервировать на складе (все)'"));
				
				Значение = ОбеспечениеВДокументахКлиентСервер.ЗначениеВыбораВариантаОбеспечения();
				Значение.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить");
				ДанныеВыбора.Добавить(Значение, НСтр("ru = 'Отгрузить (все)'"));
				
				Если ТребуетсяУказаниеСерийВСтроке Или ТребуетсяВыборСклада Тогда
					
					Значение = ОбеспечениеВДокументахКлиентСервер.ЗначениеВыбораВариантаОбеспечения();
					Значение.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.СоСклада");
					Значение.ОткрытьФормуВыбораСкладаИСерий = Истина;
					ДанныеВыбора.Добавить(Значение, НСтр("ru = 'Резервировать на складе ...'"));
					
					Значение = ОбеспечениеВДокументахКлиентСервер.ЗначениеВыбораВариантаОбеспечения();
					Значение.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить");
					Значение.ОткрытьФормуВыбораСкладаИСерий = Истина;
					ДанныеВыбора.Добавить(Значение, НСтр("ru = 'Отгрузить ...'"));
					
				ИначеЕсли РазрешеноДробитьСтроки Тогда
					
					Если Не РезервИлиОтгрузка Тогда
						Значение = ОбеспечениеВДокументахКлиентСервер.ЗначениеВыбораВариантаОбеспечения();
						Значение.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.СоСклада");
						Значение.ОткрытьФормуРазбиенияСтроки = Истина;
						Значение.ДоступноеКоличествоПриРазбиенииСтроки = Параметры.КоличествоУпаковок;
						Значение.ДопустимоеОтклонениеПриВыбореВариантаОбеспечения = Результат.ДопустимоеОтклонение;
						Значение.ОбщееКоличествоПриРазбиенииСтроки = Параметры.КоличествоУпаковок;
						ДанныеВыбора.Добавить(Значение, НСтр("ru = 'Резервировать на складе ...'"));
					КонецЕсли;
					
					Если Параметры.ВариантОбеспечения <> ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить") Тогда
						Значение = ОбеспечениеВДокументахКлиентСервер.ЗначениеВыбораВариантаОбеспечения();
						Значение.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить");
						Значение.ОткрытьФормуРазбиенияСтроки = Истина;
						Значение.ДоступноеКоличествоПриРазбиенииСтроки = Параметры.КоличествоУпаковок;
						Значение.ДопустимоеОтклонениеПриВыбореВариантаОбеспечения = Результат.ДопустимоеОтклонение;
						Значение.ОбщееКоличествоПриРазбиенииСтроки = Параметры.КоличествоУпаковок;
						ДанныеВыбора.Добавить(Значение, НСтр("ru = 'Отгрузить ...'"));
					КонецЕсли;
					
				КонецЕсли;
				
			Иначе
				
				Если РазрешеноДробитьСтроки И Параметры.Доступно > 0 Тогда
					
					Значение = ОбеспечениеВДокументахКлиентСервер.ЗначениеВыбораВариантаОбеспечения();
					Значение.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.СоСклада");
					Значение.РазбитьСтроку = Истина;
					
					Текст = НСтр("ru = 'Резервировать на складе: %1 %2'");
					Текст = СтрШаблон(Текст, Формат(Параметры.Доступно, "ЧДЦ=3"), Упаковка);
					
					ДанныеВыбора.Добавить(Значение, Текст);
					
					Значение = ОбеспечениеВДокументахКлиентСервер.ЗначениеВыбораВариантаОбеспечения();
					Значение.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить");
					Значение.РазбитьСтроку = Истина;
					
					Текст = НСтр("ru = 'Отгрузить: %1 %2'");
					Текст = СтрШаблон(Текст, Формат(Параметры.Доступно, "ЧДЦ=3"), Упаковка);
					
					ДанныеВыбора.Добавить(Значение, Текст);
					
				КонецЕсли;
				
				Если ТребуетсяВыборСклада Тогда
					
					Значение = ОбеспечениеВДокументахКлиентСервер.ЗначениеВыбораВариантаОбеспечения();
					Значение.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.СоСклада");
					Значение.ОткрытьФормуВыбораСкладаИСерий = Истина;
					ДанныеВыбора.Добавить(Значение, НСтр("ru = 'Резервировать на складе ...'"));
					
					Значение = ОбеспечениеВДокументахКлиентСервер.ЗначениеВыбораВариантаОбеспечения();
					Значение.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить");
					Значение.ОткрытьФормуВыбораСкладаИСерий = Истина;
					ДанныеВыбора.Добавить(Значение, НСтр("ru = 'Отгрузить ...'"));
					
				ИначеЕсли РазрешеноДробитьСтроки И ТребуетсяУказаниеСерийВСтроке И Параметры.Доступно > 0 Тогда
					
					Значение = ОбеспечениеВДокументахКлиентСервер.ЗначениеВыбораВариантаОбеспечения();
					Значение.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.СоСклада");
					Значение.ОткрытьФормуВыбораСкладаИСерий = Истина;
					ДанныеВыбора.Добавить(Значение, НСтр("ru = 'Резервировать на складе ...'"));
					
					Значение = ОбеспечениеВДокументахКлиентСервер.ЗначениеВыбораВариантаОбеспечения();
					Значение.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить");
					Значение.ОткрытьФормуВыбораСкладаИСерий = Истина;
					ДанныеВыбора.Добавить(Значение, НСтр("ru = 'Отгрузить ...'"));
					
				ИначеЕсли (Параметры.Доступно > 0 Или Результат.ПроводитьБезКонтроляРезервов)
					И РазрешеноДробитьСтроки Тогда
					
					Если Не РезервИлиОтгрузка Тогда
						Доступно = ?(Результат.ПроводитьБезКонтроляРезервов, Параметры.КоличествоУпаковок, Параметры.Доступно);
						Значение = ОбеспечениеВДокументахКлиентСервер.ЗначениеВыбораВариантаОбеспечения();
						Значение.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.СоСклада");
						Значение.ОткрытьФормуРазбиенияСтроки = Истина;
						Значение.ДоступноеКоличествоПриРазбиенииСтроки = Доступно;
						Значение.ДопустимоеОтклонениеПриВыбореВариантаОбеспечения = Результат.ДопустимоеОтклонение;
						Значение.ОбщееКоличествоПриРазбиенииСтроки = Параметры.КоличествоУпаковок;
						ДанныеВыбора.Добавить(Значение, НСтр("ru = 'Резервировать на складе ...'"));
					КонецЕсли;
					
					Если Параметры.ВариантОбеспечения <> ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить") Тогда
						Значение = ОбеспечениеВДокументахКлиентСервер.ЗначениеВыбораВариантаОбеспечения();
						Значение.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить");
						Значение.ОткрытьФормуРазбиенияСтроки = Истина;
						Значение.ДоступноеКоличествоПриРазбиенииСтроки = Доступно;
						Значение.ДопустимоеОтклонениеПриВыбореВариантаОбеспечения = Результат.ДопустимоеОтклонение;
						Значение.ОбщееКоличествоПриРазбиенииСтроки = Параметры.КоличествоУпаковок;
						ДанныеВыбора.Добавить(Значение, НСтр("ru = 'Отгрузить ...'"));
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Значение = ОбеспечениеВДокументахКлиентСервер.ЗначениеВыбораВариантаОбеспечения();
		Значение.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.ПереданРанее");
		ДанныеВыбора.Добавить(Значение, НСтр("ru = 'Передан ранее'"));
		
		ДопустимыеДействия = Новый Массив();
		
		Если Параметры.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар")
				Или Параметры.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.МногооборотнаяТара") Тогда
				
				ДопустимыеДействия.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.НеТребуется"));
				ДопустимыеДействия.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.КОбеспечению"));
				ДопустимыеДействия.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.РезервироватьПоМереПоступления"));
				ДопустимыеДействия.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.СоСклада"));
				ДопустимыеДействия.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить"));
				ДопустимыеДействия.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.ПереданРанее"));
				
		ИначеЕсли Параметры.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга")
					Или Параметры.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Работа")
						И Не Параметры.Обособленно Тогда
				
				ДопустимыеДействия.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.НеТребуется"));
				ДопустимыеДействия.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить"));
				
		ИначеЕсли Параметры.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Работа") Тогда
				
				ДопустимыеДействия.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.КОбеспечению"));
				ДопустимыеДействия.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить"));
				
		КонецЕсли;
		
		Если Параметры.Свойство("ДопустимыеДействия") Тогда
			ДопустимыеДействияВДокументе = Параметры.ДопустимыеДействия;
		Иначе
			ДопустимыеДействияВДокументе = ОбеспечениеВДокументахКлиентСервер.ДоступныеДействияДляВыбораОбеспеченияВСтрокеПоУмолчанию();
		КонецЕсли;
		
		КоличествоЭлементов = ДанныеВыбора.Количество();
		Для Счетчик = 1 По КоличествоЭлементов Цикл
			
			ТекущееДействие = ДанныеВыбора[КоличествоЭлементов - Счетчик].Значение.ВариантОбеспечения;
			Если ДопустимыеДействия.Найти(ТекущееДействие) = Неопределено
					Или ДопустимыеДействияВДокументе.Найти(ТекущееДействие) = Неопределено Тогда
				
				ДанныеВыбора.Удалить(ДанныеВыбора[КоличествоЭлементов - Счетчик]);
				
			КонецЕсли;
			
		КонецЦикла;
	
	Иначе
		
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = Новый СписокЗначений();
		ДанныеВыбора.Добавить(Перечисления.ВариантыОбеспечения.Отгрузить);
		ДанныеВыбора.Добавить(Перечисления.ВариантыОбеспечения.СоСклада);
		ДанныеВыбора.Добавить(Перечисления.ВариантыОбеспечения.РезервироватьПоМереПоступления);
		ДанныеВыбора.Добавить(Перечисления.ВариантыОбеспечения.КОбеспечению);
		ДанныеВыбора.Добавить(Перечисления.ВариантыОбеспечения.НеТребуется);
		ДанныеВыбора.Добавить(Перечисления.ВариантыОбеспечения.ПереданРанее);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

#КонецЕсли