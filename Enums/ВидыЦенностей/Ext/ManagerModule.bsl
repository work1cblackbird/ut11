#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает массив видов ценностей, относящихся к операциям налогового агента по НДС.
// Параметры:
//     ВключаяКомиссионныеОперации - Булево - признак необходимости включения в состав возвращаемого массива
//                                            операций реализации ценностей комитента-нерезидента.
//     ВключаяОбратноеНачисление   - Булево - признак необходимости включения в состав возвращаемого массива
//                                            операций реализации товаров п.8 ст. 161 НК.
// Возвращаемое значение: 
//     Массив - массив значений вида ПеречислениеСсылка.ВидыЦенностей.
Функция МассивВидовЦенностиНалоговыйАгент(
		ВключаяКомиссионныеОперации = Истина, ВключаяОбратноеНачисление = Ложь) Экспорт
	
	ВидыЦенностей = Новый Массив;
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентАренда);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентИностранцы);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентРеализацияИмущества);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентЭлектронныеУслуги);
	
	Если ВключаяКомиссионныеОперации Тогда
		ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентКомитент);
	КонецЕсли;
	
	Если ВключаяОбратноеНачисление Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			ВидыЦенностей,
			МассивВидовЦенностиНалоговыйАгентРеализацияТоваров());
	КонецЕсли;
	
	Возврат ВидыЦенностей;
	
КонецФункции

// Возвращает массив видов ценностей, относящихся к операциям налогового агента при покупке товаров,
// указанных в п.8 ст. 161 НК РФ.
// Возвращаемое значение: 
//     Массив - массив значений вида ПеречислениеСсылка.ВидыЦенностей.
Функция МассивВидовЦенностиНалоговыйАгентРеализацияТоваров() Экспорт
	
	ВидыЦенностей = Новый Массив;
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.АвансыВыданныеНалоговыйАгент);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.АвансыПолученныеНалоговыйАгент);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.ВозвратАвансовПолученныхНалоговыйАгент);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.ТоварыНалоговыйАгент);
	
	Возврат ВидыЦенностей;
	
КонецФункции

// Возвращает массив видов ценностей, относящихся к услугам и доп. расходам.
//
// Возвращаемое значение: 
//     Массив - массив значений вида ПеречислениеСсылка.ВидыЦенностей.
//
Функция МассивВидовЦенностиУслугДляУчетаСебестоимости() Экспорт
	
	ВидыЦенностей = Новый Массив;
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.ПрочиеРаботыИУслуги);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.ЭлектронныеУслуги);
	
	Возврат ВидыЦенностей;
	
КонецФункции

#КонецОбласти

#КонецЕсли