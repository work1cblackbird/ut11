#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ТипНоменклатурыНабор.Видимость  = ПолучитьФункциональнуюОпцию("ИспользоватьНаборы");
	Элементы.ТипНоменклатурыМногооборотнаяТара.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьМногооборотнуюТару");
	Элементы.ТипНоменклатурыРабота.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВидовНоменклатуры")
												И Не ПолучитьФункциональнуюОпцию("БазоваяВерсия");
	
	СуффиксыИменЭлементов = Новый Массив;
	СуффиксыИменЭлементов.Добавить("Набор");
	СуффиксыИменЭлементов.Добавить("МногооборотнаяТара");
	СуффиксыИменЭлементов.Добавить("Работа");
	СуффиксыИменЭлементов.Добавить("Товар");
	СуффиксыИменЭлементов.Добавить("Услуга");
	
	Для Каждого Суффикс Из СуффиксыИменЭлементов Цикл
		
		ЭлементТипНоменклатуры = ЭлементТипНоменклатурыПоСуффиксу(Суффикс);
		ЭлементТипНоменклатуры.Подсказка = Перечисления.ТипыНоменклатуры.ПодсказкаПоТипуНоменклатуры(
										   ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры." + Суффикс));
	КонецЦикла;
	
	ТипНоменклатуры = Параметры.ТипНоменклатуры;
	
	СвязьОсобенностейУчетаИФО = Перечисления.ОсобенностиУчетаНоменклатуры.СвязьОсобенностейУчетаИФО();
	
	Для Каждого ЗначениеПеречисления Из Перечисления.ОсобенностиУчетаНоменклатуры Цикл
		
		ОсобенностьВключена = Ложь;
		ИменаФО = СвязьОсобенностейУчетаИФО.Получить(ЗначениеПеречисления);
		
		Если ИменаФО <> Неопределено Тогда
			МассивИменФО = СтрРазделить(ИменаФО, ",");
			Для Каждого СтрМас Из МассивИменФО Цикл
				Если Не ПолучитьФункциональнуюОпцию(СтрМас) Тогда
					ОсобенностьВключена = Ложь;
					Прервать;
				Иначе
					ОсобенностьВключена = Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ИмяЭлементаОсобенностьУчета = ИмяЭлементаОсобенностьУчетаПоЗначениюПеречисления(ЗначениеПеречисления);
		Если ОсобенностьВключена Тогда
			Элементы[ИмяЭлементаОсобенностьУчета].Подсказка =
											Перечисления.ОсобенностиУчетаНоменклатуры.ПодсказкаПоОсобенностиУчетаНоменклатуры(ЗначениеПеречисления);
		Иначе
			Элементы[ИмяЭлементаОсобенностьУчета].Видимость = Ложь;	
		КонецЕсли;
		
	КонецЦикла;	
	
	ЕстьОсобенностьПоТоварам = Ложь;
	Для Каждого ЭлементУправления Из Элементы.ГруппаОсобенностиТовараЛокализация.ПодчиненныеЭлементы Цикл
		Если ЭлементУправления.Видимость Тогда
			ЕстьОсобенностьПоТоварам = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ОсобенностьУчетаБезОсобенностейУчета.Видимость = ЕстьОсобенностьПоТоварам;
	Элементы.ДекорацияОсобенностиТовараОтступ.Видимость     = ЕстьОсобенностьПоТоварам;
	
	ЕстьОсобенностьПоУслугам = Ложь;
	Для Каждого ЭлементУправления Из Элементы.ГруппаОсобенностиУслугиЗначения.ПодчиненныеЭлементы Цикл
		Если ЭлементУправления.Видимость Тогда
			ЕстьОсобенностьПоУслугам = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ДекорацияОсобенностиУслугиОтступ.Видимость = ЕстьОсобенностьПоУслугам;
	
	Если ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар Тогда
		Если Не ЗначениеЗаполнено(Параметры.ОсобенностьУчета) Тогда
			ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.БезОсобенностейУчета;
		Иначе
			ОсобенностьУчета = Параметры.ОсобенностьУчета;
		КонецЕсли;
	ИначеЕсли ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга Тогда
		Если Не ЗначениеЗаполнено(Параметры.ОсобенностьУчета) Тогда
			ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.ОрганизациейПродавцом;
		Иначе
			ОсобенностьУчета = Параметры.ОсобенностьУчета;
		КонецЕсли;
	КонецЕсли;
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Результат = Новый Структура("ТипНоменклатуры, ОсобенностьУчета");
	
	Результат.ТипНоменклатуры = ТипНоменклатуры;
	
	Если ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар") Тогда
		Если ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.БезОсобенностейУчета") Тогда
			Результат.ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ПустаяСсылка");
		Иначе
			Результат.ОсобенностьУчета = ОсобенностьУчета;
		КонецЕсли;
	ИначеЕсли ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга") Тогда
		Результат.ОсобенностьУчета = ОсобенностьУчета;
	Иначе
		Результат.ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ПустаяСсылка");
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипНоменклатурыПриИзменении(Элемент)
	
	Если ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар") Тогда
		ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.БезОсобенностейУчета");
	ИначеЕсли ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга") Тогда
		ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ОрганизациейПродавцом");
	Иначе
		ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОсобенностьУчетаТовараПриИзменении(Элемент)
	
	ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар");
	
КонецПроцедуры

&НаКлиенте
Процедура ОсобенностьУчетаУслугиПриИзменении(Элемент)
	
	ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ИмяЭлементаОсобенностьУчетаПоЗначениюПеречисления(ЗначениеПеречисления)
	
	Если ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.БезОсобенностейУчета Тогда
		Возврат "ОсобенностьУчетаБезОсобенностейУчета";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.АлкогольнаяПродукция Тогда
		Возврат "ОсобенностьУчетаАлкогольнаяПродукция";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.СодержитДрагоценныеМатериалы Тогда
		Возврат "ОсобенностьУчетаСодержитДрагоценныеМатериалы";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ПродукцияИзНатуральногоМеха Тогда
		Возврат "ОсобенностьУчетаПродукцияИзНатуральногоМеха";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.КиЗГИСМ Тогда
		Возврат "ОсобенностьУчетаКиЗГИСМ";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС Тогда
		Возврат "ОсобенностьУчетаПодконтрольнаяПродукцияВЕТИС";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ОрганизациейПродавцом Тогда
		Возврат "ОсобенностьУчетаОрганизациейПродавцом";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ОрганизациейПоАгентскойСхеме Тогда
		Возврат "ОсобенностьУчетаОрганизациейПоАгентскойСхеме";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.Партнером Тогда
		Возврат "ОсобенностьУчетаПартнером";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция Тогда
		Возврат "ОсобенностьУчетаТабачнаяПродукция";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция Тогда
		Возврат "ОсобенностьУчетаОбувнаяПродукция";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ЛегкаяПромышленность Тогда
		Возврат "ОсобенностьУчетаЛегкаяПромышленность";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС Тогда
		Возврат "ОсобенностьУчетаМолочнаяПродукцияПодконтрольнаяВЕТИС";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.Шины Тогда
		Возврат "ОсобенностьУчетаШины";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.Фотоаппараты Тогда
		Возврат "ОсобенностьУчетаФотоаппараты";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.Духи Тогда
		Возврат "ОсобенностьУчетаДухи";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.Велосипеды Тогда
		Возврат "ОсобенностьУчетаВелосипеды";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.КреслаКоляски Тогда
		Возврат "ОсобенностьУчетаКреслаКоляски";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.АльтернативныйТабак Тогда
		Возврат "ОсобенностьУчетаАльтернативныйТабак";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.УпакованнаяВода Тогда
		Возврат "ОсобенностьУчетаУпакованнаяВода";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияБезВЕТИС Тогда
		Возврат "ОсобенностьУчетаМолочнаяПродукцияБезВЕТИС";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.Антисептики Тогда
		Возврат "ОсобенностьУчетаАнтисептики";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.БАДы Тогда
		Возврат "ОсобенностьУчетаБАДы";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.НикотиносодержащаяПродукция Тогда
		Возврат "ОсобенностьУчетаНикотиносодержащаяПродукция";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.Пиво Тогда
		Возврат "ОсобенностьУчетаПиво";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.СоковаяПродукция Тогда
		Возврат "ОсобенностьУчетаСоковаяПродукция";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.БезалкогольноеПиво Тогда
		Возврат "ОсобенностьУчетаБезалкогольноеПиво";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.Зерно Тогда
		Возврат "ОсобенностьУчетаЗерно";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗерна Тогда
		Возврат "ОсобенностьУчетаПродуктыПереработкиЗерна";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ЗерноВЕТИС Тогда
		Возврат "ОсобенностьУчетаЗерноВЕТИС";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗернаВЕТИС Тогда
		Возврат "ОсобенностьУчетаПродуктыПереработкиЗернаВЕТИС";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН Тогда
		Возврат "ОсобенностьУчетаПодконтрольнаяПродукцияСАТУРН";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС Тогда
		Возврат "ОсобенностьУчетаМорепродуктыПодконтрольныеВЕТИС";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхБезВЕТИС Тогда
		Возврат "ОсобенностьУчетаКормаДляЖивотныхБезВЕТИС";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС Тогда
		Возврат "ОсобенностьУчетаКормаДляЖивотныхПодконтрольныеВЕТИС";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС Тогда
		Возврат "ОсобенностьУчетаМясоПодконтрольноеВЕТИС";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ВетеринарныеПрепараты Тогда
		Возврат "ОсобенностьУчетаВетеринарныеПрепараты";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ИгрыИИгрушкиДляДетей Тогда
		Возврат "ОсобенностьУчетаИгрыИИгрушкиДляДетей";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.РадиоэлектроннаяПродукция Тогда
		Возврат "ОсобенностьУчетаРадиоэлектроннаяПродукция";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ТитановаяМеталлопродукция Тогда
		Возврат "ОсобенностьУчетаТитановаяМеталлопродукция";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияБезВЕТИС Тогда
		Возврат "ОсобенностьУчетаКонсервированнаяПродукцияБезВЕТИС";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС Тогда
		Возврат "ОсобенностьУчетаКонсервированнаяПродукцияПодконтрольнаяВЕТИС";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.РастительныеМасла Тогда
		Возврат "ОсобенностьУчетаРастительныеМасла";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ОптоволокноИОптоволоконнаяПродукция Тогда
		Возврат "ОсобенностьУчетаОптоволокноИОптоволоконнаяПродукция";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ПарфюмерныеИКосметическиеСредстваИБытоваяХимия Тогда
		Возврат "ОсобенностьУчетаПарфюмерныеИКосметическиеСредстваИБытоваяХимия";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ПечатнаяПродукция Тогда
		Возврат "ОсобенностьУчетаПечатнаяПродукция";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ОтопительныеПриборы Тогда
		Возврат "ОсобенностьУчетаОтопительныеПриборы";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.СтроительныеМатериалы Тогда
		Возврат "ОсобенностьУчетаСтроительныеМатериалы";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ПиротехническиеИзделияИСредстваПожарнойБезопасности Тогда
		Возврат "ОсобенностьУчетаПиротехническиеИзделияИСредстваПожарнойБезопасности";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.АлкогольнаяПродукцияДо9Процентов Тогда
		Возврат "ОсобенностьУчетаАлкогольнаяПродукцияДо9Процентов";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ТелефоныИНоутбуки Тогда
		Возврат "ОсобенностьУчетаТелефоныИНоутбуки";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.КабельнаяПродукция Тогда
		Возврат "ОсобенностьУчетаКабельнаяПродукция";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.Бакалея Тогда
		Возврат "ОсобенностьУчетаБакалея";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.МоторныеМасла Тогда
		Возврат "ОсобенностьУчетаМоторныеМасла";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.БезалкогольныеНапитки Тогда
		Возврат "ОсобенностьУчетаБезалкогольныеНапитки";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ПивоВПотребительскихУпаковках Тогда
		Возврат "ОсобенностьУчетаПивоВПотребительскихУпаковках";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.ТехническиеСредстваРеабилитации Тогда
		Возврат "ОсобенностьУчетаТехническиеСредстваРеабилитации";
	ИначеЕсли ЗначениеПеречисления = Перечисления.ОсобенностиУчетаНоменклатуры.МедицинскиеИзделия Тогда
		Возврат "ОсобенностьУчетаМедицинскиеИзделия";
	Иначе
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не определен элемент формы для %1'"), ЗначениеПеречисления);
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ЭлементТипНоменклатурыПоСуффиксу(Суффикс)
	
	Если Суффикс = "Набор" Тогда
		Возврат Элементы.ТипНоменклатурыНабор;
	ИначеЕсли Суффикс = "МногооборотнаяТара" Тогда
		Возврат Элементы.ТипНоменклатурыМногооборотнаяТара;	
	ИначеЕсли Суффикс = "Работа" Тогда
		Возврат Элементы.ТипНоменклатурыРабота;
	ИначеЕсли Суффикс = "Товар" Тогда
		Возврат Элементы.ТипНоменклатурыТовар;
	ИначеЕсли Суффикс = "Услуга" Тогда
		Возврат Элементы.ТипНоменклатурыУслуга;
	Иначе
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не определены элемент формы для типа номенклатуры %1'"), Суффикс);
	КонецЕсли;
	
КонецФункции

#КонецОбласти


