// @strict-types

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Доверенности = Новый Соответствие();
	
	Для Каждого Ошибка Из Параметры.ОшибкиПроверкиПолномочий Цикл
		
		ПараметрыПоиска = Новый Структура("Документ, Ссылка", Ошибка.ЭлектронныйДокумент, Ошибка.Доверенность);
		НайденныеСтроки = ТаблицаОшибок.НайтиСтроки(ПараметрыПоиска);
		Если НайденныеСтроки.Количество() > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТаблицы = ТаблицаОшибок.Добавить();
		СтрокаТаблицы.Ссылка = Ошибка.Доверенность;
		СтрокаТаблицы.ТекстОшибки = Ошибка.ТекстОшибки;
		
		Если ТипЗнч(СтрокаТаблицы.Ссылка) = Тип("СправочникСсылка.МЧД003") Тогда
			СтрокаТаблицы.Полномочия = Справочники.МЧД003.ТекстПолномочий(СтрокаТаблицы.Ссылка);
		Иначе
			СтрокаТаблицы.Полномочия = МашиночитаемыеДоверенности.ТекстПолномочий(, СтрокаТаблицы.Ссылка);
		КонецЕсли;
		
		СтрокаТаблицы.Документ = Ошибка.ЭлектронныйДокумент;
		СвойстваМЧД = МашиночитаемыеДоверенности.НовыеСвойстваДоверенности();
		СвойстваМЧД.Ссылка = СтрокаТаблицы.Ссылка;
		Если ТипЗнч(СтрокаТаблицы.Ссылка) = Тип("СправочникСсылка.МЧД003") Тогда
			СвойстваСтрокой = "ДатаВыдачи, СтатусВРеестреФНС, Верна, ПолномочияОграничены, ВариантЗаполненияПолномочий";
		Иначе
			СвойстваСтрокой = "ДатаВыдачи, ДатаОкончания,
				|Отозвана, ДатаОтзыва, СтатусВРеестреФНС, Верна, ПолномочияОграничены, ВариантЗаполненияПолномочий";
		КонецЕсли;
		
		ДанныеМЧД = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаТаблицы.Ссылка, СвойстваСтрокой);
		ЗаполнитьЗначенияСвойств(СвойстваМЧД, ДанныеМЧД);
		СвойстваМЧД.ПравилоПроверки =
			РегистрыСведений.ПравилаПроверкиПолномочийПоМЧД.ПравилоПроверки(СтрокаТаблицы.Ссылка).Правило;
		СвойстваМЧД.ПолномочияУказаныИзКлассификатора = МашиночитаемыеДоверенности.ПолномочияМЧДУказаныИзКлассификатора(
			СтрокаТаблицы.Ссылка,
			ДанныеМЧД.ВариантЗаполненияПолномочий);
		
		СтрокаТаблицы.ДоверенностьНеНастроена =
			НЕ МашиночитаемыеДоверенности.ВозможнаАвтопроверкаПолномочий(СвойстваМЧД);
			
		Если Доверенности[Ошибка.Доверенность] = Неопределено Тогда
			Доверенности.Вставить(Ошибка.Доверенность, Истина);
		КонецЕсли;
		
	КонецЦикла;
	
	ТекстПредупреждения = НСтр("ru = 'Сертификатом <a href = ""%1"">%2</a> можно подписать только по доверенности.
		|Найдены действующие доверенности (%3), но они не подходят по полномочиям.'");
	ТекстПредупреждения = СтроковыеФункции.ФорматированнаяСтрока(ТекстПредупреждения,
		ПолучитьНавигационнуюСсылку(Параметры.Сертификат), Параметры.Сертификат, Доверенности.Количество());
	Элементы.ТекстПредупреждения1.Заголовок = ТекстПредупреждения;
	
	ТекстПредупреждения = НСтр("ru = 'Что делать:'");
	Элементы.ТекстПредупреждения2.Заголовок = ТекстПредупреждения;
	
	ТекстРекомендации = НСтр("ru = 'настройте правила проверки полномочий;'");
	Элементы.ТекстРекомендации1.Заголовок = ТекстРекомендации;
	
	ТекстРекомендации = НСтр("ru = 'либо оформите подходящую доверенность;'");
	Элементы.ТекстРекомендации2.Заголовок = ТекстРекомендации;
	
	ТекстРекомендации = НСтр("ru = 'либо передайте документ на подпись руководителю.'");
	Элементы.ТекстРекомендации3.Заголовок = ТекстРекомендации;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаОшибок

&НаКлиенте
Процедура ОшибкиПроверкиПолномочийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ТаблицаОшибокСсылка ИЛИ Поле = Элементы.ТаблицаОшибокПолномочия Тогда
		ПоказатьЗначение(, ТаблицаОшибок[ВыбраннаяСтрока].Ссылка);
	ИначеЕсли Поле = Элементы.ТаблицаОшибокДокумент Тогда
		ПоказатьЗначение(, ТаблицаОшибок[ВыбраннаяСтрока].Документ);
	ИначеЕсли Поле = Элементы.ТаблицаОшибокТекстОшибки Тогда
		ПоказатьПредупреждение(, ТаблицаОшибок[ВыбраннаяСтрока].ТекстОшибки,,
			Элемент.ПодчиненныеЭлементы.ТаблицаОшибокТекстОшибки.Заголовок);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра(
		"ЦветФона", ЦветаСтиля.ЦветФонаНедействительнаяМЧД);
	
	ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаОшибок.ДоверенностьНеНастроена");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Истина;
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ТаблицаОшибокТекстОшибки");
	
КонецПроцедуры

#КонецОбласти
