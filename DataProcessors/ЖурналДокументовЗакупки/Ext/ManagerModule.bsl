#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт

КонецПроцедуры

// Определяет состав документов и хозяйственных операций, доступных для отображения в рабочем месте.
//
// Параметры:
//  ОтборХозяйственныеОперации		 - СписокЗначений - список значений типа ПеречислениеСсылка.ХозяйственныеОперации
//  ОтборТипыДокументов				 - СписокЗначений - список значений типа СправочникСсылка.ИдентификаторыОбъектовМетаданных
//  КлючНазначенияИспользования		 - Строка - ключ рабочего места для которого вызывается функция.
// 
// Возвращаемое значение:
//   См. ОбщегоНазначенияУТ.НоваяТаблицаХозяйственныеОперацииИДокументы
//
Функция ИнициализироватьХозяйственныеОперацииИДокументы(ОтборХозяйственныеОперации,
		ОтборТипыДокументов, КлючНазначенияИспользования) Экспорт
		
	ХозяйственныеОперацииИДокументы = ХозяйственныеОперацииИДокументы();
		
	Если КлючНазначенияИспользования = "ДокументыЗакупки" Тогда
		КлючНастроек = "";
	Иначе
		КлючНастроек = КлючНазначенияИспользования;
	КонецЕсли;
	
	ТаблицаЗначенийДоступно = ОбщегоНазначенияУТ.ДоступныеХозяйственныеОперацииИДокументы(ХозяйственныеОперацииИДокументы,
		ОтборХозяйственныеОперации, ОтборТипыДокументов, КлючНастроек);
		
	ИспользоватьАктыРасхожденийПослеПриемкиПоПриемкамТоваровНаХранение = Ложь;
	ИспользоватьАктыРасхожденийПослеОтгрузкиПоОтгрузкамТоваровСХранения = Ложь;
	ИспользоватьАктыРасхожденийПослеПриемкиПоПриемкамТоваровНаХранение = ПолучитьФункциональнуюОпцию(
		"ИспользоватьАктыРасхожденийПослеПриемки");
	ИспользоватьАктыРасхожденийПослеОтгрузкиПоОтгрузкамТоваровСХранения = ПолучитьФункциональнуюОпцию(
		"ИспользоватьАктыРасхожденийПослеОтгрузки");
	ИспользоватьУправленческуюОрганизацию = ПолучитьФункциональнуюОпцию(
		"ИспользоватьУправленческуюОрганизацию");
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьАктыРасхожденийПослеПриемки") Тогда
		СтрокиКУдалению = ТаблицаЗначенийДоступно.НайтиСтроки(Новый Структура("ПолноеИмяДокумента",
			Метаданные.Документы.АктОРасхожденияхПослеПриемки.ПолноеИмя()));
		Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
			Если СтрокаКУдалению.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи Тогда
				ТаблицаЗначенийДоступно.Удалить(СтрокаКУдалению);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не ИспользоватьАктыРасхожденийПослеПриемкиПоПриемкамТоваровНаХранение Тогда
		СтрокиКУдалению = ТаблицаЗначенийДоступно.НайтиСтроки(Новый Структура("ПолноеИмяДокумента, ХозяйственнаяОперация",
																	Метаданные.Документы.АктОРасхожденияхПослеПриемки.ПолноеИмя(),
																	Перечисления.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи));
		Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
			ТаблицаЗначенийДоступно.Удалить(СтрокаКУдалению);
		КонецЦикла;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьАктыРасхожденийПослеОтгрузки") Тогда
		СтрокиКУдалению = ТаблицаЗначенийДоступно.НайтиСтроки(Новый Структура("ПолноеИмяДокумента",
			Метаданные.Документы.АктОРасхожденияхПослеОтгрузки.ПолноеИмя()));
		Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
			Если СтрокаКУдалению.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ОтгрузкаПринятыхСПравомПродажиТоваровСХранения Тогда
				ТаблицаЗначенийДоступно.Удалить(СтрокаКУдалению);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не ИспользоватьАктыРасхожденийПослеОтгрузкиПоОтгрузкамТоваровСХранения Тогда
		СтрокиКУдалению = ТаблицаЗначенийДоступно.НайтиСтроки(Новый Структура("ПолноеИмяДокумента, ХозяйственнаяОперация",
																	Метаданные.Документы.АктОРасхожденияхПослеОтгрузки.ПолноеИмя(),
																	Перечисления.ХозяйственныеОперации.ОтгрузкаПринятыхСПравомПродажиТоваровСХранения));
		Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
			ТаблицаЗначенийДоступно.Удалить(СтрокаКУдалению);
		КонецЦикла;
	КонецЕсли;
	
	Если Не ИспользоватьУправленческуюОрганизацию Тогда
		СтрокиКУдалению = ТаблицаЗначенийДоступно.НайтиСтроки(Новый Структура("ПолноеИмяДокумента, ХозяйственнаяОперация",
																	Метаданные.Документы.ПриобретениеТоваровУслуг.ПолноеИмя(),
																	Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет));
		Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
			ТаблицаЗначенийДоступно.Удалить(СтрокаКУдалению);
		КонецЦикла;
	КонецЕсли;
		
	Возврат ТаблицаЗначенийДоступно;
	
КонецФункции

Функция ХозяйственныеОперацииИДокументы()
	
	ХозяйственныеОперацииИДокументы = ОбщегоНазначенияУТ.НоваяТаблицаХозяйственныеОперацииИДокументы();
	
	// ПриобретениеТоваровУслуг
	
	СтрокаПриобретениеТоваровУслуг = ХозяйственныеОперацииИДокументы.Добавить();
	Строка = СтрокаПриобретениеТоваровУслуг;
	Строка.КлючНазначенияИспользования 	= "Накладные";
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика;
	Строка.ДобавитьКнопкуСоздать		= Истина;
	Строка.ТипДокумента 				= Тип("ДокументСсылка.ПриобретениеТоваровУслуг");
	Строка.ПолноеИмяДокумента			= "Документ.ПриобретениеТоваровУслуг";
	Строка.ЗаголовокРабочегоМеста = НСтр("ru = 'Документы закупки (накладные по приемке)'");
	Строка.МенеджерРасчетаГиперссылкиКОформлению = "Обработка.ЖурналДокументовЗакупки";
	
	СтрокаЗакупкаЧерезПодотчетноеЛицо = ХозяйственныеОперацииИДокументы.Добавить();
	СтрокаЗакупкаЧерезПодотчетноеЛицо.ХозяйственнаяОперация                 = Перечисления.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо;
	СтрокаЗакупкаЧерезПодотчетноеЛицо.ДобавитьКнопкуСоздать                 = Истина;
	СтрокаЗакупкаЧерезПодотчетноеЛицо.ТипДокумента                          = Тип("ДокументСсылка.ПриобретениеТоваровУслуг");
	СтрокаЗакупкаЧерезПодотчетноеЛицо.ПолноеИмяДокумента                    = "Документ.ПриобретениеТоваровУслуг";
	СтрокаЗакупкаЧерезПодотчетноеЛицо.КлючНазначенияИспользования           = "ЗакупкаЧерезПодотчетноеЛицо";
	СтрокаЗакупкаЧерезПодотчетноеЛицо.ЗаголовокРабочегоМеста                = НСтр("ru = 'Документы закупки через подотчетное лицо'");
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаПриобретениеТоваровУслуг);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ПриемНаКомиссию;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаПриобретениеТоваровУслуг);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаПриобретениеТоваровУслуг);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаПриобретениеТоваровУслуг);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаПриобретениеТоваровУслуг);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаПриобретениеТоваровУслуг);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаПриобретениеТоваровУслуг);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаПриобретениеТоваровУслуг);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаПриобретениеТоваровУслуг);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути;
	
	// АктОРасхожденияхПослеОтгрузки
	
	СтрокаАктОРасхожденияхПослеОтгрузки = ХозяйственныеОперацииИДокументы.Добавить();
	Строка = СтрокаАктОРасхожденияхПослеОтгрузки;
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ВозвратТоваровПоставщику;
	Строка.ДобавитьКнопкуСоздать		= Истина;
	Строка.ТипДокумента 				= Тип("ДокументСсылка.АктОРасхожденияхПослеОтгрузки");
	Строка.ПолноеИмяДокумента			= "Документ.АктОРасхожденияхПослеОтгрузки";
	Строка.ИспользуютсяСтатусы          = Истина;
	Строка.КлючНазначенияИспользования 	= "АктОРасхожденияхПослеОтгрузки";
	Строка.ЗаголовокРабочегоМеста = НСтр("ru = 'Документы закупки (расхождения)'");
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаАктОРасхожденияхПослеОтгрузки);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ВозвратТоваровКомитенту;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаАктОРасхожденияхПослеОтгрузки);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ОтгрузкаПринятыхСПравомПродажиТоваровСХранения;
	
	// АктОРасхожденияхПослеПриемки
	
	СтрокаАктОРасхожденияхПослеПриемки = ХозяйственныеОперацииИДокументы.Добавить();
	Строка = СтрокаАктОРасхожденияхПослеПриемки;
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика;
	Строка.ДобавитьКнопкуСоздать		= Истина;
	Строка.ТипДокумента 				= Тип("ДокументСсылка.АктОРасхожденияхПослеПриемки");
	Строка.ПолноеИмяДокумента			= "Документ.АктОРасхожденияхПослеПриемки";
	Строка.ИспользуютсяСтатусы          = Истина;
	Строка.КлючНазначенияИспользования 	= "АктОРасхожденияхПослеПриемки";
	Строка.ЗаголовокРабочегоМеста = НСтр("ru = 'Документы закупки (расхождения)'");
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаАктОРасхожденияхПослеПриемки);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаАктОРасхожденияхПослеПриемки);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ПриемНаКомиссию;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаАктОРасхожденияхПослеПриемки);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаАктОРасхожденияхПослеПриемки);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаАктОРасхожденияхПослеПриемки);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаАктОРасхожденияхПослеПриемки);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаАктОРасхожденияхПослеПриемки);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаАктОРасхожденияхПослеПриемки);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаАктОРасхожденияхПослеПриемки);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки;
	
	// КорректировкиПоступлений
	
	СтрокаКорректировкаПриобретения = ХозяйственныеОперацииИДокументы.Добавить();
	Строка = СтрокаКорректировкаПриобретения;
	Строка.КлючНазначенияИспользования 	= "КорректировкиПоступлений";
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ИсправлениеОшибок;
	Строка.ДобавитьКнопкуСоздать		= Истина;
	Строка.ТипДокумента 				= Тип("ДокументСсылка.КорректировкаПриобретения");
	Строка.ПолноеИмяДокумента			= "Документ.КорректировкаПриобретения";
	Строка.ЗаголовокРабочегоМеста = НСтр("ru = 'Документы закупки (корректировки)'");
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаКорректировкаПриобретения);
	Строка.ХозяйственнаяОперация		= Перечисления.ХозяйственныеОперации.КорректировкаПоСогласованиюСторон;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаКорректировкаПриобретения);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.КорректировкаПриобретенияУменьшениеЗадолженностиСводно;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаКорректировкаПриобретения);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.КорректировкаПриобретенияУвеличениеЗадолженностиСводно;

	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаКорректировкаПриобретения);
	Строка.ХозяйственнаяОперация		= Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика;
	Строка.ДобавитьКнопкуСоздать		= Ложь;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаКорректировкаПриобретения);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути;
	Строка.ДобавитьКнопкуСоздать		= Ложь;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаКорректировкаПриобретения);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки;
	Строка.ДобавитьКнопкуСоздать		= Ложь;
	
	// СписаниеРасхожденийПоступлениеПриобретение
	
	СтрокаСписаниеРасхожденийПоступлениеПриобретение = ХозяйственныеОперацииИДокументы.Добавить();
	Строка = СтрокаСписаниеРасхожденийПоступлениеПриобретение;
	Строка.КлючНазначенияИспользования 	= "СписаниеРасхожденийПоступлениеПриобретение";
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.СписаниеРасхожденийПоступлениеПриобретение;
	Строка.ДобавитьКнопкуСоздать		= Истина;
	Строка.ТипДокумента 				= Тип("ДокументСсылка.СписаниеРасхожденийПоступлениеПриобретение");
	Строка.ПолноеИмяДокумента			= "Документ.СписаниеРасхожденийПоступлениеПриобретение";
	Строка.ЗаголовокРабочегоМеста = НСтр("ru = 'Документы закупки (корректировки)'");
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		Строка = ХозяйственныеОперацииИДокументы.Добавить();
		ЗаполнитьЗначенияСвойств(Строка,СтрокаКорректировкаПриобретения);
		Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.КорректировкаДоВводаОстатков;
	КонецЕсли;
	
	// ПриобретениеУслугПрочихАктивов
	
	СтрокаПоступлениеУслуг = ХозяйственныеОперацииИДокументы.Добавить();
	Строка = СтрокаПоступлениеУслуг;
	Строка.КлючНазначенияИспользования 	= "ПоступленияУслугПрочихАктивов";
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика;
	Строка.ДобавитьКнопкуСоздать		= Истина;
	Строка.ТипДокумента 				= Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов");
	Строка.ПолноеИмяДокумента			= "Документ.ПриобретениеУслугПрочихАктивов";
	Строка.ЗаголовокРабочегоМеста = НСтр("ru = 'Документы закупки (накладные)'");
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаПоступлениеУслуг);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо;
	
	// ВозвратТоваровПоставщику
	
	СтрокаВозвратТоваровПоставщику = ХозяйственныеОперацииИДокументы.Добавить();
	
	Строка                                       = СтрокаВозвратТоваровПоставщику;
	Строка.КлючНазначенияИспользования           = "ВозвратТоваровПоставщику";
	Строка.ХозяйственнаяОперация                 = Перечисления.ХозяйственныеОперации.ВозвратТоваровПоставщику;
	Строка.ДобавитьКнопкуСоздать                 = Истина;
	Строка.ТипДокумента                          = Тип("ДокументСсылка.ВозвратТоваровПоставщику");
	Строка.ПолноеИмяДокумента                    = "Документ.ВозвратТоваровПоставщику";
	Строка.ЗаголовокРабочегоМеста                = НСтр("ru = 'Документы закупки (возвраты поставщикам)'");
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка, СтрокаВозвратТоваровПоставщику);
	Строка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратТоваровКомитенту;
	
	#Область ПриемТоваровНаХранение
	
	// ПриемкаТоваровНаХранение
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	Строка.КлючНазначенияИспользования 	= "ПриемкаТоваровНаХранение";
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи;
	Строка.ДобавитьКнопкуСоздать		= Истина;
	Строка.ТипДокумента 				= Тип("ДокументСсылка.ПриемкаТоваровНаХранение");
	Строка.ПолноеИмяДокумента			= "Документ.ПриемкаТоваровНаХранение";
	Строка.ЗаголовокРабочегоМеста = НСтр("ru = 'Документы закупки (приемка на хранение)'");
	Строка.МенеджерРасчетаГиперссылкиКОформлению = "Обработка.ЖурналДокументовЗакупки";
	
	// ОтгрузкаТоваровСХранения
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	Строка.КлючНазначенияИспользования = "ОтгрузкаТоваровСХранения";
	Строка.ХозяйственнаяОперация       = Перечисления.ХозяйственныеОперации.ОтгрузкаПринятыхСПравомПродажиТоваровСХранения;
	Строка.ДобавитьКнопкуСоздать       = Истина;
	Строка.ТипДокумента                = Тип("ДокументСсылка.ОтгрузкаТоваровСХранения");
	Строка.ПолноеИмяДокумента          = "Документ.ОтгрузкаТоваровСХранения";
	Строка.ЗаголовокРабочегоМеста      = НСтр("ru = 'Документы закупки (отгрузки с хранения)'");
	
	// ВыкупПринятыхНаХранениеТоваров
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	Строка.КлючНазначенияИспользования           = "ВыкупПринятыхНаХранениеТоваров";
	Строка.ХозяйственнаяОперация                 = Перечисления.ХозяйственныеОперации.ВыкупПринятыхНаХранениеТоваров;
	Строка.ДобавитьКнопкуСоздать                 = Истина;
	Строка.ТипДокумента                          = Тип("ДокументСсылка.ВыкупПринятыхНаХранениеТоваров");
	Строка.ПолноеИмяДокумента                    = "Документ.ВыкупПринятыхНаХранениеТоваров";
	Строка.ЗаголовокРабочегоМеста                = НСтр("ru = 'Документы закупки (выкупы товаров)'");
	Строка.МенеджерРасчетаГиперссылкиКОформлению = "Документ.ВыкупПринятыхНаХранениеТоваров";
	
	// ОтчетОСписанииТоваровСХранения
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	Строка.КлючНазначенияИспользования           = "ОтчетОСписанииТоваровСХранения";
	Строка.ХозяйственнаяОперация                 = Перечисления.ХозяйственныеОперации.СписаниеПринятыхТоваровНаРасходы;
	Строка.ДобавитьКнопкуСоздать                 = Истина;
	Строка.ТипДокумента                          = Тип("ДокументСсылка.ОтчетОСписанииТоваровСХранения");
	Строка.ПолноеИмяДокумента                    = "Документ.ОтчетОСписанииТоваровСХранения";
	Строка.ЗаголовокРабочегоМеста                = НСтр("ru = 'Документы закупки (списания с хранения)'");
	Строка.МенеджерРасчетаГиперссылкиКОформлению = "Документ.ОтчетОСписанииТоваровСХранения";
	
	СтрокаСписание = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаСписание, Строка);
	СтрокаСписание.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеПринятыхТоваровЗаСчетПоклажедателя;
	
	#КонецОбласти
	
	// ПоступлениеТоваров
	
	СтрокаПоступлениеТоваров = ХозяйственныеОперацииИДокументы.Добавить();
	Строка = СтрокаПоступлениеТоваров;
	Строка.КлючНазначенияИспользования 	= "Накладные";
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаПоступлениеИзТоваровВПути;
	Строка.ДобавитьКнопкуСоздать		= Истина;
	Строка.ТипДокумента 				= Тип("ДокументСсылка.ПоступлениеТоваровНаСклад");
	Строка.ПолноеИмяДокумента			= "Документ.ПоступлениеТоваровНаСклад";
	Строка.ЗаголовокРабочегоМеста = НСтр("ru = 'Документы закупки (накладные по приемке)'");
	Строка.МенеджерРасчетаГиперссылкиКОформлению = "Обработка.ЖурналДокументовЗакупки";
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаПоступлениеТоваров);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаНеотфактурованнаяПоставка;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаПоступлениеТоваров);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаПоступлениеТоваров);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСПоступлениеИзТоваровВПути;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаПоступлениеТоваров);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСНеотфактурованнаяПоставка;
	
	
	СтрокаТаможенныеДекларацииИмпорт = ХозяйственныеОперацииИДокументы.Добавить();
	Строка = СтрокаТаможенныеДекларацииИмпорт;
	Строка.КлючНазначенияИспользования 	= "ТаможенныеДекларацииИмпорт";
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту;
	Строка.ДобавитьКнопкуСоздать		= Истина;
	Строка.ТипДокумента 				= Тип("ДокументСсылка.ТаможеннаяДекларацияИмпорт");
	Строка.ПолноеИмяДокумента			= "Документ.ТаможеннаяДекларацияИмпорт";
	Строка.ИспользуютсяСтатусы          = Истина;
	Строка.ЗаголовокРабочегоМеста = НСтр("ru = 'Документы закупки (ГТД)'");
	Строка.МенеджерРасчетаГиперссылкиКОформлению = "Документ.ТаможеннаяДекларацияИмпорт";
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаТаможенныеДекларацииИмпорт);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаТаможенныеДекларацииИмпорт);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути;
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка,СтрокаТаможенныеДекларацииИмпорт);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки;
	
	// ПоступлениеУслугВПодразделение
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	Строка.КлючНазначенияИспользования 	= "Накладные";
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаНеотфактурованнаяПоставка;
	Строка.ДобавитьКнопкуСоздать		= Истина;
	Строка.ТипДокумента 				= Тип("ДокументСсылка.ПоступлениеУслугВПодразделение");
	Строка.ПолноеИмяДокумента			= "Документ.ПоступлениеУслугВПодразделение";
	Строка.ЗаголовокРабочегоМеста = НСтр("ru = 'Документы закупки (накладные по приемке)'");
	Строка.МенеджерРасчетаГиперссылкиКОформлению = "Обработка.ЖурналДокументовЗакупки";
	
	УчетНДСУП.ОписаниеОперацийИТиповДокументовДляЖурналаДокументовЗакупки(ХозяйственныеОперацииИДокументы);
	
	Возврат ХозяйственныеОперацииИДокументы;
	
КонецФункции

// Ключ назначения формы по умолчанию.
// 
// Возвращаемое значение:
//  Строка - идентификатор форм обработки "ЖурналДокументовЗакупки" по умолчанию.
Функция КлючНазначенияФормыПоУмолчанию() Экспорт
	
	Возврат "ДокументыЗакупки";
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Параметры:
// 	Команды - см. ПодключаемыеКомандыПереопределяемый.ПриОпределенииКомандПодключенныхКОбъекту.Команды
Процедура ПриОпределенииКомандПодключенныхКОбъекту(Команды) Экспорт
	
	МассивКомандОтчетов = Команды.НайтиСтроки(Новый Структура("Вид", "Отчеты"));
	МассивКомандПечати = Команды.НайтиСтроки(Новый Структура("Вид", "Печать"));
	
	МенеджерСостояниеВыполненияДокументов = Метаданные.Отчеты.СостояниеВыполненияДокументов.ПолноеИмя();
	МенеджерСостояниеРасчетовСПоставщиками = Метаданные.Отчеты.СостояниеРасчетовСПоставщиками.ПолноеИмя();
	МенеджерКарточкаРасчетовСПоставщиками = Метаданные.Отчеты.КарточкаРасчетовСПоставщиками.ПолноеИмя();
	МенеджерКарточкаРасчетовПоПринятойВозвратнойТаре = Метаданные.Отчеты.КарточкаРасчетовПоПринятойВозвратнойТаре.ПолноеИмя();
	МенеджерОтклоненияОтУсловийЗакупок = Метаданные.Отчеты.ОтклоненияОтУсловийЗакупок.ПолноеИмя();
	МенеджерАнализЦенПоставщиков = Метаданные.Отчеты.АнализЦенПоставщиков.ПолноеИмя();
	
	Для каждого ТекКоманда Из МассивКомандОтчетов Цикл
		Если ТекКоманда.Менеджер = МенеджерСостояниеВыполненияДокументов Тогда
			ТекКоманда.Порядок = 1;
			ТекКоманда.ВидимостьВФормах = "СписокДокументов, ФормаСозданныеДокументы";
		КонецЕсли;
		Если ТекКоманда.Менеджер = МенеджерСостояниеРасчетовСПоставщиками Тогда
			ТекКоманда.Важность = "Обычное";
			ТекКоманда.Порядок = 1;
		КонецЕсли;
		Если ТекКоманда.Менеджер = МенеджерКарточкаРасчетовСПоставщиками Тогда
			ТекКоманда.Важность = "Обычное";
			ТекКоманда.Порядок = 2;
		КонецЕсли;
		Если ТекКоманда.Менеджер = МенеджерКарточкаРасчетовПоПринятойВозвратнойТаре Тогда
			ТекКоманда.Важность = "Обычное";
			ТекКоманда.Порядок = 3;
		КонецЕсли;
		Если ТекКоманда.Менеджер = МенеджерОтклоненияОтУсловийЗакупок Тогда
			ТекКоманда.Важность = "СмТакже";
			ТекКоманда.Порядок = 1;
		КонецЕсли;
		Если ТекКоманда.Менеджер = МенеджерАнализЦенПоставщиков Тогда
			ТекКоманда.Важность = "СмТакже";
			ТекКоманда.Порядок = 2;
		КонецЕсли;
	КонецЦикла;
	
	ЖурналДокументовЗакупкиЛокализация.ПриОпределенииКомандПодключенныхКОбъекту(Команды);
	
	Для каждого ТекКоманда Из МассивКомандПечати Цикл
		ТекКоманда.ВидимостьВФормах = "СписокДокументов";
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьГиперссылкуКОформлениюФоновоеЗадание(Параметры, АдресХранилища) Экспорт

	Результат = Новый Структура("КОформлению, СмТакжеВРаботе");

	Результат.КОформлению = ОбщегоНазначенияУТ.СформироватьГиперссылкуКОформлению(Параметры[0], Параметры[1]);
	
	МассивМенеджеровРасчетаСмТакжеВРаботе = Новый Массив();
	МассивМенеджеровРасчетаСмТакжеВРаботе.Добавить("Документ.ДоверенностьВыданная");
	МассивМенеджеровРасчетаСмТакжеВРаботе.Добавить("Документ.ЗаказПоставщику");
	МассивМенеджеровРасчетаСмТакжеВРаботе.Добавить("Обработка.ФормированиеПередачТоваровМеждуОрганизациямиИВыкупов");
	
	ЖурналДокументовЗакупкиЛокализация.ПриОпределенииИсточниковКОформлению(МассивМенеджеровРасчетаСмТакжеВРаботе);
	
	Результат.СмТакжеВРаботе = ОбщегоНазначенияУТ.СформироватьГиперссылкуСмТакжеВРаботе(МассивМенеджеровРасчетаСмТакжеВРаботе, Параметры[1]);

	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

#Область ФормированиеГиперссылкиВЖурналеЗакупок 

Функция ТекстЗапросаЗаказыКОформлению(Организация = Неопределено, Склад = Неопределено, МассивСсылок = Неопределено)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1 РАЗРЕШЕННЫЕ
	|	ЗаказыПоставщикам.ЗаказПоставщику КАК Ссылка
	|ИЗ
	|	РегистрНакопления.ЗаказыПоставщикам.Остатки(, ЗаказПоставщику ССЫЛКА Документ.ЗаказПоставщику И &УсловиеОтбора) КАК ЗаказыПоставщикам
	|ГДЕ
	|	ЗаказыПоставщикам.КОформлениюОстаток > 0";
	
	УсловиеОтбора = "ИСТИНА";
	Если ЗначениеЗаполнено(Организация) Тогда
		УсловиеОтбора = УсловиеОтбора + "
		|И ЗаказПоставщику.Организация = &Организация";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Склад) Тогда
		УсловиеОтбора = УсловиеОтбора + "
		|И ЗаказПоставщику.Склад = &Склад";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МассивСсылок) Тогда
		УсловиеОтбора = УсловиеОтбора + "
		|И ЗаказПоставщику.Ссылка В (&МассивСсылок)";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&УсловиеОтбора", УсловиеОтбора);
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаРаспоряженияНаПриемку(Организация = Неопределено, Склад = Неопределено, МассивСсылок = Неопределено)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1 РАЗРЕШЕННЫЕ
	|	ТаблицаРаспоряжений.ДокументПоступления КАК Ссылка,
	|	МАКСИМУМ(ТаблицаРаспоряжений.Склад) КАК Склад,
	|	МАКСИМУМ(ТаблицаРаспоряжений.Номенклатура) КАК Номенклатура,
	|	МАКСИМУМ(ТаблицаРаспоряжений.Характеристика) КАК Характеристика,
	|	МАКСИМУМ(ТаблицаРаспоряжений.Назначение) КАК Назначение,
	|	МАКСИМУМ(ТаблицаРаспоряжений.Серия) КАК Серия,
	|	МАКСИМУМ(ТаблицаРаспоряжений.Отправитель) КАК Отправитель
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Остатки(
	|			,
	|			(ДокументПоступления ССЫЛКА Документ.ЗаказПоставщику
	|				ИЛИ ДокументПоступления ССЫЛКА Документ.ПриобретениеТоваровУслуг
	|				ИЛИ ДокументПоступления ССЫЛКА Документ.ПриемкаТоваровНаХранение
	|				ИЛИ ДокументПоступления ССЫЛКА Справочник.ДоговорыКонтрагентов
	|				ИЛИ ДокументПоступления ССЫЛКА Справочник.СоглашенияСПоставщиками)
	|				И &УсловиеОтбора) КАК ТаблицаРаспоряжений
	|ГДЕ
	|	ТаблицаРаспоряжений.КОформлениюПоступленийПоОрдерамОстаток <> 0
	|	И &УсловиеОтбора
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРаспоряжений.ДокументПоступления";
	
	УсловиеОтбора = "ИСТИНА";
	Если ЗначениеЗаполнено(Организация) Тогда
		УсловиеОтбора = УсловиеОтбора + "
		|И ДокументПоступления.Организация = &Организация";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Склад) Тогда
		УсловиеОтбора = УсловиеОтбора + "
		|И ДокументПоступления.Склад = &Склад";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МассивСсылок) Тогда
		УсловиеОтбора = УсловиеОтбора + "
		|И ДокументПоступления.Ссылка В (&МассивСсылок)";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&УсловиеОтбора", УсловиеОтбора);
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ЕстьЗаказыКОформлению(Организация = Неопределено, Склад = Неопределено) Экспорт
	
	Если НЕ (ПравоДоступа("Чтение", Метаданные.Документы.ЗаказПоставщику)
		И ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ЗаказыПоставщикам)
		И ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыКПоступлению)) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапросаЗаказыКОформлению(Организация, Склад);

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ЕстьРаспоряженияНаПриемку(Организация = Неопределено, Склад = Неопределено) Экспорт
	
	Если НЕ ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыКПоступлению) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапросаРаспоряженияНаПриемку(Организация, Склад);

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	Возврат Не Запрос.Выполнить().Пустой();
		
КонецФункции

Функция СформироватьГиперссылкуКОформлению(Параметры) Экспорт
	
	Организация = Параметры.Организация;
	Склад = Параметры.Склад;
	
	ПоказыватьЗаказы = ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыПоставщикам")
		И ПравоДоступа("Чтение", Метаданные.Документы.ЗаказПоставщику)
		И ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ЗаказыПоставщикам)
		И ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыКПоступлению);
	
	ПоказыватьРаспоряженияНаПриемку = СкладыСервер.ЕстьОрдерныйНаПоступлениеСклад(Склад,ТекущаяДатаСеанса())
		И ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыКПоступлению);
		
	ПоказыватьРаспоряженияНаПоступления = (ПолучитьФункциональнуюОпцию("ИспользоватьНеотфактурованныеПоставки")
		Или ПолучитьФункциональнуюОпцию("ИспользоватьТоварыВПутиОтПоставщиков"))
		И ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыКПоступлению);
		
	ГиперссылкаПоЗаказам = Неопределено;
	ГиперссылкаПоПриемке = Неопределено;
	ГиперссылкаПоПоступлениям = Неопределено;
	
	ТекстГиперссылкаПоПоступлениям = НСтр("ru = 'Поступления'");
	ТекстГиперссылкиПоЗаказам = НСтр("ru = 'Приобретения'");
	ТекстГиперссылкиПоПриемке = НСтр("ru = 'Контроль ордеров'");
	
	ЦветНезаполненный = ЦветаСтиля.НезаполненноеПолеТаблицы;
	ЦветЗаполненный = ЦветаСтиля.ГиперссылкаЦвет;
	
	МассивСтрок = Новый Массив;
	
	Если ПоказыватьЗаказы Тогда
		ЕстьЗаказы = ЕстьЗаказыКОформлению(Организация, Склад);
		ГиперссылкаПоЗаказам = Новый ФорматированнаяСтрока(ТекстГиперссылкиПоЗаказам,,
			?(ЕстьЗаказы,ЦветЗаполненный,ЦветНезаполненный),
			,
			"СтраницаРаспоряженияНаОформление");
		МассивСтрок.Добавить(ГиперссылкаПоЗаказам);
	КонецЕсли;
	
	Если ПоказыватьРаспоряженияНаПоступления Тогда
		ЕстьРаспоряжения = ЕстьРаспоряженияНаПоступление(Организация, Склад);
		ГиперссылкаПоПоступлениям = Новый ФорматированнаяСтрока(ТекстГиперссылкаПоПоступлениям,,
			?(ЕстьРаспоряжения,ЦветЗаполненный,ЦветНезаполненный),
			,
			"СтраницаРаспоряженияНаПоступление");
			
		Если МассивСтрок.Количество() > 0 Тогда
			МассивСтрок.Добавить("; ");
		КонецЕсли;
		МассивСтрок.Добавить(ГиперссылкаПоПоступлениям)
	КонецЕсли;
	
	Если ПоказыватьРаспоряженияНаПриемку Тогда
		ЕстьРаспоряжения = ЕстьРаспоряженияНаПриемку(Организация, Склад);
		ГиперссылкаПоПриемке = Новый ФорматированнаяСтрока(ТекстГиперссылкиПоПриемке,,
			?(ЕстьРаспоряжения,ЦветЗаполненный,ЦветНезаполненный),
			,
			"СтраницаРаспоряженияНаПриемку");
			
		Если МассивСтрок.Количество() > 0 Тогда
			МассивСтрок.Добавить("; ");
		КонецЕсли;
		МассивСтрок.Добавить(ГиперссылкаПоПриемке);
	КонецЕсли;
	
	Если МассивСтрок.Количество() > 0 Тогда
		Возврат Новый ФорматированнаяСтрока(МассивСтрок);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ТекстЗапросаРаспоряженияНаПоступление(Организация = Неопределено, Склад = Неопределено, МассивСсылок = Неопределено)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1 РАЗРЕШЕННЫЕ
	|	ТаблицаРаспоряжений.ДокументПоступления КАК Ссылка,
	|	МАКСИМУМ(ТаблицаРаспоряжений.Склад) КАК Склад,
	|	МАКСИМУМ(ТаблицаРаспоряжений.Номенклатура) КАК Номенклатура,
	|	МАКСИМУМ(ТаблицаРаспоряжений.Характеристика) КАК Характеристика,
	|	МАКСИМУМ(ТаблицаРаспоряжений.Назначение) КАК Назначение,
	|	МАКСИМУМ(ТаблицаРаспоряжений.Серия) КАК Серия,
	|	МАКСИМУМ(ТаблицаРаспоряжений.Отправитель) КАК Отправитель
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Остатки(
	|			,
	|			(ДокументПоступления ССЫЛКА Документ.ЗаказПоставщику
	|				ИЛИ ДокументПоступления ССЫЛКА Документ.ПриобретениеТоваровУслуг
	|				ИЛИ ДокументПоступления ССЫЛКА Документ.ПриемкаТоваровНаХранение
	|				ИЛИ ДокументПоступления ССЫЛКА Справочник.ДоговорыКонтрагентов
	|				ИЛИ ДокументПоступления ССЫЛКА Справочник.СоглашенияСПоставщиками)
	|				И ХозяйственнаяОперация В (&СписокХозОпераций)
	|				И &УсловиеОтбора) КАК ТаблицаРаспоряжений
	|ГДЕ
	|	(ТаблицаРаспоряжений.КОформлениюПоступленийПоНакладнымОстаток <> 0
	|	ИЛИ ТаблицаРаспоряжений.КОформлениюПоступленийПоРаспоряжениюОстаток <> 0)
	|	И &УсловиеОтбора
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРаспоряжений.ДокументПоступления";
	
	УсловиеОтбора = "ИСТИНА";
	Если ЗначениеЗаполнено(Организация) Тогда
		УсловиеОтбора = УсловиеОтбора + "
		|И ДокументПоступления.Организация = &Организация";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Склад) Тогда
		УсловиеОтбора = УсловиеОтбора + "
		|И ДокументПоступления.Склад = &Склад";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МассивСсылок) Тогда
		УсловиеОтбора = УсловиеОтбора + "
		|И ДокументПоступления.Ссылка В (&МассивСсылок)";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&УсловиеОтбора", УсловиеОтбора);
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ЕстьРаспоряженияНаПоступление(Организация = Неопределено, Склад = Неопределено) Экспорт
	
	Если НЕ ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыКПоступлению) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапросаРаспоряженияНаПоступление(Организация, Склад);

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("СписокХозОпераций", ЗакупкиСервер.ХозяйственныеОперацииРаздельнойЗакупкиБезОтборов());
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция СформироватьГиперссылкуСмТакжеВРаботе(Параметры) Экспорт
	
	ХозяйственныеОперацииИДокументы = ХозяйственныеОперацииИДокументы();
	НайденныеСтроки = ХозяйственныеОперацииИДокументы.НайтиСтроки(
		Новый Структура("КлючНазначенияИспользования", Параметры.КлючНазначенияИспользования));
	
	Если НайденныеСтроки.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	//Все строки с одинаковым ключом должны иметь одинаковый заголовок рабочего места 
	СтрокаПоКлючуНазначенияИспользования = НайденныеСтроки[0];
	
	Возврат Новый ФорматированнаяСтрока(СтрокаПоКлючуНазначенияИспользования.ЗаголовокРабочегоМеста, , , ,
				"Обработка.ЖурналДокументовЗакупки.Форма.СписокДокументов");
	
КонецФункции


#КонецОбласти

#КонецОбласти

#КонецЕсли
