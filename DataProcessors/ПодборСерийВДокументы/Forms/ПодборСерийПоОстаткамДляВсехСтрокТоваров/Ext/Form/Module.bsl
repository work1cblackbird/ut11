#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения;  //используется механизмом обработки изменения реквизитов ТЧ

&НаКлиенте
Перем ТекущееКоличество; //кеш количества в текущей строке для расчета количества серий

&НаКлиенте
Перем НужноЗадаватьВопросПередЗакрытием;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьУсловноеОформление();

	ТолькоПросмотр = Параметры.ТолькоПросмотр;
	Если ТолькоПросмотр Тогда
		Элементы.ЗавершитьВводСерий.Видимость = Ложь;
	КонецЕсли; 
	
	Склад       = Параметры.Склад;
	Помещение   = Параметры.Помещение;
	Регистратор = Параметры.Регистратор;
	АдресВоВременномХранилище = Параметры.АдресВоВременномХранилище;
	ПараметрыУказанияСерий = Параметры.ПараметрыУказанияСерий;
	
	ЕстьУпаковки = ПараметрыУказанияСерий.ПоляСвязи.Найти("Упаковка") <> Неопределено;
	Элементы.Упаковка.Доступность = Не ЕстьУпаковки;
	
	ПоляПоиска = "Номенклатура, Характеристика";
	Если ПараметрыУказанияСерий.ПоляСвязи.Количество() > 0 Тогда
		
		Для каждого ПолеСвязи Из ПараметрыУказанияСерий.ПоляСвязи Цикл
			
			ПоляПоиска = ПоляПоиска + ", " + ПолеСвязи;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ЗаполнитьКоличествоУпаковокВКешТаблицах();
	
	// Закешируем значения для последующего использования значений из шапки (если такие есть).
	Если Параметры.Свойство("ЗначенияПолейДляОпределенияРаспоряжения") Тогда
		ЗначенияПолейДляОпределенияРаспоряжения = Новый ФиксированнаяСтруктура(
			Параметры.ЗначенияПолейДляОпределенияРаспоряжения);
	Иначе
		ЗначенияПолейДляОпределенияРаспоряжения = Новый ФиксированнаяСтруктура();
	КонецЕсли;
	
	Если ЗначенияПолейДляОпределенияРаспоряжения.Количество() > 0 Тогда
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПараметрыУказанияСерий.ПолноеИмяОбъекта);
		Распоряжение = МенеджерОбъекта.РаспоряжениеНаВыполнениеСкладскойОперации(Параметры.ЗначенияПолейДляОпределенияРаспоряжения);
	Иначе
		Распоряжение = Неопределено;
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура(ПоляПоиска);

	ЗаполнитьЗначенияСвойств(СтруктураПоиска,Параметры);
	
	НайденныеСтроки = Объект.ТоварыКешДокумента.НайтиСтроки(СтруктураПоиска);
	Если НайденныеСтроки.Количество() = 0 Тогда
		НомерТекущейСтрокиТоваров = 0;
	Иначе
		НомерТекущейСтрокиТоваров = Объект.ТоварыКешДокумента.НайтиСтроки(СтруктураПоиска)[0].НомерСтроки;
	КонецЕсли;
	
	Инициализировать();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьДоступностьКнопокВпередНазад();
	НужноЗадаватьВопросПередЗакрытием = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность
		И НужноЗадаватьВопросПередЗакрытием Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОповещениеЗакрытия = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Список серий был изменен.
			|Сохранить изменения?'");
		
		ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(ОповещениеЗакрытия, Отказ, ЗавершениеРаботы, ТекстВопроса,
			ТекстПредупреждения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_СерииНоменклатуры" Тогда
		НайденныеСтроки = Объект.Серии.НайтиСтроки(Новый Структура("Серия", Источник));
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			СтрокаСерий = НайденныеСтроки[0];
			ЗаполнитьЗначенияСвойств(СтрокаСерий, Параметр);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура УпаковкаПриИзменении(Элемент)
	
	Если НомерТекущейСтрокиТоваров < 1 
	 Или НомерТекущейСтрокиТоваров > Объект.ТоварыКешДокумента.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	УпаковкаПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура УпаковкаОчистка(Элемент, СтандартнаяОбработка)
	
	УпаковкаПриИзменении(Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСерии

&НаКлиенте
Процедура СерииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле = Элементы.СерииСерия Тогда
		ИзменитьСериюКлиент();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СерииКоличествоУпаковокПриИзменении(Элемент)

	КоэффициентУпаковки = КоэффициентУпаковки(Упаковка, Номенклатура);
	
	ТекущиеДанные = Элементы.Серии.ТекущиеДанные;
	ТекущиеДанные.Количество = ТекущиеДанные.КоличествоУпаковок * КоэффициентУпаковки;
	
КонецПроцедуры

&НаКлиенте
Процедура СерииПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если Копирование Тогда
		ТекущееКоличество = 0;
	Иначе
		ТекущееКоличество =  ТекущиеДанные.КоличествоУпаковок;
	КонецЕсли;
	
	Если НоваяСтрока
		И Не НастройкиИспользованияСерий.ИспользоватьКоличествоСерии Тогда
		
		ТекущиеДанные.КоличествоУпаковок = 1;
		ТекущиеДанные.Количество         = 1;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СерииПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	Если Не ОтменаРедактирования Тогда
		Дельта = ТекущееКоличество - Элементы.Серии.ТекущиеДанные.КоличествоУпаковок;
		КоличествоСерий = КоличествоСерий - Дельта;
		Элементы.Серии.ТекущиеДанные.СвободныйОстаток = Элементы.Серии.ТекущиеДанные.СвободныйОстаток + Дельта;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СерииПередУдалением(Элемент, Отказ)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	ТекущееКоличество =  ТекущиеДанные.Количество;
КонецПроцедуры

&НаКлиенте
Процедура СерииПослеУдаления(Элемент)
	КоличествоСерий = КоличествоСерий - ТекущееКоличество;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отмена(Команда)
	Модифицированность = Ложь;
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВводСерий(Команда)
	ОчиститьСообщения();
    СохранитьВводСерийСервер();
	Если Не Модифицированность Тогда
		Закрыть(АдресВоВременномХранилище);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		НужноЗадаватьВопросПередЗакрытием = Ложь;
		ЗавершитьВводСерий(Неопределено);// Сохранение результатов модификации.
		
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		
		НужноЗадаватьВопросПередЗакрытием = Ложь;
		Закрыть(Неопределено);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СледующаяСтрока(Команда)
	ИзменитьТекущуюСтроку(1);
	УстановитьДоступностьКнопокВпередНазад();
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущаяСтрока(Команда)
	ИзменитьТекущуюСтроку(-1);
	УстановитьДоступностьКнопокВпередНазад();
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьДополнительныеРеквизиты(Команда)
	ТекущиеДанные = Элементы.Серии.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Серия) Тогда
		ТекстСообщения = НСтр("ru = 'В выбранной строке не заполнена серия из-за ошибки в данных об остатках. Обратитесь к администратору.'");
		Возврат;
	КонецЕсли;
	
	ЗначенияЗаполненияСерии = Новый Структура;
	ЗначенияЗаполненияСерии.Вставить("ВидНоменклатуры", ВидНоменклатуры);
	ЗначенияЗаполненияСерии.Вставить("Номер", ТекущиеДанные.Номер);
	ЗначенияЗаполненияСерии.Вставить("ГоденДо", ТекущиеДанные.ГоденДо);
	
	ПараметрыФормыСерии = Новый Структура;
	ПараметрыФормыСерии.Вставить("ЗначенияЗаполнения",ЗначенияЗаполненияСерии);
	ПараметрыФормыСерии.Вставить("Ключ",ТекущиеДанные.Серия);
	
	ОткрытьФорму("Справочник.СерииНоменклатуры.Форма.ФормаЭлемента",ПараметрыФормыСерии, ЭтаФорма,,,, Неопределено, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоFEFO(Команда)
	Если Количество = 0 Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'В документе не указано количество товара. Заполнение по FEFO не возможно.'"));
		Возврат;
	КонецЕсли;	
	ЗаполнитьПоFEFOСервер();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.КоличествоСерий.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КоличествоСерий");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("Количество");

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветОтличающейсяСтрокиДокумента);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.КоличествоСерий.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КоличествоСерий");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("Количество");

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Seagreen);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СерииСвободныйОстаток.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Серии.СвободныйОстаток");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ОтборЭлемента.ПравоеЗначение = 0;

	Элемент.Оформление.УстановитьЗначениеПараметра("ВыделятьОтрицательные", Истина);

КонецПроцедуры

#Область Прочее

&НаСервере
Процедура ЗаполнитьКоличествоУпаковокВКешТаблицах()
	
	СтруктураИзХранилища = ПолучитьИзВременногоХранилища(Параметры.АдресВоВременномХранилище);
	
	Если ПараметрыУказанияСерий.ПоляСвязи.Найти("Упаковка") <> Неопределено Тогда
		
		ТекстВыбораСерий = "";
		ТекстВыбораТоваров = "";
		
		Для Каждого СтрМас Из ПараметрыУказанияСерий.ПоляСвязи Цикл
			ТекстВыбораТоваров = ТекстВыбораТоваров + "
			|	ТаблицаТоваров." + СтрМас + ",";
			
			ТекстВыбораСерий = ТекстВыбораСерий + "
			|	ТаблицаСерий." + СтрМас + ",";
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаСерий.НомерСтроки             КАК НомерСтроки,
		|	ТаблицаСерий.ТекстВыбораТоваров,
		|	ТаблицаСерий.Номенклатура            КАК Номенклатура,
		|	ТаблицаСерий.Характеристика          КАК Характеристика,
		|	ТаблицаСерий.Серия                   КАК Серия,
		|	ТаблицаСерий.Количество              КАК Количество
		|ПОМЕСТИТЬ ТаблицаСерий
		|ИЗ
		|	&ТаблицаСерий КАК ТаблицаСерий
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаТоваров.НомераСтрокДокумента    КАК НомераСтрокДокумента,
		|	ТаблицаТоваров.ТекстВыбораТоваров,
		|	ТаблицаТоваров.Номенклатура            КАК Номенклатура,
		|	ТаблицаТоваров.Характеристика          КАК Характеристика,
		|	ТаблицаТоваров.Упаковка                КАК Упаковка,
		|	ТаблицаТоваров.СтатусУказанияСерий     КАК СтатусУказанияСерий,
		|	ТаблицаТоваров.Количество              КАК Количество
		|ПОМЕСТИТЬ ТаблицаТоваров
		|ИЗ
		|	&ТаблицаТоваров КАК ТаблицаТоваров
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаТоваров.НомерСтроки                                   КАК НомерСтроки,
		|	ТаблицаТоваров.ТекстВыбораТоваров,
		|	ТаблицаТоваров.Номенклатура                                  КАК Номенклатура,
		|	ТаблицаТоваров.Характеристика                                КАК Характеристика,
		|	ТаблицаТоваров.Серия                                         КАК Серия,
		|	ТаблицаТоваров.Количество                                    КАК Количество,
		|	ТаблицаТоваров.Количество / &ТекстЗапросаКоэффициентУпаковки КАК КоличествоУпаковок
		|ИЗ
		|	ТаблицаСерий КАК ТаблицаТоваров
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаТоваров.НомераСтрокДокумента                          КАК НомераСтрокДокумента,
		|	ТаблицаТоваров.ТекстВыбораТоваров,
		|	ТаблицаТоваров.Номенклатура                                  КАК Номенклатура,
		|	ТаблицаТоваров.Характеристика                                КАК Характеристика,
		|	ТаблицаТоваров.СтатусУказанияСерий                           КАК СтатусУказанияСерий,
		|	ТаблицаТоваров.Количество                                    КАК Количество,
		|	ТаблицаТоваров.Количество / &ТекстЗапросаКоэффициентУпаковки КАК КоличествоУпаковок
		|ИЗ
		|	ТаблицаТоваров КАК ТаблицаТоваров";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ТаблицаТоваров.ТекстВыбораТоваров,", ТекстВыбораТоваров);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ТаблицаСерий.ТекстВыбораТоваров,", ТекстВыбораСерий);
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ТекстЗапросаКоэффициентУпаковки",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
				"ТаблицаТоваров.Упаковка",
				"ТаблицаТоваров.Номенклатура"));
		
		Запрос.УстановитьПараметр("ТаблицаСерий",   СтруктураИзХранилища.ТаблицаСерий);
		Запрос.УстановитьПараметр("ТаблицаТоваров", СтруктураИзХранилища.ТаблицаТоваров);
		
		РезультатЗапрос = Запрос.ВыполнитьПакет();
		
		Объект.СерииКешДокумента.Загрузить(РезультатЗапрос[2].Выгрузить());
		Объект.ТоварыКешДокумента.Загрузить(РезультатЗапрос[3].Выгрузить());
		
	Иначе
		
		Объект.СерииКешДокумента.Загрузить(СтруктураИзХранилища.ТаблицаСерий);
		Объект.ТоварыКешДокумента.Загрузить(СтруктураИзХранилища.ТаблицаТоваров);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоFEFOСервер()
	
	КоличествоКРаспределению = Количество;
	
	КоэффициентУпаковки = КоэффициентУпаковки(Упаковка, Номенклатура);
	
	Для Каждого СтрТабл Из Объект.Серии Цикл
		Если КоличествоКРаспределению <= 0 Тогда
			СтрТабл.СвободныйОстаток   = СтрТабл.СвободныйОстаток + СтрТабл.КоличествоУпаковок;
			СтрТабл.КоличествоУпаковок = 0;
			СтрТабл.Количество = 0;
			Продолжить;
		КонецЕсли;
		
		КоличествоПоСтроке =  Мин(СтрТабл.СвободныйОстаток + СтрТабл.КоличествоУпаковок, КоличествоКРаспределению);
		Если КоличествоПоСтроке < 0 Тогда
			СтрТабл.СвободныйОстаток   = СтрТабл.СвободныйОстаток + СтрТабл.КоличествоУпаковок;
			СтрТабл.КоличествоУпаковок = 0;
		Иначе
			СтрТабл.СвободныйОстаток   = СтрТабл.СвободныйОстаток + СтрТабл.КоличествоУпаковок - КоличествоПоСтроке;
			СтрТабл.КоличествоУпаковок = КоличествоПоСтроке;
			КоличествоКРаспределению = КоличествоКРаспределению - КоличествоПоСтроке; 
		КонецЕсли;
		
		СтрТабл.Количество = СтрТабл.КоличествоУпаковок * КоэффициентУпаковки;

	КонецЦикла;
	
	КоличествоСерий = Объект.Серии.Итог("КоличествоУпаковок");
	
КонецПроцедуры

&НаСервере
Процедура СохранитьВводСерийВКеше()
	
	ЕстьПризнакУказанияСерий = ПараметрыУказанияСерий.ПоляСвязи.Найти("УказыватьСерии") <> Неопределено;
	
	ТаблицаСерий = Объект.Серии.Выгрузить();
	
	ТаблицаСерий.Свернуть("Серия,Номер,ГоденДо", "Количество,КоличествоУпаковок");
	
	Индекс = ТаблицаСерий.Количество();
	
	Пока Индекс > 0 Цикл
		Индекс = Индекс - 1;
		СтрТабл = ТаблицаСерий[Индекс];
		Если СтрТабл.КоличествоУпаковок = 0 Тогда
			ТаблицаСерий.Удалить(СтрТабл);
		КонецЕсли;
	КонецЦикла;
	
	СтрокаТоваров = Объект.ТоварыКешДокумента[НомерТекущейСтрокиТоваров-1];
	
	ТекстПоляПоиска = "";
	
	Для Каждого СтрМас Из ПараметрыУказанияСерий.ПоляСвязи Цикл
		ТекстПоляПоиска = ТекстПоляПоиска + ", " + СтрМас;
	КонецЦикла;
		
	СтруктураПоиска = Новый Структура("Номенклатура,Характеристика" + ТекстПоляПоиска);
	
	ЗаполнитьЗначенияСвойств(СтруктураПоиска,СтрокаТоваров);
	
	СтрокаТоваров.Упаковка = Упаковка;
	
	НайденныеСтрокиСерий = Объект.СерииКешДокумента.НайтиСтроки(СтруктураПоиска);
	
	Для Каждого СтрТабл Из НайденныеСтрокиСерий Цикл
		Объект.СерииКешДокумента.Удалить(СтрТабл);
	КонецЦикла;
	
	Для Каждого СтрТабл Из ТаблицаСерий Цикл
		НоваяСтрока = Объект.СерииКешДокумента.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТоваров);
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрТабл);
		Если ЕстьПризнакУказанияСерий Тогда
			НоваяСтрока.УказыватьСерии = Истина;
		КонецЕсли;
		НоваяСтрока.Упаковка = Упаковка;
	КонецЦикла;
	
	Если ЕстьПризнакУказанияСерий Тогда
		Если ТаблицаСерий.Количество() > 0 Тогда
			СтрокаТоваров.УказыватьСерии = Истина;
		Иначе
			СтрокаТоваров.УказыватьСерии = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьВводСерийСервер()
	
	СохранитьВводСерийВКеше();
	
	СтруктураВозврата = Новый Структура;
	ТаблицаСерий = Объект.СерииКешДокумента.Выгрузить();
	
	КолонкиГруппировок =
	"Номенклатура,
	|Характеристика,
	|НоменклатураОприходование,
	|ХарактеристикаОприходование,
	|Упаковка,
	|Коэффициент,
	|Склад,
	|ВариантОформления,
	|Серия,
	|НеОтгружать";
	
	КолонкиСуммирования =
	"Количество,
	|КоличествоУпаковок";
	
	Если ПараметрыУказанияСерий.ПоляСвязи.Количество() > 0 Тогда
		
		Для каждого ПолеСвязи Из ПараметрыУказанияСерий.ПоляСвязи Цикл
			
			Если ПолеСвязи <> "Склад"
				И ПолеСвязи <> "НеОтгружать"
				И ПолеСвязи <> "ВариантОформления" Тогда
				
				КолонкиГруппировок = КолонкиГруппировок + ", " + ПолеСвязи;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
		
	ТаблицаСерий.Свернуть(КолонкиГруппировок, КолонкиСуммирования);
	
	Для Каждого ТекСтр Из ТаблицаСерий Цикл
		Если ТекСтр.Коэффициент <> 0 И ТекСтр.Коэффициент <> 1 Тогда
			ТекСтр.Количество = ТекСтр.Количество * ТекСтр.Коэффициент;
			ТекСтр.КоличествоУпаковок = ТекСтр.КоличествоУпаковок * ТекСтр.Коэффициент;
		КонецЕсли;
	КонецЦикла;
	
	СтруктураВозврата.Вставить("ТаблицаСерий", ТаблицаСерий);
	Если ПараметрыУказанияСерий.ИмяТЧТовары = ПараметрыУказанияСерий.ИмяТЧСерии Тогда
		СтруктураВозврата.Вставить("ТаблицаТоваров", Объект.ТоварыКешДокумента.Выгрузить());
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(СтруктураВозврата, АдресВоВременномХранилище);
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКнопокВпередНазад()
	Если  НомерТекущейСтрокиТоваров > 1 Тогда
		Элементы.ГруппаКнопкаНазад.ТекущаяСтраница = Элементы.СтраницаКнопкаНазад;
	Иначе
		Элементы.ГруппаКнопкаНазад.ТекущаяСтраница = Элементы.СтраницаКнопкаНазадПустая;
	КонецЕсли;
	
	Если НомерТекущейСтрокиТоваров < Объект.ТоварыКешДокумента.Количество() Тогда
		Элементы.ГруппаКнопкаВперед.ТекущаяСтраница = Элементы.СтраницаКнопкаВперед;
	Иначе
		Элементы.ГруппаКнопкаВперед.ТекущаяСтраница = Элементы.СтраницаКнопкаВпередПустая;
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура Инициализировать(ПересчитыватьУпаковки = Ложь)
	
	Если НомерТекущейСтрокиТоваров < 1 
	 Или НомерТекущейСтрокиТоваров > Объект.ТоварыКешДокумента.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТоваров = Объект.ТоварыКешДокумента[НомерТекущейСтрокиТоваров-1];
	
	ЗначенияПолейСвязи = Новый Структура(СтрСоединить(ПараметрыУказанияСерий.ПоляСвязи,","));
	ЗначенияПолейСвязи.Вставить("Номенклатура");	
	ЗначенияПолейСвязи.Вставить("Характеристика");	
	ЗаполнитьЗначенияСвойств(ЗначенияПолейСвязи, СтрокаТоваров);
	
	РеквизитыНоменклатуры = Новый Структура;
	РеквизитыНоменклатуры.Вставить("ВидНоменклатуры", "ВидНоменклатуры");
	РеквизитыНоменклатуры.Вставить("ЕдиницаИзмеренияПредставление", "ЕдиницаИзмерения.Наименование");
	РеквизитыНоменклатуры.Вставить("СрокГодности", "СрокГодности");
	РеквизитыНоменклатуры.Вставить("ЕдиницаИзмеренияСрокаГодности", "ЕдиницаИзмеренияСрокаГодности");
	РеквизитыНоменклатуры.Вставить("ТипИзмеряемойВеличиныНоменклатуры", "ЕдиницаИзмерения.ТипИзмеряемойВеличины");
	
	РеквизитыНоменклатуры =	ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗначенияПолейСвязи.Номенклатура, РеквизитыНоменклатуры); 
	
	Если ЗначениеЗаполнено(СтрокаТоваров.Упаковка) Тогда
		Если РеквизитыНоменклатуры.ТипИзмеряемойВеличиныНоменклатуры <> Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук 
			 Или РеквизитыНоменклатуры.ТипИзмеряемойВеличиныНоменклатуры = Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук
				И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТоваров.Упаковка, "ТипИзмеряемойВеличины") = Перечисления.ТипыИзмеряемыхВеличин.Упаковка Тогда
			Упаковка = СтрокаТоваров.Упаковка;
		Иначе
			Упаковка = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
		КонецЕсли;
	Иначе
		Упаковка = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
	КонецЕсли;
	
	СкладыВТЧ = ПараметрыУказанияСерий.ПоляСвязи.Найти("Склад") <> Неопределено;
	ЕстьУпаковки = ПараметрыУказанияСерий.ПоляСвязи.Найти("Упаковка") <> Неопределено;
	ЕстьПризнакУказанияСерий = ПараметрыУказанияСерий.ПоляСвязи.Найти("УказыватьСерии") <> Неопределено;
	ЕстьВариантОформления = ПараметрыУказанияСерий.ПоляСвязи.Найти("ВариантОформления") <> Неопределено;
	ЕстьНеотгружаемые = ПараметрыУказанияСерий.ПоляСвязи.Найти("НеОтгружать") <> Неопределено;
	
	Если ЗначениеЗаполнено(Упаковка) Тогда;
		РеквизитыУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентВесОбъемПрочиеРеквизитыУпаковки(Упаковка, Номенклатура, "Наименование"); 
		РеквизитыНоменклатуры.Вставить("ЕдиницаИзмеренияПредставление", РеквизитыУпаковки.Наименование);
	Иначе
		РеквизитыУпаковки = Новый Структура("Коэффициент, Наименование", 1, РеквизитыНоменклатуры.ЕдиницаИзмеренияПредставление);
	КонецЕсли;
	
	Количество = СтрокаТоваров.Количество / РеквизитыУпаковки.Коэффициент;
	НомераСтрокПредставление = СтрокаТоваров.НомераСтрокДокумента;
	
	Если Не СкладыВТЧ Тогда
		ТекущийСклад = Склад;
	Иначе
		ТекущийСклад = СтрокаТоваров.Склад;
	КонецЕсли;
	
	ЗначенияПолейСвязи.Вставить("Склад", ТекущийСклад); 
	
	НастройкиИспользованияСерий = Новый ФиксированнаяСтруктура(
		Справочники.ВидыНоменклатуры.НастройкиИспользованияСерий(РеквизитыНоменклатуры.ВидНоменклатуры,ПараметрыУказанияСерий,ЗначенияПолейСвязи));
		

	НоменклатураСервер.ПроверитьВозможностьОткрытияФормыУказанияСерий(НастройкиИспользованияСерий, ПараметрыУказанияСерий, ЗначенияПолейСвязи);
	
	ЗаголовокКоличествоВДокументе = НСтр("ru = 'Необходимо для отгрузки, %ЕдиницаИзмерения%'");
	ЗаголовокКоличествоСерий      = НСтр("ru = 'Подобрано из серий, %ЕдиницаИзмерения%'");
	ЗаголовокКолонкиКоличество = "";
	Если Не Параметры.Свойство("ЗаголовокКолонкиКоличество", ЗаголовокКолонкиКоличество) Тогда
		ЗаголовокКолонкиКоличество    = НСтр("ru = 'К отгрузке'"); 
	КонецЕсли;
	ЗаголовокКолонкиКоличество    = ЗаголовокКолонкиКоличество + ", " + РеквизитыНоменклатуры.ЕдиницаИзмеренияПредставление;
	
	ТоварПредставление = НСтр("ru = '%ПредставлениеТовара%'");
	
	ЗаголовокКоличествоВДокументе = СтрЗаменить(ЗаголовокКоличествоВДокументе,"%ЕдиницаИзмерения%",РеквизитыНоменклатуры.ЕдиницаИзмеренияПредставление);
	ЗаголовокКоличествоСерий      = СтрЗаменить(ЗаголовокКоличествоСерий,"%ЕдиницаИзмерения%",РеквизитыНоменклатуры.ЕдиницаИзмеренияПредставление);
	
	Если ПараметрыУказанияСерий.ЭтоОрдер Тогда
		ВариантПолучениеДанныхИзРегистров = "ОрдерСерииИзОстатка";
	Иначе
		Если ЗначениеЗаполнено(Распоряжение)
			И ПараметрыУказанияСерий.СкладскиеОперации.Найти(Перечисления.СкладскиеОперации.ОтгрузкаКомплектовДляРазборки) <> Неопределено
			И НастройкиИспользованияСерий.УчитыватьОстаткиСерий
			И НастройкиИспользованияСерий.УказыватьПриПланированииОтгрузки Тогда
			ВариантПолучениеДанныхИзРегистров = "НакладнаяСерииИзЗаказа";
		Иначе
			ВариантПолучениеДанныхИзРегистров = "ЗаказНакладнаяСерииИзОстатка";
		КонецЕсли;
	КонецЕсли;
	
	Если ВариантПолучениеДанныхИзРегистров = "ОрдерСерииИзОстатка"
		Или ВариантПолучениеДанныхИзРегистров = "ЗаказНакладнаяСерииИзОстатка" Тогда
		
		ЗаголовокСвободногоОстатка = НСтр("ru = 'Свободный остаток, %ЕдиницаИзмерения%'");
		Если ТипЗнч(Склад) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
			Заголовок = НСтр("ru = 'Подбор серий по остаткам в производстве'");
		Иначе
			Заголовок = НСтр("ru = 'Подбор серий по остаткам на складе'");
		КонецЕсли;
		
	ИначеЕсли ВариантПолучениеДанныхИзРегистров = "НакладнаяСерииИзЗаказа" 	Тогда
		
		ЗаголовокСвободногоОстатка = НСтр("ru = 'Остаток по заказу, %ЕдиницаИзмерения%'");
		Заголовок                  = НСтр("ru = 'Подбор серий по заказу'");

	ИначеЕсли ВариантПолучениеДанныхИзРегистров = "ОрдерСерииИзНакладной" Тогда
		
		ЗаголовокСвободногоОстатка = НСтр("ru = 'Остаток по распоряжению, %ЕдиницаИзмерения%'");
		Заголовок                  = НСтр("ru = 'Подбор серий по распоряжению на отгрузку'");
		
	ИначеЕсли ТипЗнч(Склад) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
		Заголовок = НСтр("ru = 'Подбор серий по остаткам в производстве'");
	КонецЕсли;
	
	ЗаголовокСвободногоОстатка = СтрЗаменить(ЗаголовокСвободногоОстатка,"%ЕдиницаИзмерения%",РеквизитыНоменклатуры.ЕдиницаИзмеренияПредставление);
	
	Номенклатура = СтрокаТоваров.Номенклатура;
	ТоварПредставление = СтрЗаменить(ТоварПредставление,"%ПредставлениеТовара%",НоменклатураКлиентСервер.ПредставлениеНоменклатуры(СтрокаТоваров.Номенклатура, СтрокаТоваров.Характеристика));
	
	Элементы.СерииКоличествоУпаковок.Заголовок = ЗаголовокКолонкиКоличество;
	Элементы.СерииСвободныйОстаток.Заголовок   = ЗаголовокСвободногоОстатка;
	Элементы.Количество.Заголовок              = ЗаголовокКоличествоВДокументе;
	Элементы.КоличествоСерий.Заголовок         = ЗаголовокКоличествоСерий;
	
	Для Каждого Описание Из НастройкиИспользованияСерий.ОписанияИспользованияРеквизитовСерии Цикл
		Если Описание.ИмяНастройки <> "ИспользоватьRFIDМеткиСерии" Тогда
			
			ПолеФормыСерии = Элементы[Обработки.ПодборСерийВДокументы.НазванияЭлементовСерий(Описание.ИмяРеквизита).Серии]; // ПолеФормы
			ПолеФормыСерии.Видимость        = Описание.Использование; 
			
			Если Не ПустаяСтрока(Описание.Формат) Тогда
				ПолеФормыСерии.Формат               = Описание.Формат;
			КонецЕсли;	
			
			Если Не ПустаяСтрока(Описание.ПредставлениеФорматнойСтрокиВЗаголовке) Тогда
				ПредставлениеЗаголовкаКолонки = НСтр("ru = '%ПредставлениеРеквизита% (%ПредставлениеФормата%)'");
				ПредставлениеЗаголовкаКолонки = СтрЗаменить(ПредставлениеЗаголовкаКолонки, "%ПредставлениеРеквизита%", Описание.ПредставлениеРеквизита);
				ПредставлениеЗаголовкаКолонки = СтрЗаменить(ПредставлениеЗаголовкаКолонки, "%ПредставлениеФормата%", Описание.ПредставлениеФорматнойСтрокиВЗаголовке);
				
				ПолеФормыСерии.Заголовок        = ПредставлениеЗаголовкаКолонки; 
			КонецЕсли;

		КонецЕсли;
	КонецЦикла;
	
	Элементы.СерииКоличествоУпаковок.Видимость = НастройкиИспользованияСерий.ИспользоватьКоличествоСерии;
		
	// Формирование таблицы ТаблицаСерий	

	ШаблонТекстаЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаСерий.Серия,
	|	&ПоляУпаковки
	|ПОМЕСТИТЬ ТаблицаСерий
	|ИЗ
	|	&ТаблицаСерий КАК ТаблицаСерий
	|ГДЕ
	|  	ТаблицаСерий.Номенклатура = &Номенклатура
	|  	И ТаблицаСерий.Характеристика = &Характеристика
	|	И ТаблицаСерий.Назначение = &НазначениеТЧ
	|	И &УсловиеСкладыВТЧ
	|	И &УсловиеЕстьУпаковки
	|	И &УсловиеЕстьВариантОформления
	|	И &УсловиеЕстьНеотгружаемые
	|";
	
	ТекстЗамены = "";	
	Если ЕстьПризнакУказанияСерий Тогда
		ТекстЗамены = "
			|  	ТаблицаСерий.УказыватьСерии, 
			|	ВЫБОР КОГДА ТаблицаСерий.УказыватьСерии = &УказыватьСерии
			|			ТОГДА ТаблицаСерий.Количество
			|			ИНАЧЕ 0
			|	КОНЕЦ КАК Количество, ";
		Если ЕстьУпаковки Тогда
			ТекстЗамены = ТекстЗамены + "
				|	ВЫБОР КОГДА ТаблицаСерий.УказыватьСерии = &УказыватьСерии
				|			ТОГДА ТаблицаСерий.КоличествоУпаковок
				|			ИНАЧЕ 0
				|	КОНЕЦ КАК КоличествоУпаковок ";
		Иначе
			ТекстЗамены = ТекстЗамены + "
				|	ВЫБОР КОГДА ТаблицаСерий.УказыватьСерии = &УказыватьСерии
				|			ТОГДА ТаблицаСерий.Количество
				|			ИНАЧЕ 0
				|	КОНЕЦ КАК КоличествоУпаковок ";
		КонецЕсли;
	ИначеЕсли ЕстьУпаковки Тогда
		ТекстЗамены = "
		|	ТаблицаСерий.Количество,
		|	ТаблицаСерий.КоличествоУпаковок ";
	Иначе
		ТекстЗамены = "
		|	ТаблицаСерий.Количество,
		|	ТаблицаСерий.Количество КАК КоличествоУпаковок ";
	КонецЕсли;
	ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "&ПоляУпаковки", ТекстЗамены);
	
	Если СкладыВТЧ Тогда
		ТекстЗамены = "И ТаблицаСерий.Склад = &Склад";
		ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "И &УсловиеСкладыВТЧ", ТекстЗамены);
	Иначе	
		ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "И &УсловиеСкладыВТЧ", "");
	КонецЕсли;
	
	Если ЕстьВариантОформления Тогда
		ТекстЗамены = "И ТаблицаСерий.ВариантОформления = &ВариантОформления";
		ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "И &УсловиеЕстьВариантОформления", ТекстЗамены);
	Иначе	
		ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "И &УсловиеЕстьВариантОформления", "");
	КонецЕсли;
	
	Если ЕстьНеотгружаемые Тогда
		ТекстЗамены = "И ТаблицаСерий.НеОтгружать = &НеОтгружать";
		ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "И &УсловиеЕстьНеотгружаемые", ТекстЗамены);
	Иначе
		ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "И &УсловиеЕстьНеотгружаемые", "");
	КонецЕсли;

	ТекстЗапросаТаблицаСерий = ШаблонТекстаЗапроса;
	Если ЕстьУпаковки Тогда
		ТекстЗамены = "И ТаблицаСерий.Упаковка = &Упаковка";
		ТекстЗапросаТаблицаСерий = СтрЗаменить(ТекстЗапросаТаблицаСерий, "И &УсловиеЕстьУпаковки", ТекстЗамены);
	Иначе
		ТекстЗапросаТаблицаСерий = СтрЗаменить(ТекстЗапросаТаблицаСерий, "И &УсловиеЕстьУпаковки", "");
	КонецЕсли;

	ТекстЗапросаТаблицаСерийПоляСвязи = ШаблонТекстаЗапроса;
	ТекстЗапросаТаблицаСерийПоляСвязи = СтрЗаменить(ТекстЗапросаТаблицаСерийПоляСвязи, "ПОМЕСТИТЬ ТаблицаСерий", "ПОМЕСТИТЬ ТаблицаСерийПоляСвязи");
	ТекстЗапросаТаблицаСерийПоляСвязи = СтрЗаменить(ТекстЗапросаТаблицаСерийПоляСвязи, "И &УсловиеЕстьУпаковки", "");

	ТекстЗапроса = ТекстЗапросаТаблицаСерий + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|" + ТекстЗапросаТаблицаСерийПоляСвязи;
	
	ДобавляемыеПараметры = Новый Массив;
	
	Если ПараметрыУказанияСерий.ПоляСвязи.Количество() > 0 Тогда
		
		Для каждого ПолеСвязи Из ПараметрыУказанияСерий.ПоляСвязи Цикл
			
			Если ПолеСвязи <> "Склад"
				И ПолеСвязи <> "НеОтгружать"
				И ПолеСвязи <> "УказыватьСерии"
				И ПолеСвязи <> "Назначение" Тогда
				
				ТекстЗапроса = ТекстЗапроса + "
				|  	И ТаблицаСерий." + ПолеСвязи + " = &" + ПолеСвязи;
				
				ДобавляемыеПараметры.Добавить(ПолеСвязи);
				
			КонецЕсли;
				
		КонецЦикла;
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";

	// Формирование таблицы ДанныеРегистров	
	
	ТекстЗапроса = ТекстЗапроса + Обработки.ПодборСерийВДокументы.ТекстЗапросаФормированияТаблицыДанныеРегистров(ВариантПолучениеДанныхИзРегистров, Склад);
	
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	// Объединение таблиц ТаблицаСерий и ДанныеРегистров	
	
	ТекстЗапроса = ТекстЗапроса + "
	|ВЫБРАТЬ
	|	ТаблицаСерийСОстатками.Серия КАК Серия,
	|	СУММА(ТаблицаСерийСОстатками.СвободныйОстаток/&КоэффициентУпаковки) КАК СвободныйОстаток,
	|	СУММА(ТаблицаСерийСОстатками.Количество) КАК Количество,
	|	СУММА(ВЫБОР
	|			КОГДА &ПересчитыватьУпаковки
	|				ТОГДА ТаблицаСерийСОстатками.Количество / &КоэффициентУпаковки
	|			ИНАЧЕ ТаблицаСерийСОстатками.КоличествоУпаковок
	|		КОНЕЦ) КАК КоличествоУпаковок,
	|	&ТекстЗапросаРеквизитыСерийВыборка
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДанныеРегистров.Серия КАК Серия,
	|		ДанныеРегистров.СвободныйОстаток КАК СвободныйОстаток,
	|		0 КАК Количество,
	|		0 КАК КоличествоУпаковок
	|	ИЗ
	|		ДанныеРегистров КАК ДанныеРегистров
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаСерий.Серия,
	|		-ТаблицаСерий.Количество,
	|		0,
	|		0
	|	ИЗ
	|		ТаблицаСерий КАК ТаблицаСерий
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаСерий.Серия,
	|		0,
	|		ТаблицаСерий.Количество,
	|		ТаблицаСерий.КоличествоУпаковок
	|	ИЗ
	|		ТаблицаСерийПоляСвязи КАК ТаблицаСерий) КАК ТаблицаСерийСОстатками
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСерийСОстатками.Серия,
	|	&ТекстЗапросаРеквизитыСерийГруппировка
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ТаблицаСерийСОстатками.СвободныйОстаток) > 0
	|		ИЛИ СУММА(ТаблицаСерийСОстатками.Количество) > 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоденДо,
	|	Номер";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаРеквизитыСерийВыборка",
		Обработки.ПодборСерийВДокументы.ТекстЗапросаРеквизитыСерий("ТаблицаСерийСОстатками", НастройкиИспользованияСерий, Ложь));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаРеквизитыСерийГруппировка",
		Обработки.ПодборСерийВДокументы.ТекстЗапросаРеквизитыСерий("ТаблицаСерийСОстатками", НастройкиИспользованияСерий, Истина));
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;

	Запрос.УстановитьПараметр("ТаблицаСерий",Объект.СерииКешДокумента.Выгрузить());
	Запрос.УстановитьПараметр("Номенклатура",СтрокаТоваров.Номенклатура);
	Запрос.УстановитьПараметр("Характеристика",СтрокаТоваров.Характеристика);
	
	Если СтрокаТоваров.Свойство("Назначение")
		Или СтрокаТоваров.Свойство("НазначениеОтправителя") Тогда
		Если ПараметрыУказанияСерий.СкладскиеОперации.Найти(Перечисления.СкладскиеОперации.ОтгрузкаКомплектовДляРазборки) <> Неопределено
			И ПараметрыУказанияСерий.ЭтоЗаказ
			И СтрокаТоваров.Обособленно Тогда
			Назначение = Справочники.Назначения.ПустаяСсылка();
		ИначеЕсли ПараметрыУказанияСерий.ИмяПоляСкладОтправитель <> Неопределено
			И ПараметрыУказанияСерий.ИмяПоляСкладПолучатель <> Неопределено Тогда //это перемещение товаров или передача в производство
			Назначение = СтрокаТоваров.НазначениеОтправителя;
		Иначе
			Назначение = СтрокаТоваров.Назначение;
		КонецЕсли;
		НазначениеОстатков = Назначение;
	Иначе
		НазначениеОстатков = Справочники.Назначения.ПустаяСсылка();
	КонецЕсли;
	Запрос.УстановитьПараметр("Назначение", НазначениеОстатков);
	Запрос.УстановитьПараметр("НазначениеТЧ", СтрокаТоваров.Назначение);
			
	Запрос.УстановитьПараметр("ВсеСерии",Ложь);
	Если Не СкладыВТЧ Тогда
		Запрос.УстановитьПараметр("Склад",Склад);
	Иначе
		Запрос.УстановитьПараметр("Склад",СтрокаТоваров.Склад);
	КонецЕсли;
	
	Если ВариантПолучениеДанныхИзРегистров = "НакладнаяСерииИзЗаказа"
		И ЗначенияПолейДляОпределенияРаспоряжения.Количество() > 0 Тогда
		ЗначенияПолейПоСтроке = Новый Структура;
		Для Каждого СтрМас Из ПараметрыУказанияСерий.ИменаПолейДляОпределенияРаспоряжения Цикл
			ПоложениеРазделителя = СтрНайти(СтрМас, "_");		
			Если ПоложениеРазделителя = 0 Тогда
				ЗначенияПолейПоСтроке.Вставить(СтрМас, ЗначенияПолейДляОпределенияРаспоряжения[СтрМас]);
			Иначе
				ИмяПоля = Прав(СтрМас, СтрДлина(СтрМас)- ПоложениеРазделителя);
				ЗначенияПолейПоСтроке.Вставить(СтрМас, СтрокаТоваров[ИмяПоля]);
			КонецЕсли;
		КонецЦикла;
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПараметрыУказанияСерий.ПолноеИмяОбъекта);
		РаспоряжениеСтроки = МенеджерОбъекта.РаспоряжениеНаВыполнениеСкладскойОперации(ЗначенияПолейПоСтроке);
	 	Запрос.УстановитьПараметр("Распоряжение", РаспоряжениеСтроки);
	КонецЕсли;
	
	Если ЕстьВариантОформления Тогда
		Запрос.УстановитьПараметр("ВариантОформления", СтрокаТоваров.ВариантОформления);
	КонецЕсли;
	
	Если ЕстьНеотгружаемые Тогда
		Запрос.УстановитьПараметр("НеОтгружать", СтрокаТоваров.НеОтгружать);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Помещение",Помещение);
	Запрос.УстановитьПараметр("Регистратор",Регистратор);
	
	Для каждого ДобавляемыйПараметр Из ДобавляемыеПараметры Цикл
		
		Запрос.УстановитьПараметр(ДобавляемыйПараметр, СтрокаТоваров[ДобавляемыйПараметр]);
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("КоэффициентУпаковки", РеквизитыУпаковки.Коэффициент);
	Запрос.УстановитьПараметр("ПересчитыватьУпаковки", ПересчитыватьУпаковки);
	
	Объект.Серии.Загрузить(Запрос.Выполнить().Выгрузить());
	
	КоличествоСерий = Объект.Серии.Итог("КоличествоУпаковок");
	
	Если Объект.Серии.Количество() = 0 Тогда
		Элементы.ГруппаСтраницыСерии.ТекущаяСтраница = Элементы.СтраницаНетОстатков;
		Элементы.РаспределитьОстаток.Видимость = Ложь;
		Элементы.ЗаполнитьПоFEFO.Видимость = Ложь;
		Элементы.Упаковка.Видимость = Ложь;
	Иначе
		
		Элементы.Упаковка.Видимость = Истина;
		Если НастройкиИспользованияСерий.УчетСерийПоFEFO 
			И Перечисления.СкладскиеОперации.ЕстьОтгрузка(ПараметрыУказанияСерий.СкладскиеОперации) Тогда
			Элементы.ЗаполнитьПоFEFO.Видимость = Истина;
		Иначе
			Элементы.ЗаполнитьПоFEFO.Видимость = Ложь;
		КонецЕсли;
		Элементы.РаспределитьОстаток.Видимость = НастройкиИспользованияСерий.ИспользоватьКоличествоСерии;
		
		Элементы.ГруппаСтраницыСерии.ТекущаяСтраница = Элементы.СтраницаСерии;
		
		// Доп. реквизиты
		УправлениеСвойствамиУТ.ДобавитьКолонкиДополнительныхРеквизитов(
			НастройкиИспользованияСерий,
			ЭтаФорма,
			"Объект.Серии",
			"Серии",
			"СерииСвободныйОстаток",
			Истина);
			
		УправлениеСвойствамиУТ.ЗаполнитьКолонкиДополнительныхРеквизитов(
			ЭтаФорма,
			"Объект.Серии",
			"Серия");
			
	КонецЕсли;
		
	КоличествоСерий = Объект.Серии.Итог("КоличествоУпаковок");
	
	ПредставлениеОбъемаРаботы = НСтр("ru = 'Товар %НомерТекущейСтроки% из %ВсегоТоваров%:'");
	ПредставлениеОбъемаРаботы = СтрЗаменить(ПредставлениеОбъемаРаботы,"%НомерТекущейСтроки%",Строка(НомерТекущейСтрокиТоваров));
	ПредставлениеОбъемаРаботы = СтрЗаменить(ПредставлениеОбъемаРаботы,"%ВсегоТоваров%",Объект.ТоварыКешДокумента.Количество());
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьТекущуюСтроку(Направление)
	
	СохранитьВводСерийВКеше();
	НомерТекущейСтрокиТоваров = НомерТекущейСтрокиТоваров + Направление;
	
	Упаковка = Неопределено;
	Инициализировать(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСериюКлиент()
	
	ТекущиеДанные = Элементы.Серии.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияЗаполненияСерии = Новый Структура;
	ЗначенияЗаполненияСерии.Вставить("ВидНоменклатуры", ВидНоменклатуры);
	ЗначенияЗаполненияСерии.Вставить("Номер", ТекущиеДанные.Номер);
	ЗначенияЗаполненияСерии.Вставить("ГоденДо", ТекущиеДанные.ГоденДо);
	
	ПараметрыФормыСерии = Новый Структура;
	ПараметрыФормыСерии.Вставить("ЗначенияЗаполнения",ЗначенияЗаполненияСерии);
	ПараметрыФормыСерии.Вставить("Ключ",ТекущиеДанные.Серия);
	
	ОткрытьФорму("Справочник.СерииНоменклатуры.Форма.ФормаЭлемента",ПараметрыФормыСерии, ЭтаФорма,,,, Неопределено, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределитьОстаток(Команда)
	КоличествоКРаспределению = Количество - КоличествоСерий;
	
	Если Количество = 0 Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'В документе не указано количество товара. Распределение по сериям невозможно.'"));
		Возврат;
	ИначеЕсли КоличествоКРаспределению <= 0 Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Все количество товара, указанное в документе, распределено по сериям.'"));
		Возврат;
	КонецЕсли;	
	
	КоэффициентУпаковки = КоэффициентУпаковки(Упаковка, Номенклатура);
	
	Для Каждого ИдентификаторСтроки Из Элементы.Серии.ВыделенныеСтроки Цикл
		
		Если КоличествоКРаспределению <= 0 Тогда
			Прервать;
		КонецЕсли;
	
		СтрокаСерии = Объект.Серии.НайтиПоИдентификатору(ИдентификаторСтроки);
		
		КоличествоНаСтроку = Макс(Мин(КоличествоКРаспределению, СтрокаСерии.СвободныйОстаток), 0);
		КоличествоКРаспределению = КоличествоКРаспределению - КоличествоНаСтроку;
		КоличествоСерий = КоличествоСерий + КоличествоНаСтроку;
		
		СтрокаСерии.КоличествоУпаковок = СтрокаСерии.КоличествоУпаковок + КоличествоНаСтроку;
		СтрокаСерии.СвободныйОстаток = СтрокаСерии.СвободныйОстаток - КоличествоНаСтроку;
		СтрокаСерии.Количество = СтрокаСерии.КоличествоУпаковок * КоэффициентУпаковки;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КоэффициентУпаковки(Упаковка, Номенклатура)
	Возврат Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Упаковка, Номенклатура);
КонецФункции

&НаСервере
Процедура УпаковкаПриИзмененииСервер()
	
	СохранитьВводСерийВКеше();
	Инициализировать(Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
