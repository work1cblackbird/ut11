
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Возврат при получении формы для анализа.
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("УказаниеДатыПроизводства", УказаниеДатыПроизводства) Тогда
		
		УказаниеДатыПроизводства = Ложь;
		
		ГоденДо = Параметры.ГоденДо;
		
		Если Параметры.Свойство("ИспользоватьДатуПроизводства") Тогда
			ДатаПроизводства = Параметры.ДатаПроизводства;
		КонецЕсли;
		
		СрокГодности                  = Параметры.СрокГодности;
		ЕдиницаИзмеренияСрокаГодности = Параметры.ЕдиницаИзмеренияСрокаГодности;
		
		Элементы.ДатаПроизводства.Формат               = Параметры.ФорматДаты;
		Элементы.ДатаПроизводства.ФорматРедактирования = Параметры.ФорматДаты;
		
	Иначе
		
		ГоденДо = Параметры.ДатаПроизводства;
		
		Элементы.ГоденДо.Заголовок = НСтр("ru = 'Дата производства'");
		
		Элементы.ГруппаДатаПроизводства.Видимость = Ложь;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ГоденДо) Тогда
		ГоденДо = НачалоДня(ТекущаяДатаСеанса());
	КонецЕсли;
	
	Элементы.ГоденДо.ВыделенныеДаты.Очистить();
	Элементы.ГоденДо.ВыделенныеДаты.Добавить(ГоденДо);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГоденДоВыбор(Элемент, ВыбраннаяДата)
	
	ДанныеВыбора = ДанныеВыбораРезультата();
	
	Закрыть(ДанныеВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПроизводстваПриИзменении(Элемент)
	
	НоменклатураКлиентСервер.ПересчитатьДатуСерии(ДатаПроизводства);
	
	РасчитатьГоденДо();
	
КонецПроцедуры

&НаКлиенте
Процедура СрокГодностиПриИзменении(Элемент)
	
	РасчитатьГоденДо();
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияСрокаГодностиПриИзменении(Элемент)
	
	РасчитатьГоденДо();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ДанныеВыбора = ДанныеВыбораРезультата();
	
	Закрыть(ДанныеВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РасчитатьГоденДо()
	
	РассчитанныйГоденДо = НоменклатураКлиентСервер.ГоденДоПоДатеПроизводства(ДатаПроизводства, СрокГодности,
							ЕдиницаИзмеренияСрокаГодности);
	
	Если Не ЗначениеЗаполнено(РассчитанныйГоденДо) Тогда
		Возврат;
	КонецЕсли;
	
	ГоденДо = РассчитанныйГоденДо;
	
	Элементы.ГоденДо.ВыделенныеДаты.Очистить();
	Элементы.ГоденДо.ВыделенныеДаты.Добавить(ГоденДо);
	
КонецПроцедуры

&НаКлиенте
Функция ДанныеВыбораРезультата()
	
	ДанныеВыбора = Новый Структура;
	
	Если УказаниеДатыПроизводства Тогда
		ДанныеВыбора.Вставить("ДатаПроизводства", ГоденДо);
	Иначе
		ДанныеВыбора.Вставить("ГоденДо",          ГоденДо);
		ДанныеВыбора.Вставить("ДатаПроизводства", ДатаПроизводства);
	КонецЕсли;
	
	Возврат ДанныеВыбора;
	
КонецФункции

#КонецОбласти
