
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Обновляет служебные данные по количеству документов, удовлетворяющих условиям выборки.
//
// Параметры:
//   ДанныеСчетчиков        - Структура - данные по количеству документов:
//     * ИдентификаторЗадания - Строка - идентификатор задания;
//     * ОтгрузитьПоставки    - Число - количество заявок на поставку;
//     * ОтгрузитьПродажи     - Число - количество заказов отгрузки с собственных складов;
//     * ПринятьВозвраты      - Число - количество заявок на возврат.
//   ДанныеТорговойПлощадки - Структура - данные торговой площадки, 
//                              см. Обработка.ПанельАдминистрированияПродажиНаOzon.Форма.РабочееМестоУправленияПродажами.
// Возвращаемое значение:
//   Структура - данные по количеству документов:
//     * ИдентификаторЗадания - Строка - идентификатор задания;
//     * ОтгрузитьПоставки    - Число - количество заявок на поставку;
//     * ОтгрузитьПродажи     - Число - количество заказов отгрузки с собственных складов;
//     * ПринятьВозвраты      - Число - количество заявок на возврат.
//
Функция ПолучитьСчетчики(Знач ДанныеСчетчиков, Знач ДанныеТорговойПлощадки) Экспорт

	Если ДанныеСчетчиков = Неопределено Тогда
		ДанныеСчетчиков = Новый Структура;
		ДанныеСчетчиков.Вставить("ИдентификаторЗадания",    "");
		ДанныеСчетчиков.Вставить("ОтгрузитьПоставки",       0);
		ДанныеСчетчиков.Вставить("ОтгрузитьПродажи",        0);
		ДанныеСчетчиков.Вставить("ПринятьВозвратыПоставок", 0);
		ДанныеСчетчиков.Вставить("ПринятьВозвратыПродаж",   0);
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущаяДата",     НачалоДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("Организация",     ДанныеТорговойПлощадки.Организация);
	Запрос.УстановитьПараметр("Партнер",         ДанныеТорговойПлощадки.Партнер);
	Запрос.УстановитьПараметр("ЛюбойПартнер",    ДанныеТорговойПлощадки.СпособОтраженияПродажFBS = "РеализацияВПути");
	Запрос.УстановитьПараметр("Контрагент",      ДанныеТорговойПлощадки.Контрагент);
	Запрос.УстановитьПараметр("ЛюбойКонтрагент", ДанныеТорговойПлощадки.СпособОтраженияПродажFBS = "РеализацияВПути");

	ИспользуемыеХозОперации = ИнтеграцияСМаркетплейсомOzonВызовСервера.ИспользуемыеХозяйственныеОперации(
		ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы);

	ХозОперацииПоставок = Новый Массив;
	Если ДанныеТорговойПлощадки.СпособОтраженияПродажFBO = "Комиссия" Тогда
		ХозОперацииПоставок =
			ИнтеграцияСМаркетплейсомOzonВызовСервера.ХозОперацииРаздела(ИспользуемыеХозОперации.ПоставкиКомиссия);
	КонецЕсли;

	ХозОперацииПродаж = Новый Массив;
	Если ДанныеТорговойПлощадки.СпособОтраженияПродажFBS = "Комиссия" Тогда
		ХозОперацииПродаж =
			ИнтеграцияСМаркетплейсомOzonВызовСервера.ХозОперацииРаздела(ИспользуемыеХозОперации.ПоставкиКомиссия);
	ИначеЕсли ДанныеТорговойПлощадки.СпособОтраженияПродажFBS = "РеализацияВПути"
			И ДанныеТорговойПлощадки.СпособОтраженияПродажFBO = "Комиссия" Тогда
		ХозОперацииПродаж =
			ИнтеграцияСМаркетплейсомOzonВызовСервера.ХозОперацииРаздела(ИспользуемыеХозОперации.ПродажиКомиссияРеализацияВПути);
	ИначеЕсли ДанныеТорговойПлощадки.СпособОтраженияПродажFBS = "РеализацияВПути" Тогда
		ХозОперацииПродаж =
			ИнтеграцияСМаркетплейсомOzonВызовСервера.ХозОперацииРаздела(ИспользуемыеХозОперации.ПродажиРеализацияВПути);
	КонецЕсли;

	// ДанныеСчетчиков.ОтгрузитьПоставки
	Запрос.УстановитьПараметр("ЭтоПоставки",    Истина);
	Запрос.УстановитьПараметр("Договор",        ДанныеТорговойПлощадки.ДоговорПродажиЧерезСкладыТорговойПлощадки);
	Запрос.УстановитьПараметр("ЛюбойДоговор",   Ложь);
	Запрос.УстановитьПараметр("ХозОперации",    ХозОперацииПоставок);
	Запрос.УстановитьПараметр("ЛюбыеОперации",  Не ЗначениеЗаполнено(ХозОперацииПоставок));
	Запрос.Текст = ТекстЗапросаДокументыПродажиКОформлению();

	ТребуетсяПерезаполнение = (ДанныеСчетчиков.ОтгрузитьПоставки <> -1);
	ДанныеСчетчиков.ОтгрузитьПоставки = 0;
	Если ТребуетсяПерезаполнение Тогда
		ВыборкаРезультата = Запрос.Выполнить().Выбрать();
		Если ВыборкаРезультата.Следующий() Тогда
			ДанныеСчетчиков.ОтгрузитьПоставки = ВыборкаРезультата.Количество;
		КонецЕсли;
	КонецЕсли;

	// ДанныеСчетчиков.ОтгрузитьПродажи
	Запрос.УстановитьПараметр("ЭтоПоставки",    Ложь);
	Запрос.УстановитьПараметр("Договор",        ДанныеТорговойПлощадки.ДоговорПродажиЧерезСкладыСобственные);
	Запрос.УстановитьПараметр("ЛюбойДоговор",   ДанныеТорговойПлощадки.СпособОтраженияПродажFBS = "РеализацияВПути");
	Запрос.УстановитьПараметр("ХозОперации",    ХозОперацииПродаж);
	Запрос.УстановитьПараметр("ЛюбыеОперации",  Не ЗначениеЗаполнено(ХозОперацииПродаж));
	Запрос.Текст = ТекстЗапросаДокументыПродажиКОформлению();

	ТребуетсяПерезаполнение = (ДанныеСчетчиков.ОтгрузитьПродажи <> -1);
	ДанныеСчетчиков.ОтгрузитьПродажи = 0;
	Если ТребуетсяПерезаполнение Тогда
		ВыборкаРезультата = Запрос.Выполнить().Выбрать();
		Если ВыборкаРезультата.Следующий() Тогда
			ДанныеСчетчиков.ОтгрузитьПродажи = ВыборкаРезультата.Количество;
		КонецЕсли;
	КонецЕсли;

	// ДанныеСчетчиков.ПринятьВозвратыПоставок
	Запрос.УстановитьПараметр("Договор",        ДанныеТорговойПлощадки.ДоговорПродажиЧерезСкладыТорговойПлощадки);
	Запрос.УстановитьПараметр("ЛюбойДоговор",   Ложь);
	Запрос.УстановитьПараметр("ХозОперации",    ХозОперацииПоставок);
	Запрос.УстановитьПараметр("ЛюбыеОперации",  Не ЗначениеЗаполнено(ХозОперацииПоставок));
	Запрос.Текст = ТекстЗапросаДокументыВозвратыКОформлению();

	ТребуетсяПерезаполнение = (ДанныеСчетчиков.ПринятьВозвратыПоставок <> -1);
	ДанныеСчетчиков.ПринятьВозвратыПоставок = 0;
	Если ТребуетсяПерезаполнение Тогда
		ВыборкаРезультата = Запрос.Выполнить().Выбрать();
		Если ВыборкаРезультата.Следующий() Тогда
			ДанныеСчетчиков.ПринятьВозвратыПоставок = ВыборкаРезультата.Количество;
		КонецЕсли;
	КонецЕсли;

	// ДанныеСчетчиков.ПринятьВозвратыПродаж
	Запрос.УстановитьПараметр("Договор",        ДанныеТорговойПлощадки.ДоговорПродажиЧерезСкладыСобственные);
	Запрос.УстановитьПараметр("ЛюбойДоговор",   ДанныеТорговойПлощадки.СпособОтраженияПродажFBS <> "Комиссия");
	Запрос.УстановитьПараметр("ХозОперации",    ХозОперацииПродаж);
	Запрос.УстановитьПараметр("ЛюбыеОперации",  Не ЗначениеЗаполнено(ХозОперацииПродаж));
	Запрос.Текст = ТекстЗапросаДокументыВозвратыКОформлению();

	ТребуетсяПерезаполнение = (ДанныеСчетчиков.ПринятьВозвратыПродаж <> -1);
	ДанныеСчетчиков.ПринятьВозвратыПродаж = 0;
	Если ТребуетсяПерезаполнение Тогда
		ВыборкаРезультата = Запрос.Выполнить().Выбрать();
		Если ВыборкаРезультата.Следующий() Тогда
			ДанныеСчетчиков.ПринятьВозвратыПродаж = ВыборкаРезультата.Количество;
		КонецЕсли;
	КонецЕсли;

	Возврат ДанныеСчетчиков;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаДокументыПродажиКОформлению()

	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ТаблицаПереопределяемый.Распоряжение КАК Ссылка,
		|	ТаблицаПереопределяемый.Склад КАК Склад
		|ПОМЕСТИТЬ ВтОткрытыеЗаказыКлиентов
		|ИЗ
		|	РегистрСведений.РаспоряженияНаОтгрузкуКВыполнению КАК ТаблицаПереопределяемый
		|ГДЕ
		|	(ТаблицаПереопределяемый.Распоряжение ССЫЛКА Документ.ЗаказКлиента
		|			ИЛИ ТаблицаПереопределяемый.Распоряжение ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента)
		|	И ТаблицаПереопределяемый.Организация = &Организация
		|	И (ТаблицаПереопределяемый.Партнер = &Партнер
		|			ИЛИ &ЛюбойПартнер)
		|	И (ТаблицаПереопределяемый.Контрагент = &Контрагент
		|			ИЛИ &ЛюбойКонтрагент)
		|	И (ВЫБОР
		|				КОГДА ТаблицаПереопределяемый.Распоряжение ССЫЛКА Документ.ЗаказКлиента
		|					ТОГДА ВЫРАЗИТЬ(ТаблицаПереопределяемый.Распоряжение КАК Документ.ЗаказКлиента).Договор
		|				КОГДА ТаблицаПереопределяемый.Распоряжение ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
		|					ТОГДА ВЫРАЗИТЬ(ТаблицаПереопределяемый.Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Договор
		|			КОНЕЦ = &Договор
		|			ИЛИ &ЛюбойДоговор)
		|	И (ВЫБОР
		|				КОГДА ТаблицаПереопределяемый.Распоряжение ССЫЛКА Документ.ЗаказКлиента
		|					ТОГДА ВЫРАЗИТЬ(ТаблицаПереопределяемый.Распоряжение КАК Документ.ЗаказКлиента).ХозяйственнаяОперация
		|				КОГДА ТаблицаПереопределяемый.Распоряжение ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
		|					ТОГДА ВЫРАЗИТЬ(ТаблицаПереопределяемый.Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).ХозяйственнаяОперация
		|			КОНЕЦ В (&ХозОперации)
		|			ИЛИ &ЛюбыеОперации)
		|	И ТаблицаПереопределяемый.СостояниеКОформлению <> ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.Оформлены)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка,
		|	Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РаспоряженияНаОтгрузку.Распоряжение КАК Ссылка,
		|	РаспоряженияНаОтгрузку.Склад КАК Склад
		|ПОМЕСТИТЬ ВТОстаткиПоЗаказуКлиента
		|ИЗ
		|	РегистрНакопления.РаспоряженияНаОтгрузку.Обороты(
		|			,
		|			,
		|			,
		|			Распоряжение В
		|					(ВЫБРАТЬ
		|						Отбор.Ссылка
		|					ИЗ
		|						ВТОткрытыеЗаказыКлиентов КАК Отбор)
		|				И Склад В
		|					(ВЫБРАТЬ
		|						Отбор.Склад
		|					ИЗ
		|						ВтОткрытыеЗаказыКлиентов КАК Отбор)) КАК РаспоряженияНаОтгрузку
		|ГДЕ
		|	РаспоряженияНаОтгрузку.КОформлениюОборот > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВтДокументыОформленияПереопределяемый.Склад КАК Склад
		|ПОМЕСТИТЬ ВтДокументыОформленияСклады
		|ИЗ
		|	ВТОстаткиПоЗаказуКлиента КАК ВтДокументыОформленияПереопределяемый
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РаспоряженияНаОтгрузкуПереопределяемый.Распоряжение КАК ЗаказКлиента,
		|	РаспоряженияНаОтгрузкуПереопределяемый.Склад КАК Склад
		|ПОМЕСТИТЬ ВтДокументыОтобранные
		|ИЗ
		|	РегистрНакопления.РаспоряженияНаОтгрузку КАК РаспоряженияНаОтгрузкуПереопределяемый
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыОформленияСклады КАК ВтДокументыОформленияСкладыПереопределяемый
		|		ПО (ВтДокументыОформленияСкладыПереопределяемый.Склад = РаспоряженияНаОтгрузкуПереопределяемый.Склад)
		|ГДЕ
		|	РаспоряженияНаОтгрузкуПереопределяемый.Активность
		|	И РаспоряженияНаОтгрузкуПереопределяемый.КОформлению < 0
		|	И РаспоряженияНаОтгрузкуПереопределяемый.ВидДвиженияРегистра = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженияНакопления.Расход)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказКлиента,
		|	Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаПереопределяемый.Ссылка КАК ЗаказКлиента,
		|	ТаблицаПереопределяемый.Склад КАК Склад
		|ПОМЕСТИТЬ ВтЕстьНакладные
		|ИЗ
		|	ВТОстаткиПоЗаказуКлиента КАК ТаблицаПереопределяемый
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыОтобранные КАК ДокументыОтобранныеПереопределяемый
		|		ПО ТаблицаПереопределяемый.Ссылка = ДокументыОтобранныеПереопределяемый.ЗаказКлиента
		|			И ТаблицаПереопределяемый.Склад = ДокументыОтобранныеПереопределяемый.Склад
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказКлиента,
		|	Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаПереопределяемый.Ссылка КАК Ссылка,
		|	ТаблицаПереопределяемый.Склад КАК Склад
		|ПОМЕСТИТЬ ВтСостояниеНакладной
		|ИЗ
		|	ВТОстаткиПоЗаказуКлиента КАК ТаблицаПереопределяемый
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтЕстьНакладные КАК ТаблицаЕстьНакладныеПереопределяемый
		|		ПО (ТаблицаЕстьНакладныеПереопределяемый.ЗаказКлиента = ТаблицаПереопределяемый.Ссылка)
		|			И (ТаблицаЕстьНакладныеПереопределяемый.Склад = ТаблицаПереопределяемый.Склад)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаПереопределяемый.ДокументОтгрузки КАК ДокументОтгрузки,
		|	ТаблицаПереопределяемый.Склад КАК Склад,
		|	ТаблицаПереопределяемый.Номенклатура КАК Номенклатура,
		|	ТаблицаПереопределяемый.Характеристика КАК Характеристика,
		|	ТаблицаПереопределяемый.Серия КАК Серия,
		|	ТаблицаПереопределяемый.Назначение КАК Назначение,
		|	ТаблицаПереопределяемый.КОтгрузкеОстаток - ТаблицаПереопределяемый.СобраноОстаток = ТаблицаПереопределяемый.КОформлениюОстаток КАК ОстаткиРавны
		|ПОМЕСТИТЬ ВтОстаткиТоварыКОтгрузке
		|ИЗ
		|	РегистрНакопления.ТоварыКОтгрузке.Остатки(
		|			,
		|			(ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
		|				ИЛИ ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
		|				ИЛИ ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
		|					И &ЭтоПоставки)
		|				И ВЫБОР
		|					КОГДА ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
		|						ТОГДА ВЫРАЗИТЬ(ДокументОтгрузки КАК Документ.ЗаказКлиента).Организация
		|					КОГДА ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
		|						ТОГДА ВЫРАЗИТЬ(ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Организация
		|					КОГДА ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
		|						ТОГДА ВЫРАЗИТЬ(ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Организация
		|					КОГДА ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
		|						ТОГДА ВЫРАЗИТЬ(ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Организация
		|				КОНЕЦ = &Организация
		|				И (ВЫБОР
		|						КОГДА ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
		|							ТОГДА ВЫРАЗИТЬ(ДокументОтгрузки КАК Документ.ЗаказКлиента).Партнер
		|						КОГДА ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
		|							ТОГДА ВЫРАЗИТЬ(ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Партнер
		|						КОГДА ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
		|							ТОГДА ВЫРАЗИТЬ(ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Партнер
		|						КОГДА ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
		|							ТОГДА ВЫРАЗИТЬ(ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Партнер
		|					КОНЕЦ = &Партнер
		|					ИЛИ &ЛюбойПартнер)
		|				И (ВЫБОР
		|						КОГДА ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
		|							ТОГДА ВЫРАЗИТЬ(ДокументОтгрузки КАК Документ.ЗаказКлиента).Контрагент
		|						КОГДА ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
		|							ТОГДА ВЫРАЗИТЬ(ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Контрагент
		|						КОГДА ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
		|							ТОГДА ВЫРАЗИТЬ(ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Контрагент
		|						КОГДА ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
		|							ТОГДА ВЫРАЗИТЬ(ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Контрагент
		|					КОНЕЦ = &Контрагент
		|					ИЛИ &ЛюбойКонтрагент)
		|				И (ВЫБОР
		|						КОГДА ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
		|							ТОГДА ВЫРАЗИТЬ(ДокументОтгрузки КАК Документ.ЗаказКлиента).Договор
		|						КОГДА ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
		|							ТОГДА ВЫРАЗИТЬ(ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Договор
		|						КОГДА ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
		|							ТОГДА ВЫРАЗИТЬ(ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Договор
		|						КОГДА ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
		|							ТОГДА ВЫРАЗИТЬ(ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Договор
		|					КОНЕЦ = &Договор
		|					ИЛИ &ЛюбойДоговор)
		|				И (ВЫБОР
		|						КОГДА ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
		|							ТОГДА ВЫРАЗИТЬ(ДокументОтгрузки КАК Документ.ЗаказКлиента).ХозяйственнаяОперация
		|						КОГДА ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
		|							ТОГДА ВЫРАЗИТЬ(ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).ХозяйственнаяОперация
		|						КОГДА ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
		|							ТОГДА ВЫРАЗИТЬ(ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).ХозяйственнаяОперация
		|						КОГДА ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
		|							ТОГДА ВЫРАЗИТЬ(ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).ХозяйственнаяОперация
		|					КОНЕЦ В (&ХозОперации)
		|					ИЛИ &ЛюбыеОперации)) КАК ТаблицаПереопределяемый
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументОтгрузки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ТаблицаПереопределяемый.ДокументОтгрузки КАК ДокументОтгрузки,
		|	ТаблицаПереопределяемый.Склад КАК Склад
		|ПОМЕСТИТЬ ВтДокументыОтгрузки
		|ИЗ
		|	ВтОстаткиТоварыКОтгрузке КАК ТаблицаПереопределяемый
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументОтгрузки,
		|	Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВтДокументыОтгрузкиПереопределяемый.Склад КАК Склад
		|ПОМЕСТИТЬ ВтДокументыОтгрузкиСклады
		|ИЗ
		|	ВтДокументыОтгрузки КАК ВтДокументыОтгрузкиПереопределяемый
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК ДокументОтгрузки,
		|	ТоварыКОтгрузкеПереопределяемый.Склад КАК Склад
		|ПОМЕСТИТЬ ВтТоварыКОтгрузкеПереопределяемый
		|ИЗ
		|	РегистрНакопления.ТоварыКОтгрузке КАК ТоварыКОтгрузкеПереопределяемый
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыОтгрузкиСклады КАК ВтДокументыОтгрузкиСкладыПереопределяемый
		|		ПО (ВтДокументыОтгрузкиСкладыПереопределяемый.Склад = ТоварыКОтгрузкеПереопределяемый.Склад)
		|ГДЕ
		|	ТоварыКОтгрузкеПереопределяемый.Активность
		|	И ТоварыКОтгрузкеПереопределяемый.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки,
		|	ТоварыКОтгрузкеПереопределяемый.Склад
		|
		|ИМЕЮЩИЕ
		|	СУММА(ТоварыКОтгрузкеПереопределяемый.КОтгрузке) > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки,
		|	ТоварыКОтгрузкеПереопределяемый.Склад
		|ИЗ
		|	РегистрНакопления.ТоварыКОтгрузке КАК ТоварыКОтгрузкеПереопределяемый
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыОтгрузкиСклады КАК ВтДокументыОтгрузкиСкладыПереопределяемый
		|		ПО (ВтДокументыОтгрузкиСкладыПереопределяемый.Склад = ТоварыКОтгрузкеПереопределяемый.Склад)
		|ГДЕ
		|	ТоварыКОтгрузкеПереопределяемый.Активность
		|	И ТоварыКОтгрузкеПереопределяемый.КОтгрузке > 0
		|	И ТоварыКОтгрузкеПереопределяемый.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки,
		|	ТоварыКОтгрузкеПереопределяемый.Склад
		|
		|ИМЕЮЩИЕ
		|	СУММА(ТоварыКОтгрузкеПереопределяемый.Собрано) > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументОтгрузки,
		|	Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаПереопределяемый.ДокументОтгрузки КАК ДокументОтгрузки,
		|	ТаблицаПереопределяемый.Склад КАК Склад
		|ПОМЕСТИТЬ ВтЕстьОрдера
		|ИЗ
		|	ВтДокументыОтгрузки КАК ТаблицаПереопределяемый
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТоварыКОтгрузкеПереопределяемый КАК ВтТоварыКОтгрузкеПереопределяемый
		|		ПО ТаблицаПереопределяемый.Склад = ВтТоварыКОтгрузкеПереопределяемый.Склад
		|			И ТаблицаПереопределяемый.ДокументОтгрузки = ВтТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументОтгрузки,
		|	Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаПереопределяемый.ДокументОтгрузки КАК Ссылка
		|ПОМЕСТИТЬ ВтСостояниеОрдера
		|ИЗ
		|	ВтОстаткиТоварыКОтгрузке КАК ТаблицаПереопределяемый
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтЕстьОрдера КАК ТаблицаЕстьОрдераПереопределяемый
		|		ПО (ТаблицаЕстьОрдераПереопределяемый.ДокументОтгрузки = ТаблицаПереопределяемый.ДокументОтгрузки)
		|			И (ТаблицаЕстьОрдераПереопределяемый.Склад = ТаблицаПереопределяемый.Склад)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСкладыПереопределяемый
		|		ПО (СпрСкладыПереопределяемый.Ссылка = ТаблицаПереопределяемый.Склад)
		|			И (СпрСкладыПереопределяемый.ИспользоватьОрдернуюСхемуПриОтгрузке)
		|			И (СпрСкладыПереопределяемый.ДатаНачалаОрдернойСхемыПриОтгрузке <= &ТекущаяДата)
		|ГДЕ
		|	ВЫБОР
		|			КОГДА ТаблицаЕстьОрдераПереопределяемый.ДокументОтгрузки ЕСТЬ NULL
		|					И НЕ СпрСкладыПереопределяемый.Ссылка ЕСТЬ NULL
		|				ТОГДА 1
		|			КОГДА ТаблицаПереопределяемый.ОстаткиРавны
		|				ТОГДА 0
		|			ИНАЧЕ 3
		|		КОНЕЦ > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ВЫБОР
		|		КОГДА ВтСостояниеНакладнойПереопределяемый.Ссылка ЕСТЬ NULL
		|			ТОГДА ВтСостояниеОрдераПереопределяемый.Ссылка
		|		ИНАЧЕ ВтСостояниеНакладнойПереопределяемый.Ссылка
		|	КОНЕЦ КАК ДокументОтгрузки
		|ПОМЕСТИТЬ ВтНовыеОрдера
		|ИЗ
		|	ВтСостояниеНакладной КАК ВтСостояниеНакладнойПереопределяемый
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВтСостояниеОрдера КАК ВтСостояниеОрдераПереопределяемый
		|		ПО ВтСостояниеНакладнойПереопределяемый.Ссылка = ВтСостояниеОрдераПереопределяемый.Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументОтгрузки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1001
		|	КОЛИЧЕСТВО(*) КАК Количество
		|ИЗ
		|	ВтНовыеОрдера КАК ТоварыКОтгрузкеПереопределяемый
		|ГДЕ
		|	НЕ ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ЕСТЬ NULL";

	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаДокументыВозвратыКОформлению()

	ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Таблица.ДокументПоступления КАК ДокументПоступления,
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Назначение КАК Назначение,
		|	Таблица.Склад КАК Склад,
		|	0 КАК СостояниеПоступления,
		|	Таблица.КОформлениюНакладныхПоРаспоряжениюОстаток КАК КОформлениюНакладных,
		|	Таблица.КОформлениюПоступленийПоОрдерамОстаток КАК КОформлениюПоступленийПоОрдерам,
		|	Таблица.КОформлениюОрдеровОстаток КАК КОформлениюОрдеров
		|ПОМЕСТИТЬ ВтДокументыОформленияПоОстаткам
		|ИЗ
		|	РегистрНакопления.ТоварыКПоступлению.Остатки(
		|			,
		|			(ДокументПоступления ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
		|				ИЛИ ДокументПоступления ССЫЛКА Документ.ВозвратТоваровОтКлиента
		|				ИЛИ ДокументПоступления ССЫЛКА Документ.ПоступлениеТоваровОтХранителя
		|					И ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ПоступлениеТоваровОтХранителя).ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтХранителя))
		|				И ВЫБОР
		|					КОГДА ДокументПоступления ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
		|						ТОГДА ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Организация
		|					КОГДА ДокументПоступления ССЫЛКА Документ.ВозвратТоваровОтКлиента
		|						ТОГДА ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Организация
		|					КОГДА ДокументПоступления ССЫЛКА Документ.ПоступлениеТоваровОтХранителя
		|						ТОГДА ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ПоступлениеТоваровОтХранителя).Организация
		|				КОНЕЦ = &Организация
		|				И (ВЫБОР
		|						КОГДА ДокументПоступления ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
		|							ТОГДА ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Партнер
		|						КОГДА ДокументПоступления ССЫЛКА Документ.ВозвратТоваровОтКлиента
		|							ТОГДА ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ВозвратТоваровОтКлиента).Партнер
		|						КОГДА ДокументПоступления ССЫЛКА Документ.ПоступлениеТоваровОтХранителя
		|							ТОГДА ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ПоступлениеТоваровОтХранителя).Партнер
		|					КОНЕЦ = &Партнер
		|					ИЛИ &ЛюбойПартнер)
		|				И (ВЫБОР
		|						КОГДА ДокументПоступления ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
		|							ТОГДА ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Контрагент
		|						КОГДА ДокументПоступления ССЫЛКА Документ.ВозвратТоваровОтКлиента
		|							ТОГДА ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ВозвратТоваровОтКлиента).Контрагент
		|						КОГДА ДокументПоступления ССЫЛКА Документ.ПоступлениеТоваровОтХранителя
		|							ТОГДА ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ПоступлениеТоваровОтХранителя).Контрагент
		|					КОНЕЦ = &Контрагент
		|					ИЛИ &ЛюбойКонтрагент)
		|				И (ВЫБОР
		|						КОГДА ДокументПоступления ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
		|							ТОГДА ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Договор
		|						КОГДА ДокументПоступления ССЫЛКА Документ.ВозвратТоваровОтКлиента
		|							ТОГДА ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ВозвратТоваровОтКлиента).Договор
		|						КОГДА ДокументПоступления ССЫЛКА Документ.ПоступлениеТоваровОтХранителя
		|							ТОГДА ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ПоступлениеТоваровОтХранителя).Договор
		|					КОНЕЦ = &Договор
		|					ИЛИ &ЛюбойДоговор)
		|				И (ВЫБОР
		|						КОГДА ДокументПоступления ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
		|							ТОГДА ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).ХозяйственнаяОперация
		|						КОГДА ДокументПоступления ССЫЛКА Документ.ВозвратТоваровОтКлиента
		|							ТОГДА ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ВозвратТоваровОтКлиента).ХозяйственнаяОперация
		|						КОГДА ДокументПоступления ССЫЛКА Документ.ПоступлениеТоваровОтХранителя
		|							ТОГДА ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ПоступлениеТоваровОтХранителя).ХозяйственнаяОперация
		|					КОНЕЦ В (&ХозОперации)
		|					ИЛИ &ЛюбыеОперации)) КАК Таблица
		|ГДЕ
		|	(Таблица.КОформлениюНакладныхПоРаспоряжениюОстаток > 0
		|			ИЛИ Таблица.КОформлениюПоступленийПоОрдерамОстаток > 0
		|			ИЛИ Таблица.КОформлениюОрдеровОстаток > 0)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументПоступления,
		|	Номенклатура,
		|	Характеристика,
		|	Назначение,
		|	Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Таблица.ДокументПоступления КАК ДокументПоступления,
		|	Таблица.Склад КАК Склад,
		|	СУММА(Таблица.КОформлениюНакладных) КАК КОформлениюНакладныхВсего,
		|	СУММА(Таблица.КОформлениюПоступленийПоОрдерам) КАК КОформлениюПоступленийПоОрдерамВсего,
		|	СУММА(Таблица.КОформлениюОрдеров) КАК КОформлениюОрдеровВсего
		|ПОМЕСТИТЬ ВтДокументыОформленияОстаткиВсего
		|ИЗ
		|	ВтДокументыОформленияПоОстаткам КАК Таблица
		|
		|СГРУППИРОВАТЬ ПО
		|	Таблица.ДокументПоступления,
		|	Таблица.Склад
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументПоступления,
		|	Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Таблица.ДокументПоступления КАК ДокументПоступления,
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Назначение КАК Назначение,
		|	Таблица.Склад КАК Склад,
		|	МАКСИМУМ(Таблица.КОформлениюНакладных) КАК КОформлениюНакладных,
		|	МАКСИМУМ(Таблица.КОформлениюПоступленийПоОрдерам) КАК КОформлениюПоступленийПоОрдерам,
		|	МАКСИМУМ(Таблица.КОформлениюОрдеров) КАК КОформлениюОрдеров,
		|	ЕСТЬNULL(ТаблицаОстаткиВсего.КОформлениюНакладныхВсего, 0) КАК КОформлениюНакладныхВсего,
		|	ЕСТЬNULL(ТаблицаОстаткиВсего.КОформлениюПоступленийПоОрдерамВсего, 0) КАК КОформлениюПоступленийПоОрдерамВсего,
		|	ЕСТЬNULL(ТаблицаОстаткиВсего.КОформлениюОрдеровВсего, 0) КАК КОформлениюОрдеровВсего
		|ПОМЕСТИТЬ ВтДокументыОформления
		|ИЗ
		|	(ВЫБРАТЬ
		|		Таблица.ДокументПоступления КАК ДокументПоступления,
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Назначение КАК Назначение,
		|		Таблица.Склад КАК Склад,
		|		Таблица.КОформлениюНакладных КАК КОформлениюНакладных,
		|		Таблица.КОформлениюПоступленийПоОрдерам КАК КОформлениюПоступленийПоОрдерам,
		|		Таблица.КОформлениюОрдеров КАК КОформлениюОрдеров
		|	ИЗ
		|		ВтДокументыОформленияПоОстаткам КАК Таблица
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Таблица.ДокументПоступления,
		|		Таблица.Номенклатура,
		|		Таблица.Характеристика,
		|		Таблица.Назначение,
		|		Таблица.Склад,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		РегистрНакопления.ТоварыКПоступлению КАК Таблица
		|	ГДЕ
		|		Таблица.ДокументПоступления В
		|				(ВЫБРАТЬ
		|					ТаблицаДокументов.ДокументПоступления
		|				ИЗ
		|					ВтДокументыОформленияПоОстаткам КАК ТаблицаДокументов)) КАК Таблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДокументыОформленияОстаткиВсего КАК ТаблицаОстаткиВсего
		|		ПО Таблица.ДокументПоступления = ТаблицаОстаткиВсего.ДокументПоступления
		|			И Таблица.Склад = ТаблицаОстаткиВсего.Склад
		|
		|СГРУППИРОВАТЬ ПО
		|	Таблица.ДокументПоступления,
		|	Таблица.Номенклатура,
		|	Таблица.Характеристика,
		|	Таблица.Назначение,
		|	Таблица.Склад,
		|	ЕСТЬNULL(ТаблицаОстаткиВсего.КОформлениюНакладныхВсего, 0),
		|	ЕСТЬNULL(ТаблицаОстаткиВсего.КОформлениюПоступленийПоОрдерамВсего, 0),
		|	ЕСТЬNULL(ТаблицаОстаткиВсего.КОформлениюОрдеровВсего, 0)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументПоступления,
		|	Номенклатура,
		|	Характеристика,
		|	Назначение,
		|	Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Таблица.ДокументПоступления КАК Ссылка,
		|	Таблица.Склад КАК Склад
		|ПОМЕСТИТЬ ВтДокументыССостояниями
		|ИЗ
		|	ВтДокументыОформления КАК Таблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКПоступлению КАК ТаблицаОрдераПереопределяемый
		|		ПО Таблица.ДокументПоступления = ТаблицаОрдераПереопределяемый.ДокументПоступления
		|			И Таблица.Номенклатура = ТаблицаОрдераПереопределяемый.Номенклатура
		|			И Таблица.Характеристика = ТаблицаОрдераПереопределяемый.Характеристика
		|			И Таблица.Назначение = ТаблицаОрдераПереопределяемый.Назначение
		|			И Таблица.Склад = ТаблицаОрдераПереопределяемый.Склад
		|			И (ТаблицаОрдераПереопределяемый.Активность)
		|			И (ТаблицаОрдераПереопределяемый.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
		|			И (ТаблицаОрдераПереопределяемый.Регистратор ССЫЛКА Документ.ПриходныйОрдерНаТовары)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКПоступлению КАК ТаблицаНакладныеПереопределяемый
		|		ПО Таблица.ДокументПоступления = ТаблицаНакладныеПереопределяемый.ДокументПоступления
		|			И Таблица.Номенклатура = ТаблицаНакладныеПереопределяемый.Номенклатура
		|			И Таблица.Характеристика = ТаблицаНакладныеПереопределяемый.Характеристика
		|			И Таблица.Назначение = ТаблицаНакладныеПереопределяемый.Назначение
		|			И Таблица.Склад = ТаблицаНакладныеПереопределяемый.Склад
		|			И (ТаблицаНакладныеПереопределяемый.Активность)
		|			И (ТаблицаНакладныеПереопределяемый.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента)
		|
		|СГРУППИРОВАТЬ ПО
		|	Таблица.ДокументПоступления,
		|	Таблица.Склад
		|
		|ИМЕЮЩИЕ
		|	НЕ(МАКСИМУМ(ВЫБОР
		|					КОГДА Таблица.ДокументПоступления ССЫЛКА Документ.ВозвратТоваровОтКлиента
		|							ИЛИ Таблица.ДокументПоступления ССЫЛКА Документ.ПоступлениеТоваровОтХранителя
		|						ТОГДА 4
		|					КОГДА Таблица.КОформлениюНакладных = 0
		|						ТОГДА 0
		|					КОГДА Таблица.КОформлениюНакладных > 0
		|							И ЕСТЬNULL(ТаблицаНакладныеПереопределяемый.КОформлениюНакладныхПоРаспоряжению, 0) = 0
		|						ТОГДА 1
		|					КОГДА Таблица.КОформлениюНакладных > 0
		|							И ЕСТЬNULL(ТаблицаНакладныеПереопределяемый.КОформлениюНакладныхПоРаспоряжению, 0) > 0
		|						ТОГДА 2
		|					ИНАЧЕ 0
		|				КОНЕЦ) В (0, 4)
		|			И МАКСИМУМ(ВЫБОР
		|					КОГДА НЕ Таблица.Склад.ИспользоватьОрдернуюСхемуПриПоступлении
		|							ИЛИ Таблица.Склад.ИспользоватьОрдернуюСхемуПриПоступлении
		|								И Таблица.Склад.ДатаНачалаОрдернойСхемыПриПоступлении > &ТекущаяДата
		|						ТОГДА 4
		|					КОГДА ЕСТЬNULL(ТаблицаОрдераПереопределяемый.КОформлениюПоступленийПоОрдерам, 0) = 0
		|							И Таблица.КОформлениюОрдеров = 0
		|						ТОГДА 0
		|					КОГДА Таблица.КОформлениюОрдеров > 0
		|							И ЕСТЬNULL(ТаблицаОрдераПереопределяемый.КОформлениюПоступленийПоОрдерам, 0) = 0
		|						ТОГДА 1
		|					КОГДА ЕСТЬNULL(ТаблицаОрдераПереопределяемый.КОформлениюПоступленийПоОрдерам, 0) > 0
		|						ТОГДА 3
		|					ИНАЧЕ 0
		|				КОНЕЦ) В (0, 4))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка,
		|	Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1001
		|	КОЛИЧЕСТВО(*) КАК Количество
		|ИЗ
		|	ВтДокументыССостояниями КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК Реестр
		|		ПО Таблица.Ссылка = Реестр.Ссылка
		|			И Таблица.Склад = Реестр.МестоХранения.Ключ";

	Возврат ТекстЗапроса
	
КонецФункции

#КонецОбласти

#КонецЕсли
