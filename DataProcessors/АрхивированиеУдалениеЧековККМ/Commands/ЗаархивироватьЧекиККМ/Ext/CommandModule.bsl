
&НаСервере
Процедура ВыполнитьАрхивациюЧековНаСервере(КассоваяСмена)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КассоваяСмена.Ссылка КАК КассоваяСмена
	|ИЗ
	|	Документ.КассоваяСмена КАК КассоваяСмена
	|ГДЕ
	|	КассоваяСмена.Проведен
	|	И КассоваяСмена.Ссылка В (&КассоваяСмена)
	|	И КассоваяСмена.СтатусРегламентныхОпераций <> ЗНАЧЕНИЕ(Перечисление.СтатусыРегламентныхОперацийКассовойСмены.СозданОтчетЧекиЗаархивированы)
	|");
	
	Запрос.УстановитьПараметр("КассоваяСмена", КассоваяСмена);
	
	ОбрабатываемыеКассовыеСмены = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("КассоваяСмена");
	
	ПараметрыЗадания = РозничныеПродажи.НовыйПараметрыЗаданияЗакрытияКассовойСмены();
	ПараметрыЗадания.КассоваяСмена = ОбрабатываемыеКассовыеСмены;
	ПараметрыЗадания.ОбработкаВыполнена = Ложь;
	
	РозничныеПродажи.ВыполнитьАрхивациюЧековККМ(ПараметрыЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ОчиститьСообщения();
	
	ВыполнитьАрхивациюЧековНаСервере(ПараметрКоманды);
	
	Оповестить("Запись_ОтчетОРозничныхПродажах", Новый Структура, ПараметрКоманды);
	Оповестить("Запись_ОтчетОРозничныхВозвратах", Новый Структура, ПараметрКоманды);
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Архивация чеков ККМ выполнена'", ОбщегоНазначенияКлиент.КодОсновногоЯзыка()));
	
КонецПроцедуры
