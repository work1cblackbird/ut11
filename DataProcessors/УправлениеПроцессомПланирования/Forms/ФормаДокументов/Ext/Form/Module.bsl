
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма,
		Параметры, 
		"Сценарий, ВидПлана, Номенклатура, Характеристика, Назначение, НачалоПериода, ОкончаниеПериода, ТипДанных");
	СписокВидовПланов.ЗагрузитьЗначения(Параметры.СписокВидовПланов);
	ЗаполнитьПланы();
	
	ЭтоГруппа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПлана, "ЭтоГруппа");
	Если Не ЗначениеЗаполнено(Номенклатура) Тогда
		Элементы.ПланыКоличество.Видимость = Ложь;
	КонецЕсли;
	Если Не ЭтоГруппа Тогда
		Элементы.ПланыВидПлана.Видимость = Ложь;
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru = '%ОбъектПланирования%%ВидПлана% %Товар%за период с %НачалоПериода% по %ОкончаниеПериода%'");
	
	Если ЗначениеЗаполнено(ВидПлана) Тогда
		Если ЭтоГруппа Тогда
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОбъектПланирования%", НСтр("ru = 'Планы по этапу планирования'") + " ");
		Иначе
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОбъектПланирования%", НСтр("ru = 'Планы по виду плана'") + " ");
		КонецЕсли;
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ВидПлана%", ВидПлана);
	Иначе
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОбъектПланирования%", НСтр("ru = 'Все планы'"));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ВидПлана%", ВидПлана);
	КонецЕсли;
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Товар%", НСтр("ru = 'по %Номенклатура%%Характеристика%%Назначение%'") + " ");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Номенклатура%", Номенклатура);
		Если ЗначениеЗаполнено(Характеристика) Тогда
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Характеристика%", ", " + Характеристика );
		Иначе
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Характеристика%", "");
		КонецЕсли;
		Если ЗначениеЗаполнено(Назначение) Тогда
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Назначение%", ", " + Назначение);
		Иначе
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Назначение%", "");
		КонецЕсли;
		
	Иначе
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Товар%", "");
	КонецЕсли;
	
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НачалоПериода%", НачалоПериода);
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОкончаниеПериода%", ОкончаниеПериода);
	
	ТекстНадписиРасшифровка = ТекстСообщения;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ПланПродаж"
		ИЛИ ИмяСобытия = "Запись_ПланЗакупок"
		ИЛИ ИмяСобытия = "Запись_ПланОстатков"
		ИЛИ ИмяСобытия = "Запись_ПланПроизводства"
		ИЛИ ИмяСобытия = "Запись_ПланСборкиРазборки"
		ИЛИ ИмяСобытия = "Запись_ПланВнутреннихПотреблений" Тогда
		ЗаполнитьПланы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Планы.Количество() = 0 Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Планы.Количество() = 1 Тогда
		Отказ = Истина;
		ПараметрыФормыТекущаяСтрока = Новый Структура();
		ПараметрыФормыТекущаяСтрока.Вставить("Номенклатура", Номенклатура);
		ПараметрыФормыТекущаяСтрока.Вставить("Характеристика", Характеристика);
		ПараметрыФормыТекущаяСтрока.Вставить("Назначение", Назначение);
		
		
		Если ТипЗнч(Планы[0].Документ) = Тип("ДокументСсылка.КорректировкаПлановыхПотребностей") Тогда
			ПоказатьЗначение(, Планы[0].Документ);
		Иначе
			Если ЗначениеЗаполнено(Планы[0].Документ) Тогда
				ПараметрыФормы = Новый Структура("Ключ, ТекущаяСтрока", Планы[0].Документ, ПараметрыФормыТекущаяСтрока);
			Иначе
				ЗначенияЗаполнения = Новый Структура();
				ЗначенияЗаполнения.Вставить("Сценарий", Сценарий);
				ЗначенияЗаполнения.Вставить("ВидПлана", Планы[0].ВидПлана);
				ЗначенияЗаполнения.Вставить("НачалоПериода", НачалоПериода);
				ЗначенияЗаполнения.Вставить("ОкончаниеПериода", ОкончаниеПериода);
				
				ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
			КонецЕсли;
			ТипПланаСтрока = ТипПланаСтрока(Планы[0].ВидПлана);
			ОткрытьФорму("Документ." + ТипПланаСтрока + ".ФормаОбъекта", 
			ПараметрыФормы);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьСтатусВПодготовке(Команда)
	
	УстановитьСтатус("ВПодготовке", НСтр("ru = 'В подготовке'"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусНаУтверждении(Команда)
	
	УстановитьСтатус("НаУтверждении", НСтр("ru = 'В работе'"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусУтвержден(Команда)
	
	УстановитьСтатус("Утвержден", НСтр("ru = 'Утвержден'"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусОтменен(Команда)
	
	УстановитьСтатус("Отменен", НСтр("ru = 'Отменен'"));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ПланыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.Планы.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Документ) Тогда
		
		Если ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.КорректировкаПлановыхПотребностей") Тогда
			ПоказатьЗначение(, ТекущиеДанные.Документ);
		Иначе
			ПараметрыФормы = Новый Структура("Ключ", ТекущиеДанные.Документ);
			Если ЗначениеЗаполнено("Номенклатура") Тогда
				ПараметрыФормыТекущаяСтрока = Новый Структура();
				ПараметрыФормыТекущаяСтрока.Вставить("Номенклатура", Номенклатура);
				ПараметрыФормыТекущаяСтрока.Вставить("Характеристика", Характеристика);
				ПараметрыФормыТекущаяСтрока.Вставить("Назначение", Назначение);
				ПараметрыФормы.Вставить("ТекущаяСтрока", ПараметрыФормыТекущаяСтрока);
			КонецЕсли;
			
			ТипПланаСтрока = ТипПланаСтрока(ТекущиеДанные.ВидПлана);
			ОткрытьФорму("Документ." + ТипПланаСтрока + ".ФормаОбъекта", 
			ПараметрыФормы);
		КонецЕсли;
		
	Иначе
		
		ЗначенияЗаполнения = Новый Структура();
		ЗначенияЗаполнения.Вставить("Сценарий", Сценарий);
		ЗначенияЗаполнения.Вставить("ВидПлана", ТекущиеДанные.ВидПлана);
		ЗначенияЗаполнения.Вставить("НачалоПериода", НачалоПериода);
		ЗначенияЗаполнения.Вставить("ОкончаниеПериода", ОкончаниеПериода);
		
		ТипПланаСтрока = ТипПланаСтрока(ТекущиеДанные.ВидПлана);
		ОткрытьФорму("Документ." + ТипПланаСтрока + ".ФормаОбъекта", 
			Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения));
		
	КонецЕсли;
	ЭтаФорма.Закрыть()
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.ПланыДокумент.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Планы.Документ");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
		
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ГиперссылкаЦвет);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Создать'"));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПланы()
	
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		Запрос = Новый Запрос();
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(ПланыЗакупокОбороты.КоличествоОборот) КАК Количество,
		|	СУММА(ПланыЗакупокОбороты.ЗаданИнтервалПотребностиОборот) КАК ЗаданИнтервалПотребностиОборот,
		|	Документ.Ссылка КАК Документ,
		|	Документ.МоментВремени КАК МоментВремени,
		|	Документ.Дата КАК ДатаДокумента,
		|	Документ.Номер КАК НомерДокумента,
		|	Документ.Статус КАК Статус,
		|	Документ.ВидПлана КАК ВидПлана,
		|	Документ.ВидПлана.ТипПлана КАК ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255)) КАК Комментарий
		|ИЗ
		|	РегистрНакопления.ПланыЗакупок.Обороты(
		|			&НачалоПериода,
		|			&ОкончаниеПериода,
		|			,
		|			Сценарий = &Сценарий
		|				И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|					ИЛИ ВидПлана В (&СписокВидовПланов))
		|				И (Номенклатура = &Номенклатура
		|						И Характеристика = &Характеристика
		|						И Назначение = &Назначение
		|					ИЛИ &БезНоменклатуры)) КАК ПланыЗакупокОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПланЗакупок КАК Документ
		|		ПО ПланыЗакупокОбороты.ПланЗакупок = Документ.Ссылка
		|ГДЕ
		|	&Приход
		|	И Документ.НачалоПериода <= &ОкончаниеПериода
		|	И Документ.ОкончаниеПериода >= &НачалоПериода
		|	И Документ.Проведен
		|	И Документ.Сценарий = &Сценарий
		|	И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|			ИЛИ Документ.ВидПлана В (&СписокВидовПланов))
		|
		|СГРУППИРОВАТЬ ПО
		|	Документ.Ссылка,
		|	Документ.МоментВремени,
		|	Документ.Дата,
		|	Документ.Номер,
		|	Документ.Статус,
		|	Документ.ВидПлана,
		|	Документ.ВидПлана.ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СУММА(ПланыПродажОбороты.КоличествоОборот),
		|	0,
		|	Документ.Ссылка,
		|	Документ.МоментВремени,
		|	Документ.Дата,
		|	Документ.Номер,
		|	Документ.Статус,
		|	Документ.ВидПлана,
		|	Документ.ВидПлана.ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255))
		|ИЗ
		|	РегистрНакопления.ПланыПродаж.Обороты(
		|			&НачалоПериода,
		|			&ОкончаниеПериода,
		|			,
		|			Сценарий = &Сценарий
		|				И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|					ИЛИ ВидПлана В (&СписокВидовПланов))
		|				И (Номенклатура = &Номенклатура
		|						И Характеристика = &Характеристика
		|						И Назначение = &Назначение
		|					ИЛИ &БезНоменклатуры)) КАК ПланыПродажОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПланПродаж КАК Документ
		|		ПО ПланыПродажОбороты.ПланПродаж = Документ.Ссылка
		|ГДЕ
		|	&Расход
		|	И Документ.НачалоПериода <= &ОкончаниеПериода
		|	И Документ.ОкончаниеПериода >= &НачалоПериода
		|	И Документ.Проведен
		|	И Документ.Сценарий = &Сценарий
		|	И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|			ИЛИ Документ.ВидПлана В (&СписокВидовПланов))
		|
		|СГРУППИРОВАТЬ ПО
		|	Документ.Ссылка,
		|	Документ.МоментВремени,
		|	Документ.Дата,
		|	Документ.Номер,
		|	Документ.Статус,
		|	Документ.ВидПлана,
		|	Документ.ВидПлана.ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СУММА(ПланыВнутреннихПотребленийОбороты.КоличествоОборот),
		|	0,
		|	Документ.Ссылка,
		|	Документ.МоментВремени,
		|	Документ.Дата,
		|	Документ.Номер,
		|	Документ.Статус,
		|	Документ.ВидПлана,
		|	Документ.ВидПлана.ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255))
		|ИЗ
		|	РегистрНакопления.ПланыВнутреннихПотреблений.Обороты(
		|			&НачалоПериода,
		|			&ОкончаниеПериода,
		|			,
		|			Сценарий = &Сценарий
		|				И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|					ИЛИ ВидПлана В (&СписокВидовПланов))
		|				И (Номенклатура = &Номенклатура
		|						И Характеристика = &Характеристика
		|						И Назначение = &Назначение
		|					ИЛИ &БезНоменклатуры)) КАК ПланыВнутреннихПотребленийОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПланВнутреннихПотреблений КАК Документ
		|		ПО ПланыВнутреннихПотребленийОбороты.ПланВнутреннихПотреблений = Документ.Ссылка
		|ГДЕ
		|	&Расход
		|	И Документ.НачалоПериода <= &ОкончаниеПериода
		|	И Документ.ОкончаниеПериода >= &НачалоПериода
		|	И Документ.Проведен
		|	И Документ.Сценарий = &Сценарий
		|	И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|			ИЛИ Документ.ВидПлана В (&СписокВидовПланов))
		|
		|СГРУППИРОВАТЬ ПО
		|	Документ.Ссылка,
		|	Документ.МоментВремени,
		|	Документ.Дата,
		|	Документ.Номер,
		|	Документ.Статус,
		|	Документ.ВидПлана,
		|	Документ.ВидПлана.ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СУММА(ПланыОстатковОбороты.КоличествоОборот),
		|	0,
		|	Документ.Ссылка,
		|	Документ.МоментВремени,
		|	Документ.Дата,
		|	Документ.Номер,
		|	Документ.Статус,
		|	Документ.ВидПлана,
		|	Документ.ВидПлана.ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255))
		|ИЗ
		|	РегистрНакопления.ПланыОстатков.Обороты(
		|			&НачалоПериода,
		|			&ОкончаниеПериода,
		|			,
		|			Сценарий = &Сценарий
		|				И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|					ИЛИ ВидПлана В (&СписокВидовПланов))
		|				И (Номенклатура = &Номенклатура
		|						И Характеристика = &Характеристика
		|						И Назначение = &Назначение
		|					ИЛИ &БезНоменклатуры)) КАК ПланыОстатковОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПланОстатков КАК Документ
		|		ПО ПланыОстатковОбороты.ПланОстатков = Документ.Ссылка
		|ГДЕ
		|	&НачальныйОстаток
		|	И Документ.НачалоПериода <= &ОкончаниеПериода
		|	И Документ.ОкончаниеПериода >= &НачалоПериода
		|	И Документ.Проведен
		|	И Документ.Сценарий = &Сценарий
		|	И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|			ИЛИ Документ.ВидПлана В (&СписокВидовПланов))
		|
		|СГРУППИРОВАТЬ ПО
		|	Документ.Ссылка,
		|	Документ.МоментВремени,
		|	Документ.Дата,
		|	Документ.Номер,
		|	Документ.Статус,
		|	Документ.ВидПлана,
		|	Документ.ВидПлана.ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СУММА(ПланыСборкиРазборкиОбороты.КоличествоОборот),
		|	СУММА(ПланыСборкиРазборкиОбороты.ЗаданИнтервалПотребностиОборот),
		|	Документ.Ссылка,
		|	Документ.МоментВремени,
		|	Документ.Дата,
		|	Документ.Номер,
		|	Документ.Статус,
		|	Документ.ВидПлана,
		|	Документ.ВидПлана.ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255))
		|ИЗ
		|	РегистрНакопления.ПланыСборкиРазборки.Обороты(
		|			&НачалоПериода,
		|			&ОкончаниеПериода,
		|			,
		|			Сценарий = &Сценарий
		|				И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|					ИЛИ ВидПлана В (&СписокВидовПланов))
		|				И (Номенклатура = &Номенклатура
		|						И Характеристика = &Характеристика
		|						И Назначение = &Назначение
		|					ИЛИ &БезНоменклатуры)) КАК ПланыСборкиРазборкиОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПланСборкиРазборки КАК Документ
		|		ПО ПланыСборкиРазборкиОбороты.ПланСборкиРазборки = Документ.Ссылка
		|ГДЕ
		|	&Приход
		|	И Документ.НачалоПериода <= &ОкончаниеПериода
		|	И Документ.ОкончаниеПериода >= &НачалоПериода
		|	И Документ.Проведен
		|	И Документ.Сценарий = &Сценарий
		|	И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|			ИЛИ Документ.ВидПлана В (&СписокВидовПланов))
		|
		|СГРУППИРОВАТЬ ПО
		|	Документ.Ссылка,
		|	Документ.МоментВремени,
		|	Документ.Дата,
		|	Документ.Номер,
		|	Документ.Статус,
		|	Документ.ВидПлана,
		|	Документ.ВидПлана.ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СУММА(ПланыПотребленияКомплектующихОбороты.КоличествоОборот),
		|	0,
		|	Документ.Ссылка,
		|	Документ.МоментВремени,
		|	Документ.Дата,
		|	Документ.Номер,
		|	Документ.Статус,
		|	Документ.ВидПлана,
		|	Документ.ВидПлана.ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255))
		|ИЗ
		|	РегистрНакопления.ПланыПотребленияКомплектующих.Обороты(
		|			&НачалоПериода,
		|			&ОкончаниеПериода,
		|			,
		|			Сценарий = &Сценарий
		|				И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|					ИЛИ ВидПлана В (&СписокВидовПланов))
		|				И (Номенклатура = &Номенклатура
		|						И Характеристика = &Характеристика
		|						И Назначение = &Назначение
		|					ИЛИ &БезНоменклатуры)) КАК ПланыПотребленияКомплектующихОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПланСборкиРазборки КАК Документ
		|		ПО ПланыПотребленияКомплектующихОбороты.ПланСборкиРазборки = Документ.Ссылка
		|ГДЕ
		|	&Расход
		|	И Документ.НачалоПериода <= &ОкончаниеПериода
		|	И Документ.ОкончаниеПериода >= &НачалоПериода
		|	И Документ.Проведен
		|	И Документ.Сценарий = &Сценарий
		|	И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|			ИЛИ Документ.ВидПлана В (&СписокВидовПланов))
		|
		|СГРУППИРОВАТЬ ПО
		|	Документ.Ссылка,
		|	Документ.МоментВремени,
		|	Документ.Дата,
		|	Документ.Номер,
		|	Документ.Статус,
		|	Документ.ВидПлана,
		|	Документ.ВидПлана.ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СУММА(КорректировкиПлановыхПотребностейОбороты.КоличествоОборот),
		|	0,
		|	Документ.Ссылка,
		|	Документ.МоментВремени,
		|	Документ.Дата,
		|	Документ.Номер,
		|	Документ.Статус,
		|	Документ.ВидПлана,
		|	Документ.ВидПлана.ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255))
		|ИЗ
		|	РегистрНакопления.КорректировкиПлановыхПотребностей.Обороты(
		|			&НачалоПериода,
		|			&ОкончаниеПериода,
		|			,
		|			Сценарий = &Сценарий
		|				И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|					ИЛИ ВидПлана В (&СписокВидовПланов))
		|				И (Номенклатура = &Номенклатура
		|						И Характеристика = &Характеристика
		|						И Назначение = &Назначение
		|					ИЛИ &БезНоменклатуры)) КАК КорректировкиПлановыхПотребностейОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаПлановыхПотребностей КАК Документ
		|		ПО КорректировкиПлановыхПотребностейОбороты.КорректировкаПлановыхПотребностей = Документ.Ссылка
		|ГДЕ
		|	&КорректировкаРасхода
		|	И Документ.Проведен
		|	И Документ.Сценарий = &Сценарий
		|	И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|			ИЛИ Документ.ВидПлана В (&СписокВидовПланов))
		|
		|СГРУППИРОВАТЬ ПО
		|	Документ.Ссылка,
		|	Документ.МоментВремени,
		|	Документ.Дата,
		|	Документ.Номер,
		|	Документ.Статус,
		|	Документ.ВидПлана,
		|	Документ.ВидПлана.ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255))
		|
		|УПОРЯДОЧИТЬ ПО
		|	МоментВремени";
	Иначе
		Запрос = Новый Запрос();
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	0 КАК Количество,
		|	Документ.Ссылка КАК Документ,
		|	Документ.МоментВремени КАК МоментВремени,
		|	Документ.Дата КАК ДатаДокумента,
		|	Документ.Номер КАК НомерДокумента,
		|	Документ.Статус КАК Статус,
		|	Документ.ВидПлана КАК ВидПлана,
		|	Документ.ВидПлана.ТипПлана КАК ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255)) КАК Комментарий
		|ИЗ
		|	Документ.ПланЗакупок КАК Документ
		|ГДЕ
		|	Документ.НачалоПериода <= &ОкончаниеПериода
		|	И Документ.ОкончаниеПериода >= &НачалоПериода
		|	И Документ.Проведен
		|	И Документ.Статус.Порядок >=1
		|	И Документ.Сценарий = &Сценарий
		|	И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|			ИЛИ Документ.ВидПлана В (&СписокВидовПланов))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0,
		|	Документ.Ссылка,
		|	Документ.МоментВремени,
		|	Документ.Дата,
		|	Документ.Номер,
		|	Документ.Статус,
		|	Документ.ВидПлана,
		|	Документ.ВидПлана.ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255))
		|ИЗ
		|	Документ.ПланПродаж КАК Документ
		|ГДЕ
		|	Документ.НачалоПериода <= &ОкончаниеПериода
		|	И Документ.ОкончаниеПериода >= &НачалоПериода
		|	И Документ.Проведен
		|	И Документ.Статус.Порядок >=1
		|	И Документ.Сценарий = &Сценарий
		|	И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|			ИЛИ Документ.ВидПлана В (&СписокВидовПланов))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0,
		|	Документ.Ссылка,
		|	Документ.МоментВремени,
		|	Документ.Дата,
		|	Документ.Номер,
		|	Документ.Статус,
		|	Документ.ВидПлана,
		|	Документ.ВидПлана.ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255))
		|ИЗ
		|	Документ.ПланПродажПоКатегориям КАК Документ
		|ГДЕ
		|	Документ.НачалоПериода <= &ОкончаниеПериода
		|	И Документ.ОкончаниеПериода >= &НачалоПериода
		|	И Документ.Проведен
		|	И Документ.Статус.Порядок >=1
		|	И Документ.Сценарий = &Сценарий
		|	И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|			ИЛИ Документ.ВидПлана В (&СписокВидовПланов))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0,
		|	Документ.Ссылка,
		|	Документ.МоментВремени,
		|	Документ.Дата,
		|	Документ.Номер,
		|	Документ.Статус,
		|	Документ.ВидПлана,
		|	Документ.ВидПлана.ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255))
		|ИЗ
		|	Документ.ПланВнутреннихПотреблений КАК Документ
		|ГДЕ
		|	Документ.НачалоПериода <= &ОкончаниеПериода
		|	И Документ.ОкончаниеПериода >= &НачалоПериода
		|	И Документ.Проведен
		|	И Документ.Статус.Порядок >=1
		|	И Документ.Сценарий = &Сценарий
		|	И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|			ИЛИ Документ.ВидПлана В (&СписокВидовПланов))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0,
		|	Документ.Ссылка,
		|	Документ.МоментВремени,
		|	Документ.Дата,
		|	Документ.Номер,
		|	Документ.Статус,
		|	Документ.ВидПлана,
		|	Документ.ВидПлана.ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255))
		|ИЗ
		|	Документ.ПланОстатков КАК Документ
		|ГДЕ
		|	Документ.НачалоПериода <= &ОкончаниеПериода
		|	И Документ.ОкончаниеПериода >= &НачалоПериода
		|	И Документ.Проведен
		|	И Документ.Статус.Порядок >=1
		|	И Документ.Сценарий = &Сценарий
		|	И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|			ИЛИ Документ.ВидПлана В (&СписокВидовПланов))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0,
		|	Документ.Ссылка,
		|	Документ.МоментВремени,
		|	Документ.Дата,
		|	Документ.Номер,
		|	Документ.Статус,
		|	Документ.ВидПлана,
		|	Документ.ВидПлана.ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255))
		|ИЗ
		|	Документ.ПланСборкиРазборки КАК Документ
		|ГДЕ
		|	Документ.НачалоПериода <= &ОкончаниеПериода
		|	И Документ.ОкончаниеПериода >= &НачалоПериода
		|	И Документ.Проведен
		|	И Документ.Статус.Порядок >=1
		|	И Документ.Сценарий = &Сценарий
		|	И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|			ИЛИ Документ.ВидПлана В (&СписокВидовПланов))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0,
		|	Документ.Ссылка,
		|	Документ.МоментВремени,
		|	Документ.Дата,
		|	Документ.Номер,
		|	Документ.Статус,
		|	Документ.ВидПлана,
		|	Документ.ВидПлана.ТипПлана,
		|	ВЫРАЗИТЬ(Документ.Комментарий КАК СТРОКА(255))
		|ИЗ
		|	Документ.КорректировкаПлановыхПотребностей КАК Документ
		|ГДЕ
		|	Документ.Период <= &ОкончаниеПериода
		|	И Документ.Период >= &НачалоПериода
		|	И Документ.Проведен
		|	И Документ.Статус.Порядок >=1
		|	И Документ.Сценарий = &Сценарий
		|	И (&ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|			ИЛИ Документ.ВидПлана В (&СписокВидовПланов))
		|
		|УПОРЯДОЧИТЬ ПО
		|	МоментВремени";
	КонецЕсли;
		
	Запрос.УстановитьПараметр("Сценарий", Сценарий);
	Запрос.УстановитьПараметр("ВидПлана", ВидПлана);
	Запрос.УстановитьПараметр("СписокВидовПланов", СписокВидовПланов.ВыгрузитьЗначения());
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	Запрос.УстановитьПараметр("Назначение", Назначение);
	Запрос.УстановитьПараметр("БезНоменклатуры", Не ЗначениеЗаполнено(Номенклатура));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	Запрос.УстановитьПараметр("НачальныйОстаток", ТипДанных = "НачальныйОстаток" Или ТипДанных = "");
	Запрос.УстановитьПараметр("Приход", ТипДанных = "Приход" Или ТипДанных = "");
	Запрос.УстановитьПараметр("Расход", ТипДанных = "Расход" Или ТипДанных = "");
	Запрос.УстановитьПараметр("КорректировкаРасхода", ТипДанных = "КорректировкаРасхода" Или ТипДанных = "" Или (ТипДанных = "Расход" И Не ЗначениеЗаполнено(ВидПлана)));
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаПланов = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	Планы.Загрузить(ТаблицаПланов);
	
	Если Не ЗначениеЗаполнено(Номенклатура)
		И ЗначениеЗаполнено(ВидПлана) Тогда
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПлана, "ЭтоГруппа") Тогда
			
			Для Каждого ЭлементСписка Из СписокВидовПланов Цикл
				НоваяСтрока = Планы.Добавить();
				НоваяСтрока.ВидПлана = ЭлементСписка.Значение;
				НоваяСтрока.Документ = Неопределено
			КонецЦикла;
			
		Иначе
			
			НоваяСтрока = Планы.Добавить();
			НоваяСтрока.Документ = Неопределено;
			НоваяСтрока.ВидПлана =  ВидПлана;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ТипПланаСтрока(ВидПлана)
	ТипПлана = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПлана, "ТипПлана");
	Если ТипПлана = Перечисления.ТипыПланов.ПланЗакупок Тогда
		СтрокаТипПлана = "ПланЗакупок";
	ИначеЕсли ТипПлана = Перечисления.ТипыПланов.ПланОстатков Тогда
		СтрокаТипПлана = "ПланОстатков";
	ИначеЕсли ТипПлана = Перечисления.ТипыПланов.ПланПродаж Тогда
		СтрокаТипПлана = "ПланПродаж";
	ИначеЕсли ТипПлана = Перечисления.ТипыПланов.ПланПродажПоКатегориям Тогда
		СтрокаТипПлана = "ПланПродажПоКатегориям";
	ИначеЕсли ТипПлана = Перечисления.ТипыПланов.ПланСборкиРазборки Тогда
		СтрокаТипПлана = "ПланСборкиРазборки";
	ИначеЕсли ТипПлана = Перечисления.ТипыПланов.ПланВнутреннихПотреблений Тогда
		СтрокаТипПлана = "ПланВнутреннихПотреблений";
	КонецЕсли;
	
	Возврат СтрокаТипПлана
КонецФункции

&НаКлиенте
Процедура УстановитьСтатус(Статус, ТекстСтатуса)
	
	ВыделенныеСтроки = ОбщегоНазначенияУТКлиент.ПроверитьПолучитьВыделенныеВСпискеСсылки(Элементы.Планы);
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ВыделенныеКорректировки = Новый Массив;
	ВыделенныеПланы = Новый Массив;
	
	Для Каждого Индекс Из ВыделенныеСтроки Цикл
		СсылкаНаДокумент = Планы.НайтиПоИдентификатору(Индекс).Документ;
		Если ТипЗнч(СсылкаНаДокумент) = Тип("ДокументСсылка.КорректировкаПлановыхПотребностей") Тогда
			Если (Статус = "Утвержден" ИЛИ Статус = "Отменен") Тогда 
				ВыделенныеКорректировки.Добавить(СсылкаНаДокумент);
			КонецЕсли;
		ИначеЕсли ЗначениеЗаполнено(СсылкаНаДокумент) Тогда
			ВыделенныеПланы.Добавить(СсылкаНаДокумент);
		КонецЕсли;
	КонецЦикла;
	
	Если ВыделенныеКорректировки.Количество() = 0
		И ВыделенныеПланы.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru='Команда не может быть выполнена для указанного объекта!'"));
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru='У выделенных в списке документов будет установлен статус ""%Статус%"". Продолжить?'");
	ТекстВопроса = СтрЗаменить(ТекстВопроса, "%Статус%", ТекстСтатуса);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Статус", Статус);
	ДополнительныеПараметры.Вставить("ТекстСтатуса", ТекстСтатуса);
	ДополнительныеПараметры.Вставить("ВыделенныеКорректировки", ВыделенныеКорректировки);
	ДополнительныеПараметры.Вставить("ВыделенныеПланы", ВыделенныеПланы);
	
	Оповещение = Новый ОписаниеОповещения("УстановитьСтатусЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
		
	ОчиститьСообщения();
	КоличествоОбработанных = 0;
	
	КоличествоОбработанных = ОбщегоНазначенияУТВызовСервера.УстановитьСтатусДокументов(
		ДополнительныеПараметры.ВыделенныеПланы, 
		ДополнительныеПараметры.Статус);
		
	СтатусКорректировки = ?(ДополнительныеПараметры.Статус = "Утвержден", "Утверждена", "Отменена");
	КоличествоОбработанных = КоличествоОбработанных + ОбщегоНазначенияУТВызовСервера.УстановитьСтатусДокументов(
		ДополнительныеПараметры.ВыделенныеКорректировки, 
		СтатусКорректировки);
		
	ОбщегоНазначенияУТКлиент.ОповеститьПользователяОбУстановкеСтатуса(Элементы.Планы, 
		КоличествоОбработанных, 
		ДополнительныеПараметры.ВыделенныеПланы.Количество() + ДополнительныеПараметры.ВыделенныеКорректировки.Количество(), 
		ДополнительныеПараметры.ТекстСтатуса);
		
	ЗаполнитьПланы();
	
КонецПроцедуры

#КонецОбласти
