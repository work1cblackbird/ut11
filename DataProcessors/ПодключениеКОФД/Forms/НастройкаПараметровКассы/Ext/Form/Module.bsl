///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Параметры.Касса) Тогда
		ВызватьИсключение НСтр("ru = 'Некорректные параметры формы.'");
	Иначе
		Касса = Параметры.Касса;
	КонецЕсли;
	
	Если Не ОФДСлужебный.НастройкаПодключенияДоступна() Тогда
		ВызватьИсключение НСтр("ru = 'У пользователя недостаточно прав для изменения настроек.'");
	КонецЕсли;
	
	ДанныеНастроекПоКассе = РегистрыСведений.НастройкиКассОФД.ДанныеНастроекПоКассе(Касса);
	
	Если ДанныеНастроекПоКассе <> Неопределено Тогда
		
		НастройкаПодключения    = ДанныеНастроекПоКассе.НастройкаПодключения;
		Магазин                 = ДанныеНастроекПоКассе.Магазин;
		ПредставлениеКассы      = ДанныеНастроекПоКассе.ПредставлениеКассы;
		СценарийИспользования   = ДанныеНастроекПоКассе.СценарийИспользования;
		ЗаводскойНомерФН        = ДанныеНастроекПоКассе.ЗаводскойНомерФН;
		РегистрационныйНомерККТ = ДанныеНастроекПоКассе.РегистрационныйНомерККТ;
		
	Иначе
		
		Элементы.ДекорацияПояснениеКФорме.Заголовок = НСтр("ru = 'Для продолжения выберите настройку подключения.'");
		
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НастройкаПодключенияСоздание(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ПослеЗакрытияФормыСозданияНастройки",
		ЭтотОбъект);
		
	ОФДКлиент.СлужебнаяПодключитьОФД(ОповещениеОЗавершении);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	
	ВыполнитьЗаписьНастроекКассы();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ВыполнитьЗаписьНастроекКассы();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодключение(Команда)
	
	Если ЭтотОбъект.Модифицированность Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Перед проверкой настроек необходимо записать изменения.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Проверка параметров подключения к оператору фискальных данных.'");
	
	РезультатВыполнения = ПроверитьПараметрыПодключенияЗапускЗадания(
		НастройкаПодключения,
		ЭтотОбъект.УникальныйИдентификатор);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ПроверитьПараметрыПодключенияЗавершение",
		ЭтотОбъект);
		
	Если РезультатВыполнения.Статус = "Выполнено" Или РезультатВыполнения.Статус = "Ошибка" Тогда
		ПроверитьПараметрыПодключенияЗавершение(РезультатВыполнения, Неопределено);
		Возврат;
	КонецЕсли;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения,
		ОповещениеОЗавершении,
		ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура ПодключитьКассу(Команда)
	
	Если Не ЗначениеЗаполнено(НастройкаПодключения) Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Для продолжения необходимо выбрать настройку подключения.'"),
			,
			"НастройкаПодключения");
			
		Возврат;
		
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Касса", Касса);
	ПараметрыОткрытия.Вставить("НастройкаПодключения", НастройкаПодключения);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ПодключитьКассуПриПомощиМастераЗавершение",
		ЭтотОбъект);
	
	ОткрытьФорму(
		"Обработка.ПодключениеКОФД.Форма.НастройкаСценарияИспользования",
		ПараметрыОткрытия,
		,
		,
		,
		,
		ОповещениеОЗавершении);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьКассу(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ОтключитьКассуПослеЗакрытияВопроса", ЭтотОбъект);
	ПоказатьВопрос(
		Оповещение,
		НСтр("ru = 'Отключить получение данных от ОФД по кассе?'"),
		РежимДиалогаВопрос.ДаНет);
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеЗакрытияФормыСозданияНастройки (Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат <> КодВозвратаДиалога.Отмена Тогда
		НастройкаПодключения = Результат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьПараметрыПодключенияЗапускЗадания(Знач НастройкаПодключения, Знач УникальныйИдентификатор)
	
	ПараметрыФункции = Новый Структура;
	ПараметрыФункции.Вставить("НастройкаПодключения", НастройкаПодключения);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания
		= НСтр("ru = 'Проверка параметров подключения к оператору фискальных данных.'");
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"ОФДСлужебный.КассыНастройкиПодключения",
		ПараметрыФункции);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьПараметрыПодключенияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		
		Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			ПоказатьПредупреждение(
				Неопределено,
				РезультатОперации.СообщениеОбОшибке);
			Возврат;
		КонецЕсли;
		
		Для Каждого ДанныеМагазин Из РезультатОперации.ДанныеКасс Цикл
			Для Каждого ДанныеКассы Из ДанныеМагазин.ПереченьКасс Цикл
				
				Если Не ДанныеКассы.РегистрационныйНомер = РегистрационныйНомерККТ Тогда
					Продолжить;
				КонецЕсли;
				
				Если ДанныеКассы.НомерФискальногоНакопителя = ЗаводскойНомерФН Тогда
					ПоказатьПредупреждение(
						Неопределено,
						НСтр("ru = 'Проверка подключения завершена успешно.'"));
					Возврат;
				Иначе
					ПоказатьПредупреждение(
						Неопределено,
						НСтр("ru = 'Номер фискального накопителя не совпадает с данными ОФД. Проверьте правильность ввода.'"));
					Возврат;
				КонецЕсли;
				
			КонецЦикла;
		КонецЦикла;
		
		ПоказатьПредупреждение(
			Неопределено,
			НСтр("ru = 'Ошибка. По данным ОФД касса не найдена.'"));
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ПоказатьПредупреждение(
			Неопределено,
			Результат.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьКассуПослеЗакрытияВопроса(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Операция отключения кассы на портале.'");
	
	РезультатВыполнения = ОтключитьКассуЗапускЗадания(
		РегистрационныйНомерККТ,
		НастройкаПодключения,
		ЭтотОбъект.УникальныйИдентификатор);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ОтключитьКассуЗавершение",
		ЭтотОбъект);
	
	Если РезультатВыполнения.Статус = "Выполнено" Или РезультатВыполнения.Статус = "Ошибка" Тогда
		ОтключитьКассуЗавершение(РезультатВыполнения, Неопределено);
		Возврат;
	КонецЕсли;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОтключитьКассуЗапускЗадания(
	Знач РегистрационныйНомерККТ,
	Знач НастройкаПодключения,
	Знач УникальныйИдентификатор)
	
	ПараметрыФункции = Новый Структура;
	ПараметрыФункции.Вставить("РегистрационныйНомерККТ", РегистрационныйНомерККТ);
	ПараметрыФункции.Вставить("НастройкаПодключения",    НастройкаПодключения);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Отключение кассы на портале.'");
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"СервисИнтеграцииСОФД.ОтключитьКассу",
		ПараметрыФункции);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Процедура ОтключитьКассуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			ПоказатьПредупреждение(
				Неопределено,
				РезультатОперации.СообщениеОбОшибке);
		Иначе
			
			УдалитьНастройкиКассы(Касса);
			
			ПоказатьПредупреждение(
				Неопределено,
				НСтр("ru = 'Отключение кассы завершено успешно.'"));
				
			НастройкаПодключения    = ПредопределенноеЗначение("Справочник.НастройкиПодключенияКОФД.ПустаяСсылка");
			Магазин                 = "";
			ПредставлениеКассы                   = "";
			СценарийИспользования   = "";
			РегистрационныйНомерККТ = "";
			ЗаводскойНомерФН        = "";
		
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ПоказатьПредупреждение(
			Неопределено,
			Результат.КраткоеПредставлениеОшибки);
	КонецЕсли;
		
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьКассуПриПомощиМастераЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено
		Или Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Результат);
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗаписьНастроекКассы()
	
	НастройкиКассы = Новый Структура;
	НастройкиКассы.Вставить("НастройкаПодключения",    НастройкаПодключения);
	НастройкиКассы.Вставить("РегистрационныйНомерККТ", РегистрационныйНомерККТ);
	НастройкиКассы.Вставить("ЗаводскойНомерФН",        ЗаводскойНомерФН);
	НастройкиКассы.Вставить("Магазин",                 Магазин);
	НастройкиКассы.Вставить("ПредставлениеКассы",      ПредставлениеКассы);
	НастройкиКассы.Вставить("СценарийИспользования",   СценарийИспользования);
	
	ЗаписатьНастройкиКассыОФД(Касса ,НастройкиКассы);
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьНастройкиКассыОФД(Знач Касса, Знач НастройкиКассы)
	
	РегистрыСведений.НастройкиКассОФД.ЗаписатьНастройкиКассы(Касса, НастройкиКассы);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьНастройкиКассы(Знач Касса)
	
	РегистрыСведений.НастройкиКассОФД.УдалитьНастройкиКассы(Касса);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступность()
	
	Если СценарийИспользования = "ОшибкаПолученияДанных" Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ЗаводскойНомерФН.ТолькоПросмотр     = Не ЗначениеЗаполнено(ЗаводскойНомерФН);
	Элементы.ПроверитьПодключение.Видимость      = ЗначениеЗаполнено(ЗаводскойНомерФН);
	Элементы.ГруппаПрикладныеНастройки.Видимость = ЗначениеЗаполнено(Магазин);
	
	Если ПустаяСтрока(СценарийИспользования)
		Или СценарийИспользования = "Отмена" Тогда
		Элементы.ДекорацияПояснениеКФорме.Заголовок =
			НСтр("ru = 'Для работы с кассой необходимо выполнить подключение.'");
		Элементы.ПодключитьКассу.Видимость           = Истина;
		Элементы.ОтключитьКассу.Видимость            = Ложь;
		Элементы.НастройкаПодключения.ТолькоПросмотр = Ложь;
	ИначеЕсли СценарийИспользования = "ИспользоватьЗагрузкуИтоговСмены" Тогда
		Элементы.ДекорацияПояснениеКФорме.Заголовок =
			НСтр("ru = 'Выполняет сверку итогов смены с данными ОФД перед ее закрытием.'");
		Элементы.ПодключитьКассу.Видимость           = Ложь;
		Элементы.ОтключитьКассу.Видимость            = Истина;
		Элементы.НастройкаПодключения.ТолькоПросмотр = Истина;
	ИначеЕсли СценарийИспользования = "ИспользоватьЗагрузкуДокументов" Тогда
		Элементы.ДекорацияПояснениеКФорме.Заголовок =
			НСтр("ru = 'Выполняет сверку итогов смены с данными ОФД перед ее закрытием, загрузку данных продаж в документы,
				|просмотр данных чеков по документу.'");
		Элементы.ПодключитьКассу.Видимость           = Ложь;
		Элементы.ОтключитьКассу.Видимость            = Истина;
		Элементы.НастройкаПодключения.ТолькоПросмотр = Истина;
	Иначе
		Элементы.ДекорацияПояснениеКФорме.Заголовок =
			НСтр("ru = 'Касса используется для получения данных из ОФД.'");
		Элементы.ПодключитьКассу.Видимость           = Ложь;
		Элементы.ОтключитьКассу.Видимость            = Истина;
		Элементы.НастройкаПодключения.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаводскойНомерФНПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры


#КонецОбласти
