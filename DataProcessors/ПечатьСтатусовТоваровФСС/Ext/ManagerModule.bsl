#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	СтруктураТипов = ОбщегоНазначенияУТ.СоответствиеМассивовПоТипамОбъектов(МассивОбъектов);
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ДанныеПроверкиТоваровФСС") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ДанныеПроверкиТоваровФСС",
			НСтр("ru = 'Статус товаров ФСС'"),
			СформироватьПечатнуюФормуСтатусТоваровФСС(СтруктураТипов, ОбъектыПечати, ПараметрыПечати));
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуСтатусТоваровФСС(СтруктураТипов, ОбъектыПечати, ПараметрыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СтатусТоваровФСС";
	
	НомерТипаДокумента = 0;
	
	Для Каждого СтруктураОбъектов Из СтруктураТипов Цикл
		
		Если НЕ (СтруктураОбъектов.Ключ = "Документ.ЧекККМ"
			Или СтруктураОбъектов.Ключ = "Документ.ЧекККМВозврат") Тогда
			Продолжить;
		КонецЕсли;
		
		НомерТипаДокумента = НомерТипаДокумента + 1;
		Если НомерТипаДокумента > 1 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ЗаполнитьТабличныйДокументСтатусТоваровФСС(ТабличныйДокумент, ПараметрыПечати.ДанныеДляПечати, ОбъектыПечати);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Параметры:
// 	ТабличныйДокумент - ТабличныйДокумент
// 	ДанныеДляПечати - Структура:
// 		* ДанныеОплаты - Структура:
// 			** Ссылка - ДокументСсылка.ЧекККМ, ДокументСсылка.ЧекККМВозврат - 
// 			** ЭтоВозврат - Булево
// 			** СуммаЧека - Число
// 			** СуммаЧекФСС - Число
// 			** СуммаЭС - Число
// 		* ТоварыФСС - ТаблицаЗначений
// 		* ЗаголовокДокумента - Строка
// 	ОбъектыПечати - СписокЗначений - 
// 
Процедура ЗаполнитьТабличныйДокументСтатусТоваровФСС(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ШаблонОшибкиТовары = НСтр("ru = 'В документе %1 отсутствуют товары ФСС. Печать товарного чека не требуется'");
	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьСтатусовТоваровФСС.ПФ_MXL_ДанныеПроверкиТоваровФСС");
	
	ДанныеОплаты = ДанныеДляПечати.ДанныеОплаты;
	ТоварыФСС = ДанныеДляПечати.ТоварыФСС;
	
	ЭтоВозврат = ДанныеОплаты.ЭтоВозврат;
	
	Если ТоварыФСС.Количество() = 0 Тогда
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОшибкиТовары,
			ДанныеОплаты.Ссылка);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ДанныеОплаты.Ссылка);
		Возврат;
		
	КонецЕсли;
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	
	Если ЭтоВозврат Тогда
		ЗаголовокТаблицы = НСтр("ru = 'Товары к возврату на электронный сертификат ФСС'");
	Иначе
		ЗаголовокТаблицы = НСтр("ru = 'Товары к оплате электронным сертификатом ФСС'");
	КонецЕсли;
	
	УстановитьПараметр(ОбластьЗаголовок, "ЗаголовокТаблицы", ЗаголовокТаблицы);
	
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
	
	СуммаЧекФСС = 0;
	
	Для Каждого СтрокаТоваров Из ТоварыФСС Цикл
		
		ОбластьДанныхСтроки = Макет.ПолучитьОбласть("Строка");
		УстановитьПараметр(
			ОбластьДанныхСтроки,
			"Наименование",
			СтрШаблон("%1, %2", СтрокаТоваров.Артикул, СокрЛП(СтрокаТоваров.Номенклатура)));
		ОбластьДанныхСтроки.Параметры.Заполнить(СтрокаТоваров);
		ТабличныйДокумент.Вывести(ОбластьДанныхСтроки);
		
		СуммаЧекФСС = СуммаЧекФСС + СтрокаТоваров.Сумма;
	КонецЦикла;
	
	ОбластьПодвалФСС = Макет.ПолучитьОбласть("ПодвалФСС");
	УстановитьПараметр(ОбластьПодвалФСС, "Сумма", СуммаЧекФСС);
	УстановитьПараметр(ОбластьПодвалФСС, "СуммаФСС", ДанныеОплаты.СуммаЭС);
	ТабличныйДокумент.Вывести(ОбластьПодвалФСС);
	
	Если НЕ ЭтоВозврат Тогда
		
		ОбластьПрочиеТовары = Макет.ПолучитьОбласть("ПрочиеТовары");
		УстановитьПараметр(ОбластьПрочиеТовары, "Сумма", ДанныеОплаты.СуммаЧека - СуммаЧекФСС);
		ТабличныйДокумент.Вывести(ОбластьПрочиеТовары);
		
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		УстановитьПараметр(ОбластьПодвал, "Сумма", ДанныеОплаты.СуммаЧека);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
	КонецЕсли;
	
	УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеОплаты.Ссылка);
	
КонецПроцедуры // ЗаполнитьТабличныйДокументТоварныйЧек()

Процедура УстановитьПараметр(ОбластьМакета, ИмяПараметра, ЗначениеПараметра)
	ОбластьМакета.Параметры.Заполнить(Новый Структура(ИмяПараметра, ЗначениеПараметра));
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
