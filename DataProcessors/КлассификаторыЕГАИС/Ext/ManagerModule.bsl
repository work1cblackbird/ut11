// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВидыАлкогольнойПродукции() Экспорт
	
	Макет = ПолучитьМакет("ВидыАлкогольнойПродукции");
	
	МетаКлассификаторАлкогольнойПродукцииЕГАИС = Метаданные.Справочники.ВидыАлкогольнойПродукции;
	ДлинаКода         = МетаКлассификаторАлкогольнойПродукцииЕГАИС.ДлинаКода;
	ДлинаНаименования = МетаКлассификаторАлкогольнойПродукцииЕГАИС.ДлинаНаименования;
	
	ТаблицаВидовПродукции = Новый ТаблицаЗначений;
	ТаблицаВидовПродукции.Колонки.Добавить("Код",          Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(ДлинаКода)));
	ТаблицаВидовПродукции.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(ДлинаНаименования)));
	ТаблицаВидовПродукции.Колонки.Добавить("Маркируемый",  Новый ОписаниеТипов("Булево"));
	ТаблицаВидовПродукции.Колонки.Добавить("ВидЛицензии",  Новый ОписаниеТипов("ПеречислениеСсылка.ВидыЛицензийАлкогольнойПродукции"));
	
	КоличествоСтрок = Макет.ВысотаТаблицы;
	
	Для НомерСтроки = 2 По КоличествоСтрок Цикл
		
		НоваяСтрока = ТаблицаВидовПродукции.Добавить();
		
		НоваяСтрока.Код          = СокрЛП(Макет.Область("R" + НомерСтроки + "C2").Текст);
		НоваяСтрока.Наименование = СокрЛП(Макет.Область("R" + НомерСтроки + "C1").Текст);
		
		Маркируемый = СокрЛП(Макет.Область("R" + НомерСтроки + "C5").Текст);
		Если Маркируемый = "1" Тогда
			НоваяСтрока.Маркируемый = Истина;
		КонецЕсли;
		
		ГруппаПродукции = СокрЛП(Макет.Область("R" + НомерСтроки + "C4").Текст);
		Если ГруппаПродукции = "АП" Тогда
			Если НоваяСтрока.Маркируемый Тогда
				НоваяСтрока.ВидЛицензии = Перечисления.ВидыЛицензийАлкогольнойПродукции.АлкогольнаяПродукция;
			Иначе
				НоваяСтрока.ВидЛицензии = Перечисления.ВидыЛицензийАлкогольнойПродукции.Пиво;
			КонецЕсли;
		ИначеЕсли ГруппаПродукции = "ССНП" Тогда
			НоваяСтрока.ВидЛицензии = Перечисления.ВидыЛицензийАлкогольнойПродукции.СпиртосодержащаяНеПищеваяПродукция;
		ИначеЕсли ГруппаПродукции = "ССПП" Тогда
			НоваяСтрока.ВидЛицензии = Перечисления.ВидыЛицензийАлкогольнойПродукции.СпиртосодержащаяПищеваяПродукция;
		ИначеЕсли ГруппаПродукции = "ЭС" Тогда
			НоваяСтрока.ВидЛицензии = Перечисления.ВидыЛицензийАлкогольнойПродукции.Спирт;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаВидовПродукции;
	
КонецФункции

#КонецОбласти

#КонецЕсли
