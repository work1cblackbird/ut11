
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Запрос по документу основания.
// 
// Возвращаемое значение:
//  Строка - Запрос по документу основания
Функция ЗапросПоДокументуОснования() Экспорт
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ЭлектроннаяСопроводительнаяВедомостьДокументыОснования.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТОснования
		|ИЗ
		|	Документ.ЭлектроннаяСопроводительнаяВедомость.ДокументыОснования КАК
		|		ЭлектроннаяСопроводительнаяВедомостьДокументыОснования
		|ГДЕ
		|	ЭлектроннаяСопроводительнаяВедомостьДокументыОснования.ДокументОснование В (&ДокументОснование)
		|СГРУППИРОВАТЬ ПО
		|	ЭлектроннаяСопроводительнаяВедомостьДокументыОснования.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЭлектроннаяТранспортнаяНакладнаяДокументыОснования.Ссылка
		|ИЗ
		|	Документ.ЭлектроннаяТранспортнаяНакладная.ДокументыОснования КАК ЭлектроннаяТранспортнаяНакладнаяДокументыОснования
		|ГДЕ
		|	ЭлектроннаяТранспортнаяНакладнаяДокументыОснования.ДокументОснование В (&ДокументОснование)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЭлектронныйЗаказНарядДокументыОснования.Ссылка
		|ИЗ
		|	Документ.ЭлектронныйЗаказНаряд.ДокументыОснования КАК ЭлектронныйЗаказНарядДокументыОснования
		|ГДЕ
		|	ЭлектронныйЗаказНарядДокументыОснования.ДокументОснование В (&ДокументОснование)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТИПЗНАЧЕНИЯ(РеестрЭПДПереопределяемый.Ссылка) КАК ТипСсылки,
		|	РеестрЭПДПереопределяемый.НомерДокументаИБ КАК НомерДокументаИБ,
		|	РеестрЭПДПереопределяемый.НомерЭПД КАК НомерЭПД,
		|	РеестрЭПДПереопределяемый.ДатаЭПД КАК ДатаЭПД,
		|	РеестрЭПДПереопределяемый.ДатаДокументаИБ КАК ДатаСоздания,
		|	РеестрЭПДПереопределяемый.Организация КАК Организация,
		|	РеестрЭПДПереопределяемый.Грузоотправитель КАК Грузоотправитель,
		|	РеестрЭПДПереопределяемый.Грузополучатель КАК Грузополучатель,
		|	РеестрЭПДПереопределяемый.Перевозчик КАК Перевозчик,
		|	РеестрЭПДПереопределяемый.ТекущийШаг КАК ТекущийШаг,
		|	РеестрЭПДПереопределяемый.ТекущийШагВыполнен КАК ТекущийШагВыполнен,
		|	РеестрЭПДПереопределяемый.Комментарий КАК Комментарий,
		|	РеестрЭПДПереопределяемый.Ссылка КАК Ссылка,
		|	СостоянияЭДПереопределяемый.СостояниеЭДО,
		|	СостоянияЭДПереопределяемый.ПредставлениеСостояния,
		|	РеестрЭПДПереопределяемый.Проведен,
		|	РеестрЭПДПереопределяемый.ПометкаУдаления,
		|	РеестрЭПДПереопределяемый.ТекущийШагМП,
		|	РеестрЭПДПереопределяемый.ИдентификаторЗаписиМП КАК ИдентификаторЗаписиМП,
		|	ВЫБОР
		|		КОГДА РеестрЭПДПереопределяемый.Организация = РеестрЭПДПереопределяемый.Перевозчик
		|			ТОГДА ИСТИНА
		|		КОГДА ТИПЗНАЧЕНИЯ(РеестрЭПДПереопределяемый.Ссылка) В (&МассивТиповДокументовСОграничениемШаговМП)
		|		И РеестрЭПДПереопределяемый.ТекущийШаг В (&МассивШаговДляПодключенияМП)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК МобильноеПриложениеДоступно,
		|	РеестрЭПДПереопределяемый.НаименованиеМП КАК МобильноеПриложение,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(РеестрЭПДПереопределяемый.ТекущийШагМП, """") В (&МассивШаговМПТребующихРеакцию)
		|			ТОГДА 6
		|		КОГДА ЕСТЬNULL(РеестрЭПДПереопределяемый.ТекущийШагМП, """") В (&МассивШаговМПТребующихРеакциюОтклонение)
		|			ТОГДА 7
		|		КОГДА РеестрЭПДПереопределяемый.ОжидаемПросмотрМП = ИСТИНА
		|			ТОГДА 1
		|		КОГДА РеестрЭПДПереопределяемый.МобильноеПриложение = """"
		|			ТОГДА 0
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КартинкаМобильноеПриложение,
		|	РеестрЭПДПереопределяемый.НаименованиеМП КАК МобильноеПриложениеБезГиперссылки,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(РеестрЭПДПереопределяемый.ТекущийШагМП, """") В (&МассивШаговМПТребующихРеакцию)
		|			ТОГДА 6
		|		КОГДА ЕСТЬNULL(РеестрЭПДПереопределяемый.ТекущийШагМП, """") В (&МассивШаговМПТребующихРеакциюОтклонение)
		|			ТОГДА 7
		|		КОГДА РеестрЭПДПереопределяемый.ОжидаемПросмотрМП = ИСТИНА
		|			ТОГДА 1
		|		КОГДА РеестрЭПДПереопределяемый.МобильноеПриложение = """"
		|			ТОГДА 0
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КартинкаМобильноеПриложениеБезГиперссылки,
		|	ВЫБОР
		|		КОГДА РеестрЭПДПереопределяемый.ПометкаУдаления
		|			ТОГДА 5
		|		ИНАЧЕ 2
		|	КОНЕЦ КАК НестандартнаяКартинка,
		|	ВЫРАЗИТЬ("""" КАК СТРОКА(500)) КАК ДокументОснование
		|ИЗ
		|	ВТОснования КАК ВТОснования
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РеестрЭПД КАК РеестрЭПДПереопределяемый
		|		ПО ВТОснования.Ссылка = РеестрЭПДПереопределяемый.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодключенныеМПЭПД КАК ПодключенныеМПЭПД
		|		ПО (ПодключенныеМПЭПД.ИдентификаторМП = РеестрЭПДПереопределяемый.МобильноеПриложение)
		|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияПоОбъектамУчетаЭДО КАК СостоянияЭДПереопределяемый
		|		ПО (СостоянияЭДПереопределяемый.СсылкаНаОбъект = РеестрЭПДПереопределяемый.Ссылка)}
		|{ГДЕ
		|	РеестрЭПДПереопределяемый.ДатаЭПД КАК Дата,
		|	(РеестрЭПДПереопределяемый.ДатаЭПД >= &НачалоПериода
		|	И (&КонецПериода = ДАТАВРЕМЯ(1, 1, 1)
		|	ИЛИ РеестрЭПДПереопределяемый.ДатаЭПД <= &КонецПериода)) КАК Поле2}";

	Возврат ТекстЗапроса;

КонецФункции

// Заполнить обязательные параметры запроса.
// 
// Параметры:
//  Параметры - Структура, ПараметрыРедактированияКомпоновкиДанных - Параметры запроса или компоновки данных
Процедура ЗаполнитьОбязательныеПараметрыЗапроса(Параметры) Экспорт
	
	МассивШаговМПТребующихРеакцию = Новый Массив;
	// ЭТрН
	МассивШаговМПТребующихРеакцию.Добавить("acceptance");
	МассивШаговМПТребующихРеакцию.Добавить("unloading");	
	// ЭПЛ
	МассивШаговМПТребующихРеакцию.Добавить("ShippingList_medic_before");
	МассивШаговМПТребующихРеакцию.Добавить("ShippingList_inspection");
	МассивШаговМПТребующихРеакцию.Добавить("ShippingList_odometr_before");
	МассивШаговМПТребующихРеакцию.Добавить("ShippingList_odometr_after");
	МассивШаговМПТребующихРеакцию.Добавить("ShippingList_medic_after");
	
	МассивШаговМПТребующихРеакциюОтклонение = Новый Массив;
	// ЭТрН
	МассивШаговМПТребующихРеакциюОтклонение.Добавить("acceptance_reject");
	МассивШаговМПТребующихРеакциюОтклонение.Добавить("unloading_reject");
	// ЭПЛ
	МассивШаговМПТребующихРеакциюОтклонение.Добавить("ShippingList_medic_before_reject");
	
	// Пока для ЭПЛ поддерживается только водитель, выбрать может перевозчик на титуле оформителя (Роль = 1)
	СоответствиеТитуловИШагов = ОбменСГИСЭПД.СоответствиеТитуловИШагов();
	МассивШаговДляПодключенияМП = Новый Массив;
	МассивШаговДляПодключенияМП.Добавить(СоответствиеТитуловИШагов.Получить(Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул1));
	МассивШаговДляПодключенияМП.Добавить(СоответствиеТитуловИШагов.Получить(Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул3));
	МассивШаговДляПодключенияМП.Добавить(СоответствиеТитуловИШагов.Получить(Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул4));
	МассивШаговДляПодключенияМП.Добавить(СоответствиеТитуловИШагов.Получить(Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул5));
	
	МассивТиповДокументовСОграничениемШаговМП = Новый Массив;
	МассивТиповДокументовСОграничениемШаговМП.Добавить(Тип("ДокументСсылка.ЭлектронныйПутевойЛист"));
			
	Если ТипЗнч(Параметры) = Тип("ПараметрыРедактированияКомпоновкиДанных")
		Или ТипЗнч(Параметры) = Тип("ЗначенияПараметровДанныхКомпоновкиДанных") Тогда
		Параметры.УстановитьЗначениеПараметра("МассивШаговМПТребующихРеакцию", МассивШаговМПТребующихРеакцию);
		Параметры.УстановитьЗначениеПараметра("МассивШаговМПТребующихРеакциюОтклонение", МассивШаговМПТребующихРеакциюОтклонение);
		Параметры.УстановитьЗначениеПараметра("МассивШаговДляПодключенияМП", МассивШаговДляПодключенияМП);
		Параметры.УстановитьЗначениеПараметра("МассивТиповДокументовСОграничениемШаговМП", МассивТиповДокументовСОграничениемШаговМП);
	ИначеЕсли ТипЗнч(Параметры) = Тип("Структура") Тогда
		Параметры.Вставить("МассивШаговМПТребующихРеакцию", МассивШаговМПТребующихРеакцию);
		Параметры.Вставить("МассивШаговМПТребующихРеакциюОтклонение", МассивШаговМПТребующихРеакциюОтклонение);
		Параметры.Вставить("МассивШаговДляПодключенияМП", МассивШаговДляПодключенияМП);
		Параметры.Вставить("МассивТиповДокументовСОграничениемШаговМП", МассивТиповДокументовСОграничениемШаговМП);	
	КонецЕсли;
		
КонецПроцедуры


#КонецОбласти

#КонецЕсли
