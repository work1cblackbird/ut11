
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Предустановленные поля
	СтрокаПоиска = ПоляПоиска.Добавить();
	СтрокаПоиска.Значение = "Водитель.СерияВУ,Водитель.НомерВУ";
	СтрокаПоиска.Представление = "Водительское удостоверение";
	
	СтрокаПоиска = ПоляПоиска.Добавить();
	СтрокаПоиска.Значение = "Водитель.Фамилия,Водитель.Имя,Водитель.Отчество";
	СтрокаПоиска.Представление = "ФИО водителя";
	
	СтрокаПоиска = ПоляПоиска.Добавить();
	СтрокаПоиска.Значение = "ТранспортноеСредство.РегистрационныйНомер";
	СтрокаПоиска.Представление = "Гос. номер автомобиля";
	
	СтрокаПоиска = ПоляПоиска.Добавить();
	СтрокаПоиска.Значение = "Груз.ОтгрузочноеНаименованиеГруза";
	СтрокаПоиска.Представление = "Наименование груза";
	
	// Дополнительные пользовательские реквизиты
	Если ОбменСГИСЭПДКлиентСервер.ИспользуютсяДополнительныеРеквизитыЭПД() > 0 Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	НастройкиДополнительныхРеквизитовЭПД.ГруппаДанных,
		|	НастройкиДополнительныхРеквизитовЭПД.ИдентификаторИнформации,
		|	НастройкиДополнительныхРеквизитовЭПД.ТипДанных,
		|	НастройкиДополнительныхРеквизитовЭПД.Заголовок
		|ИЗ
		|	РегистрСведений.НастройкиДополнительныхРеквизитовЭПД КАК НастройкиДополнительныхРеквизитовЭПД";
		Выборка = Запрос.Выполнить().Выбрать();	
		Пока Выборка.Следующий() Цикл
			СтрокаПоиска = ПоляПоиска.Добавить();
			СтрокаПоиска.Значение = Выборка.ГруппаДанных + "ДополнительнаяИнформация.Значение|" 
				+ Выборка.ТипДанных + "|" + Выборка.ИдентификаторИнформации;
			СтрокаПоиска.Представление = Выборка.Заголовок;	
		КонецЦикла;
	КонецЕсли;	
	
	// Переданные при открытии формы поля
	Если Параметры.Свойство("ПоляПоиска") Тогда
		ПоискПоВыборочнымПолям = ?(Параметры.ПоляПоиска.Количество() > 0, 1, 0);
		Для Каждого Поле Из Параметры.ПоляПоиска Цикл
			ПолеЕстьВСписке = Ложь;
			Для Каждого ЭлементСписка Из ПоляПоиска Цикл
				Если ЗначениеЗаполнено(Поле.Значение) 
				И СтрНайти(ЭлементСписка.Значение, Поле.Значение) Тогда
					ЭлементСписка.Пометка = Поле.Пометка;
					ПолеЕстьВСписке = Истина;
				КонецЕсли;
			КонецЦикла;
			Если ПолеЕстьВСписке = Ложь Тогда
				СтрокаПоиска = ПоляПоиска.Добавить();
				СтрокаПоиска.Значение = Поле.Значение;
				СтрокаПоиска.Представление = Поле.Представление;
				СтрокаПоиска.Пометка = Поле.Пометка;	
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	СтрокаПоиска = ПоляПоиска.Добавить();
	СтрокаПоиска.Кнопка = "Еще...";
	
	Элементы.ПоляПоиска.Доступность = ПоискПоВыборочнымПолям = 1;

КонецПроцедуры

&НаКлиенте
Процедура ПоляПоискаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ПоляПоискаКнопка" Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ПоляПоиска", СписокПолейПоТаблице());
		
		ПоляПоискаПередНачаломДобавления_Завершение = Новый ОписаниеОповещения("ПоляПоискаПередНачаломДобавления_Завершение", ЭтотОбъект);
		
		ОткрытьФорму("Обработка.ЭлектронныеПеревозочныеДокументы.Форма.ФормаВыбораПолейПоиска", 
			ПараметрыФормы, 
			ЭтотОбъект, 
			УникальныйИдентификатор, , , 
			ПоляПоискаПередНачаломДобавления_Завершение, 
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПоляПоискаПередНачаломДобавления_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		СтрокаПоиска = ПоляПоиска.Вставить(ПоляПоиска.Количество() - 1);
		СтрокаПоиска.Значение = Результат.Значение + "|" + Результат.ТипДанных;
		СтрокаПоиска.Представление = Результат.Представление;
		СтрокаПоиска.Пометка = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоВыборочнымПолямПриИзменении(Элемент)
	
	Элементы.ПоляПоиска.Доступность = ПоискПоВыборочнымПолям = 1;
	
КонецПроцедуры

&НаКлиенте
Функция СписокПолейПоТаблице()
	
	Результат = Новый СписокЗначений;
	Для Каждого СтрокаПоиска Из ПоляПоиска Цикл
		Если ЗначениеЗаполнено(СтрокаПоиска.Значение) Тогда
			Если ПоискПоВыборочнымПолям = 0 Тогда 
				СтрокаПоиска.Пометка = Ложь;	
			КонецЕсли;
			Результат.Добавить(СтрокаПоиска.Значение, СтрокаПоиска.Представление, СтрокаПоиска.Пометка);
		КонецЕсли;	
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть(СписокПолейПоТаблице());

КонецПроцедуры
