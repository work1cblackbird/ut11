#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует таблицу документов к оформлению по остаткам прослеживаемых товаров на комиссии, в номерах ГТД по которым не установлен признак РНПТ.
// Такие товары необходимо "виртуально" вернуть комитенту, а затем снова принять на комиссию с указанием РНПТ.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица документов к оформлению по остаткам прослеживаемых товаров на комиссии
//
Функция ПолучитьДокументыКОформлениюПоПрослеживаемымТоварамНаКомиссии() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДокументовКОформлениюПоТоварамНаКомиссии();
	Запрос.УстановитьПараметр("ДатаНачалаПрослеживаемости", УчетПрослеживаемыхТоваровЛокализация.ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров());
	ТаблицаДокументов = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаДокументов;
	
КонецФункции

// Формирует текст запроса таблицы возвратов товаров от комиссионера.
// 
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция СформироватьТекстЗапросаТаблицаВозвратыТоваровОтКомиссионеров() Экспорт
	
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТоварыПереданныеНаКомиссиюОстатки.АналитикаУчетаНоменклатуры.МестоХранения КАК Комиссионер,
		|	ТоварыПереданныеНаКомиссиюОстатки.Соглашение КАК Соглашение,
		|	ТоварыПереданныеНаКомиссиюОстатки.Организация КАК Организация
		|ПОМЕСТИТЬ ВТОстатки
		|ИЗ
		|	РегистрНакопления.ТоварыПереданныеНаКомиссию.Остатки КАК ТоварыПереданныеНаКомиссиюОстатки
		|ГДЕ
		|	ТоварыПереданныеНаКомиссиюОстатки.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
		|	И НЕ ЕСТЬNULL(ТоварыПереданныеНаКомиссиюОстатки.НомерГТД.ТипНомераГТД, ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.ПустаяСсылка)) В
		|			(ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТ),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТКомплекта))
		|	И ЕСТЬNULL(ТоварыПереданныеНаКомиссиюОстатки.АналитикаУчетаНоменклатуры.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
		|	И ЕСТЬNULL(ТоварыПереданныеНаКомиссиюОстатки.Соглашение.КомиссионерВедетУчетПоРНПТ, ЛОЖЬ)
		|	И ТоварыПереданныеНаКомиссиюОстатки.КоличествоОстаток > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОтчетКомиссионера.Партнер КАК Партнер,
		|	ОтчетКомиссионера.Соглашение КАК Соглашение,
		|	ОтчетКомиссионера.Организация КАК Организация,
		|	МАКСИМУМ(ИСТИНА) КАК ОформленОтчет
		|ПОМЕСТИТЬ ОформленныеОтчеты
		|ИЗ
		|	Документ.ОтчетКомиссионера КАК ОтчетКомиссионера
		|ГДЕ
		|	ОтчетКомиссионера.Проведен = ИСТИНА
		|	И ОтчетКомиссионера.Дата >= &ДатаПрослеживаемости
		|СГРУППИРОВАТЬ ПО
		|	ОтчетКомиссионера.Партнер,
		|	ОтчетКомиссионера.Соглашение,
		|	ОтчетКомиссионера.Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТОстатки.Комиссионер КАК Комиссионер,
		|	ВТОстатки.Соглашение КАК Соглашение,
		|	ВТОстатки.Организация КАК Организация,
		|	ЕСТЬNULL(ОформленныеОтчеты.ОформленОтчет, ЛОЖЬ) КАК ОформленОтчет
		|ИЗ
		|	ВТОстатки КАК ВТОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОформленныеОтчеты КАК ОформленныеОтчеты
		|		ПО ВТОстатки.Комиссионер = ОформленныеОтчеты.Партнер
		|		И ВТОстатки.Соглашение = ОформленныеОтчеты.Соглашение
		|		И ВТОстатки.Организация = ОформленныеОтчеты.Организация
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТОстатки.Соглашение,
		|	ВТОстатки.Организация,
		|	ВТОстатки.Комиссионер,
		|	ЕСТЬNULL(ОформленныеОтчеты.ОформленОтчет, ЛОЖЬ)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Формирует текст запроса таблицы к оформлению передач.
// 
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция СформироватьТекстЗапросаТаблицаКОформлениюПередач() Экспорт
	
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВозвратТоваровОтКлиентаТовары.Ссылка КАК ДокументВозврат,
		|	ВозвратТоваровОтКлиентаТовары.Ссылка.Организация КАК Организация,
		|	ВозвратТоваровОтКлиентаТовары.Ссылка.Партнер КАК Партнер,
		|	ВозвратТоваровОтКлиентаТовары.Ссылка.Соглашение КАК Соглашение,
		|	ВозвратТоваровОтКлиентаТовары.Ссылка.Дата КАК ДатаВозврата,
		|	ВозвратТоваровОтКлиентаТовары.Ссылка.Склад КАК Склад,
		|	ВозвратТоваровОтКлиентаТовары.Ссылка.Номер КАК Номер
		|ПОМЕСТИТЬ ВТВозвраты
		|ИЗ
		|	Документ.ВозвратТоваровОтКлиента.Товары КАК ВозвратТоваровОтКлиентаТовары
		|ГДЕ
		|	ВозвратТоваровОтКлиентаТовары.Ссылка.Проведен
		|	И ЕСТЬNULL(ВозвратТоваровОтКлиентаТовары.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
		|	И ВозвратТоваровОтКлиентаТовары.Ссылка.Склад = &Склад
		|
		|СГРУППИРОВАТЬ ПО
		|	ВозвратТоваровОтКлиентаТовары.Ссылка,
		|	ВозвратТоваровОтКлиентаТовары.Ссылка.Организация,
		|	ВозвратТоваровОтКлиентаТовары.Ссылка.Партнер,
		|	ВозвратТоваровОтКлиентаТовары.Ссылка.Соглашение,
		|	ВозвратТоваровОтКлиентаТовары.Ссылка.Дата,
		|	ВозвратТоваровОтКлиентаТовары.Ссылка.Склад,
		|	ВозвратТоваровОтКлиентаТовары.Ссылка.Номер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РеализацияТоваровУслуг.Ссылка КАК ДокументПередача,
		|	РеализацияТоваровУслуг.Дата КАК ДатаПередачи,
		|	1 КАК КолВоПередач,
		|	РеализацияТоваровУслуг.Склад КАК Склад,
		|	РеализацияТоваровУслуг.Организация КАК Организация,
		|	РеализацияТоваровУслуг.Партнер КАК Партнер,
		|	РеализацияТоваровУслуг.ОснованиеНомер КАК ОснованиеНомер,
		|	РеализацияТоваровУслуг.ОснованиеДата КАК ОснованиеДата
		|ПОМЕСТИТЬ ВТПередачи
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|ГДЕ
		|	РеализацияТоваровУслуг.ХозяйственнаяОперация = &ХОПередачаНаКомиссию
		|	И РеализацияТоваровУслуг.Проведен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТВозвраты.ДокументВозврат КАК Возврат,
		|	ВТВозвраты.Организация КАК Организация,
		|	ВТВозвраты.Партнер КАК Комиссионер,
		|	ВТВозвраты.Соглашение КАК Соглашение,
		|	СУММА(ЕСТЬNULL(ВТПередачи.КолВоПередач, 0)) КАК Передача
		|ИЗ
		|	ВТВозвраты КАК ВТВозвраты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПередачи КАК ВТПередачи
		|		ПО (НАЧАЛОПЕРИОДА(ВТВозвраты.ДатаВозврата, ДЕНЬ) = НАЧАЛОПЕРИОДА(ВТПередачи.ОснованиеДата, ДЕНЬ))
		|			И ВТВозвраты.Организация = ВТПередачи.Организация
		|			И ВТВозвраты.Склад = ВТПередачи.Склад
		|			И ВТВозвраты.Партнер = ВТПередачи.Партнер
		|			И ВТВозвраты.Номер = ВТПередачи.ОснованиеНомер
		|
		|	// УсловиеКОформлению
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТВозвраты.ДокументВозврат,
		|	ВТВозвраты.Партнер,
		|	ВТВозвраты.Организация,
		|	ВТВозвраты.Соглашение";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаДокументовКОформлениюПоТоварамНаКомиссии()
	
	Возврат
	
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВидыЗапасов.Ссылка КАК ВидЗапасов
	|ПОМЕСТИТЬ ВидыЗапасовКомиссия
	|ИЗ
	|	Справочник.ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|	И НЕ ВидыЗапасов.РеализацияЗапасовДругойОрганизации
	|	И ВидыЗапасов.ВладелецТовара ССЫЛКА Справочник.Партнеры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТоварыОрганизаций.Организация КАК Организация,
	|	ТоварыОрганизаций.ВидЗапасов.ВладелецТовара КАК Комитент,
	|	ТоварыОрганизаций.ВидЗапасов.Соглашение КАК Соглашение,
	|	ВЫРАЗИТЬ(Аналитика.МестоХранения КАК Справочник.Склады) КАК Склад,
	|	ЗНАЧЕНИЕ(Документ.ВозвратТоваровПоставщику.ПустаяСсылка) КАК ДокументВозврата,
	|	ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка) КАК ДокументПриемаНаКомиссию,
	|	ИСТИНА КАК ЕстьОстаток,
	|	ЛОЖЬ КАК ЕстьПроведенныйВозврат,
	|	ЛОЖЬ КАК ЕстьПроведенныйПрием
	|ПОМЕСТИТЬ ОрганизацииИКомитентыСОстатками
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Остатки(&ДатаНачалаПрослеживаемости, ВидЗапасов В
	|		(ВЫБРАТЬ
	|			ВидЗапасов
	|		ИЗ
	|			ВидыЗапасовКомиссия КАК ВидыЗапасовКомиссия)) КАК ТоварыОрганизаций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТоварыОрганизаций.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|ГДЕ
	|	ЕСТЬNULL(Аналитика.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
	|	И НЕ ЕСТЬNULL(ТоварыОрганизаций.НомерГТД.ТипНомераГТД, ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.ПустаяСсылка)) В
	|			(ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТ),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТКомплекта))
	|	И ТоварыОрганизаций.КоличествоОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВозвратТоваровПоставщику.Организация КАК Организация,
	|	ВозвратТоваровПоставщику.Партнер КАК Комитент,
	|	ВозвратТоваровПоставщику.Соглашение КАК Соглашение,
	|	ВозвратТоваровПоставщику.Склад КАК Склад,
	|	МАКСИМУМ(ВозвратТоваровПоставщику.Ссылка) КАК ДокументВозврата,
	|	ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка) КАК ДокументПриемаНаКомиссию,
	|	ЛОЖЬ КАК ЕстьОстаток,
	|	МАКСИМУМ(ВозвратТоваровПоставщику.Ссылка.Проведен) КАК ЕстьПроведенныйВозврат,
	|	ЛОЖЬ КАК ЕстьПроведенныйПрием
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику КАК ВозвратТоваровПоставщику
	|ГДЕ
	|	НЕ ВозвратТоваровПоставщику.ПометкаУдаления
	|	И ВозвратТоваровПоставщику.КорректировкаОстатковРНПТ
	|СГРУППИРОВАТЬ ПО
	|	ВозвратТоваровПоставщику.Организация,
	|	ВозвратТоваровПоставщику.Партнер,
	|	ВозвратТоваровПоставщику.Соглашение,
	|	ВозвратТоваровПоставщику.Склад,
	|	ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПриобретениеТоваровУслуг.Организация КАК Организация,
	|	ПриобретениеТоваровУслуг.Партнер КАК Комитент,
	|	ПриобретениеТоваровУслуг.Соглашение КАК Соглашение,
	|	ПриобретениеТоваровУслуг.Склад КАК Склад,
	|	ЗНАЧЕНИЕ(Документ.ВозвратТоваровПоставщику.ПустаяСсылка) КАК ДокументВозврата,
	|	МАКСИМУМ(ПриобретениеТоваровУслуг.Ссылка) КАК ДокументПриемаНаКомиссию,
	|	ЛОЖЬ КАК ЕстьОстаток,
	|	ЛОЖЬ КАК ЕстьПроведенныйВозврат,
	|	МАКСИМУМ(ПриобретениеТоваровУслуг.Ссылка.Проведен) КАК ЕстьПроведенныйПрием
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
	|ГДЕ
	|	НЕ ПриобретениеТоваровУслуг.ПометкаУдаления
	|	И ПриобретениеТоваровУслуг.КорректировкаОстатковРНПТ
	|СГРУППИРОВАТЬ ПО
	|	ПриобретениеТоваровУслуг.Организация,
	|	ПриобретениеТоваровУслуг.Партнер,
	|	ПриобретениеТоваровУслуг.Соглашение,
	|	ПриобретениеТоваровУслуг.Склад,
	|	ЗНАЧЕНИЕ(Документ.ВозвратТоваровПоставщику.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОрганизацииИКомитентыСОстатками.Организация КАК Организация,
	|	ОрганизацииИКомитентыСОстатками.Комитент КАК Комитент,
	|	ОрганизацииИКомитентыСОстатками.Соглашение КАК Соглашение,
	|	ОрганизацииИКомитентыСОстатками.Склад КАК Склад,
	|	МАКСИМУМ(ОрганизацииИКомитентыСОстатками.ДокументВозврата) КАК ДокументВозврата,
	|	МАКСИМУМ(ОрганизацииИКомитентыСОстатками.ДокументПриемаНаКомиссию) КАК ДокументПриемаНаКомиссию,
	|	МАКСИМУМ(ОрганизацииИКомитентыСОстатками.ЕстьОстаток) КАК ЕстьОстаток,
	|	МАКСИМУМ(ОрганизацииИКомитентыСОстатками.ЕстьПроведенныйВозврат) КАК ЕстьПроведенныйВозврат,
	|	МАКСИМУМ(ОрганизацииИКомитентыСОстатками.ЕстьПроведенныйПрием) КАК ЕстьПроведенныйПрием
	|ПОМЕСТИТЬ ПредварительнаяТаблицаДокументов
	|ИЗ
	|	ОрганизацииИКомитентыСОстатками КАК ОрганизацииИКомитентыСОстатками
	|СГРУППИРОВАТЬ ПО
	|	ОрганизацииИКомитентыСОстатками.Организация,
	|	ОрганизацииИКомитентыСОстатками.Комитент,
	|	ОрганизацииИКомитентыСОстатками.Соглашение,
	|	ОрганизацииИКомитентыСОстатками.Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредварительнаяТаблицаДокументов.Организация КАК Организация,
	|	ПредварительнаяТаблицаДокументов.Комитент КАК Комитент,
	|	ПредварительнаяТаблицаДокументов.Соглашение КАК Соглашение,
	|	ПредварительнаяТаблицаДокументов.Склад КАК Склад,
	|	ПредварительнаяТаблицаДокументов.ДокументВозврата,
	|	ВЫБОР
	|		КОГДА ПредварительнаяТаблицаДокументов.ДокументВозврата = ЗНАЧЕНИЕ(Документ.ВозвратТоваровПоставщику.ПустаяСсылка)
	|			ТОГДА 3
	|		КОГДА ПредварительнаяТаблицаДокументов.ДокументВозврата.Проведен
	|			ТОГДА 0
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК ДокументВозвратаСостояние,
	|	ПредварительнаяТаблицаДокументов.ДокументПриемаНаКомиссию,
	|	ВЫБОР
	|		КОГДА
	|			ПредварительнаяТаблицаДокументов.ДокументПриемаНаКомиссию = ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка)
	|			ТОГДА 3
	|		КОГДА ПредварительнаяТаблицаДокументов.ДокументПриемаНаКомиссию.Проведен
	|			ТОГДА 0
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК ДокументПриемаСостояние
	|ИЗ
	|	ПредварительнаяТаблицаДокументов КАК ПредварительнаяТаблицаДокументов
	|ГДЕ
	|	ВЫБОР
	|		КОГДА ПредварительнаяТаблицаДокументов.ЕстьОстаток
	|			ТОГДА ИСТИНА
	|		КОГДА ПредварительнаяТаблицаДокументов.ЕстьПроведенныйВозврат
	|		И НЕ ПредварительнаяТаблицаДокументов.ЕстьПроведенныйПрием
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	Комитент,
	|	Соглашение,
	|	Склад
	|АВТОУПОРЯДОЧИВАНИЕ";
	
КонецФункции

#КонецОбласти

#КонецЕсли