#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Склад = Параметры.Склад;
	
	ПравоСозданияСклада = ПравоДоступа("Добавление", Метаданные.Справочники.Склады);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СгенерироватьСклад",
		"Видимость",
		ПравоСозданияСклада);
	
	ЗаголовокЭлемента = СтрШаблон(Элементы.ДекорацияИнформация.Заголовок, ?(ПравоСозданияСклада, НСтр("ru='или сгенерируйте новый'"),""));
	Элементы.ДекорацияИнформация.Заголовок = ЗаголовокЭлемента;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СгенерироватьСклад(Команда)
	СгенерироватьСкладНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Ок(Команда)
	Закрыть(Склад);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СгенерироватьСкладНаСервере()
	
	НовыйСклад = Справочники.Склады.СоздатьЭлемент();
	НовыйСклад.ВыборГруппы                       = Перечисления.ВыборГруппыСкладов.Запретить;
	НовыйСклад.ТипСклада                         = Перечисления.ТипыСкладов.ОптовыйСклад;
	НовыйСклад.УровеньОбслуживания               = Перечисления.УровниОбслуживания.УровеньОбслуживания84_0;
	НовыйСклад.ИсточникИнформацииОЦенахДляПечати = Перечисления.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости;
	НовыйСклад.ИспользованиеРабочихУчастков      = Перечисления.ИспользованиеСкладскихРабочихУчастков.НеИспользовать;
	НовыйСклад.УчитыватьСебестоимостьПоСериям    = Истина;
	НовыйСклад.КонтролироватьСвободныеОстатки    = Истина;
	НовыйСклад.Наименование                      = НСтр("ru='Виртуальный склад прослеживаемых товаров'");
	НовыйСклад.Записать();
	
	Склад = НовыйСклад.Ссылка;
	
КонецПроцедуры

#КонецОбласти