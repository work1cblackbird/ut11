
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	КодФормы = "ЗагрузкаЦенПоставщикаИзВнешнихФайлов";
	
	Объект.Партнер = Параметры.Партнер;
	ТаблицаВидовЦен = УстановкаЦенСервер.ЗагрузитьСправочникВидовЦенПоставщика(ЭтаФорма);
	
	Для Каждого СтрокаТЧ Из ТаблицаВидовЦен Цикл
		
		НоваяСтрока = ВыбранныеЦены.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
		НоваяСтрока.Выбрана = НЕ Параметры.ВыбранныеВидыЦен.Найти(СтрокаТЧ.Ссылка) = Неопределено;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ВидыЦенПоставщиков"
		И Параметр.Партнер = Объект.Партнер Тогда
		
		ПеречитатьВидыЦенПоставщикаНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВыбранныеЦены

&НаКлиенте
Процедура ВыбранныеЦеныПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Владелец", Объект.Партнер);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", Отбор);
	
	ОткрытьФорму("Справочник.ВидыЦенПоставщиков.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ОбновитьПорядок = Ложь;
	Индекс = 1;
	Для Каждого СтрокаТЧ Из ВыбранныеЦены Цикл
		Если СтрокаТЧ.РеквизитДопУпорядочивания <> Индекс Тогда
			ОбновитьПорядок = Истина;
		КонецЕсли;
		Индекс = Индекс + 1;
	КонецЦикла;
	
	Если ОбновитьПорядок Тогда
		СохранитьНастройкуПорядкаВидовЦенПоставщика();
	КонецЕсли;
	
	ОповеститьОВыборе(УстановкаЦенКлиентСервер.ВыбранныеВидыЦен(ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсеВидыЦен(Команда)
	
	Для Каждого СтрокаТаблицы Из ВыбранныеЦены Цикл
		Если Не СтрокаТаблицы.Выбрана И Не СтрокаТаблицы.ЗапрещенныйВидЦены Тогда
			СтрокаТаблицы.Выбрана = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьВсеВидыЦен(Команда)
	
	Для Каждого СтрокаТаблицы Из ВыбранныеЦены Цикл
		Если СтрокаТаблицы.Выбрана И Не СтрокаТаблицы.ЗапрещенныйВидЦены Тогда
			СтрокаТаблицы.Выбрана = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВыбранныеЦены.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВыбранныеЦены.ПометкаУдаления");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);

КонецПроцедуры

#Область Прочее

&НаСервере
Процедура СохранитьНастройкуПорядкаВидовЦенПоставщика()
	
	УстановкаЦенСервер.СохранитьНастройкуПорядкаВидовЦенПоставщика(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПеречитатьВидыЦенПоставщикаНаСервере()
	
	УстановкаЦенСервер.ПеречитатьВыбранныеЦены(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти