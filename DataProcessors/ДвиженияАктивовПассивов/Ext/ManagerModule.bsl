#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограмныйИнтерфейс

// Выполняет проведение документов по регистру активов и пассивов.
// Параметры:
//	Параметры - Структура - Результаты работы метода.
//	АдресХранилища - Строка - Адрес, по которому будет помещены результаты работы.
//
Процедура СформироватьДвиженияПоРегистру(Параметры, АдресХранилища = "") Экспорт
	
	НеЗаписыватьДвижения = Параметры.НеЗаписыватьДвижения;
	
	Замеры = Новый Структура;
	НачалоФормированияДвижений = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	ТипыРегистраторов = КоличествоРегистраторов();
	ДобавитьЗамер(Замеры, "ФормированиеСпискаДокументовКПроведению", НачалоЗамера);
	ВывестиПротокол(НСтр("ru = 'Начало заполнения. Предстоит обработать:'"), ТипыРегистраторов);
	
	Процесс = ИнициализироватьСведенияОЗаполнении(ТипыРегистраторов);
	ОбработанныеТипы = Процесс.ОбработанныеТипы;
	ПоследнийОбработанный = "";
	Если ОбработанныеТипы.Количество() > 0 Тогда
		ПоследнийОбработанный = ОбработанныеТипы[ОбработанныеТипы.Количество()-1].Имя;
	КонецЕсли;
	Для Каждого ТипРегистратора Из ТипыРегистраторов Цикл
		
		ОбработанныйТип = ОбработанныеТипы.Найти(ТипРегистратора.Имя, "Имя");
		ДокументОбработан = ОбработанныйТип <> Неопределено;
		Если ДокументОбработан И ОбработанныйТип.Имя <> ПоследнийОбработанный Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьДвиженияРегистраПоДокументам(ТипРегистратора, ОбработанныеТипы, НеЗаписыватьДвижения);
		
	КонецЦикла;
	Итоги = ОбработанныеТипы.Добавить();
	Итоги.Имя = НСтр("ru = 'Итого'");
	Итоги.ВсегоДокументов = ОбработанныеТипы.Итог("ВсегоДокументов");
	Итоги.ВсегоДвижений = ОбработанныеТипы.Итог("ВсегоДвижений");
	Итоги.ВремяЗаполнения = ОбработанныеТипы.Итог("ВремяЗаполнения");
	Итоги.ВсегоЗатрачено = ПредставлениеВремени(Итоги.ВремяЗаполнения);
	
	Сведения = Константы.СведенияОбОбновленииИБ.Получить().Получить(); //Структура
	Сведения.Удалить("ЗаполнениеАктивовПассивов");
	Константы.СведенияОбОбновленииИБ.Установить(Новый ХранилищеЗначения(Сведения));
	
	ДобавитьЗамер(Замеры, "ФормированиеДвижений", НачалоФормированияДвижений);
	ВывестиПротокол(НСтр("ru = 'Конец заполнения. Всего обработано:'"), ОбработанныеТипы, Замеры);
	
	Константы.ФормироватьУправленческийБаланс.Установить(Истина);
	Константы.ЗаполненыДвиженияАктивовПассивов.Установить(Истина);
	
	Параметры.Вставить("ЗагрузкаВыполнена", Истина);
	Параметры.Вставить("Замеры", Замеры);
	Если ПустаяСтрока(АдресХранилища) Тогда
		Параметры.Вставить("ТипыРегистраторов", ОбработанныеТипы);
	Иначе
		ПоместитьВоВременноеХранилище(Параметры, АдресХранилища);
	КонецЕсли;
	
КонецПроцедуры

// формирует движения по регистру ПрочиеАктивыПассивы
// Принцип формирования движений см. презентацию проекта https://partners.v8.1c.ru/forum/topic/1484492#m_1484492
//
// Параметры:
//  СсылкаНаДокумент - ДокументСсылка, Структура из КлючИЗначение - ссылка документа для которого необходимо сформировать и записать движения упр. баланса:
//                                      * Ссылка - ДокументСсылка
//  Движения - Структура из КлючИЗначение:
//               * Ключ - Строка - Имя регистра как в метаданных
//               * Значение - РегистрНакопленияНаборЗаписей.ПрочиеАктивыПассивы
//             КоллекцияДвижений - наборы записей регистров документа пеерданные в коллекции двжений или структуре имитирующей эту коллекцию.
//
Процедура ДобавитьДвиженияДокумента(СсылкаНаДокумент, Движения) Экспорт
	
	НачалоФормированияДвижений = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	ВремяВыгрузкиНаборовДвижений = 0;
	ВремяВыгрузкиРасчетовПоДокументам = 0;
	Если ТипЗнч(Движения) = Тип("КоллекцияДвижений") Тогда
		ТаблицыДвиженийДокумента = ВыгрузитьНаборыДвижений(СсылкаНаДокумент, Движения);
		ВремяВыгрузкиНаборовДвижений = (ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЗамера) / 1000;
		ТаблицыДвиженийДокумента.Свойство("ВремяВыгрузкиРасчетовПоДокументам", ВремяВыгрузкиРасчетовПоДокументам);
		ТаблицыДвиженийДокумента.Удалить("ВремяВыгрузкиРасчетовПоДокументам");
	Иначе
		ВыгрузитьРасчетыПоСрокам(СсылкаНаДокумент, Движения);
		ТаблицыДвиженийДокумента = Движения;
	КонецЕсли;
	
	ПараметрыДвижений = Новый Структура;
	ПараметрыДвижений.Вставить("Ссылка", СсылкаНаДокумент);
	ПараметрыДвижений.Вставить("ТаблицыДляДвижений", ТаблицыДвиженийДокумента);
	
	ЗапросДвижений = ЗапросДвижений(ПараметрыДвижений);
	
	Если ЗапросДвижений <> Неопределено Тогда
		Замеры = ПараметрыДвижений.Протокол.Замеры;
		Замеры.Вставить("ВыгрузкаНаборовДвижений", ВремяВыгрузкиНаборовДвижений);
		Замеры.Вставить("ВыгрузкаРасчетовПоДокументам", ВремяВыгрузкиРасчетовПоДокументам);
		
		НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
		ДвиженияАктивовПассивов = ЗапросДвижений.Выполнить().Выгрузить();
		ДобавитьЗамер(Замеры, "ВыполнениеЗапросаДвижений", НачалоЗамера);
		
		Если ТипЗнч(Движения) = Тип("КоллекцияДвижений") Тогда
			Движения.ПрочиеАктивыПассивы.Загрузить(ДвиженияАктивовПассивов);
			Движения.ПрочиеАктивыПассивы.Записывать = Истина;
		ИначеЕсли ТипЗнч(Движения) = Тип("Структура") Тогда
			Движения.Вставить("ПрочиеАктивыПассивы", ДвиженияАктивовПассивов);
		КонецЕсли;
		
		ДобавитьЗамер(Замеры, "ВсегоФормированиеДвижений", НачалоФормированияДвижений);
	КонецЕсли;
	
КонецПроцедуры

// Актуализирует движения по упр. балансу после распределения отложенных взаиморасчетов.
//
// Параметры:
//  РегистраторыКОтражению - Массив - документы обработанные отложенным распределением взаиморасчетов
//  ПараметрыОтладки - Структура - дополнительные параметры.
//
Процедура ОбновитьДвиженияПоРасчетамСПартнерами(РегистраторыКОтражению, ПараметрыОтладки = Неопределено) Экспорт
	
	Если ТипЗнч(РегистраторыКОтражению) = Тип("Массив") И РегистраторыКОтражению.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НачалоОбновленияРасчетовСПартнерами = ТекущаяУниверсальнаяДатаВМиллисекундах();
	ПараметрыДвижений = Новый Структура;
	ПараметрыДвижений.Вставить("ВидОтложенногоРасчета", Перечисления.ВидыИсточниковДвижений.РасчетыСПартнерами);
	ПараметрыДвижений.Вставить("ДокументыКОтражению", РегистраторыКОтражению);
	ПараметрыДвижений.Вставить("ИмяВременнойТаблицыРезультата", "втНовыеДвиженияАктивовПассивов");
	
	ЗапросДвижений = ЗапросДвижений(ПараметрыДвижений, ПараметрыОтладки);
	Если ЗапросДвижений = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Замеры = ПараметрыДвижений.Протокол.Замеры;
	
	ЗапросДвижений.Текст = ЗапросДвижений.Текст +
	"// Движения активов И пассивов НЕ затронутые обновлением расчетов с партнерами
	|ВЫБРАТЬ
	|	Т.Период,
	|	Т.Регистратор,
	|	Т.ВидДвижения,
	|	Т.Организация,
	|	Т.Подразделение,
	|	Т.НаправлениеДеятельности,
	|	Т.Статья,
	|	Т.Аналитика,
	|	Т.Сумма,
	|	Т.РасчетСебестоимости,
	|	Т.ВидИсточника,
	|	Т.Источник
	|ИЗ
	|	РегистрНакопления.ПрочиеАктивыПассивы КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ втНовыеДвиженияАктивовПассивов КАК НовыеДвижения
	|	ПО Т.Период = НовыеДвижения.Период
	|	И Т.Регистратор = НовыеДвижения.Регистратор
	|	И Т.Организация = НовыеДвижения.Организация
	|	И Т.Источник = НовыеДвижения.Источник
	|ГДЕ
	|	Т.Регистратор В (&МассивРегистраторов)
	|	И НЕ Т.ВидИсточника В (
	|		&ВидОтложенногоРасчета,
	|		ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.РасчетБаланса))
	|	И НовыеДвижения.Регистратор ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Обновленные движения активов и пассивов
	|ВЫБРАТЬ
	|	Т.Период,
	|	Т.Регистратор,
	|	Т.ВидДвижения,
	|	Т.Организация,
	|	Т.Подразделение,
	|	Т.НаправлениеДеятельности,
	|	Т.Статья,
	|	Т.Аналитика,
	|	Т.Сумма,
	|	Т.РасчетСебестоимости,
	|	Т.ВидИсточника,
	|	Т.Источник
	|ИЗ
	|	втНовыеДвиженияАктивовПассивов КАК Т
	|
	|ИТОГИ ПО
	|	Т.Регистратор";
	
	НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	РезультатЗапроса = ЗапросДвижений.Выполнить();
	ВыборкаПоДокументам = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ДобавитьЗамер(Замеры, "ВыполнениеЗапросаДвижений", НачалоЗамера);
	
	НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Пока ВыборкаПоДокументам.Следующий() Цикл
		НаборЗаписей = РегистрыНакопления.ПрочиеАктивыПассивы.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаПоДокументам.Регистратор);
		Выборка = ВыборкаПоДокументам.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		КонецЦикла;
		Если НаборЗаписей.Количество() > 0 Тогда
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		КонецЕсли;
	КонецЦикла;
	ДобавитьЗамер(Замеры, "ЗаписьДвижений", НачалоЗамера);
		
	ДобавитьЗамер(Замеры, "ВсегоОбновлениеРасчетовСПартнерами", НачалоОбновленияРасчетовСПартнерами);
	
	Если ТипЗнч(ПараметрыОтладки) = Тип("Структура") Тогда
		ПараметрыОтладки.Вставить("Протокол", ПараметрыДвижений.Протокол);
	КонецЕсли;
	
КонецПроцедуры

// Формирует запрос к базе данных для получения движений управленческого баланса
//
// Параметры:
//  ПараметрыДвижений - Структура - входные даные для формирования движений по упр. балансу (см. ИнициализироватьПараметрыДвижений)
//  ПараметрыОтладки - Структура - дополнительные параметры.
//
// Возвращаемое значение:
//  Запрос - настроенный объект запроса для получения движений по упр. балансу.
//
Функция ЗапросДвижений(ПараметрыДвижений, ПараметрыОтладки = Неопределено) Экспорт
	
	ИнициализироватьПараметрыДвижений(ПараметрыДвижений, ПараметрыОтладки);
	
	НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	ЗапросДвижений = ЗапросДвиженийНаправлений(ПараметрыДвижений);
	Если ЗапросДвижений.Параметры.ВсеТаблицы.Количество() > 0 Тогда
		ДобавитьТекстЗапросаВременныхТаблицКонтроляБаланса(ЗапросДвижений);
		ДобавитьТекстЗапросаДвиженийАктивовПассивов(ЗапросДвижений);
		
		ИменаВременныхТаблиц = СтрСоединить(ЗапросДвижений.Параметры.ВсеТаблицы, ",");
		ИменаВременныхТаблиц = СтрЗаменить(ИменаВременныхТаблиц,",РезультатВыборки","");
		ЗапросДвижений.УстановитьПараметр("ИменаВременныхТаблиц", ИменаВременныхТаблиц);
	КонецЕсли;
	
	ЗапросДвижений.Текст = СтрСоединить(ЗапросДвижений.Параметры.ТекстыЗапросов, РазделительЗапросов());
	ЗапросДвижений.Параметры.Удалить("ТекстыЗапросов");
	
	ДобавитьЗамер(ПараметрыДвижений.Протокол.Замеры, "СборкаЗапроса", НачалоЗамера);
	
	Если ПустаяСтрока(ЗапросДвижений.Текст) Тогда
		Возврат Неопределено;
	КонецЕсли;
	Возврат ЗапросДвижений;
	
КонецФункции

// Отражает переданные документы в управленческом балансе
//
// Параметры:
//  ДокументыКОтражению - ДокументСсылка, Массив из ДокументСсылка, МенеджерВременныхТаблиц - документы к отражению в упр. балансе
//                        Если передан менеджер временных таблиц, 
//                        тогда по умолчанию временная таблица содержащая регистраторы должна иметь имя
//                        "ДокументыКОтражению" и содержать поле "Регистратор" иначе в дополнительных параметрах,
//                        в ключе "ТаблицаРегистраторов" может быть передана структура с описанием временной таблицы.
//                        В структуре должны быть следующие ключи: ИмяТаблицы, ПолеРегистратора.
//  ТаблицыДляДвижений - Структура - в ключах структуры передаются имена балансовых регистров. 
//                       Если не передана, тогда для переданных документов будет сформирован запрос выборки ко всем
//                       балансовым регистрам в системе. Список балансовых регистров см. Перечисление.ИсточникиУправленческогоБаланса.
//
Процедура ОтразитьДокументыВУправленческомБалансе(ДокументыКотражению, ТаблицыДляДвижений = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыДвижений = Новый Структура;
	ПараметрыДвижений.Вставить("ВидОтложенногоРасчета", Перечисления.ВидыИсточниковДвижений.РасчетБаланса);
	ПараметрыДвижений.Вставить("ВыборкаПоРегистратору", Истина);
	ПараметрыДвижений.Вставить("ДокументыКОтражению",   ДокументыКотражению);
	Если ДополнительныеПараметры <> Неопределено Тогда
		Для Каждого Параметр Из ДополнительныеПараметры Цикл
			ПараметрыДвижений.Вставить(Параметр.Ключ, Параметр.Значение);
		КонецЦикла;
	КонецЕсли;
	Если ТаблицыДляДвижений <> Неопределено Тогда
		ПараметрыДвижений.Вставить("ТаблицыДляДвижений", ТаблицыДляДвижений);
	КонецЕсли;
	
	ЗапросДвижений = ЗапросДвижений(ПараметрыДвижений);
	Если ЗапросДвижений <> Неопределено Тогда
		ВыборкаПоДокументам = ЗапросДвижений.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоДокументам.Следующий() Цикл
			НаборЗаписей = РегистрыНакопления.ПрочиеАктивыПассивы.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаПоДокументам.Регистратор);
			Выборка = ВыборкаПоДокументам.Выбрать();
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
			КонецЦикла;
			Если НаборЗаписей.Количество() > 0 Тогда
				НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения");
				НаборЗаписей.Записать();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;// Запрос движений активов/пассив подготовлен без ошибок
	
КонецПроцедуры

// Сервисная процедура, используемая в обработчиках обновления.
// Возвращает список регистров-источников управленческого баланса.
//
// Возвращаемое значение:
//  Структура - балансовые регистры формирующие управленческий баланс
// 
Функция БалансовыеРегистры(ВключаяОнлайнРасчеты = Ложь) Экспорт
	
	Исключения = Новый Соответствие;
	Если НЕ ВключаяОнлайнРасчеты Тогда
		Исключения.Вставить("РасчетыСКлиентами", Истина);
		Исключения.Вставить("РасчетыСПоставщиками", Истина);
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		Исключения.Вставить("РасчетыСКлиентамиПоДокументам", Истина);
		Исключения.Вставить("РасчетыСПоставщикамиПоДокументам", Истина);
	Иначе
		Исключения.Вставить("РасчетыСКлиентамиПоСрокам", Истина);
		Исключения.Вставить("РасчетыСПоставщикамиПоСрокам", Истина);
	КонецЕсли;
	
	Таблицы = Новый Структура;
	ИсточникиБаланса = Метаданные.Перечисления.ИсточникиУправленческогоБаланса;
	Для Каждого Источник Из ИсточникиБаланса.ЗначенияПеречисления Цикл
		Если Исключения[Источник.Имя] = Истина Тогда
			Продолжить;
		КонецЕсли;
		Если Метаданные.РегистрыНакопления.Найти(Источник.Имя) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Таблицы.Вставить(Источник.Имя, Истина);
	КонецЦикла;
	Возврат Таблицы;
	
КонецФункции

// формирует и записывает движения по регистру ПрочиеАктивыПассивы
// Принцип формирования движений см. презентацию проекта https://partners.v8.1c.ru/forum/topic/1484492#m_1484492
//
// Параметры:
//  СсылкаНаДокумент - ДокументСсылка, Структура из КлючИЗначение - ссылка документа для которого необходимо сформировать и записать движения упр. баланса:
//                                      * Ссылка - ДокументСсылка
//  Движения - Структура из КлючИЗначение:
//               * Ключ - Строка- Имя регистра
//               * Значение - РегистрНакопленияНаборЗаписей.ПрочиеАктивыПассивы
//             КоллекцияДвижений - наборы записей регистров документа пеерданные в коллекции двжений или структуре имитирующей эту коллекцию.
//
Процедура ОбновитьДвиженияДокумента(СсылкаНаДокумент, Движения) Экспорт
	
	Если ТипЗнч(Движения) = Тип("КоллекцияДвижений") 
		И Движения.Найти("ПрочиеАктивыПассивы") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьДвиженияДокумента(СсылкаНаДокумент, Движения);
	Если ТипЗнч(Движения) = Тип("КоллекцияДвижений") Тогда
		Движения.ПрочиеАктивыПассивы.ДополнительныеСвойства.Вставить("ФормированиеУправленческогоБаланса");
		Движения.ПрочиеАктивыПассивы.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииФормированияДвиженийУправленческогоБаланса

#Область СформироватьЗапросДвиженийАктивовПассивов

// Запрос движений направлений.
// 
// Параметры:
//  Параметры - Структура - Параметры:
// * Ссылка - ДокументСсылка, Структура из КлючИЗначение -
// * ТаблицыДляДвижений - Структура, ТаблицаЗначений -:
// ** ВремяВыгрузкиРасчетовПоДокументам - Число -
// 
// Возвращаемое значение:
//  Запрос
Функция ЗапросДвиженийНаправлений(Параметры)
	
	Запрос = Параметры.ЗапросДвижений;
	Запрос.УстановитьПараметр("ТекстыЗапросов", Новый Массив);
	
	ПервичныеДвижения = Параметры.ТаблицыДляДвижений;
	ЕстьРасчетыСКлиентамиПоДокументам = ЕстьРасчетыПоДокументам(ПервичныеДвижения, "РасчетыСКлиентами");
	ЕстьРасчетыСПоставщикамиПоДокументам = ЕстьРасчетыПоДокументам(ПервичныеДвижения, "РасчетыСПоставщиками");
	Для Каждого Движения Из ПервичныеДвижения Цикл
		
		ИмяТаблицы = Движения.Ключ;
		Префикс = "Таблица";
		Если СтрНачинаетсяС(ИмяТаблицы, Префикс) Тогда
			ИмяТаблицы = Прав(ИмяТаблицы, СтрДлина(ИмяТаблицы)-СтрДлина(Префикс));
		КонецЕсли;
		
		Если ИмяТаблицы = "ДенежныеСредстваБезналичные" Тогда
			ДобавитьТекстЗапросаДенежныеСредства(Запрос, Движения.Значение, "Безналичные");
			
		ИначеЕсли ИмяТаблицы = "ДенежныеСредстваНаличные" Тогда
			ДобавитьТекстЗапросаДенежныеСредства(Запрос, Движения.Значение, "Наличные");
			
		ИначеЕсли ИмяТаблицы = "ДенежныеСредстваВКассахККМ" Тогда
			ДобавитьТекстЗапросаДенежныеСредстваВКассахККМ(Запрос, Движения.Значение);
			
		ИначеЕсли ИмяТаблицы = "ДенежныеСредстваВПути" Тогда
			ДобавитьТекстЗапросаДенежныеСредстваВПути(Запрос, Движения.Значение);
			
		ИначеЕсли ИмяТаблицы = "ДенежныеСредстваУПодотчетныхЛиц" Тогда
			ДобавитьТекстЗапросаДенежныеСредстваУПодотчетныхЛиц(Запрос, Движения.Значение);
			
		ИначеЕсли ИмяТаблицы = "РасчетыСКлиентамиПоСрокам" Тогда
			ДобавитьТекстЗапросаРасчетыСКлиентамиПоСрокам(Запрос, Движения.Значение);
			
		ИначеЕсли ИмяТаблицы = "РасчетыСКлиентамиПоДокументам" Тогда
			ДобавитьТекстЗапросаРасчетыСПартнерамиПоДокументам(Запрос, Движения.Значение, "РасчетыСКлиентами");
			
		ИначеЕсли ИмяТаблицы = "РасчетыСКлиентами" И (НЕ ЕстьРасчетыСКлиентамиПоДокументам ИЛИ Параметры.ЗаполнениеРегистра) Тогда
			ДобавитьТекстЗапросаРасчетыСПартнерами(Запрос, Движения.Значение, "РасчетыСКлиентами");
			
		ИначеЕсли ИмяТаблицы = "РасчетыСПоставщикамиПоСрокам" Тогда
			ДобавитьТекстЗапросаРасчетыСПоставщикамиПоСрокам(Запрос, Движения.Значение);
			
		ИначеЕсли ИмяТаблицы = "РасчетыСПоставщикамиПоДокументам" Тогда
			ДобавитьТекстЗапросаРасчетыСПартнерамиПоДокументам(Запрос, Движения.Значение, "РасчетыСПоставщиками");
			
		ИначеЕсли ИмяТаблицы = "РасчетыСПоставщиками" И (НЕ ЕстьРасчетыСПоставщикамиПоДокументам ИЛИ Параметры.ЗаполнениеРегистра) Тогда
			ДобавитьТекстЗапросаРасчетыСПартнерами(Запрос, Движения.Значение, "РасчетыСПоставщиками");
		
		ИначеЕсли ИмяТаблицы = "РасчетыПоФинансовымИнструментам" Тогда
			ДобавитьТекстЗапросаРасчетыПоФинансовымИнструментам(Запрос, Движения.Значение);
		
		ИначеЕсли ИмяТаблицы = "ПодарочныеСертификаты" Тогда
			ДобавитьТекстЗапросаПодарочныеСертификаты(Запрос, Движения.Значение);
			
		ИначеЕсли ИмяТаблицы = "ПереданнаяВозвратнаяТара" Тогда
			ДобавитьТекстЗапросаПереданнаяВозвратнаяТара(Запрос, Движения.Значение);
			
		ИначеЕсли ИмяТаблицы = "ПринятаяВозвратнаяТара" Тогда
			ДобавитьТекстЗапросаПринятаяВозвратнаяТара(Запрос, Движения.Значение);
			
		ИначеЕсли ИмяТаблицы = "ПрочиеДоходы" Тогда
			ДобавитьТекстЗапросаПрочиеДоходы(Запрос, Движения.Значение);
			
		ИначеЕсли ИмяТаблицы = "ПрочиеРасходы" Тогда
			ДобавитьТекстЗапросаПрочиеРасходы(Запрос, Движения.Значение);
			
		ИначеЕсли ИмяТаблицы = "ПартииПрочихРасходов" Тогда
			ДобавитьТекстЗапросаПартииПрочихРасходов(Запрос, Движения.Значение);
			
		ИначеЕсли ИмяТаблицы = "СебестоимостьТоваров" Тогда
			ДобавитьТекстЗапросаСебестоимостьТоваров(Запрос, Движения.Значение);
			
		ИначеЕсли ИмяТаблицы = "ТоварыКОформлениюОтчетовКомитенту" Тогда
			ДобавитьТекстЗапросаТоварыКОформлениюОтчетовКомитенту(Запрос, Движения.Значение);
			
		ИначеЕсли ИмяТаблицы = "УслугиКОформлениюОтчетовПринципалу" Тогда
			ДобавитьТекстЗапросаУслугиКОформлениюОтчетовПринципалу(Запрос, Движения.Значение);
		
		ИначеЕсли ИмяТаблицы = "ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках" Тогда
			ДобавитьТекстЗапросаТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках(Запрос, Движения.Значение);
			
			
		ИначеЕсли ИмяТаблицы = "ПроцентныеРасходыДисконтирования" Тогда
			ДобавитьТекстЗапросаПроцентныеРасходыДисконтирования(Запрос, Движения.Значение);
			
		ИначеЕсли ИмяТаблицы = "ПрочиеАктивыПассивы" Тогда
			ДобавитьТекстЗапросаПрочиеАктивыПассивы(Запрос, Движения.Значение);
			
		КонецЕсли;
		
	КонецЦикла;// по таблицам первичных движений
	
	Возврат Запрос;
	
КонецФункции

#Область ТекстыЗапросовДвиженийПоРегистрам

// ДенежныеСредстваБезналичные
// ДенежныеСредстваНаличные
// ДенежныеСредстваВКассахККМ
Процедура ДобавитьТекстЗапросаДенежныеСредства(Запрос, ТаблицаДвижений, ТипДС)
	
	ИмяТаблицы = "ДенежныеСредстваБезналичные";
	Если ТипДС = "Наличные" Тогда
		ИмяТаблицы = "ДенежныеСредстваНаличные";
	КонецЕсли;
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
		
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекстЗапроса = 
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Период КАК Период,
		|	Т.Организация КАК Организация,
		|	Т.БанковскийСчет КАК БанковскийСчет,
		|	Т.СуммаУпр КАК СуммаУпр,
		|	Т.Сторно КАК Сторно
		|	
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.СуммаУпр <> 0";
		Если ТипДС = "Наличные" Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "БанковскийСчет", "Касса");
		КонецЕсли;
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
		
	КонецЕсли;
	
	ТекстЗапроса = 
	"// %1
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.Организация КАК Организация,
	|	МестаХраненияДС.Подразделение КАК Подразделение,
	|	МестаХраненияДС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ДенежныеСредстваБезналичные) КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	СУММА(Т.СуммаУпр) КАК Сумма,
	|	СУММА(ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -Т.СуммаУпр ИНАЧЕ Т.СуммаУпр КОНЕЦ) КАК СуммаБаланса,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.ДвиженияБаланса) КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	ЛОЖЬ КАК РасчетСебестоимости,
	|	Т.Сторно КАК Сторно,
	|	ВЫБОР КОГДА МестаХраненияДС.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПустоеНаправление
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	ПервичныеДвижения КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчетаОрганизаций КАК МестаХраненияДС
	|	ПО Т.БанковскийСчет = МестаХраненияДС.Ссылка
	|ГДЕ
	|	Т.СуммаУпр <> 0
	|	И &Отбор
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Период,
	|	Т.Регистратор,
	|	Т.Организация,
	|	Т.ВидДвижения,
	|	МестаХраненияДС.Подразделение,
	|	МестаХраненияДС.НаправлениеДеятельности,
	|	Т.БанковскийСчет,
	|	Т.Сторно";
	
	Если ТипДС = "Наличные" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДенежныеСредстваБезналичные", "ДенежныеСредстваНаличные");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "БанковскиеСчетаОрганизаций", "Кассы");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "БанковскийСчет", "Касса");
	КонецЕсли;
	
	ПодготовитьДвиженияНаправлений(Запрос, ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения);
	
КонецПроцедуры

// ДенежныеСредстваВКассахККМ
Процедура ДобавитьТекстЗапросаДенежныеСредстваВКассахККМ(Запрос, ТаблицаДвижений)
	
	ИмяТаблицы = Метаданные.РегистрыНакопления.ДенежныеСредстваВКассахККМ.Имя;
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
		
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекстЗапроса = 
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Период КАК Период,
		|	Т.Организация КАК Организация,
		|	Т.КассаККМ КАК КассаККМ,
		|	Т.СуммаУпр КАК СуммаУпр,
		|	Т.Сторно КАК Сторно
		|	
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.СуммаУпр <> 0";
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
		
	КонецЕсли;
	
	ТекстЗапроса = 
	"// %1
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.Организация КАК Организация,
	|	МестаХраненияДС.Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ДенежныеСредстваНаличные) КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	СУММА(Т.СуммаУпр) КАК Сумма,
	|	СУММА(ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -Т.СуммаУпр ИНАЧЕ Т.СуммаУпр КОНЕЦ) КАК СуммаБаланса,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.ДвиженияБаланса) КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	ЛОЖЬ КАК РасчетСебестоимости,
	|	Т.Сторно КАК Сторно,
	|	ИСТИНА КАК ПустоеНаправление
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	ПервичныеДвижения КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КассыККМ КАК МестаХраненияДС
	|	ПО Т.КассаККМ = МестаХраненияДС.Ссылка
	|ГДЕ
	|	Т.СуммаУпр <> 0
	|	И &Отбор
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Период,
	|	Т.Регистратор,
	|	Т.Организация,
	|	Т.ВидДвижения,
	|	МестаХраненияДС.Подразделение,
	|	Т.КассаККМ,
	|	Т.Сторно";
	
	ПодготовитьДвиженияНаправлений(Запрос, ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения);
	
КонецПроцедуры

// ДенежныеСредстваВПути
Процедура ДобавитьТекстЗапросаДенежныеСредстваВПути(Запрос, ТаблицаДвижений)
	
	ИмяТаблицы = Метаданные.РегистрыНакопления.ДенежныеСредстваВПути.Имя;
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
	
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекстЗапроса =
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Период КАК Период,
		|	Т.Организация КАК Организация,
		|	Т.Получатель КАК Получатель,
		|	Т.СуммаУпр КАК СуммаУпр,
		|	Т.Сторно КАК Сторно
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.СуммаУпр <> 0";
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
		
	Иначе
		ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	КонецЕсли;
	
	ТекстЗапроса =
	"// %1
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Т.Получатель) = ТИП(Справочник.БанковскиеСчетаОрганизаций)
	|			ТОГДА ЕСТЬNULL(ВЫРАЗИТЬ(Т.Получатель КАК Справочник.БанковскиеСчетаОрганизаций).Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	|		КОГДА ТИПЗНАЧЕНИЯ(Т.Получатель) = ТИП(Справочник.Кассы)
	|			ТОГДА ЕСТЬNULL(ВЫРАЗИТЬ(Т.Получатель КАК Справочник.Кассы).Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Т.Получатель) = ТИП(Справочник.БанковскиеСчетаОрганизаций)
	|			ТОГДА ЕСТЬNULL(ВЫРАЗИТЬ(Т.Получатель КАК Справочник.БанковскиеСчетаОрганизаций).НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
	|		КОГДА ТИПЗНАЧЕНИЯ(Т.Получатель) = ТИП(Справочник.Кассы)
	|			ТОГДА ЕСТЬNULL(ВЫРАЗИТЬ(Т.Получатель КАК Справочник.Кассы).НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ДенежныеСредстваВПути) КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	СУММА(Т.СуммаУпр) КАК Сумма,
	|	СУММА(ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -Т.СуммаУпр ИНАЧЕ Т.СуммаУпр КОНЕЦ) КАК СуммаБаланса,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.ДвиженияБаланса) КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	ЛОЖЬ КАК РасчетСебестоимости,
	|	Т.Сторно КАК Сторно
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	ПервичныеДвижения КАК Т
	|ГДЕ
	|	Т.СуммаУпр <> 0
	|	И &Отбор
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Регистратор,
	|	Т.ВидДвижения,
	|	Т.Период,
	|	Т.Организация,
	|	Т.Получатель,
	|	Т.Сторно";
	
	ПодготовитьДвиженияНаправлений(Запрос, ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения);
	
КонецПроцедуры

// ДенежныеСредстваУПодотчетныхЛиц
Процедура ДобавитьТекстЗапросаДенежныеСредстваУПодотчетныхЛиц(Запрос, ТаблицаДвижений)
	
	ИмяТаблицы = Метаданные.РегистрыНакопления.ДенежныеСредстваУПодотчетныхЛиц.Имя;
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
	
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекстЗапроса =
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Период КАК Период,
		|	Т.Организация КАК Организация,
		|	Т.Подразделение КАК Подразделение,
		|	Т.СуммаУпр КАК СуммаУпр,
		|	Т.Сторно КАК Сторно
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.СуммаУпр <> 0";
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
	
	КонецЕсли;
	
	ТекстЗапроса =
	"// %1
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.Организация КАК Организация,
	|	Т.Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ДенежныеСредстваУПодотчетныхЛиц) КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	СУММА(Т.СуммаУпр) КАК Сумма,
	|	СУММА(ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -Т.СуммаУпр ИНАЧЕ Т.СуммаУпр КОНЕЦ) КАК СуммаБаланса,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.ДвиженияБаланса) КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	ЛОЖЬ КАК РасчетСебестоимости,
	|	Т.Сторно КАК Сторно
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	ПервичныеДвижения КАК Т
	|ГДЕ
	|	Т.СуммаУпр <> 0
	|	И &Отбор
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Регистратор,
	|	Т.ВидДвижения,
	|	Т.Период,
	|	Т.Организация,
	|	Т.Подразделение,
	|	Т.Сторно";
	
	ПодготовитьДвиженияНаправлений(Запрос, ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения);
	
КонецПроцедуры

// РасчетыСКлиентами, РасчетыСПоставщиками
// 
// Параметры:
// 	Запрос - Запрос -
// 	ТаблицаДвижений - ТаблицаЗначений - 
// 	ТипРасчетов - Строка - Описание
Процедура ДобавитьТекстЗапросаРасчетыСПартнерами(Запрос, ТаблицаДвижений, ТипРасчетов)
	
	ИмяТаблицыПоДокументам = "РасчетыСКлиентамиПоДокументам";
	Если ТипРасчетов = "РасчетыСПоставщиками" Тогда
		ИмяТаблицыПоДокументам = "РасчетыСПоставщикамиПоДокументам";
	КонецЕсли;
	ЕстьРасчетыПоДокументам = Запрос.Параметры.ТаблицыДвиженийНаправлений.Найти(ИмяТаблицыПоДокументам) <> Неопределено;
	Если ЕстьРасчетыПоДокументам И НЕ Запрос.Параметры.ЗаполнениеРегистра ИЛИ Запрос.Параметры.НовыеВзаиморасчеты Тогда
		Возврат;
	КонецЕсли;
	
	ИмяТаблицы = ТипРасчетов;
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
	
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекстЗапроса =
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Период КАК Период,
		|	Т.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Т.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Т.СуммаУпр КАК СуммаУпр,
		|	Т.Сторно КАК Сторно
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.СуммаУпр <> 0";
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
	
	КонецЕсли;
	
	ТекстЗапроса =
	"// %1
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	АналитикаПартнеров.Организация КАК Организация,
	|	Т.ОбъектРасчетов.Подразделение КАК Подразделение,
	|	АналитикаПартнеров.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗадолженностьКлиентов) КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	СУММА(Т.СуммаУпр) КАК Сумма,
	|	СУММА(ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -Т.СуммаУпр ИНАЧЕ Т.СуммаУпр КОНЕЦ) КАК СуммаБаланса,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.РасчетыСПартнерами) КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	ЛОЖЬ КАК РасчетСебестоимости,
	|	Т.Сторно КАК Сторно
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	ПервичныеДвижения КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПартнеров
	|	ПО Т.АналитикаУчетаПоПартнерам = АналитикаПартнеров.КлючАналитики
	|	И &РасчетыПоДокументам
	|ГДЕ
	|	Т.СуммаУпр <> 0
	|	И &Отбор
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Период,
	|	Т.Регистратор,
	|	АналитикаПартнеров.Организация,
	|	Т.ВидДвижения,
	|	Т.ОбъектРасчетов.Подразделение,
	|	АналитикаПартнеров.НаправлениеДеятельности,
	|	Т.Сторно";
	
	Если Запрос.Параметры.ЗаполнениеРегистра Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
						"И &РасчетыПоДокументам",
						"ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК РасчетыПоДокументам
						|	ПО Т.Регистратор = РасчетыПоДокументам.Регистратор");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
						"И &Отбор",
						"И РасчетыПоДокументам.Регистратор ЕСТЬ NULL
						|	И &Отбор");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"И &РасчетыПоДокументам","");
	КонецЕсли;
	
	Если ТипРасчетов = "РасчетыСПоставщиками" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РасчетыСКлиентами", "РасчетыСПоставщиками");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ЗадолженностьКлиентов", "ЗадолженностьПередПоставщиками");
	КонецЕсли;
	
	ПодготовитьДвиженияНаправлений(Запрос, ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения, "АналитикаПартнеров.Организация");
	
КонецПроцедуры

// РасчетыСКлиентамиПоДокументам, РасчетыСПоставщикамиПоДокументам
Процедура ДобавитьТекстЗапросаРасчетыСПартнерамиПоДокументам(Запрос, ТаблицаДвижений, ТипРасчетов)
	
	Если Запрос.Параметры.НовыеВзаиморасчеты Тогда
		Возврат;
	КонецЕсли;
	
	ИмяТаблицы = "РасчетыСКлиентамиПоДокументам";
	Если ТипРасчетов = "РасчетыСПоставщиками" Тогда
		ИмяТаблицы = "РасчетыСПоставщикамиПоДокументам";
	КонецЕсли;
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
	
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекстЗапроса =
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Период КАК Период,
		|	Т.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Т.ЗаказКлиента КАК ЗаказКлиента,
		|	Т.ДолгУпр КАК ДолгУпр,
		|	Т.ПредоплатаУпр КАК ПредоплатаУпр
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.ДолгУпр <> 0 ИЛИ Т.ПредоплатаУпр <> 0";
		
		Если ТипРасчетов = "РасчетыСПоставщиками" Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ЗаказКлиента", "ЗаказПоставщику");
		КонецЕсли;
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
	
	КонецЕсли;
	
	ТекстЗапроса =
	"// %1
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	АналитикаПартнеров.Организация КАК Организация,
	|	Т.ЗаказКлиента.Подразделение КАК Подразделение,
	|	АналитикаПартнеров.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА АналитикаПартнеров.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ТОГДА &СтатьяДолгаСобственныхОрганизаций
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗадолженностьКлиентов)
	|	КОНЕЦ КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	СУММА(Т.ДолгУпр) КАК Сумма,
	|	СУММА(ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -Т.ДолгУпр ИНАЧЕ Т.ДолгУпр КОНЕЦ) КАК СуммаБаланса,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.РасчетыСПартнерами) КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	ЛОЖЬ КАК РасчетСебестоимости,
	|	ЛОЖЬ КАК Сторно
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	ПервичныеДвижения КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПартнеров
	|	ПО Т.АналитикаУчетаПоПартнерам = АналитикаПартнеров.КлючАналитики
	|ГДЕ
	|	Т.ДолгУпр <> 0
	|	И &Отбор
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Период,
	|	Т.Регистратор,
	|	АналитикаПартнеров.Организация,
	|	Т.ВидДвижения,
	|	Т.ЗаказКлиента.Подразделение,
	|	АналитикаПартнеров.НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА АналитикаПартнеров.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ТОГДА &СтатьяДолгаСобственныхОрганизаций
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗадолженностьКлиентов)
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	АналитикаПартнеров.Организация КАК Организация,
	|	Т.ЗаказКлиента.Подразделение КАК Подразделение,
	|	АналитикаПартнеров.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА АналитикаПартнеров.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ТОГДА &СтатьяПредоплатыСобственныхОрганизаций
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПолученныеАвансы)
	|	КОНЕЦ КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	СУММА(Т.ПредоплатаУпр) КАК Сумма,
	|	СУММА(ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -Т.ПредоплатаУпр ИНАЧЕ Т.ПредоплатаУпр КОНЕЦ) КАК СуммаБаланса,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.РасчетыСПартнерами) КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	ЛОЖЬ КАК РасчетСебестоимости,
	|	ЛОЖЬ КАК Сторно
	|
	|ИЗ
	|	ПервичныеДвижения КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПартнеров
	|	ПО Т.АналитикаУчетаПоПартнерам = АналитикаПартнеров.КлючАналитики
	|ГДЕ
	|	Т.ПредоплатаУпр <> 0
	|	И &Отбор
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Период,
	|	Т.Регистратор,
	|	АналитикаПартнеров.Организация,
	|	Т.ВидДвижения,
	|	Т.ЗаказКлиента.Подразделение,
	|	АналитикаПартнеров.НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА АналитикаПартнеров.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ТОГДА &СтатьяПредоплатыСобственныхОрганизаций
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПолученныеАвансы)
	|	КОНЕЦ";
	
	Если ТипРасчетов = "РасчетыСПоставщиками" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ЗаказКлиента", "ЗаказПоставщику");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ЗадолженностьКлиентов", "ЗадолженностьПередПоставщиками");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПолученныеАвансы", "ВыданныеАвансы");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СтатьяДолгаСобственныхОрганизаций", "СтатьяДолгаСобственныхОрганизацийПоставщики");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СтатьяПредоплатыСобственныхОрганизаций", "СтатьяПредоплатыСобственныхОрганизацийПоставщики");
		СтатьяДолгаСобственныхОрганизацийПоставщики = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ОбязательстваПередСобственнымиОрганизациями;
		СтатьяПредоплатыСобственныхОрганизацийПоставщики = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ЗадолженностьСобственныхОрганизаций;
		Запрос.УстановитьПараметр("СтатьяДолгаСобственныхОрганизацийПоставщики", СтатьяДолгаСобственныхОрганизацийПоставщики);
		Запрос.УстановитьПараметр("СтатьяПредоплатыСобственныхОрганизацийПоставщики", СтатьяПредоплатыСобственныхОрганизацийПоставщики);
	КонецЕсли;
	
	СтатьяДолгаСобственныхОрганизаций = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ЗадолженностьСобственныхОрганизаций;
	СтатьяПредоплатыСобственныхОрганизаций = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ОбязательстваПередСобственнымиОрганизациями;
	Запрос.УстановитьПараметр("СтатьяДолгаСобственныхОрганизаций", СтатьяДолгаСобственныхОрганизаций);
	Запрос.УстановитьПараметр("СтатьяПредоплатыСобственныхОрганизаций", СтатьяПредоплатыСобственныхОрганизаций);
	
	ПодготовитьДвиженияНаправлений(Запрос, ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения, "АналитикаПартнеров.Организация");
	
КонецПроцедуры

// РасчетыСКлиентамиПоСрокам, РасчетыСПоставщикамиПоСрокам
Процедура ДобавитьТекстЗапросаРасчетыСКлиентамиПоСрокам(Запрос, ТаблицаДвижений)
	
	ИмяТаблицы = "РасчетыСКлиентамиПоСрокам";
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
	
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекстЗапроса =
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК ДокументРегистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Период КАК Период,
		|	Т.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Т.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Т.ДолгУпр КАК ДолгУпр,
		|	Т.ПредоплатаУпр КАК ПредоплатаУпр,
		|	Т.Сторно КАК Сторно,
		|	Т.ХозяйственнаяОперация КАК ХозяйственнаяОперация
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.ДолгУпр <> 0 ИЛИ Т.ПредоплатаУпр <> 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АналитикаУчетаПоПартнерам";
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
	
	КонецЕсли;
	
	ТекстЗапроса =
	"// %1
	|ВЫБРАТЬ
	|		Т.ДокументРегистратор КАК Регистратор,
	|		Т.Период КАК Период,
	|		АналитикаПартнеров.Организация КАК Организация,
	|		Т.ОбъектРасчетов КАК ОбъектРасчетов,
	|		АналитикаПартнеров.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ВЫБОР
	|			КОГДА АналитикаПартнеров.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|				ТОГДА &СтатьяДолгаСобственныхОрганизацийКлиенты
	|			ИНАЧЕ
	|				ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗадолженностьКлиентов)
	|		КОНЕЦ КАК Статья,
	|		НЕОПРЕДЕЛЕНО КАК Аналитика,
	|		Т.Сторно КАК Сторно,
	|		ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -Т.ДолгУпр ИНАЧЕ Т.ДолгУпр КОНЕЦ КАК Сумма,
	|		ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -Т.ДолгУпр ИНАЧЕ Т.ДолгУпр КОНЕЦ КАК СуммаБаланса
	|ПОМЕСТИТЬ ТаблицаДвиженийКлиентыПоСрокам
	|	ИЗ
	|		ПервичныеДвижения КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПартнеров
	|		ПО Т.АналитикаУчетаПоПартнерам = АналитикаПартнеров.КлючАналитики
	|	ГДЕ
	|		Т.ДолгУпр <> 0
	|		И &Отбор
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Т.ДокументРегистратор КАК Регистратор,
	|		Т.Период КАК Период,
	|		АналитикаПартнеров.Организация КАК Организация,
	|		Т.ОбъектРасчетов КАК ОбъектРасчетов,
	|		АналитикаПартнеров.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ВЫБОР
	|			КОГДА АналитикаПартнеров.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|				ТОГДА &СтатьяПредоплатыСобственныхОрганизацийКлиенты
	|			ИНАЧЕ
	|				ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПолученныеАвансы)
	|		КОНЕЦ КАК Статья,
	|		НЕОПРЕДЕЛЕНО КАК Аналитика,
	|		Т.Сторно КАК Сторно,
	|		ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА -Т.ПредоплатаУпр ИНАЧЕ Т.ПредоплатаУпр КОНЕЦ КАК Сумма,
	|		ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА -Т.ПредоплатаУпр ИНАЧЕ Т.ПредоплатаУпр КОНЕЦ КАК СуммаБаланса
	|
	|	ИЗ
	|		ПервичныеДвижения КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПартнеров
	|		ПО Т.АналитикаУчетаПоПартнерам = АналитикаПартнеров.КлючАналитики
	|	ГДЕ
	|		Т.ПредоплатаУпр <> 0
	|		И &Отбор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов;
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Регистратор КАК Регистратор,
	|	ВЫБОР КОГДА ВложенныйЗапрос.Сумма > 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	ВложенныйЗапрос.Период КАК Период,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.Подразделение КАК Подразделение,
	|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВложенныйЗапрос.Статья КАК Статья,
	|	ВложенныйЗапрос.Аналитика КАК Аналитика,
	|	ВЫБОР КОГДА ВложенныйЗапрос.Сумма > 0
	|		ТОГДА ВложенныйЗапрос.Сумма
	|		ИНАЧЕ -ВложенныйЗапрос.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ВложенныйЗапрос.СуммаБаланса КАК СуммаБаланса,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.РасчетыСПартнерами) КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	ЛОЖЬ КАК РасчетСебестоимости,
	|	ВложенныйЗапрос.Сторно КАК Сторно
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ (
	|	ВЫБРАТЬ
	|		Т.Регистратор КАК Регистратор,
	|		Т.Период КАК Период,
	|		Т.Организация КАК Организация,
	|		ОбъектыРасчетов.Подразделение КАК Подразделение,
	|		Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		Т.Статья КАК Статья,
	|		Т.Аналитика КАК Аналитика,
	|		Т.Сторно КАК Сторно,
	|		СУММА(Т.Сумма) КАК Сумма,
	|		СУММА(Т.СуммаБаланса) КАК СуммаБаланса
	|
	|	ИЗ
	|		ТаблицаДвиженийКлиентыПоСрокам КАК Т
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|				ПО Т.ОбъектРасчетов = ОбъектыРасчетов.Ссылка
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Т.Период,
	|		Т.Регистратор,
	|		Т.Организация,
	|		ОбъектыРасчетов.Подразделение,
	|		Т.НаправлениеДеятельности,
	|		Т.Статья,
	|		Т.Аналитика,
	|		Т.Сторно)КАК ВложенныйЗапрос
	|
	|ГДЕ
	|	ВложенныйЗапрос.Сумма <> 0;
	|
	|УНИЧТОЖИТЬ ТаблицаДвиженийКлиентыПоСрокам";
	
	СтатьяДолгаСобственныхОрганизацийКлиенты = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ЗадолженностьСобственныхОрганизаций;
	СтатьяПредоплатыСобственныхОрганизацийКлиенты = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ОбязательстваПередСобственнымиОрганизациями;
	Запрос.УстановитьПараметр("СтатьяДолгаСобственныхОрганизацийКлиенты", СтатьяДолгаСобственныхОрганизацийКлиенты);
	Запрос.УстановитьПараметр("СтатьяПредоплатыСобственныхОрганизацийКлиенты", СтатьяПредоплатыСобственныхОрганизацийКлиенты);
	
	ДобавитьСоединениеРегистраторов(Запрос, ТекстЗапроса, ПервичныеДвижения);
	ТекстЗапроса  = СтрЗаменить(ТекстЗапроса,
						"Т.Регистратор = ДокументыКОтражению.Регистратор",
						"Т.ДокументРегистратор = ДокументыКОтражению.Регистратор");
	ДобавитьОтбор(Запрос, ТекстЗапроса, "АналитикаПартнеров.Организация", "Т.ДокументРегистратор");
	ЗаменитьПодстановки(ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения);
	
	ДобавитьЗначениеВПараметр(Запрос, "ТекстыЗапросов", ТекстЗапроса);
	ДобавитьЗначениеВПараметр(Запрос, "ТаблицыДвиженийНаправлений", ИмяТаблицы);
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", ИмяТаблицы);
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаРасчетыСПоставщикамиПоСрокам(Запрос, ТаблицаДвижений)
	
	ИмяТаблицы = "РасчетыСПоставщикамиПоСрокам";
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
	
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекстЗапроса =
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК ДокументРегистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Период КАК Период,
		|	Т.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Т.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Т.ДолгУпр КАК ДолгУпр,
		|	Т.ПредоплатаУпр КАК ПредоплатаУпр,
		|	Т.Сторно КАК Сторно,
		|	Т.ХозяйственнаяОперация КАК ХозяйственнаяОперация
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.ДолгУпр <> 0 ИЛИ Т.ПредоплатаУпр <> 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АналитикаУчетаПоПартнерам";
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
	
	КонецЕсли;
	
	ТекстЗапроса =
	"// %1
	|ВЫБРАТЬ
	|		Т.ДокументРегистратор КАК Регистратор,
	|		Т.Период КАК Период,
	|		АналитикаПартнеров.Организация КАК Организация,
	|		Т.ОбъектРасчетов КАК ОбъектРасчетов,
	|		АналитикаПартнеров.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ВЫБОР
	|			КОГДА АналитикаПартнеров.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|				ТОГДА &СтатьяДолгаСобственныхОрганизацийПоставщики
	|			ИНАЧЕ
	|				ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗадолженностьПередПоставщиками)
	|		КОНЕЦ КАК Статья,
	|		НЕОПРЕДЕЛЕНО КАК Аналитика,
	|		Т.Сторно КАК Сторно,
	|		ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА -Т.ДолгУпр ИНАЧЕ Т.ДолгУпр КОНЕЦ КАК Сумма,
	|		ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА -Т.ДолгУпр ИНАЧЕ Т.ДолгУпр КОНЕЦ КАК СуммаБаланса
	|ПОМЕСТИТЬ ТаблицаДвиженийПоставщикиПоСрокам
	|	ИЗ
	|		ПервичныеДвижения КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПартнеров
	|		ПО Т.АналитикаУчетаПоПартнерам = АналитикаПартнеров.КлючАналитики
	|	ГДЕ
	|		Т.ДолгУпр <> 0
	|		И &Отбор
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Т.ДокументРегистратор КАК Регистратор,
	|		Т.Период КАК Период,
	|		АналитикаПартнеров.Организация КАК Организация,
	|		Т.ОбъектРасчетов КАК ОбъектРасчетов,
	|		АналитикаПартнеров.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ВЫБОР
	|			КОГДА АналитикаПартнеров.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|				ТОГДА &СтатьяПредоплатыСобственныхОрганизацийПоставщики
	|			ИНАЧЕ
	|				ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ВыданныеАвансы)
	|		КОНЕЦ КАК Статья,
	|		НЕОПРЕДЕЛЕНО КАК Аналитика,
	|		Т.Сторно КАК Сторно,
	|		ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -Т.ПредоплатаУпр ИНАЧЕ Т.ПредоплатаУпр КОНЕЦ КАК Сумма,
	|		ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -Т.ПредоплатаУпр ИНАЧЕ Т.ПредоплатаУпр КОНЕЦ КАК СуммаБаланса
	|
	|	ИЗ
	|		ПервичныеДвижения КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПартнеров
	|		ПО Т.АналитикаУчетаПоПартнерам = АналитикаПартнеров.КлючАналитики
	|	ГДЕ
	|		Т.ПредоплатаУпр <> 0
	|		И &Отбор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов
	|;
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Регистратор КАК Регистратор,
	|	ВЫБОР КОГДА ВложенныйЗапрос.Сумма > 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	ВложенныйЗапрос.Период КАК Период,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.Подразделение КАК Подразделение,
	|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВложенныйЗапрос.Статья КАК Статья,
	|	ВложенныйЗапрос.Аналитика КАК Аналитика,
	|	ВЫБОР КОГДА ВложенныйЗапрос.Сумма > 0
	|		ТОГДА ВложенныйЗапрос.Сумма
	|		ИНАЧЕ -ВложенныйЗапрос.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ВложенныйЗапрос.СуммаБаланса КАК СуммаБаланса,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.РасчетыСПартнерами) КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	ЛОЖЬ КАК РасчетСебестоимости,
	|	ВложенныйЗапрос.Сторно КАК Сторно
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ (
	|	ВЫБРАТЬ
	|		Т.Регистратор КАК Регистратор,
	|		Т.Период КАК Период,
	|		Т.Организация КАК Организация,
	|		ОбъектыРасчетов.Подразделение КАК Подразделение,
	|		Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		Т.Статья КАК Статья,
	|		Т.Аналитика КАК Аналитика,
	|		Т.Сторно КАК Сторно,
	|		СУММА(Т.Сумма) КАК Сумма,
	|		СУММА(Т.СуммаБаланса) КАК СуммаБаланса
	|	ИЗ
	|		ТаблицаДвиженийПоставщикиПоСрокам КАК Т
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|				ПО Т.ОбъектРасчетов = ОбъектыРасчетов.Ссылка
	|
	|	СГРУППИРОВАТЬ ПО
	|		Т.Период,
	|		Т.Регистратор,
	|		Т.Организация,
	|		ОбъектыРасчетов.Подразделение,
	|		Т.НаправлениеДеятельности,
	|		Т.Статья,
	|		Т.Аналитика,
	|		Т.Сторно)КАК ВложенныйЗапрос
	|
	|
	|ГДЕ
	|	ВложенныйЗапрос.Сумма <> 0;
	|
	|УНИЧТОЖИТЬ ТаблицаДвиженийПоставщикиПоСрокам";
	
	СтатьяДолгаСобственныхОрганизацийПоставщики = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ОбязательстваПередСобственнымиОрганизациями;
	СтатьяПредоплатыСобственныхОрганизацийПоставщики = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ЗадолженностьСобственныхОрганизаций;
	Запрос.УстановитьПараметр("СтатьяДолгаСобственныхОрганизацийПоставщики", СтатьяДолгаСобственныхОрганизацийПоставщики);
	Запрос.УстановитьПараметр("СтатьяПредоплатыСобственныхОрганизацийПоставщики", СтатьяПредоплатыСобственныхОрганизацийПоставщики);
	
	ДобавитьСоединениеРегистраторов(Запрос, ТекстЗапроса, ПервичныеДвижения);
	ТекстЗапроса  = СтрЗаменить(ТекстЗапроса,
						"Т.Регистратор = ДокументыКОтражению.Регистратор",
						"Т.ДокументРегистратор = ДокументыКОтражению.Регистратор");
	ДобавитьОтбор(Запрос, ТекстЗапроса, "АналитикаПартнеров.Организация", "Т.ДокументРегистратор");
	ЗаменитьПодстановки(ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения);
	
	ДобавитьЗначениеВПараметр(Запрос, "ТекстыЗапросов", ТекстЗапроса);
	ДобавитьЗначениеВПараметр(Запрос, "ТаблицыДвиженийНаправлений", ИмяТаблицы);
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", ИмяТаблицы);
	
КонецПроцедуры

// РасчетыПоФинансовымИнструментам
Процедура ДобавитьТекстЗапросаРасчетыПоФинансовымИнструментам(Запрос, ТаблицаДвижений)
	
	ИмяТаблицы = Метаданные.РегистрыНакопления.РасчетыПоФинансовымИнструментам.Имя;
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
	
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекстЗапроса =
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Период КАК Период,
		|	Т.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Т.Договор КАК Договор,
		|	Т.ТипСуммы КАК ТипСуммы,
		|	Т.СуммаУпр КАК СуммаУпр,
		|	Т.Сторно КАК Сторно
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.СуммаУпр <> 0";
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
	
	КонецЕсли;
	
	ТекстЗапроса =
	"// %1
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	ДанныеДоговора.Организация КАК Организация,
	|	ДанныеДоговора.Подразделение КАК Подразделение,
	|	ДанныеДоговора.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА ДанныеДоговора.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗадолженностьПоКредитам)
	|		КОГДА ДанныеДоговора.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ДепозитыВБанках)
	|		КОГДА ДанныеДоговора.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗаймыВыданные)
	|	КОНЕЦ КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	СУММА(Т.СуммаУпр) КАК Сумма,
	|	СУММА(ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -Т.СуммаУпр ИНАЧЕ Т.СуммаУпр КОНЕЦ) КАК СуммаБаланса,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.ДвиженияБаланса) КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	ЛОЖЬ КАК РасчетСебестоимости,
	|	Т.Сторно КАК Сторно
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	ПервичныеДвижения КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКредитовИДепозитов КАК ДанныеДоговора
	|	ПО Т.Договор = ДанныеДоговора.Ссылка
	|ГДЕ
	|	Т.СуммаУпр <> 0
	|	И Т.Договор ССЫЛКА Справочник.ДоговорыКредитовИДепозитов
	|	И &Отбор
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Период,
	|	Т.Регистратор,
	|	ДанныеДоговора.Организация,
	|	Т.ВидДвижения,
	|	ДанныеДоговора.ХарактерДоговора,
	|	ДанныеДоговора.Подразделение,
	|	ДанныеДоговора.НаправлениеДеятельности,
	|	Т.Сторно
	|";
	
	ПодготовитьДвиженияНаправлений(Запрос, ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения, "ДанныеДоговора.Организация");
	
КонецПроцедуры

// ПодарочныеСертификаты
Процедура ДобавитьТекстЗапросаПодарочныеСертификаты(Запрос, ТаблицаДвижений)
	
	ИмяТаблицы = Метаданные.РегистрыНакопления.ПодарочныеСертификаты.Имя;
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
	
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекстЗапроса =
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Период КАК Период,
		|	Т.Сумма КАК Сумма,
		|	Т.ПодарочныйСертификат КАК ПодарочныйСертификат,
		|	Т.Сторно КАК Сторно
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.Сумма <> 0";
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
	
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	ЕСТЬNULL(Т.ПодарочныйСертификат.Организация.ВалютаРегламентированногоУчета, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК Валюта,
	|	Т.ПодарочныйСертификат.Организация КАК Организация,
	|	Т.Сторно КАК Сторно,
	|	СУММА(Т.Сумма) КАК Сумма,
	|	СУММА(ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА Т.Сумма ИНАЧЕ -Т.Сумма КОНЕЦ) КАК СуммаБаланса
	|
	|ПОМЕСТИТЬ втДанныеРегистраПодарочныеСертификаты
	|ИЗ
	|	ПервичныеДвижения КАК Т
	|ГДЕ
	|	Т.Сумма <> 0
	|	И &Отбор
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Регистратор,
	|	Т.ВидДвижения,
	|	Т.Период,
	|	Т.ПодарочныйСертификат.Организация.ВалютаРегламентированногоУчета,
	|	Т.ПодарочныйСертификат.Организация,
	|	Т.Сторно
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	ЕСТЬNULL(КурсыВалют.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК Валюта,
	|	ЕСТЬNULL(КурсыВалют.БазоваяВалюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК БазоваяВалюта,
	|	МАКСИМУМ(КурсыВалют.Период) КАК Период,
	|	КурсыВалютУпр.Валюта КАК ВалютаУпр,
	|	КурсыВалютУпр.БазоваяВалюта КАК БазоваяВалютаУпр,
	|	МАКСИМУМ(КурсыВалютУпр.Период) КАК ПериодУпр
	|ПОМЕСТИТЬ втПериодыПодарочныеСертификаты
	|ИЗ
	|	втДанныеРегистраПодарочныеСертификаты КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО Т.Валюта = КурсыВалют.Валюта
	|			И Т.Валюта = КурсыВалют.БазоваяВалюта
	|			И Т.Период >= КурсыВалют.Период
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалютУпр
	|		ПО КурсыВалютУпр.Валюта = &ВалютаУправленческогоУчета
	|			И Т.Валюта = КурсыВалютУпр.БазоваяВалюта
	|			И Т.Период >= КурсыВалютУпр.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Регистратор,
	|	КурсыВалют.БазоваяВалюта,
	|	КурсыВалют.Валюта,
	|	КурсыВалютУпр.БазоваяВалюта,
	|	КурсыВалютУпр.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПериоды.Регистратор КАК Регистратор,
	|	втПериоды.Период КАК Период,
	|	втПериоды.Валюта КАК Валюта,
	|	ЕСТЬNULL(КурсыВалют.КурсЧислитель, 0) / ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1)* ЕСТЬNULL(КурсыВалютУпр.КурсЗнаменатель, 0) / ЕСТЬNULL(КурсыВалютУпр.КурсЧислитель, 1) КАК КоэффициентУпр,
	|	ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) КАК КурсЧислительВалюты,
	|	ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) КАК КурсЗнаменательВалюты,
	|	&ВалютаУправленческогоУчета КАК ВалютаУпр,
	|	ЕСТЬNULL(КурсыВалютУпр.КурсЧислитель, 1) КАК КурсЧислительВалютыУпр,
	|	ЕСТЬNULL(КурсыВалютУпр.КурсЗнаменатель, 1) КАК КурсЗнаменательВалютыУпр
	|ПОМЕСТИТЬ втКурсыВалютПодарочныеСертификаты
	|ИЗ
	|	втПериодыПодарочныеСертификаты КАК втПериоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО втПериоды.Валюта = КурсыВалют.Валюта
	|			И втПериоды.Период = КурсыВалют.Период
	|			И втПериоды.БазоваяВалюта = КурсыВалют.БазоваяВалюта
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалютУпр
	|		ПО КурсыВалютУпр.Валюта = &ВалютаУправленческогоУчета
	|			И втПериоды.ПериодУпр = КурсыВалютУпр.Период
	|			И втПериоды.БазоваяВалютаУпр = КурсыВалютУпр.БазоваяВалюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// %1
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПолученныеАвансы) КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	ВЫБОР КОГДА Т.Валюта = КурсыВалют.Валюта
	|		ТОГДА Т.Сумма
	|		ИНАЧЕ ВЫРАЗИТЬ(Т.Сумма * КурсыВалют.КоэффициентУпр КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР КОГДА Т.Валюта = КурсыВалют.Валюта
	|		ТОГДА Т.СуммаБаланса
	|		ИНАЧЕ ВЫРАЗИТЬ(Т.СуммаБаланса * КурсыВалют.КоэффициентУпр КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаБаланса,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.ДвиженияБаланса) КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	ЛОЖЬ КАК РасчетСебестоимости,
	|	Т.Сторно КАК Сторно
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	втДанныеРегистраПодарочныеСертификаты КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсыВалютПодарочныеСертификаты КАК КурсыВалют
	|	ПО Т.Регистратор = КурсыВалют.Регистратор";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%1", ИмяТаблицы);
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", "втДанныеРегистраПодарочныеСертификаты");
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", "втПериодыПодарочныеСертификаты");
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", "втКурсыВалютПодарочныеСертификаты");
	
	ПодготовитьДвиженияНаправлений(Запрос, ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения);
	
КонецПроцедуры
 
// ПереданнаяВозвратнаяТара
Процедура ДобавитьТекстЗапросаПереданнаяВозвратнаяТара(Запрос, ТаблицаДвижений)
	
	ИмяТаблицы = Метаданные.РегистрыНакопления.ПереданнаяВозвратнаяТара.Имя;
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
	
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекстЗапроса =
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Период КАК Период,
		|	Т.ДокументПередачи КАК ДокументПередачи,
		|	Т.ВидЗапасов КАК ВидЗапасов,
		|	Т.ПредусмотренЗалог КАК ПредусмотренЗалог,
		|	Т.Сумма КАК Сумма,
		|	Т.Сторно КАК Сторно
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.Сумма <> 0";
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
	
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.ДокументПередачи.Валюта КАК Валюта,
	|	Т.ДокументПередачи.Организация КАК Организация,
	|	ЕСТЬNULL(Т.ДокументПередачи.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение,
	|	ЕСТЬNULL(Т.ДокументПередачи.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	Т.Сторно КАК Сторно,
	|	СУММА(Т.Сумма) КАК Сумма,
	|	СУММА(ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -Т.Сумма ИНАЧЕ Т.Сумма КОНЕЦ) КАК СуммаБаланса
	|
	|ПОМЕСТИТЬ втДанныеРегистраПереданнаяТара
	|ИЗ
	|	ПервичныеДвижения КАК Т
	|ГДЕ
	|	Т.Сумма <> 0
	|	И НЕ Т.ПредусмотренЗалог
	|	И &Отбор
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Регистратор,
	|	Т.ВидДвижения,
	|	Т.Период,
	|	Т.ДокументПередачи.Валюта,
	|	Т.ДокументПередачи.Организация,
	|	ЕСТЬNULL(Т.ДокументПередачи.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)),
	|	ЕСТЬNULL(Т.ДокументПередачи.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
	|	Т.Сторно
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	КурсыВалют.Валюта КАК Валюта,
	|	КурсыВалют.БазоваяВалюта КАК БазоваяВалюта,
	|	МАКСИМУМ(КурсыВалют.Период) КАК Период,
	|	КурсыВалютУпр.Валюта КАК ВалютаУпр,
	|	КурсыВалютУпр.БазоваяВалюта КАК БазоваяВалютаУпр,
	|	МАКСИМУМ(КурсыВалютУпр.Период) КАК ПериодУпр
	|ПОМЕСТИТЬ втПериодыПереданнаяТара
	|ИЗ
	|	втДанныеРегистраПереданнаяТара КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО Т.Валюта = КурсыВалют.Валюта
	|			И Т.Период >= КурсыВалют.Период
	|			И Т.Организация.ВалютаРегламентированногоУчета = КурсыВалют.БазоваяВалюта
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалютУпр
	|		ПО КурсыВалютУпр.Валюта = &ВалютаУправленческогоУчета
	|			И Т.Период >= КурсыВалютУпр.Период
	|			И Т.Организация.ВалютаРегламентированногоУчета = КурсыВалютУпр.БазоваяВалюта
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Регистратор,
	|	КурсыВалют.Валюта,
	|	КурсыВалют.БазоваяВалюта,
	|	КурсыВалютУпр.Валюта,
	|	КурсыВалютУпр.БазоваяВалюта 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПериоды.Регистратор КАК Регистратор,
	|	втПериоды.Период КАК Период,
	|	втПериоды.Валюта КАК Валюта,
	|	ЕСТЬNULL(КурсыВалют.КурсЧислитель, 0) / ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1)* ЕСТЬNULL(КурсыВалютУпр.КурсЗнаменатель, 0) / ЕСТЬNULL(КурсыВалютУпр.КурсЧислитель, 1) КАК КоэффициентУпр,
	|	ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) КАК КурсЧислительВалюты,
	|	ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) КАК КурсЗнаменательВалюты,
	|	&ВалютаУправленческогоУчета КАК ВалютаУпр,
	|	ЕСТЬNULL(КурсыВалютУпр.КурсЧислитель, 1) КАК КурсЧислительВалютыУпр,
	|	ЕСТЬNULL(КурсыВалютУпр.КурсЗнаменатель, 1) КАК КурсЗнаменательВалютыУпр
	|ПОМЕСТИТЬ втКурсыВалютПереданнаяТара
	|ИЗ
	|	втПериодыПереданнаяТара КАК втПериоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО втПериоды.Валюта = КурсыВалют.Валюта
	|			И втПериоды.Период = КурсыВалют.Период
	|			И втПериоды.БазоваяВалюта = КурсыВалют.БазоваяВалюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалютУпр
	|		ПО КурсыВалютУпр.Валюта = &ВалютаУправленческогоУчета
	|			И втПериоды.ПериодУпр = КурсыВалютУпр.Период
	|			И втПериоды.БазоваяВалютаУпр = КурсыВалютУпр.БазоваяВалюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// %1
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.Организация КАК Организация,
	|	Т.Подразделение КАК Подразделение,
	|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗадолженностьКлиентов) КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	ВЫРАЗИТЬ(Т.Сумма * КурсыВалют.КоэффициентУпр КАК ЧИСЛО(31,2)) КАК Сумма,
	|	ВЫРАЗИТЬ(Т.СуммаБаланса * КурсыВалют.КоэффициентУпр КАК ЧИСЛО(31,2)) КАК СуммаБаланса,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.ДвиженияБаланса) КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	ЛОЖЬ КАК РасчетСебестоимости,
	|	Т.Сторно КАК Сторно
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	втДанныеРегистраПереданнаяТара КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсыВалютПереданнаяТара КАК КурсыВалют
	|	ПО Т.Регистратор = КурсыВалют.Регистратор";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%1", ИмяТаблицы);
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", "втДанныеРегистраПереданнаяТара");
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", "втПериодыПереданнаяТара");
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", "втКурсыВалютПереданнаяТара");
	
	ПодготовитьДвиженияНаправлений(Запрос, ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения, "Т.ДокументПередачи.Организация");
	
КонецПроцедуры

// ПринятаяВозвратнаяТара
Процедура ДобавитьТекстЗапросаПринятаяВозвратнаяТара(Запрос, ТаблицаДвижений)
	
	ИмяТаблицы = Метаданные.РегистрыНакопления.ПринятаяВозвратнаяТара.Имя;
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
	
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекстЗапроса =
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Период КАК Период,
		|	Т.ДокументПоступления КАК ДокументПоступления,
		|	Т.ПредусмотренЗалог КАК ПредусмотренЗалог,
		|	Т.Сумма КАК Сумма,
		|	Т.Сторно КАК Сторно
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.Сумма <> 0";
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
	
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.ДокументПоступления.Валюта КАК Валюта,
	|	Т.ДокументПоступления.Организация КАК Организация,
	|	Т.ДокументПоступления.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Т.ДокументПоступления) = ТИП(Документ.ПриобретениеТоваровУслуг)
	|			ТОГДА ВЫРАЗИТЬ(Т.ДокументПоступления КАК Документ.ПриобретениеТоваровУслуг).НаправлениеДеятельности
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	Т.Сторно КАК Сторно,
	|	СУММА(Т.Сумма) КАК Сумма,
	|	СУММА(ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА Т.Сумма ИНАЧЕ -Т.Сумма КОНЕЦ) КАК СуммаБаланса
	|
	|ПОМЕСТИТЬ втДанныеРегистраПринятаяТара
	|ИЗ
	|	ПервичныеДвижения КАК Т
	|ГДЕ
	|	Т.Сумма <> 0
	|	И НЕ Т.ПредусмотренЗалог
	|	И &Отбор
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Регистратор,
	|	Т.ВидДвижения,
	|	Т.Период,
	|	Т.ДокументПоступления.Валюта,
	|	Т.ДокументПоступления.Организация,
	|	Т.ДокументПоступления.Подразделение,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Т.ДокументПоступления) = ТИП(Документ.ПриобретениеТоваровУслуг)
	|			ТОГДА ВЫРАЗИТЬ(Т.ДокументПоступления КАК Документ.ПриобретениеТоваровУслуг).НаправлениеДеятельности
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ,
	|	Т.Сторно
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	КурсыВалют.Валюта КАК Валюта,
	|	КурсыВалют.БазоваяВалюта КАК БазоваяВалюта,
	|	МАКСИМУМ(КурсыВалют.Период) КАК Период,
	|	КурсыВалютУпр.Валюта КАК ВалютаУпр,
	|	КурсыВалютУпр.БазоваяВалюта КАК БазоваяВалютаУпр,
	|	МАКСИМУМ(КурсыВалютУпр.Период) КАК ПериодУпр
	|ПОМЕСТИТЬ втПериодыПринятаяТара
	|ИЗ
	|	втДанныеРегистраПринятаяТара КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО Т.Валюта = КурсыВалют.Валюта
	|			И Т.Период >= КурсыВалют.Период
	|			И Т.Организация.ВалютаРегламентированногоУчета = КурсыВалют.БазоваяВалюта
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалютУпр
	|		ПО КурсыВалютУпр.Валюта = &ВалютаУправленческогоУчета
	|			И Т.Период >= КурсыВалютУпр.Период
	|			И Т.Организация.ВалютаРегламентированногоУчета = КурсыВалютУпр.БазоваяВалюта
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Регистратор,
	|	КурсыВалют.Валюта,
	|	КурсыВалют.БазоваяВалюта,
	|	КурсыВалютУпр.Валюта,
	|	КурсыВалютУпр.БазоваяВалюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПериоды.Регистратор КАК Регистратор,
	|	втПериоды.Период КАК Период,
	|	втПериоды.Валюта КАК Валюта,
	|	ЕСТЬNULL(КурсыВалют.КурсЧислитель, 0) / ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1)* ЕСТЬNULL(КурсыВалютУпр.КурсЗнаменатель, 0) / ЕСТЬNULL(КурсыВалютУпр.КурсЧислитель, 1) КАК КоэффициентУпр,
	|	ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) КАК КурсЧислительВалюты,
	|	ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) КАК КурсЗнаменательВалюты,
	|	&ВалютаУправленческогоУчета КАК ВалютаУпр,
	|	ЕСТЬNULL(КурсыВалютУпр.КурсЧислитель, 1) КАК КурсЧислительВалютыУпр,
	|	ЕСТЬNULL(КурсыВалютУпр.КурсЗнаменатель, 1) КАК КурсЗнаменательВалютыУпр
	|ПОМЕСТИТЬ втКурсыВалютПринятаяТара
	|ИЗ
	|	втПериодыПринятаяТара КАК втПериоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО втПериоды.Валюта = КурсыВалют.Валюта
	|			И втПериоды.Период = КурсыВалют.Период
	|			И втПериоды.БазоваяВалюта = КурсыВалют.БазоваяВалюта
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалютУпр
	|		ПО КурсыВалютУпр.Валюта = &ВалютаУправленческогоУчета
	|			И втПериоды.ПериодУпр = КурсыВалютУпр.Период
	|			И втПериоды.БазоваяВалютаУпр = КурсыВалютУпр.БазоваяВалюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// %1
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.Организация КАК Организация,
	|	Т.Подразделение КАК Подразделение,
	|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР 
	|		КОГДА ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ВозвратТоваровПоставщику)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыНаОптовыхСкладах)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗадолженностьКлиентов)
	|	КОНЕЦ КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	ВЫРАЗИТЬ(Т.Сумма * КурсыВалют.КоэффициентУпр КАК ЧИСЛО(31,2)) КАК Сумма,
	|	ВЫРАЗИТЬ(Т.СуммаБаланса * КурсыВалют.КоэффициентУпр КАК ЧИСЛО(31,2)) КАК СуммаБаланса,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.ДвиженияБаланса) КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	ЛОЖЬ КАК РасчетСебестоимости,
	|	Т.Сторно КАК Сторно
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	втДанныеРегистраПринятаяТара КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсыВалютПринятаяТара КАК КурсыВалют
	|	ПО Т.Регистратор = КурсыВалют.Регистратор";
	
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", "втДанныеРегистраПринятаяТара");
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", "втПериодыПринятаяТара");
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", "втКурсыВалютПринятаяТара");
	
	ПодготовитьДвиженияНаправлений(Запрос, ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения, "Т.ДокументПоступления.Организация");
	
КонецПроцедуры

// ПрочиеДоходы
Процедура ДобавитьТекстЗапросаПрочиеДоходы(Запрос, ТаблицаДвижений)
	
	ИмяТаблицы = Метаданные.РегистрыНакопления.ПрочиеДоходы.Имя;
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
	
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекстЗапроса =
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Период КАК Период,
		|	Т.Организация КАК Организация,
		|	Т.Подразделение КАК Подразделение,
		|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Т.РасчетСебестоимости КАК РасчетСебестоимости,
		|	Т.Сумма КАК Сумма,
		|	Т.Сторно КАК Сторно
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.Сумма <> 0";
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
	
	КонецЕсли;
	
	ТекстЗапроса =
	"// %1
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.Организация КАК Организация,
	|	Т.Подразделение КАК Подразделение,
	|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ДоходыТекущегоПериода) КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	СУММА(Т.Сумма) КАК Сумма,
	|	СУММА(ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА Т.Сумма ИНАЧЕ -Т.Сумма КОНЕЦ) КАК СуммаБаланса,
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.РасчетКурсовыхРазниц)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.РасчетыСПартнерами)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.РасчетСебестоимости)
	|	КОНЕЦ КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	Т.РасчетСебестоимости КАК РасчетСебестоимости,
	|	Т.Сторно КАК Сторно
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	ПервичныеДвижения КАК Т
	|ГДЕ
	|	Т.Сумма <> 0
	|	И &Отбор
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Регистратор,
	|	Т.ВидДвижения,
	|	Т.Период,
	|	Т.Организация,
	|	Т.Подразделение,
	|	Т.НаправлениеДеятельности,
	|	Т.РасчетСебестоимости,
	|	Т.Сторно";
	
	ПодготовитьДвиженияНаправлений(Запрос, ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения);
	
КонецПроцедуры

// ПрочиеРасходы
Процедура ДобавитьТекстЗапросаПрочиеРасходы(Запрос, ТаблицаДвижений)
	
	ИмяТаблицы = Метаданные.РегистрыНакопления.ПрочиеРасходы.Имя;
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
	
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекстЗапроса =
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Период КАК Период,
		|	Т.Организация КАК Организация,
		|	Т.Подразделение КАК Подразделение,
		|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Т.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	Т.СтатьяРасходов КАК СтатьяРасходов,
		|	Т.АналитикаРасходов КАК АналитикаРасходов,
		|	Т.РасчетСебестоимости КАК РасчетСебестоимости,
		|	Т.Сторно КАК Сторно,
		|	Т.Сумма КАК Сумма,
		|	Т.СуммаУпр КАК СуммаУпр
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.Сумма <> 0 ИЛИ Т.СуммаУпр <> 0";
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
	
	КонецЕсли;
	
	ТекстЗапроса =
	"// %1
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Регистратор КАК Регистратор,
	|	ВложенныйЗапрос.ВидДвижения КАК ВидДвижения,
	|	ВложенныйЗапрос.Период КАК Период,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.Подразделение КАК Подразделение,
	|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВложенныйЗапрос.Статья КАК Статья,
	|	ВложенныйЗапрос.Аналитика КАК Аналитика,
	|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
	|	СУММА(ВложенныйЗапрос.СуммаБаланса) КАК СуммаБаланса,
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ВложенныйЗапрос.Регистратор) = ТИП(Документ.РасчетКурсовыхРазниц)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.РасчетыСПартнерами)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.РасчетСебестоимости)
	|	КОНЕЦ КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	ВложенныйЗапрос.РасчетСебестоимости КАК РасчетСебестоимости,
	|	ВложенныйЗапрос.Сторно КАК Сторно
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	(ВЫБРАТЬ
	|		Т.Регистратор КАК Регистратор,
	|		Т.ВидДвижения КАК ВидДвижения,
	|		Т.Период КАК Период,
	|		Т.Организация КАК Организация,
	|		Т.Подразделение КАК Подразделение,
	|		Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ВЫБОР
	|			КОГДА СтатьиРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов)
	|				ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасходыБудущихПериодов)
	|			ИНАЧЕ
	|				ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасходыТекущегоПериода)
	|		КОНЕЦ КАК Статья,
	|		ВЫБОР
	|			КОГДА СтатьиРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|					И &ИспользоватьВНА2_4 И Т.Период >= &ДатаНачалаУчетаВНА2_4 // Учет ОС версия 2.4
	|				ТОГДА Т.АналитикаРасходов
	|			ИНАЧЕ
	|				НЕОПРЕДЕЛЕНО
	|		КОНЕЦ КАК Аналитика,
	|		Т.Сумма КАК Сумма,
	|		ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -Т.Сумма ИНАЧЕ Т.Сумма КОНЕЦ КАК СуммаБаланса,
	|		Т.РасчетСебестоимости КАК РасчетСебестоимости,
	|		Т.Сторно КАК Сторно
	|
	|	ИЗ
	|		ПервичныеДвижения КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|		ПО Т.СтатьяРасходов = СтатьиРасходов.Ссылка
	|	ГДЕ
	|		Т.Сумма <> 0
	|		И &Отбор
	|	) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.ВидДвижения,
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.Подразделение,
	|	ВложенныйЗапрос.НаправлениеДеятельности,
	|	ВложенныйЗапрос.Статья,
	|	ВложенныйЗапрос.Аналитика,
	|	ВложенныйЗапрос.РасчетСебестоимости,
	|	ВложенныйЗапрос.Сторно
	|";
	
	ПодготовитьДвиженияНаправлений(Запрос, ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения);
	
КонецПроцедуры

// ПартииПрочихРасходов
Процедура ДобавитьТекстЗапросаПартииПрочихРасходов(Запрос, ТаблицаДвижений)
	
	ИмяТаблицы = Метаданные.РегистрыНакопления.ПартииПрочихРасходов.Имя;
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
	
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекстЗапроса =
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Период КАК Период,
		|	Т.Организация КАК Организация,
		|	Т.Подразделение КАК Подразделение,
		|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Т.Сторно КАК Сторно,
		|	Т.Стоимость КАК Стоимость
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.Стоимость <> 0";
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
	
	КонецЕсли;
	
	ТекстЗапроса =
	"// %1
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.Организация КАК Организация,
	|	Т.Подразделение КАК Подразделение,
	|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасходыТекущегоПериода) КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	СУММА(Т.Стоимость) КАК Сумма,
	|	СУММА(ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -Т.Стоимость ИНАЧЕ Т.Стоимость КОНЕЦ) КАК СуммаБаланса,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.РасчетСебестоимости) КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	&РасчетСебестоимости КАК РасчетСебестоимости,
	|	Т.Сторно КАК Сторно
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	ПервичныеДвижения КАК Т
	|ГДЕ
	|	Т.Стоимость <> 0
	// Движения регистра "Партии прочих расходов" отражаются в управленческом балансе только при выключенном партионном учете 2.2.
	// При партионном учете 2.2 все расходы учитываются в регистре "Прочие расходы". 
	|	И (НЕ &ПартионныйУчетВерсии22
	|		ИЛИ &ДатаПереходаНаПартионныйУчетВерсии22 > Т.Период)
	|	И &Отбор
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Регистратор,
	|	Т.ВидДвижения,
	|	Т.Период,
	|	Т.Организация,
	|	Т.Подразделение,
	|	Т.НаправлениеДеятельности,
	|	Т.Сторно";
	
	Запрос.УстановитьПараметр("ПартионныйУчетВерсии22", РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии22());
	Запрос.УстановитьПараметр("ДатаПереходаНаПартионныйУчетВерсии22", РасчетСебестоимостиПовтИсп.ДатаПереходаНаПартионныйУчетВерсии22());
	
	ПодготовитьДвиженияНаправлений(Запрос, ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения);
	
КонецПроцедуры

// СебестоимостьТоваров
Процедура ДобавитьТекстЗапросаСебестоимостьТоваров(Запрос, ТаблицаДвижений)
	
	ИмяТаблицы = Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя;
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
	
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекстЗапроса =
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Период КАК Период,
		|	Т.Организация КАК Организация,
		|	Т.ВидЗапасов КАК ВидЗапасов,
		|	Т.КорВидЗапасов КАК КорВидЗапасов,
		|	Т.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
		|	Т.РазделУчета КАК РазделУчета,
		|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	Т.Стоимость КАК Стоимость,
		|	Т.ДопРасходы КАК ДопРасходы,
		|	Т.Трудозатраты КАК Трудозатраты,
		|	Т.ПостатейныеПостоянныеСНДС КАК ПостатейныеПостоянныеСНДС,
		|	Т.ПостатейныеПеременныеСНДС КАК ПостатейныеПеременныеСНДС,
		|	Т.РасчетСебестоимости КАК РасчетСебестоимости,
		|	Т.Сторно КАК Сторно
		|
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.Стоимость + Т.ДопРасходы + Т.Трудозатраты + Т.ПостатейныеПостоянныеСНДС + Т.ПостатейныеПеременныеСНДС <> 0";
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
	
	КонецЕсли;
	
	ТекстЗапроса =
	"// %1
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
	|			ТОГДА Аналитика.Подразделение
	|		КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
	|			ТОГДА Аналитика.СкладскаяТерритория.Подразделение
	|		КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|			ТОГДА ЕСТЬNULL(Договоры.Подразделение, ЕСТЬNULL(ДоговорыИнтеркампани.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)))
	|		КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.ДоговорКонтрагента)
	|			ТОГДА Аналитика.Договор.Подразделение
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР
	|		КОГДА Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию)
	|			И Т.КорНаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ТОГДА Т.КорНаправлениеДеятельности
	|		КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|				И Т.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеПереработчику)
	|			ТОГДА ЕСТЬNULL(Договоры.НаправлениеДеятельности, ЕСТЬNULL(ДоговорыИнтеркампани.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)))
	|		ИНАЧЕ
	|			ЕСТЬNULL(Назначения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) 
	|				И Аналитика.СкладскаяТерритория.ТипСклада = ЗНАЧЕНИЕ(Перечисление.ТипыСкладов.РозничныйМагазин) ТОГДА
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыВРознице)
	|
	|		КОГДА Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) 
	|				И Аналитика.СкладскаяТерритория.ТипСклада = ЗНАЧЕНИЕ(Перечисление.ТипыСкладов.ОптовыйСклад) ТОГДА
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыНаОптовыхСкладах)
	|
	|		КОГДА Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) 
	|				И Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер) ТОГДА
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыКОформлениюОтчетовКомитенту)
	|
	|		КОГДА Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) ТОГДА
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасходыТекущегоПериода)
	|
	|		КОГДА Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеПереработчику) ТОГДА
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыПереданныеВПереработку)
	|
	|		КОГДА Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
	|			ИЛИ Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение) ТОГДА
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.НезавершенноеПроизводство)
	|
	|		КОГДА Т.РазделУчета В (
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути),
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыВПути)) ТОГДА
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыВПути)
	|
	|		КОГДА Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки) ТОГДА
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.НеотфактурованныеПоставки)
	|
	|		КОГДА Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыПереданныеПартнерам) 
	|				И Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.ДоговорКонтрагента) ТОГДА
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыНаОптовыхСкладах)
	|
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыПереданныеНаКомиссию)
	|	КОНЕЦ КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	СУММА(Т.Стоимость + Т.ДопРасходы + Т.Трудозатраты + Т.ПостатейныеПостоянныеСНДС + Т.ПостатейныеПеременныеСНДС) КАК Сумма,
	|	СУММА(ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -(Т.Стоимость + Т.ДопРасходы + Т.Трудозатраты + Т.ПостатейныеПостоянныеСНДС + Т.ПостатейныеПеременныеСНДС) ИНАЧЕ (Т.Стоимость + Т.ДопРасходы + Т.Трудозатраты + Т.ПостатейныеПостоянныеСНДС + Т.ПостатейныеПеременныеСНДС) КОНЕЦ) КАК СуммаБаланса,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.РасчетСебестоимости) КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	Т.РасчетСебестоимости КАК РасчетСебестоимости,
	|	Т.Сторно КАК Сторно
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	ПервичныеДвижения КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК КорВидыЗапасов
	|	ПО Т.КорВидЗапасов = КорВидыЗапасов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|	ПО Т.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК Назначения
	|	ПО Аналитика.Назначение = Назначения.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК Договоры
	|	ПО КорВидыЗапасов.Договор = Договоры.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыМеждуОрганизациями КАК ДоговорыИнтеркампани
	|	ПО КорВидыЗапасов.Договор = ДоговорыИнтеркампани.Ссылка
	|ГДЕ
	|	НЕ Т.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
	|						ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку))
	|	И (Т.Стоимость + Т.ДопРасходы + Т.Трудозатраты + Т.ПостатейныеПостоянныеСНДС + Т.ПостатейныеПеременныеСНДС) <> 0
	|	И &Отбор
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Регистратор,
	|	Т.ВидДвижения,
	|	Т.Период,
	|	Т.Организация,
	|	ВЫБОР
	|		КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
	|			ТОГДА Аналитика.Подразделение
	|		КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
	|			ТОГДА Аналитика.СкладскаяТерритория.Подразделение
	|		КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|			ТОГДА ЕСТЬNULL(Договоры.Подразделение, ЕСТЬNULL(ДоговорыИнтеркампани.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)))
	|		КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.ДоговорКонтрагента)
	|			ТОГДА Аналитика.Договор.Подразделение
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию)
	|			И Т.КорНаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ТОГДА Т.КорНаправлениеДеятельности
	|		КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|				И Т.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеПереработчику)
	|			ТОГДА ЕСТЬNULL(Договоры.НаправлениеДеятельности, ЕСТЬNULL(ДоговорыИнтеркампани.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)))
	|		ИНАЧЕ
	|			ЕСТЬNULL(Назначения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) 
	|				И Аналитика.СкладскаяТерритория.ТипСклада = ЗНАЧЕНИЕ(Перечисление.ТипыСкладов.РозничныйМагазин) ТОГДА
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыВРознице)
	|
	|		КОГДА Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) 
	|				И Аналитика.СкладскаяТерритория.ТипСклада = ЗНАЧЕНИЕ(Перечисление.ТипыСкладов.ОптовыйСклад) ТОГДА
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыНаОптовыхСкладах)
	|
	|		КОГДА Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) 
	|				И Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер) ТОГДА
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыКОформлениюОтчетовКомитенту)
	|
	|		КОГДА Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) ТОГДА
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасходыТекущегоПериода)
	|
	|		КОГДА Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеПереработчику) ТОГДА
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыПереданныеВПереработку)
	|
	|		КОГДА Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
	|			ИЛИ Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение) ТОГДА
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.НезавершенноеПроизводство)
	|
	|		КОГДА Т.РазделУчета В (
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути),
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыВПути)) ТОГДА
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыВПути)
	|
	|		КОГДА Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки) ТОГДА
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.НеотфактурованныеПоставки)
	|
	|		КОГДА Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыПереданныеПартнерам) 
	|				И Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.ДоговорКонтрагента) ТОГДА
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыНаОптовыхСкладах)
	|
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыПереданныеНаКомиссию)
	|	КОНЕЦ,
	|	Т.РасчетСебестоимости,
	|	Т.Сторно
	|
	|ИМЕЮЩИЕ
	|	СУММА(Т.Стоимость + Т.ДопРасходы + Т.Трудозатраты + Т.ПостатейныеПостоянныеСНДС + Т.ПостатейныеПеременныеСНДС) <> 0";
	
	ПодготовитьДвиженияНаправлений(Запрос, ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения);
	
КонецПроцедуры

// ТоварыКОформлениюОтчетовКомитенту
Процедура ДобавитьТекстЗапросаТоварыКОформлениюОтчетовКомитенту(Запрос, ТаблицаДвижений)
	
	ИмяТаблицы = Метаданные.РегистрыНакопления.ТоварыКОформлениюОтчетовКомитенту.Имя;
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
	
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекстЗапроса =
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Организация КАК Организация,
		|	Т.Период КАК Период,
		|	Т.Валюта КАК Валюта,
		|	Т.ВидЗапасов КАК ВидЗапасов,
		|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	Т.Сторно КАК Сторно,
		|	Т.СуммаВыручки КАК СуммаВыручки,
		|	Т.СуммаВыручкиУпр КАК СуммаВыручкиУпр
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.СуммаВыручки <> 0 ИЛИ Т.СуммаВыручкиУпр <> 0";
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
	
	КонецЕсли;
	
	ТекстЗапроса =
	"// %1
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	Т.Организация КАК Организация,
	|	Т.Период КАК Период,
	|	Т.Валюта КАК Валюта,
	|	ЕСТЬNULL(Договоры.Подразделение, ЕСТЬNULL(ДоговорыИнтеркампани.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))) КАК Подразделение,
	|	ЕСТЬNULL(Договоры.НаправлениеДеятельности, ЕСТЬNULL(ДоговорыИнтеркампани.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))) КАК НаправлениеДеятельности,
	|	Т.Сторно КАК Сторно,
	|	СУММА(Т.СуммаВыручки) КАК СуммаВал,
	|	СУММА(Т.СуммаВыручкиУпр) КАК СуммаУпр,
	|	СУММА(ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА Т.СуммаВыручки ИНАЧЕ -Т.СуммаВыручки КОНЕЦ) КАК СуммаБалансаВал,
	|	СУММА(ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА Т.СуммаВыручкиУпр ИНАЧЕ -Т.СуммаВыручкиУпр КОНЕЦ) КАК СуммаБалансаУпр
	|
	|ПОМЕСТИТЬ втДанныеРегистраТоварыКОформлению
	|ИЗ
	|	ПервичныеДвижения КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
	|	ПО Т.ВидЗапасов = ВидыЗапасов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК Договоры
	|	ПО ВидыЗапасов.Договор = Договоры.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыМеждуОрганизациями КАК ДоговорыИнтеркампани
	|	ПО ВидыЗапасов.Договор = ДоговорыИнтеркампани.Ссылка
	|ГДЕ
	|	(Т.СуммаВыручки <> 0 ИЛИ Т.СуммаВыручкиУпр <> 0)
	|	И &Отбор
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Регистратор,
	|	Т.ВидДвижения,
	|	Т.Организация,
	|	Т.Период,
	|	Т.Валюта,
	|	ВидыЗапасов.Организация,
	|	ЕСТЬNULL(Договоры.Подразделение, ЕСТЬNULL(ДоговорыИнтеркампани.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))),
	|	ЕСТЬNULL(Договоры.НаправлениеДеятельности, ЕСТЬNULL(ДоговорыИнтеркампани.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))),
	|	Т.Сторно
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	КурсыВалют.Валюта КАК Валюта,
	|	КурсыВалют.БазоваяВалюта КАК БазоваяВалюта,
	|	МАКСИМУМ(КурсыВалют.Период) КАК Период,
	|	КурсыВалютУпр.Валюта КАК ВалютаУпр,
	|	КурсыВалютУпр.БазоваяВалюта КАК БазоваяВалютаУпр,
	|	МАКСИМУМ(КурсыВалютУпр.Период) КАК ПериодУпр
	|ПОМЕСТИТЬ втПериодыТоварыКОформлению
	|ИЗ
	|	втДанныеРегистраТоварыКОформлению КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО Т.Валюта = КурсыВалют.Валюта
	|			И Т.Период >= КурсыВалют.Период
	|			И Т.Организация.ВалютаРегламентированногоУчета = КурсыВалют.БазоваяВалюта
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалютУпр
	|		ПО КурсыВалютУпр.Валюта = &ВалютаУправленческогоУчета
	|			И Т.Период >= КурсыВалютУпр.Период
	|			И Т.Организация.ВалютаРегламентированногоУчета = КурсыВалютУпр.БазоваяВалюта
	|СГРУППИРОВАТЬ ПО
	|	Т.Регистратор,
	|	КурсыВалют.Валюта,
	|	КурсыВалют.БазоваяВалюта,
	|	КурсыВалютУпр.Валюта,
	|	КурсыВалютУпр.БазоваяВалюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПериоды.Регистратор КАК Регистратор,
	|	втПериоды.Период КАК Период,
	|	втПериоды.Валюта КАК Валюта,
	|	ЕСТЬNULL(КурсыВалют.КурсЧислитель, 0) / ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1)* ЕСТЬNULL(КурсыВалютУпр.КурсЗнаменатель, 0) / ЕСТЬNULL(КурсыВалютУпр.КурсЧислитель, 1) КАК КоэффициентУпр,
	|	ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) КАК КурсЧислительВалюты,
	|	ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) КАК КурсЗнаменательВалюты,
	|	&ВалютаУправленческогоУчета КАК ВалютаУпр,
	|	ЕСТЬNULL(КурсыВалютУпр.КурсЧислитель, 1) КАК КурсЧислительВалютыУпр,
	|	ЕСТЬNULL(КурсыВалютУпр.КурсЗнаменатель, 1) КАК КурсЗнаменательВалютыУпр
	|ПОМЕСТИТЬ втКурсыВалютТоварыКОформлению
	|ИЗ
	|	втПериодыТоварыКОформлению КАК втПериоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО втПериоды.Валюта = КурсыВалют.Валюта
	|			И втПериоды.Период = КурсыВалют.Период
	|			И втПериоды.БазоваяВалюта = КурсыВалют.БазоваяВалюта
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалютУпр
	|		ПО КурсыВалютУпр.Валюта = &ВалютаУправленческогоУчета
	|			И втПериоды.ПериодУпр = КурсыВалютУпр.Период
	|			И втПериоды.БазоваяВалютаУпр = КурсыВалютУпр.БазоваяВалюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// %1
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.Организация КАК Организация,
	|	Т.Подразделение КАК Подразделение,
	|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыКОформлениюОтчетовКомитенту) КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	ВЫБОР
	|		КОГДА Т.СуммаУпр <> 0
	|			ТОГДА Т.СуммаУпр
	|		ИНАЧЕ ВЫРАЗИТЬ(Т.СуммаВал * КурсыВалют.КоэффициентУпр КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА Т.СуммаБалансаВал <> 0
	|			ТОГДА Т.СуммаБалансаВал
	|		ИНАЧЕ ВЫРАЗИТЬ(Т.СуммаБалансаВал * КурсыВалют.КоэффициентУпр КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаБаланса,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.ДвиженияБаланса) КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	ЛОЖЬ КАК РасчетСебестоимости,
	|	Т.Сторно КАК Сторно
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	втДанныеРегистраТоварыКОформлению КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсыВалютТоварыКОформлению КАК КурсыВалют
	|	ПО Т.Регистратор = КурсыВалют.Регистратор";
	
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", "втДанныеРегистраТоварыКОформлению");
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", "втПериодыТоварыКОформлению");
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", "втКурсыВалютТоварыКОформлению");
	
	ПодготовитьДвиженияНаправлений(Запрос, ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения, "ВидыЗапасов.Организация");
	
КонецПроцедуры

// УслугиКОформлениюОтчетовПринципалу
Процедура ДобавитьТекстЗапросаУслугиКОформлениюОтчетовПринципалу(Запрос, ТаблицаДвижений)
	
	ИмяТаблицы = Метаданные.РегистрыНакопления.УслугиКОформлениюОтчетовПринципалу.Имя;
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
	
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекстЗапроса =
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Период КАК Период,
		|	Т.Валюта КАК Валюта,
		|	Т.Организация КАК Организация,
		|	Т.Соглашение КАК Соглашение,
		|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	Т.Сторно КАК Сторно,
		|	Т.СуммаВыручки КАК СуммаВыручки,
		|	Т.СуммаВыручкиУпр КАК СуммаВыручкиУпр
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.СуммаВыручки <> 0 ИЛИ Т.СуммаВыручкиУпр <> 0";
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
	
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	ВЫБОР
	|		КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.Валюта КАК Валюта,
	|	Т.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
	|			ТОГДА Т.АналитикаУчетаНоменклатуры.Подразделение
	|		КОГДА Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
	|			ТОГДА Т.АналитикаУчетаНоменклатуры.СкладскаяТерритория.Подразделение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|	КОНЕЦ КАК Подразделение,
	|	ЕСТЬNULL(Т.Соглашение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	Т.Сторно КАК Сторно,
	|	СУММА(Т.СуммаВыручки) КАК СуммаВал,
	|	СУММА(Т.СуммаВыручкиУпр) КАК СуммаУпр,
	|	СУММА(ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА Т.СуммаВыручки ИНАЧЕ -Т.СуммаВыручки КОНЕЦ) КАК СуммаБалансаВал,
	|	СУММА(ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА Т.СуммаВыручкиУпр ИНАЧЕ -Т.СуммаВыручкиУпр КОНЕЦ) КАК СуммаБалансаУпр
	|ПОМЕСТИТЬ втДанныеРегистраУслугиКОформлению
	|ИЗ
	|	ПервичныеДвижения КАК Т
	|ГДЕ
	|	(Т.СуммаВыручки <> 0 ИЛИ Т.СуммаВыручкиУпр <> 0)
	|	И &Отбор
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Регистратор,
	|	Т.ВидДвижения,
	|	Т.Период,
	|	Т.Валюта,
	|	Т.Организация,
	|	ВЫБОР
	|		КОГДА Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
	|			ТОГДА Т.АналитикаУчетаНоменклатуры.Подразделение
	|		КОГДА Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
	|			ТОГДА Т.АналитикаУчетаНоменклатуры.СкладскаяТерритория.Подразделение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|	КОНЕЦ,
	|	ЕСТЬNULL(Т.Соглашение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
	|	Т.Сторно
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	КурсыВалют.Валюта КАК Валюта,
	|	КурсыВалют.БазоваяВалюта КАК БазоваяВалюта,
	|	МАКСИМУМ(КурсыВалют.Период) КАК Период,
	|	КурсыВалютУпр.Валюта КАК ВалютаУпр,
	|	КурсыВалютУпр.БазоваяВалюта КАК БазоваяВалютаУпр,
	|	МАКСИМУМ(КурсыВалютУпр.Период) КАК ПериодУпр
	|ПОМЕСТИТЬ втПериодыУслугиКОформлению
	|ИЗ
	|	втДанныеРегистраУслугиКОформлению КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО Т.Валюта = КурсыВалют.Валюта
	|			И Т.Период >= КурсыВалют.Период
	|			И Т.Организация.ВалютаРегламентированногоУчета = КурсыВалют.БазоваяВалюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалютУпр
	|		ПО (КурсыВалютУпр.Валюта = &ВалютаУправленческогоУчета)
	|			И Т.Период >= КурсыВалютУпр.Период
	|			И Т.Организация.ВалютаРегламентированногоУчета = КурсыВалютУпр.БазоваяВалюта
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Регистратор,
	|	КурсыВалют.Валюта,
	|	КурсыВалют.БазоваяВалюта,
	|	КурсыВалютУпр.Валюта,
	|	КурсыВалютУпр.БазоваяВалюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПериоды.Регистратор КАК Регистратор,
	|	втПериоды.Период КАК Период,
	|	втПериоды.Валюта КАК Валюта,
	|	ЕСТЬNULL(КурсыВалют.КурсЧислитель, 0) / ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) * ЕСТЬNULL(КурсыВалютУпр.КурсЗнаменатель, 0) / ЕСТЬNULL(КурсыВалютУпр.КурсЧислитель, 1) КАК КоэффициентУпр,
	|	ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) КАК КурсЧислительВалюты,
	|	ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) КАК КурсЗнаменательВалюты,
	|	&ВалютаУправленческогоУчета КАК ВалютаУпр,
	|	ЕСТЬNULL(КурсыВалютУпр.КурсЧислитель, 1) КАК КурсЧислительВалютыУпр,
	|	ЕСТЬNULL(КурсыВалютУпр.КурсЗнаменатель, 1) КАК КурсЗнаменательВалютыУпр
	|ПОМЕСТИТЬ втКурсыВалютУслугКОформлению
	|ИЗ
	|	втПериодыУслугиКОформлению КАК втПериоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО втПериоды.Валюта = КурсыВалют.Валюта
	|			И втПериоды.Период = КурсыВалют.Период
	|			И втПериоды.БазоваяВалюта = КурсыВалют.БазоваяВалюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалютУпр
	|		ПО (КурсыВалютУпр.Валюта = &ВалютаУправленческогоУчета)
	|			И втПериоды.ПериодУпр = КурсыВалютУпр.Период
	|			И втПериоды.БазоваяВалютаУпр = КурсыВалютУпр.БазоваяВалюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.Организация КАК Организация,
	|	Т.Подразделение КАК Подразделение,
	|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.УслугиКОформлениюОтчетовПринципалу) КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	ВЫБОР
	|		КОГДА Т.СуммаУпр <> 0
	|			ТОГДА Т.СуммаУпр
	|		ИНАЧЕ ВЫРАЗИТЬ(Т.СуммаВал * КурсыВалют.КоэффициентУпр КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА Т.СуммаБалансаУпр <> 0
	|			ТОГДА Т.СуммаУпр
	|		ИНАЧЕ ВЫРАЗИТЬ(Т.СуммаБалансаВал * КурсыВалют.КоэффициентУпр КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаБаланса,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.ДвиженияБаланса) КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	ЛОЖЬ КАК РасчетСебестоимости,
	|	Т.Сторно КАК Сторно
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	втДанныеРегистраУслугиКОформлению КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсыВалютУслугКОформлению КАК КурсыВалют
	|		ПО Т.Регистратор = КурсыВалют.Регистратор";
	
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", "втДанныеРегистраУслугиКОформлению");
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", "втПериодыУслугиКОформлению");
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", "втКурсыВалютУслугКОформлению");
	
	ПодготовитьДвиженияНаправлений(Запрос, ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения, "ВидыЗапасов.Организация");
	
КонецПроцедуры

// ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках
Процедура ДобавитьТекстЗапросаТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках(Запрос, ТаблицаДвижений)
	
	ИмяТаблицы = Метаданные.РегистрыНакопления.ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках.Имя;
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
	
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекстЗапроса =
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Период КАК Период,
		|	Т.Валюта КАК Валюта,
		|	Т.Организация КАК Организация,
		|	Т.Назначение КАК Назначение,
		|	Т.Сторно КАК Сторно,
		|	Т.СуммаСНДС КАК СуммаСНДС,
		|	Т.СуммаУпр КАК СуммаУпр
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.СуммаСНДС <> 0 ИЛИ Т.СуммаУпр <> 0
		|";
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
	
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.Валюта КАК Валюта,
	|	Т.Организация КАК Организация,
	|	Т.Назначение.Договор.Подразделение КАК Подразделение,
	|	Т.Назначение.Договор.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Т.Сторно КАК Сторно,
	|	СУММА(Т.СуммаСНДС) КАК СуммаВал,
	|	СУММА(Т.СуммаУпр) КАК СуммаУпр,
	|	СУММА(Т.СуммаСНДС) КАК СуммаБалансаВал,
	|	СУММА(Т.СуммаУпр) КАК СуммаБалансаУпр
	|ПОМЕСТИТЬ втДанныеРегистраТоварыУслугиКОформлениюОтчетовКомитенту
	|ИЗ
	|	ПервичныеДвижения КАК Т
	|ГДЕ
	|	(Т.СуммаСНДС <> 0 ИЛИ Т.СуммаУпр <> 0)
	|	И &Отбор
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Регистратор,
	|	Т.ВидДвижения,
	|	Т.Период,
	|	Т.Валюта,
	|	Т.Организация,
	|	Т.Назначение.Договор.Подразделение,
	|	Т.Назначение.Договор.НаправлениеДеятельности,
	|	Т.Сторно
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	КурсыВалют.Валюта КАК Валюта,
	|	КурсыВалют.БазоваяВалюта КАК БазоваяВалюта,
	|	МАКСИМУМ(КурсыВалют.Период) КАК Период,
	|	КурсыВалютУпр.Валюта КАК ВалютаУпр,
	|	КурсыВалютУпр.БазоваяВалюта КАК БазоваяВалютаУпр,
	|	МАКСИМУМ(КурсыВалютУпр.Период) КАК ПериодУпр
	|ПОМЕСТИТЬ втПериодыТоварыУслугиКОформлениюОтчетовКомитенту
	|ИЗ
	|	втДанныеРегистраТоварыУслугиКОформлениюОтчетовКомитенту КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО Т.Валюта = КурсыВалют.Валюта
	|			И Т.Период >= КурсыВалют.Период
	|			И Т.Организация.ВалютаРегламентированногоУчета = КурсыВалют.БазоваяВалюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалютУпр
	|		ПО (КурсыВалютУпр.Валюта = &ВалютаУправленческогоУчета)
	|			И Т.Период >= КурсыВалютУпр.Период
	|			И Т.Организация.ВалютаРегламентированногоУчета = КурсыВалютУпр.БазоваяВалюта
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Регистратор,
	|	КурсыВалют.Валюта,
	|	КурсыВалют.БазоваяВалюта,
	|	КурсыВалютУпр.Валюта,
	|	КурсыВалютУпр.БазоваяВалюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПериоды.Регистратор КАК Регистратор,
	|	втПериоды.Период КАК Период,
	|	втПериоды.Валюта КАК Валюта,
	|	ЕСТЬNULL(КурсыВалют.КурсЧислитель, 0) / ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) * ЕСТЬNULL(КурсыВалютУпр.КурсЗнаменатель, 0) / ЕСТЬNULL(КурсыВалютУпр.КурсЧислитель, 1) КАК КоэффициентУпр,
	|	ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) КАК КурсЧислительВалюты,
	|	ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) КАК КурсЗнаменательВалюты,
	|	&ВалютаУправленческогоУчета КАК ВалютаУпр,
	|	ЕСТЬNULL(КурсыВалютУпр.КурсЧислитель, 1) КАК КурсЧислительВалютыУпр,
	|	ЕСТЬNULL(КурсыВалютУпр.КурсЗнаменатель, 1) КАК КурсЗнаменательВалютыУпр
	|ПОМЕСТИТЬ втКурсыВалютТоварыУслугиКОформлениюОтчетовКомитенту
	|ИЗ
	|	втПериодыТоварыУслугиКОформлениюОтчетовКомитенту КАК втПериоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО втПериоды.Валюта = КурсыВалют.Валюта
	|			И втПериоды.Период = КурсыВалют.Период
	|			И втПериоды.БазоваяВалюта = КурсыВалют.БазоваяВалюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалютУпр
	|		ПО (КурсыВалютУпр.Валюта = &ВалютаУправленческогоУчета)
	|			И втПериоды.ПериодУпр = КурсыВалютУпр.Период
	|			И втПериоды.БазоваяВалютаУпр = КурсыВалютУпр.БазоваяВалюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.Организация КАК Организация,
	|	Т.Подразделение КАК Подразделение,
	|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыРаботыКОформлениюОтчетовКомитенту) КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	ВЫБОР
	|		КОГДА Т.СуммаУпр <> 0
	|			ТОГДА Т.СуммаУпр
	|		ИНАЧЕ ВЫРАЗИТЬ(Т.СуммаВал * КурсыВалют.КоэффициентУпр КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА Т.СуммаБалансаУпр <> 0
	|			ТОГДА Т.СуммаУпр
	|		ИНАЧЕ ВЫРАЗИТЬ(Т.СуммаБалансаВал * КурсыВалют.КоэффициентУпр КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаБаланса,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.ДвиженияБаланса) КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	ЛОЖЬ КАК РасчетСебестоимости,
	|	Т.Сторно КАК Сторно
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	втДанныеРегистраТоварыУслугиКОформлениюОтчетовКомитенту КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсыВалютТоварыУслугиКОформлениюОтчетовКомитенту КАК КурсыВалют
	|		ПО Т.Регистратор = КурсыВалют.Регистратор";
	
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", "втДанныеТоварыУслугиКОформлениюОтчетовКомитенту");
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", "втПериодыТоварыУслугиКОформлениюОтчетовКомитенту");
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", "втКурсыВалютТоварыУслугиКОформлениюОтчетовКомитенту");
	
	ПодготовитьДвиженияНаправлений(Запрос, ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения);
	
КонецПроцедуры



Процедура ДобавитьТекстЗапросаПроцентныеРасходыДисконтирования(Запрос, ТаблицаДвижений)
	
	ИмяТаблицы = "ПроцентныеРасходыДисконтирования";
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
	
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекстЗапроса =
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Период КАК Период,
		|	Т.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Т.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Т.СуммаДисконтированияУпр КАК СуммаДисконтированияУпр,
		|	Т.Сторно КАК Сторно
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.СуммаДисконтированияУпр <> 0
		|";
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
	
	КонецЕсли;
	
	ТекстЗапроса =
	"// %1
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.ОбъектРасчетов.Организация КАК Организация,
	|	Т.ОбъектРасчетов.Подразделение КАК Подразделение,
	|	Т.ОбъектРасчетов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасходыДисконтирования) КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	СУММА(Т.СуммаДисконтированияУпр) КАК Сумма,
	|	СУММА(ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА -Т.СуммаДисконтированияУпр ИНАЧЕ Т.СуммаДисконтированияУпр КОНЕЦ) КАК СуммаБаланса,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.РасчетПроцентовПоДисконтированию) КАК ВидИсточника,
	|	&Источник КАК Источник,
	|	ЛОЖЬ КАК РасчетСебестоимости,
	|	Т.Сторно КАК Сторно
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	ПервичныеДвижения КАК Т
	|ГДЕ
	|	Т.СуммаДисконтированияУпр <> 0
	|	И &Отбор
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Период,
	|	Т.Регистратор,
	|	Т.ВидДвижения,
	|	Т.ОбъектРасчетов.Организация,
	|	Т.ОбъектРасчетов.Подразделение,
	|	Т.ОбъектРасчетов.НаправлениеДеятельности,
	|	Т.Сторно";
	
	ДобавитьСоединениеРегистраторов(Запрос, ТекстЗапроса, ПервичныеДвижения);
	ДобавитьОтбор(Запрос, ТекстЗапроса, "Т.ОбъектРасчетов.Организация", "Т.Регистратор");
	ЗаменитьПодстановки(ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения);
	
	ДобавитьЗначениеВПараметр(Запрос, "ТекстыЗапросов", ТекстЗапроса);
	ДобавитьЗначениеВПараметр(Запрос, "ТаблицыДвиженийНаправлений", ИмяТаблицы);
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", ИмяТаблицы);
	
КонецПроцедуры

// ПрочиеАктивыПассивы
Функция ДобавитьТекстЗапросаПрочиеАктивыПассивы(Запрос, ТаблицаДвижений, ТолькоДС = Ложь)
	
	ИмяТаблицы = Метаданные.РегистрыНакопления.ПрочиеАктивыПассивы.Имя;
	ПервичныеДвижения = "РегистрНакопления."+ИмяТаблицы;
	
	Если ТипЗнч(ТаблицаДвижений) = Тип("Строка") Тогда
		ПервичныеДвижения = ТаблицаДвижений;
	ИначеЕсли ТипЗнч(ТаблицаДвижений) = Тип("ТаблицаЗначений") Тогда
	
		Если ТаблицаДвижений.Количество() = 0 Тогда
			Возврат "";
		КонецЕсли;
		
		ТекстЗапроса =
		"// ПервичныеДвижения
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	Т.ВидДвижения КАК ВидДвижения,
		|	Т.Период КАК Период,
		|	Т.Организация КАК Организация,
		|	Т.Подразделение КАК Подразделение,
		|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Т.Статья КАК Статья,
		|	Т.Аналитика КАК Аналитика,
		|	Т.Сумма КАК Сумма,
		|	Т.ВидИсточника КАК ВидИсточника,
		|	Т.Источник КАК Источник,
		|	Т.РасчетСебестоимости КАК РасчетСебестоимости,
		|	Т.Сторно КАК Сторно
		|
		|ПОМЕСТИТЬ ПервичныеДвижения
		|ИЗ
		|	&ИмяТаблицы КАК Т
		|ГДЕ
		|	Т.Сумма <> 0
		|	И Т.ВидИсточника = ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.ПустаяСсылка)";
		
		ПервичныеДвижения = ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
	
	КонецЕсли;
	
	ТекстЗапроса =
	"// %1
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.Организация КАК Организация,
	|	Т.Подразделение КАК Подразделение,
	|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Т.Статья КАК Статья,
	|	Т.Аналитика КАК Аналитика,
	|	СУММА(Т.Сумма) КАК Сумма,
	|	СУММА(ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -Т.Сумма ИНАЧЕ Т.Сумма КОНЕЦ) КАК СуммаБаланса,
	|	Т.ВидИсточника КАК ВидИсточника,
	|	Т.Источник КАК Источник,
	|	Т.РасчетСебестоимости КАК РасчетСебестоимости,
	|	Т.Сторно КАК Сторно,
	|	ВЫБОР КОГДА Т.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПустоеНаправление
	|
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	ПервичныеДвижения КАК Т
	|ГДЕ
	|	Т.Сумма <> 0
	|	И &Отбор
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Регистратор,
	|	Т.ВидДвижения,
	|	Т.Период,
	|	Т.Организация,
	|	Т.Подразделение,
	|	Т.НаправлениеДеятельности,
	|	Т.Статья,
	|	Т.Аналитика,
	|	Т.ВидИсточника,
	|	Т.Источник,
	|	Т.РасчетСебестоимости,
	|	Т.Сторно";
	
	ДобавитьСоединениеРегистраторов(Запрос, ТекстЗапроса, ПервичныеДвижения);
	ДобавитьОтборАктивовПассивов(Запрос, ТекстЗапроса, ТолькоДС);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПервичныеДвижения", ПервичныеДвижения);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%1", ИмяТаблицы);
	Если ТолькоДС Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяТаблицы", ИмяТаблицы);
	
	ДобавитьЗначениеВПараметр(Запрос, "ТекстыЗапросов", ТекстЗапроса);
	ДобавитьЗначениеВПараметр(Запрос, "ТаблицыДвиженийНаправлений", ИмяТаблицы);
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", ИмяТаблицы);
	
КонецФункции

#КонецОбласти

// Параметры:
// 	Запрос - Запрос -
Процедура ДобавитьТекстЗапросаВременныхТаблицКонтроляБаланса(Запрос)
	
	ШаблонЗапросаКонтроляБаланса =
	"// ИсходнаяТаблицаКонтроляБаланса_&ИмяТаблицы
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.Организация КАК Организация,
	|	Т.Подразделение КАК Подразделение,
	|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Т.Статья КАК Статья,
	|	Т.Аналитика КАК Аналитика,
	|	Т.Сумма КАК Сумма,
	|	Т.СуммаБаланса КАК СуммаБаланса,
	|	Т.ВидИсточника КАК ВидИсточника,
	|	ЗНАЧЕНИЕ(Перечисление.ИсточникиУправленческогоБаланса.ИмяИсточника) КАК Источник,
	|	Т.РасчетСебестоимости КАК РасчетСебестоимости,
	|	Т.Сторно КАК Сторно
	|ИЗ
	|	&ИмяТаблицы КАК Т";
	ТекстыЗапросовНаправлений = Новый Массив;
	Для Каждого ИмяТаблицы Из Запрос.Параметры.ТаблицыДвиженийНаправлений Цикл
		Источник = Запрос.Параметры.ИсточникВременнойТаблицы[ИмяТаблицы];
		Если Источник = Неопределено Тогда
			Источник = ИмяТаблицы;
		КонецЕсли;
		Если ИмяТаблицы = "ПрочиеАктивыПассивы" Тогда
			ШаблонЗапросаКонтроляБаланса = СтрЗаменить(ШаблонЗапросаКонтроляБаланса,
				"ЗНАЧЕНИЕ(Перечисление.ИсточникиУправленческогоБаланса.ИмяИсточника)",
				"ВЫБОР КОГДА Т.ВидИсточника = ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.ПустаяСсылка)
				|		ТОГДА НЕОПРЕДЕЛЕНО
				|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ИсточникиУправленческогоБаланса.ИмяИсточника)
				|	КОНЕЦ");
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ШаблонЗапросаКонтроляБаланса, "&ИмяТаблицы", ИмяТаблицы);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяИсточника", Источник);
		Если ТекстыЗапросовНаправлений.Количество() = 0 Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИЗ", "ПОМЕСТИТЬ ДвиженияНаправлений" + Символы.ПС + "ИЗ");
		КонецЕсли;
		ТекстыЗапросовНаправлений.Добавить(ТекстЗапроса);
		
	КонецЦикла;
	ОбъединитьВсе = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|";
	
	ТекстЗапросаДвижений = СтрСоединить(ТекстыЗапросовНаправлений, ОбъединитьВсе);
	
	ДобавитьЗначениеВПараметр(Запрос, "ТекстыЗапросов", ТекстЗапросаДвижений);
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", "ДвиженияНаправлений");
	
КонецПроцедуры

// Параметры:
// 	Запрос - Запрос -
Процедура ДобавитьТекстЗапросаДвиженийАктивовПассивов(Запрос)
	
	ТекстЗапросаДвиженияАктивовПассивов = 
	"ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период КАК Период,
	|	Т.Организация КАК Организация,
	|	Т.Подразделение КАК Подразделение,
	|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Т.Статья КАК Статья,
	|	Т.Аналитика КАК Аналитика,
	|	Т.Сумма КАК Сумма,
	|	Т.ВидИсточника КАК ВидИсточника,
	|	Т.Источник КАК Источник,
	|	Т.РасчетСебестоимости КАК РасчетСебестоимости,
	|	Т.Сторно КАК Сторно,
	|	Т.Регистратор КАК ДокументДвижения
	|,&ВременнаяТаблица
	|ИЗ
	|	ДвиженияНаправлений КАК Т
	|ГДЕ
	|	&Отбор";
	
	ТаблицаРезультата = "";
	Если ЗначениеЗаполнено(Запрос.Параметры.ИмяВременнойТаблицыРезультата) Тогда
		ТекстЗапросаДвиженияАктивовПассивов = ТекстЗапросаДвиженияАктивовПассивов + 
		"
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	Регистратор,
		|	Организация,
		|	Источник
		|";
		ТаблицаРезультата = "ПОМЕСТИТЬ " + Запрос.Параметры.ИмяВременнойТаблицыРезультата;
		ТекстЗапросаДвиженияАктивовПассивов = ТекстЗапросаДвиженияАктивовПассивов + РазделительЗапросов();
	КонецЕсли;
	ТекстЗапросаДвиженияАктивовПассивов = СтрЗаменить(ТекстЗапросаДвиженияАктивовПассивов, ",&ВременнаяТаблица", ТаблицаРезультата);
	
	Если Запрос.Параметры.ВыборкаПоРегистратору Тогда
		ТекстЗапросаДвиженияАктивовПассивов = ТекстЗапросаДвиженияАктивовПассивов + 
		"
		|
		|ИТОГИ ПО
		|	Регистратор
		|";
	КонецЕсли;
	
	ЗаполнениеРегистра = Запрос.Параметры.ЗаполнениеРегистра;
	Если ЗначениеЗаполнено(Запрос.Параметры.ВидОтложенногоРасчета) И НЕ ЗаполнениеРегистра Тогда
		ОтборДвиженийАктивовПассивов =  "(Т.ВидИсточника В (
										|		&ВидОтложенногоРасчета,
										|		ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.РасчетБаланса),
										|		ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.ДвиженияБаланса))
										|	ИЛИ Т.Источник В (&ОбновленныеИсточники))
										|	И НЕ Т.Источник В (
										|		ЗНАЧЕНИЕ(Перечисление.ИсточникиУправленческогоБаланса.ПустаяСсылка),
										|		ЗНАЧЕНИЕ(Перечисление.ИсточникиУправленческогоБаланса.ПрочиеАктивыПассивы))";
		
		ТекстЗапросаДвиженияАктивовПассивов = СтрЗаменить(ТекстЗапросаДвиженияАктивовПассивов,
												"&Отбор",
												ОтборДвиженийАктивовПассивов);
	Иначе
		ТекстЗапросаДвиженияАктивовПассивов = СтрЗаменить(ТекстЗапросаДвиженияАктивовПассивов,"&Отбор","ИСТИНА");
	КонецЕсли;
	
	ДобавитьЗначениеВПараметр(Запрос, "ТекстыЗапросов", ТекстЗапросаДвиженияАктивовПассивов);
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", "РезультатВыборки");
	
КонецПроцедуры

#КонецОбласти
 
#Область ЗаполнениеАктивовПассивов

// Функция возвращает сведения необходимые для запонения регистра ПрочиеАктивыПассивы
//
// Параметры:
//  ТипыРегистраторов - см. КоличествоРегистраторов
//
// Возвращаемое значение:
// 	Структура - Содержит в себе сведения заполнении регистра:
// 	* Описание - Строка - 
// 	* ОбработанныеТипы - см. ОписаниеТиповРегистра
// 	* ВсегоДокументов - Число - Всего документов к отработке
// 	* ОбработаноДокументов - Число -
// 	* Пользователь - СправочникСсылка.Пользователи - 
// 	* ВремяЗапуска - Дата -
//
Функция ИнициализироватьСведенияОЗаполнении(ТипыРегистраторов) Экспорт

	ОбработанныеТипы = ОписаниеТиповРегистра(ТипыРегистраторов);
	
	Сведения = Константы.СведенияОбОбновленииИБ.Получить().Получить();
	Если ТипЗнч(Сведения) <> Тип("Структура") Тогда
		Сведения = Новый Структура;
	КонецЕсли;
	
	Если Сведения.Свойство("ЗаполнениеАктивовПассивов") Тогда
		Процесс = Сведения.ЗаполнениеАктивовПассивов;
	Иначе
		Процесс = Новый Структура("Описание,ВсегоДокументов,ОбработаноДокументов", "", 0, 0);
		Процесс.ВсегоДокументов = ТипыРегистраторов.Итог("ВсегоДокументов");
		Процесс.Вставить("ОбработанныеТипы", ОбработанныеТипы);
		Процесс.Вставить("Пользователь", Пользователи.ТекущийПользователь());
		Процесс.Вставить("ВремяЗапуска", ТекущаяДатаСеанса());
		Сведения.Вставить("ЗаполнениеАктивовПассивов", Процесс);
		Константы.СведенияОбОбновленииИБ.Установить(Новый ХранилищеЗначения(Сведения));
	КонецЕсли;
	
	Возврат Процесс;
	
КонецФункции

Процедура ЗаполнитьДвиженияРегистраПоДокументам(ТипРегистратора, ОбработанныеТипы, НеЗаписыватьДвижения = Ложь)
	
	НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	ИмяДокумента = ТипРегистратора.Имя;
	СинонимДокумента = ТипРегистратора.Синоним;
	ВсегоДокументов = ТипРегистратора.ВсегоДокументов;
	
	Сведения = Константы.СведенияОбОбновленииИБ.Получить().Получить();
	Процесс = Сведения.ЗаполнениеАктивовПассивов; //Структура
	Процесс.Вставить("ИмяДокумента", ИмяДокумента);
	Константы.СведенияОбОбновленииИБ.Установить(Новый ХранилищеЗначения(Сведения));
	
	#Область ИнициализацияЦиклаОбработки
	РазмерПорции = 5000;
	МоментВремени = Новый МоментВремени('39991231');
	Если Процесс.Свойство("МоментВремени") Тогда
		МоментВремени = Процесс.МоментВремени;
	КонецЕсли;
	Запрос = ИнициализироватьЗапросДокументов(ИмяДокумента, РазмерПорции);
	ЗаписаноДвижений = 0;
	ОбработаноДокументов = 0;
	ТребуетсяОбработать = ВсегоДокументов;
	#КонецОбласти
	
	Пока ТребуетсяОбработать > 0 Цикл
		
		МенеджерТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.МенеджерВременныхТаблиц = МенеджерТаблиц;
		Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
		
		РезультатПакета = Запрос.ВыполнитьПакет();
		Результат = РезультатПакета[1]; // РезультатЗапроса - 
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		МоментВремени = Выборка.МоментВремени;
		
		ПараметрыДвижений = Новый Структура;
		ПараметрыДвижений.Вставить("ВидОтложенногоРасчета", Перечисления.ВидыИсточниковДвижений.РасчетБаланса);
		ПараметрыДвижений.Вставить("ДокументыКОтражению", МенеджерТаблиц);
		ПараметрыДвижений.Вставить("ВыборкаПоРегистратору", Истина);
		ЗапросДвижений = ЗапросДвижений(ПараметрыДвижений);
		Если ЗапросДвижений = Неопределено Тогда
			ТребуетсяОбработать = ТребуетсяОбработать - РазмерПорции;
			Продолжить;
		КонецЕсли;
		
		Обработано = ЗаписатьСформированныеДвижения(ИмяДокумента, ЗапросДвижений, НеЗаписыватьДвижения);
		ОбработаноДокументов = ОбработаноДокументов + Обработано.Документов;
		ЗаписаноДвижений = ЗаписаноДвижений + Обработано.Движений;
		
		Процесс.Описание = СтрШаблон("%1 (%2 из %3)", СинонимДокумента, ОбработаноДокументов, ВсегоДокументов);
		Процесс.Вставить("МоментВремени", МоментВремени);
		Константы.СведенияОбОбновленииИБ.Установить(Новый ХранилищеЗначения(Сведения));
		
		ТребуетсяОбработать = ТребуетсяОбработать - РазмерПорции;
		
	КонецЦикла;
	
	#Область ФиксацияРезультатовОбработки
	Процесс.Удалить("МоментВремени");
	
	ОбработанныйТип = ОбработанныеТипы.Добавить();
	ЗаполнитьЗначенияСвойств(ОбработанныйТип, ТипРегистратора);
	ОбработанныйТип.ВремяЗаполнения = (ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЗамера) / 1000;
	ОбработанныйТип.ВсегоДвижений = ЗаписаноДвижений;
	ОбработанныйТип.ВсегоДокументов = ОбработаноДокументов;
	ОбработанныйТип.ВсегоЗатрачено = ПредставлениеВремени(ОбработанныйТип.ВремяЗаполнения);
	
	Процесс.ОбработаноДокументов = Процесс.ОбработаноДокументов + ВсегоДокументов;
	Процесс.Описание = СтрШаблон("%1 (%2 из %3)", СинонимДокумента, ОбработаноДокументов, ВсегоДокументов);
	Процесс.Вставить("ОбработанныеТипы", ОбработанныеТипы);
	Константы.СведенияОбОбновленииИБ.Установить(Новый ХранилищеЗначения(Сведения));
	#КонецОбласти
	
КонецПроцедуры

Функция ИнициализироватьЗапросДокументов(ИмяДокумента, РазмерПорции)
	
	ШаблонЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	Т.Ссылка КАК Регистратор,
	|	Т.МоментВремени
	|ПОМЕСТИТЬ ДокументыКОтражению
	|ИЗ
	|	&ИмяДокумента КАК Т
	|ГДЕ
	|	Т.Проведен
	|	И Т.МоментВремени <= &МоментВремени
	|
	|УПОРЯДОЧИТЬ ПО
	|	Т.МоментВремени УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДокументыКОтражению.МоментВремени КАК МоментВремени
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|УПОРЯДОЧИТЬ ПО
	|	МоментВремени";
	ТекстЗапроса = СтрЗаменить(ШаблонЗапроса, "1000", Формат(РазмерПорции, "ЧГ=0"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяДокумента", ИмяДокумента);
	Запрос = Новый Запрос(ТекстЗапроса);
	Возврат Запрос;
	
КонецФункции

Функция ЗаписатьСформированныеДвижения(Знач ИмяДокумента, ЗапросДвижений, НеЗаписыватьДвижения = Ложь)
	
	ЗаписыватьДвижения = НЕ НеЗаписыватьДвижения;
	ЗаписаноДвижений = 0;
	ОбработаноДокументов = 0;
	Если СтрНайти(ИмяДокумента, ".") > 0 Тогда
		ИмяДокумента = СтрРазделить(ИмяДокумента, ".")[1];
	КонецЕсли;
	МетаданныеРегистратора = Метаданные.Документы[ИмяДокумента];
	ВыборкаПоДокументу = ЗапросДвижений.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоДокументу.Следующий() Цикл

		Регистратор = ВыборкаПоДокументу.Регистратор;
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("Документ." + ИмяДокумента);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Регистратор);
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПрочиеАктивыПассивы.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", Регистратор);
			
			Блокировка.Заблокировать();
			
			// Записать правильные движения
			Набор = РегистрыНакопления.ПрочиеАктивыПассивы.СоздатьНаборЗаписей();
			Набор.Отбор.Регистратор.Установить(Регистратор);
			
			Выборка = ВыборкаПоДокументу.Выбрать();
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(Набор.Добавить(), Выборка);
				ЗаписаноДвижений = ЗаписаноДвижений + 1;
			КонецЦикла;
			Если ЗаписыватьДвижения Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
			КонецЕсли;
			ОбработаноДокументов = ОбработаноДокументов + 1;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать %Документ% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", Регистратор);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации_ЗаполнениеРегистра(),
									УровеньЖурналаРегистрации.Ошибка,
									МетаданныеРегистратора,
									Регистратор,
									ТекстСообщения);
			
		КонецПопытки;

	КонецЦикла;
	
	Возврат Новый Структура("Документов,Движений",ОбработаноДокументов,ЗаписаноДвижений);
	
КонецФункции

#КонецОбласти

#Область ПрочиеПроцедурыИФункцииУправленческогоБаланса
// Собирает все необходимое для формирования движений по управленческому балансу
// Поцедура формирования движений может быть вызвана:
//    - при онлайн проведении документа
//    - после выполнения отложенных расчетов.
//
// Параметры:
//  ИсходныеДанные  - Структура - минимально необходимые исходные даные для формирования движений
//                 при онлайн проведении это все таблицы движений для записи в наборы регистров проводимого документа
//                 при оффлайн формировании имя отложенного расчета вызвавшего формирование 
//                     и имена регистров строкой по которым отложенный расчет изменил движения для актуализации их в
//                     управленческом балансе
//  ПараметрыОтладки  - Структура - разные отладочные параметры см. в коде.
//
// Возвращаемое значение:
//   Структура - Структура параметров в которой собрано все необходимое для формирования движений взависимости от
//               контекста вызова.
//
Функция ИнициализироватьПараметрыДвижений(ПараметрыДвижений, ПараметрыОтладки = Неопределено)
	
	НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	СсылкаДокумента = Неопределено;
	ПараметрыДвижений.Свойство("Ссылка", СсылкаДокумента);
	
	ВидОтложенногоРасчета = Неопределено;
	ПараметрыДвижений.Свойство("ВидОтложенногоРасчета", ВидОтложенногоРасчета);
	ПараметрыДвижений.Вставить("ВидОтложенногоРасчета", ВидОтложенногоРасчета);
	ЗаполнениеРегистра = ВидОтложенногоРасчета = Перечисления.ВидыИсточниковДвижений.РасчетБаланса;
	ПараметрыДвижений.Вставить("ЗаполнениеРегистра", ЗаполнениеРегистра);

	#Область ДокументыКОтражению
	// ДокументыКОтражению к отражению могут быть заданы Ссылкой, МассивомСсылок, ВременнойТаблицей ДокументыКОтражению с
	// полем Регистратор.
	МенеджерТаблиц = Новый МенеджерВременныхТаблиц;
	МассивРегистраторов = Неопределено;
	ДокументыКОтражению = Неопределено;
	ТаблицаРегистраторов = Неопределено;
	Если ПараметрыДвижений.Свойство("ДокументыКОтражению", ДокументыКОтражению) Тогда
		Если ТипЗнч(ДокументыКОтражению) = Тип("МенеджерВременныхТаблиц") Тогда
			МенеджерТаблиц = ДокументыКОтражению;
			Если ПараметрыДвижений.Свойство("ТаблицаРегистраторов") Тогда
				ТаблицаРегистраторов = ПараметрыДвижений.ТаблицаРегистраторов;
			Иначе
				ТаблицаРегистраторов = ОписаниеТаблицыРегистраторов();
			КонецЕсли;
		Иначе
			МассивРегистраторов = ДокументыКОтражению;
		КонецЕсли;
	КонецЕсли;
	#КонецОбласти
	
	Если ПараметрыДвижений.Свойство("МенеджерВременныхТаблиц") Тогда
		МенеджерТаблиц = ПараметрыДвижений.МенеджерВременныхТаблиц;
	КонецЕсли;
	
	#Область ТаблицыДляДвижений
	Если НЕ ПараметрыДвижений.Свойство("ТаблицыДляДвижений") Тогда
		ПараметрыДвижений.Вставить("ТаблицыДляДвижений", ТаблицыОтложенногоРасчета(ВидОтложенногоРасчета));
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыДвижений.ТаблицыДляДвижений) = Тип("Строка") Тогда
		ТаблицыДляДвижений = Новый Структура(ПараметрыДвижений.ТаблицыДляДвижений);
		ПараметрыДвижений.Вставить("ТаблицыДляДвижений", ТаблицыДляДвижений);
	КонецЕсли;
	#КонецОбласти

	#Область Отладка
	ПараметрыДвижений.Вставить("Записывать", Истина);
	Если ТипЗнч(ПараметрыОтладки) = Тип("Структура") Тогда
		
		Если ПараметрыОтладки.Свойство("НеЗаписыватьДвижения") Тогда
			ПараметрыДвижений.Записывать = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	#КонецОбласти

	#Область ЗапросДвижений
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерТаблиц;
	Запрос.УстановитьПараметр("Регистратор", СсылкаДокумента);
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета", Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ТаблицыДвиженийНаправлений", Новый Массив);
	Запрос.УстановитьПараметр("ВсеТаблицы", Новый Массив);
	Запрос.УстановитьПараметр("ИсточникВременнойТаблицы", Новый Соответствие);
	Запрос.УстановитьПараметр("НовыеВзаиморасчеты", Константы.НоваяАрхитектураВзаиморасчетов.Получить());
	Запрос.УстановитьПараметр("ДатаНачалаУчетаВНА2_4", '00010101');
	Запрос.УстановитьПараметр("ИспользоватьВНА2_4", Ложь);
	#Область УстановкаПереданныхПараметров
	Если МассивРегистраторов <> Неопределено Тогда
		Запрос.УстановитьПараметр("МассивРегистраторов", МассивРегистраторов);
	КонецЕсли;
	Если ТаблицаРегистраторов <> Неопределено Тогда
		Запрос.УстановитьПараметр("ТаблицаРегистраторов", ТаблицаРегистраторов);
	КонецЕсли;
	МассивОрганизаций = Неопределено;
	Если ПараметрыДвижений.Свойство("МассивОрганизаций", МассивОрганизаций) Тогда
		Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	КонецЕсли;
	Период = Неопределено;
	Если ПараметрыДвижений.Свойство("Период", Период) Тогда
		Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Период));
		Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Период));
	КонецЕсли;
	ЭтоРасчетСебестоимости = ВидОтложенногоРасчета = Перечисления.ВидыИсточниковДвижений.РасчетСебестоимости;
	Запрос.УстановитьПараметр("РасчетСебестоимости", ЭтоРасчетСебестоимости);
	
	Запрос.УстановитьПараметр("ВидОтложенногоРасчета", ВидОтложенногоРасчета);
	Запрос.УстановитьПараметр("ОбновленныеИсточники", ОбновленныеИсточники(ВидОтложенногоРасчета));
	Запрос.УстановитьПараметр("ЗаполнениеРегистра", ЗаполнениеРегистра);
	Запрос.УстановитьПараметр("ВыборкаПоРегистратору", ПараметрыДвижений.Свойство("ВыборкаПоРегистратору"));
	
	ИмяВременнойТаблицыРезультата = Неопределено;
	ПараметрыДвижений.Свойство("ИмяВременнойТаблицыРезультата", ИмяВременнойТаблицыРезультата);
	Запрос.УстановитьПараметр("ИмяВременнойТаблицыРезультата", ИмяВременнойТаблицыРезультата);
	#КонецОбласти
	#КонецОбласти

	Замеры = Новый Структура;
	Протокол = Новый Структура("Замеры", Замеры);
	ПараметрыДвижений.Вставить("Протокол", Протокол);
	ПараметрыДвижений.Вставить("ЗапросДвижений", Запрос);
	
	ДобавитьЗамер(ПараметрыДвижений.Протокол.Замеры,"ИнициализацияПараметровДвижений",НачалоЗамера);
	
	Возврат ПараметрыДвижений;
	
КонецФункции

// Возвращает выгруженные в таблицы значений движения наборов записей
// 
// Параметры:
// 	СсылкаНаДокумент - ДокументСсылка, Структура - Описание
// 	ДвиженияДокумента - КоллекцияДвижений - Описание
// Возвращаемое значение:
// 	Структура - Описание:
// * ВремяВыгрузкиРасчетовПоДокументам - Число -
Функция ВыгрузитьНаборыДвижений(СсылкаНаДокумент, ДвиженияДокумента) Экспорт
	
	ТаблицыДвижений = Новый Структура;
	НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	ПустойВидИсточника = Перечисления.ВидыИсточниковДвижений.ПустаяСсылка();
	ВыгруженыРасчетыСКлиентами = Ложь;
	ВыгруженыРасчетыСПоставщиками = Ложь;
	ЭтоНовый = Ложь;
	Если ДвиженияДокумента.ПрочиеАктивыПассивы.ДополнительныеСвойства.Свойство("СвойстваДокумента") Тогда
		ЭтоНовый = ДвиженияДокумента.ПрочиеАктивыПассивы.ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый;
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		ВыгрузитьРасчетыПоСрокам(СсылкаНаДокумент, ТаблицыДвижений);
		ВыгруженыРасчетыСКлиентами = ТаблицыДвижений.Свойство("РасчетыСКлиентамиПоСрокам");
		ВыгруженыРасчетыСПоставщиками = ТаблицыДвижений.Свойство("РасчетыСПоставщикамиПоСрокам");
	ИначеЕсли НЕ ЭтоНовый Тогда
		ВыгруженыРасчетыСКлиентами = ВыгрузитьРасчетыПоДокументам(ТаблицыДвижений, ДвиженияДокумента, "РасчетыСКлиентами");
		ВыгруженыРасчетыСПоставщиками = ВыгрузитьРасчетыПоДокументам(ТаблицыДвижений, ДвиженияДокумента, "РасчетыСПоставщиками");
	КонецЕсли;
	ВремяВыгрузкиРасчетовПоДокументам = (ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЗамера) / 1000;
	ТаблицыДвижений.Вставить("ВремяВыгрузкиРасчетовПоДокументам", ВремяВыгрузкиРасчетовПоДокументам);
	
	БалансовыеРегистры = БалансовыеРегистры();
	Если НЕ ВыгруженыРасчетыСКлиентами Тогда
		БалансовыеРегистры.Вставить("РасчетыСКлиентами");
	КонецЕсли;
	Если НЕ ВыгруженыРасчетыСПоставщиками Тогда
		БалансовыеРегистры.Вставить("РасчетыСПоставщиками");
	КонецЕсли;
	Для Каждого Набор Из ДвиженияДокумента Цикл
		
		ИмяНабора = Строка(Набор);
		Если СтрНайти(ИмяНабора, "РегистрНакопления") = 0 Тогда
			Продолжить;
		КонецЕсли;
		ИмяНабора = СтрЗаменить(ИмяНабора, "РегистрНакопленияНаборЗаписей.", "");
		
		БалансовыйРегистр = БалансовыеРегистры.Свойство(ИмяНабора);
		Если НЕ БалансовыйРегистр Тогда
			Продолжить;
		КонецЕсли;
		
		Если Набор.Количество() = 0 И НЕ Набор.Записывать Тогда
			Набор.Прочитать();
		КонецЕсли;
		
		Если Набор.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ИмяНабора = "ПрочиеАктивыПассивы" Тогда
			ОтборДвижений = Новый Структура("ВидИсточника", ПустойВидИсточника);
			ТаблицаДвижений = Набор.Выгрузить().Скопировать(ОтборДвижений);
		Иначе
			ТаблицаДвижений = Набор.Выгрузить();
		КонецЕсли;
		
		ТаблицыДвижений.Вставить(ИмяНабора, ТаблицаДвижений);
	КонецЦикла;
	
	Возврат ТаблицыДвижений;
	
КонецФункции

Функция ВыгрузитьРасчетыПоДокументам(ТаблицыДвижений, Движения, Знач ИмяНабора)
	
	ИмяНабора = ИмяНабора + "ПоДокументам";
	НаборРасчетовПоДокументам = Движения.Найти(ИмяНабора);
	Если НаборРасчетовПоДокументам <> Неопределено Тогда
		Если НЕ НаборРасчетовПоДокументам.Записывать Тогда
			НаборРасчетовПоДокументам.Прочитать();
			Если НаборРасчетовПоДокументам.Количество() > 0 Тогда
				ТаблицыДвижений.Вставить(ИмяНабора,НаборРасчетовПоДокументам.Выгрузить());
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат ТаблицыДвижений.Свойство(ИмяНабора);
	
КонецФункции

Процедура ВыгрузитьРасчетыПоСрокам(ДокументРегистратор, ТаблицыДвижений)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЛОЖЬ КАК РасчетыСКлиентами,
	|	Т.*
	|ПОМЕСТИТЬ втРасчетыПоСрокам
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК Т
	|ГДЕ
	|	Т.ДокументРегистратор = &ДокументРегистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА КАК РасчетыСКлиентами,
	|	Т.*
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Т
	|ГДЕ
	|	Т.ДокументРегистратор = &ДокументРегистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.ДокументРегистратор КАК Регистратор,
	|	Т.Регистратор КАК РегистраторРасчетов,
	|	Т.*
	|ИЗ
	|	втРасчетыПоСрокам КАК Т
	|ИТОГИ ПО
	|	Т.РасчетыСКлиентами";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДокументРегистратор", ДокументРегистратор);
	
	ВидыРасчетов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВидыРасчетов.Следующий() Цикл
		
		Если ВидыРасчетов.РасчетыСКлиентами Тогда
			ИмяРегистра = "РасчетыСКлиентамиПоСрокам";
			Набор = РегистрыНакопления.РасчетыСКлиентамиПоСрокам.СоздатьНаборЗаписей();
		Иначе
			ИмяРегистра = "РасчетыСПоставщикамиПоСрокам";
			Набор = РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.СоздатьНаборЗаписей();
		КонецЕсли;
		
		Движения = Набор.Выгрузить();
		Движения.Колонки.Регистратор.Имя = "РегистраторРасчетов";
		Движения.Колонки.ДокументРегистратор.Имя = "Регистратор";
		Выборка = ВидыРасчетов.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = Движения.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		КонецЦикла;
		ТаблицыДвижений.Вставить(ИмяРегистра, Движения);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ЕстьРасчетыПоДокументам(ТаблицыДвижений, ВидРасчетов)
	
	Результат = Ложь;
	ИмяНабора = ВидРасчетов + "ПоДокументам";
	Если ТаблицыДвижений.Свойство(ИмяНабора) Тогда
		Результат = Истина;
		Если ТипЗнч(ТаблицыДвижений[ИмяНабора]) = Тип("ТаблицаЗначений") Тогда
			Результат = ТаблицыДвижений[ИмяНабора].Количество() > 0 
		КонецЕсли;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьОтбор(Запрос, ТекстЗапроса, ПолеОрганизации = "", ПолеРегистратора = "")
	
	ТекстОтбора = "";
	Если Запрос.Параметры.Свойство("МассивРегистраторов") Тогда
		ТекстОтбора = ТекстОтбора + "И Т.Регистратор В (&МассивРегистраторов)";
		Если НЕ ПустаяСтрока(ПолеРегистратора) Тогда
			ТекстОтбора = СтрЗаменить(ТекстОтбора, "Т.Регистратор", ПолеРегистратора);
		КонецЕсли;
	КонецЕсли;
	
	ПСТаб = Символы.ПС+Символы.Таб;
	Если Запрос.Параметры.Свойство("МассивОрганизаций") Тогда
		ТекстОтбора = ТекстОтбора + ?(ТекстОтбора = "", "", ПСТаб) 
					+ "И Т.Организация В (&МассивОрганизаций)";
		Если НЕ ПустаяСтрока(ПолеОрганизации) Тогда
			ТекстОтбора = СтрЗаменить(ТекстОтбора, "Т.Организация", ПолеОрганизации);
		КонецЕсли;
	КонецЕсли;
	
	Если Запрос.Параметры.Свойство("НачалоПериода") Тогда
		ТекстОтбора = ТекстОтбора + ?(ТекстОтбора = "", "", ПСТаб)
					+ "И Т.Период МЕЖДУ &НачалоПериода И &КонецПериода";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"И &Отбор",ТекстОтбора);
	
КонецПроцедуры

Процедура ДобавитьОтборАктивовПассивов(Запрос, ТекстЗапроса, ТолькоДС = Ложь)
	
	Если Запрос.Параметры.ЗаполнениеРегистра Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
									"И &Отбор",
									"И Т.ВидИсточника = ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.ПустаяСсылка)
									|	И &Отбор");
	ИначеЕсли ЗначениеЗаполнено(Запрос.Параметры.ВидОтложенногоРасчета) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
									"И &Отбор",
									"И НЕ Т.ВидИсточника В (
									|		&ВидОтложенногоРасчета,
									|		ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.РасчетБаланса))
									|	И НЕ Т.Источник В (&ОбновленныеИсточники)
									|	И &Отбор");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
									"И &Отбор",
									"И Т.ВидИсточника = ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.ПустаяСсылка)
									|	И &Отбор");
	КонецЕсли;
	
	Если ТолькоДС Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
									"И &Отбор",
									"И Т.Статья В (
									|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ДенежныеСредстваБезналичные),
									|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ДенежныеСредстваНаличные))
									|	И &Отбор");
	КонецЕсли;
	
	ДобавитьОтбор(Запрос, ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
// 	Запрос - Запрос -
// 	ТекстЗапроса - Строка - Описание
// 	ИмяТаблицы - Строка - Описание
// 	ТаблицаДвижений - ТаблицаЗначений - Описание
Функция ПодготовитьПервичныеДвижения(Запрос, ТекстЗапроса, ИмяТаблицы, ТаблицаДвижений);
	
	ПервичныеДвижения = "Первичные" + ИмяТаблицы;
	Запрос.УстановитьПараметр(ИмяТаблицы, ТаблицаДвижений);
	ЗаменитьПодстановки(ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения);
	ДобавитьЗначениеВПараметр(Запрос, "ТекстыЗапросов", ТекстЗапроса);
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", ПервичныеДвижения);
	
	Возврат ПервичныеДвижения;
	
КонецФункции

// Параметры:
// 	Запрос - Запрос - 
// 	ТекстЗапроса - Строка - 
// 	ИмяТаблицы - Строка - 
// 	ПервичныеДвижения - Строка -
Процедура ПодготовитьДвиженияНаправлений(Запрос, ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения, ПолеОрганизации = "")
	
	ДобавитьСоединениеРегистраторов(Запрос, ТекстЗапроса, ПервичныеДвижения);
	ДобавитьОтбор(Запрос, ТекстЗапроса, ПолеОрганизации);
	ЗаменитьПодстановки(ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения);
	
	ДобавитьЗначениеВПараметр(Запрос, "ТекстыЗапросов", ТекстЗапроса);
	ДобавитьЗначениеВПараметр(Запрос, "ТаблицыДвиженийНаправлений", ИмяТаблицы);
	ДобавитьЗначениеВПараметр(Запрос, "ВсеТаблицы", ИмяТаблицы);
	
КонецПроцедуры

// Параметры:
// 	Запрос - Запрос - 
// 	ИмяПараметра - Строка - 
// 	НовоеЗначение - Строка - 
Процедура ДобавитьЗначениеВПараметр(Запрос, ИмяПараметра, НовоеЗначение)
	
	Параметр = Запрос.Параметры[ИмяПараметра]; // Массив из Строки -
	Параметр.Добавить(НовоеЗначение);
	
КонецПроцедуры

// Параметры:
// 	Запрос - Запрос - 
// 	ТекстЗапроса - Строка - Описание
// 	ПервичныеДвижения - Строка - Описание
Процедура ДобавитьСоединениеРегистраторов(Запрос, ТекстЗапроса, ПервичныеДвижения)
	
	Если Запрос.Параметры.Свойство("ТаблицаРегистраторов") Тогда
		втРегистраторов = Запрос.Параметры.ТаблицаРегистраторов;
		ШаблонСоединения = "
		|	%1 КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ %2 КАК ДокументыКОтражению
		|	ПО Т.Регистратор = ДокументыКОтражению.%3";
		
		ТекстСоединения = СтрШаблон(ШаблонСоединения, ПервичныеДвижения, втРегистраторов.ИмяТаблицы, втРегистраторов.ПолеРегистратора);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПервичныеДвижения КАК Т", ТекстСоединения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаменитьПодстановки(ТекстЗапроса, ИмяТаблицы, ПервичныеДвижения)
	
	Источник = СтрШаблон("ЗНАЧЕНИЕ(Перечисление.ИсточникиУправленческогоБаланса.%1)", ИмяТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Источник", Источник);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяТаблицы", ИмяТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%1", ИмяТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПервичныеДвижения", ПервичныеДвижения);
	
КонецПроцедуры

Функция ТаблицыОтложенногоРасчета(ВидОтложенногоРасчета)
	
	Таблицы = Новый Структура;
	МетаданныеРегистров = Метаданные.РегистрыНакопления;
	Если ВидОтложенногоРасчета = Перечисления.ВидыИсточниковДвижений.РасчетСебестоимости Тогда
		Таблицы.Вставить(МетаданныеРегистров.ПрочиеАктивыПассивы.Имя, "ВТАктивыПассивы");
		Таблицы.Вставить(МетаданныеРегистров.ПрочиеДоходы.Имя, "ВТПрочиеДоходы");
		Таблицы.Вставить(МетаданныеРегистров.ПрочиеРасходы.Имя, "ВТПрочиеРасходы");
		Таблицы.Вставить(МетаданныеРегистров.ПартииПрочихРасходов.Имя, "ВТПартииПрочихРасходов");
		Таблицы.Вставить(МетаданныеРегистров.СебестоимостьТоваров.Имя, "ВТСебестоимость");
		
	ИначеЕсли ВидОтложенногоРасчета = Перечисления.ВидыИсточниковДвижений.РасчетыСПартнерами Тогда
		Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
			Таблицы.Вставить(МетаданныеРегистров.РасчетыСКлиентамиПоСрокам.Имя);
			Таблицы.Вставить(МетаданныеРегистров.РасчетыСПоставщикамиПоСрокам.Имя);
		Иначе
			Таблицы.Вставить(МетаданныеРегистров.РасчетыСКлиентамиПоДокументам.Имя);
			Таблицы.Вставить(МетаданныеРегистров.РасчетыСПоставщикамиПоДокументам.Имя);
		КонецЕсли;
		Таблицы.Вставить(МетаданныеРегистров.ПрочиеАктивыПассивы.Имя);
		Таблицы.Вставить(МетаданныеРегистров.ПрочиеРасходы.Имя);
		Таблицы.Вставить(МетаданныеРегистров.ПартииПрочихРасходов.Имя);
		Таблицы.Вставить(МетаданныеРегистров.СебестоимостьТоваров.Имя);
		
	ИначеЕсли ВидОтложенногоРасчета = Перечисления.ВидыИсточниковДвижений.РасчетБаланса Тогда
		Таблицы = БалансовыеРегистры();
		
	КонецЕсли;
	Возврат Таблицы;
	
КонецФункции

Функция ОбновленныеИсточники(ВидОтложенногоРасчета)
		
	Результат = Новый Массив;
	Источники = Перечисления.ИсточникиУправленческогоБаланса;
	Если ВидОтложенногоРасчета = Перечисления.ВидыИсточниковДвижений.РасчетыСПартнерами Тогда
		Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
			Результат.Добавить(Источники.РасчетыСКлиентамиПоСрокам);
			Результат.Добавить(Источники.РасчетыСПоставщикамиПоСрокам);
		Иначе
			Результат.Добавить(Источники.РасчетыСКлиентамиПоДокументам);
			Результат.Добавить(Источники.РасчетыСПоставщикамиПоДокументам);
		КонецЕсли;
		Результат.Добавить(Источники.ПрочиеРасходы);
		Результат.Добавить(Источники.ПартииПрочихРасходов);
		Результат.Добавить(Источники.СебестоимостьТоваров);
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

Функция ТекстЗапросаРеквизитаОбъектаРасчетов(
			РеквизИсточника = "Т.ОбъектРасчетов",
			РеквизитОбъектаРасчетов = "Договор",
			ТипыРеквизита = "ОпределяемыеТипы.ОбъектРасчетов") Экспорт
	
	ТекстПоля = "ВЫБОР" + Символы.ПС;
	ШаблонУсловия = "		КОГДА ТИПЗНАЧЕНИЯ(%1) = ТИП(%2)
					|			ТОГДА ВЫРАЗИТЬ(%1 КАК %2).%3";
	ШаблонДоговора = "		КОГДА ТИПЗНАЧЕНИЯ(%1) = ТИП(%2)
					 |			ТОГДА %1";
	УсловияВыбора = Новый Массив;
	ТипыДоговоров = Метаданные.Справочники.КлючиАналитикиУчетаПоПартнерам.Реквизиты.Договор.Тип.Типы();
	ТипыДоговоров.Добавить(Тип("СправочникСсылка.ПодарочныеСертификаты"));
	
	Если ТипЗнч(ТипыРеквизита) = Тип("Строка") Тогда
		ТипыОбъектовРасчета = ОбщегоНазначенияУТ.МетаданныеПоИмени(ТипыРеквизита).Тип.Типы();
	ИначеЕсли ТипЗнч(ТипыРеквизита) = Тип("Массив") Тогда
		ТипыОбъектовРасчета = ТипыРеквизита;
	КонецЕсли;
	Для Каждого ТипОбъектаРасчета Из ТипыОбъектовРасчета Цикл
		
		Если ТипОбъектаРасчета = Тип("NULL") ИЛИ ТипОбъектаРасчета = Тип("НЕОПРЕДЕЛЕНО") Тогда
			Продолжить;
		КонецЕсли;
		
		ПолноеИмяТипа = Метаданные.НайтиПоТипу(ТипОбъектаРасчета).ПолноеИмя();
		Если СтрНайти(ПолноеИмяТипа, "Перечисление") > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ВРег(РеквизитОбъектаРасчетов) = "ДОГОВОР" И ТипыДоговоров.Найти(ТипОбъектаРасчета) <> Неопределено Тогда
			ТекстУсловия = СтрШаблон(ШаблонДоговора, РеквизИсточника, ПолноеИмяТипа);
		Иначе
			ТекстУсловия = СтрШаблон(ШаблонУсловия, РеквизИсточника, ПолноеИмяТипа, РеквизитОбъектаРасчетов);
		КонецЕсли;
		УсловияВыбора.Добавить(ТекстУсловия);
		
	КонецЦикла;
	ТекстУсловия = СтрСоединить(УсловияВыбора, Символы.ПС);
	Возврат ТекстПоля + ТекстУсловия + Символы.ПС + "КОНЕЦ";
	
КонецФункции

Функция РазделительЗапросов()
	
	Возврат "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|";
		
КонецФункции

Процедура ДобавитьЗамер(Замеры, ИмяЗамера, НачалоЗамера)
	
	ВремяЗамера = (ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЗамера) / 1000;
	Замеры.Вставить(ИмяЗамера, ПредставлениеВремени(ВремяЗамера));
	
КонецПроцедуры

// Функция возвращает типы регистраторов регистра ПрочиеАктивыПассивы
//
// Возвращаемое значение:
// 	ТаблицаЗначений - Содержит колество обновляемых документов:
// 	* Имя - Строка - 
// 	* Синоним - Строка -
// 	* ВсегоДокументов - Число -	
//
Функция КоличествоРегистраторов()
	
	Исключения = Новый Соответствие;
	Исключения.Вставить(Метаданные.Документы.КорректировкаРегистров.Имя, Истина);
	Исключения.Вставить(Метаданные.Документы.РасчетСебестоимостиТоваров.Имя, Истина);
	
	ШаблонЗапроса =
	"ВЫБРАТЬ
	|	""&ИмяДокумента"" КАК Имя,
	|	""&СинонимДокумента"" КАК Синоним,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Т.Ссылка) КАК ВсегоДокументов
	|ИЗ
	|	&ИмяДокумента КАК Т
	|ГДЕ
	|	Т.Проведен";
	ТипыРегистраторов = Метаданные.РегистрыНакопления.ПрочиеАктивыПассивы.СтандартныеРеквизиты.Регистратор.Тип.Типы();
	МассивЗапросов = Новый Массив;
	Для Каждого ТипРегистратора Из ТипыРегистраторов Цикл
		
		МетаданныеРегистратора = Метаданные.НайтиПоТипу(ТипРегистратора);
		Если Исключения[МетаданныеРегистратора.Имя] = Неопределено Тогда
			ТекстЗапроса = СтрЗаменить(ШаблонЗапроса, "&ИмяДокумента", "Документ." + МетаданныеРегистратора.Имя);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СинонимДокумента", СтрЗаменить(МетаданныеРегистратора.Синоним,"""",""));
			МассивЗапросов.Добавить(ТекстЗапроса);
		КонецЕсли;
		
	КонецЦикла;
	
	ОбъединитьВсе = 
	"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|";
	
	ТекстВложенногоЗапроса = СтрСоединить(МассивЗапросов, ОбъединитьВсе);
	Шаблон = 
	"ВЫБРАТЬ
	|	Т.Имя,
	|	Т.Синоним,
	|	Т.ВсегоДокументов
	|ИЗ
	|	&ВложенныйЗапрос КАК Т
	|ГДЕ
	|	Т.ВсегоДокументов > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Имя";
	
	ТекстЗапроса = СтрЗаменить(Шаблон, "&ВложенныйЗапрос", "(" + ТекстВложенногоЗапроса + ")");
	Запрос = Новый Запрос(ТекстЗапроса);
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

// Функция возвращает типы регистраторов регистра ПрочиеАктивыПассивы
//
// Параметры:
//  ТипыРегистраторов - см. КоличествоРегистраторов
//
// Возвращаемое значение:
// 	ТаблицаЗначений - Содержит в себе данные документа:
// 	* Имя - Строка - 
// 	* Синоним - Строка -
// 	* ВсегоДокументов - Число -
// 	* ВсегоДвижений - Число -
// 	* ВремяЗаполнения - Число -
// 	* ВсегоЗатрачено - Число - 
//
Функция ОписаниеТиповРегистра(ТипыРегистраторов)
	
	ОбработанныеТипы = ТипыРегистраторов.СкопироватьКолонки();
	ОбработанныеТипы.Колонки.Добавить("ВсегоДвижений", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10, 0)));
	ОбработанныеТипы.Колонки.Добавить("ВремяЗаполнения", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15, 3)));
	ОбработанныеТипы.Колонки.Добавить("ВсегоЗатрачено", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(30)));
	
	Возврат ОбработанныеТипы;
	
КонецФункции

Функция ПредставлениеВремени(Знач Секунды)
	
	Часы = Цел(Секунды / 3600);
	Секунды = Секунды - Часы*3600;
	
	Минуты  = Цел(Секунды / 60);
	Секунды = Секунды - Минуты*60;
	
	ЦелыеСекунды = Цел(Секунды);
	
	Секунды = Секунды - ЦелыеСекунды;
		
	ПредставлениеВремени = ""
		+ ?(Часы = 0, "", Строка(Часы) + ":")
		+ ?(Минуты = 0, ?(Часы>0,"00:",""), ?(Минуты<10 И Часы>0,"0"+Строка(Минуты),Строка(Минуты)) + ":")
		+ ?(ЦелыеСекунды = 0, ?(Минуты>0 ИЛИ Часы>0,"00",""), ?(ЦелыеСекунды<10 И (Минуты>0 ИЛИ Часы>0),"0"+Строка(ЦелыеСекунды),Строка(ЦелыеСекунды)))
		+ ?(ЦелыеСекунды>0 ИЛИ Минуты>0 ИЛИ Часы>0,Прав(Строка(Секунды),СтрДлина(Строка(Секунды))-1),Строка(ЦелыеСекунды+Секунды));
		
	Возврат ПредставлениеВремени;
	
КонецФункции

Процедура ВывестиПротокол(ТекстСообщения, ТипыРегистраторов, Замеры = Неопределено)
	
	ТекстТаблицы = СформироватьТекстПоТаблице(ТипыРегистраторов);
	ТекстЗамеров = СформироватьТекстЗамеров(Замеры);
	ЗаписьЖурналаРегистрации(
		ИмяСобытияЖурналаРегистрации_ЗаполнениеРегистра(),
		УровеньЖурналаРегистрации.Информация,
		,
		,
		ТекстСообщения + ТекстТаблицы + ТекстЗамеров);
	
КонецПроцедуры

Функция ИмяСобытияЖурналаРегистрации_ЗаполнениеРегистра()
	
	Возврат НСтр("ru = 'Заполнение регистра ""Активы и пассивы""'", ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

Функция СформироватьТекстПоТаблице(ТипыРегистраторов)
	
	Текст = Символы.ПС+Символы.ПС;
	ЗаголовокТаблицы = НСтр("ru = 'Имя документа - Всего документов'");
	ШаблонСтроки = Символы.Таб + "%1 - %2" + Символы.ПС;
	Если ТипыРегистраторов.Колонки.Количество() > 3 Тогда
		ЗаголовокТаблицы = НСтр("ru = 'Имя документа - Обработано документов / Записано движений - Всего затрачено (ЧЧ:ММ:СС,мс)'");
		ШаблонСтроки = Символы.Таб + "%1 - %2 / %3 - %4" + Символы.ПС;
	КонецЕсли;
	Текст = Текст + ЗаголовокТаблицы + Символы.ПС+Символы.ПС;
	Для Каждого Регистратор Из ТипыРегистраторов Цикл
		НоваяСтрока = СтрШаблон(ШаблонСтроки, Регистратор.Имя, Регистратор.ВсегоДокументов);
		Если ТипыРегистраторов.Колонки.Количество() > 3 Тогда
			НоваяСтрока = СтрШаблон(ШаблонСтроки, Регистратор.Имя, Регистратор.ВсегоДокументов, Регистратор.ВсегоДвижений, Регистратор.ВсегоЗатрачено);
		КонецЕсли;
		Текст = Текст + НоваяСтрока;
	КонецЦикла;
	Если ТипыРегистраторов.Колонки.Количество() = 3 Тогда
		ИтоговаяСтрока = СтрШаблон(ШаблонСтроки, НСтр("ru = 'Итого'"), ТипыРегистраторов.Итог("ВсегоДокументов"));
		Текст = Текст + ИтоговаяСтрока;
	КонецЕсли;
	Возврат Текст;
	
КонецФункции

Функция СформироватьТекстЗамеров(Замеры)
	
	Если ТипЗнч(Замеры) <> Тип("Структура") Тогда
		Возврат "";
	КонецЕсли;
	
	Текст = Символы.ПС + НСтр("ru = 'Затраты времени:'")+Символы.ПС;
	Для Каждого Замер Из Замеры Цикл
		
		Текст = Текст + СтрШаблон("	%1 - %2", Замер.Ключ, Замер.Значение)+Символы.ПС;
		
	КонецЦикла;
	Возврат Текст;
	
КонецФункции

// Возвращет структуру описывающую временную таблицу в которой содержатся документы к отражению в управленческом балансе
// 
// Возвращаемое значение:
// 	Структура - Описание:
// * ПолеРегистратора - Строка -
// * ИмяТаблицы - Строка -
Функция ОписаниеТаблицыРегистраторов() Экспорт
	
	ТаблицаРегистраторов = Новый Структура;
	ТаблицаРегистраторов.Вставить("ИмяТаблицы"      , "ДокументыКОтражению");
	ТаблицаРегистраторов.Вставить("ПолеРегистратора", "Регистратор");
	
	Возврат ТаблицаРегистраторов;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли