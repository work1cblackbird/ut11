#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АвтоНавигационнаяСсылка = Ложь;
	НавигационнаяСсылка = "e1cib/app/" + ИмяФормы;
	
	Если НЕ ЗначениеЗаполнено(Параметры.ПозицияТорговогоПредложения) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПозицияТорговогоПредложения = Параметры.ПозицияТорговогоПредложения;
	
	ДополнитьДанныеПозицииТорговогоПредложения();
	
	Элементы.ФормаПоказатьПредупреждения.Видимость = Ложь;
	
	ПроисходитОбновлениеДанных = Истина;
	ДлительнаяОперацияФормированияКарточки = 
		СформироватьКарточкуВФоне(ПозицияТорговогоПредложения, РежимРедактирования, УникальныйИдентификатор, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОжидатьЗавершенияФормированияКарточки();
	ОбновитьОтображениеЭлементовФормы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТабличныйДокументОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	Если ПроисходитОбновлениеДанных Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли; 
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Расшифровка", Расшифровка);
	
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ПослеИзмененияДанныхПозицииТорговогоПредложения", ЭтотОбъект,
		ДополнительныеПараметры);
	
	ТорговыеПредложенияКлиент.ОбработатьНажатиеПоГиперссылкеРеквизитаКарточкиТорговогоПредложения(ПозицияТорговогоПредложения,
				Расшифровка, ЭтотОбъект, Элемент, СтандартнаяОбработка, ОписаниеОповещенияОЗавершении);
	
КонецПроцедуры

&НаКлиенте
Процедура ТабличныйДокументВыбор(Элемент, Область, СтандартнаяОбработка)
	
	Если ПроисходитОбновлениеДанных Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли; 
	
	Если Область.Имя = "СледующееИзображение" Тогда
		
		Элементы.ТабличныйДокумент.ТекущаяОбласть = ТабличныйДокумент.Области.ЗаголовокИзображения;
		
		ПереключитьИзображение(Истина);
		
	ИначеЕсли Область.Имя = "ПредыдущееИзображение" Тогда
		
		Элементы.ТабличныйДокумент.ТекущаяОбласть = ТабличныйДокумент.Области.ЗаголовокИзображения;
		
		ПереключитьИзображение(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьРежимРедактирования(Команда)
	
	РежимРедактирования = НЕ РежимРедактирования;
	ПереформироватьКарточку(Неопределено);
	ОбновитьОтображениеЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПредупреждения(Команда)
	
	ПозицииТорговыхПредложений = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПозицияТорговогоПредложения);
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("ПозицияТорговогоПредложения" , ПозицияТорговогоПредложения);
	ПараметрыОткрытияФормы.Вставить("ПозицииТорговыхПредложений"  , ПозицииТорговыхПредложений);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеИзмененияДанныхПозицииТорговогоПредложения", ЭтотОбъект);
		
	ТорговыеПредложенияКлиент.ОткрытьФормуДиагностикиТорговыхПредложений(ПараметрыОткрытияФормы, ЭтаФорма, ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОтображениеФормы


&НаКлиенте
Процедура ОбновитьОтображениеЭлементовФормы()

	Элементы.ФормаИзменитьРежимРедактирования.Пометка   = РежимРедактирования;
	Элементы.ФормаИзменитьРежимРедактирования.Заголовок = 
		?(РежимРедактирования, НСтр("ru = 'Завершить редактирование'"), НСтр("ru = 'Режим редактирования'"));

КонецПроцедуры 
#КонецОбласти

#Область ФормированиеКарточки

&НаСервере
Процедура ДополнитьДанныеПозицииТорговогоПредложения()
	
	Если Не ТипЗнч(ПозицияТорговогоПредложения) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	СвойстваПредложения = Новый Структура("Организация, Валюта");
	ТорговыеПредложенияПереопределяемый.ПолучитьСвойстваТорговогоПредложения(ПозицияТорговогоПредложения.ПрайсЛист, СвойстваПредложения);
	
	ПозицияТорговогоПредложения.Вставить("Организация", СвойстваПредложения.Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереформироватьКарточку(Расшифровка)
	
	ДлительнаяОперацияФормированияКарточки = СформироватьКарточкуВФоне(ПозицияТорговогоПредложения, РежимРедактирования, УникальныйИдентификатор, Истина);
	ПроисходитОбновлениеДанных             = Истина;
	
	ОжидатьЗавершенияФормированияКарточки(Расшифровка);
	
КонецПроцедуры
 
&НаСервереБезКонтекста
Функция СформироватьКарточкуВФоне(ПозицияТорговогоПредложения, РежимРедактирования, УникальныйИдентификаторФормы, ТребуетсяВыполнятьПроверку)

	НаименованиеЗадания = НСтр("ru = 'Формирование карточки публикуемого торгового предложения 1С:Бизнес-сеть'");
	ИмяПроцедуры        = "Обработки.ТорговыеПредложения.СформироватьКарточкуПубликуемогоТорговогоПредложения";
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ПозицияТорговогоПредложения" , ПозицияТорговогоПредложения);
	ПараметрыПроцедуры.Вставить("РежимРедактирования"         , РежимРедактирования);
	ПараметрыПроцедуры.Вставить("ТребуетсяВыполнятьПроверку"  , ТребуетсяВыполнятьПроверку);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(ИмяПроцедуры, ПараметрыПроцедуры, ПараметрыВыполнения);

КонецФункции

&НаКлиенте
Процедура ОжидатьЗавершенияФормированияКарточки(Расшифровка = Неопределено)

	Если ДлительнаяОперацияФормированияКарточки.Статус = "Выполняется" Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("СформироватьКарточкуЗавершение", ЭтотОбъект, Расшифровка);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперацияФормированияКарточки, ОписаниеОповещения,
			ПараметрыОжидания);
		
		ОтображениеСостояния = Элементы.ТабличныйДокумент.ОтображениеСостояния;
		ОтображениеСостояния.Видимость                      = Истина;
		ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
		ОтображениеСостояния.Картинка                       = БиблиотекаКартинок.ДлительнаяОперация48;
		ОтображениеСостояния.Текст                          = НСтр("ru = 'Карточка формируется...'");

	Иначе
		СформироватьКарточкуЗавершение(ДлительнаяОперацияФормированияКарточки);
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура СформироватьКарточкуЗавершение(ДлительнаяОперация, ДополнительныеПараметры = Неопределено) Экспорт 
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	БизнесСетьСлужебныйКлиент.ВывестиСообщенияФоновогоЗадания(ДлительнаяОперация);
	
	Если ДлительнаяОперация.Статус <> "Выполнено" Тогда
		
		Если ЗначениеЗаполнено(ДлительнаяОперация.КраткоеПредставлениеОшибки) Тогда
			ВывестиТекстОшибкиВТабличныйДокумент(ДлительнаяОперация.КраткоеПредставлениеОшибки);
		КонецЕсли;
		Возврат;
		
	КонецЕсли;
	
	ПроисходитОбновлениеДанных = Ложь;
	
	АдресДанныхКарточки = ДлительнаяОперация.АдресРезультата;
	ДанныеКарточки      = ПолучитьИзВременногоХранилища(АдресДанныхКарточки);
	
	ТабличныйДокумент   = ДанныеКарточки.ТабличныйДокумент;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ТабличныйДокумент, "НеИспользовать");
	
	Элементы.ФормаПоказатьПредупреждения.Видимость = ДанныеКарточки.ЕстьПредупреждения;
	
	РежимДобавления = ЗначениеЗаполнено(ДополнительныеПараметры)
		И ДополнительныеПараметры.Свойство("ИмяРеквизита")
		И ДополнительныеПараметры.ИмяРеквизита = "Изображения.ФайлИзображения"
		И ДополнительныеПараметры.ДополнительныйРеквизит.ТипДействия = "Добавление";
	УстановитьТекущееИзображение(РежимДобавления);
	
	Если ДанныеКарточки.Публикуется Тогда
		Описание = НСтр("ru = 'Карточка торгового предложения'");
		Заголовок = СтрШаблон("%1 (%2)", ПозицияТорговогоПредложения.Номенклатура, Описание);
	Иначе
		Описание = НСтр("ru = 'Карточка торгового предложения предварительный просмотр'");
		Заголовок = СтрШаблон("%1 (%2)", ПозицияТорговогоПредложения.Номенклатура, Описание);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиТекстОшибкиВТабличныйДокумент(ТекстОшибки)
		
	ОтображениеСостояния = Элементы.ТабличныйДокумент.ОтображениеСостояния;
	
	ОтображениеСостояния.Видимость                      = Истина;
	ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
	ОтображениеСостояния.Картинка                       = Новый Картинка;
	ОтображениеСостояния.Текст                          = ТекстОшибки;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

&НаКлиенте
Процедура ПослеИзмененияДанныхПозицииТорговогоПредложения(РезультатРедактирования, ДополнительныеПараметры) Экспорт 
	
	Если ЗначениеЗаполнено(ДополнительныеПараметры) И ДополнительныеПараметры.Свойство("Расшифровка") Тогда
		ПереформироватьКарточку(ДополнительныеПараметры.Расшифровка);
	Иначе
		ПереформироватьКарточку(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Изображения

&НаСервере
Процедура ПереключитьИзображение(СледующееИзображение)
	
	ДанныеКарточки = ПолучитьИзВременногоХранилища(АдресДанныхКарточки);
	
	РедактированиеИзображенийРазрешено = ДанныеКарточки.РедактированиеИзображенийРазрешено;
	НоменклатураСервиса                = ДанныеКарточки.НоменклатураСервиса;
		
	Если СледующееИзображение И ИндексТекущегоИзображения + 1 = ДанныеКарточки.ДанныеИзображений.Количество() Тогда
		Если РедактированиеИзображенийРазрешено
			И НЕ УстановленоИзображениеДобавить
			И НЕ НоменклатураСервиса Тогда
			УстановленоИзображениеДобавить = Истина;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ УстановленоИзображениеДобавить Тогда
		
		Если НЕ СледующееИзображение И ИндексТекущегоИзображения = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Если СледующееИзображение Тогда
			ИндексТекущегоИзображения = ИндексТекущегоИзображения + 1;
		Иначе
			ИндексТекущегоИзображения = ИндексТекущегоИзображения - 1;
		КонецЕсли;
		
	ИначеЕсли НЕ СледующееИзображение Тогда
		УстановленоИзображениеДобавить = Ложь;
	КонецЕсли;

	Если НЕ УстановленоИзображениеДобавить Тогда
		
		ДанныеКартинки = ДанныеКарточки.ДанныеИзображений.Получить(ИндексТекущегоИзображения);
		
		Если ДанныеКарточки.ДанныеИзображений.Количество() > 1 Тогда
			ТабличныйДокумент.Области.ЗаголовокИзображения.Текст
				= СтрШаблон("Изображение (%1/%2)",
					ИндексТекущегоИзображения + 1, ДанныеКарточки.ДанныеИзображений.Количество());
		Иначе
			ТабличныйДокумент.Области.ЗаголовокИзображения.Текст
				= Нстр("ru='Изображение'");
		КонецЕсли;
			
		ТабличныйДокумент.Рисунки.ИзображениеПредложения.Картинка = Новый Картинка(ДанныеКартинки);
		УстановленоИзображениеДобавить = Ложь;
		
	Иначе
		
		ТабличныйДокумент.Области.ЗаголовокИзображения.Текст = Нстр("ru='Добавить изображение'");
		ТабличныйДокумент.Рисунки.ИзображениеПредложения.Картинка = БиблиотекаКартинок.ПлюсТорговыеПредложения;

	КонецЕсли;

	Если РедактированиеИзображенийРазрешено Тогда
		
		РасшифровкаИзображениеПредложения = ТабличныйДокумент.Рисунки.ИзображениеПредложения.Расшифровка;
		Если ТипЗнч(РасшифровкаИзображениеПредложения) = Тип("Структура") Тогда
			ДополнительныеПараметры = РасшифровкаИзображениеПредложения.ДополнительныйРеквизит;
			Если ДополнительныеПараметры.Свойство("ИндексИзображения") Тогда
				ДополнительныеПараметры.ИндексИзображения = ИндексТекущегоИзображения;
				ДополнительныеПараметры.ТипДействия
					= ?(УстановленоИзображениеДобавить, "Добавление", "Редактирование");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекущееИзображение(РежимДобавления)
	
	ДанныеКарточки = ПолучитьИзВременногоХранилища(АдресДанныхКарточки);
	
	РедактированиеИзображенийРазрешено = ДанныеКарточки.РедактированиеИзображенийРазрешено;
	НоменклатураСервиса                = ДанныеКарточки.НоменклатураСервиса;
	
	Если ДанныеКарточки.ДанныеИзображений.Количество() = 0
		И (НЕ РедактированиеИзображенийРазрешено ИЛИ НоменклатураСервиса) Тогда
		ИндексТекущегоИзображения = 0;
		УстановленоИзображениеДобавить = Ложь;
		Возврат;
	ИначеЕсли ДанныеКарточки.ДанныеИзображений.Количество() = 0 Тогда
		УстановленоИзображениеДобавить = Истина;
	ИначеЕсли РежимДобавления Тогда
		ИндексТекущегоИзображения = ДанныеКарточки.ДанныеИзображений.Количество() - 1;
		УстановленоИзображениеДобавить = Ложь;
	ИначеЕсли РедактированиеИзображенийРазрешено
		И УстановленоИзображениеДобавить
		И НЕ НоменклатураСервиса Тогда
		УстановленоИзображениеДобавить = Истина;
	ИначеЕсли ИндексТекущегоИзображения >= ДанныеКарточки.ДанныеИзображений.Количество() Тогда
		ИндексТекущегоИзображения = 0;
		УстановленоИзображениеДобавить = Ложь;
		Возврат;
	ИначеЕсли НЕ РедактированиеИзображенийРазрешено Тогда
		УстановленоИзображениеДобавить = Ложь;
	КонецЕсли;
	
	Если НЕ УстановленоИзображениеДобавить Тогда
		
		ДанныеКартинки = ДанныеКарточки.ДанныеИзображений.Получить(ИндексТекущегоИзображения);
		
		Если ДанныеКарточки.ДанныеИзображений.Количество() > 1 Тогда
			ТабличныйДокумент.Области.ЗаголовокИзображения.Текст
				= СтрШаблон("Изображение (%1/%2)",
				ИндексТекущегоИзображения + 1, ДанныеКарточки.ДанныеИзображений.Количество());
		Иначе
			ТабличныйДокумент.Области.ЗаголовокИзображения.Текст
				= Нстр("ru='Изображение'");
		КонецЕсли;
		
		ТабличныйДокумент.Рисунки.ИзображениеПредложения.Картинка = Новый Картинка(ДанныеКартинки);
		УстановленоИзображениеДобавить = Ложь;
		
	Иначе
		
		ТабличныйДокумент.Области.ЗаголовокИзображения.Текст = Нстр("ru='Добавить изображение'");
		ТабличныйДокумент.Рисунки.ИзображениеПредложения.Картинка = БиблиотекаКартинок.ПлюсТорговыеПредложения;
		
	КонецЕсли;
	
	Если РедактированиеИзображенийРазрешено Тогда
		
		РасшифровкаИзображениеПредложения = ТабличныйДокумент.Рисунки.ИзображениеПредложения.Расшифровка;
		Если ТипЗнч(РасшифровкаИзображениеПредложения) = Тип("Структура") Тогда
			ДополнительныеПараметры = РасшифровкаИзображениеПредложения.ДополнительныйРеквизит;
			Если ДополнительныеПараметры.Свойство("ИндексИзображения") Тогда
				ДополнительныеПараметры.ИндексИзображения = ИндексТекущегоИзображения;
				ДополнительныеПараметры.ТипДействия
					= ?(УстановленоИзображениеДобавить, "Добавление", "Редактирование");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
