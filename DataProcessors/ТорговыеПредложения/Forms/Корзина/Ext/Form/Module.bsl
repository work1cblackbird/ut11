
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Параметры.Организация;
	
	УстановитьУсловноеОформление();
	НастроитьФормуПриСоздании();
	
	АдресКорзины = ТорговыеПредложенияСлужебный.АдресКорзиныИзЛокальногоХранилища();
	ЗапуститьЧтениеКорзины();
	
	УстановитьВидимостьДекорацияРегистрация();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОжидатьЗавершенияЧтенияКорзины();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьКонтактныеДанныеОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ТорговыеПредложения_ПеречитатьДанныеКорзины"
		И Источник <> ЭтотОбъект Тогда
		
		АдресДанныхКорзины = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметр, "АдресДанныхКорзины", "");
		ПеречитатьКорзинуНаСервере(АдресДанныхКорзины);
		
	ИначеЕсли ИмяСобытия = "БизнесСеть_РегистрацияОрганизаций" Тогда
		
		УстановитьВидимостьДекорацияРегистрация();
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АдресДоставкиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение <> "ДобавитьАдрес" Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьФормуКонтактнойИнформации();
	
КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуКонтактнойИнформации();
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьИсходящиеДокументыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьФормуИсходящихДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницыКорзиныПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Элементы.КомандаСтраницаНоменклатура.Пометка = (ТекущаяСтраница = Элементы.СтраницаТовары);
	Элементы.КомандаСтраницаИнформацииОЗаказе.Пометка = (ТекущаяСтраница = Элементы.СтраницаИнформацияОЗаказе);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьЗаказОтправленОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьФормуИсходящихДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
	СпособДоставкиПриИзмененииНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура СпособДоставкиПриИзменении(Элемент)
	
	СпособДоставкиПриИзмененииНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(АдресДоставки) Тогда
		Если СпособДоставки = "Доставка" Тогда
			ПроверитьАдресПоказатьПодсказку();
		КонецЕсли;
	Иначе
		УстановитьЗаголовокПодсказкуАдреса(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияУдалитьАдресНажатие(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПриУдаленияАдресаЗавершение", ЭтаФорма);
	ПоказатьВопрос(ОписаниеОповещения, НСтр("ru = 'Адрес будет удален из списка. Продолжить?'"), РежимДиалогаВопрос.ДаНет);	
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРегистрацияОбработкаНавигационнойСсылки(
	Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Организация.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПодключениеОрганизацииПродолжение", ЭтотОбъект);
	ТорговыеПредложенияКлиент.ОткрытьФормуПодключенияОрганизации(Организация, ЭтотОбъект, Оповещение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗаказы

&НаКлиенте
Процедура ЗаказыПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Заказы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ИдентификаторСвязи = ИдентификаторТекущегоЗаказа Тогда
		Возврат;
	КонецЕсли;
	
	ПриАктивацииЗаказаНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура НоменклатураКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ИдентификаторПредложения = ТекущиеДанные.ИдентификаторПредложения;
	
	ДобавитьТоварИзменитьКоличество(
		ИдентификаторПредложения, 
		ТекущиеДанные.Количество, 
		ТекущиеДанные.АдресКомандыИзменитьКоличество);
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураКоличествоОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ОчиститьСообщения();
	
	КоличествоВведеноКорректно = ТорговыеПредложенияКлиент.КоличествоВведеноКорректно(
		СтроковыеФункцииКлиентСервер.СтрокаВЧисло(Текст),
		ТекущиеДанные.МинимальноеКоличество,
		ТекущиеДанные.КратностьУпаковки);
	
	Если Не КоличествоВведеноКорректно Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьТовар(Команда)
	
	ТорговыеПредложенияКлиент.ОткрытьФормуПоискаПоОтборам();

КонецПроцедуры

&НаКлиенте
Процедура УдалитьТовар(Команда)
	
	ТекущиеДанныеЗаказа = Элементы.Заказы.ТекущиеДанные;
	
	Если ТекущиеДанныеЗаказа = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторЗаказаВСервисе = ТекущиеДанныеЗаказа.ИдентификаторЗаказаВСервисе;
	
	ТекущиеДанныеТовара = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанныеТовара = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	НастроитьЭлементыДлительнойОперации(Истина, Истина);
	
	УдалитьТоварИзКорзины(ТекущиеДанныеТовара.ИдентификаторПредложения, 
		ИдентификаторЗаказаВСервисе, ТекущиеДанныеТовара.АдресКомандыУдалить);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗаказ(Команда)
	
	ОтправитьЗаказНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницаИнформацииОЗаказе(Команда)
	
	Элементы.КомандаСтраницаНоменклатура.Пометка = Ложь;
	Элементы.КомандаСтраницаИнформацииОЗаказе.Пометка = Истина;
	
	Элементы.СтраницыКорзины.ТекущаяСтраница = Элементы.СтраницаИнформацияОЗаказе;
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницаНоменклатура(Команда)
	
	Элементы.КомандаСтраницаНоменклатура.Пометка = Истина;
	Элементы.КомандаСтраницаИнформацииОЗаказе.Пометка = Ложь;
	
	Элементы.СтраницыКорзины.ТекущаяСтраница = Элементы.СтраницаТовары;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьКорзину(Команда)
	
	Оповещение = Новый ОписаниеОповещения("УдалитьКорзинуЗавершение", ЭтотОбъект);
	ТорговыеПредложенияКлиент.УдалитьКорзинуДиалог(ЭтотОбъект, Организация, АдресКорзины, Оповещение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область КонтактнаяИнформация

&НаКлиенте
Процедура ОткрытьФормуКонтактнойИнформации()
	
	ПараметрыВида = ПараметрыВидаКонтактнойИнформации();
	
	ПараметрыВида.Наименование = НСтр("ru = 'Укажите адрес доставки'");
	ПараметрыВида.РедактированиеТолькоВДиалоге      = Истина;
	
	ПараметрыОткрытия = УправлениеКонтактнойИнформациейКлиент.ПараметрыФормыКонтактнойИнформации(
		ПараметрыВида, АдресДоставки);
	
	Оповещение = Новый ОписаниеОповещения("ОткрытьФормуКонтактнойИнформацииЗавершение", ЭтотОбъект);
	
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия, ЭтотОбъект, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКонтактнойИнформацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеАдреса = Результат.Значение;
	ОбработатьАдресПослеВыбора(ЗначениеАдреса);
	
	ТекущиеДанные = Элементы.Заказы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ЗаполнитьАдресаЗаказа(ЭтотОбъект, ТекущиеДанные);
	
	АдресДоставки = ЗначениеАдреса;
	
	ПроверитьАдресПоказатьПодсказку();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыВидаКонтактнойИнформации()
	
	Возврат УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации(
		ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес"));
	
КонецФункции

&НаСервере
Процедура ОбработатьАдресПослеВыбора(ЗначениеАдреса)
	
	СведенияОбАдресе = РаботаСАдресами.СведенияОбАдресе(ЗначениеАдреса, Новый Структура("КодыАдреса", Истина));
	
	СтрокаАдреса = СтрокаАдресаПоЗначениюАдреса(АдресаОрганизации, Организация, ЗначениеАдреса);
	Если СтрокаАдреса = Неопределено Тогда
		СтрокаАдреса = АдресаОрганизации.Добавить();
	Иначе
		ЗначениеАдреса = СтрокаАдреса.ЗначенияПолей;
		Возврат;
	КонецЕсли;
	
	СтрокаАдреса.КодСтраны            = СведенияОбАдресе.КодСтраны;
	СтрокаАдреса.ИдентификаторФИАС    = Строка(СведенияОбАдресе.ИдентификаторАдресногоОбъекта);
	СтрокаАдреса.ПредставлениеАдреса  = СведенияОбАдресе.Представление;
	СтрокаАдреса.Организация          = Организация;
	СтрокаАдреса.ЭтоПроизвольныйАдрес = Истина;
	СтрокаАдреса.ЗначенияПолей        = ЗначениеАдреса;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьАдресПоказатьПодсказку()
	
	Если СпособДоставки = "Самовывоз" Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(АдресДоставки) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Элементы.Заказы.ТекущаяСтрока;
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаАдреса = АдресаОрганизации.НайтиСтроки(
		Новый Структура("ЗначенияПолей, Организация", АдресДоставки, Организация))[0];
	
	ДанныеАдреса = РаботаСАдресами.АдресПоИдентификатору(СтрокаАдреса.ИдентификаторФИАС);
	
	СведенияОбАдресе = РаботаСАдресами.СведенияОбАдресе(ДанныеАдреса, Новый Структура("КодыАдреса", Истина));
	
	ИдентификаторыДляПроверки = Новый Массив;
	
	ДобавитьИдентификаторВМассив(ИдентификаторыДляПроверки, СведенияОбАдресе.Идентификаторы.Регион);
	ДобавитьИдентификаторВМассив(ИдентификаторыДляПроверки, СведенияОбАдресе.Идентификаторы.Район);
	ДобавитьИдентификаторВМассив(ИдентификаторыДляПроверки, СведенияОбАдресе.Идентификаторы.МуниципальныйРайон);
	ДобавитьИдентификаторВМассив(ИдентификаторыДляПроверки, СведенияОбАдресе.Идентификаторы.Город);
	
	ВозможнаДоставкаПоАдресу = Ложь;
	
	ТекущиеДанные = Заказы.НайтиПоИдентификатору(ТекущаяСтрока);
	
	Для каждого ЭлементКоллекции Из ТекущиеДанные.РегионыДоставки Цикл
		
		Если Не ЗначениеЗаполнено(ЭлементКоллекции.ИдентификаторФИАС)
			И СведенияОбАдресе.КодСтраны = ЭлементКоллекции.КодСтраны Тогда
			
			ВозможнаДоставкаПоАдресу = Истина;
			Прервать;
		КонецЕсли;
		
		Если ИдентификаторыДляПроверки.Найти(ЭлементКоллекции.ИдентификаторФИАС) <> Неопределено Тогда
			ВозможнаДоставкаПоАдресу = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ВозможнаДоставкаПоАдресу Тогда
		УстановитьЗаголовокВидимостьПодсказкиАдреса(ЭтотОбъект, "ПодсказкаНеТребуется");
	Иначе
		УстановитьЗаголовокВидимостьПодсказкиАдреса(ЭтотОбъект, "НеПоддерживаетсяАдрес");	
	КонецЕсли;
	
	СтрокаАдреса = СтрокаАдресаПоЗначениюАдреса(АдресаОрганизации, Организация, АдресДоставки);
	
	Если СтрокаАдреса <> Неопределено И СтрокаАдреса.ЭтоПроизвольныйАдрес Тогда
		Элементы.ДекорацияУдалитьАдрес.Видимость = Истина;
	Иначе
		Элементы.ДекорацияУдалитьАдрес.Видимость = Ложь;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаАдресаПоЗначениюАдреса(Знач АдресаОрганизации, Знач ТекущаяОрганизация, Знач ЗначенияПолей)
	
	Результат = Неопределено;
	
	СтрокиАдресов = АдресаОрганизации.
		НайтиСтроки(Новый Структура("Организация, ЗначенияПолей", ТекущаяОрганизация, ЗначенияПолей));
	
	Если ЗначениеЗаполнено(СтрокиАдресов) Тогда
		Результат = СтрокиАдресов[0];
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ДобавитьИдентификаторВМассив(МассивИдентификаторов, Идентификатор)
	
	Если Не ЗначениеЗаполнено(Идентификатор) Тогда
		Возврат;
	КонецЕсли;
	
	МассивИдентификаторов.Добавить(Строка(Идентификатор));
	
КонецПроцедуры

#КонецОбласти

#Область РаботаФормы

&НаСервере
Процедура УстановитьУсловноеОформление()

	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НоменклатураСостояние.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Товары.ЕстьНарушенияОграничений");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра(
		"ЦветТекста", 
		ЦветаСтиля.НезаполненныйРеквизитЦветБЭД);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДекорацияРегистрация();
	
	ТребуетсяПодключение = БизнесСеть.ОрганизацияНеПодключенаТребуетсяПовторноеПодключение(Организация);
	ВидимостьДекорацияРегистрация = Не Организация.Пустая() И ТребуетсяПодключение;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "ДекорацияРегистрация", "Видимость", ВидимостьДекорацияРегистрация);
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ЗаполнитьСписокАдресовОрганизации();
	
	ДанныеОрганизации = ДанныеОрганизацииИзНастроек(Организация);
	Если ДанныеОрганизации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеОрганизации);
	
	ЗаполнитьАдресаОрганизацииИзНастроек(Организация, ДанныеОрганизации);
	
	УстановитьВидимостьДекорацияРегистрация();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьДанныеФормыВСтрокуЗаказа()
	
	Если Не ЗначениеЗаполнено(ИдентификаторТекущегоЗаказа) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиЗаказов = Заказы.НайтиСтроки(Новый Структура("ИдентификаторСвязи", ИдентификаторТекущегоЗаказа));
	
	Если Не ЗначениеЗаполнено(СтрокиЗаказов) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаЗаказа = СтрокиЗаказов[0];
	
	ПоляЗаполнения = РедактируемыеПоляЗаказа();
	
	ЗаполнитьЗначенияСвойств(СтрокаЗаказа, ЭтотОбъект, ПоляЗаполнения);
	
	СтрокаЗаказа.Телефон = ТелефонЦифрами(СтрокаЗаказа.Телефон);
	
	УстановитьКонтактныеДанныеОрганизацииВсемЗаказам();
	
	СохранитьКонтактныеДанныеОрганизации();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РедактируемыеПоляЗаказа()
	
	// Поля реквизита форма Заказы.
	Результат = "Организация, НаименованиеПокупателя, ЭлектроннаяПочта, 
		|Телефон, КомментарийКЗаказу, СпособДоставки, АдресДоставки,
		|СуммаСНДС, СуммаНДС, Валюта";
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьКонтактныеДанныеОрганизацииВсемЗаказам()
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	ПоляЗаполненияЗаказа = РедактируемыеПоляЗаказа();
	ПоляЗаполнения = СтрРазделить(ПоляЗаполненияЗаказа, ",", Ложь);
	
	Для Каждого ЭлементКоллекции Из Заказы Цикл
		
		Если ЗначениеЗаполнено(ЭлементКоллекции.Организация)
			И Организация <> ЭлементКоллекции.Организация Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого ПолеЗаказа Из ПоляЗаполнения Цикл
			ТекущееПоле = СтрЗаменить(СокрЛП(ПолеЗаказа), Символы.ПС, "");
			Если ЗначениеЗаполнено(ЭлементКоллекции[ТекущееПоле]) Тогда
				Продолжить;
			КонецЕсли;
			ЭлементКоллекции[ТекущееПоле] = ЭтотОбъект[ТекущееПоле];
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СпособДоставкиПриИзмененииНаКлиенте()
	
	ТекущиеДанные = Элементы.Заказы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	АдресДоставки = "";
	
	ЗаполнитьАдресаЗаказа(ЭтотОбъект, ТекущиеДанные);
	
	УстановитьЗаголовокПодсказкуАдреса(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокПодсказкуАдреса(Форма)
	
	Элементы       = Форма.Элементы;
	СпособДоставки = Форма.СпособДоставки;
	
	Если СпособДоставки = "Самовывоз" Тогда
		Элементы.АдресДоставки.Заголовок = НСтр("ru = 'Адрес самовывоза'");
	Иначе
		Элементы.АдресДоставки.Заголовок = НСтр("ru = 'Адрес доставки'");
	КонецЕсли;
	
	Если Элементы.Заказы.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Форма.Заказы.НайтиПоИдентификатору(Элементы.Заказы.ТекущаяСтрока);
	
	УстановитьСвойстваАдресДоставки(Элементы, Ложь, Ложь, Ложь);
	
	Если СпособДоставки = "Самовывоз"
		И Не ЗначениеЗаполнено(ТекущиеДанные.АдресаСамовывоза) Тогда
		
		УстановитьЗаголовокВидимостьПодсказкиАдреса(Форма, "НевозможенСамовывоз");
		
	ИначеЕсли СпособДоставки = "Доставка"
		И Не ЗначениеЗаполнено(ТекущиеДанные.РегионыДоставки) Тогда
		
		УстановитьЗаголовокВидимостьПодсказкиАдреса(Форма, "НевозможнаДоставка");

	Иначе
		
		АдресДоставкиЗаполнен = Не ПустаяСтрока(Форма.АдресДоставки);
		ЭтоДоставка = СпособДоставки = "Доставка";
		КнопкаУдалитьАдрес = Не ЭтоДоставка И АдресДоставкиЗаполнен;
		УстановитьСвойстваАдресДоставки(Элементы, Истина, КнопкаУдалитьАдрес, ЭтоДоставка);
		УстановитьЗаголовокВидимостьПодсказкиАдреса(Форма, "ПодсказкаНеТребуется");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСвойстваАдресДоставки(Элементы, ЗначениеАдреса, ЗначениеДекорации, ЗначениеКнопкиОткрытие)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "АдресДоставки", "Доступность", ЗначениеАдреса);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "ДекорацияУдалитьАдрес", "Видимость", ЗначениеДекорации);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "АдресДоставки", "КнопкаОткрытия", ЗначениеКнопкиОткрытие);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокВидимостьПодсказкиАдреса(Форма, ВидПодсказки)
	
	Элементы = Форма.Элементы;
	
	Если ВидПодсказки = "НевозможенСамовывоз" Тогда
		
		Элементы.ПояснениеКАдресу.Заголовок 
			= НСтр("ru = 'Поставщик не указал адреса самовывоза. Возможно, осуществить самовывоз нельзя.'");
			
	ИначеЕсли ВидПодсказки = "НевозможнаДоставка" Тогда	
		
		Элементы.ПояснениеКАдресу.Заголовок 
			= НСтр("ru = 'Поставщик не указал регионы доставки. Возможно, поставщик не осуществляет доставку.'");

	ИначеЕсли ВидПодсказки = "НеПоддерживаетсяАдрес" Тогда	
		
		Элементы.ПояснениеКАдресу.Заголовок 
			= НСтр("ru = 'Выбранный адрес не входит в состав регионов доставки, указанных поставщиком. Возможно, поставщик не осуществляет доставку по этому адресу.'");
			
	КонецЕсли;
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, "ПояснениеКАдресу", "Видимость", ВидПодсказки <> "ПодсказкаНеТребуется");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуИсходящихДокументов()
	
	ОчиститьСообщения();
	
	ПараметрыОткрытия = Новый Структура;
	
	ПараметрыОткрытия.Вставить("РежимИсходящихДокументов", Истина);
	
	ОткрытьФорму("Обработка.БизнесСеть.Форма.ДокументыОбмена", ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСтраницуУспешнойОтправки()
		
	Элементы.СтраницыЗаказыТовары.ТекущаяСтраница = Элементы.СтраницаЗавершение;
	ЭтотОбъект.ТекущийЭлемент = Элементы.Закрыть;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокАдресовОрганизации()
	
	Адреса = БизнесСеть.АдресаОрганизации(Организация);
	
	Для Каждого ТекущийАдрес Из Адреса Цикл
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ПредставлениеАдреса", ТекущийАдрес.Представление);
		ПараметрыОтбора.Вставить("Организация", Организация);
		НайденныеСтроки = АдресаОрганизации.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СведенияОбАдресе = РаботаСАдресами.СведенияОбАдресе(
			ТекущийАдрес.ЗначенияПолей, Новый Структура("КодыАдреса", Истина));
		
		ИдентификаторАдреса = ?(
			ЗначениеЗаполнено(СведенияОбАдресе.ИдентификаторДома),
			Строка(СведенияОбАдресе.ИдентификаторДома),
			Строка(СведенияОбАдресе.ИдентификаторАдресногоОбъекта));
		
		Если Не ЗначениеЗаполнено(ИдентификаторАдреса) Тогда
			Продолжить;
		КонецЕсли;
		
		КодСтраны = Строка(СведенияОбАдресе.КодСтраны);
		
		НоваяСтрока = АдресаОрганизации.Добавить();
		
		НоваяСтрока.ПредставлениеАдреса = ТекущийАдрес.Представление;
		НоваяСтрока.КодСтраны           = КодСтраны;
		НоваяСтрока.ИдентификаторФИАС   = ИдентификаторАдреса;
		НоваяСтрока.Организация         = Организация;
		НоваяСтрока.ЗначенияПолей       = ТекущийАдрес.ЗначенияПолей;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПриСоздании()
	
	УстановитьНастройкиПустойКорзины();
	
	НастроитьЭлементыДлительнойОперации(Истина);
	
	ЗаполнитьСписокАдресовОрганизации();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНастройкиПустойКорзины()
	
	Элементы.ГруппаПодсказкаПослеОтправки.Видимость = Ложь;
	Элементы.КартинкаПредупреждение.Видимость       = Ложь;
	Элементы.ПояснениеКАдресу.Видимость             = Ложь;
	Элементы.ДекорацияУдалитьАдрес.Видимость        = Ложь;
	Элементы.ОчиститьКорзину.Доступность = Заказы.Количество();
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьПодсказкуИсходящихДокументов(ПредставлениеЗаказа)
	
	ШаблонПодсказки = "%1 " + НСтр("ru = 'отправлен. Все отправленные заказы можно увидеть в'") 
		+ " <a href=""ИсходящиеДокументы"">" 
		+ НСтр("ru = 'Исходящих документах'")
		+ "</a>.";
		
	ЗаголовокПодсказки = СтрШаблон(ШаблонПодсказки, ПредставлениеЗаказа);
	
	Элементы.НадписьЗаказОтправлен.Заголовок
		= СтроковыеФункции.ФорматированнаяСтрока(ЗаголовокПодсказки);
		
	Элементы.ГруппаПодсказкаПослеОтправки.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСообщитьОНарушенииОграничений(Отказ)
	
	ТекущиеДанные = Элементы.Заказы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ТоварыЗаказа = Товары.НайтиСтроки(
		Новый Структура("ИдентификаторСвязи", ТекущиеДанные.ИдентификаторСвязи));
	
	Для Каждого ТекущийТовар Из ТоварыЗаказа Цикл
		
		// В ТекущийТовар.НарушенияОграничений попадают: 
		// - NoOfferAvailable - предложение товара больше недоступно
		// - TemporarilyOutOfSales - предложение товара временно снято с продажи
		Если ЗначениеЗаполнено(ТекущийТовар.НарушенияОграничений) Тогда
			
			Отказ = Истина;
			
			Для Каждого ТекущееНарушение Из ТекущийТовар.НарушенияОграничений Цикл
				
				ТекстСообщения = СтрШаблон("%1: %2", 
					ТекущийТовар.ПредставлениеНоменклатуры,
					ТекущееНарушение.Значение);
				
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
				
			КонецЦикла;
			
		КонецЕсли
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеДанных(Отказ)
	
	Если Не ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(ЭлектроннаяПочта) Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Некорректно заполнен адрес электронной почты'"),,
			"ЭлектроннаяПочта");
			
		Отказ = Истина;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Функция ТелефонЦифрами(Знач Телефон)
	
	СтрокаТелефона = "";
	Числа = "0123456789";
	
	ДлинаПредставленияТелефона = СтрДлина(Телефон);
	Для Индекс = 1 По ДлинаПредставленияТелефона Цикл
		
		Символ = Сред(Телефон, Индекс, 1);
		Если СтрНайти(Числа, Символ) > 0 Тогда
			СтрокаТелефона = СтрокаТелефона + Символ;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СтрокаТелефона;
	
КонецФункции

&НаСервере
Процедура УстановитьЗаголовокЗаказа()
	
	Элементы.НадписьЗаказы.Заголовок = "";
	
	КоличествоЗаказов = Заказы.Количество();
	Если КоличествоЗаказов = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонЗаголовка = НСтр("ru = 'Заказы к отправке. Осталось отправить %1.'");
	
	ИсходнаяСтрока = НСтр("ru=';%1 заказ;;%1 заказа;%1 заказов;'");
	ПодстрокаСЧислом = СтрокаСЧислом(
		ИсходнаяСтрока,
		КоличествоЗаказов,
		ВидЧисловогоЗначения.Количественное);
	
	Элементы.НадписьЗаказы.Заголовок = СтрШаблон(ШаблонЗаголовка, ПодстрокаСЧислом);
	
КонецПроцедуры

#КонецОбласти

#Область ФоновыеЗадания

#Область ЧтениеКорзины

&НаСервере
Процедура ЗапуститьЧтениеКорзины()
	
	ПараметрыМетода = Новый Структура;
	
	ПараметрыМетода.Вставить("Организация", Организация);
	ПараметрыМетода.Вставить("АдресКорзины", АдресКорзины);
	
	ФоновоеЗаданиеЧтениеКорзины = ТорговыеПредложенияСлужебный.СоздатьПрочитатьКорзинуВФоне(ПараметрыМетода, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗавершенияЧтенияКорзины()
	
	Если Не ЗначениеЗаполнено(ФоновоеЗаданиеЧтениеКорзины) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ВыводитьСообщения    = Истина;
	
	Оповещение = Новый ОписаниеОповещения("ЧтениеКорзиныЗавершение", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗаданиеЧтениеКорзины, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ЧтениеКорзиныЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	ЧтениеКорзиныЗавершениеНаСервере(Результат.АдресРезультата);
	
КонецПроцедуры

&НаСервере
Процедура ЧтениеКорзиныЗавершениеНаСервере(АдресРезультата)
	
	ПеречитатьКорзинуНаСервере(АдресРезультата);
	
	ЗаполнитьКонтактныеДанныеОрганизацийПриСозданииФормы();
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСТоваром

&НаКлиенте
Процедура ДобавитьТоварИзменитьКоличество(ИдентификаторПредложения, Количество, АдресКоманды);
	
	ПараметрыОповещения = Новый Структура("ИдентификаторПредложения", ИдентификаторПредложения);
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияДействияНадТоваром", ЭтотОбъект, ПараметрыОповещения);
	
	ПараметрыРаботыСТоваром = ТорговыеПредложенияКлиент.ОписаниеПараметровРаботыСТоваромКорзины();
	
	ПараметрыРаботыСТоваром.Организация                    = Организация;
	ПараметрыРаботыСТоваром.АдресКомандыИзменитьКоличество = АдресКоманды;
	ПараметрыРаботыСТоваром.Количество                     = Количество;
	ПараметрыРаботыСТоваром.ПрочитатьКорзину               = Истина;
	ПараметрыРаботыСТоваром.АдресКорзины                   = АдресКорзины;
	
	ТорговыеПредложенияКлиент.ВыполнитьДействиеСТоваромКорзины(ПараметрыРаботыСТоваром, ЭтотОбъект, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьТоварИзКорзины(ИдентификаторПредложения, ИдентификаторЗаказа, АдресКоманды)
	
	ПараметрыОповещения = Новый Структура("ИдентификаторПредложения, ИдентификаторЗаказа", 
		ИдентификаторПредложения, ИдентификаторЗаказа);
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияДействияНадТоваром", ЭтотОбъект, ПараметрыОповещения);
	
	ПараметрыРаботыСТоваром = ТорговыеПредложенияКлиент.ОписаниеПараметровРаботыСТоваромКорзины();
	
	ПараметрыРаботыСТоваром.Организация         = Организация;
	ПараметрыРаботыСТоваром.АдресКомандыУдалить = АдресКоманды;
	ПараметрыРаботыСТоваром.ПрочитатьКорзину    = Истина;
	ПараметрыРаботыСТоваром.АдресКорзины        = АдресКорзины;
	ПараметрыРаботыСТоваром.ВидОперации         = "Удаление";
		
	ТорговыеПредложенияКлиент.ВыполнитьДействиеСТоваромКорзины(ПараметрыРаботыСТоваром, ЭтотОбъект, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыполненияДействияНадТоваром(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	ПослеВыполненияДействияНадТоваромНаСервере(Результат.АдресРезультата, ДополнительныеПараметры);
	
	ТорговыеПредложенияКлиент.ОповеститьОбИзмененииКорзины(Результат.АдресРезультата, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеВыполненияДействияНадТоваромНаСервере(АдресРезультата, ДополнительныеПараметры)
	
	ПеречитатьКорзинуНаСервере(АдресРезультата);
	
	Если ДополнительныеПараметры.Свойство("ИдентификаторЗаказа") Тогда
		НастроитьЭлементыДлительнойОперации(Ложь, Истина);
		ПроверитьУстановитьТекущийЗаказ(ДополнительныеПараметры.ИдентификаторЗаказа);
	Иначе
		УстановитьТекущийТовар(ДополнительныеПараметры.ИдентификаторПредложения);	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Корзина

&НаСервере
Процедура ЗаполнитьКонтактныеДанныеОрганизацийПриСозданииФормы()
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеОрганизации = ДанныеОрганизацииИзНастроек(Организация);
	
	Если ДанныеОрганизации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьАдресаОрганизацииИзНастроек(Организация, ДанныеОрганизации);
	
	Для каждого ЭлементКоллекции Из Заказы Цикл
		ЗаполнитьЗначенияСвойств(ЭлементКоллекции, ДанныеОрганизации);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьАдресаОрганизацииИзНастроек(Организация, ДанныеОрганизации)
	
	Для Каждого ТекущийАдрес Из ДанныеОрганизации.Адреса Цикл
		
		Если Не ЗначениеЗаполнено(ТекущийАдрес.ПредставлениеАдреса) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокиАдресов = АдресаОрганизации.НайтиСтроки(
			Новый Структура("ЗначенияПолей, Организация", ТекущийАдрес.ЗначенияПолей, Организация));
		
		Если ЗначениеЗаполнено(СтрокиАдресов) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = АдресаОрганизации.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущийАдрес);
		
		НоваяСтрока.ЭтоПроизвольныйАдрес = Истина;
		НоваяСтрока.Организация = Организация;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РедактируемыеКолонкиЗаказа()
	
	Результат = "Организация, НаименованиеПокупателя, ЭлектроннаяПочта, 
		|Телефон, КомментарийКЗаказу, СпособДоставки, АдресДоставки,
		|ИдентификаторЗаказаВСервисе";
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ПеречитатьКорзинуНаСервере(АдресДанныхКорзины = Неопределено, Отказ = Ложь)
	
	НастроитьЭлементыДлительнойОперации(Ложь);
	
	ДанныеСервиса = ПолучитьИзВременногоХранилища(АдресДанныхКорзины);
	
	Если Не ЗначениеЗаполнено(ДанныеСервиса) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЗаказыДоПерезаполнения = Новый ТаблицаЗначений;
	Если ЗначениеЗаполнено(Заказы) Тогда
		ЗаказыДоПерезаполнения = Заказы.Выгрузить(, РедактируемыеКолонкиЗаказа());
	КонецЕсли;
	
	ОчиститьДанныеКорзины();
	УстановитьНастройкиПустойКорзины();
	
	ДанныеКорзины = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеСервиса, "ДанныеКорзины", Неопределено);
	Если ДанныеКорзины = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Корзина пустая, форма открыта. При добавлении товаров из других форм, присваиваем адрес корзины.
	Если ПустаяСтрока(АдресКорзины) Тогда
		АдресКорзины = ДанныеКорзины.АдресКорзины;
	КонецЕсли;
	
	ЗаполнитьЗаказы(ДанныеКорзины.Заказы, ЗаказыДоПерезаполнения);
	
	УстановитьЗаголовокЗаказа();
	
	Элементы.ОчиститьКорзину.Доступность = Заказы.Количество();
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьКорзинуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьКорзинуЗавершениеНаСервере(Результат.АдресРезультата);
	
	ТорговыеПредложенияКлиент.ОповеститьОбИзмененииКорзины(АдресКорзины, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьКорзинуЗавершениеНаСервере(АдресРезультата)
	
	ОчиститьДанныеКорзины();
	
	УстановитьНастройкиПустойКорзины();
	
	УдалитьАдресКорзиныИзЛокальногоХранилища();
	ПеречитатьКорзинуНаСервере(АдресРезультата);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьАдресКорзиныИзЛокальногоХранилища()
	
	ТорговыеПредложенияСлужебный.УдалитьАдресКорзиныИзЛокальногоХранилища();
	АдресКорзины = "";
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьДанныеКорзины()
	
	Заказы.Очистить();
	Товары.Очистить();
	ИдентификаторТекущегоЗаказа = Неопределено;
	ПредупреждениеПоЗаказу = "";
	Элементы.НадписьЗаказы.Заголовок = "";
	Элементы.НадписьДеталиЗаказа.Заголовок = "";
	
КонецПроцедуры

#КонецОбласти

#Область НастройкиФормы

&НаСервере
Процедура СохранитьКонтактныеДанныеОрганизации()
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НаименованиеПокупателя)
		И Не ЗначениеЗаполнено(ЭлектроннаяПочта)
		И Не ЗначениеЗаполнено(Телефон) Тогда
		
		Возврат;
	КонецЕсли;
	
	Настройки = ОписаниеКонтактныхДанныхОрганизации();
	
	Настройки.НаименованиеПокупателя = НаименованиеПокупателя;
	Настройки.ЭлектроннаяПочта       = ЭлектроннаяПочта;
	Настройки.Телефон                = Телефон;
	
	СтрокиАдресов = АдресаОрганизации.НайтиСтроки(
		Новый Структура("ЭтоПроизвольныйАдрес, Организация", Истина, Организация));
	
	Адреса = Новый Массив;
	
	Для каждого ЭлементКоллекции Из СтрокиАдресов Цикл
		
		Если Не ЗначениеЗаполнено(ЭлементКоллекции.ПредставлениеАдреса) Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеАдреса = Новый Структура;
		ДанныеАдреса.Вставить("КодСтраны", );
		ДанныеАдреса.Вставить("ИдентификаторФИАС", );
		ДанныеАдреса.Вставить("ЗначенияПолей", );
		ДанныеАдреса.Вставить("ПредставлениеАдреса", );
		
		ЗаполнитьЗначенияСвойств(ДанныеАдреса, ЭлементКоллекции);
		
		Адреса.Добавить(ДанныеАдреса);
		
	КонецЦикла;
	
	Настройки.Адреса = Адреса;
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(КлючНастроек(), Организация.УникальныйИдентификатор(), Настройки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеОрганизацииИзНастроек(Организация)
	
	Результат = ОписаниеКонтактныхДанныхОрганизации();
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеХранилища = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		КлючНастроек(), 
		Организация.УникальныйИдентификатор());
		
	Если Не ЗначениеЗаполнено(ДанныеХранилища) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Результат, ДанныеХранилища);
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция КлючНастроек()
	
	Результат = "КонтактныеДанныхОрганизацийБизнесСеть";
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОписаниеКонтактныхДанныхОрганизации()
	
	Результат = Новый Структура;
	
	Результат.Вставить("НаименованиеПокупателя", "");
	Результат.Вставить("ЭлектроннаяПочта",       "");
	Результат.Вставить("Телефон",                "");
	Результат.Вставить("Адреса",                 Новый Массив);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Заказы

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьАдресаЗаказа(Форма, ТекущиеДанные)
	
	АдресаОрганизации = Форма.АдресаОрганизации;
	СпособДоставки    = Форма.СпособДоставки;
	Организация       = Форма.Организация;
	АдресДоставки     = Форма.Элементы.АдресДоставки;
	
	АдресДоставки.СписокВыбора.Очистить();
	
	Если СпособДоставки = "Самовывоз" Тогда
		
		Для каждого ТекущийАдрес Из ТекущиеДанные.АдресаСамовывоза Цикл
			АдресДоставки.СписокВыбора.Добавить(ТекущийАдрес.ИдентификаторФИАС, ТекущийАдрес.ПредставлениеАдреса);
		КонецЦикла;
		
	Иначе
		
		ПараметрыОтбора = Новый Структура("Организация", Организация);
		СтрокиАдресов = АдресаОрганизации.НайтиСтроки(ПараметрыОтбора);
		
		Для Каждого ТекущийАдрес Из СтрокиАдресов Цикл
			АдресДоставки.СписокВыбора.Добавить(ТекущийАдрес.ЗначенияПолей, ТекущийАдрес.ПредставлениеАдреса);
		КонецЦикла;
		
		Представление = Новый ФорматированнаяСтрока(НСтр("ru = 'Добавить адрес'"), , , , "ДобавитьАдрес");
		АдресДоставки.СписокВыбора.Добавить("ДобавитьАдрес", Представление);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекущийТовар(ИдентификаторПредложения)
	
	СтрокиТовара = Товары.НайтиСтроки(Новый Структура("ИдентификаторПредложения", ИдентификаторПредложения));
	
	Если Не ЗначениеЗаполнено(СтрокиТовара) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиЗаказа = Заказы.НайтиСтроки(Новый Структура("ИдентификаторСвязи", СтрокиТовара[0].ИдентификаторСвязи));
	
	Если Не ЗначениеЗаполнено(СтрокиЗаказа) Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Заказы.ТекущаяСтрока = СтрокиЗаказа[0].ПолучитьИдентификатор();
	
	Элементы.Товары.ТекущаяСтрока = СтрокиТовара[0].ПолучитьИдентификатор();
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНоменклатуруПоЗаказу(ИдентификаторСвязи, ТоварыЗаказа, НарушенияОграничений)
	
	Для каждого ТекущийТовар Из ТоварыЗаказа Цикл
		
		НоваяСтрока = Товары.Добавить();
		
		НоваяСтрока.ПредставлениеНоменклатуры     = ТекущийТовар.НаименованиеТовара;
		НоваяСтрока.Артикул                       = ТекущийТовар.Артикул;
		НоваяСтрока.ПредставлениеЕдиницыИзмерения = ТекущийТовар.ЕдиницаИзмерения.КраткоеНаименование;
		НоваяСтрока.Количество                    = ТекущийТовар.Количество;
		НоваяСтрока.Цена                          = ТекущийТовар.Цена;
		НоваяСтрока.ЦенаДоСкидки                  = ТекущийТовар.ЦенаДоСкидки;
		НоваяСтрока.ПредставлениеЦены             = ТорговыеПредложенияСлужебный.ПредставлениеЦены(ТекущийТовар.Цена);
		НоваяСтрока.СуммаСНДС                     = ТекущийТовар.Итоги.СуммаСНДС;
		НоваяСтрока.СуммаНДС                      = ТекущийТовар.Итоги.СуммаНДС;
		НоваяСтрока.СтавкаНДС                     = ТекущийТовар.СтавкаНДС;
		НоваяСтрока.Состояние                     = ТекущийТовар.СтатусДоступностиТовара;
		НоваяСтрока.ИдентификаторСвязи            = ИдентификаторСвязи;
		НоваяСтрока.АдресКомандыУдалить           = ТекущийТовар.АдресаКоманд.УдалитьТовар;
		НоваяСтрока.АдресКомандыИзменитьКоличество = ТекущийТовар.АдресаКоманд.УстановитьКоличество;
		НоваяСтрока.ИдентификаторПредложения      = ТекущийТовар.Идентификатор;
		НоваяСтрока.НарушенияОграничений          = НарушениеПоЗаказу(НарушенияОграничений, ТекущийТовар.Идентификатор);
		НоваяСтрока.ЕстьНарушенияОграничений      = ЗначениеЗаполнено(НоваяСтрока.НарушенияОграничений);
		
		НоваяСтрока.МинимальноеКоличество = 
			?(ТекущийТовар.МинимальноеКоличество = 0, 
				1, 
				ТекущийТовар.МинимальноеКоличество);
		
		НоваяСтрока.КратностьУпаковки = 
			?(ТекущийТовар.КратностьУпаковки = 0, 
				1, 
				ТекущийТовар.КратностьУпаковки);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриАктивацииЗаказаНаСервере()
	
	Если Элементы.Заказы.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьДанныеФормыВСтрокуЗаказа();
	
	ТекущиеДанные = Заказы.НайтиПоИдентификатору(Элементы.Заказы.ТекущаяСтрока);
	ИдентификаторТекущегоЗаказа = ТекущиеДанные.ИдентификаторСвязи;
	
	ОтборСтрок = Новый ФиксированнаяСтруктура(
		Новый Структура("ИдентификаторСвязи", ТекущиеДанные.ИдентификаторСвязи));
	
	Элементы.Товары.ОтборСтрок = ОтборСтрок;
	
	ПоляЗаполненияЗаказа = РедактируемыеПоляЗаказа();
	ПоляЗаполнения = СтрРазделить(ПоляЗаполненияЗаказа, ",", Ложь);
	Для Каждого Поле Из ПоляЗаполнения Цикл
		НаименованиеПоля = СтрЗаменить(СокрЛ(Поле), Символы.ПС, "");
		Если ЗначениеЗаполнено(ТекущиеДанные[НаименованиеПоля]) Тогда
			ЭтотОбъект[НаименованиеПоля] = ТекущиеДанные[НаименованиеПоля];
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьАдресаЗаказа(ЭтотОбъект, ТекущиеДанные);
	
	ШаблонЗаголовка = НСтр("ru = 'Детали %1 для %2'");
	
	ПредставлениеДокумента = НСтр("ru = 'заказа'");
	
	ЭтоЗапросЦен = ЭтоЗапросЦен(ТекущиеДанные);
	
	Если ЭтоЗапросЦен Тогда
		ПредставлениеДокумента = НСтр("ru = 'запроса цен'");
	КонецЕсли;
	
	Элементы.НадписьДеталиЗаказа.Заголовок = СтрШаблон(
		ШаблонЗаголовка, ПредставлениеДокумента, ТекущиеДанные.ПредставлениеПоставщика);
	
	ПредупреждениеПоЗаказу = "";
	Элементы.КартинкаПредупреждение.Видимость = Ложь;
	Если ЗначениеЗаполнено(ТекущиеДанные.НарушенияОграничений) Тогда
		ПредупреждениеПоЗаказу = ТекущиеДанные.НарушенияОграничений[0].Значение;
		Элементы.КартинкаПредупреждение.Видимость = Истина;
	КонецЕсли;
	
	УстановитьЗаголовокПодсказкуАдреса(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьУстановитьТекущийЗаказ(АдресЗаказа)
	
	СтрокиЗаказа = Заказы.НайтиСтроки(Новый Структура("ИдентификаторЗаказаВСервисе", АдресЗаказа));
	
	Если Не ЗначениеЗаполнено(СтрокиЗаказа) Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Заказы.ТекущаяСтрока = СтрокиЗаказа[0].ПолучитьИдентификатор();
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьДанныеЗаказа()
	
	// Поля заполнения формы.
	ПоляЗаполнения = "НаименованиеПокупателя, ЭлектроннаяПочта, 
		|Телефон, КомментарийКЗаказу, АдресДоставки,
		|СуммаСНДС, СуммаНДС";
	
	ПоляДляОчистки = СтрРазделить(ПоляЗаполнения, ",", Ложь);
	
	Для Каждого ЭлементКоллекции Из ПоляДляОчистки Цикл
		ЭтотОбъект[СокрЛП(ЭлементКоллекции)] = "";
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗаказы(ЗаказыКорзины, ЗаказыДоПерезаполнения = Неопределено)
	
	ЕстьНарушения = Ложь;
	
	Для каждого ТекущийЗаказ Из ЗаказыКорзины Цикл
		
		НоваяСтрока = Заказы.Добавить();
		
		ЭтоЗапросЦен = ЗначениеЗаполнено(ТекущийЗаказ.АдресаКоманд.СоздатьЗапросЦен);
		
		Если ЭтоЗапросЦен Тогда
			НоваяСтрока.ПредставлениеЗаказа = СтрШаблон(НСтр("ru = 'Запрос цен для %1'"), ТекущийЗаказ.Поставщик.Наименование);
		Иначе
			НоваяСтрока.ПредставлениеЗаказа = СтрШаблон(НСтр("ru = 'Заказ для %1'"), ТекущийЗаказ.Поставщик.Наименование);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекущийЗаказ.НарушенияОграничений) Тогда
			
			НоваяСтрока.НарушенияОграничений = НарушениеПоЗаказу(ТекущийЗаказ.НарушенияОграничений);
			
			Если ЗначениеЗаполнено(НоваяСтрока.НарушенияОграничений) Тогда
				ЕстьНарушения = Истина;
			КонецЕсли;
			
		КонецЕсли;

		НоваяСтрока.ПредставлениеПоставщика  = ТекущийЗаказ.Поставщик.Наименование;
		НоваяСтрока.КоличествоПозиций        = ТекущийЗаказ.Товары.Количество();
		НоваяСтрока.СуммаСНДС                = ТекущийЗаказ.Итоги.СуммаСНДС;
		НоваяСтрока.СуммаНДС                 = ТекущийЗаказ.Итоги.СуммаНДС;
		НоваяСтрока.Валюта                   = ТекущийЗаказ.Итоги.Валюта;
		НоваяСтрока.ИдентификаторСвязи       = Новый УникальныйИдентификатор;
		НоваяСтрока.Организация              = Организация;
		НоваяСтрока.Картинка                 = ?(ЗначениеЗаполнено(НоваяСтрока.НарушенияОграничений), 2, 0);
		НоваяСтрока.АдресКомандыСоздатьЗаказ = ТекущийЗаказ.АдресаКоманд.СоздатьЗаказ;
		НоваяСтрока.АдресКомандыСоздатьЗапросЦен = ТекущийЗаказ.АдресаКоманд.СоздатьЗапросЦен;
		НоваяСтрока.ИдентификаторЗаказаВСервисе  = ТекущийЗаказ.АдресаКоманд.АдресЗаказа;
		НоваяСтрока.РегионыДоставки.Загрузить(ТекущийЗаказ.ДанныеДоставки.РегионыДоставки);
		НоваяСтрока.АдресаСамовывоза.Загрузить(ТекущийЗаказ.ДанныеДоставки.АдресаСамовывоза);
		
		Если ЗначениеЗаполнено(НоваяСтрока.РегионыДоставки) 
			И ЗначениеЗаполнено(НоваяСтрока.АдресаСамовывоза) Тогда
			
			НоваяСтрока.СпособДоставки = "Доставка";
			
		ИначеЕсли Не ЗначениеЗаполнено(НоваяСтрока.РегионыДоставки) Тогда
			НоваяСтрока.СпособДоставки = "Самовывоз";
		Иначе
			НоваяСтрока.СпособДоставки = "Доставка";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЗаказыДоПерезаполнения) Тогда
			ЗаполнитьРедактируемыеДанныеЗаказа(НоваяСтрока, ЗаказыДоПерезаполнения);
		КонецЕсли;
		
		ЗаполнитьНоменклатуруПоЗаказу(
			НоваяСтрока.ИдентификаторСвязи, 
			ТекущийЗаказ.Товары, 
			ТекущийЗаказ.НарушенияОграничений);
		
	КонецЦикла;
	
	Если ЕстьНарушения Тогда
		Элементы.Заказы.КартинкаСтрок = БиблиотекаКартинок.ПризнакПодключенияОрганизацииБизнесСеть;
	Иначе
		Элементы.Заказы.КартинкаСтрок = Новый Картинка;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НарушениеПоЗаказу(СписокНарушений, ИдентификаторПредложения = "")
	
	СтрокиНарушений = СписокНарушений.НайтиСтроки(
		Новый Структура("ИдентификаторПредложения", ИдентификаторПредложения));
	
	Результат = Новый СписокЗначений;
	
	Для Каждого ТекущееНарушение Из СтрокиНарушений Цикл
		Результат.Добавить(ТекущееНарушение.ТекстСообщения);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьРедактируемыеДанныеЗаказа(СтрокаДанных, ЗаказыДоПерезаполнения)
	
	Если Не ЗначениеЗаполнено(ЗаказыДоПерезаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиДанныхЗаказа = ЗаказыДоПерезаполнения.НайтиСтроки(
		Новый Структура("ИдентификаторЗаказаВСервисе", СтрокаДанных.ИдентификаторЗаказаВСервисе));
	
	Если Не ЗначениеЗаполнено(СтрокиДанныхЗаказа) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(СтрокаДанных, СтрокиДанныхЗаказа[0]);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключениеОрганизацииПродолжение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.СтатусПодключения = "Подключена" Тогда
		
		СохранитьДанныеФормыИЗапуститьЧтениеКорзины();
		ОжидатьЗавершенияЧтенияКорзины();
		
	КонецЕсли;
	
	УстановитьВидимостьДекорацияРегистрация();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьДанныеФормыИЗапуститьЧтениеКорзины();
	
	СохранитьДанныеФормыВСтрокуЗаказа();
	ЗапуститьЧтениеКорзины();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗаказНаКлиенте()
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	
	ПроверитьСообщитьОНарушенииОграничений(Отказ);
	
	ПроверитьЗаполнениеДанных(Отказ);
	
	Если Не ПроверитьЗаполнение() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПодключениеОрганизацииПродолжение", ЭтотОбъект);
	ИнтеграцияТорговыеПредложенияКлиент.ОткрытьФормуПодключенияОрганизацииСПроверкойПодключения(
		Организация, ЭтотОбъект, Оповещение, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОтправитьЗаказПеречитатьКорзинуВФоне();
	
КонецПроцедуры

&НаСервере
Функция ПараметрыСозданияЗаказа()
	
	ТекущиеДанные = Заказы.НайтиПоИдентификатору(Элементы.Заказы.ТекущаяСтрока);
	
	ПараметрыФормы = ТорговыеПредложенияСлужебный.ОписаниеПараметровСозданияЗаказаКорзины();
	
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, ТекущиеДанные);
	
	Если ЗначениеЗаполнено(ТекущиеДанные.АдресКомандыСоздатьЗаказ) Тогда
		ПараметрыФормы.АдресКоманды = ТекущиеДанные.АдресКомандыСоздатьЗаказ;
	ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.АдресКомандыСоздатьЗапросЦен) Тогда
		ПараметрыФормы.АдресКоманды = ТекущиеДанные.АдресКомандыСоздатьЗапросЦен;
	КонецЕсли;
	
	СтрокиДанных = Неопределено;
	
	Если ТекущиеДанные.СпособДоставки = "Самовывоз" Тогда
		СтрокиДанных = ТекущиеДанные.АдресаСамовывоза.НайтиСтроки(
			Новый Структура("ИдентификаторФИАС", ТекущиеДанные.АдресДоставки));
	Иначе
		СтрокиДанных = АдресаОрганизации.НайтиСтроки(
			Новый Структура("ЗначенияПолей, Организация", ТекущиеДанные.АдресДоставки, ТекущиеДанные.Организация));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокиДанных) Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыФормы, СтрокиДанных[0]);
	КонецЕсли;
	
	ПараметрыФормы.АдресКорзины = АдресКорзины;
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаКлиенте
Процедура ОтправитьЗаказПеречитатьКорзинуВФоне()
	
	ПредставлениеЗаказа = Элементы.Заказы.ТекущиеДанные.ПредставлениеЗаказа;
	
	ПараметрыОповещения = Новый Структура("ПредставлениеЗаказа", ПредставлениеЗаказа);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ОтправитьЗаказПеречитатьКорзинуЗавершение", ЭтотОбъект, ПараметрыОповещения);
	
	ДлительнаяОперация = СоздатьЗаказПеречитатьКорзинуВФоне(УникальныйИдентификатор);

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ВыводитьСообщения = Истина;

	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция СоздатьЗаказПеречитатьКорзинуВФоне(УникальныйИдентификатор)
	
	СохранитьДанныеФормыВСтрокуЗаказа();
	
	НастроитьЭлементыДлительнойОперации(Истина);
	
	ПараметрыСозданияЗаказа = ПараметрыСозданияЗаказа();
	
	Результат = ТорговыеПредложенияСлужебный.СоздатьЗаказПеречитатьКорзинуВФоне(
		ПараметрыСозданияЗаказа, УникальныйИдентификатор);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура НастроитьЭлементыДлительнойОперации(ЭтоНачалоДлительнойОперации, ЭтоОперацияСТоваром = Ложь)
	
	Если ЭтоОперацияСТоваром Тогда
		Элементы.ТоварыУдалитьТовар.Доступность = Не ЭтоНачалоДлительнойОперации;
	Иначе
		Элементы.КартинкаДлительнаяОперация.Видимость = ЭтоНачалоДлительнойОперации;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗаказПеречитатьКорзинуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	
	БизнесСетьСлужебныйКлиент.ВывестиСообщенияФоновогоЗадания(Результат, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПеречитатьКорзинуНаСервере(Результат.АдресРезультата, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьПодсказкуИсходящихДокументов(ДополнительныеПараметры.ПредставлениеЗаказа);
	
	БизнесСетьСлужебныйКлиент.ПоказатьОповещениеБизнесСети(НСтр("ru = 'Заказ отправлен'"));
	
	ТорговыеПредложенияКлиент.ОповеститьОбИзмененииКорзины(Результат.АдресРезультата, ЭтотОбъект);
	
	Если Заказы.Количество() = 0 Тогда
		ОчиститьДанныеЗаказа();
		ПоказатьСтраницуУспешнойОтправки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоЗапросЦен(ДанныеЗаказа)
	
	Результат = ДанныеЗаказа.СуммаСНДС = 0;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ВопросПриУдаленияАдресаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьАдресИзСписка();
	
	ТекущиеДанные = Элементы.Заказы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ЗаполнитьАдресаЗаказа(ЭтотОбъект, ТекущиеДанные);
	
	УстановитьЗаголовокПодсказкуАдреса(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьАдресИзСписка()
	
	СтрокаАдреса = СтрокаАдресаПоЗначениюАдреса(АдресаОрганизации, Организация, АдресДоставки);
	
	Если СтрокаАдреса <> Неопределено Тогда
		АдресаОрганизации.Удалить(СтрокаАдреса);
	КонецЕсли;
	
	АдресДоставки = "";
	
	Элементы.ДекорацияУдалитьАдрес.Видимость = Ложь;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти