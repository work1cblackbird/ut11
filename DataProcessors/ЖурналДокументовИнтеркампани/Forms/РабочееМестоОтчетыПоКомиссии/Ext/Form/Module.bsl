
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Возврат при получении формы для анализа.
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("КлючНазначенияФормы")
		И ЗначениеЗаполнено(Параметры.КлючНазначенияФормы) Тогда
		
		КлючНазначенияИспользования = Параметры.КлючНазначенияФормы;
		
	Иначе
		КлючНазначенияИспользования = КлючНазначенияФормыПоУмолчанию();
	КонецЕсли;
	
	ВосстановитьНастройки(Параметры);
	НастроитьССылкуСмТакже();	
	// При открытии из обработки "ОперацииЗакрытияМесяца" необходимо скрыть отборы.
	Элементы.Комитент.Видимость    = Не Параметры.Свойство("ПериодРегистрации");
	Элементы.Комиссионер.Видимость = Не Параметры.Свойство("ПериодРегистрации");
	
	ЗаполнитьТоварыКОформлению();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПредставлениеПериода = ОбщегоНазначенияУТКлиент.ПолучитьПредставлениеПериодаРегистрации(Период.ДатаНачала);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы Тогда
		СохранитьНастройки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ОтчетПоКомиссииМеждуОрганизациями"
		Или ИмяСобытия = "Запись_ОтчетПоКомиссииМеждуОрганизациямиОСписании" Тогда
		
		ЗаполнитьТоварыКОформлению();
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредставлениеПериодаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Оповещение     = Новый ОписаниеОповещения("ПредставлениеПериодаНачалоВыбораЗавершение", ЭтотОбъект);
	ПараметрыФормы = Новый Структура("Значение, РежимВыбораПериода", Период.ДатаНачала, "МЕСЯЦ");
	
	ОткрытьФорму("ОбщаяФорма.ВыборПериода", ПараметрыФормы, ЭтотОбъект, ЭтотОбъект.УникальныйИдентификатор, , ,
		Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаНачалоВыбораЗавершение(ВыбранныйПериод, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйПериод <> Неопределено Тогда
		ДатаНачала    = НачалоМесяца(ВыбранныйПериод);
		ДатаОкончания = КонецМесяца(ВыбранныйПериод);
		
		Период.ДатаНачала    = ДатаНачала;
		Период.ДатаОкончания = ДатаОкончания;
		
		ПредставлениеПериода = ОбщегоНазначенияУТКлиент.ПолучитьПредставлениеПериодаРегистрации(Период.ДатаНачала);
		
		ЗаполнитьТоварыКОформлению();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ОбщегоНазначенияУТКлиент.РегулированиеПредставленияПериодаРегистрации(Направление, СтандартнаяОбработка,
		Период.ДатаНачала, ПредставлениеПериода);
	
	Период.ДатаОкончания = КонецМесяца(Период.ДатаНачала);
	
	ДатаНачала    = Период.ДатаНачала;
	ДатаОкончания = Период.ДатаОкончания;
	
	ЗаполнитьТоварыКОформлению();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодДатаНачалаПриИзменении(Элемент)
	
	ОчиститьСообщения();
	УстановитьОтборПоПериоду();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодДатаОкончанияПриИзменении(Элемент)
	
	ОчиститьСообщения();
	УстановитьОтборПоПериоду();
	
КонецПроцедуры

&НаКлиенте
Процедура КомитентПриИзменении(Элемент)
	
	ЗаполнитьТоварыКОформлению();
	
КонецПроцедуры

&НаКлиенте
Процедура КомиссионерПриИзменении(Элемент)
	
	ЗаполнитьТоварыКОформлению();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовСписков

&НаКлиенте
Процедура ТоварыКОформлениюОтчетовКомитентуПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьОтчетПоКомиссии(Команда)
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина,
		"Обработка.ЖурналДокументовИнтеркампани.РабочееМестоОтчетыПоКомиссии.Команда.СоздатьОтчетПоКомиссии");
	
	Строка = Элементы.ТоварыКОформлениюОтчетовКомитенту.ТекущиеДанные;
	
	Если Не ОбщегоНазначенияУТКлиент.ВыбраныДокументыКОформлению(
		Строка, ПараметрыЖурнала()) Тогда
		Возврат;
	КонецЕсли;
	
	Если Строка <> Неопределено Тогда
		
		СтруктураОснование = Новый Структура();
		СтруктураОснование.Вставить("НачалоПериода",                 Период.ДатаНачала);
		СтруктураОснование.Вставить("КонецПериода",                  Период.ДатаОкончания);
		СтруктураОснование.Вставить("Организация",                   Строка.Комитент);
		СтруктураОснование.Вставить("Комиссионер",                   Строка.Комиссионер);
		СтруктураОснование.Вставить("Валюта",                        Строка.Валюта);
		СтруктураОснование.Вставить("Договор",                       Строка.Договор);
		СтруктураОснование.Вставить("ВидЦены",                       Строка.ВидЦены);
		СтруктураОснование.Вставить("НалогообложениеНДС",			 Строка.НалогообложениеНДС);
		СтруктураОснование.Вставить("ЗаполнятьПоТоварамКОформлению", Истина);
		
		СтруктураПараметры = Новый Структура("Основание", СтруктураОснование);
		
		Если Строка.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ОтчетПоКомиссииМеждуОрганизациями") Тогда
			ОткрытьФорму("Документ.ОтчетПоКомиссииМеждуОрганизациями.ФормаОбъекта", СтруктураПараметры, ЭтотОбъект);
		Иначе
			ОткрытьФорму("Документ.ОтчетПоКомиссииМеждуОрганизациямиОСписании.ФормаОбъекта", СтруктураПараметры, ЭтотОбъект);
		КонецЕсли;
	Иначе
		ПоказатьПредупреждение(,НСтр("ru = 'Команда не может быть выполнена для указанного объекта!'"));
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗаполнитьТоварыКОформлению();
	
КонецПроцедуры

&НаКлиенте
Процедура СмТакжеВРаботеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	СтруктураБыстрогоОтбора = Новый Структура;
	СтруктураБыстрогоОтбора.Вставить("Организация", Комитент);
	СтруктураБыстрогоОтбора.Вставить("МестоХранения", Неопределено);
	
	ПараметрыЖурнала = Новый Структура;
	ПараметрыЖурнала.Вставить("СтруктураБыстрогоОтбора",СтруктураБыстрогоОтбора);
	ПараметрыЖурнала.Вставить("КлючНазначенияИспользования","ОтчетПоКомиссииМеждуОрганизациями");
		
	ПараметрыЖурнала.Вставить("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора);
	
	ОткрытьФорму(НавигационнаяСсылкаФорматированнойСтроки, ПараметрыЖурнала); 
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция КлючНазначенияФормыПоУмолчанию()
	
	Возврат "ДокументыИнтеркампани";
	
КонецФункции

&НаСервере
Процедура ВосстановитьНастройки(Параметры)
	
	Если Параметры.Свойство("ПериодРегистрации") Тогда
		ФормаОткрытаПоГиперссылке = Истина;
		
		Комитент    = Параметры.Организация;
		Комиссионер = Параметры.Организация;
		
		ДатаНачала     = НачалоМесяца(Параметры.ПериодРегистрации);
		ДатаОкончания = КонецМесяца(Параметры.ПериодРегистрации);
		
		Период.ДатаНачала    = ДатаНачала;
		Период.ДатаОкончания = ДатаОкончания;
	ИначеЕсли Параметры.Свойство("ОтборыФормыСписка") Тогда
		ФормаОткрытаПоГиперссылке = Истина;
		
		Комитент = Параметры.ОтборыФормыСписка.Организация;
		
		ДатаНачала     = НачалоМесяца(ТекущаяДатаСеанса());;
		ДатаОкончания = КонецМесяца(ДатаНачала);
		
		Период.ДатаНачала    = ДатаНачала;
		Период.ДатаОкончания = ДатаОкончания;
	Иначе
		ФормаОткрытаПоГиперссылке = Ложь;
		
		КлючОбъекта = "Обработка.ЖурналДокументовИнтеркампани.Форма.РабочееМестоОтчетыПоКомиссии_1";
		Настройки   = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(КлючОбъекта, КлючНазначенияИспользования);
		
		Если Настройки <> Неопределено Тогда
			Период         = Настройки.Период;
			ДатаНачала     = Период.ДатаНачала;
			ДатаОкончания = Период.ДатаОкончания;
			
			Комитент    = Настройки.Комитент;
			Комиссионер = Настройки.Комиссионер;
		Иначе
			ДатаНачала     = НачалоМесяца(ТекущаяДатаСеанса());
			ДатаОкончания = КонецМесяца(ДатаНачала);
			
			Период.ДатаНачала    = ДатаНачала;
			Период.ДатаОкончания = ДатаОкончания;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТоварыКОформлению()
	
	ТекстЗапроса = Обработки.ЖурналДокументовИнтеркампани.ТекстЗапросаОформляемыеОтчетыПоКомиссии();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;
	
	ТекстОтчетОПродажах = НСтр("ru='Отчет о продажах'");
	ТекстОтчетОСписании = НСтр("ru='Отчет о списании'");
	
	КонецПериода  = Новый Граница(КонецДня(Период.ДатаОкончания), ВидГраницы.Включая);
	
	Запрос.УстановитьПараметр("Комитент", Комитент);
	Запрос.УстановитьПараметр("УстановленОтборПоКомитенту", ЗначениеЗаполнено(Комитент));
	Запрос.УстановитьПараметр("Комиссионер", Комиссионер);
	Запрос.УстановитьПараметр("УстановленОтборПоКомиссионеру", ЗначениеЗаполнено(Комиссионер));
	Запрос.УстановитьПараметр("НачГраница", Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонГраница", КонецПериода);
	Запрос.УстановитьПараметр("ТекстОтчетОПродажах", ТекстОтчетОПродажах);
	Запрос.УстановитьПараметр("ТекстОтчетОСписании", ТекстОтчетОСписании);
	Запрос.УстановитьПараметр("ОтобратьПоПродаже", Истина);
	Запрос.УстановитьПараметр("ОтобратьПоСписанию", Истина);
	
	НастройкиНалоговУчетныхПолитик.ДополнитьМенеджерВременныхТаблицДействующимиПараметрамиНалоговУчетныхПолитик(
		Метаданные.РегистрыСведений.НастройкиСистемыНалогообложения.Имя,
		Запрос.МенеджерВременныхТаблиц,
		КонецДня(Период.ДатаОкончания),
		"ВТУчетныеПолитикиОрганизаций",
		Комиссионер);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	ТоварыКОформлениюОтчетовКомитенту.Загрузить(РезультатЗапроса);
	
	Элементы.ТоварыКОформлениюОтчетовКомитенту.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
	
	Если ФормаОткрытаПоГиперссылке Тогда
		Возврат;
	КонецЕсли;
	
	КлючОбъекта = "Обработка.ЖурналДокументовИнтеркампани.Форма.РабочееМестоОтчетыПоКомиссии_1";
	ИменаСохраняемыхРеквизитов = "Период,Комитент,Комиссионер";
	
	Настройки = Новый Структура(ИменаСохраняемыхРеквизитов);
	ЗаполнитьЗначенияСвойств(Настройки, ЭтаФорма);
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(КлючОбъекта, КлючНазначенияИспользования, Настройки);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоПериоду()
	
	Отказ = Ложь;
	
	Если Период.ДатаНачала > Период.ДатаОкончания Тогда
		Период.ДатаНачала		= ДатаНачала;
		Период.ДатаОкончания	= ДатаОкончания;
		
		ТекстСообщения = НСтр("ru = 'Дата начала периода не может быть больше даты окончания периода'");
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ПериодДатаНачала", "Период.ДатаНачала", Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Период.ДатаНачала)
		Или Не ЗначениеЗаполнено(Период.ДатаОкончания)
		Или Месяц(Период.ДатаНачала) <> Месяц(Период.ДатаОкончания)
		Или Год(Период.ДатаНачала) <> Год(Период.ДатаОкончания) Тогда
		
		Период.ДатаНачала		= ДатаНачала;
		Период.ДатаОкончания	= ДатаОкончания;
		
		ТекстСообщения = НСтр("ru = 'Дата начала периода и дата окончания периода должны находиться в пределах одного месяца'");
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДатаНачала		= Период.ДатаНачала;
	ДатаОкончания	= Период.ДатаОкончания;
	
	ЗаполнитьТоварыКОформлению();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьССылкуСмТакже()
	
	ПараметрыФормирования = Новый Структура;
	ПараметрыФормирования.Вставить("КлючНазначенияИспользования", "ОтчетПоКомиссииМеждуОрганизациями");
	
	МассивМенеджеровРасчетаСмТакжеВРаботе = Новый Массив();
	МассивМенеджеровРасчетаСмТакжеВРаботе.Добавить("Обработка.ЖурналДокументовИнтеркампани");
		
	СмТакжеВРаботе = ОбщегоНазначенияУТ.СформироватьГиперссылкуСмТакжеВРаботе(МассивМенеджеровРасчетаСмТакжеВРаботе, ПараметрыФормирования);
	Элементы.СмТакжеВРаботе.Видимость = ЗначениеЗаполнено(СмТакжеВРаботе);
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыЖурнала()
	
	СтруктураБыстрогоОтбора = Новый Структура;
	СтруктураБыстрогоОтбора.Вставить("Организация",   Комиссионер);
	СтруктураБыстрогоОтбора.Вставить("МестоХранения", Неопределено);
	
	ПараметрыЖурнала = Новый Структура;
	ПараметрыЖурнала.Вставить("СтруктураБыстрогоОтбора",	СтруктураБыстрогоОтбора);
	ПараметрыЖурнала.Вставить("ИмяРабочегоМеста",			"ЖурналДокументовИнтеркампани");
	ПараметрыЖурнала.Вставить("КлючНазначенияФормы",		КлючНазначенияИспользования);
	ПараметрыЖурнала.Вставить("СинонимЖурнала", НСтр("ru = 'Документы между организациями'"));
	
	Возврат ПараметрыЖурнала;
	
КонецФункции

#КонецОбласти
