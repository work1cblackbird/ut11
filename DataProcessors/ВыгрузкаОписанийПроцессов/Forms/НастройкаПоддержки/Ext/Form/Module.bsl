
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СформироватьТекстОписаниеПоддержки();
		
	ТекущаяВерсия = ДемонстрационныеСценарии.ПредставлениеТекущаяВерсияКонфигурации();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура ПоставитьНаПоддержку(Команда)
	
	ОчиститьСообщения(); 
	
	Отказ = Ложь;
	
	ПроверитьДоступностьВыполненияПостановкиНаПоддержку(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОповещениеООтветеНаВопрос = Новый ОписаниеОповещения("ПослеОтветаНаВопросПостановкаНаПоддержку", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Хотите поставить ""Описания процессов""  на поддержку от текущей версии?'");
	
	ПоказатьВопрос(ОповещениеООтветеНаВопрос, ТекстВопроса,
	              РежимДиалогаВопрос.ДаНет,,
	              , НСтр("ru = 'Постановка на поддержку'"));
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроверитьДоступностьВыполненияПостановкиНаПоддержку(Отказ)
	
	
	Если ПустаяСтрока(Поставщик) Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Для постановки на поддержку от текущей версии требуется указать значение ""Поставщик""'"),
			, 
			"Поставщик",
			,
			Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопросПостановкаНаПоддержку(ОтветНаВопрос, ДополнительныеПараметры) Экспорт

	Если ОтветНаВопрос <> КодВозвратаДиалога.Да Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ДлительнаяОперация = ВыполнитьПостановкуНаПоддержкуДлительнаяОперация(УникальныйИдентификатор);
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ВыполнитьПостановкуНаПоддержкуЗавершение(ДлительнаяОперация, ДополнительныеПараметры);
		
	ИначеЕсли ДлительнаяОперация.Статус = "Выполняется" Тогда
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.Заголовок = НСтр("ru = 'Постановка на поддержку описаний процессов'");
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ВыполнитьПостановкуНаПоддержкуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ВыполнитьПостановкуНаПоддержкуДлительнаяОперация(УникальныйИдентификатор)
	
	ПараметрыФормирования = Новый Структура;
	ПараметрыФормирования.Вставить("Поставщик",  Поставщик);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Постановка на поддержку от текущей версии описаний процессов'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне("ДемонстрационныеСценарии.ПоставитьОписанияПроцессовНаПоддержкуОтТекущейВерсии",
	                                                       ПараметрыФормирования, ПараметрыВыполнения);
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьПостановкуНаПоддержкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		ОповеститьОРезультатахПостановкиНаПоддержку(Результат, ДополнительныеПараметры);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОРезультатахПостановкиНаПоддержку(Результат, ДополнительныеПараметры)

	Если Не ЭтоАдресВременногоХранилища(Результат.АдресРезультата) Тогда 
		Возврат;
	КонецЕсли;
	
	ДанныеВыполнения =  ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	
	Если ДанныеВыполнения.Успешно Тогда
		
		ТекстОповещения = НСтр("ru = 'Постановка описаний на поддержку успешно завершена'");
		СформироватьТекстОписаниеПоддержки();
		
	Иначе
		
		ТекстОповещения = НСтр("ru = 'Постановка описаний на поддержку завершилась с ошибкой.'");
		
		ТекстСообщенияПользователю = СтрШаблон(НСтр("ru = 'Операция не выполнена по причине: %1'"), ДанныеВыполнения.СообщениеОбОшибке);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщенияПользователю);
		
	КонецЕсли;
		
	ПоказатьОповещениеПользователя(НСтр("ru = 'Постановка описаний на поддержку'") ,, ТекстОповещения, БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьТекстОписаниеПоддержки()
	
	РезультатЧтения = ДемонстрационныеСценарии.РезультатЧтенияКонстантыОписанияПроцессовНаПоддержке(); 
	
	ТекстЗаголовка = "";
	
	Если НЕ ПустаяСтрока(РезультатЧтения.ДанныеКонфигурации) 
		И Не ПустаяСтрока(РезультатЧтения.Поставщик) Тогда
		
		ТекстЗаголовка = ДемонстрационныеСценарииКлиентСервер.ТекстОписанияПоставки(РезультатЧтения.ДанныеКонфигурации, РезультатЧтения.Поставщик);
		
	КонецЕсли;
	
	ОписанияПроцессовНаПоддержке = ТекстЗаголовка;
	
КонецПроцедуры

#КонецОбласти


