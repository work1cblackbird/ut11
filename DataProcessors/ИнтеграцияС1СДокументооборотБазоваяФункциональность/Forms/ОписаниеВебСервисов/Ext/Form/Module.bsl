#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОписаниеВебСервисов = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ОписаниеВебСервисов();
	
	Параметры.Свойство("Ссылка", Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ПодключитьОбработчикОжидания("ПерейтиКСсылке", 0.2, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолеHTMLДокументаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Не ЗначениеЗаполнено(ДанныеСобытия.HRef) Тогда
		Возврат;
	КонецЕсли;
	
	Позиция = СтрНайти(ДанныеСобытия.HRef, "#", НаправлениеПоиска.СКонца);
	Ссылка = Сред(ДанныеСобытия.HRef, Позиция + 1);
	ПодключитьОбработчикОжидания("ПерейтиКСсылке", 0.2, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПерейтиКСсылке()
	
	// При проблемах с совместимостью браузеров откроем документ без позиционирования
	// на ссылке, проглотив исключение как некритичное.
	Попытка
		Документ = Элементы.ПолеHTMLДокумента.Документ;
		Для Каждого Элемент Из Документ.querySelectorAll(СтрШаблон("a[name=""%1""]", Ссылка)) Цикл
			Элемент.scrollIntoView(Истина);
			Прервать;
		КонецЦикла;
	Исключение
		Возврат;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти