
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбработатьПереданныеПараметры();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокКонтрагентовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подобрать(Команда)
	
	ОписаниеОповещенияОПодборе = Новый ОписаниеОповещения("ПослеПодбораКонтрагентов", ЭтотОбъект);
	ПараметрыОткрытия = Новый Структура;
	
	Если ОграничивающийСписокКонтрагентов.Количество() > 0 Тогда
		Если ВариантОграниченияСписка = ПредопределенноеЗначение("Перечисление.ВариантыОграниченийСпискаКонтрагентовEDI.БелыйСписок") Тогда
			ПараметрыОткрытия.Вставить("ТолькоИзСпискаКонтрагентов", ОграничивающийСписокКонтрагентов);
		Иначе
			СписокНеПоказывать = СписокКонтрагентов.Скопировать();
			Для Каждого ЭлементСписка Из ОграничивающийСписокКонтрагентов Цикл
				СписокНеПоказывать.Добавить(ЭлементСписка.Значение);
			КонецЦикла;
			ПараметрыОткрытия.Вставить("НеПоказыватьКонтрагентов", СписокНеПоказывать);
		КонецЕсли;
	Иначе
		ПараметрыОткрытия.Вставить("НеПоказыватьКонтрагентов",   СписокКонтрагентов);
	КонецЕсли;
	
	СервисEDIКлиентПереопределяемый.ОткрытьФормуВыбораКонтрагентов(ЭтотОбъект, ПараметрыОткрытия, ОписаниеОповещенияОПодборе);
	
КонецПроцедуры

&НаКлиенте
Процедура Перенести(Команда)
	
	Закрыть(СписокКонтрагентов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбработатьПереданныеПараметры()
	
	СписокКонтрагентов               = Параметры.СписокКонтрагентов;
	ОграничивающийСписокКонтрагентов = Параметры.ОграничивающийСписокКонтрагентов;
	ВариантОграниченияСписка         = Параметры.ВариантОграниченияСписка;
	Заголовок = Параметры.Заголовок;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодбораКонтрагентов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Результат) = Тип("Массив") Тогда
		Для Каждого ЭлементМассива Из Результат Цикл
			СписокКонтрагентов.Добавить(ЭлементМассива);
		КонецЦикла;
	Иначе
		СписокКонтрагентов.Добавить(Результат);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

