
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	ДокументыEDI.ПостроитьДеревоСтатусов(ДеревоСтатусовEDI);
	ВариантОтбораПоАктуальностиВерсииEDI = Перечисления.ВариантыОтбораПоНеактуальнойВерсииEDI.НеУчитыватьОтборПоСтатусам;
	
	ОбработатьПереданныеПараметры();
	
	УправлениеДоступностью(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДеревоСтатусовEDIВыбранПриИзменении(Элемент)
	
	ДокументыEDIКлиент.ДеревоСтатусовВыбранПриИзменении(ЭтотОбъект, Элемент, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОтборПоСтатусамEDIПриИзменении(Элемент)
	
	УправлениеДоступностью(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОтборПоНеактуальнойВерсииEDIПриИзменении(Элемент)
	
	УправлениеДоступностью(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область ДеревоСтатусовEDI

&НаКлиенте
Процедура СнятьВсеСтатусы(Команда)
	
	ДокументыEDIКлиент.ИзменитьФлажкиПодчиненныхПриИзмененииРодителя(Ложь, ДеревоСтатусовEDI);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВсеСтатусы(Команда)
	
	ДокументыEDIКлиент.ИзменитьФлажкиПодчиненныхПриИзмененииРодителя(Истина, ДеревоСтатусовEDI);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборСтатусыНаНашейСтороне(Команда)
	
	ДокументыEDIКлиентСервер.УстановитьПометкиДеревоСтатусовСогласноВыбраннымСтатусам(
		ДеревоСтатусовEDI,
		ДокументыEDIКлиентСервер.МассивСтатусовНаСторонеПокупателя());
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборСтатусыНаСторонеКонтрагента(Команда)
	
	ДокументыEDIКлиентСервер.УстановитьПометкиДеревоСтатусовСогласноВыбраннымСтатусам(
		ДеревоСтатусовEDI,
		ДокументыEDIКлиентСервер.МассивСтатусовНаСторонеПродавца());
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	ДокументыEDI.УстановитьУсловноеОформлениеДеревоСтатусов(ЭтотОбъект, УсловноеОформление);
	
КонецПроцедуры

&НаКлиенте
Процедура Перенести(Команда)
	
	Закрыть(ДокументыEDIИнтеграцияКлиент.ПараметрыОтбораПоСтатусамEDIПоДаннымФормы(ЭтотОбъект));
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПереданныеПараметры()
	 
	ИспользоватьОтборПоСтатусамEDI = Параметры.ИспользоватьОтборПоСтатусамEDI;
	
	Если ТипЗнч(Параметры.МассивВыбранныхСтатусовEDI) = Тип("Массив") Тогда
		
		ДокументыEDIКлиентСервер.УстановитьПометкиДеревоСтатусовСогласноВыбраннымСтатусам(ДеревоСтатусовEDI, Параметры.МассивВыбранныхСтатусовEDI);
		
	КонецЕсли;
	
	ИспользоватьОтборПоНеактуальнойВерсииEDI = Параметры.ИспользоватьОтборПоНеактуальнойВерсииEDI;
	ВариантОтбораПоАктуальностиВерсииEDI     = Параметры.ВариантОтбораПоАктуальностиВерсииEDI;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеДоступностью(Форма)

	Форма.Элементы.ГруппаОтборПоАктуальнойВерсииОтступ.Доступность = Форма.ИспользоватьОтборПоНеактуальнойВерсииEDI;
	Форма.Элементы.ГруппаОтборыСтатусыEDI.Доступность              = Форма.ИспользоватьОтборПоСтатусамEDI;

КонецПроцедуры

#КонецОбласти