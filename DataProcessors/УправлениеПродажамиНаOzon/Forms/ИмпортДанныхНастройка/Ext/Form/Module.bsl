
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВариантЗагрузкиДанных          = Параметры.ВариантЗагрузкиДанных;
	ИмяИдентификатора              = Параметры.ИмяИдентификатора;
	РазмерПорцииЗагрузкиДанных     = Параметры.РазмерПорцииЗагрузкиДанных;
	СоздатьЭлементыНоменклатуры    = Параметры.СоздатьЭлементыНоменклатуры;
	ЗагрузитьЦеныПродажи           = Параметры.ЗагрузитьЦеныПродажи;
	ЗаписатьЦеныОтдельнойКомандой  = Параметры.ЗаписатьЦеныОтдельнойКомандой;
	ЗагрузитьИзображения           = Параметры.ЗагрузитьИзображения;
	СортироватьСтрокиДереваТоваров = Параметры.СортироватьСтрокиДереваТоваров;
	
	ОграничитьРазмерПорцииЗагрузкиДанных = ЗначениеЗаполнено(РазмерПорцииЗагрузкиДанных);
	
	Элементы.ЗагрузитьЦеныПродажи.Доступность = ПравоДоступа("Проведение", Метаданные.Документы.УстановкаЦенНоменклатуры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОграничитьРазмерПорцииЗагрузкиДанныхПриИзменении(Элемент)
	
	Если ОграничитьРазмерПорцииЗагрузкиДанных Тогда
		Если РазмерПорцииЗагрузкиДанных = 0 Тогда
			РазмерПорцииЗагрузкиДанных = 1000;
		КонецЕсли;
		РазмерПорцииЗагрузкиДанных = Макс(1, РазмерПорцииЗагрузкиДанных);
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура РазмерПорцииЗагрузкиДанныхПриИзменении(Элемент)
	
	Если РазмерПорцииЗагрузкиДанных = 0 Тогда
		ОграничитьРазмерПорцииЗагрузкиДанных = Ложь;
		УстановитьВидимостьДоступность();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьЦеныПродажиПриИзменении(Элемент)
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Результат = Новый Структура;    
	Результат.Вставить("ВариантЗагрузкиДанных",          ВариантЗагрузкиДанных);
	Результат.Вставить("ИмяИдентификатора",              ИмяИдентификатора);
	Результат.Вставить("РазмерПорцииЗагрузкиДанных",     ?(ОграничитьРазмерПорцииЗагрузкиДанных, РазмерПорцииЗагрузкиДанных, Неопределено));
	Результат.Вставить("СоздатьЭлементыНоменклатуры",    СоздатьЭлементыНоменклатуры);
	Результат.Вставить("ЗагрузитьЦеныПродажи",           ЗагрузитьЦеныПродажи);
	Результат.Вставить("ЗаписатьЦеныОтдельнойКомандой",  ЗаписатьЦеныОтдельнойКомандой);
	Результат.Вставить("ЗагрузитьИзображения",           ЗагрузитьИзображения);
	Результат.Вставить("СортироватьСтрокиДереваТоваров", СортироватьСтрокиДереваТоваров);
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьВидимостьДоступность()
	
	Элементы.РазмерПорцииЗагрузкиДанных.Видимость           = ОграничитьРазмерПорцииЗагрузкиДанных;
	Элементы.ОграничитьРазмерПорцииЗагрузкиДанных.Заголовок = ?(ОграничитьРазмерПорцииЗагрузкиДанных,
		НСтр("ru = 'Ограничить получение данных для карточек товаров до'"),
		НСтр("ru = 'Ограничить получение данных для карточек товаров'"));
	Элементы.ГруппаЗаписатьЦеныОтдельнойКомандой.Доступность = ЗагрузитьЦеныПродажи;
	
КонецПроцедуры

#КонецОбласти
