
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Макет = Обработки.УправлениеПродажамиНаOzon.ПолучитьМакет("СписокСкладовFBO");

	КолонкаПредставлениеСклада  = 1;
	КолонкаНаименованияСклада   = 2;
	КолонкаИдентификатораСклада = 3;
	КолонкаАдресаСклада         = 4;
	КолонкаДопСведений          = 5;

	Для НомерСтроки = 2 По Макет.ВысотаТаблицы Цикл
		НовыйСклад = ТаблицаСкладовFBO.Добавить();

		НовыйСклад.НаименованиеОбъектаМаркетплейса  = Макет.ПолучитьОбласть(НомерСтроки, КолонкаНаименованияСклада,
			НомерСтроки, КолонкаНаименованияСклада).ТекущаяОбласть.Текст;
		НовыйСклад.ИдентификаторОбъектаМаркетплейса = Макет.ПолучитьОбласть(НомерСтроки, КолонкаИдентификатораСклада,
			НомерСтроки, КолонкаИдентификатораСклада).ТекущаяОбласть.Текст;

		ПредставлениеСклада = Макет.ПолучитьОбласть(НомерСтроки, КолонкаПредставлениеСклада,
			НомерСтроки, КолонкаПредставлениеСклада).ТекущаяОбласть.Текст;
		Адрес = Макет.ПолучитьОбласть(НомерСтроки, КолонкаАдресаСклада, НомерСтроки,
			КолонкаАдресаСклада).ТекущаяОбласть.Текст;
		ДополнительныеСведения = Макет.ПолучитьОбласть(НомерСтроки, КолонкаДопСведений,
			НомерСтроки, КолонкаДопСведений).ТекущаяОбласть.Текст;
		НовыйСклад.ДополнительныеСведения = СокрЛП(ПредставлениеСклада) + ": " + Адрес
			+ ?(ЗначениеЗаполнено(ДополнительныеСведения), ". " + ДополнительныеСведения + "", "");
	КонецЦикла;

	ТаблицаСкладовFBO.Сортировать("НаименованиеОбъектаМаркетплейса");

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаСкладовFBO

&НаКлиенте
Процедура ТаблицаСкладовFBOВыборЗначения(Элемент, Значение, СтандартнаяОбработка)

	ТекущиеДанные = Элемент.ТекущиеДанные;

	ДанныеВыбора = Новый Структура;
	ДанныеВыбора.Вставить("ИдентификаторОбъектаМаркетплейса", ТекущиеДанные.ИдентификаторОбъектаМаркетплейса);
	ДанныеВыбора.Вставить("НаименованиеОбъектаМаркетплейса",  ТекущиеДанные.НаименованиеОбъектаМаркетплейса);
	ДанныеВыбора.Вставить("ДополнительныеСведения",           ТекущиеДанные.ДополнительныеСведения);

	Оповестить("ВыбранСкладFBO", ДанныеВыбора);

КонецПроцедуры

#КонецОбласти
