
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УчетнаяЗаписьМаркетплейса = Параметры.УчетнаяЗаписьМаркетплейса;
	НеОбновлятьДанныеТорговойПлощадки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗаписьМаркетплейса, "НеОбновлятьДанныеТорговойПлощадки");
	
	ШаблонИнформации = НСтр("ru = 'Для организации %1 авторизация на Ozon выполнена'");
	СтрокаИнформации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИнформации, УчетнаяЗаписьМаркетплейса);

	Элементы.ПодсказкаФормы.Заголовок = СтрокаИнформации;
	Элементы.ПодсказкаФормы.ЦветТекста = ЦветаСтиля.ЦветТекстаУспех;

	УчитыватьПривилегированныйРежим = Ложь;
	ПроверятьПраваАдминистрированияСистемы = Ложь;
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь(
		, // Проверять текущего пользователя.
		ПроверятьПраваАдминистрированияСистемы,
		УчитыватьПривилегированныйРежим);

	Элементы.НеОбновлятьДанныеТорговойПлощадки.ТолькоПросмотр = Не ЭтоПолноправныйПользователь;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НеОбновлятьДанныеТорговойПлощадкиПриИзменении(Элемент)
    
    Если ИнтеграцияСМаркетплейсомOzonВызовСервера.ИзменитьКонтрольОбновленияДанныхТорговойПлощадки(УчетнаяЗаписьМаркетплейса, НеОбновлятьДанныеТорговойПлощадки) Тогда
        Оповестить("ОбновитьСписокПодключений");
        Оповестить("Запись_УчетныеЗаписиМаркетплейсов", УчетнаяЗаписьМаркетплейса);
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отмена(Команда)

	Закрыть(Неопределено);

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьНастройки(Команда)

	ИнтеграцияСМаркетплейсомOzonКлиент.ОчисткаНастроекАвторизацииВопрос(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОчиститьНастройкиЗавершение(Результат, Параметры) Экспорт 

	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	ОчисткаУспешна = ОчиститьНастройкиНаСервере(УчетнаяЗаписьМаркетплейса);

	Если ОчисткаУспешна Тогда 
		Оповестить("ОбновитьСписокПодключений");
		Оповестить("УчетнаяЗаписьДеактивирована", УчетнаяЗаписьМаркетплейса);
		Закрыть(Истина);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ОчиститьНастройкиНаСервере(УчетнаяЗаписьМаркетплейса)

	ОчисткаУспешна = ИнтеграцияСМаркетплейсомOzonСервер.ОчиститьНастройкиУчетнойЗаписи(УчетнаяЗаписьМаркетплейса);

	Возврат ОчисткаУспешна;

КонецФункции

#КонецОбласти

