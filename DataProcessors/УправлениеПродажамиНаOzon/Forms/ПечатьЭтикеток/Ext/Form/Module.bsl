
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПрефиксЭтикеток = "Tags-";
	УстановитьУсловноеОформление();
	
	ПараметрыОбъекты = Неопределено;
	Параметры.Свойство("Объекты", ПараметрыОбъекты);
	ЭтоЗаказКлиента = Параметры.ЭтоЗаказКлиента;
	ЭтоFBO          = Параметры.ЭтоFBO;
	Если ЗначениеЗаполнено(ПараметрыОбъекты) Тогда
		Объекты.ЗагрузитьЗначения(ПараметрыОбъекты);
	КонецЕсли;
	
	Если ЭтоFBO Тогда
		ОбновитьНаСервереFBO(); 
	Иначе  
		ОбновитьНаСервереFBS(); 
	КонецЕсли;
	
	Если ЭтоFBO Тогда         
		СкрытьНерекомендуемыеРасширения = (Этикетки.Количество() > 0); 
		
		Элементы.ПрефиксЭтикеток.Видимость            = Истина;
		Элементы.ПрефиксЭтикеток.Видимость            = Не ЭтоЗаказКлиента;
		Элементы.ДекорацияОшибокНетFBS.Видимость      = Ложь;
		Элементы.ДекорацияПредупреждениеFBS.Видимость = Ложь;
		
		Если ЭтоЗаказКлиента Тогда
			Элементы.ЭтикеткиСведенияОбОбъекте.Заголовок = НСтр("ru = 'Номер заказа'");
			Элементы.ЭтикеткиНомерОбъекта.Заголовок      = НСтр("ru = 'Номер отправления'");
		Иначе  
			Элементы.ЭтикеткиСведенияОбОбъекте.Заголовок = НСтр("ru = 'Сведения о поставке'");
			Элементы.ЭтикеткиНомерОбъекта.Заголовок      = НСтр("ru = 'Номер поставки'"); 
			Элементы.ЭтикеткиСсылка.Заголовок            = НСтр("ru = 'Этикетки поставки'");
			Элементы.ПолучитьЭтикетки.Видимость          = Ложь;
		КонецЕсли;
		
	Иначе  
		Элементы.ПрефиксЭтикеток.Видимость                         = Ложь;
		Элементы.ПолучитьЭтикетки.Видимость                        = Истина; 
		Элементы.ЭтикеткиСкрытьНерекомендуемыеРасширения.Видимость = Ложь;
		Элементы.ЭтикеткиСведенияОбОбъекте.Заголовок               = НСтр("ru = 'Номер заказа'");
		Элементы.ЭтикеткиНомерОбъекта.Заголовок                    = НСтр("ru = 'Номер отправления'");  
		Элементы.ЭтикеткиСсылка.Заголовок                          = НСтр("ru = 'Этикетки'");
		Элементы.ДекорацияОшибокНет.Видимость                      = Ложь;
		Элементы.ДекорацияПредупреждение.Видимость                 = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ЭтикеткиСкрытьНерекомендуемыеРасширения.Пометка = СкрытьНерекомендуемыеРасширения;
	
	УстановитьВидимостьДоступность();
	ПолучитьВидимуюСтрокуСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_Файл" Тогда
		Если Параметр.Свойство("ЭтоНовый") 
				И Параметр.ЭтоНовый Тогда
			ОбновитьНаКлиенте(Параметр.Файл);
			
		ИначеЕсли Параметр.Свойство("Событие") 
					И Параметр.Событие = "ДанныеФайлаИзменены" Тогда
			ОбновитьНаКлиенте(Источник);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЭтикетки

&НаКлиенте
Процедура ЭтикеткиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Поле.Имя = "ЭтикеткиСсылка" Тогда
		СтандартнаяОбработка = Ложь;
		ЭтикеткиСсылкаВыбрать(Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтикеткиПриАктивизацииСтроки(Элемент)

	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаПодвалСтатусОшибокНет.Видимость      = ЗначениеЗаполнено(ТекущиеДанные.Ссылка) И (СтрНайти("ZIP, PDF", ВРег(ТекущиеДанные.Расширение)) > 0);
	Элементы.ГруппаПодвалСтатусПредупреждение.Видимость = Не ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Или (СтрНайти("ZIP, PDF", ВРег(ТекущиеДанные.Расширение)) = 0);

КонецПроцедуры

&НаКлиенте
Процедура ЭтикеткиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтикеткиПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтикеткиПометкаПриИзменении(Элемент)

	ТекущиеДанные = Элементы.Этикетки.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Ссылка)
			Или (СтрНайти("ZIP, PDF", ВРег(ТекущиеДанные.Расширение)) = 0) Тогда
		ТекущиеДанные.Пометка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтикеткиСсылкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭтикеткиСсылкаВыбрать(Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьФлажки(Команда)

	Для Каждого СтрокаТаблицыЗначений Из Этикетки Цикл
		Если ЗначениеЗаполнено(СтрокаТаблицыЗначений.Ссылка) 
				И (СтрНайти("ZIP, PDF", ВРег(СтрокаТаблицыЗначений.Расширение)) > 0) Тогда
			СтрокаТаблицыЗначений.Пометка = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)

	Для Каждого СтрокаТаблицыЗначений Из Этикетки Цикл
		СтрокаТаблицыЗначений.Пометка = Ложь;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	Если ЭтоFBO Тогда
		ОбновитьНаСервереFBO(); 
	Иначе  
		ОбновитьНаСервереFBS(); 
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	ПолучитьВидимуюСтрокуСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьНерекомендуемыеРасширения(Команда)
	
	СкрытьНерекомендуемыеРасширения                          = Не СкрытьНерекомендуемыеРасширения;
	Элементы.ЭтикеткиСкрытьНерекомендуемыеРасширения.Пометка = СкрытьНерекомендуемыеРасширения;
	
	ПолучитьВидимуюСтрокуСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ПечатныеДокументы", ПолучитьПакетДокументов());
	ПараметрыОткрытия.Вставить("ПечатьСразу",       Ложь);
	
	ОткрытьФорму("Обработка.УправлениеПродажамиНаOzon.Форма.ПредварительныйПросмотрЭтикетокПоставки",
		ПараметрыОткрытия,
		ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПечатьНаПринтер(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ПечатныеДокументы", ПолучитьПакетДокументов());
	ПараметрыОткрытия.Вставить("ПечатьСразу",       Истина);
	
	ОткрытьФорму("Обработка.УправлениеПродажамиНаOzon.Форма.ПредварительныйПросмотрЭтикетокПоставки",
		ПараметрыОткрытия,
		ЭтотОбъект);
		
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьЭтикетки(Команда)
	
	Ошибка = ПолучитьЭтикеткиНаСервере();
	
	Если Не ПустаяСтрока(Ошибка.КодОшибки) Тогда
		ИнтеграцияСМаркетплейсамиКлиент.ВывестиСостояние(Ошибка,, Истина);
	Иначе     
		Если ЭтоFBO Тогда
			ОбновитьНаСервереFBO(); 
		Иначе  
			ОбновитьНаСервереFBS(); 
		КонецЕсли;
		
		УстановитьВидимостьДоступность();
		ПолучитьВидимуюСтрокуСписка();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРасписаниеПолучениеЭтикеток(Команда)

	Префиксы     = ИнтеграцияСМаркетплейсомOzonКлиентСервер.ПрефиксыСервиса();
	Наименование = ИнтеграцияСМаркетплейсомOzonКлиентСервер.НаименованиеРегламентногоЗадания(
		Префиксы.ПолучениеЭтикетокДляЗаказовТорговойПлощадки);
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("УчетнаяЗаписьМаркетплейса",        УчетнаяЗапись);
	ПараметрыФормы.Вставить("Наименование",                     Наименование);
	ПараметрыФормы.Вставить("ИмяМетаданных",                    "ПолучениеЭтикетокДляЗаказовТорговойПлощадки");
	ПараметрыФормы.Вставить("Префикс",                          Префиксы.ПолучениеЭтикетокДляЗаказовТорговойПлощадки);
	ПараметрыФормы.Вставить("ОткрыватьПодОграниченнымиПравами", Истина);
	
	ОткрытьФорму("Справочник.УчетныеЗаписиМаркетплейсов.Форма.РегламентноеЗадание", ПараметрыФормы);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента      = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтикеткиСсылка.Имя);

	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ПодчеркнутыйШрифтДиалоговМаркетплейсов);
	
	//
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента      = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтикеткиСсылка.Имя);

	ОтборЭлемента               = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Этикетки.Ссылка");
	ОтборЭлемента.ВидСравнения  = ВидСравненияКомпоновкиДанных.НеЗаполнено;

	Если ЭтоЗаказКлиента Тогда
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст",      НСтр("ru = '<Выполняется запрос этикеток>'"));
	Иначе
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст",      НСтр("ru = '<Выбрать файл с этикетками поставки>'"));
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Шрифт",      ШрифтыСтиля.ПодчеркнутыйШрифтДиалоговМаркетплейсов);
	КонецЕсли;
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийОшибкуТекст);
	
	//
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента      = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтикеткиНомерОбъекта.Имя);
	ПолеЭлемента      = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтикеткиСсылка.Имя);

	ОтборЭлемента               = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Этикетки.Ссылка");
	ОтборЭлемента.ВидСравнения  = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ОтборЭлемента                = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Этикетки.Расширение");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = "zip";
	
	ОтборЭлемента                = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Этикетки.Расширение");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = "pdf";
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
	//
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента      = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтикеткиПометка.Имя);
	ПолеЭлемента      = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтикеткиРаспоряжение.Имя);
	ПолеЭлемента      = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтикеткиСведенияОбОбъекте.Имя);
	ПолеЭлемента      = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтикеткиНомерОбъекта.Имя);
	ПолеЭлемента      = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтикеткиСсылка.Имя);

	ОтборЭлемента                = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("СкрытьНерекомендуемыеРасширения");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента                = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Этикетки.Расширение");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = "";
	
	ОтборЭлемента                = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Этикетки.Расширение");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = "zip";
	
	ОтборЭлемента                = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Этикетки.Расширение");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = "pdf";
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступность()
	
	ТекущиеДанные = Элементы.Этикетки.ТекущиеДанные;

	Элементы.ГруппаФлажки.Видимость						  = (Этикетки.Количество() > 0);
	Элементы.ОткрытьРасписаниеПолучениеЭтикеток.Видимость = ЗначениеЗаполнено(УчетнаяЗапись);
	
	Элементы.ГруппаПодвалСтатусОшибка.Видимость			  = (Этикетки.Количество() = 0);
	Элементы.ГруппаПодвалСтатусОшибокНет.Видимость		  = (Этикетки.Количество() > 0) И (ТекущиеДанные <> Неопределено) И ЗначениеЗаполнено(ТекущиеДанные.Ссылка) И (СтрНайти("ZIP, PDF", ВРег(ТекущиеДанные.Расширение)) > 0);
	Элементы.ГруппаПодвалСтатусПредупреждение.Видимость   = (Этикетки.Количество() > 0) И (ТекущиеДанные <> Неопределено) И (Не ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Или (СтрНайти("ZIP, PDF", ВРег(ТекущиеДанные.Расширение)) = 0));

	Элементы.ФормаПечать.Видимость						  = (Этикетки.Количество() > 0);
	Элементы.ФормаПечатьНаПринтер.Видимость				  = (Этикетки.Количество() > 0);
	Элементы.ФормаЗакрыть.КнопкаПоУмолчанию				  = (Этикетки.Количество() = 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВидимуюСтрокуСписка()
	
	Элементы.ЭтикеткиСкрытьНерекомендуемыеРасширения.Видимость = Ложь;
	
	Для Каждого СтрокаТабличнойЧасти Из Этикетки Цикл
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Ссылка) 
				И (СтрНайти("ZIP, PDF", ВРег(СтрокаТабличнойЧасти.Расширение)) > 0) Тогда
			Элементы.ЭтикеткиСкрытьНерекомендуемыеРасширения.Видимость = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если Не СкрытьНерекомендуемыеРасширения Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Этикетки.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено 
			И ЗначениеЗаполнено(ТекущиеДанные.Ссылка) 
			И (СтрНайти("ZIP, PDF", ВРег(ТекущиеДанные.Расширение)) > 0) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТабличнойЧасти Из Этикетки Цикл
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Ссылка) 
				И (СтрНайти("ZIP, PDF", ВРег(СтрокаТабличнойЧасти.Расширение)) > 0) Тогда
			Элементы.Этикетки.ТекущаяСтрока = СтрокаТабличнойЧасти.ПолучитьИдентификатор();
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	СкрытьНерекомендуемыеРасширения                            = Ложь;
	Элементы.ЭтикеткиСкрытьНерекомендуемыеРасширения.Пометка   = СкрытьНерекомендуемыеРасширения;
	Элементы.ЭтикеткиСкрытьНерекомендуемыеРасширения.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаСервереFBS()
	
	Этикетки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДокументЗаказКлиента.Ссылка КАК Заказ,
		|	ЗаказыТорговыхПлощадок.УчетнаяЗапись КАК УчетнаяЗапись,
		|	ЗаказыТорговыхПлощадок.ДокументОтгрузки КАК Отправление,
		|	ЗаказыТорговыхПлощадок.НомерЗаказа КАК НомерЗаказа,
		|	ЗаказыТорговыхПлощадок.НомерОтправления КАК НомерОтправления,
		|	ЗаказыТорговыхПлощадок.Статус КАК СтатусЗаказа,
		|	ЕСТЬNULL(ЗаказКлиентаПрисоединенныеФайлы.Ссылка, НЕОПРЕДЕЛЕНО) КАК Ссылка,
		|	ЕСТЬNULL(ЗаказКлиентаПрисоединенныеФайлы.Наименование, """") КАК Наименование,
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ЗаказКлиентаПрисоединенныеФайлы.ПутьКФайлу КАК СТРОКА(1024)), """") КАК ПутьКФайлу,
		|	ЕСТЬNULL(ЗаказКлиентаПрисоединенныеФайлы.Расширение, """") КАК Расширение
		|ПОМЕСТИТЬ ВТ_ОбъектыПечати
		|ИЗ
		|	Документ.ЗаказКлиента КАК ДокументЗаказКлиента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|		ПО ДокументЗаказКлиента.Ссылка = ЗаказыТорговыхПлощадок.Заказ
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗаказКлиентаПрисоединенныеФайлы КАК ЗаказКлиентаПрисоединенныеФайлы
		|		ПО ДокументЗаказКлиента.Ссылка = ЗаказКлиентаПрисоединенныеФайлы.ВладелецФайла
		|			И (НЕ ЗаказКлиентаПрисоединенныеФайлы.ПометкаУдаления)
		|ГДЕ
		|	ДокументЗаказКлиента.Ссылка В(&Объекты)
		|	И ЗаказыТорговыхПлощадок.ДокументОтгрузки = НЕОПРЕДЕЛЕНО
		|	И НЕ ЗаказыТорговыхПлощадок.УчетнаяЗапись ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументПередачаТоваровХранителюТовары.ЗаказКлиента,
		|	ЗаказыТорговыхПлощадок.УчетнаяЗапись,
		|	ДокументПередачаТоваровХранителюТовары.Ссылка,
		|	ЗаказыТорговыхПлощадок.НомерЗаказа,
		|	ЗаказыТорговыхПлощадок.НомерОтправления,
		|	ЗаказыТорговыхПлощадок.Статус,
		|	ЕСТЬNULL(ПередачаТоваровХранителюПрисоединенныеФайлы.Ссылка, НЕОПРЕДЕЛЕНО),
		|	ЕСТЬNULL(ПередачаТоваровХранителюПрисоединенныеФайлы.Наименование, """"),
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ПередачаТоваровХранителюПрисоединенныеФайлы.ПутьКФайлу КАК СТРОКА(1024)), """"),
		|	ЕСТЬNULL(ПередачаТоваровХранителюПрисоединенныеФайлы.Расширение, """")
		|ИЗ
		|	Документ.ПередачаТоваровХранителю.Товары КАК ДокументПередачаТоваровХранителюТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|		ПО ((ВЫРАЗИТЬ(ДокументПередачаТоваровХранителюТовары.ЗаказКлиента КАК Документ.ЗаказКлиента)) = ЗаказыТорговыхПлощадок.Заказ)
		|			И ДокументПередачаТоваровХранителюТовары.Ссылка = ЗаказыТорговыхПлощадок.ДокументОтгрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПередачаТоваровХранителюПрисоединенныеФайлы КАК ПередачаТоваровХранителюПрисоединенныеФайлы
		|		ПО ДокументПередачаТоваровХранителюТовары.Ссылка = ПередачаТоваровХранителюПрисоединенныеФайлы.ВладелецФайла
		|			И (НЕ ПередачаТоваровХранителюПрисоединенныеФайлы.ПометкаУдаления)
		|ГДЕ
		|	(ДокументПередачаТоваровХранителюТовары.Ссылка В (&Объекты)
		|			ИЛИ ЗаказыТорговыхПлощадок.Заказ В (&Объекты)
		|				И НЕ ЗаказыТорговыхПлощадок.УчетнаяЗапись ЕСТЬ NULL)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗаказыТорговыхПлощадок.Заказ,
		|	ЗаказыТорговыхПлощадок.УчетнаяЗапись,
		|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка,
		|	ЗаказыТорговыхПлощадок.НомерЗаказа,
		|	ЗаказыТорговыхПлощадок.НомерОтправления,
		|	ЗаказыТорговыхПлощадок.Статус,
		|	ЕСТЬNULL(ЗаказКлиентаПрисоединенныеФайлы.Ссылка, НЕОПРЕДЕЛЕНО),
		|	ЕСТЬNULL(ЗаказКлиентаПрисоединенныеФайлы.Наименование, """"),
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ЗаказКлиентаПрисоединенныеФайлы.ПутьКФайлу КАК СТРОКА(1024)), """"),
		|	ЕСТЬNULL(ЗаказКлиентаПрисоединенныеФайлы.Расширение, """")
		|ИЗ
		|	Документ.РасходныйОрдерНаТовары.ТоварыПоРаспоряжениям КАК РасходныйОрдерНаТоварыТоварыПоРаспоряжениям
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|		ПО РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка = ЗаказыТорговыхПлощадок.ДокументОтгрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗаказКлиентаПрисоединенныеФайлы КАК ЗаказКлиентаПрисоединенныеФайлы
		|		ПО ((ВЫРАЗИТЬ(РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение КАК Документ.ЗаказКлиента)) = ЗаказКлиентаПрисоединенныеФайлы.ВладелецФайла)
		|			И (НЕ ЗаказКлиентаПрисоединенныеФайлы.ПометкаУдаления)
		|ГДЕ
		|	(РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка В (&Объекты)
		|			ИЛИ ЗаказыТорговыхПлощадок.Заказ В (&Объекты)
		|				И НЕ ЗаказыТорговыхПлощадок.УчетнаяЗапись ЕСТЬ NULL)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РеализацияТоваровУслуг.ЗаказКлиента,
		|	ЗаказыТорговыхПлощадок.УчетнаяЗапись,
		|	РеализацияТоваровУслуг.Ссылка,
		|	ЗаказыТорговыхПлощадок.НомерЗаказа,
		|	ЗаказыТорговыхПлощадок.НомерОтправления,
		|	ЗаказыТорговыхПлощадок.Статус,
		|	ЕСТЬNULL(РеализацияТоваровУслугПрисоединенныеФайлы.Ссылка, НЕОПРЕДЕЛЕНО),
		|	ЕСТЬNULL(РеализацияТоваровУслугПрисоединенныеФайлы.Наименование, """"),
		|	ЕСТЬNULL(ВЫРАЗИТЬ(РеализацияТоваровУслугПрисоединенныеФайлы.ПутьКФайлу КАК СТРОКА(1024)), """"),
		|	ЕСТЬNULL(РеализацияТоваровУслугПрисоединенныеФайлы.Расширение, """")
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|		ПО ((ВЫРАЗИТЬ(РеализацияТоваровУслуг.ЗаказКлиента КАК Документ.ЗаказКлиента)) = ЗаказыТорговыхПлощадок.Заказ)
		|			И РеализацияТоваровУслуг.Ссылка = ЗаказыТорговыхПлощадок.ДокументОтгрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РеализацияТоваровУслугПрисоединенныеФайлы КАК РеализацияТоваровУслугПрисоединенныеФайлы
		|		ПО РеализацияТоваровУслуг.Ссылка = РеализацияТоваровУслугПрисоединенныеФайлы.ВладелецФайла
		|			И (НЕ РеализацияТоваровУслугПрисоединенныеФайлы.ПометкаУдаления)
		|ГДЕ
		|	(РеализацияТоваровУслуг.Ссылка В (&Объекты)
		|			ИЛИ ЗаказыТорговыхПлощадок.Заказ В (&Объекты)
		|				И НЕ ЗаказыТорговыхПлощадок.УчетнаяЗапись ЕСТЬ NULL)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОбъектыПечати.Заказ КАК Заказ,
		|	ОбъектыПечати.УчетнаяЗапись КАК УчетнаяЗапись,
		|	ОбъектыПечати.Отправление КАК Отправление,
		|	ОбъектыПечати.НомерЗаказа КАК НомерЗаказа,
		|	ОбъектыПечати.НомерОтправления КАК НомерОтправления,
		|	ОбъектыПечати.СтатусЗаказа КАК СтатусЗаказа,
		|	ОбъектыПечати.Ссылка КАК Ссылка,
		|	ОбъектыПечати.Наименование КАК Наименование,
		|	ОбъектыПечати.ПутьКФайлу КАК ПутьКФайлу,
		|	ОбъектыПечати.Расширение КАК Расширение
		|ИЗ
		|	ВТ_ОбъектыПечати КАК ОбъектыПечати";
	
	Запрос.УстановитьПараметр("Объекты", Объекты);
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаДанных = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Пока ВыборкаДанных.Следующий() Цикл
		СтрокаТаблицыЗначений					= Этикетки.Добавить();
		СтрокаТаблицыЗначений.Пометка			= ЗначениеЗаполнено(ВыборкаДанных.Ссылка) И (СтрНайти("ZIP, PDF", ВРег(ВыборкаДанных.Расширение)) > 0);
		СтрокаТаблицыЗначений.Распоряжение		= ?(ЗначениеЗаполнено(ВыборкаДанных.Отправление), ВыборкаДанных.Отправление, ВыборкаДанных.Заказ);
		СтрокаТаблицыЗначений.СтатусЗаказа      = ВыборкаДанных.СтатусЗаказа;
		СтрокаТаблицыЗначений.СведенияОПоставке = СокрЛП(ВыборкаДанных.НомерЗаказа);
		СтрокаТаблицыЗначений.НомерПоставки		= СокрЛП(ВыборкаДанных.НомерОтправления);
		СтрокаТаблицыЗначений.Ссылка			= ВыборкаДанных.Ссылка;
		СтрокаТаблицыЗначений.ПутьКФайлу		= ВыборкаДанных.ПутьКФайлу;
		СтрокаТаблицыЗначений.Расширение		= ВыборкаДанных.Расширение; 
		СтрокаТаблицыЗначений.УчетнаяЗапись     = ВыборкаДанных.УчетнаяЗапись;
	КонецЦикла;
	
	УчетныеЗаписи = Этикетки.Выгрузить(,"УчетнаяЗапись");
	УчетныеЗаписи.Свернуть("УчетнаяЗапись");
	
	Если УчетныеЗаписи.Количество() = 1 Тогда
		УчетнаяЗапись = УчетныеЗаписи[0].УчетнаяЗапись;
	Иначе
		УчетнаяЗапись = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаСервереFBO()
	
	Этикетки.Очистить();

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДокументЗаказКлиента.Ссылка КАК Распоряжение,
		|	ДокументЗаказКлиента.Организация КАК Организация,
		|	ДокументЗаказКлиента.Партнер КАК Партнер,
		|	ДокументЗаказКлиента.Контрагент КАК Контрагент,
		|	ДокументЗаказКлиента.Соглашение КАК Соглашение,
		|	ДокументЗаказКлиента.Договор КАК Договор,
		|	"""" КАК Комментарий,
		|	ДокументЗаказКлиента.НомерПоДаннымКлиента КАК НомерПоДаннымКлиента
		|ПОМЕСТИТЬ ВТ_Распоряжения
		|ИЗ
		|	Документ.ЗаказКлиента КАК ДокументЗаказКлиента
		|ГДЕ
		|	ДокументЗаказКлиента.Ссылка В(&Объекты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументПередачаТоваровХранителю.Ссылка,
		|	ДокументПередачаТоваровХранителю.Организация,
		|	ДокументПередачаТоваровХранителю.Партнер,
		|	ДокументПередачаТоваровХранителю.Контрагент,
		|	ДокументПередачаТоваровХранителю.Соглашение,
		|	ДокументПередачаТоваровХранителю.Договор,
		|	ДокументПередачаТоваровХранителю.Комментарий,
		|	""""
		|ИЗ
		|	Документ.ПередачаТоваровХранителю КАК ДокументПередачаТоваровХранителю
		|ГДЕ
		|	ДокументПередачаТоваровХранителю.Ссылка В(&Объекты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументПередачаТоваровХранителюТовары.ЗаказКлиента,
		|	ДокументЗаказКлиента.Организация,
		|	ДокументЗаказКлиента.Партнер,
		|	ДокументЗаказКлиента.Контрагент,
		|	ДокументЗаказКлиента.Соглашение,
		|	ДокументЗаказКлиента.Договор,
		|	ДокументПередачаТоваровХранителюТовары.Ссылка.Комментарий,
		|	ДокументЗаказКлиента.НомерПоДаннымКлиента
		|ИЗ
		|	Документ.ПередачаТоваровХранителю.Товары КАК ДокументПередачаТоваровХранителюТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДокументЗаказКлиента
		|		ПО ((ВЫРАЗИТЬ(ДокументПередачаТоваровХранителюТовары.ЗаказКлиента КАК Документ.ЗаказКлиента)) = ДокументЗаказКлиента.Ссылка)
		|ГДЕ
		|	ДокументПередачаТоваровХранителюТовары.Ссылка В(&Объекты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение,
		|	ДокументЗаказКлиента.Организация,
		|	ДокументЗаказКлиента.Партнер,
		|	ДокументЗаказКлиента.Контрагент,
		|	ДокументЗаказКлиента.Соглашение,
		|	ДокументЗаказКлиента.Договор,
		|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка.Комментарий,
		|	ДокументЗаказКлиента.НомерПоДаннымКлиента
		|ИЗ
		|	Документ.РасходныйОрдерНаТовары.ТоварыПоРаспоряжениям КАК РасходныйОрдерНаТоварыТоварыПоРаспоряжениям
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДокументЗаказКлиента
		|		ПО ((ВЫРАЗИТЬ(РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение КАК Документ.ЗаказКлиента)) = ДокументЗаказКлиента.Ссылка)
		|ГДЕ
		|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка В(&Объекты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение,
		|	ДокументПередачаТоваровХранителю.Организация,
		|	ДокументПередачаТоваровХранителю.Партнер,
		|	ДокументПередачаТоваровХранителю.Контрагент,
		|	ДокументПередачаТоваровХранителю.Соглашение,
		|	ДокументПередачаТоваровХранителю.Договор,
		|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка.Комментарий,
		|	""""
		|ИЗ
		|	Документ.РасходныйОрдерНаТовары.ТоварыПоРаспоряжениям КАК РасходныйОрдерНаТоварыТоварыПоРаспоряжениям
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровХранителю КАК ДокументПередачаТоваровХранителю
		|		ПО ((ВЫРАЗИТЬ(РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение КАК Документ.ПередачаТоваровХранителю)) = ДокументПередачаТоваровХранителю.Ссылка)
		|ГДЕ
		|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка В(&Объекты)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Распоряжение;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СправочникУчетныеЗаписиМаркетплейсов.Организация КАК Организация,
		|	СправочникУчетныеЗаписиМаркетплейсов.Партнер КАК Партнер,
		|	СправочникУчетныеЗаписиМаркетплейсов.Контрагент КАК Контрагент,
		|	СправочникУчетныеЗаписиМаркетплейсов.Соглашение КАК Соглашение,
		|	СправочникУчетныеЗаписиМаркетплейсов.ДоговорПродажиЧерезСкладыТорговойПлощадки КАК Договор
		|ПОМЕСТИТЬ ВТ_УчетныеЗаписи
		|ИЗ
		|	Справочник.УчетныеЗаписиМаркетплейсов КАК СправочникУчетныеЗаписиМаркетплейсов
		|ГДЕ
		|	СправочникУчетныеЗаписиМаркетплейсов.ВидМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.МаркетплейсOzon)
		|	И НЕ СправочникУчетныеЗаписиМаркетплейсов.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СправочникУчетныеЗаписиМаркетплейсов.Организация,
		|	СправочникУчетныеЗаписиМаркетплейсов.Партнер,
		|	СправочникУчетныеЗаписиМаркетплейсов.Контрагент,
		|	СправочникУчетныеЗаписиМаркетплейсов.Соглашение,
		|	СправочникУчетныеЗаписиМаркетплейсов.ДоговорПродажиЧерезСкладыСобственные
		|ИЗ
		|	Справочник.УчетныеЗаписиМаркетплейсов КАК СправочникУчетныеЗаписиМаркетплейсов
		|ГДЕ
		|	СправочникУчетныеЗаписиМаркетплейсов.ВидМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.МаркетплейсOzon)
		|	И НЕ СправочникУчетныеЗаписиМаркетплейсов.ПометкаУдаления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	Партнер,
		|	Контрагент,
		|	Соглашение,
		|	Договор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТ_Распоряжения.Распоряжение КАК Распоряжение,
		|	ВЫРАЗИТЬ(ВТ_Распоряжения.Комментарий КАК СТРОКА(256)) КАК Комментарий,
		|	ВЫРАЗИТЬ(ВТ_Распоряжения.НомерПоДаннымКлиента КАК СТРОКА(256)) КАК НомерПоДаннымКлиента,
		|	ЕСТЬNULL(ЗаказКлиентаПрисоединенныеФайлы.Ссылка, НЕОПРЕДЕЛЕНО) КАК Ссылка,
		|	ЕСТЬNULL(ЗаказКлиентаПрисоединенныеФайлы.Наименование, """") КАК Наименование,
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ЗаказКлиентаПрисоединенныеФайлы.ПутьКФайлу КАК СТРОКА(1024)), """") КАК ПутьКФайлу,
		|	ЕСТЬNULL(ЗаказКлиентаПрисоединенныеФайлы.Расширение, """") КАК Расширение
		|ИЗ
		|	ВТ_Распоряжения КАК ВТ_Распоряжения
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗаказКлиентаПрисоединенныеФайлы КАК ЗаказКлиентаПрисоединенныеФайлы
		|		ПО ВТ_Распоряжения.Распоряжение = ЗаказКлиентаПрисоединенныеФайлы.ВладелецФайла
		|			И (НЕ ЗаказКлиентаПрисоединенныеФайлы.ПометкаУдаления)
		|ГДЕ
		|	ВТ_Распоряжения.Распоряжение ССЫЛКА Документ.ЗаказКлиента
		|	И ВТ_Распоряжения.Распоряжение.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию)
		|	И (ВТ_Распоряжения.Организация, ВТ_Распоряжения.Партнер, ВТ_Распоряжения.Контрагент, ВТ_Распоряжения.Соглашение, ВТ_Распоряжения.Договор) В
		|			(ВЫБРАТЬ
		|				ВТ_УчетныеЗаписи.Организация КАК Организация,
		|				ВТ_УчетныеЗаписи.Партнер КАК Партнер,
		|				ВТ_УчетныеЗаписи.Контрагент КАК Контрагент,
		|				ВТ_УчетныеЗаписи.Соглашение КАК Соглашение,
		|				ВТ_УчетныеЗаписи.Договор КАК Договор
		|			ИЗ
		|				ВТ_УчетныеЗаписи КАК ВТ_УчетныеЗаписи)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТ_Распоряжения.Распоряжение,
		|	ВЫРАЗИТЬ(ВТ_Распоряжения.Комментарий КАК СТРОКА(256)),
		|	ВЫРАЗИТЬ(ВТ_Распоряжения.НомерПоДаннымКлиента КАК СТРОКА(256)),
		|	ЕСТЬNULL(ПередачаТоваровХранителюПрисоединенныеФайлы.Ссылка, НЕОПРЕДЕЛЕНО),
		|	ЕСТЬNULL(ПередачаТоваровХранителюПрисоединенныеФайлы.Наименование, """"),
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ПередачаТоваровХранителюПрисоединенныеФайлы.ПутьКФайлу КАК СТРОКА(1024)), """"),
		|	ЕСТЬNULL(ПередачаТоваровХранителюПрисоединенныеФайлы.Расширение, """")
		|ИЗ
		|	ВТ_Распоряжения КАК ВТ_Распоряжения
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПередачаТоваровХранителюПрисоединенныеФайлы КАК ПередачаТоваровХранителюПрисоединенныеФайлы
		|		ПО ВТ_Распоряжения.Распоряжение = ПередачаТоваровХранителюПрисоединенныеФайлы.ВладелецФайла
		|			И (НЕ ПередачаТоваровХранителюПрисоединенныеФайлы.ПометкаУдаления)
		|ГДЕ
		|	ВТ_Распоряжения.Распоряжение ССЫЛКА Документ.ПередачаТоваровХранителю
		|	И ВТ_Распоряжения.Распоряжение.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию)
		|	И (ВТ_Распоряжения.Организация, ВТ_Распоряжения.Партнер, ВТ_Распоряжения.Контрагент, ВТ_Распоряжения.Соглашение, ВТ_Распоряжения.Договор) В
		|			(ВЫБРАТЬ
		|				ВТ_УчетныеЗаписи.Организация КАК Организация,
		|				ВТ_УчетныеЗаписи.Партнер КАК Партнер,
		|				ВТ_УчетныеЗаписи.Контрагент КАК Контрагент,
		|				ВТ_УчетныеЗаписи.Соглашение КАК Соглашение,
		|				ВТ_УчетныеЗаписи.Договор КАК Договор
		|			ИЗ
		|				ВТ_УчетныеЗаписи КАК ВТ_УчетныеЗаписи)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Распоряжение,
		|	Наименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_Распоряжения";
	
	ИспользоватьПартнеровИКонтрагентов = ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровИКонтрагентов");
	ИспользоватьСоглашенияСКлиентами   = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами");
	ИспользуютсяДоговорыКонтрагентов   = ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами");

	Если Не ИспользоватьПартнеровИКонтрагентов Тогда
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст, 
			"ВТ_Распоряжения.Контрагент",
			"ИСТИНА");
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст, 
			"ВТ_УчетныеЗаписи.Контрагент КАК Контрагент",
			"ИСТИНА");
	КонецЕсли;

	Если Не ИспользоватьСоглашенияСКлиентами Тогда
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст, 
			"ВТ_Распоряжения.Соглашение",
			"ИСТИНА");
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст, 
			"ВТ_УчетныеЗаписи.Соглашение КАК Соглашение",
			"ИСТИНА");
	КонецЕсли;

	Если Не ИспользуютсяДоговорыКонтрагентов Тогда
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст, 
			"ВТ_Распоряжения.Договор",
			"ИСТИНА");
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст, 
			"ВТ_УчетныеЗаписи.Договор КАК Договор",
			"ИСТИНА");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Объекты", Объекты);
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаДанных = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Префиксы = СтрРазделить(СтрЗаменить(ПрефиксЭтикеток, " ", ""), ";", Ложь);
	
	Пока ВыборкаДанных.Следующий() Цикл
		НомерПоставки = ВРег(ВыборкаДанных.Наименование);
		Для Каждого Префикс Из Префиксы Цикл
			НомерПоставки = СтрЗаменить(НомерПоставки, ВРег(Префикс), "");
		КонецЦикла;
		
		СведенияОПоставке = "";
		Если ЗначениеЗаполнено(ВыборкаДанных.НомерПоДаннымКлиента) И ЗначениеЗаполнено(ВыборкаДанных.Комментарий) Тогда
			СведенияОПоставке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Заявка № %1 / %2'"), 
				СокрЛП(ВыборкаДанных.НомерПоДаннымКлиента),
				СокрЛП(ВыборкаДанных.Комментарий));
		ИначеЕсли ЗначениеЗаполнено(ВыборкаДанных.НомерПоДаннымКлиента) Тогда
			СведенияОПоставке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Заявка № %1'"), 
				СокрЛП(ВыборкаДанных.НомерПоДаннымКлиента));
		Иначе
			СведенияОПоставке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1'"), 
				СокрЛП(ВыборкаДанных.Комментарий));
		КонецЕсли;
		
		СтрокаТаблицыЗначений					= Этикетки.Добавить();
		СтрокаТаблицыЗначений.Пометка			= ЗначениеЗаполнено(ВыборкаДанных.Ссылка) И (СтрНайти("ZIP, PDF", ВРег(ВыборкаДанных.Расширение)) > 0);
		СтрокаТаблицыЗначений.Распоряжение		= ВыборкаДанных.Распоряжение;
		СтрокаТаблицыЗначений.СведенияОПоставке	= СведенияОПоставке;
		СтрокаТаблицыЗначений.НомерПоставки		= СокрЛП(НомерПоставки);
		СтрокаТаблицыЗначений.Ссылка			= ВыборкаДанных.Ссылка;
		СтрокаТаблицыЗначений.ПутьКФайлу		= ВыборкаДанных.ПутьКФайлу;
		СтрокаТаблицыЗначений.Расширение		= ВыборкаДанных.Расширение;

		Если ЗначениеЗаполнено(ВыборкаДанных.Комментарий) 
				И СтрНайти(ВРег(ВыборкаДанных.Комментарий), НомерПоставки) = 0 Тогда
			СтрокаТаблицыЗначений.Пометка = Ложь;
		КонецЕсли;
	КонецЦикла; 

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНаКлиенте(Ссылки)
	
	Если ТипЗнч(Ссылки) <> Тип("Массив") Тогда
		Ссылки = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылки);
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Этикетки.ТекущиеДанные;
	Распоряжение  = ?(ТекущиеДанные = Неопределено, Неопределено, ТекущиеДанные.Распоряжение);
	Ссылка        = ?(Ссылки.Количество() = 0, ?(ТекущиеДанные = Неопределено, Неопределено, ТекущиеДанные.Ссылка), Ссылки[0]);
	
	Префиксы = СтрРазделить(СтрЗаменить(ПрефиксЭтикеток, " ", ""), ";", Ложь);
	
	Для Каждого ЭлементКоллекции Из Ссылки Цикл
		ЗначенияРеквизитов = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(
								ЭлементКоллекции, 
								"ПометкаУдаления, ВладелецФайла, Наименование, ПутьКФайлу, Расширение");
								
		НомерПоставки = ВРег(ЗначенияРеквизитов.Наименование);
		Для Каждого Префикс Из Префиксы Цикл
			НомерПоставки = СтрЗаменить(НомерПоставки, ВРег(Префикс), "");
		КонецЦикла;
		
		ДобавитьСтроку  = Истина;
		Отбор           = Новый Структура("Распоряжение", ЗначенияРеквизитов.ВладелецФайла);
		НайденныеСтроки = Этикетки.НайтиСтроки(Отбор);
		
		Для Каждого СтрокаТаблицыЗначений Из НайденныеСтроки Цикл
			Если СтрокаТаблицыЗначений.Ссылка = ЭлементКоллекции
					Или Не ЗначениеЗаполнено(СтрокаТаблицыЗначений.Ссылка) Тогда
				Если Не ЗначенияРеквизитов.ПометкаУдаления Тогда
					СтрокаТаблицыЗначений.Пометка    = ЗначениеЗаполнено(ЭлементКоллекции) И (СтрНайти("ZIP, PDF", ВРег(ЗначенияРеквизитов.Расширение)) > 0);
					СтрокаТаблицыЗначений.Ссылка     = ЭлементКоллекции;
					СтрокаТаблицыЗначений.ПутьКФайлу = ЗначенияРеквизитов.ПутьКФайлу;
					СтрокаТаблицыЗначений.Расширение = ЗначенияРеквизитов.Расширение;
					Если ЭтоFBO Тогда
						СтрокаТаблицыЗначений.НомерПоставки = СокрЛП(НомерПоставки);
					КонецЕсли;
				КонецЕсли;
				
				ДобавитьСтроку = Ложь;
			КонецЕсли;
		КонецЦикла;
		
		Если ДобавитьСтроку Тогда
			СведенияОПоставке = "";
			Если ТипЗнч(ЗначенияРеквизитов.ВладелецФайла) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
				НомерПоДаннымКлиента = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(ЗначенияРеквизитов.ВладелецФайла, "НомерПоДаннымКлиента");
				Если ЗначениеЗаполнено(НомерПоДаннымКлиента) Тогда
					СведенияОПоставке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Заявка № %1'"), 
						СокрЛП(НомерПоДаннымКлиента));
				КонецЕсли;
			Иначе
				СведенияОПоставке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '%1'"), 
					ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(ЗначенияРеквизитов.ВладелецФайла, "Комментарий"));
			КонецЕсли;
			
			СтрокаТаблицыЗначений					= Этикетки.Добавить();
			СтрокаТаблицыЗначений.Пометка			= ЗначениеЗаполнено(ЭлементКоллекции) И (СтрНайти("ZIP, PDF", ВРег(ЗначенияРеквизитов.Расширение)) > 0);
			СтрокаТаблицыЗначений.Распоряжение		= ЗначенияРеквизитов.ВладелецФайла;
			СтрокаТаблицыЗначений.СведенияОПоставке	= СведенияОПоставке;
			СтрокаТаблицыЗначений.Ссылка			= ЭлементКоллекции;
			СтрокаТаблицыЗначений.ПутьКФайлу		= ЗначенияРеквизитов.ПутьКФайлу;
			СтрокаТаблицыЗначений.Расширение		= ЗначенияРеквизитов.Расширение;
			Если ЭтоFBO Тогда
				СтрокаТаблицыЗначений.НомерПоставки = СокрЛП(НомерПоставки);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Этикетки.Сортировать("Распоряжение, НомерПоставки");
	Индекс = Этикетки.Количество();
	
	Пока Индекс > 0 Цикл
		Индекс                = Индекс - 1;
		СтрокаТаблицыЗначений = Этикетки[Индекс];
		
		Если ЗначениеЗаполнено(СтрокаТаблицыЗначений.Ссылка) 
				И ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(СтрокаТаблицыЗначений.Ссылка, "ПометкаУдаления") Тогда
			СтрокаТаблицыЗначений.Пометка       = Ложь;
			СтрокаТаблицыЗначений.НомерПоставки = "";
			СтрокаТаблицыЗначений.Ссылка        = Неопределено;
			СтрокаТаблицыЗначений.ПутьКФайлу    = "";
			СтрокаТаблицыЗначений.Расширение    = "";
			
			Отбор           = Новый Структура("Распоряжение", СтрокаТаблицыЗначений.Распоряжение);
			НайденныеСтроки = Этикетки.НайтиСтроки(Отбор);
			
			Если НайденныеСтроки.Количество() > 1 Тогда
				Этикетки.Удалить(Индекс);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Отбор           = Новый Структура("Распоряжение, Ссылка", Распоряжение, Ссылка);
	НайденныеСтроки = Этикетки.НайтиСтроки(Отбор);
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		Элементы.Этикетки.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	ПолучитьВидимуюСтрокуСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтикеткиСсылкаВыбрать(Элемент)
	
	ТекущиеДанные = Элементы.Этикетки.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОповещениеОВыборе = Новый ОписаниеОповещения("ЭтикеткиСсылкаНачалоВыбораЗавершение", ЭтотОбъект,);
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Тогда
		Если ДобавлениеФайловДоступно(ТекущиеДанные.Распоряжение) Тогда
			РаботаСФайламиКлиент.ДобавитьФайлы(
				ТекущиеДанные.Распоряжение, 
				УникальныйИдентификатор, 
				НСтр("ru = 'Все файлы'") + " (*.*)|*.*" 
					+ "|" + НСтр("ru = 'Документ PDF'") + " (*.pdf)|*.pdf" 
					+ "|" + НСтр("ru = 'Архив этикеток'") + " (*.zip)|*.zip",
				Неопределено,
				ОповещениеОВыборе);
			
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'Добавление файлов запрещено правами доступа текущего пользователя.'"), 60);
		КонецЕсли;
		
	Иначе
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("ВладелецФайла",     ТекущиеДанные.Распоряжение);
		ПараметрыОткрытия.Вставить("ТекущаяСтрока",     ТекущиеДанные.Ссылка);
		ПараметрыОткрытия.Вставить("СкрыватьВладельца", Истина);
		ПараметрыОткрытия.Вставить("РежимВыбора",       Истина);
		
		ОткрытьФорму("Обработка.РаботаСФайлами.Форма.ПрисоединенныеФайлы",
			ПараметрыОткрытия,
			Элемент,
			,
			,
			,
			ОповещениеОВыборе,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтикеткиСсылкаНачалоВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		ОбновитьНаКлиенте(РезультатЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПакетДокументов()
	
	Результат = Новый Соответствие;
	
	Для Каждого СтрокаТабличнойЧасти Из Этикетки Цикл
		Если СтрокаТабличнойЧасти.Пометка Тогда
			ДанныеФайла = РаботаСФайлами.ДанныеФайла(СтрокаТабличнойЧасти.Ссылка, УникальныйИдентификатор);
			
			Если ВРег(ДанныеФайла.Расширение) = ВРег("zip") Тогда
				ВременныйКаталог = ПолучитьИмяВременногоФайла() + ПолучитьРазделительПути();
				СоздатьКаталог(ВременныйКаталог);

				ДвоичныеДанныеАрхива = ПолучитьИзВременногоХранилища(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
				
				Попытка
					ЧтениеZipФайла = Новый ЧтениеZipФайла(ДвоичныеДанныеАрхива.ОткрытьПотокДляЧтения());
					ЧтениеZipФайла.ИзвлечьВсе(ВременныйКаталог);
					ЧтениеZipФайла.Закрыть();
				Исключение
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не удалось распаковать архив %1 по причине %2'"), 
						СтрокаТабличнойЧасти.Ссылка,
						ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
						
					Попытка
						УдалитьФайлы(ВременныйКаталог);
					Исключение
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Не удалось удалить каталог временных файлов %1 по причине %2'"), 
							ВременныйКаталог,
							ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
							
						// ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
					КонецПопытки;
					
					ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
					Продолжить;
				КонецПопытки;
				
				ФайлыPDF = НайтиФайлы(ВременныйКаталог, "*.pdf", Истина);
				Индекс   = 1;
				
				Для Каждого Файл Из ФайлыPDF Цикл
					Попытка
						ДанныеФайла = Новый ДвоичныеДанные(Файл.ПолноеИмя);
						Результат.Вставить(СтрокаТабличнойЧасти.НомерПоставки + " (" + Формат(Индекс, "ЧН=; ЧГ=0") + ")", ПоместитьВоВременноеХранилище(ДанныеФайла, УникальныйИдентификатор));
						
					Исключение
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Не удалось прочитать данные файла %1 из архива %2 по причине %3'"), 
							Файл.Имя,
							СтрокаТабличнойЧасти.Ссылка,
							ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
							
						ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
					КонецПопытки;
					
					Индекс = Индекс + 1;
				КонецЦикла;
				
				Попытка
					УдалитьФайлы(ВременныйКаталог);
				Исключение
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не удалось удалить каталог временных файлов %1 по причине %2'"), 
						ВременныйКаталог,
						ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
						
					// ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
				КонецПопытки;
				
			ИначеЕсли ВРег(ДанныеФайла.Расширение) = ВРег("pdf") Тогда
				Результат.Вставить(СтрокаТабличнойЧасти.НомерПоставки, ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ДобавлениеФайловДоступно(Ссылка)
	
	МетаданныеИмя = Ссылка.Метаданные().Имя + "ПрисоединенныеФайлы";
	
	Возврат ПравоДоступа("Добавление", Метаданные.Справочники[МетаданныеИмя]);
	
КонецФункции

&НаСервере
Функция ПолучитьЭтикеткиНаСервере()
	
	ТаблицаУчетныхЗаписей = Этикетки.Выгрузить(,"УчетнаяЗапись");
	ТаблицаУчетныхЗаписей.Свернуть("УчетнаяЗапись");
	
	Для Каждого Строка Из ТаблицаУчетныхЗаписей Цикл
		ПараметрыОтбора = Новый Структура();
		ПараметрыОтбора.Вставить("УчетнаяЗапись", Строка.УчетнаяЗапись);
		ПараметрыОтбора.Вставить("СтатусЗаказа",  Перечисления.СтатусыЗаказовТорговыхПлощадок.ГотовКОтгрузке);
		СтрокиТаблицы = Этикетки.НайтиСтроки(ПараметрыОтбора);
		
		НомераОтправлений = Новый Массив;
		Для Каждого Стр Из СтрокиТаблицы Цикл
			НомераОтправлений.Добавить(Стр.НомерПоставки);
		КонецЦикла;
		
		Ошибка = ИнтеграцияСМаркетплейсомOzonСервер.ПолучитьЭтикеткиОтправленийСТорговойПлощадки(
			Строка.УчетнаяЗапись,
			НомераОтправлений,
			5,
			3,
			Истина);
	КонецЦикла;
	
	Возврат Ошибка;
	
КонецФункции

#КонецОбласти
