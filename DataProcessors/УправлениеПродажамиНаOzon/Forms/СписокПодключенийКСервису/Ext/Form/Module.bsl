
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	КоличествоПодключений = Справочники.УчетныеЗаписиМаркетплейсов.ПолучитьКоличествоПодключений(
		ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon"));

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Если КоличествоПодключений = 0 Тогда
		ОткрытьФорму("Обработка.УправлениеПродажамиНаOzon.Форма.ПодключениеКСервису",, ВладелецФормы);
		Отказ = Истина;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник) 

	Если ИмяСобытия = "ЗакрытьФормуСпискаПодключений" Тогда
		Закрыть();
	ИначеЕсли ИмяСобытия = "ОбновитьСписокПодключений" Тогда
		Элементы.СписокПодключений.Обновить();
	ИначеЕсли ИмяСобытия = "ВывестиСообщение" И ЗначениеЗаполнено(Параметр) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Параметр);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокПодключений

&НаКлиенте
Процедура СписокПодключенийВыборЗначения(Элемент, Значение, СтандартнаяОбработка)

	ПараметрыОткрытияФормы = Новый Структура("УчетнаяЗаписьМаркетплейса", Значение);
	ОткрытьФорму("Обработка.УправлениеПродажамиНаOzon.Форма.ТекущееСостояниеАвторизации", ПараметрыОткрытияФормы);

КонецПроцедуры

&НаКлиенте
Процедура СписокПодключенийПередНачаломИзменения(Элемент, Отказ)
	
	Отказ                = Истина;
	СтандартнаяОбработка = Истина;
	
	СписокПодключенийВыборЗначения(Элемент, Элементы.СписокПодключений.ТекущиеДанные.Ссылка, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключить(Команда)

	ОткрытьФорму("Обработка.УправлениеПродажамиНаOzon.Форма.ПодключениеКСервису");

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьНастройкиАвторизации(Команда)

	ИнтеграцияСМаркетплейсомOzonКлиент.ОчисткаНастроекАвторизацииВопрос(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаКлиенте
Процедура ОчиститьНастройкиЗавершение(Результат, Параметры) Экспорт 

	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	ТекущиеДанные = Элементы.СписокПодключений.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ОчисткаУспешна = ОчиститьНастройкиНаСервере(ТекущиеДанные.Ссылка);

	Если ОчисткаУспешна Тогда
		Элементы.СписокПодключений.Обновить();
		Оповестить("УчетнаяЗаписьДеактивирована", ТекущиеДанные.Ссылка);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ОчиститьНастройкиНаСервере(УчетнаяЗаписьМаркетплейса)

	ОчисткаУспешна = ИнтеграцияСМаркетплейсомOzonСервер.ОчиститьНастройкиУчетнойЗаписи(УчетнаяЗаписьМаркетплейса);

	Возврат ОчисткаУспешна;

КонецФункции

#КонецОбласти