#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Команды

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//
// Возвращаемое значение:
//   СтрокаТаблицыЗначений - новая команда отчета.
Функция ДобавитьКомандуСостояниеОбеспеченияСпискаЗаказовКлиента(КомандыОтчетов) Экспорт
	
	Если ПравоДоступа("Просмотр", Метаданные.Обработки.СостояниеОбеспеченияЗаказов) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Обработки.СостояниеОбеспеченияЗаказов.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Состояние обеспечения'");
		
		КомандаОтчет.МножественныйВыбор = Истина;
		КомандаОтчет.Важность = "Важное";
		КомандаОтчет.ИмяПараметраФормы = "Заказы";
		КомандаОтчет.ПараметрыФормы = Новый Структура("КонтекстИспользования", "СПИСОКЗАКАЗОВ");
		
		КомандаОтчет.ФункциональныеОпции = "ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента";
		
		КомандаОтчет.ЕстьУсловияВидимости = Истина;
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаОтчет,
			"ЭтоЗаказКакСчет",
			Ложь, 
			ВидСравненияКомпоновкиДанных.Равно);
		
		Возврат КомандаОтчет;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

// Определяет параметры доступности номенклатуры для заказа. Используется в командах резервирования на складе.
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура - номенклатура
//  Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика 
//  Назначение - СправочникСсылка.Назначения - назначение
//  Обособленно - Булево - строка обособлена
//  Склад - СправочникСсылка.Склады - склад
//  ЗаказНаОтгрузку - ОпределяемыйТип.ОжидаемаяОтгрузка - заказ
// Возвращаемое значение:
//  Структура - структура с полями:
//   * ПотребностьПоЗаказу - Число - потребность в товаре на складе в заказе, кроме строк с действием "Не обеспечивать" и "Отгрузить"
//   * РезервПодЗаказом - Число - количество потребности обеспеченное действием "Резервировать на складе в заказе"
//   * Доступно - Число - количество доступное для отгрузки заказу, для строк с действиями "Резервировать по мере поступления" и "К обеспечению"
Функция ЧисловыеПараметрыРезервирования(Номенклатура, Характеристика, Назначение, Обособленно, Склад, ЗаказНаОтгрузку) Экспорт
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	Запрос.УстановитьПараметр("Назначение", Назначение);
	Запрос.УстановитьПараметр("Обособленно", Обособленно);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("ЗаказНаОтгрузку", ЗаказНаОтгрузку);
	Текст =
		"ВЫБРАТЬ
		|	&Номенклатура КАК Номенклатура,
		|	&Характеристика КАК Характеристика,
		|	ВЫБОР
		|		КОГДА &Обособленно
		|			ТОГДА &Назначение
		|		ИНАЧЕ
		|			ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	КОНЕЦ КАК Назначение,
		|	&Склад КАК Склад,
		|	&ЗаказНаОтгрузку КАК ЗаказНаОтгрузку
		|ПОМЕСТИТЬ Аналитики";
		
	Тексты = Новый Массив();
	Тексты.Добавить(Текст);
	Текст = ТекстЗапросаЧисловыеПараметрыРезервирования(Ложь);
	Тексты.Добавить(Текст);
	Текст =
		"ВЫБРАТЬ
		|	Показатели.ПотребностьПоЗаказу КАК ПотребностьПоЗаказу,
		|	Показатели.РезервироватьНаСкладеПодЗаказом КАК РезервироватьНаСкладеПодЗаказом,
		|	Показатели.Свободно КАК Свободно,
		|	Показатели.ЗарезервированоПоЗаказу КАК ЗарезервированоПоЗаказу
		|ИЗ
		|	Показатели КАК Показатели";
	Тексты.Добавить(Текст);
	Запрос.Текст = СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());
	Выборка = Запрос.Выполнить().Выбрать();
	
	Показатели = Новый Структура("ПотребностьПоЗаказу,РезервПодЗаказом,Доступно", 0, 0, 0);
	Если Выборка.Следующий() Тогда
		
		Показатели.ПотребностьПоЗаказу = Выборка.ПотребностьПоЗаказу;
		Показатели.РезервПодЗаказом = Выборка.РезервироватьНаСкладеПодЗаказом;
		Показатели.Доступно = Выборка.Свободно + Выборка.ЗарезервированоПоЗаказу;
		Показатели.Доступно = Макс(0, 
			Мин(Показатели.Доступно, Показатели.ПотребностьПоЗаказу - Показатели.РезервПодЗаказом));
			
	КонецЕсли;
	Возврат Показатели;
	
КонецФункции

// Определяет параметры доступности номенклатуры для заказа.
// Параметры:
//  СУчетомИзменений - Булево - Истина, если показатели получают для непроведенного документа
// Возвращаемое значение:
//  Строка - текст запроса.
Функция ТекстЗапросаЧисловыеПараметрыРезервирования(СУчетомИзменений) Экспорт
	
	Тексты = Новый Массив();
	Если СУчетомИзменений Тогда
		Текст =
			"ВЫБРАТЬ
			|	Потребности.Заказ КАК Заказ,
			|	Потребности.Номенклатура КАК Номенклатура,
			|	Потребности.Характеристика КАК Характеристика,
			|	Потребности.Склад КАК Склад,
			|	Потребности.Назначение КАК Назначение,
			|	Потребности.РезервироватьНаСкладеОстаток КАК РезервироватьНаСкладеОстаток,
			|	Потребности.РезервироватьПоМереПоступленияОстаток КАК РезервироватьПоМереПоступленияОстаток,
			|	Потребности.ОтложитьРезервированиеОстаток КАК ОтложитьРезервированиеОстаток,
			|	Потребности.КОбеспечениюОстаток КАК КОбеспечениюОстаток
			|ПОМЕСТИТЬ Потребности
			|ИЗ
			|	РегистрНакопления.ЗапасыИПотребности.Остатки(,
			|		(Заказ,Номенклатура,Характеристика,Склад,Назначение) В(
			|			ВЫБРАТЬ
			|				Таблица.ЗаказНаОтгрузку КАК Заказ,
			|				Таблица.Номенклатура КАК Номенклатура,
			|				Таблица.Характеристика КАК Характеристика,
			|				Таблица.Склад КАК Склад,
			|				Таблица.Назначение КАК Назначение
			|			ИЗ
			|				Аналитики КАК Таблица)) КАК Потребности
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Изменения.Заказ КАК Заказ,
			|	Изменения.Номенклатура КАК Номенклатура,
			|	Изменения.Характеристика КАК Характеристика,
			|	Изменения.Склад КАК Склад,
			|	Изменения.Назначение КАК Назначение,
			|	Изменения.РезервироватьНаСкладе КАК РезервироватьНаСкладеОстаток,
			|	Изменения.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступленияОстаток,
			|	Изменения.ОтложитьРезервирование КАК ОтложитьРезервированиеОстаток,
			|	Изменения.КОбеспечению КАК КОбеспечениюОстаток
			|ИЗ
			|	ЗапасыИПотребностиИзменение КАК Изменения
			|ГДЕ
			|	Изменения.РезервироватьНаСкладе <> 0
			|	ИЛИ Изменения.РезервироватьПоМереПоступления <> 0
			|	ИЛИ Изменения.ОтложитьРезервирование <> 0
			|	ИЛИ Изменения.КОбеспечению <> 0
			|ИНДЕКСИРОВАТЬ ПО
			|	Заказ, Номенклатура, Характеристика, Склад, Назначение";
	Иначе
		Текст =
			"ВЫБРАТЬ
			|	Потребности.Заказ КАК Заказ,
			|	Потребности.Номенклатура КАК Номенклатура,
			|	Потребности.Характеристика КАК Характеристика,
			|	Потребности.Склад КАК Склад,
			|	Потребности.Назначение КАК Назначение,
			|	Потребности.РезервироватьНаСкладеОстаток КАК РезервироватьНаСкладеОстаток,
			|	Потребности.РезервироватьПоМереПоступленияОстаток КАК РезервироватьПоМереПоступленияОстаток,
			|	Потребности.ОтложитьРезервированиеОстаток КАК ОтложитьРезервированиеОстаток,
			|	Потребности.КОбеспечениюОстаток КАК КОбеспечениюОстаток
			|ПОМЕСТИТЬ Потребности
			|ИЗ
			|	РегистрНакопления.ЗапасыИПотребности.Остатки(,
			|		(Заказ,Номенклатура,Характеристика,Склад,Назначение) В(
			|			ВЫБРАТЬ
			|				Таблица.ЗаказНаОтгрузку КАК Заказ,
			|				Таблица.Номенклатура КАК Номенклатура,
			|				Таблица.Характеристика КАК Характеристика,
			|				Таблица.Склад КАК Склад,
			|				Таблица.Назначение КАК Назначение
			|			ИЗ
			|				Аналитики КАК Таблица)) КАК Потребности
			|ИНДЕКСИРОВАТЬ ПО
			|	Заказ, Номенклатура, Характеристика, Склад, Назначение";
	КонецЕсли;
	Тексты.Добавить(Текст);
	
	Если СУчетомИзменений Тогда
		Текст =
			"ВЫБРАТЬ
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	Набор.Назначение КАК Назначение,
			|	СУММА(Набор.РезервироватьНаСкладеОстаток) КАК РезервироватьНаСкладеОстаток,
			|	СУММА(Набор.РезервироватьПоМереПоступленияОстаток) КАК РезервироватьПоМереПоступленияОстаток,
			|	СУММА(Набор.ВНаличииОстаток) КАК ВНаличииОстаток
			|ПОМЕСТИТЬ Остатки
			|ИЗ(
			|	ВЫБРАТЬ
			|		ЗапасыИПотребности.Номенклатура КАК Номенклатура,
			|		ЗапасыИПотребности.Характеристика КАК Характеристика,
			|		ЗапасыИПотребности.Склад КАК Склад,
			|		ЗапасыИПотребности.Назначение КАК Назначение,
			|		ЗапасыИПотребности.РезервироватьНаСкладеОстаток КАК РезервироватьНаСкладеОстаток,
			|		ЗапасыИПотребности.РезервироватьПоМереПоступленияОстаток КАК РезервироватьПоМереПоступленияОстаток,
			|		ЗапасыИПотребности.ВНаличииОстаток КАК ВНаличииОстаток
			|	ИЗ
			|		РегистрНакопления.ЗапасыИПотребности.Остатки(,
			|			(Номенклатура,Характеристика,Склад,Назначение) В(
			|				ВЫБРАТЬ
			|					Таблица.Номенклатура КАК Номенклатура,
			|					Таблица.Характеристика КАК Характеристика,
			|					Таблица.Склад КАК Склад,
			|					Таблица.Назначение КАК Назначение
			|				ИЗ
			|					Аналитики КАК Таблица)) КАК ЗапасыИПотребности
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Изменения.Номенклатура КАК Номенклатура,
			|		Изменения.Характеристика КАК Характеристика,
			|		Изменения.Склад КАК Склад,
			|		Изменения.Назначение КАК Назначение,
			|		Изменения.РезервироватьНаСкладе КАК РезервироватьНаСкладеОстаток,
			|		Изменения.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступленияОстаток,
			|		Изменения.ВНаличии КАК ВНаличииОстаток
			|	ИЗ
			|		ЗапасыИПотребностиИзменение КАК Изменения
			|	ГДЕ
			|		Изменения.РезервироватьНаСкладе <> 0
			|		ИЛИ Изменения.РезервироватьПоМереПоступления <> 0
			|		ИЛИ Изменения.ВНаличии <> 0) КАК Набор
			|СГРУППИРОВАТЬ ПО
			|	Набор.Номенклатура,
			|	Набор.Характеристика,
			|	Набор.Склад,
			|	Набор.Назначение
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура, Характеристика, Склад, Назначение";
	Иначе
		Текст =
			"ВЫБРАТЬ
			|	ЗапасыИПотребности.Номенклатура КАК Номенклатура,
			|	ЗапасыИПотребности.Характеристика КАК Характеристика,
			|	ЗапасыИПотребности.Склад КАК Склад,
			|	ЗапасыИПотребности.Назначение КАК Назначение,
			|	ЗапасыИПотребности.РезервироватьНаСкладеОстаток КАК РезервироватьНаСкладеОстаток,
			|	ЗапасыИПотребности.РезервироватьПоМереПоступленияОстаток КАК РезервироватьПоМереПоступленияОстаток,
			|	ЗапасыИПотребности.ВНаличииОстаток КАК ВНаличииОстаток
			|ПОМЕСТИТЬ Остатки
			|ИЗ
			|	РегистрНакопления.ЗапасыИПотребности.Остатки(,
			|		(Номенклатура,Характеристика,Склад,Назначение) В(
			|			ВЫБРАТЬ
			|				Таблица.Номенклатура КАК Номенклатура,
			|				Таблица.Характеристика КАК Характеристика,
			|				Таблица.Склад КАК Склад,
			|				Таблица.Назначение КАК Назначение
			|			ИЗ
			|				Аналитики КАК Таблица)) КАК ЗапасыИПотребности
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура, Характеристика, Склад, Назначение";
	КонецЕсли;
	Тексты.Добавить(Текст);
	
	Текст =
		"ВЫБРАТЬ
		|	Набор.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|	Набор.Номенклатура КАК Номенклатура,
		|	Набор.Характеристика КАК Характеристика,
		|	Набор.Склад КАК Склад,
		|	Набор.Назначение КАК Назначение,
		|	СУММА(Набор.ПотребностьПоЗаказу) КАК ПотребностьПоЗаказу,
		|	СУММА(Набор.РезервироватьНаСкладеПодЗаказом) КАК РезервироватьНаСкладеПодЗаказом,
		|	СУММА(Набор.РезервироватьПоМереПоступленияПодЗаказом) КАК РезервироватьПоМереПоступленияПодЗаказом,
		|	СУММА(Набор.РезервироватьНаСкладеВсего) КАК РезервироватьНаСкладеВсего,
		|	СУММА(Набор.РезервироватьПоМереПоступленияВсего) КАК РезервироватьПоМереПоступленияВсего,
		|	СУММА(Набор.ВНаличииВсего) КАК ВНаличииВсего,
		|	СУММА(Набор.Свободно) КАК Свободно,
		|	СУММА(Набор.ЗарезервированоПоЗаказу) КАК ЗарезервированоПоЗаказу
		|ПОМЕСТИТЬ Показатели
		|ИЗ(
		|	ВЫБРАТЬ
		|		Потребности.Заказ КАК ЗаказНаОтгрузку,
		|		Потребности.Номенклатура КАК Номенклатура,
		|		Потребности.Характеристика КАК Характеристика,
		|		Потребности.Склад КАК Склад,
		|		Потребности.Назначение КАК Назначение,
		|		Потребности.РезервироватьНаСкладеОстаток
		|			+ Потребности.РезервироватьПоМереПоступленияОстаток
		|			+ Потребности.ОтложитьРезервированиеОстаток
		|			+ Потребности.КОбеспечениюОстаток КАК ПотребностьПоЗаказу,
		|		Потребности.РезервироватьНаСкладеОстаток КАК РезервироватьНаСкладеПодЗаказом,
		|		Потребности.РезервироватьПоМереПоступленияОстаток КАК РезервироватьПоМереПоступленияПодЗаказом,
		|		0 КАК РезервироватьНаСкладеВсего,
		|		0 КАК РезервироватьПоМереПоступленияВсего,
		|		0 КАК ВНаличииВсего,
		|		0 КАК Свободно,
		|		0 КАК ЗарезервированоПоЗаказу
		|	ИЗ
		|		Потребности КАК Потребности
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Аналитики.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|		ЗапасыИПотребности.Номенклатура КАК Номенклатура,
		|		ЗапасыИПотребности.Характеристика КАК Характеристика,
		|		ЗапасыИПотребности.Склад КАК Склад,
		|		ЗапасыИПотребности.Назначение КАК Назначение,
		|		0 КАК ПотребностьПоЗаказу,
		|		0 КАК РезервироватьНаСкладеПодЗаказом,
		|		0 КАК РезервироватьПоМереПоступленияПодЗаказом,
		|		ЗапасыИПотребности.РезервироватьНаСкладеОстаток КАК РезервироватьНаСкладеВсего,
		|		ЗапасыИПотребности.РезервироватьПоМереПоступленияОстаток КАК РезервироватьПоМереПоступленияВсего,
		|		ЗапасыИПотребности.ВНаличииОстаток КАК ВНаличииВсего,
		|		ВЫБОР
		|			КОГДА ЗапасыИПотребности.ВНаличииОстаток
		|					- ЗапасыИПотребности.РезервироватьНаСкладеОстаток
		|					- ЗапасыИПотребности.РезервироватьПоМереПоступленияОстаток > 0
		|				ТОГДА
		|					ЗапасыИПотребности.ВНаличииОстаток
		|					- ЗапасыИПотребности.РезервироватьНаСкладеОстаток
		|					- ЗапасыИПотребности.РезервироватьПоМереПоступленияОстаток
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК Свободно,
		|		0 КАК ЗарезервированоПоЗаказу
		|	ИЗ
		|		Остатки КАК ЗапасыИПотребности
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Аналитики КАК Аналитики
		|			ПО Аналитики.Номенклатура = ЗапасыИПотребности.Номенклатура
		|			И Аналитики.Характеристика = ЗапасыИПотребности.Характеристика
		|			И Аналитики.Склад = ЗапасыИПотребности.Склад
		|			И Аналитики.Назначение = ЗапасыИПотребности.Назначение
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ОбеспечениеЗаказа.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|		ОбеспечениеЗаказа.Номенклатура КАК Номенклатура,
		|		ОбеспечениеЗаказа.Характеристика КАК Характеристика,
		|		ОбеспечениеЗаказа.Склад КАК Склад,
		|		ОбеспечениеЗаказа.Назначение КАК Назначение,
		|		0 КАК ПотребностьПоЗаказу,
		|		0 КАК РезервироватьНаСкладеПодЗаказом,
		|		0 КАК РезервироватьПоМереПоступленияПодЗаказом,
		|		0 КАК РезервироватьНаСкладеВсего,
		|		0 КАК РезервироватьПоМереПоступленияВсего,
		|		0 КАК ВНаличииВсего,
		|		0 КАК Свободно,
		|		ОбеспечениеЗаказа.Зарезервировано КАК ЗарезервированоПоЗаказу
		|	ИЗ
		|		РегистрСведений.РаспределениеЗапасов КАК ОбеспечениеЗаказа
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Аналитики КАК Таблица
		|		ПО Таблица.ЗаказНаОтгрузку = ОбеспечениеЗаказа.ЗаказНаОтгрузку
		|			И Таблица.Номенклатура = ОбеспечениеЗаказа.Номенклатура
		|			И Таблица.Характеристика = ОбеспечениеЗаказа.Характеристика
		|			И Таблица.Склад = ОбеспечениеЗаказа.Склад
		|			И Таблица.Назначение = ОбеспечениеЗаказа.Назначение
		|			И ОбеспечениеЗаказа.Состояние В(
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченНаСкладе),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ЗарезервированПриПоступлении))) КАК Набор
		|СГРУППИРОВАТЬ ПО
		|	Набор.ЗаказНаОтгрузку,
		|	Набор.Номенклатура,
		|	Набор.Характеристика,
		|	Набор.Склад,
		|	Набор.Назначение
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаОтгрузку, Номенклатура, Характеристика, Склад, Назначение";
	Тексты.Добавить(Текст);
	
	Текст = СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());
	Возврат Текст;
	
КонецФункции

#КонецОбласти

#КонецЕсли
