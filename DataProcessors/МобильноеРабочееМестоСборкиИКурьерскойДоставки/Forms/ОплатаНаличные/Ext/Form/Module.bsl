
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Распоряжение", Распоряжение);
	Параметры.Свойство("СуммаКОплате", СуммаКОплате);
	Параметры.Свойство("Валюта", Валюта);
	Параметры.Свойство("Склад", Склад);
	
	ОбновитьДанныеФормы();
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолученнаяСуммаПриИзменении(Элемент)
	
	ПересчитатьСдачу();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьКомандуПоПроцессу(Команда)
	
	Отказ = Ложь;
	
	Если ПолученнаяСумма >= СуммаКОплате Тогда
		
		ПринятьОплатуИОформитьЧек(Отказ);
		
		Если Не Отказ Тогда
			Закрыть(Истина);
		КонецЕсли;
	Иначе
		Если ПолученнаяСумма = 0 Тогда
			ТекстОшибки = НСтр("ru='Не указана полученная сумма.'");
		Иначе
			ТекстОшибки = НСтр("ru='Полученная сумма меньше суммы к оплате.'");
		КонецЕсли;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПолученнуюСумму(Команда)
	
	Если Команда.Имя = "ДобавитьБезСдачи" Тогда
		ПолученнаяСумма = СуммаКОплате;
	ИначеЕсли Команда.Имя = "Добавить5000" Тогда
		ПолученнаяСумма = ПолученнаяСумма + 5000;
	ИначеЕсли Команда.Имя = "Добавить1000" Тогда
		ПолученнаяСумма = ПолученнаяСумма + 1000;
	ИначеЕсли Команда.Имя = "Добавить500" Тогда
		ПолученнаяСумма = ПолученнаяСумма + 500;
	ИначеЕсли Команда.Имя = "Добавить200" Тогда
		ПолученнаяСумма = ПолученнаяСумма + 200;
	ИначеЕсли Команда.Имя = "Добавить100" Тогда
		ПолученнаяСумма = ПолученнаяСумма + 100;
	КонецЕсли;
	
	ПересчитатьСдачу();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДанныеФормы()
	
	Модуль = Обработки.МобильноеРабочееМестоСборкиИКурьерскойДоставки;
	СуммаКОплатеПредставление = Модуль.СуммаКОплатеПредставление(ЭтотОбъект);
	ВалютаПредставление = Модуль.ВалютаПредставление(Валюта);
	
	Элементы.ДекорацияСуммаКОплате.Заголовок = Новый ФорматированнаяСтрока(СуммаКОплатеПредставление,
		ШрифтыСтиля.ШрифтТекстаУвеличенныйВыделенныйСборкаИДоставка,
		ЦветаСтиля.ЦветТекстаОсновнойСборкаИДоставка);
		
	Элементы.ДекорацияВводСуммыВалюта.Заголовок = Новый ФорматированнаяСтрока(ВалютаПредставление,
		ШрифтыСтиля.ШрифтТекстаУвеличенныйВыделенныйСборкаИДоставка,
		ЦветаСтиля.ЦветТекстаОсновнойСборкаИДоставка);
		
	ПересчитатьСдачу();
	ПереопределитьТекущуюКомандуПоПроцессу();
		
КонецПроцедуры

&НаСервере
Процедура ПересчитатьСдачу()
	
	Сдача = ПолученнаяСумма - СуммаКОплате;
	Модуль = Обработки.МобильноеРабочееМестоСборкиИКурьерскойДоставки;
	СдачаПредставление = Модуль.СдачаПредставление(ЭтотОбъект);
	
	Элементы.ДекорацияСдача.Заголовок = Новый ФорматированнаяСтрока(СдачаПредставление,
		ШрифтыСтиля.ШрифтТекстаСборкаИДоставка,
		ЦветаСтиля.ЦветТекстаОсновнойСборкаИДоставка);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПолученнаяСумма.Имя);
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПолученнаяСумма");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ОтборЭлемента.ПравоеЗначение = СуммаКОплате;
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтрицательныхЗначений);
	
КонецПроцедуры

#Область ДействияПоПроцессу

&НаСервере
Процедура ПереопределитьТекущуюКомандуПоПроцессу()
	
	Обработки.МобильноеРабочееМестоСборкиИКурьерскойДоставки.ПереопределитьТекущуюКомандуПоПроцессу(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПринятьОплатуИОформитьЧек(Отказ = Ложь) 
	
	Модуль = Обработки.МобильноеРабочееМестоСборкиИКурьерскойДоставки;
	Модуль.СоздатьПКОВКассуКурьера(Распоряжение, СуммаКОплате, Отказ);
	
	Если СтрНайти(ТекущееДействие, "ОформитьЧек")
		И Не Отказ Тогда
		Модуль.ОформитьЧек(Распоряжение, Отказ);
	КонецЕсли;
	
	Если Не Отказ Тогда
		Модуль.ЗавершитьДоставку(Распоряжение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
