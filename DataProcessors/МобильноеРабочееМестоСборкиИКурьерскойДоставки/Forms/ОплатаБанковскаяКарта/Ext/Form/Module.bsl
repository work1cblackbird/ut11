
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Распоряжение", Распоряжение);
	Параметры.Свойство("СуммаКОплате", СуммаКОплате);
	Параметры.Свойство("Валюта", Валюта);
	Параметры.Свойство("Склад", Склад);
	
	ОбновитьДанныеФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьКомандуПоПроцессу(Команда)
	
	Отказ = Ложь;
	
	ПринятьОплатуИОформитьЧек(Отказ);
	
	Если Не Отказ Тогда
		Закрыть(Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДанныеФормы()
	
	Модуль = Обработки.МобильноеРабочееМестоСборкиИКурьерскойДоставки;
	СуммаКОплатеПредставление = Модуль.СуммаКОплатеПредставление(ЭтаФорма);
	
	Элементы.ДекорацияСуммаКОплате.Заголовок = Новый ФорматированнаяСтрока(СуммаКОплатеПредставление,
		ШрифтыСтиля.ШрифтТекстаУвеличенныйВыделенныйСборкаИДоставка,
		ЦветаСтиля.ЦветТекстаОсновнойСборкаИДоставка);
		
	ПереопределитьТекущуюКомандуПоПроцессу();
		
КонецПроцедуры

#Область ДействияПоПроцессу

&НаСервере
Процедура ПереопределитьТекущуюКомандуПоПроцессу()
	
	Обработки.МобильноеРабочееМестоСборкиИКурьерскойДоставки.ПереопределитьТекущуюКомандуПоПроцессу(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПринятьОплатуИОформитьЧек(Отказ = Ложь) 
	
	Модуль = Обработки.МобильноеРабочееМестоСборкиИКурьерскойДоставки;
	Модуль.СоздатьЭквайринговуюОперацию(Распоряжение, СуммаКОплате, Отказ);
	
	Если СтрНайти(ТекущееДействие, "ОформитьЧек")
		И Не Отказ Тогда
		Модуль.ОформитьЧек(Распоряжение, Отказ);
	КонецЕсли;
	
	Если Не Отказ Тогда
		Модуль.ЗавершитьДоставку(Распоряжение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
