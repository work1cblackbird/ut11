
#Область ОбработчикиСобытийФормы
// Код процедур и функций

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Диспетчер     = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Пользователи.ТекущийПользователь(), "ФизическоеЛицо");
	ТипСотрудника = Обработки.МобильноеРабочееМестоСборкиИКурьерскойДоставки.ТипСотрудникаДиспетчер();
	ЭтоКонтроль   = Параметры.ЭтоКонтроль;
	ПоказыватьФотоТоваров = Параметры.ПоказыватьФотоТоваров;
	ТекущийСклад = Параметры.ТекущийСклад;
	
	УстановитьУсловноеОформление();
	ОбновитьДанныеФормыСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СфомироватьПредставлениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.Склады.Форма.ФормаВыбора") Тогда
		
		ТекущийСклад = ВыбранноеЗначение;
		СфомироватьПредставлениеОтборов();
		ОбновитьДанныеФормы();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокСотрудников

&НаКлиенте
Процедура СписокСотрудникиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Описание = Новый ОписаниеОповещения("ОбновитьДанныеФормы", ЭтаФорма);
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Сотрудник",    Элемент.ТекущиеДанные.Сотрудник);
	ПараметрыФормы.Вставить("ТекущийСклад", ТекущийСклад);
	ПараметрыФормы.Вставить("ЭтоКонтроль",  ЭтоКонтроль);
	ПараметрыФормы.Вставить("ЭтоДоставка",  Истина);
	ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
	
	ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоСборкиИКурьерскойДоставки.Форма.ДиспетчерНазначениеРаспоряжений",ПараметрыФормы,
		ЭтаФорма,,,,Описание,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтборыОчистить(Команда)
	
	ТекущийСклад = Неопределено;
	СфомироватьПредставлениеОтборов();
	ОбновитьДанныеФормы();

КонецПроцедуры

&НаКлиенте
Процедура ОтборыОткрыть(Команда)
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытияФормы.Вставить("МножественныйВыбор", Ложь);
	ПараметрыОткрытияФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыОткрытияФормы.Вставить("Отбор", Новый Структура("Ссылка", ДоступныеСклады()));
	
	ОткрытьФорму(
		"Справочник.Склады.Форма.ФормаВыбора",
		ПараметрыОткрытияФормы,
		ЭтаФорма,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьДанныеФормы(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОбновитьДанныеФормыСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеФормыСервер()
	
	СписокСотрудников.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СотрудникиМагазинов.Склад КАК Склад
		|ПОМЕСТИТЬ СкладыДоступныеДиспетчеру
		|ИЗ
		|	РегистрСведений.СотрудникиМагазинов КАК СотрудникиМагазинов
		|ГДЕ
		|	СотрудникиМагазинов.Диспетчер
		|	И СотрудникиМагазинов.Склад В ИЕРАРХИИ(&Склад)
		|	И СотрудникиМагазинов.Сотрудник = &Диспетчер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СотрудникиМагазинов.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ Курьеры
		|ИЗ
		|	СкладыДоступныеДиспетчеру КАК СкладыДоступныеДиспетчеру
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиМагазинов КАК СотрудникиМагазинов
		|		ПО СкладыДоступныеДиспетчеру.Склад = СотрудникиМагазинов.Склад
		|ГДЕ
		|	СотрудникиМагазинов.Курьер
		|
		|СГРУППИРОВАТЬ ПО
		|	СотрудникиМагазинов.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтатусыСборкиИДоставки.Распоряжение.Курьер КАК Курьер,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СтатусыСборкиИДоставки.Распоряжение) КАК Доставляется
		|ПОМЕСТИТЬ РаспоряженияДоставляется
		|ИЗ
		|	РегистрСведений.СтатусыСборкиИДоставки КАК СтатусыСборкиИДоставки
		|ГДЕ
		|	СтатусыСборкиИДоставки.Статус = &Доставляется
		|	И СтатусыСборкиИДоставки.Распоряжение.Склад В ИЕРАРХИИ(&Склад)
		|
		|СГРУППИРОВАТЬ ПО
		|	СтатусыСборкиИДоставки.Распоряжение.Курьер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтатусыСборкиИДоставки.Распоряжение.Курьер КАК Курьер,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СтатусыСборкиИДоставки.Распоряжение) КАК Доставлено,
		|	МАКСИМУМ(СтатусыСборкиИДоставки.ДатаДоставки) КАК ПоследняяДатаДоставки
		|ПОМЕСТИТЬ РаспоряженияДоставлено
		|ИЗ
		|	РегистрСведений.СтатусыСборкиИДоставки КАК СтатусыСборкиИДоставки
		|ГДЕ
		|	СтатусыСборкиИДоставки.ДатаДоставки МЕЖДУ &НачалоДня И &КонецДня
		|	И СтатусыСборкиИДоставки.Распоряжение.Склад В ИЕРАРХИИ(&Склад)
		|
		|СГРУППИРОВАТЬ ПО
		|	СтатусыСборкиИДоставки.Распоряжение.Курьер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтатусыСборкиИДоставки.Распоряжение.Курьер КАК Курьер,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СтатусыСборкиИДоставки.Распоряжение) КАК Назначено
		|ПОМЕСТИТЬ РаспоряженияНазначено
		|ИЗ
		|	РегистрСведений.СтатусыСборкиИДоставки КАК СтатусыСборкиИДоставки
		|ГДЕ
		|	СтатусыСборкиИДоставки.Статус В(&СтатусыКДоставке)
		|	И СтатусыСборкиИДоставки.Распоряжение.Склад В ИЕРАРХИИ(&Склад)
		|
		|СГРУППИРОВАТЬ ПО
		|	СтатусыСборкиИДоставки.Распоряжение.Курьер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(Курьеры.Сотрудник, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК Сотрудник,
		|	ЕСТЬNULL(РаспоряженияДоставляется.Доставляется, 0) КАК Доставляется,
		|	ЕСТЬNULL(РаспоряженияДоставлено.Доставлено, 0) КАК Доставлено,
		|	ВЫБОР
		|		КОГДА НЕ Курьеры.Сотрудник ЕСТЬ NULL
		|			ТОГДА ЕСТЬNULL(РаспоряженияНазначено.Назначено, 0)
		|	КОНЕЦ КАК Назначено,
		|	ЕСТЬNULL(РаспоряженияДоставлено.ПоследняяДатаДоставки, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ПоследняяДоставка,
		|	ВЫБОР
		|		КОГДА Курьеры.Сотрудник ЕСТЬ NULL
		|			ТОГДА ЕСТЬNULL(РаспоряженияНазначено.Назначено, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Назначить
		|ИЗ
		|	Курьеры КАК Курьеры
		|		ЛЕВОЕ СОЕДИНЕНИЕ РаспоряженияДоставляется КАК РаспоряженияДоставляется
		|		ПО Курьеры.Сотрудник = РаспоряженияДоставляется.Курьер
		|		ЛЕВОЕ СОЕДИНЕНИЕ РаспоряженияДоставлено КАК РаспоряженияДоставлено
		|		ПО Курьеры.Сотрудник = РаспоряженияДоставлено.Курьер
		|		ПОЛНОЕ СОЕДИНЕНИЕ РаспоряженияНазначено КАК РаспоряженияНазначено
		|		ПО Курьеры.Сотрудник = РаспоряженияНазначено.Курьер
		|ИТОГИ
		|	СУММА(Доставляется),
		|	СУММА(Доставлено),
		|	СУММА(Назначено),
		|	СУММА(Назначить)
		|ПО
		|	ОБЩИЕ";
	
	Запрос.УстановитьПараметр("Диспетчер", Диспетчер);
	Запрос.УстановитьПараметр("Склад", ТекущийСклад);
	Запрос.УстановитьПараметр("Доставляется", Перечисления.СтатусыСборкиИДоставки.Доставляется);
	Запрос.УстановитьПараметр("СтатусыКДоставке", СкладыСервер.СтатусыРаспоряженийДляНачалаДоставкиКурьером());
	Запрос.УстановитьПараметр("НачалоДня", НачалоДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("КонецДня", КонецДня(ТекущаяДатаСеанса()));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ВыборкаОбщийИтог.Следующий();	// Общий итог
	Элементы.НазначеноИтогЗначение.Заголовок = ВыборкаОбщийИтог.Назначено;
	Элементы.ДоставляетсяИтогЗначение.Заголовок = ВыборкаОбщийИтог.Доставляется;
	Элементы.ДоставленоИтогЗначение.Заголовок = ВыборкаОбщийИтог.Доставлено;
	Элементы.НазначитьИтогЗначение.Заголовок = ВыборкаОбщийИтог.Назначить;
	
	ВыборкаДетальныеЗаписи = ВыборкаОбщийИтог.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если НЕ ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Сотрудник) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = СписокСотрудников.Добавить();
		НоваяСтрока.Сотрудник = ВыборкаДетальныеЗаписи.Сотрудник;
		
		НоваяСтрока.ДоставляетсяПодпись = НСтр("ru='Доставляется'") + " ";
		НоваяСтрока.Доставляется = ВыборкаДетальныеЗаписи.Доставляется;
		
		НоваяСтрока.ДоставленоПодпись = НСтр("ru='Доставлено'") + " ";
		НоваяСтрока.Доставлено = ВыборкаДетальныеЗаписи.Доставлено;
		
		НоваяСтрока.Назначено = ВыборкаДетальныеЗаписи.Назначено;
		
		НоваяСтрока.ПоследняяДоставкаПодпись = НСтр("ru='Последняя сборка'") + " - ";
		Если ВыборкаДетальныеЗаписи.ПоследняяДоставка = Дата(1,1,1) Тогда
			НоваяСтрока.ПоследняяДоставка = НСтр("ru='нет'");
		Иначе
			НоваяСтрока.ПоследняяДоставка = НСтр("ru='в'") + " " + Формат(ВыборкаДетальныеЗаписи.ПоследняяДоставка,"ДФ=HH:mm");
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СфомироватьПредставлениеОтборов()
	
	ПредставленияОтборов = "";
	Если ЗначениеЗаполнено(ТекущийСклад) Тогда
		ПредставленияОтборов = ТекущийСклад;
		Элементы.КомандаОтборыОткрыть.ЦветТекста = WebЦвета.Черный;
		Элементы.ГруппаКомандаОтборыКоличествоОчистить.Видимость = Истина;
		Элементы.РамкаОтборыОткрыть.Картинка = БиблиотекаКартинок.РамкаМенюЧерная;
	Иначе
		ПредставленияОтборов = НСтр("ru='Выбрать магазин/склад'");
		Элементы.КомандаОтборыОткрыть.ЦветТекста = WebЦвета.ТемноСерый;
		Элементы.ГруппаКомандаОтборыКоличествоОчистить.Видимость = Ложь;
		Элементы.РамкаОтборыОткрыть.Картинка = БиблиотекаКартинок.РамкаМенюСерая;
	КонецЕсли;
	
	Элементы.КомандаОтборыОткрыть.Заголовок = ПредставленияОтборов;
	
КонецПроцедуры

&НаСервере
Функция ДоступныеСклады()
	
	Возврат СкладыСервер.ДоступныеСкладыСотрудникаПоТипуСотрудника(Диспетчер, ТипСотрудника);
	
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Если ЭтоКонтроль Тогда
		Заголовок = НСтр("ru='Контроль доставки'");
	КонецЕсли;
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокСотрудникиНазначено.Имя);
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокСотрудников.Назначено");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "0";
	Если ЭтоКонтроль Тогда
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПроблема);
	Иначе
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаСтатусаГотовСборкаИДоставка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФорму(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт 
	ОбновитьДанныеФормы();
	СфомироватьПредставлениеОтборов();
КонецПроцедуры

#КонецОбласти
