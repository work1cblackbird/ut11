
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	КлючНастроекФормы = "Обработка.ЗапросыКоммерческихПредложений.Форма.ПоискПоОтборам/ИсторияПоиска";
	ИсторияПоиска = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(КлючНастроекФормы, "");

	Если ТипЗнч(ИсторияПоиска) <> Тип("ТаблицаЗначений") Тогда
		Элементы.ГруппыФормы.ТекущаяСтраница = Элементы.ГруппаИсторияПустая;
	Иначе
		Элементы.ГруппыФормы.ТекущаяСтраница = Элементы.ГруппаИстория;
		
		ТекущаяДатаСоздания = Дата(1,1,1);
		Для Каждого СтрокаИстории Из ИсторияПоиска Цикл
			
			ДатаСоздания = СтрокаИстории.ДатаСоздания;
			
			Если НачалоДня(ДатаСоздания) <> ТекущаяДатаСоздания Тогда
				НоваяГруппа = ТаблицаИстории.Добавить();
				НоваяГруппа.Группа = Истина;
				НоваяГруппа.ПредставлениеГруппы = Формат(ДатаСоздания, "ДФ='dd.MM.yyyy (дддд)'");
				ТекущаяДатаСоздания = НачалоДня(ДатаСоздания);
			КонецЕсли;
			
			НоваяСтрока = ТаблицаИстории.Добавить();
			НоваяСтрока.Группа = Ложь;
			НоваяСтрока.Представление = СтрокаИстории.ПредставлениеПоиска;
			НоваяСтрока.Время = Формат(ДатаСоздания,"ДФ=ЧЧ:мм");
			НоваяСтрока.ПараметрыПоиска = СтрокаИстории.ПараметрыПоиска;
			
		КонецЦикла;
		
		Элементы.ТаблицаИстории.ТекущаяСтрока = 1;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаИстории

&НаКлиенте
Процедура ТаблицаИсторииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТаблицаИстории.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено
		Или ТекущиеДанные.Группа Тогда
		Возврат;
	КонецЕсли;
	
	Закрыть(ТекущиеДанные.ПараметрыПоиска);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	/////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаИстории.Группа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ТаблицаИсторииПредставлениеГруппы");
	
	/////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаИстории.Группа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ТаблицаИсторииОтступ");
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ТаблицаИсторииНазвание");
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ТаблицаИсторииВремя");
	
	/////////////////////////////////////////////////////////////////////////////////////////////////////////

КонецПроцедуры


#КонецОбласти