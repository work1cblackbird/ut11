
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("РегионРежимОтбора",   РегионРежимОтбора);
	Параметры.Свойство("РегионПредставление", РегионПредставление);
	Параметры.Свойство("РегионЗначенияПолей", РегионЗначенияПолей);
	Параметры.Свойство("Организация",         Организация);
	
	Если ПустаяСтрока(РегионРежимОтбора) Тогда
		РегионРежимОтбора = 0;
	КонецЕсли;
		
	УстановитьВидимостьДоступность(РегионРежимОтбора, Элементы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РежимВыбораПриИзменении(Элемент)
	
	УстановитьВидимостьДоступность(РегионРежимОтбора, Элементы);
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаПрофильОбменаНажатие(Элемент)
	
	ПараметрыФормы = БизнесСетьСлужебныйКлиент.ОписаниеПараметровФормыНастройкиРегионов();
	
	ПараметрыФормы.Организация          = Организация;
	ПараметрыФормы.ЗаголовокФормы       = НСтр("ru = 'Регионы поиска'");
	ПараметрыФормы.ТолькоРегионы        = Истина;
	ПараметрыФормы.КлючХраненияНастроек = "ЗапросыКоммерческихПредложений";
	
	БизнесСетьСлужебныйКлиент.ОткрытьФормуНастройкиРегионов(ПараметрыФормы);
		
КонецПроцедуры

&НаКлиенте
Процедура РегионПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОткрытьФормуКонтактнойИнформации();
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаОК(Команда)
	
	Если РегионРежимОтбора = 2 И ПустаяСтрока(РегионПредставление) Тогда
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбран регион'"),, "РегионПредставление");
		Возврат;
	КонецЕсли;
	
	ПараметрыЗакрытия = Новый Структура;
	
	ПараметрыЗакрытия.Вставить("РегионРежимОтбора",   РегионРежимОтбора);
	ПараметрыЗакрытия.Вставить("РегионПредставление", РегионПредставление);
	ПараметрыЗакрытия.Вставить("РегионЗначенияПолей", РегионЗначенияПолей);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьДоступность(РежимВыбора, Элементы)
	
	Элементы.ГиперссылкаПрофильОбмена.Доступность = РежимВыбора = 1;
	Элементы.ГруппаВводаРегиона.Доступность = РежимВыбора = 2;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКонтактнойИнформации()
	
	ВидКонтактнойИнформации = Новый Структура;
	ВидКонтактнойИнформации.Вставить("Тип", ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес"));
	ВидКонтактнойИнформации.Вставить("ВключатьСтрануВПредставление", Истина);
	ВидКонтактнойИнформации.Вставить("РедактированиеТолькоВДиалоге", Истина);
	ВидКонтактнойИнформации.Вставить("ОбязательноеЗаполнение", Истина);
	ВидКонтактнойИнформации.Вставить("ПроверятьКорректность",  Истина);
	
	ПараметрыОткрытия = УправлениеКонтактнойИнформациейКлиент.ПараметрыФормыКонтактнойИнформации(
		ВидКонтактнойИнформации, РегионЗначенияПолей,
		РегионПредставление,, ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес"));
	ПараметрыОткрытия.Вставить("Заголовок", НСтр("ru = 'Введите регион поставщиков'"));
	
	Оповещение = Новый ОписаниеОповещения("ОткрытьФормуКонтактнойИнформацииЗавершение", ЭтотОбъект);
		
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия, ЭтотОбъект, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКонтактнойИнформацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		РегионПредставление = Результат.Представление;
		РегионЗначенияПолей = Результат.Значение;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти