
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокПользователейПараметр = Неопределено;
	Параметры.Свойство("СписокПользователей", СписокПользователейПараметр); 
	Параметры.Свойство("Организация", Организация); 
	
	ДанныеОрганизации = ИнтеграцияЭДО.РегистрационныеДанныеОрганизации(Организация);
	
	НаименованиеОрганизации = ДанныеОрганизации.Наименование;
	ДатаВступленияВСилу = ТекущаяДатаСеанса();
	
	МассивТипов = Метаданные.ОпределяемыеТипы.Пользователь.Тип.Типы();
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивТипов, Метаданные.ОпределяемыеТипы.ФизическоеЛицо.Тип.Типы());
	СписокПользователей.ТипЗначения = Новый ОписаниеТипов(МассивТипов);
	
	Если ЗначениеЗаполнено(СписокПользователейПараметр) Тогда
		СписокПользователей.ЗагрузитьЗначения(СписокПользователейПараметр);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияПредупреждениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "Приказ" Тогда
		СтандартнаяОбработка = Ложь;
		
		НастройкиВнутреннегоЭДОСлужебныйКлиент.ОткрытьФормуФормированияПоложенияОбИспользованииПЭП_УНЭП(Организация, ЭтотОбъект);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьЛистОзнакомления(Команда)
	
	ПараметрыЗаполнения = Новый Структура;
	
	ПараметрыЗаполнения.Вставить("НаименованиеОрганизации", НаименованиеОрганизации);
	ПараметрыЗаполнения.Вставить("СписокПользователей", СписокПользователей.ВыгрузитьЗначения());
	ПараметрыЗаполнения.Вставить("Организация", Организация);
	ПараметрыЗаполнения.Вставить("ДатаВступленияВСилу",  Формат(ДатаВступленияВСилу, "ДЛФ=D"));
	
	УведомленияОбИспользованииПЭП_УНЭП = ШаблонУведомленияОбИспользованииПЭП_УНЭП(ПараметрыЗаполнения);
	
	КоллекцияПечатныхФорм = УправлениеПечатьюКлиент.НоваяКоллекцияПечатныхФорм("УведомленияОбИспользованииПЭП");
	ОписаниеПечатнойФормы = УправлениеПечатьюКлиент.ОписаниеПечатнойФормы(КоллекцияПечатныхФорм, "УведомленияОбИспользованииПЭП"); 
	ОписаниеПечатнойФормы.ТабличныйДокумент = УведомленияОбИспользованииПЭП_УНЭП;

	УправлениеПечатьюКлиент.ПечатьДокументов(КоллекцияПечатныхФорм);
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ШаблонУведомленияОбИспользованииПЭП_УНЭП(ПараметрыЗаполнения)
	
	Возврат НастройкиВнутреннегоЭДОСлужебный.ШаблонУведомленияОбИспользованииПЭП_УНЭП(ПараметрыЗаполнения);
		
КонецФункции

#КонецОбласти
