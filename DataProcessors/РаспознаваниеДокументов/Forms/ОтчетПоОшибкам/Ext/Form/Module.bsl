
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ДекорацияПредупреждение.Заголовок = Параметры.ТекстПредупреждения;
	Элементы.ДекорацияПредупреждение.Высота = СтрЧислоСтрок(Параметры.ТекстПредупреждения) + 1;
	ОшибкиПроведения = Параметры.ОшибкиПроведения;
	
	Если Параметры.Свойство("ОткрытыйДокумент") Тогда
		ОткрытыйДокумент = Параметры.ОткрытыйДокумент;
	КонецЕсли;
	
	ДеревоОшибок = РеквизитФормыВЗначение("ОтчетДерево");
	Для Каждого ЭтаОшибка Из ОшибкиПроведения Цикл
		ДокументСОшибкой = ЭтаОшибка.Ключ;
		КореньДерева = ДеревоОшибок.Строки.Добавить();
		КореньДерева.Ссылка = ДокументСОшибкой;
		
		Если ТипЗнч(ДокументСОшибкой) = Тип("ДокументСсылка.РаспознанныйДокумент") Тогда
			ДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСОшибкой, "ТипДокумента, ВариантОбработки, Наименование");
			КореньДерева.ТипДокумента = ДанныеДокумента.ТипДокумента;
			КореньДерева.ВариантОбработки = ДанныеДокумента.ВариантОбработки;
			КореньДерева.Документ = Строка(ДанныеДокумента.ТипДокумента) + " " + ДанныеДокумента.Наименование;
		Иначе
			КореньДерева.Документ = Строка(ДокументСОшибкой);
		КонецЕсли;
		
		Для Каждого СообщениеОбОшибке Из ЭтаОшибка.Значение Цикл
			СтрокаДерева = КореньДерева.Строки.Добавить();
			СтрокаДерева.ТекстДерева = СообщениеОбОшибке.Текст;
		КонецЦикла;
	КонецЦикла;
	ЗначениеВРеквизитФормы(ДеревоОшибок, "ОтчетДерево");
	
	СброситьРазмерыИПоложениеОкна();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтчетДерево

&НаКлиенте
Процедура ОтчетДеревоВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДокументСОшибкой = Элемент.ТекущиеДанные.Ссылка;
	Если ОткрытыйДокумент = ДокументСОшибкой Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДокументСОшибкой) = Тип("ДокументСсылка.РаспознанныйДокумент") Тогда
		ПараметрыФормы = Новый Структура("Ключ, ЧастьКомплекта", ДокументСОшибкой, Истина);
		ИмяФормыОбработчика = РаспознаваниеДокументовСлужебныйКлиентПовтИсп
			.ПолучитьИмяОткрываемойФормыПоТипу(Элемент.ТекущиеДанные.ТипДокумента, Элемент.ТекущиеДанные.ВариантОбработки);
		
		ОткрытьФорму(ИмяФормыОбработчика, ПараметрыФормы);
	Иначе
		ПоказатьЗначение(, ДокументСОшибкой);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СброситьРазмерыИПоложениеОкна()

	ИмяПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		ПолноеИмяФормы = Метаданные.Обработки.РаспознаваниеДокументов.Формы.ОтчетПоОшибкам.ПолноеИмя();
		ХранилищеСистемныхНастроек.Удалить(ПолноеИмяФормы, "", ИмяПользователя);
	КонецЕсли;
	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);

КонецПроцедуры

#КонецОбласти