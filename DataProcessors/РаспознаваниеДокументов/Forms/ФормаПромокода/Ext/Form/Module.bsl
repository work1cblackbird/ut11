#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	Элементы.ФормаПрименить.Доступность = Ложь;
	Элементы.ГруппаРезультат.Видимость = Истина;
	Элементы.ГруппаРезультат.ЦветФона = ОбщегоНазначенияКлиент.ЦветСтиля("ЦветФонаВниманиеБРД");
	Элементы.ДлительнаяОперация.Видимость = Истина;
	Результат = НСтр("ru = 'Проверка промокода, секундочку...'");
	
	ДлительнаяОперация = НачатьПроверкуПромокода();
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		
		Обработчик = Новый ОписаниеОповещения("ПослеПроверкиПромокода", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПринят(Команда)
	
	Закрыть("ПромокодПринят");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция НачатьПроверкуПромокода()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Активация промокода в фоне'");
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"РаспознаваниеДокументовSDK.АктивироватьПромокод",
		Промокод
	);
	
КонецФункции

&НаКлиенте
Процедура ПослеПроверкиПромокода(ДлительнаяОперация, Контекст) Экспорт
	
	Элементы.ФормаПрименить.Доступность = Истина;
	Элементы.ДлительнаяОперация.Видимость = Ложь;
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		РезультатАктивации = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		
		Если РезультатАктивации.Применен Тогда
			Элементы.ГруппаРезультат.ЦветФона = ОбщегоНазначенияКлиент.ЦветСтиля("ЦветФонаБаннерБРД");
			Элементы.ФормаПрименить.Видимость = Ложь;
			Элементы.ФормаЗакрыть.Видимость = Ложь;
			Элементы.ФормаЗакрытьПринят.Видимость = Истина;
			Элементы.ФормаЗакрытьПринят.КнопкаПоУмолчанию = Истина;
		Иначе
			Элементы.ГруппаРезультат.ЦветФона = ОбщегоНазначенияКлиент.ЦветСтиля("ЦветФонаПредупрежденияБРД");
		КонецЕсли;
		
		Результат = РезультатАктивации.ТекстПользователю;
		
	Иначе
		
		Элементы.Результат.Видимость = Истина;
		Элементы.ГруппаРезультат.ЦветФона = ОбщегоНазначенияКлиент.ЦветСтиля("ЦветФонаПредупрежденияБРД");
		
		Результат = ДлительнаяОперация.КраткоеПредставлениеОшибки;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
