#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Параметры.ВидДлительнойОперации) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПараметрыОперации = Параметры.ПараметрыОперации;
	ПараметрыФормы = ЗаполнитьПараметрыОткрытияОбщейФормыДлительнойОперации(ЭтотОбъект);
	ВидДлительнойОперации = Параметры.ВидДлительнойОперации;
	
	// оформление элементов формы
	ШаблонПояснения = НСтр("ru = 'Форма вызывается из метода ""%1"" модуля менеджера ""%2"" для показа общей формы ""%3"".'");
	ИмяМетодаВызова = "ОбработкаПолученияФормы";
	ТекстПояснения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонПояснения,
		ИмяМетодаВызова,
		РеквизитФормыВЗначение("Объект").Метаданные().ПолноеИмя(),
		Метаданные.ОбщиеФормы.ДлительнаяОперация.Имя);
	Элементы.ДекорацияПояснение.Заголовок = ТекстПояснения;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Если ВидДлительнойОперации = "РаспределитьРасчетыСКлиентами" Тогда
		Отказ = Истина;
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ОткрытьОбщуюФормуДлительнойОперацииРаспределитьРасчетыСКлиентамиЗавершение", ЭтотОбъект);
		ФормаДлительнойОперации = ОткрытьФорму("ОбщаяФорма.ДлительнаяОперация", ПараметрыФормы, ВладелецФормы,,,, ОповещениеОЗавершении);
		Если Не ЗначениеЗаполнено(ПараметрыФормы.ИдентификаторЗадания) Тогда
			ФормированиеФискальныхЧековВызовСервера.РаспределитьРасчетыСКлиентами(ПараметрыОперации);
			Если ФормаДлительнойОперации.Открыта() Тогда
				ФормаДлительнойОперации.Закрыть();
			КонецЕсли;
		КонецЕсли;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьФормуОбработкиПредпросмотрЧека()
	
	ОповещениеОЗавершении = Неопределено;
	Если Не ОписаниеОповещенияОЗакрытии = Неопределено Тогда
		ОповещениеОЗавершении = Новый ОписаниеОповещения(ОписаниеОповещенияОЗакрытии.ИмяПроцедуры,
														ОписаниеОповещенияОЗакрытии.Модуль,
														ОписаниеОповещенияОЗакрытии.ДополнительныеПараметры);
	КонецЕсли;
	ПараметрыОткрытияФормы = Новый Структура("ПараметрыОперации", ПараметрыОперации);
	ОткрытьФорму("Обработка.ПредпросмотрЧека.Форма", ПараметрыОткрытияФормы, ВладелецФормы, , , , ОповещениеОЗавершении, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаполнитьПараметрыОткрытияОбщейФормыДлительнойОперации(Форма)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекстСообщения", 				Форма.Параметры.ТекстСообщения);
	ПараметрыФормы.Вставить("ИдентификаторЗадания", 		Форма.Параметры.ИдентификаторЗадания);
	ПараметрыФормы.Вставить("ВыводитьОкноОжидания",       	Форма.Параметры.ВыводитьОкноОжидания);
	ПараметрыФормы.Вставить("ВыводитьПрогрессВыполнения", 	Форма.Параметры.ВыводитьПрогрессВыполнения);
	ПараметрыФормы.Вставить("ВыводитьСообщения",          	Форма.Параметры.ВыводитьСообщения);
	
	Возврат ПараметрыФормы;
КонецФункции

&НаКлиенте
Процедура ОткрытьОбщуюФормуДлительнойОперацииРаспределитьРасчетыСКлиентамиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ВзаиморасчетыРассчитаны(ПараметрыОперации) Тогда
		ОткрытьФормуОбработкиПредпросмотрЧека();
		ОписаниеОповещенияОЗакрытии = Неопределено;
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Печать чека невозможна. Не выполнена операция актуализации распределения расчетов с клиентами.
														|Подробная информация в журнале регистрации.'"));
	КонецЕсли;
	
	Если Открыта() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВзаиморасчетыРассчитаны(ПараметрыОперации)
	Возврат ФормированиеФискальныхЧековСерверПереопределяемый.ВзаиморасчетыРассчитаны(ПараметрыОперации);
КонецФункции

#КонецОбласти
