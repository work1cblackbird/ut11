
#Область ОписаниеПеременных

&НаСервере
Перем СпецСимволы;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ИмяВИсточнике = Параметры.ИмяВИсточнике;
	ИмяВСервисе = Параметры.ИмяВСервисе;
	ЗаблокироватьФлагВыбора = Параметры.ЗаблокироватьФлагВыбора;
	Выбрано = Параметры.Выбрано;
	РежимВыбора = Параметры.РежимВыбора;
	ПоказыватьТолькоВыбранные = Параметры.ПоказыватьТолькоВыбранные;
	
	УстановитьСпецСимволы();
	
	УстановитьВидимость();
	
	ЗаполнитьРеквизитыПоОбъекту();
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыКоллекцииВыбраноПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.РеквизитыКоллекции.ТекущиеДанные;
	
	// Откат флага в предыдущее состояние.
	Если ТекущаяСтрока.ЗаблокироватьФлагВыбора
		И ТекущаяСтрока.Выбрано = 2 Тогда
		ТекущаяСтрока.Выбрано = 1;
		Возврат;
	ИначеЕсли ТекущаяСтрока.ЗаблокироватьФлагВыбора
		И ТекущаяСтрока.Выбрано = 0 Тогда
		ТекущаяСтрока.Выбрано = 2;
		Возврат;
	ИначеЕсли ТекущаяСтрока.ЗаблокироватьФлагВыбора
		И ТекущаяСтрока.Выбрано = 1 Тогда
		ТекущаяСтрока.Выбрано = 0;
		Возврат;
	КонецЕсли;
	
	Если ТекущаяСтрока.Уровень = 1
		И ТекущаяСтрока.ЭтоРеквизитКоллекции Тогда
		Выбрано = (ТекущаяСтрока.Выбрано = 1);
	КонецЕсли;
	
	Если ТекущаяСтрока.Выбрано = 2 Тогда
		ТекущаяСтрока.Выбрано = 0;
	КонецЕсли;
	
	ИзменениеЗначения = ?(ТекущаяСтрока.Выбрано = 0, -1, 1);
	
	ТекущаяСтрокаОбходВверх = ТекущаяСтрока.ПолучитьРодителя();
	Пока ТекущаяСтрокаОбходВверх <> Неопределено Цикл
		ТекущаяСтрокаОбходВверх.ОтмеченоПодчиненных 
			= ТекущаяСтрокаОбходВверх.ОтмеченоПодчиненных + ИзменениеЗначения;
		БылоВыбрано = ТекущаяСтрокаОбходВверх.Выбрано;
		УстановитьФлагВыбраноУГруппы(ТекущаяСтрокаОбходВверх);
		Если Булево(БылоВыбрано) <> Булево(ТекущаяСтрокаОбходВверх.Выбрано) Тогда
			ТекущаяСтрокаОбходВверх = ТекущаяСтрокаОбходВверх.ПолучитьРодителя();
			ИзменениеЗначения = ?(ТекущаяСтрокаОбходВверх.Выбрано = 0, -1, 1);
		Иначе
			ТекущаяСтрокаОбходВверх = Неопределено;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область УправлениеЭлементамиФормы

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ТекущийШрифт = Элементы.РеквизитыКоллекции.Шрифт;
	ЖирныйШрифт = Новый Шрифт(ТекущийШрифт, , , Истина);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.РеквизитыКоллекции.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("РеквизитыКоллекции.ЗаблокироватьФлагВыбора");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ЖирныйШрифт);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ЗаполнитьРеквизитыПоОбъекту()
	
	Настройки = СервисПрогнозирования.ПолучитьНастройкиСервиса();
	
	РеквизитыКоллекций = СервисПрогнозированияПереопределяемый.ПолучитьОписаниеРеквизитовВсехКоллекций();
	
	ОписаниеКоллекции = РеквизитыКоллекций[ИмяВИсточнике];
	СохраненноеОписаниеКоллекции = Настройки.Коллекции[ИмяВИсточнике];
	
	КатегориальнаяКоллекция = ОписаниеКоллекции.Категориальный;
	
	РеквизитыКоллекции.ПолучитьЭлементы().Очистить();
	
	// 1. Предопределенные реквизиты сервиса.
	// 1.х. Конкретный реквизит.
	НоваяСтрокаВерхнийУровень = РеквизитыКоллекции.ПолучитьЭлементы().Добавить();
	НоваяСтрокаВерхнийУровень.Наименование = НСтр("ru = 'Предопределенные реквизиты сервиса'");
	НоваяСтрокаВерхнийУровень.ЗаблокироватьФлагВыбора = ЗаблокироватьФлагВыбора;
	НоваяСтрокаВерхнийУровень.Выбрано = Число(Выбрано);
	НоваяСтрокаВерхнийУровень.ЭтоРеквизитКоллекции = Истина;
	НоваяСтрокаВерхнийУровень.Уровень = 1;
	НоваяСтрокаВерхнийУровень.ЭтоГруппа = Истина;
	Для Каждого КлючЗначение Из ОписаниеКоллекции.ВложенноеОписание Цикл
		ОписаниеРеквизита = КлючЗначение.Значение; // см. СервисПрогнозированияПереопределяемыйКлиентСервер.ОписаниеВыгружаемогоЭлементаДанных.
		
		Если Настройки.Коллекции.Получить(ОписаниеКоллекции.ИмяВИсточнике) <> Неопределено
				И ТипЗнч(Настройки.Коллекции[ОписаниеКоллекции.ИмяВИсточнике].ВложенноеОписание) = Тип("Структура")
				И Настройки.Коллекции[ОписаниеКоллекции.ИмяВИсточнике].ВложенноеОписание.Свойство(ОписаниеРеквизита.ИмяВИсточнике) Тогда
			Выгружать = Настройки.Коллекции[ОписаниеКоллекции.ИмяВИсточнике].ВложенноеОписание[ОписаниеРеквизита.ИмяВИсточнике].Выгружать;
		Иначе
			Выгружать = ОписаниеРеквизита.Обязательный;
		КонецЕсли;
		
		Если ПоказыватьТолькоВыбранные
			И Не Выгружать Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = НоваяСтрокаВерхнийУровень.ПолучитьЭлементы().Добавить();
		
		НоваяСтрока.Наименование = ОписаниеРеквизита.Представление;
		НоваяСтрока.ЗаблокироватьФлагВыбора = ОписаниеРеквизита.Обязательный;
		НоваяСтрока.ИмяВИсточнике = ОписаниеРеквизита.ИмяВИсточнике;
		НоваяСтрока.ИмяВСервисе = ОписаниеРеквизита.ИмяВСервисе;
		НоваяСтрока.ТипДанных = ОписаниеРеквизита.ТипДанных;
		НоваяСтрока.Выбрано = Число(Выгружать);
		
		НоваяСтрока.ЭтоРеквизитКоллекции = Истина;
		НоваяСтрока.Уровень = 2;
		НоваяСтрока.ПредставлениеОбъектаМетаданных = ИмяВИсточнике;
		
		НоваяСтрокаВерхнийУровень.ОтмеченоПодчиненных 
			= НоваяСтрокаВерхнийУровень.ОтмеченоПодчиненных + НоваяСтрока.Выбрано;
		
	КонецЦикла;
	УстановитьФлагВыбраноУГруппы(НоваяСтрокаВерхнийУровень);
	
	// 2. Реквизиты связанных с коллекцией объектов конфигурации.
	// 2.х. Объект метаданных.
	// 2.x.1. Реквизиты объекта.
	// 2.х.1.у. Конкретный реквизит.
	// 2.x.2. Доп. реквизиты объекта.
	// 2.х.2.у. Конкретный доп. реквизит.
	// 2.x.3. Доп. свойства объекта.
	// 2.х.3.у. Конкретное доп. свойство.
	НоваяСтрокаАгрегацияВерхнихУровней = РеквизитыКоллекции.ПолучитьЭлементы().Добавить();
	НоваяСтрокаАгрегацияВерхнихУровней.Наименование = НСтр("ru = 'Реквизиты связанных с коллекцией объектов конфигурации'");
	НоваяСтрокаАгрегацияВерхнихУровней.ЭтоАгрегаторПользовательскихРеквизитов = Истина;
	НоваяСтрокаАгрегацияВерхнихУровней.ЗаблокироватьФлагВыбора = Истина;
	НоваяСтрокаАгрегацияВерхнихУровней.Уровень = 1;
	НоваяСтрокаАгрегацияВерхнихУровней.ЭтоГруппа = Истина;
	
	ОписаниеМетаданных = СервисПрогнозированияПереопределяемый.ПолучитьОписаниеМетаданныхПоКоллекции(ИмяВИсточнике);
	Для Каждого ОписаниеОбъектаМетаданных Из ОписаниеМетаданных Цикл
		
		ОбъектМетаданных = ОписаниеОбъектаМетаданных.ОбъектМетаданных;
		
		НоваяСтрокаАгрегацияОбъекта = НоваяСтрокаАгрегацияВерхнихУровней.ПолучитьЭлементы().Добавить();
		НоваяСтрокаАгрегацияОбъекта.Наименование = ОбъектМетаданных.Представление();
		НоваяСтрокаАгрегацияОбъекта.ЗаблокироватьФлагВыбора = Истина;
		НоваяСтрокаАгрегацияОбъекта.Уровень = 2;
		НоваяСтрокаАгрегацияОбъекта.ЭтоГруппа = Истина;
		
		РеквизитыДополнительные = УправлениеСвойствамиПереопределяемый.СписокСвойствДляВидаОбъектов(ОписаниеОбъектаМетаданных.ИмяОбъектаДопРеквизитовСвойств, "ДополнительныеРеквизиты");
		СведенияДополнительные = УправлениеСвойствамиПереопределяемый.СписокСвойствДляВидаОбъектов(ОписаниеОбъектаМетаданных.ИмяОбъектаДопРеквизитовСвойств, "ДополнительныеСведения");
		
		НоваяСтрокаВерхнийУровень = НоваяСтрокаАгрегацияОбъекта.ПолучитьЭлементы().Добавить();
		НоваяСтрокаВерхнийУровень.Наименование = НСтр("ru = 'Реквизиты объекта'");
		НоваяСтрокаВерхнийУровень.ЗаблокироватьФлагВыбора = Истина;
		НоваяСтрокаВерхнийУровень.Уровень = 3;
		НоваяСтрокаВерхнийУровень.ЭтоГруппа = Истина;
		
		РеквизитыОбъекта = ОбъектМетаданных.Реквизиты; // КоллекцияОбъектовМетаданных.
		Для Каждого СтрокаРеквизита Из РеквизитыОбъекта Цикл
			ИмяДопРеквизита = ОчиститьТекст("Реквизит_" + СтрокаРеквизита.Имя);
			Выгружать = ТипЗнч(СохраненноеОписаниеКоллекции.ВложенноеОписание) = Тип("Структура")
				И СохраненноеОписаниеКоллекции.ВложенноеОписание.Свойство(ИмяДопРеквизита)
				И СохраненноеОписаниеКоллекции.ВложенноеОписание[ИмяДопРеквизита].Выгружать;
				
			Если ПоказыватьТолькоВыбранные
				И Не Выгружать Тогда
				Продолжить;
			КонецЕсли;
			ТипРеквизита = СтрокаРеквизита.Тип;
			
			НоваяСтрока = НоваяСтрокаВерхнийУровень.ПолучитьЭлементы().Добавить();
			НоваяСтрока.Наименование = СтрокаРеквизита.Представление();
			НоваяСтрока.ТипРеквизита = ТипРеквизита;
			НоваяСтрока.ИмяВИсточнике = ИмяДопРеквизита;
			НоваяСтрока.ИмяВСервисе = СтроковыеФункции.СтрокаЛатиницей(ИмяДопРеквизита);
			Если КатегориальнаяКоллекция Тогда
				НоваяСтрока.ТипДанных = "string";
			Иначе
				Если ТипРеквизита.СодержитТип(Тип("Число")) Тогда
					НоваяСтрока.ТипДанных = "number";
				Иначе
					НоваяСтрока.ТипДанных = "string";
				КонецЕсли;
			КонецЕсли;
			НоваяСтрока.Выбрано = Число(Выгружать);
			
			НоваяСтрокаВерхнийУровень.ОтмеченоПодчиненных 
				= НоваяСтрокаВерхнийУровень.ОтмеченоПодчиненных + НоваяСтрока.Выбрано;
			
			НоваяСтрока.ЭтоРеквизитМетаданных = Истина;
			НоваяСтрока.Уровень = 4;
			НоваяСтрока.ПредставлениеОбъектаМетаданных = НоваяСтрокаАгрегацияОбъекта.Наименование;
			
		КонецЦикла;
		УстановитьФлагВыбраноУГруппы(НоваяСтрокаВерхнийУровень);
		НоваяСтрокаАгрегацияОбъекта.ОтмеченоПодчиненных 
			= НоваяСтрокаАгрегацияОбъекта.ОтмеченоПодчиненных + Число(Булево(НоваяСтрокаВерхнийУровень.Выбрано));
		
		НоваяСтрокаВерхнийУровень = НоваяСтрокаАгрегацияОбъекта.ПолучитьЭлементы().Добавить();
		НоваяСтрокаВерхнийУровень.Наименование = НСтр("ru = 'Дополнительные реквизиты объекта'");
		НоваяСтрокаВерхнийУровень.ЗаблокироватьФлагВыбора = Истина;
		НоваяСтрокаВерхнийУровень.Уровень = 3;
		НоваяСтрокаВерхнийУровень.ЭтоГруппа = Истина;
		Если РеквизитыДополнительные <> Неопределено Тогда
			Для Каждого СтрокаРеквизита Из РеквизитыДополнительные Цикл
				ИмяДопРеквизита = ОчиститьТекст("ДопРеквизит_" + СтрокаРеквизита.Наименование);
				Выгружать = ТипЗнч(СохраненноеОписаниеКоллекции.ВложенноеОписание) = Тип("Структура")
					И СохраненноеОписаниеКоллекции.ВложенноеОписание.Свойство(ИмяДопРеквизита)
					И СохраненноеОписаниеКоллекции.ВложенноеОписание[ИмяДопРеквизита].Выгружать;
					
				Если ПоказыватьТолькоВыбранные
					И Не Выгружать Тогда
					Продолжить;
				КонецЕсли;
				ТипЗначения = СтрокаРеквизита.ТипЗначения;
				
				НоваяСтрока = НоваяСтрокаВерхнийУровень.ПолучитьЭлементы().Добавить();
				НоваяСтрока.Наименование = СтрокаРеквизита.Наименование;
				НоваяСтрока.ТипРеквизита = ТипЗначения;
				НоваяСтрока.ИмяВИсточнике = ИмяДопРеквизита;
				НоваяСтрока.ИмяВСервисе = СтроковыеФункции.СтрокаЛатиницей(ИмяДопРеквизита);
				Если КатегориальнаяКоллекция Тогда
					НоваяСтрока.ТипДанных = "string";
				Иначе
					Если ТипЗначения.СодержитТип(Тип("Число")) Тогда
						НоваяСтрока.ТипДанных = "number";
					Иначе
						НоваяСтрока.ТипДанных = "string";
					КонецЕсли;
				КонецЕсли;
				НоваяСтрока.Выбрано = Число(Выгружать);
				
				НоваяСтрокаВерхнийУровень.ОтмеченоПодчиненных 
					= НоваяСтрокаВерхнийУровень.ОтмеченоПодчиненных + НоваяСтрока.Выбрано;
				
				НоваяСтрока.ЭтоРеквизитДополнительный = Истина;
				НоваяСтрока.Уровень = 4;
				НоваяСтрока.ПредставлениеОбъектаМетаданных = НоваяСтрокаАгрегацияОбъекта.Наименование;
				НоваяСтрока.Свойство = СтрокаРеквизита.Свойство;
				
			КонецЦикла;
		КонецЕсли;
		УстановитьФлагВыбраноУГруппы(НоваяСтрокаВерхнийУровень);
		НоваяСтрокаАгрегацияОбъекта.ОтмеченоПодчиненных 
			= НоваяСтрокаАгрегацияОбъекта.ОтмеченоПодчиненных + Число(Булево(НоваяСтрокаВерхнийУровень.Выбрано));
		
		НоваяСтрокаВерхнийУровень = НоваяСтрокаАгрегацияОбъекта.ПолучитьЭлементы().Добавить();
		НоваяСтрокаВерхнийУровень.Наименование = НСтр("ru = 'Дополнительные сведения объекта'");
		НоваяСтрокаВерхнийУровень.ЗаблокироватьФлагВыбора = Истина;
		НоваяСтрокаВерхнийУровень.Уровень = 3;
		НоваяСтрокаВерхнийУровень.ЭтоГруппа = Истина;
		Если СведенияДополнительные <> Неопределено Тогда
			Для Каждого СтрокаРеквизита Из СведенияДополнительные Цикл
				ИмяДопРеквизита = ОчиститьТекст("ДопСвойство_" + СтрокаРеквизита.Наименование);
				Выгружать = ТипЗнч(СохраненноеОписаниеКоллекции.ВложенноеОписание) = Тип("Структура")
					И СохраненноеОписаниеКоллекции.ВложенноеОписание.Свойство(ИмяДопРеквизита)
					И СохраненноеОписаниеКоллекции.ВложенноеОписание[ИмяДопРеквизита].Выгружать;
					
				Если ПоказыватьТолькоВыбранные
					И Не Выгружать Тогда
					Продолжить;
				КонецЕсли;
				ТипЗначения = СтрокаРеквизита.ТипЗначения;
				
				НоваяСтрока = НоваяСтрокаВерхнийУровень.ПолучитьЭлементы().Добавить();
				НоваяСтрока.Наименование = СтрокаРеквизита.Наименование;
				НоваяСтрока.ТипРеквизита = ТипЗначения;
				НоваяСтрока.ИмяВИсточнике = ИмяДопРеквизита;
				НоваяСтрока.ИмяВСервисе = СтроковыеФункции.СтрокаЛатиницей(ИмяДопРеквизита);
				Если КатегориальнаяКоллекция Тогда
					НоваяСтрока.ТипДанных = "string";
				Иначе
					Если ТипЗначения.СодержитТип(Тип("Число")) Тогда
						НоваяСтрока.ТипДанных = "number";
					Иначе
						НоваяСтрока.ТипДанных = "string";
					КонецЕсли;
				КонецЕсли;
				НоваяСтрока.Выбрано = Число(Выгружать);
				
				НоваяСтрокаВерхнийУровень.ОтмеченоПодчиненных 
					= НоваяСтрокаВерхнийУровень.ОтмеченоПодчиненных + НоваяСтрока.Выбрано;
				
				НоваяСтрока.ЭтоСвойствоДополнительное = Истина;
				НоваяСтрока.Уровень = 4;
				НоваяСтрока.ПредставлениеОбъектаМетаданных = НоваяСтрокаАгрегацияОбъекта.Наименование;
				НоваяСтрока.Свойство = СтрокаРеквизита.Свойство;
				
			КонецЦикла;
		КонецЕсли;
		
		УстановитьФлагВыбраноУГруппы(НоваяСтрокаВерхнийУровень);
		НоваяСтрокаАгрегацияОбъекта.ОтмеченоПодчиненных 
			= НоваяСтрокаАгрегацияОбъекта.ОтмеченоПодчиненных + Число(Булево(НоваяСтрокаВерхнийУровень.Выбрано));
		
		УстановитьФлагВыбраноУГруппы(НоваяСтрокаАгрегацияОбъекта);
		
		НоваяСтрокаАгрегацияВерхнихУровней.ОтмеченоПодчиненных 
			= НоваяСтрокаАгрегацияВерхнихУровней.ОтмеченоПодчиненных + Число(Булево(НоваяСтрокаАгрегацияОбъекта.Выбрано));
		
	КонецЦикла;
	
	УстановитьФлагВыбраноУГруппы(НоваяСтрокаАгрегацияВерхнихУровней);
	
КонецПроцедуры

&НаСервере
Функция ОчиститьТекст(Текст)
	
	Возврат СтрСоединить(СтрРазделить(Текст, СпецСимволы, Ложь));
	
КонецФункции

&НаСервере
Процедура УстановитьСпецСимволы()
	Диапазоны = Новый Массив;
	Диапазоны.Добавить(Новый Структура("Мин, Макс", 0, 32));
	Диапазоны.Добавить(Новый Структура("Мин, Макс", 127, 191));
	
	СпецСимволы = " .,+-/*?=<>()%!@#$%&*""№:;{}[]?()\|/^`~'"; // "_" исключен из запрещенных. 
	Для Каждого Диапазон Из Диапазоны Цикл
		Для КодСимвола = Диапазон.Мин По Диапазон.Макс Цикл
			СпецСимволы = СпецСимволы + Символ(КодСимвола);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Ок(Команда)
	
	ОкНаСервере();
	
	ПараметрыОповещения = Новый Структура("ОбновитьСписокРеквизитов", Истина);
	Оповестить("ПодборРеквизитовКоллекций", ПараметрыОповещения, ВладелецФормы);
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	ОчиститьСообщения();
	
	ТекущаяСтрока = Элементы.РеквизитыКоллекции.ТекущиеДанные;
	Если ТекущаяСтрока.ЭтоГруппа Тогда
		ТекстСообщенияОбОшибке = НСтр("ru = 'Выбор групп запрещен.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщенияОбОшибке);
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока.Выбрано = Истина;
	
	ОкНаСервере();
	
	ПредставлениеТипаРеквизита = "";
	Если ТекущаяСтрока.ЭтоРеквизитМетаданных Тогда
		ПредставлениеТипаРеквизита = НСтр("ru = 'реквизит'");
	ИначеЕсли ТекущаяСтрока.ЭтоРеквизитДополнительный Тогда
		ПредставлениеТипаРеквизита = НСтр("ru = 'дополнительный реквизит'");
	ИначеЕсли ТекущаяСтрока.ЭтоСвойствоДополнительное Тогда
		ПредставлениеТипаРеквизита = НСтр("ru = 'дополнительное свойство'");
	ИначеЕсли ТекущаяСтрока.ЭтоРеквизитКоллекции Тогда
		ПредставлениеТипаРеквизита = НСтр("ru = 'реквизит коллекции'");
	КонецЕсли;
	Представление = СтрШаблон(НСтр("ru = '%1 (%2): %3'"), 
		ТекущаяСтрока.ПредставлениеОбъектаМетаданных, ПредставлениеТипаРеквизита, ТекущаяСтрока.Наименование);
	
	ПараметрыОповещения = Новый Структура();
	ПараметрыОповещения.Вставить("ОбновитьСписокРеквизитов", Истина);
	ПараметрыОповещения.Вставить("ИмяВИсточнике", ТекущаяСтрока.ИмяВИсточнике);
	ПараметрыОповещения.Вставить("ИмяВСервисе", ТекущаяСтрока.ИмяВСервисе);
	ПараметрыОповещения.Вставить("Представление", Представление);
	ПараметрыОповещения.Вставить("Свойство", ТекущаяСтрока.Свойство);
	
	Оповестить("ПодборРеквизитовКоллекцийВыборРеквизита", ПараметрыОповещения, ВладелецФормы);
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОкНаСервере()
	
	Настройки = СервисПрогнозирования.ПолучитьНастройкиСервиса();
	Описание = СервисПрогнозированияПереопределяемый.ПолучитьОписаниеРеквизитовВсехКоллекций();
	ОписаниеТекущейКоллекции = Описание[ИмяВИсточнике];
	
	КоллекцияВыгружалась = РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.КоллекцияВыгружалась(ИмяВИсточнике);
	
	КУдалению = Новый Массив();
	Для Каждого КлючЗначение Из ОписаниеТекущейКоллекции.ВложенноеОписание Цикл
		Если ТипЗнч(КлючЗначение.Значение.ИмяВИсточнике) = Тип("Строка")
			И СтрНачинаетсяС(КлючЗначение.Значение.ИмяВИсточнике, "Реквизит_")
				Или СтрНачинаетсяС(КлючЗначение.Значение.ИмяВИсточнике, "ДопРеквизит_")
				Или СтрНачинаетсяС(КлючЗначение.Значение.ИмяВИсточнике, "ДопСвойство_") Тогда
			
			КУдалению.Добавить(КлючЗначение.Ключ);
		КонецЕсли;
	КонецЦикла;
	Для Каждого ЭлементКУдалению Из КУдалению Цикл
		ОписаниеТекущейКоллекции.ВложенноеОписание.Удалить(ЭлементКУдалению);
	КонецЦикла;
	
	ПроверяемыйТип = Тип("Число");
	ОсновноеОписание = СервисПрогнозированияПереопределяемый.ОсновноеОписаниеВыгружаемогоЭлементаДанных();
	
	// Описание доп.информации:
	// ИмяВСервисе - "Нет"
	// ИмяВИсточнике - ОбъектМетаданных.Реквизит.ИмяРеквизита
	//               - ОбъектМетаданных.ДопРеквизит.ИмяРеквизита
	//               - ОбъектМетаданных.ДопСвойство.ИмяРеквизита
	//               - РеализацияТоваровУслуг.ДопРеквизит.Экспедитор
	
	// 1. Предопределенные реквизиты сервиса.
	// 2. Реквизиты связанных с коллекцией объектов конфигурации.
	Для Каждого ВерхнийУровень Из РеквизитыКоллекции.ПолучитьЭлементы() Цикл
		Если ВерхнийУровень.ЭтоАгрегаторПользовательскихРеквизитов Тогда
			// 2. Реквизиты связанных с коллекцией объектов конфигурации.
			Для Каждого УровеньМетаданных Из ВерхнийУровень.ПолучитьЭлементы() Цикл
				// 2.х. Объект метаданных.
				Для Каждого УровеньТиповРеквизитов Из УровеньМетаданных.ПолучитьЭлементы() Цикл
					// 2.x.1. Реквизиты объекта.
					// 2.x.2. Доп. реквизиты объекта.
					// 2.x.3. Доп. свойства объекта.
					Для Каждого УровеньРеквизитов Из УровеньТиповРеквизитов.ПолучитьЭлементы() Цикл
						// 2.x.y.z. Конкретный реквизит, доп. реквизит, доп. свойство.
						ИмяДопРеквизита = УровеньРеквизитов.ИмяВИсточнике;
						Выгружать = УровеньРеквизитов.Выбрано;
						
						Если ТипЗнч(ОписаниеТекущейКоллекции.ВложенноеОписание) <> Тип("Структура") Тогда
							Продолжить;
						КонецЕсли;
						
						Если Не УровеньРеквизитов.Выбрано Тогда
							Продолжить;
						КонецЕсли;
						
						Категориальный = УровеньРеквизитов.ТипРеквизита.Типы().Найти(ПроверяемыйТип) = Неопределено;
						
						ПредставлениеТипаРеквизита = "";
						Если УровеньРеквизитов.ЭтоРеквизитМетаданных Тогда
							ПредставлениеТипаРеквизита = НСтр("ru = 'реквизит'");
						ИначеЕсли УровеньРеквизитов.ЭтоРеквизитДополнительный Тогда
							ПредставлениеТипаРеквизита = НСтр("ru = 'дополнительный реквизит'");
						ИначеЕсли УровеньРеквизитов.ЭтоСвойствоДополнительное Тогда
							ПредставлениеТипаРеквизита = НСтр("ru = 'дополнительное свойство'");
						ИначеЕсли УровеньРеквизитов.ЭтоРеквизитКоллекции Тогда
							ПредставлениеТипаРеквизита = НСтр("ru = 'реквизит коллекции'");
						КонецЕсли;
						Представление = СтрШаблон(НСтр("ru = '%1 (%2): %3'"), 
							УровеньРеквизитов.ПредставлениеОбъектаМетаданных, ПредставлениеТипаРеквизита, УровеньРеквизитов.Наименование);
						
						ОсновноеОписание.Вставить("ИмяВСервисе",   УровеньРеквизитов.ИмяВСервисе);
						ОсновноеОписание.Вставить("Обязательный",  Ложь);
						ОсновноеОписание.Вставить("Выгружать",     Выгружать);
						ОсновноеОписание.Вставить("ИмяВИсточнике", ИмяДопРеквизита);
						ОсновноеОписание.Вставить("ТипДанных",     УровеньРеквизитов.ТипДанных);
						
						ДобавляемоеОписание = СервисПрогнозированияПереопределяемыйКлиентСервер.ОписаниеВыгружаемогоЭлементаДанных(
							ОсновноеОписание, Представление, Категориальный);
						ДобавляемоеОписание.ЭтоПользовательскоеПоле = Истина;
						ДобавляемоеОписание.ДополнительноеСвойство = УровеньРеквизитов.Свойство;
						ОписаниеТекущейКоллекции.ВложенноеОписание.Вставить(ИмяДопРеквизита, ДобавляемоеОписание);
					КонецЦикла;
					
				КонецЦикла;
			КонецЦикла;
			
		Иначе
			// 1. Предопределенные реквизиты сервиса.
			Для Каждого УровеньРеквизитов Из ВерхнийУровень.ПолучитьЭлементы() Цикл
				// 1.х. Конкретный реквизит.
				ОписаниеТекущейКоллекции.ВложенноеОписание[УровеньРеквизитов.ИмяВИсточнике].Выгружать
					= УровеньРеквизитов.Выбрано;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	ОписаниеТекущейКоллекции.Выгружать = Выбрано;
	Настройки.Коллекции[ИмяВИсточнике] = ОписаниеТекущейКоллекции;
	
	СервисПрогнозирования.ОбновитьНастройкиСервиса(Настройки);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьФлагВыбраноУГруппы(ТекущаяСтрока)
	
	Если Не ТекущаяСтрока.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущаяСтрока.ОтмеченоПодчиненных = 0 Тогда
		ТекущаяСтрока.Выбрано = 0;
	ИначеЕсли ТекущаяСтрока.ОтмеченоПодчиненных = ТекущаяСтрока.ПолучитьЭлементы().Количество() Тогда
		ТекущаяСтрока.Выбрано = 1;
	Иначе
		ТекущаяСтрока.Выбрано = 2;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()
	
	Если РежимВыбора Тогда
		Элементы.РеквизитыКоллекции.ТолькоПросмотр = Истина;
		Элементы.Выбрать.Видимость = Истина;
		Элементы.Выбрать.КнопкаПоУмолчанию = Истина;
		Элементы.Ок.Видимость = Ложь;
		Элементы.РеквизитыКоллекцииВыбрано.Видимость = Ложь;
	Иначе
		Элементы.Выбрать.Видимость = Ложь;
		Элементы.Ок.Видимость = Истина;
		Элементы.Ок.КнопкаПоУмолчанию = Истина;
		Элементы.РеквизитыКоллекцииВыбрано.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыКоллекцииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если РежимВыбора Тогда
		Выбрать(Неопределено);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти