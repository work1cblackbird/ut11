
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	БанковскийСчетКасса = Параметры.БанковскийСчетКасса;
	БанковскийСчетКассаПредставление = Строка(БанковскийСчетКасса);
	
	Если Параметры.Сумма < 0 Тогда
		Сумма = -Параметры.Сумма;
	Иначе
		Сумма = Параметры.Сумма;
	КонецЕсли;
	
	Элементы.Сумма.МаксимальноеЗначение = Сумма;
	
	БанковскиеСчетаКассы = Обработки.ПлатежныйКалендарь.БанковскиеСчетаКассыДоступныеДляЗаявки(Параметры.ЗаявкиКПереносу);
	Если Не БанковскиеСчетаКассы.Количество() Тогда
		ТекстСообщения = НСтр("ru = 'Нет возможности разбить выбранный платеж.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Отказ = Истина;
	Иначе
		Элементы.БанковскийСчетКассаПредставление.СписокВыбора.ЗагрузитьЗначения(БанковскиеСчетаКассы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ДополнительныеПараметры = ЭтаФорма.ОписаниеОповещенияОЗакрытии.ДополнительныеПараметры;
	ДополнительныеПараметры.Вставить("Сумма", Сумма);
	ДополнительныеПараметры.Вставить("Дата", Дата);
	ДополнительныеПараметры.Вставить("БанковскийСчетКасса", БанковскийСчетКасса);
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскийСчетКассаПредставлениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	БанковскийСчетКасса = ВыбранноеЗначение;
	БанковскийСчетКассаПредставление = Строка(ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти