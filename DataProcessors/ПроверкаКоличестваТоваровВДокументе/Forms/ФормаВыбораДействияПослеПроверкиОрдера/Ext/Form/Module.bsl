
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	// Возврат при получении формы для анализа.
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполняемыеСвойства = "Ордер, ОтгрузкаПоЗаданиюНаПеревозку, ЗаданиеНаПеревозку";
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, ЗаполняемыеСвойства);
	
	ВариантЗавершенияПроверкиОрдера = "УстановитьСтатусПроверен";
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗаданиеНаПервозкуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура("Ордер", Ордер);
	
	ОткрытьФорму("Документ.РасходныйОрдерНаТовары.Форма.ФормаВыбораЗаданияНаПеревозку", ПараметрыФормы, Элемент, , , , ,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если ВариантЗавершенияПроверкиОрдера = "УстановитьСтатусКОтгрузке"
		И ОтгрузкаПоЗаданиюНаПеревозку
		И Не ЗначениеЗаполнено(ЗаданиеНаПеревозку) Тогда
		
		ТекстСообщения = НСтр("ru = 'Не выбрано задание на перевозку.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Неопределено, "ЗаданиеНаПеревозку");
		
		Возврат;
		
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура("ВариантЗавершенияПроверкиОрдера, БольшеНеСпрашивать, ЗаданиеНаПеревозку",
		ВариантЗавершенияПроверкиОрдера, БольшеНеСпрашивать, ЗаданиеНаПеревозку);
	
	Оповестить("УстановленВариантЗавершенияПроверкиОрдера", ПараметрыОповещения, ЭтаФорма.ИмяФормы);
	
	Закрыть(КодВозвратаДиалога.ОК);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента      = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЗаданиеНаПеревозку.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ВариантЗавершенияПроверкиОрдера");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "УстановитьСтатусКОтгрузке";
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ОтгрузкаПоЗаданиюНаПеревозку");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЗаданиеНаПеревозку");
	ОтборЭлемента.ВидСравнения  = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
КонецПроцедуры

#КонецОбласти
