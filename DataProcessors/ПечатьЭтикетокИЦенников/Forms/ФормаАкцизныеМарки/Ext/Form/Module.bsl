
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Объект.КоличествоЭкземпляров = 1;
	Объект.НазначениеШаблона     = Перечисления.НазначенияШаблоновЭтикетокИЦенников.ЭтикеткаДляАкцизныхМарок;
	Объект.ШаблонЭтикетки        = Справочники.ШаблоныЭтикетокИЦенников.ШаблонПоУмолчанию(Объект.НазначениеШаблона);
	
	Если ЭтоАдресВременногоХранилища(Параметры.АдресВХранилище) Тогда
		
		ТаблицаТоваров = ПолучитьИзВременногоХранилища(Параметры.АдресВХранилище);
		
		Объект.АкцизныеМарки.Загрузить(ТаблицаТоваров);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Печать(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыДляПечатиАкцизныхМарок = ПолучитьПараметрыДляАкцизныхМарок();
	ПечатьЭтикетокИЦенниковЛокализацияКлиент.ПечатьЭтикеткаАкцизныеМарки(ПараметрыДляПечатиАкцизныхМарок, Объект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьПараметрыДляАкцизныхМарок()
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("АдресВХранилище"       , ПоместитьВоВременноеХранилище(Объект.АкцизныеМарки.Выгрузить(), УникальныйИдентификатор));
	ПараметрыПечати.Вставить("ШаблонЭтикетки"        , Объект.ШаблонЭтикетки);
	ПараметрыПечати.Вставить("КоличествоЭкземпляров" , Объект.КоличествоЭкземпляров);
	ПараметрыПечати.Вставить("СтруктураМакетаШаблона", Неопределено);
	
	Возврат ПараметрыПечати;
	
КонецФункции

#КонецОбласти