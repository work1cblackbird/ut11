#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	
	// показывать
	ДатаДействующихЦен                         = Параметры.ДатаДействующихЦен;
	ПоказыватьИзменениеЦены                    = Параметры.ПоказыватьИзменениеЦены;
	ПоказыватьПроцентНаценки                   = Параметры.ПоказыватьПроцентНаценки;
	ПоказыватьДействующиеЦены                  = Параметры.ПоказыватьДействующиеЦены;
	ПоказыватьФлажокПанельБыстрогоРедактирования = Параметры.Свойство("ПоказыватьПанельБыстрогоРедактирования", ПоказыватьПанельБыстрогоРедактирования);
	ПоказыватьФлажокПоказыватьФормулы          = Параметры.Свойство("ПоказыватьФормулы", ПоказыватьФормулы);
	ПоказыватьФлажокПоказыватьВалюту           = Параметры.Свойство("ПоказыватьВалюту", ПоказыватьВалюту);
	ПоказыватьФлажокПоказыватьЕдиницыИзмерения = Параметры.Свойство("ПоказыватьЕдиницыИзмерения", ПоказыватьЕдиницыИзмерения);
	
	// параметры
	ПоказыватьФлажокРассчитыватьАвтоматически  = Параметры.Свойство("РассчитыватьАвтоматически", РассчитыватьАвтоматически);
	ПоказыватьФлажокОтображатьВлияющиеЦены     = Параметры.Свойство("ОтображатьВлияющиеЦены", ОтображатьВлияющиеЦены);
	ПоказыватьФлажокИспользоватьОднуКолонкуДляКлючаЦен = Параметры.Свойство("ИспользоватьОднуКолонкуДляКлючаЦен", ИспользоватьОднуКолонкуДляКлючаЦен);
	ПоказыватьФлажокЗафиксироватьКолонкуНоменклатуры   = Параметры.Свойство("ЗафиксироватьКолонкуНоменклатуры", ЗафиксироватьКолонкуНоменклатуры);
	
	УстановитьДоступностьЭлементовФормы(ЭтаФорма);
	
	Если Не Параметры.ПоказыватьДату Тогда
		Элементы.ПоказыватьСтарыеЦены.Заголовок = НСтр("ru='Показывать действующие цены'");
		Элементы.ДатаДействующихЦен.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ПоказыватьПанельБыстрогоРедактирования.Видимость 	= ПоказыватьФлажокПанельБыстрогоРедактирования;
	Элементы.ПоказыватьФормулы.Видимость 						= ПоказыватьФлажокПоказыватьФормулы;
	Элементы.ПоказыватьВалюту.Видимость 						= ПоказыватьФлажокПоказыватьВалюту;
	Элементы.ПоказыватьЕдиницыИзмерения.Видимость 				= ПоказыватьФлажокПоказыватьЕдиницыИзмерения;
	Элементы.РассчитыватьАвтоматически.Видимость 				= ПоказыватьФлажокРассчитыватьАвтоматически;
	Элементы.ОтображатьВлияющиеЦены.Видимость 					= ПоказыватьФлажокОтображатьВлияющиеЦены;
	Элементы.ИспользоватьОднуКолонкуДляКлючаЦен.Видимость 		= ПоказыватьФлажокИспользоватьОднуКолонкуДляКлючаЦен;
	Элементы.ЗафиксироватьКолонкуНоменклатуры.Видимость 		= ПоказыватьФлажокЗафиксироватьКолонкуНоменклатуры;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)

	СтруктураВозврата = Новый Структура();
	СтруктураВозврата.Вставить("ПоказыватьИзменениеЦены",   				ПоказыватьИзменениеЦены);
	СтруктураВозврата.Вставить("ПоказыватьПроцентНаценки",  				ПоказыватьПроцентНаценки);
	СтруктураВозврата.Вставить("ПоказыватьДействующиеЦены", 				ПоказыватьДействующиеЦены);
	СтруктураВозврата.Вставить("ДатаДействующихЦен",        				ДатаДействующихЦен);
	СтруктураВозврата.Вставить("ПоказыватьПанельБыстрогоРедактирования", ПоказыватьПанельБыстрогоРедактирования);		
	
	СтруктураВозврата.Вставить("ПоказыватьФормулы",							ПоказыватьФормулы);
	СтруктураВозврата.Вставить("ПоказыватьВалюту",							ПоказыватьВалюту);
	СтруктураВозврата.Вставить("ПоказыватьЕдиницыИзмерения",				ПоказыватьЕдиницыИзмерения);

	СтруктураВозврата.Вставить("РассчитыватьАвтоматически",					РассчитыватьАвтоматически);
	СтруктураВозврата.Вставить("ОтображатьВлияющиеЦены",					ОтображатьВлияющиеЦены);
	СтруктураВозврата.Вставить("ИспользоватьОднуКолонкуДляКлючаЦен",		ИспользоватьОднуКолонкуДляКлючаЦен);
	СтруктураВозврата.Вставить("ЗафиксироватьКолонкуНоменклатуры",			ЗафиксироватьКолонкуНоменклатуры);

	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьСтарыеЦеныПриИзменении(Элемент)
	
	ОбновитьЗначенияСвязанныхРеквизитов();
	УстановитьДоступностьЭлементовФормы(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьЭлементовФормы(Форма)
	
	Форма.Элементы.ДатаДействующихЦен.Доступность       = Форма.ПоказыватьДействующиеЦены;
	Форма.Элементы.ПоказыватьИзменениеЦены.Доступность  = Форма.ПоказыватьДействующиеЦены;
	Форма.Элементы.ПоказыватьПроцентНаценки.Доступность = Форма.ПоказыватьДействующиеЦены;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗначенияСвязанныхРеквизитов()
	
	Если Не ПоказыватьДействующиеЦены Тогда
		ПоказыватьИзменениеЦены = Ложь;
		ПоказыватьПроцентНаценки = Ложь;
		ДатаДействующихЦен = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
