
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Элементы.СтраницыПоОргФормеПредпринимателя.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.СтраницыСообщенияИННИП.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.СтраницыСообщенияИННЮЛ.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.СтраницыСообщенияКППЮЛ.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.СтраницыСообщенияОКПО.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.СтраницыСообщенияОГРН.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	
	Если Параметры.ИндивидуальныйПредприниматель = 1 Тогда
		Элементы.СтраницыПоОргФормеПредпринимателя.ТекущаяСтраница = Элементы.СтраницаИндивидуальныйПредприниматель;
		Заголовок = НСтр("ru='Реквизиты индивидуального предпринимателя'");
	Иначе
		Элементы.СтраницыПоОргФормеПредпринимателя.ТекущаяСтраница = Элементы.СтраницаЮридическоеЛицо;
		Заголовок = НСтр("ru='Реквизиты юридического лица'");
	КонецЕсли;
	
	// Заполним значения реквизитов
	РеквизитыФормы = ПолучитьРеквизиты();
	Для Каждого РеквизитФормы Из РеквизитыФормы Цикл 
		Если Параметры.Свойство(РеквизитФормы.Имя) Тогда
			ЭтаФорма[РеквизитФормы.Имя] = Параметры[РеквизитФормы.Имя];
		КонецЕсли;
	КонецЦикла;
	
	ОГРНСоответствуетТребованиям = Истина;
	
	УстановитьЦветЗаголовкаПоляОшибки();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИННИППриИзменении(Элемент)
	ИННПриИзменении();
КонецПроцедуры

&НаКлиенте
Процедура ИННЮЛПриИзменении(Элемент)
	ИННПриИзменении();
КонецПроцедуры

&НаКлиенте
Процедура КППЮЛПриИзменении(Элемент)
	ПриИзмененииКПП();
	ПроверитьКорректностьКПП();
КонецПроцедуры

&НаКлиенте
Процедура КодПоОКПОПриИзменении(Элемент)
	ПроверитьКорректностьКодПоОКПО();
КонецПроцедуры

&НаКлиенте
Процедура ОГРНПриИзменении(Элемент)
	ПроверитьКорректностьОГРН();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	ПараметрыЗакрытия = Новый Структура("
		|ИндивидуальныйПредприниматель, 
		|ИНН, 
		|КодНалоговогоОргана, 
		|КодПоОКПО, 
		|КПП, 
		|ОГРН, 
		|Префикс");
	ЗаполнитьЗначенияСвойств(ПараметрыЗакрытия, ЭтаФорма);
	
	Закрыть(ПараметрыЗакрытия);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПриИзмененииРеквизитов

&НаКлиенте 
Процедура ИННПриИзменении()
	// Проверим корректность заполнения
	ПроверитьКорректностьИНН();
	
	// Заполним КПП
	Если ПустаяСтрока(ИНН) И НЕ ПустаяСтрока(КПП) Тогда
		КПП = "";
	КонецЕсли;	
	
	Если СтрДлина(ИНН) >= 4 
	 И ПустаяСтрока(КПП) Тогда
		
		КПП = Лев(ИНН, 4) + "01001";
		
		ПриИзмененииКПП();
		
		ПроверитьКорректностьКПП();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииКПП()
	Если СтрДлина(КПП) >= 4 
	 И ПустаяСтрока(КодНалоговогоОргана) Тогда
		
		КодНалоговогоОргана = Лев(КПП, 4);
		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаКлиенте 
Процедура ПроверитьКорректностьИНН()
	СообщениеОшибки = "";
	
	ИННСоответствуетТребованиям = ПустаяСтрока(ИНН) ИЛИ РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, ИндивидуальныйПредприниматель = 0, СообщениеОшибки);
	
	СообщениеИНН = СообщениеОшибки;
	
	Если Не ИННСоответствуетТребованиям Тогда
		Элементы.СтраницыСообщенияИННИП.ТекущаяСтраница = Элементы.ЕстьСообщенияИННИП;
		Элементы.СтраницыСообщенияИННЮЛ.ТекущаяСтраница = Элементы.ЕстьСообщенияИННЮЛ;
		Элементы.ИННИП.ЦветТекстаЗаголовка = ЦветЗаголовкаПоляОшибки;
		Элементы.ИННЮЛ.ЦветТекстаЗаголовка = ЦветЗаголовкаПоляОшибки;
	Иначе
		Элементы.СтраницыСообщенияИННИП.ТекущаяСтраница = Элементы.НетСообщенийИННИП;
		Элементы.СтраницыСообщенияИННЮЛ.ТекущаяСтраница = Элементы.НетСообщенийИННЮЛ;
		Элементы.ИННИП.ЦветТекстаЗаголовка = ЦветЗаголовкаАвто;
		Элементы.ИННЮЛ.ЦветТекстаЗаголовка = ЦветЗаголовкаАвто;
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте 
Процедура ПроверитьКорректностьКПП()
	СообщениеОшибки = "";
	
	КППСоответствуетТребованиям = ПустаяСтрока(КПП) ИЛИ РегламентированныеДанныеКлиентСервер.КППСоответствуетТребованиям(КПП, СообщениеОшибки);
	
	СообщениеКПП = СообщениеОшибки;
	
	Если Не КППСоответствуетТребованиям Тогда
		Элементы.СтраницыСообщенияКППЮЛ.ТекущаяСтраница = Элементы.ЕстьСообщенияКППЮЛ;
		Элементы.КППЮЛ.ЦветТекстаЗаголовка = ЦветЗаголовкаПоляОшибки;
	Иначе
		Элементы.СтраницыСообщенияКППЮЛ.ТекущаяСтраница = Элементы.НетСообщенийКППЮЛ;
		Элементы.КППЮЛ.ЦветТекстаЗаголовка = ЦветЗаголовкаАвто;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте 
Процедура ПроверитьКорректностьКодПоОКПО()
	СообщениеОшибки = "";
	
	КодПоОКПОСоответствуетТребованиям = ПустаяСтрока(КодПоОКПО) ИЛИ РегламентированныеДанныеКлиентСервер.КодПоОКПОСоответствуетТребованиям(КодПоОКПО, ИндивидуальныйПредприниматель = 0, СообщениеОшибки);
	
	СообщениеКодПоОКПО = СообщениеОшибки;
	
	Если Не КодПоОКПОСоответствуетТребованиям Тогда
		Элементы.СтраницыСообщенияОКПО.ТекущаяСтраница = Элементы.ЕстьСообщенияОКПО;
		Элементы.КодПоОКПО.ЦветТекстаЗаголовка = ЦветЗаголовкаПоляОшибки;
	Иначе
		Элементы.СтраницыСообщенияОКПО.ТекущаяСтраница = Элементы.НетСообщенийОКПО;
		Элементы.КодПоОКПО.ЦветТекстаЗаголовка = ЦветЗаголовкаАвто;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте 
Процедура ПроверитьКорректностьОГРН()
	СообщениеОшибки = "";
	
	ОГРНСоответствуетТребованиям = ПустаяСтрока(ОГРН) ИЛИ РегламентированныеДанныеКлиентСервер.ОГРНСоответствуетТребованиям(ОГРН, ИндивидуальныйПредприниматель = 0, СообщениеОшибки);
	
	СообщениеОГРН = СообщениеОшибки;
	
	Если Не ОГРНСоответствуетТребованиям Тогда
		Элементы.СтраницыСообщенияОГРН.ТекущаяСтраница = Элементы.ЕстьСообщенияОГРН;
		Элементы.ОГРН.ЦветТекстаЗаголовка = ЦветЗаголовкаПоляОшибки;
	Иначе
		Элементы.СтраницыСообщенияОГРН.ТекущаяСтраница = Элементы.НетСообщенийОГРН;
		Элементы.ОГРН.ЦветТекстаЗаголовка = ЦветЗаголовкаАвто;
	КонецЕсли;
	Элементы.ЗавершитьРедактирование.Доступность = ОГРНСоответствуетТребованиям;
КонецПроцедуры

&НаСервере 
Процедура УстановитьЦветЗаголовкаПоляОшибки()
	ЦветЗаголовкаПоляОшибки = ЦветаСтиля.ПоясняющийОшибкуТекст;
	ЦветЗаголовкаАвто = ЦветаСтиля.ЦветТекстаФормы;
КонецПроцедуры

#КонецОбласти

#КонецОбласти
