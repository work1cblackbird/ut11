
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьПараметрыПоПолучателю();
	
	Если Не Параметры.МножественныйВыбор Тогда
		Элементы.Получатели.РежимВыделения = РежимВыделенияТаблицы.Одиночный;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НастройкаВРазрезеДоговоровПриИзменении(Элемент)
	
	ЗаполнитьПараметрыПоРазрезуДоговоров();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПолучатели

&НаСервереБезКонтекста
Процедура ПолучателиПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Для Каждого Получатель Из Строки Цикл
		Если ЗначениеЗаполнено(Получатель.Значение.Данные.Договор) Тогда
			ПредставлениеПолучателя = СтрШаблон(НСтр("ru='%1, Договор: %2'"),
				Получатель.Значение.Данные.Получатель,
				Получатель.Значение.Данные.Договор);
			Получатель.Значение.Оформление["Получатель"].УстановитьЗначениеПараметра("Текст", ПредставлениеПолучателя);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ВыбратьПолучателя();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьПолучателя();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьПолучателя()
	
	ВыбранныеПолучатели = Новый Массив();
	Для Каждого Строка Из Элементы.Получатели.ВыделенныеСтроки Цикл
		ВыбранныеПолучатели.Добавить(Элементы.Получатели.ДанныеСтроки(Строка));
	КонецЦикла;

	Закрыть(ВыбранныеПолучатели);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыПоРазрезуДоговоров()
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
	Получатели, "ВРазрезеДоговоров", НастройкаВРазрезеДоговоров);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыПоПолучателю()
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Получатели, "Отправитель", Параметры.Отправитель);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Получатели, "ИдентификаторОтправителя", Параметры.ИдентификаторОтправителя);
	
	ПустойДоговор = ОбменСКонтрагентамиИнтеграция.ВсеПустыеЗначенияДоговораСКонтрагентомЭДО();
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Получатели, "ПустойДоговор", ПустойДоговор);
	
	ИспользуетсяПрямойОбменЭлектроннымиДокументами = НастройкиЭДО.ИспользуетсяПрямойОбменЭлектроннымиДокументами();
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Получатели, "ИспользоватьПрямойОбмен", ИспользуетсяПрямойОбменЭлектроннымиДокументами);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Получатели, "ВидыДокументов", Параметры.ВидыДокументов);
		
	ЗаполнитьПараметрыПоРазрезуДоговоров();
	
КонецПроцедуры

#КонецОбласти
