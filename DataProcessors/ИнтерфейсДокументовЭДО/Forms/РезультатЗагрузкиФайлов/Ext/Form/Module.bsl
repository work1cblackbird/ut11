
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьВидимостьИЗаголовки();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьИЗаголовки()
	
	ТекстСообщения = "";
	СчетчикЗагруженных = 0;
	Для Каждого Строка Из Параметры.РезультатЗагрузки Цикл
		Если Строка.Успех Тогда
			СчетчикЗагруженных = СчетчикЗагруженных + 1;
		Иначе
			СтрокаОшибки = ОшибкиЗагрузки.Добавить();
			СтрокаОшибки.Получатель = Строка.Получатель;
			СтрокаОшибки.ИдентификаторПолучателя = Строка.ИдентификаторПолучателя;
			СтрокаОшибки.ТекстОшибки = Строка.ТекстОшибки;
			СтрокаОшибки.ИмяФайла = Строка.ИмяФайла;
		КонецЕсли;
	КонецЦикла;
	
	Если СчетчикЗагруженных = 0
		И Параметры.РезультатЗагрузки.Количество() > 0 Тогда
		Заголовок = НСтр("ru='Файлы не загружены'");
	ИначеЕсли СчетчикЗагруженных = 0
		И Параметры.РезультатЗагрузки.Количество() = 0 Тогда
		Заголовок = НСтр("ru='Файл не загружены'");
	ИначеЕсли СчетчикЗагруженных = 1
		И Параметры.РезультатЗагрузки.Количество() = 1 Тогда
		Заголовок = НСтр("ru='Файл загружен'");
	КонецЕсли;
	
	Если СчетчикЗагруженных > 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru=';
			|Создан %1 документ в папке &2.
			|Подпишите его и отправьте получателю.;
			|;
			|Создано %1 документа в папке &2.
			|Подпишите эти документы и отправьте получателям.;
			|Создано %1 документов в папке &2.
			|Подпишите эти документы и отправьте получателям.;'"),
			СчетчикЗагруженных);
		ТекстСообщения =  СтрЗаменить(ТекстСообщения, "&2", """Исходящие""");
		Элементы.ДекорацияРезультатЗагрузки.Заголовок = ТекстСообщения;
	КонецЕсли;
	
	Элементы.ДекорацияРезультатЗагрузки.Видимость = СчетчикЗагруженных > 0;
	
	Если СчетчикЗагруженных <> Параметры.РезультатЗагрузки.Количество() Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru=';
			|%1 документ не удалось создать:;
			|;
			|%1 документа не удалось создать:;
			|%1 документов не удалось создать:;'"),
			Параметры.РезультатЗагрузки.Количество() - СчетчикЗагруженных);
		Элементы.ДекорацияОшибкиПоРезультатуЗагрузки.Заголовок = ТекстСообщения;
	Иначе
		Элементы.ДекорацияОшибкиПоРезультатуЗагрузки.Видимость = Ложь;
		Элементы.ОшибкиЗагрузки.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
