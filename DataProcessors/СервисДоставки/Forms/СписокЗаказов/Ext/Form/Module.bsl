#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не СервисДоставки.ПравоРаботыССервисомДоставки(Истина) Тогда
		
		Отказ = Истина;
		
	КонецЕсли;
	
	ТипГрузоперевозки = Параметры.ТипГрузоперевозки;
	Если Не ЗначениеЗаполнено(ТипГрузоперевозки) Тогда
		
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не выбран тип грузоперевозки'"));
		Отказ = Истина;
		
	ИначеЕсли Не СервисДоставки.ТипГрузоперевозкиДоступен(ТипГрузоперевозки) Тогда
		
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Выбранный тип грузоперевозки не доступен'"));
		Отказ = Истина;
		
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДоступнаОтправкаЗаказовНаДоставку = СервисДоставки.ПравоОтправкиЗаказовНаДоставкуПеревозчику();
	
	НастроитьФормуПоТипуГрузоперевозки();
	
	АвтоНавигационнаяСсылка = Ложь;
	НавигационнаяСсылка = "e1cib/command/" + СтрЗаменить(ИмяФормы, "Форма", "Команда");
	
	УстановитьУсловноеОформление();
	
	// Установка полученных параметров и отборов.
	Параметры.Свойство("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	Параметры.Свойство("Отправитель",	ОтборОтправитель);
	Параметры.Свойство("Получатель",	ОтборПолучатель);
	Параметры.Свойство("РежимВыбора",	РежимВыбора);
	Параметры.Свойство("ОбработкаВыбора",	ОбработкаВыбора);
	
	Если РежимВыбора 
		И ОбработкаВыбора = СервисДоставкиКлиентСервер.ИмяПроцедурыДобавитьДокументОснованиеВВыбранныйЗаказНаДоставку() Тогда
		Параметры.Свойство("ДокументОснование", ДокументОснование);
		МассивСсылокДляДобавления = Новый Массив();
		МассивСсылокДляДобавления.Добавить(ДокументОснование);
		ДокументыОснованияСписок = СервисДоставки.ДокументыОснованияСписок(МассивСсылокДляДобавления);
		Если ДокументыОснованияСписок.Количество() Тогда 
			ПараметрыЗаказаНаДоставку = ДокументыОснованияСписок[0];
			Если Не ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьерика() Тогда
				ОтборОтправитель = ?(ЗначениеЗаполнено(ОтборОтправитель), ОтборОтправитель, ПараметрыЗаказаНаДоставку.Отправитель);
			КонецЕсли;
			ОтборПолучатель = ?(ЗначениеЗаполнено(ОтборПолучатель), ОтборПолучатель, ПараметрыЗаказаНаДоставку.Получатель);;
		КонецЕсли;
		ОтборСостояние = Новый СписокЗначений();
		ОтборСостояние.Добавить(0, "Черновик");
	Иначе
		Параметры.Свойство("ДокументОснование", ОтборДокументОснование);
	КонецЕсли;
	
	СервисДоставкиСлужебный.ПроверитьОрганизациюБизнесСети(ОрганизацияБизнесСетиСсылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОтборыИзменение = ЗначениеЗаполнено(ОтборОтправитель) ИЛИ ЗначениеЗаполнено(ОтборПолучатель)
						ИЛИ ЗначениеЗаполнено(ОтборОтправительАдресПредставление)
						ИЛИ ЗначениеЗаполнено(ОтборПолучательАдресПредставление)
						ИЛИ ЗначениеЗаполнено(ОтборДокументОснование);
	
	ЗаполнитьСпискиВыбора();
	
	ЗаполнитьНастройкиПодсистемы();
	
	УстановитьЗначенияПоУмолчанию();
	
	СформироватьПараметрыОтбора();
	
	// Запуск фонового задания для загрузки справочников.
	ФоновоеЗаданиеПолучитьСостояния = ПолучитьСостоянияВФоне();
	ФоновоеЗаданиеПолучитьГрузоперевозчиков = ПолучитьГрузоперевозчиковВФоне();
	Если ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьерика() Тогда
		ФоновоеЗаданиеПолучитьПунктыВыдачиКлиента = ПолучитьПунктыВыдачиКлиентаВФоне();
		ФоновоеЗаданиеПолучитьСостояниеОпцииКурьерика = ПолучитьСостояниеОпцииКурьерикаВФоне(ОрганизацияБизнесСетиСсылка);
		ФоновоеЗаданиеПолучитьСписокКурьеров = ПолучитьКурьеровВФоне();
	КонецЕсли;
	
	// Запуск фонового задания для поиска.
	ФоновоеЗаданиеПолучитьЗаказыНаДоставку = ПолучитьЗаказыНаДоставкуВФоне();
	
	// Установка видимости доступности элементов.
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НастроитьЭлементыОтбораДляРолиОрганизации();
	СформироватьПредставлениеОтбораСостояния();
	ПометитьКомандуСТекущимРазмеромСтраницы();
	УстановитьВидимостьДоступностьЭлементовМультизаказов();
	
	Если ЗначениеЗаполнено(ФоновоеЗаданиеПолучитьСостояния) Тогда
		
		ПараметрыОперации = Новый Структура("ИмяПроцедуры, НаименованиеОперации, ВыводитьОкноОжидания");
		ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьСостояния();
		ПараметрыОперации.НаименованиеОперации = НСтр("ru = 'Получение списка состояний.'");
		ПараметрыОперации.ВыводитьОкноОжидания = Истина;
		
		ОжидатьЗавершениеВыполненияЗапроса(ПараметрыОперации);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ФоновоеЗаданиеПолучитьГрузоперевозчиков) Тогда
		
		ПараметрыОперации = Новый Структура("ИмяПроцедуры, НаименованиеОперации, ВыводитьОкноОжидания");
		ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьГрузоперевозчиков();
		ПараметрыОперации.НаименованиеОперации = НСтр("ru = 'Получение списка грузоперевозчиков.'");
		
		ОжидатьЗавершениеВыполненияЗапроса(ПараметрыОперации);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ФоновоеЗаданиеПолучитьПунктыВыдачиКлиента) Тогда
		
		ПараметрыОперации = Новый Структура("ИмяПроцедуры, НаименованиеОперации, ВыводитьОкноОжидания");
		ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьПунктыВыдачиКлиента();
		ПараметрыОперации.НаименованиеОперации = НСтр("ru = 'Получение списка пунктов выдачи клиента.'");
		
		ОжидатьЗавершениеВыполненияЗапроса(ПараметрыОперации);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ФоновоеЗаданиеПолучитьСписокКурьеров) Тогда
		
		ПараметрыОперации = Новый Структура("ИмяПроцедуры, НаименованиеОперации, ВыводитьОкноОжидания");
		ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьСписокКурьеров();
		ПараметрыОперации.НаименованиеОперации = НСтр("ru = 'Получение списка курьеров.'");
		
		ОжидатьЗавершениеВыполненияЗапроса(ПараметрыОперации);
		
	КонецЕсли;	

	Если ЗначениеЗаполнено(ФоновоеЗаданиеПолучитьЗаказыНаДоставку) Тогда 
		
		ПараметрыОперации = Новый Структура("ИмяПроцедуры, НаименованиеОперации, ВыводитьОкноОжидания");
		ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьЗаказыНаДоставку();
		ПараметрыОперации.НаименованиеОперации = НСтр("ru = 'Получение заказов на доставку.'");
		
		ОжидатьЗавершениеВыполненияЗапроса(ПараметрыОперации);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ФоновоеЗаданиеПолучитьСостояниеОпцииКурьерика) Тогда
		
		ПараметрыОперации = Новый Структура("ИмяПроцедуры, НаименованиеОперации, ВыводитьОкноОжидания");
		ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьСостояниеОпцииКурьерика();
		ПараметрыОперации.НаименованиеОперации = НСтр("ru = 'Получение информации по подписке.'");
		
		ОжидатьЗавершениеВыполненияЗапроса(ПараметрыОперации);
		
	КонецЕсли;

	Если ЗначениеЗаполнено(ИнтервалОбновленияИнформацииПоЗаказам) Тогда
		ПодключитьОбработчикОжидания("ОбновлениеИнформацииПоЗаказам", ИнтервалОбновленияИнформацииПоЗаказам);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСписокЗаказовНаДоставку" Тогда
		Если ЗначениеЗаполнено(Параметр) Тогда
			
			Если Параметр = ТипГрузоперевозки Тогда
				
				Страницы.Страница = ?(ОтборыИзменение,-1,Страницы.Страница);
				ПолучитьЗаказыНаДоставку();
				
				Если ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьерика() Тогда
					ПолучитьСостояниеОпцияКурьерика();
					ПолучитьСписокКурьеров();
					УстановитьДоступностьЭлементовПоЗначениюОпции();
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если Не РежимВыбора Тогда
		СохранитьНастройкиСпискаПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область ЭлементыБыстрогоОтбора

&НаКлиенте
Процедура ОрганизацияСсылкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СписокВыбора = Элементы.ОрганизацияСсылка.СписокВыбора;
	СписокВыбора.Очистить();
	
	ОрганизацииБизнесСети = ОрганизацииБизнесСетиНаСервере();
	Для Каждого ТекущаяОрганизация Из ОрганизацииБизнесСети Цикл // Структура
		СписокВыбора.Добавить(ТекущаяОрганизация.Организация, ТекущаяОрганизация.Наименование);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияБизнесСетиСсылкаПриИзменении(Элемент)
	
	ПолучитьЗаказыНаДоставку();
	ПолучитьГрузоперевозчиков();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияБизнесСетиСсылкаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПунктВыдачиПриИзменении(Элемент)

	ПолучитьЗаказыНаДоставку();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборРольОрганизацииОчистка(Элемент, СтандартнаяОбработка)
	
	ОтборРольОрганизации = 0;
	ОтборРольОрганизацииПредставление = "";
	НастроитьЭлементыОтбораДляРолиОрганизации();
	ПолучитьЗаказыНаДоставку();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборРольОрганизацииОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗначениеСписка = Элементы.ОтборРольОрганизации.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
	Если ЗначениеСписка = Неопределено Тогда
		ОтборРольОрганизации = 0;
		ОтборРольОрганизацииПредставление = "";
	Иначе
		ОтборРольОрганизации = ВыбранноеЗначение;
		ОтборРольОрганизацииПредставление = ЗначениеСписка.Представление;
	КонецЕсли;
	НастроитьЭлементыОтбораДляРолиОрганизации();
	ПолучитьЗаказыНаДоставку();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСостояниеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуВыбораСостояний();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСостояниеОчистка(Элемент, СтандартнаяОбработка)
	
	ОтборСостояние.Очистить();
	СформироватьПредставлениеОтбораСостояния();
	ПолучитьЗаказыНаДоставку();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСостояниеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ВыбранноеЗначение = 107 Тогда
		ОткрытьФормуВыбораСостояний();
	Иначе
		
		ЭлементСписка = Элементы.ОтборСостояние.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
		ОтборСостояние.Очистить();
		ОтборСостояние.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
		СформироватьПредставлениеОтбораСостояния();
		ПолучитьЗаказыНаДоставку();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКурьерПриИзменении(Элемент)
	
	ПолучитьЗаказыНаДоставку();
	
КонецПроцедуры

#КонецОбласти

#Область ЭлементыОтборов

#Область Отправитель

&НаКлиенте
Процедура ОтборНомерЗаказаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ОтборНомерЗаказа) Тогда
		ОтборНомерЗаказа = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ОтборНомерЗаказа, 9);
	КонецЕсли;
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборНомерЗаказаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных,
	СтандартнаяОбработка)
	
	Если НомерЗаказаНекорректный(Текст) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборДокументОснованиеОчистка(Элемент, СтандартнаяОбработка)
	
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборДокументОснованиеПриИзменении(Элемент)
	
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОтправительПриИзменении(Элемент)
	
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОтправительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	УчастникГрузоперевозкиНачалоВыбора(Элемент, 1, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОтправительОчистка(Элемент, СтандартнаяОбработка)
	
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОтправительОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОбработатьВыборТипаУчастникаГрузоперевозки(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область Получатель

&НаКлиенте
Процедура ОтборПолучательПриИзменении(Элемент)
	
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПолучательНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	УчастникГрузоперевозкиНачалоВыбора(Элемент, 2, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПолучательОчистка(Элемент, СтандартнаяОбработка)
	
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПолучательОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОбработатьВыборТипаУчастникаГрузоперевозки(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область Откуда

&НаКлиенте
Процедура ОтборОткудаПриИзменении(Элемент)
	
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОткудаОчистка(Элемент, СтандартнаяОбработка)
	
	ОтборОтправительАдресЗначение = "";
	ОтборОтправительАдресВладелец = Неопределено;
	ОтборОтправительАдресПредставление = "";
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОткудаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	АдресНачалоВыбора(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОткудаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	АдресОбработкаВыбора(Элемент, ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти

#Область Куда

&НаКлиенте
Процедура ОтборКудаПриИзменении(Элемент)
	
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКудаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	АдресНачалоВыбора(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКудаОчистка(Элемент, СтандартнаяОбработка)
	
	ОтборПолучательАдресЗначение = "";
	ОтборПолучательАдресВладелец = Неопределено;
	ОтборПолучательАдресПредставление = "";
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКудаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	АдресОбработкаВыбора(Элемент, ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти

#Область Перевозчик

&НаКлиенте
Процедура ОтборГрузоперевозчикОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗначениеСписка = Элементы.ОтборГрузоперевозчик.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
	Если ЗначениеСписка = Неопределено Тогда
		ОтборГрузоперевозчик = "";
		ОтборГрузоперевозчикНаименование = "";
	Иначе
		ОтборГрузоперевозчик = ВыбранноеЗначение;
		ОтборГрузоперевозчикНаименование = ЗначениеСписка.Представление;
	КонецЕсли;
	
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборГрузоперевозчикОчистка(Элемент, СтандартнаяОбработка)
	
	ОтборГрузоперевозчик = "";
	ОтборГрузоперевозчикНаименование = "";
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборГрузоперевозчикОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ОтборГрузоперевозчик <> "" Тогда
		ОткрытьФормуГрузоперевозчика();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СтатусОплаты

&НаКлиенте
Процедура ОтборСтатусОплатыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗначениеСписка = Элементы.ОтборСтатусОплаты.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
	Если ЗначениеСписка = Неопределено Тогда
		ОтборСтатусОплаты = 0;
		ОтборСтатусОплатыНаименование = "";
	Иначе
		ОтборСтатусОплаты = ВыбранноеЗначение;
		ОтборСтатусОплатыНаименование = ЗначениеСписка.Представление;
	КонецЕсли;
	
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборНаложенныйПлатежОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = 2 Тогда
		ОтборНаложенныйПлатежПолучен = 0;
		ОтборНаложенныйПлатежПолученНаименование = "";
		Элементы.ОтборНаложенныйПлатежПолучен.Доступность = Ложь;
	Иначе
		Элементы.ОтборНаложенныйПлатежПолучен.Доступность = Истина;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ЗначениеСписка = Элементы.ОтборНаложенныйПлатеж.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
	Если ЗначениеСписка = Неопределено Тогда
		ОтборНаложенныйПлатеж = 0;
		ОтборНаложенныйПлатежНаименование = "";
	Иначе
		ОтборНаложенныйПлатеж = ВыбранноеЗначение;
		ОтборНаложенныйПлатежНаименование = ЗначениеСписка.Представление;
	КонецЕсли;
	
	ЗарегистрироватьИзменениеОтборов();

КонецПроцедуры

&НаКлиенте
Процедура ОтборНаложенныйПлатежПолученОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ЗначениеСписка = Элементы.ОтборНаложенныйПлатежПолучен.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
	Если ЗначениеСписка = Неопределено Тогда
		ОтборНаложенныйПлатежПолучен = 0;
		ОтборНаложенныйПлатежПолученНаименование = "";
	Иначе
		ОтборНаложенныйПлатежПолучен = ВыбранноеЗначение;
		ОтборНаложенныйПлатежПолученНаименование = ЗначениеСписка.Представление;
	КонецЕсли;

	ЗарегистрироватьИзменениеОтборов();

КонецПроцедуры

&НаКлиенте
Процедура ОтборСтатусОплатыОчистка(Элемент, СтандартнаяОбработка)
	
	ОтборСтатусОплаты = 0;
	ОтборСтатусОплатыНаименование = "";
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборНаложенныйПлатежОчистка(Элемент, СтандартнаяОбработка)

	ОтборНаложенныйПлатеж = 0;
	ОтборНаложенныйПлатежПолучен = 0;
	ОтборНаложенныйПлатежПолученНаименование = "";
	ОтборНаложенныйПлатежНаименование = "";
	Элементы.ОтборНаложенныйПлатежПолучен.Доступность = Истина;
	
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборНаложенныйПлатежПолученОчистка(Элемент, СтандартнаяОбработка)

	ОтборНаложенныйПлатежПолучен = 0;
	ОтборНаложенныйПлатежПолученНаименование = "";
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

#КонецОбласти

#Область Периоды

&НаКлиенте
Процедура ПериодДатаСозданияДатаНачалаПриИзменении(Элемент)
	
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодДатаСозданияДатаОкончанияПриИзменении(Элемент)
	
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодДатаОтгрузкиДатаНачалаПриИзменении(Элемент)
	
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодДатаОтгрузкиДатаОкончанияПриИзменении(Элемент)
	
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодДатаДоставкиДатаНачалаПриИзменении(Элемент)
	
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодДатаДоставкиДатаОкончанияПриИзменении(Элемент)
	
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

&НаКлиенте
Процедура ОбновитьСостояниеОпцииНажатие(Элемент)
	
	ПолучитьСостояниеОпцияКурьерика();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПереходНаНовыйТипАвторизацииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ПараметрыФормыУстаревшегоПодключения) = Тип("Структура") Тогда
		
		ОбработчикОповещения = Новый ОписаниеОповещения("ПриЗакрытииОкнаАвторизации", ЭтотОбъект);
		
		ОткрытьФорму("Обработка.СервисДоставки.Форма.АвторизацияВЛичномКабинетеГрузоперевозчика",
			ПараметрыФормыУстаревшегоПодключения,
			ЭтотОбъект,,,,
			ОбработчикОповещения,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияТребуетсяПовторнаяОтправкаНажатие(Элемент)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Организация", ОрганизацияБизнесСетиСсылка);
	ПараметрыОткрытия.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	ПараметрыОткрытия.Вставить("СписокЗаказов", СписокЗаказовТребуетсяПовторнаяОтправка);
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ПослеПовторнойОтправкиЗаказов", ЭтотОбъект);
	
	ОткрытьФорму("Обработка.СервисДоставки.Форма.ФормаПовторнойОтправкиЗаказов",
		ПараметрыОткрытия,
		ЭтотОбъект,,,,
		ОбработчикОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииПоля(Элемент)
	
	ПоискСтроки = Список.НайтиСтроки(Новый Структура("Идентификатор", ПредыдущийИдентификатор));
	Если ЗначениеЗаполнено(ПоискСтроки) Тогда
		Элементы.Список.ТекущаяСтрока = ПоискСтроки[0].ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если Не ТекущиеДанные = Неопределено Тогда
		Элементы.ВыполнитьДействиеОтменить.Доступность = ТекущиеДанные.ДоступнаОтмена;
		ПредыдущийИдентификатор = Элементы.Список.ТекущиеДанные.Идентификатор;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаказовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если Поле.Имя = "СписокДокументОснованиеПредставление" 
		И ТекущиеДанные.ДокументыОснования.Количество() Тогда 
		
		Если ТекущиеДанные.ДокументыОснования.Количество() = 1 Тогда
			ПоказатьЗначение(,ТекущиеДанные.ДокументыОснования[0].Значение);
		Иначе
		
			Основания = Новый Массив;
			Для Каждого ТекЭлемент Из ТекущиеДанные.ДокументыОснования Цикл
				Основания.Добавить(ТекЭлемент.Значение);
			КонецЦикла;
			
			ОткрытьФорму("Обработка.СервисДоставки.Форма.СписокПроизвольныхОбъектов",
				Новый Структура("МассивОбъектов,ЗаголовокФормы", Основания, НСтр("ru = 'Основания заказа на доставку'")),,,,,,
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
		
	ИначеЕсли Поле.Имя = "СписокСостояние" 
			И ТекущиеДанные.СостояниеИдентификатор <> 0 
			И Не ТекущиеДанные.ЭтоМультизаказ
			И ТипГрузоперевозки <> СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьерика() Тогда
			
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("ИдентификаторЗаказа", ТекущиеДанные.Идентификатор);
		ПараметрыОткрытияФормы.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
		ПараметрыОткрытияФормы.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
			
		ОткрытьФорму(
			"Обработка.СервисДоставки.Форма.ОтслеживаниеЗаказа",
			ПараметрыОткрытияФормы,
			ЭтотОбъект,ТекущиеДанные.Идентификатор,,,,
			РежимОткрытияОкнаФормы.Независимый);
				
	ИначеЕсли Поле.Имя = "СписокМультизаказПредставление" 
			И ТекущиеДанные.МультизаказИдентификатор <> "" Тогда 
			
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("ИдентификаторЗаказа", ТекущиеДанные.МультизаказИдентификатор);
		ПараметрыОткрытияФормы.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
		ПараметрыОткрытияФормы.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
			
		СервисДоставкиКлиент.ОткрытьФормуКарточкиМультизаказаНаДоставку(ПараметрыОткрытияФормы);
			
	ИначеЕсли РежимВыбора Тогда
		ОбработатьРезультатВыбораЗаказаНаДоставку(ВыбраннаяСтрока);
	ИначеЕсли ТекущиеДанные.ЭтоМультизаказ Тогда
		
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("ИдентификаторЗаказа", ТекущиеДанные.Идентификатор);
		ПараметрыОткрытияФормы.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
		ПараметрыОткрытияФормы.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
			
		СервисДоставкиКлиент.ОткрытьФормуКарточкиМультизаказаНаДоставку(ПараметрыОткрытияФормы);
		
	Иначе
		ОткрытьФормуЗаказа(Список.НайтиПоИдентификатору(ВыбраннаяСтрока));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаказовПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ОткрытьФормуЗаказа(Элементы.Список.ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьОтчетРеестрЗаказов(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	ПараметрыОткрытия.Вставить("Организация", ОрганизацияБизнесСетиСсылка);
	ПараметрыОткрытия.Вставить("ПунктВыдачи", ОтборПунктВыдачи);
	ПараметрыОткрытия.Вставить("Состояние", ОтборСостояние);
	ПараметрыОткрытия.Вставить("КлючВарианта", "РеестрЗаказов");
	ПараметрыОткрытия.Вставить("КурьерИдентификатор", ОтборКурьер);
	ПараметрыОткрытия.Вставить("СписокКурьеров", Элементы.ОтборКурьер.СписокВыбора);
	
	ОткрытьФорму("Отчет.СервисДоставкиРеестрЗаказов.ФормаОбъекта", ПараметрыОткрытия, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьОтборы(Команда)
	
	ЗарегистрироватьИзменениеОтборов(Ложь);
	Элементы.ГруппаОтборы.Скрыть();
	ПолучитьЗаказыНаДоставку();
	
КонецПроцедуры

&НаКлиенте
Процедура Отменить(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	
	Если ТекущаяСтрока <> Неопределено Тогда
		
		ОписаниеОЗавершении = Новый ОписаниеОповещения("ЗавершениеОтменитьЗаказ", ЭтотОбъект);
		
		ПоказатьВопрос(ОписаниеОЗавершении, НСтр("ru='Отменить заказ?'"), РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	Если ОтборыИзменение = Истина Тогда
		ЗарегистрироватьИзменениеОтборов(Ложь);
	КонецЕсли;
	
	Если ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьерика() Тогда
		ПолучитьСостояниеОпцияКурьерика();
		ПолучитьСписокКурьеров();
		УстановитьДоступностьЭлементовПоЗначениюОпции();
	КонецЕсли;
	
	ПолучитьЗаказыНаДоставку();
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ИдентификаторыЗаказов = Новый СписокЗначений();
	
	ТекущаяСтрока = Неопределено;
	Для Каждого ИдентификаторСтроки Из Элементы.Список.ВыделенныеСтроки Цикл
		ТекущаяСтрока = Список.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если ТекущаяСтрока = Неопределено Или Не ЗначениеЗаполнено(ТекущаяСтрока.Идентификатор) Тогда
			Продолжить;
		КонецЕсли;
		
		ИдентификаторыЗаказов.Добавить(ТекущаяСтрока.Идентификатор);
	КонецЦикла;
	
	Если ИдентификаторыЗаказов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("ИдентификаторыЗаказов", ИдентификаторыЗаказов);
	ПараметрыОткрытияФормы.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	ПараметрыОткрытияФормы.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	
	ОткрытьФорму(
		"Обработка.СервисДоставки.Форма.ПечатныеФормы",
		ПараметрыОткрытияФормы,
		ЭтотОбъект,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗаказНаДоставкуБезОснования(Команда)
	
	СоздатьЗаказНаДоставку();
	
КонецПроцедуры

&НаКлиенте
Процедура Отправить(Команда)
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	ПараметрыОткрытияФормы.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	
	ОткрытьФорму(
		"Обработка.СервисДоставки.Форма.ПомощникОтправкиЗаказов",
		ПараметрыОткрытияФормы,
		ЭтотОбъект,"ПомощникОтправкиЗаказов",,,,
		РежимОткрытияОкнаФормы.Независимый);
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПериодДатаДоставки(Команда)
	УстановитьИнтервал("ПериодДатаДоставки");
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПериодДатаОтгрузки(Команда)
	УстановитьИнтервал("ПериодДатаОтгрузки");
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПериодДатаСоздания(Команда)
	УстановитьИнтервал("ПериодДатаСоздания");
КонецПроцедуры

&НаКлиенте
Процедура НавигацияСтраницаПервая(Команда)
	
	ПерейтиПоНомеруСтраницы(0);
	
КонецПроцедуры

&НаКлиенте
Процедура НавигацияСтраницаПоследняя(Команда)
	
	ПерейтиПоНомеруСтраницы(Страницы.КоличествоСтраниц);
	
КонецПроцедуры

&НаКлиенте
Процедура НавигацияСтраницаПредыдущая(Команда)
	
	ПерейтиПоНомеруСтраницы(Страницы.Страница - 1);
	
КонецПроцедуры

&НаКлиенте
Процедура НавигацияСтраницаСледующая(Команда)
	
	ПерейтиПоНомеруСтраницы(Страницы.Страница + 1);
	
КонецПроцедуры

&НаКлиенте
Процедура НавигацияСтраницаТекущаяСтраница(Команда)
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("МаксимальныйНомерСтраницы", Страницы.КоличествоСтраниц);
	ПараметрыОткрытияФормы.Вставить("НомерСтраницы",             Страницы.Страница);
	
	ОткрытьФорму(
		"Обработка.СервисДоставки.Форма.ПереходКСтраницеПоНомеру",
		ПараметрыОткрытияФормы,
		ЭтотОбъект,,,,
		Новый ОписаниеОповещения("ПослеВыбораНомераСтраницы", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеГрузоперевозчики(Команда)
	
	ОчиститьСообщения();
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ОрганизацияБизнесСети", ОрганизацияБизнесСетиСсылка);
	ПараметрыФормы.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	
	Если ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьерика() Тогда
		
		ПараметрыФормы.Вставить("ОпцияДоступнаКурьерика", ОпцияДоступнаКурьерика);
		ПараметрыФормы.Вставить("КомментарийОпцияКурьерика", КомментарийОпцияКурьерика);
		ПараметрыФормы.Вставить("БалансОпцияКурьерика", БалансОпцияКурьерика);
		ПараметрыФормы.Вставить("СрокДействияОпцияКурьерика", СрокДействияОпцияКурьерика);
		ПараметрыФормы.Вставить("ОтображатьУведомлениеПоОпцииКурьерика", ОтображатьУведомлениеПоОпцииКурьерика);
		
	КонецЕсли;
	
	СервисДоставкиКлиент.ОткрытьФормуДоступныхПеревозчиков(ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРазмерСтраницы(Команда)
	
	ИмяЭлемента = Команда.Имя;
	СтрокаПоиска = "ИзменитьРазмерСтраницы";
	
	Если СтрНайти(ИмяЭлемента, СтрокаПоиска) Тогда
		
		РазмерСтраницы = Число(СтрЗаменить(ИмяЭлемента, СтрокаПоиска, ""));
		
		Если РазмерСтраницы <> Страницы.РазмерСтраницы Тогда
			Страницы.РазмерСтраницы = РазмерСтраницы;
			СброситьСтраницы();
			ПолучитьЗаказыНаДоставку();
		КонецЕсли;
		
		ПометитьКомандуСТекущимРазмеромСтраницы();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСтрокуИзСписка(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	
	Если ТекущаяСтрока <> Неопределено Тогда
		ОбработатьРезультатВыбораЗаказаНаДоставку(ТекущаяСтрока)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗагрузитьНаложенныеПлатежи(Команда)
	ЗагрузитьНаложенныеПлатежи();
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКСпискуПлатежныхДокументов(Команда)
	СервисДоставкиКлиентПереопределяемый.ПерейтиКСпискуПлатежныхДокументов();
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьЗаказыИзМультизаказов(Команда)
	
	ПоказыватьТолькоМультизаказы = Не ПоказыватьТолькоМультизаказы;
	УстановитьВидимостьДоступностьЭлементовМультизаказов();
	ПолучитьЗаказыНаДоставку();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеНастройки(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	ПараметрыФормы.Вставить("СоответствиеСкладов", СоответствиеСкладов);
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ПослеРедактированияНастроек", ЭтотОбъект);
	
	ОткрытьФорму("Обработка.СервисДоставки.Форма.ОбщиеНастройки", ПараметрыФормы, ЭтотОбъект,,,,
		ОбработчикОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры
	
&НаКлиенте
Процедура ПослеРедактированияНастроек(Значение, ДополнительныеПараметры) Экспорт
	
	ПолучитьСостояниеОпцияКурьерика();
	
	ОтключитьОбработчикОжидания("ОбновлениеИнформацииПоЗаказам");
	
	ЗаполнитьНастройкиПодсистемы();
	
	Если ЗначениеЗаполнено(ИнтервалОбновленияИнформацииПоЗаказам) Тогда
		ПодключитьОбработчикОжидания("ОбновлениеИнформацииПоЗаказам", ИнтервалОбновленияИнформацииПоЗаказам);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЖурналЗаказов(Команда)
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	ПараметрыОткрытияФормы.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	
	ОткрытьФорму("Обработка.СервисДоставки.Форма.ФормаИсторииЗаказов",
		ПараметрыОткрытияФормы,
		ЭтотОбъект,,,,,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтчетРеестрЗаказовПоКурьеру(Команда)
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	
	КурьерИдентификатор = ОтборКурьер;
	Если ПустаяСтрока(КурьерИдентификатор) Тогда
		СписокВыбора = Элементы.ОтборКурьер.СписокВыбора;
		Если СписокВыбора.Количество() > 0 Тогда
			КурьерИдентификатор = СписокВыбора[0].Значение;
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Укажите курьера", , "ОтборКурьер", , Отказ);
		КонецЕсли;
	КонецЕсли;
	
	ПериодОтчета = Новый СтандартныйПериод(ВариантСтандартногоПериода.Последние7Дней);
	ПериодОтчета.ДатаНачала = ?(ЗначениеЗаполнено(ПериодДатаДоставки.ДатаНачала),
		ПериодДатаДоставки.ДатаНачала,
		ПериодОтчета.ДатаНачала);
	ПериодОтчета.ДатаОкончания = ?(ЗначениеЗаполнено(ПериодДатаДоставки.ДатаОкончания),
		ПериодДатаДоставки.ДатаОкончания,
		ПериодОтчета.ДатаОкончания);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("КлючВарианта", "РеестрЗаказовПоКурьеру");
	ПараметрыОткрытия.Вставить("ПериодОтчета", ПериодОтчета);
	ПараметрыОткрытия.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	ПараметрыОткрытия.Вставить("Организация", ОрганизацияБизнесСетиСсылка);
	ПараметрыОткрытия.Вставить("ПунктВыдачи", ОтборПунктВыдачи);
	ПараметрыОткрытия.Вставить("Состояние", ОтборСостояние);
	ПараметрыОткрытия.Вставить("КурьерИдентификатор", КурьерИдентификатор);
	ПараметрыОткрытия.Вставить("СписокКурьеров", Элементы.ОтборКурьер.СписокВыбора);
	
	ОткрытьФорму("Отчет.СервисДоставкиРеестрЗаказов.ФормаОбъекта", ПараметрыОткрытия, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНастройкиПодсистемы()
	
	НастройкиПодсистемы = СервисДоставки.НастройкиПодсистемыСервисДоставки(ТипГрузоперевозки);
	
	ИспользоватьАвтообновление = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		НастройкиПодсистемы,
		"ИспользоватьАвтообновление",
		Ложь);
	
	НастройкаИнтервалОбновленияИнформацииПоЗаказам = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		НастройкиПодсистемы,
		"ИнтервалОбновленияИнформацииПоЗаказам",
		0);
		
	Если ИспользоватьАвтообновление
		И НастройкаИнтервалОбновленияИнформацииПоЗаказам > 0 Тогда
		
		ИнтервалОбновленияИнформацииПоЗаказам = НастройкаИнтервалОбновленияИнформацииПоЗаказам * 60;
		
	Иначе
		
		ИнтервалОбновленияИнформацииПоЗаказам = 0;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеИнформацииПоЗаказам() Экспорт
	
	ПолучитьЗаказыНаДоставку(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораНомераСтраницы(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Страницы.Страница = Результат;
	ПолучитьЗаказыНаДоставку();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеОтменитьЗаказ(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда //Отменить
		ОтменитьЗаказНаДоставку();
	ИначеЕсли Результат = КодВозвратаДиалога.Повторить Тогда //Отменить платно
		ОтменитьЗаказНаДоставку(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресОбработкаВыбора(Элемент, ВыбранноеЗначение)
	
	ПараметрыОтбора = Новый Структура(); 
	
	ИмяРеквизита = Элемент.Имя;
	
	Если ВыбранноеЗначение = 1 Тогда
		
		ОткрытьФормуВыбора("АдресОрганизацииСервисДоставки", Элемент.Имя + "Владелец", ПараметрыОтбора);

	ИначеЕсли ВыбранноеЗначение = 2 Тогда
			
		ОткрытьФормуВыбора("АдресКонтрагентаСервисДоставки", Элемент.Имя + "Владелец", ПараметрыОтбора);
		
	ИначеЕсли ВыбранноеЗначение = 3 Тогда
			
		АдресНачалоВыбора(Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОпределяемыйТипСодержитТипЗначения(ИмяОпределяемогоТипа, Значение)
	
	Возврат Метаданные.ОпределяемыеТипы[ИмяОпределяемогоТипа].Тип.СодержитТип(ТипЗнч(Значение));
	
КонецФункции

&НаСервере
Процедура НастроитьФормуПоТипуГрузоперевозки()
	
	ПредставлениеТипаГрузоперевозки = СервисДоставкиКлиентСервер.ПредставлениеТипаГрузоперевозки(ТипГрузоперевозки);
	
	Заголовок = СтрШаблон(НСтр("ru = '%1: Заказы на доставку'"),
		ПредставлениеТипаГрузоперевозки);
	
	Элементы.ОтборНомерЗаказа.ПодсказкаВвода = СтрШаблон(НСтр("ru = 'Номер заказа %1'"),
		ПредставлениеТипаГрузоперевозки);
	
	Это1СДоставка = ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СДоставка();
	Это1СКурьер = ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьер();
	Это1СКурьерика = ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьерика();

	Элементы.ОбработкаСервисДоставкиОтслеживаниеЗаказа.Видимость = Это1СДоставка;
	Элементы.ОткрытьЖурналЗаказов.Видимость = Это1СДоставка;
	
	Элементы.СписокГруппаНаложенныйПлатеж.Видимость = Это1СКурьер;
	Элементы.ГруппаОтборыНаложенныйПлатеж.Видимость = Это1СКурьер;

	Элементы.СписокДатаОтгрузки.Видимость = Не Это1СКурьерика;
	Элементы.ОтборОтправитель.Видимость = Не Это1СКурьерика;
	Элементы.ОтборОтправительАдрес.Видимость = Не Это1СКурьерика;
	Элементы.ОтборГрузоперевозчик.Видимость = Не Это1СКурьерика;
	Элементы.ГруппаПериодДатаОтгрузки.Видимость = Не Это1СКурьерика;
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьКомандуСТекущимРазмеромСтраницы()
	
	Элементы.ИзменитьРазмерСтраницы20.Видимость = Истина;
	Элементы.ИзменитьРазмерСтраницы40.Видимость = Истина;
	Элементы.ИзменитьРазмерСтраницы60.Видимость = Истина;
	Элементы.ИзменитьРазмерСтраницы100.Видимость = Истина;
	
	Элементы["ИзменитьРазмерСтраницы" + РазмерСтраницы].Видимость = Ложь;
	Элементы.ГруппаРазмерСтраницы.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='%1 строк'"),
		РазмерСтраницы);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпискиВыбора()
	
	СписокРолейОрганизации = Элементы.ОтборРольОрганизации.СписокВыбора;
	СписокРолейОрганизации.Очистить();
	СписокРолейОрганизации.Добавить(1, НСтр("ru='Отправитель'"));
	СписокРолейОрганизации.Добавить(2, НСтр("ru='Получатель'"));
	СписокРолейОрганизации.Добавить(3, НСтр("ru='Плательщик'"));
	
	СписокСтатусовОплаты = Элементы.ОтборСтатусОплаты.СписокВыбора;
	СписокСтатусовОплаты.Очистить();
	СписокСтатусовОплаты.Добавить(1, НСтр("ru='Оплачен'"));
	СписокСтатусовОплаты.Добавить(2, НСтр("ru='Требует оплаты'"));
	
	СписокСтатусовОплаты = Элементы.ОтборНаложенныйПлатеж.СписокВыбора;
	СписокСтатусовОплаты.Очистить();
	СписокСтатусовОплаты.Добавить(1, НСтр("ru='С наложенным платежом'"));
	СписокСтатусовОплаты.Добавить(2, НСтр("ru='Без наложенного платежа'"));
	
	СписокСтатусовОплаты = Элементы.ОтборНаложенныйПлатежПолучен.СписокВыбора;
	СписокСтатусовОплаты.Очистить();
	СписокСтатусовОплаты.Добавить(1, НСтр("ru='Оплачен'"));
	СписокСтатусовОплаты.Добавить(2, НСтр("ru='Требует оплаты'"));
	
	СписокСтатусовВсе = Элементы.ОтборСостояние.СписокВыбора;
	СписокСтатусовВсе.Очистить();
	СписокСтатусовВсе.Добавить(101, НСтр("ru='Все черновики'"));
	СписокСтатусовВсе.Добавить(102, НСтр("ru='Все к отгрузке'"));
	СписокСтатусовВсе.Добавить(103, НСтр("ru='Все отгруженные'"));
	СписокСтатусовВсе.Добавить(104, НСтр("ru='Все готовые к выдаче'"));
	СписокСтатусовВсе.Добавить(105, НСтр("ru='Все возвращаемые'"));
	СписокСтатусовВсе.Добавить(106, НСтр("ru='Все отмененные'"));
	СписокСтатусовВсе.Добавить(107, НСтр("ru='Настроить...'"),,БиблиотекаКартинок.НастройкаСписка);
	
	СписокВыбора = Элементы.ОрганизацияСсылка.СписокВыбора;
	ОрганизацииБизнесСети = ОрганизацииБизнесСетиНаСервере();
	Для Каждого ТекущаяОрганизация Из ОрганизацииБизнесСети Цикл //Структура
		СписокВыбора.Добавить(ТекущаяОрганизация.Организация, ТекущаяОрганизация.Наименование);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначенияПоУмолчанию()
	
	КлючНастроекФормы = "Обработка.СервисДоставки.Форма.СписокЗаказов/ТекущиеДанные";
	Настройки = ХранилищеСистемныхНастроек.Загрузить(КлючНастроекФормы);
	Если Настройки = Неопределено Тогда
		Настройки = Новый Соответствие;
	КонецЕсли;
	
	Если Настройки.Получить("ОрганизацияБизнесСетиСсылка") <> Неопределено 
		И ЗначениеЗаполнено(ОрганизацияБизнесСетиСсылка) Тогда
		Настройки.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
		ХранилищеСистемныхНастроек.Сохранить(КлючНастроекФормы,,Настройки);
	КонецЕсли;
	
	КлючОбщихНастроекФормы = "Обработка.СервисДоставки.Форма.СписокЗаказов";
	ОтборРольОрганизации = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(КлючОбщихНастроекФормы, "ОтборРольОрганизации",
							?(Настройки.Получить("ОтборРольОрганизации") = Неопределено, 0, Настройки.Получить("ОтборРольОрганизации")));
							
	Если ОтборСостояние.Количество() = 0 Тогда
		ОтборСостояние = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(КлючОбщихНастроекФормы, "ОтборСостояние", 
							Настройки.Получить("ОтборСостояние"));
	КонецЕсли;

	ПериодДатаСоздания = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(КлючОбщихНастроекФормы, "ПериодДатаСоздания",
							?(Настройки.Получить("ПериодДатаСоздания") = Неопределено, ВариантСтандартногоПериода.Месяц, Настройки.Получить("ПериодДатаСоздания")));
	РазмерСтраницы = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(КлючОбщихНастроекФормы, "РазмерСтраницы",
							?(Настройки.Получить("РазмерСтраницы") = Неопределено, 20, Настройки.Получить("РазмерСтраницы")));
	
	Страницы = СервисДоставки.НовыйПараметрыСтраницСервиса(РазмерСтраницы);

	СформироватьПредставленияПоНаложеннымПлатежам();

	КлючОбщихНастроекФормы = "Обработка.СервисДоставки.Форма.СписокЗаказов." + ТипГрузоперевозки;
	ПоказыватьТолькоМультизаказы = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(КлючОбщихНастроекФормы, "ПоказыватьТолькоМультизаказы",
							ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьер());
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПредставленияПоНаложеннымПлатежам(ИдетПроцессЗагрузки = Ложь)
	
	Элементы.ЗагрузитьНаложенныеПлатежи.Доступность = Не ИдетПроцессЗагрузки;
	Если ИдетПроцессЗагрузки Тогда
		Элементы.ЗагрузитьНаложенныеПлатежи.Заголовок = НСтр("ru = 'Идет процесс загрузки'");
	Иначе
		Элементы.ЗагрузитьНаложенныеПлатежи.Заголовок = НСтр("ru = 'Загрузить'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокДокументОснованиеПредставление.Имя);
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ЕстьДокументыОснования");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветГиперссылкиБЭД);
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ПодчеркнутыйТекстСервисДоставки);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокСостояние.Имя);
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.СостояниеИдентификатор");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = 0;
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ЭтоМультизаказ");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТипГрузоперевозки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьерика();
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветГиперссылкиБЭД);
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ПодчеркнутыйТекстСервисДоставки);

	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Список.Имя);
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ЭтоМультизаказ");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ШрифтВыделенныйСервисДоставки);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокМультизаказПредставление.Имя);
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ЭтоМультизаказ");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.МультизаказПредставление");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветГиперссылкиБЭД);
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ПодчеркнутыйТекстСервисДоставки);

КонецПроцедуры

&НаКлиенте
Процедура НастроитьЭлементыОтбораДляРолиОрганизации()
	
	Если ОтборРольОрганизации = 1 Тогда
		
		Если ОпределяемыйТипСодержитТипЗначения("КонтрагентСервисДоставки", ОтборОтправитель) Тогда
			ОтборОтправитель = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОтборРольОрганизации = 2 Тогда
		
		Если ОпределяемыйТипСодержитТипЗначения("КонтрагентСервисДоставки", ОтборПолучатель) Тогда
			ОтборПолучатель = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьСписокВыбораОтправитель();
	ЗаполнитьСписокВыбораПолучатель();
	ЗаполнитьСписокВыбораОтборОткуда();
	ЗаполнитьСписокВыбораОтборКуда();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНавигационноеМеню()
	
	Элементы.ГруппаНавигация.Доступность = (Страницы.КоличествоСтраниц > 1);
	
	КоличествоСтраницНадпись = ?(Страницы.КоличествоСтраниц=1,1,Страницы.КоличествоСтраниц);
	Элементы.НавигацияСтраницаТекущаяСтраница.Заголовок =
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Страница %1 из %2'"),
			Страницы.Страница, КоличествоСтраницНадпись);
			
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступность()
	
	Элементы.ВыбратьСтрокуИзСписка.Видимость = РежимВыбора;
	Элементы.СписокСоздать.Видимость = Не РежимВыбора;
	Элементы.ДоступныеПеревозчики.Видимость = Не РежимВыбора;
	Элементы.ОбработкаСервисДоставкиОтслеживаниеЗаказа.Видимость = Не РежимВыбора;
	Элементы.ОтборСостояние.Доступность = Не РежимВыбора;
	Элементы.ОбщиеНастройки.Видимость = Не РежимВыбора;
	Элементы.Отправить.Видимость = Не РежимВыбора;
	
	Элементы.ВыбратьСтрокуИзСписка.КнопкаПоУмолчанию = РежимВыбора;
	
	Элементы.СписокНаложенныйПлатежПолучен.Видимость = 
		(ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьер());
	Элементы.Отправить.Видимость = ДоступнаОтправкаЗаказовНаДоставку 
		И Не ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьерика();
	
	ИспользоватьНаложенныеПлатежи = ПолучитьФункциональнуюОпцию("ИспользоватьНаложенныеПлатежиСервисДоставки");
	Элементы.ГруппаДействияСНаложеннымиПлатежами.Видимость = ИспользоватьНаложенныеПлатежи
		И (ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьер());
	
	ЭтоКурьерика = ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьерика();
	
	Элементы.СписокМультизаказПредставление.Видимость = Не ЭтоКурьерика;
	Элементы.ПоказыватьЗаказыИзМультизаказов.Видимость = Не ЭтоКурьерика;
	Элементы.СписокПечать.Видимость = Не ЭтоКурьерика;
	Элементы.ОтборПунктВыдачи.Видимость = ЭтоКурьерика;
	Элементы.ОтборРольОрганизации.Видимость = Не ЭтоКурьерика;
	Элементы.СписокМультизаказНомер.Видимость = Не ЭтоКурьерика;
	Элементы.СписокМультизаказДата.Видимость = Не ЭтоКурьерика;
	Элементы.СписокГруппаНаложенныйПлатеж.Видимость = Не ЭтоКурьерика;
	Элементы.СписокОплатаЗаказаКурьерика.Видимость = ЭтоКурьерика;
	Элементы.СписокОтправитель.Видимость = Не ЭтоКурьерика;
	Элементы.СписокПунктВыдачи.Видимость = ЭтоКурьерика;
	Элементы.ГруппаПодпискаНаСервис.Видимость = ЭтоКурьерика;
	Элементы.ОтборКурьер.Видимость = ЭтоКурьерика;
	Элементы.ОткрытьОтчетРеестрЗаказовПоКурьеру.Видимость = ЭтоКурьерика;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступностьЭлементовМультизаказов()
	
	Если ТипГрузоперевозки <> СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьерика() Тогда
		
		Элементы.ПоказыватьЗаказыИзМультизаказов.Видимость =
			(ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьер());
		
		Элементы.ПоказыватьЗаказыИзМультизаказов.Пометка = Не ПоказыватьТолькоМультизаказы;
		Элементы.СписокМультизаказНомер.Видимость = Не ПоказыватьТолькоМультизаказы;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗаказНаДоставку()
	
	ПараметрыОткрытияФормы = Новый Структура();
	ПараметрыОткрытияФормы.Вставить("РежимМастера", 0);
	ПараметрыОткрытияФормы.Вставить("ИдентификаторЗаказа");
	ПараметрыОткрытияФормы.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	ПараметрыОткрытияФормы.Вставить("Организация", ОрганизацияБизнесСетиСсылка);
	
	Если ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьерика()
		И ОпцияДоступнаКурьерика Тогда
		
		ПараметрыОткрытияФормы.Вставить("СоответствиеСкладов", СоответствиеСкладов);
		ПараметрыОткрытияФормы.Вставить("ОпцияДоступнаКурьерика", ОпцияДоступнаКурьерика);
		ПараметрыОткрытияФормы.Вставить("КомментарийОпцияКурьерика", КомментарийОпцияКурьерика);
		ПараметрыОткрытияФормы.Вставить("БалансОпцияКурьерика", БалансОпцияКурьерика);
		ПараметрыОткрытияФормы.Вставить("СрокДействияОпцияКурьерика", СрокДействияОпцияКурьерика);
		ПараметрыОткрытияФормы.Вставить("ОтображатьУведомлениеПоОпцииКурьерика", ОтображатьУведомлениеПоОпцииКурьерика);
		
		Если ЗначениеЗаполнено(ОтборПунктВыдачи) Тогда
			ПараметрыОткрытияФормы.Вставить("ПунктВыдачи", ОтборПунктВыдачи);
		КонецЕсли;
		
	КонецЕсли;
	
	ОткрытьФормуКарточкиЗаказаНаДоставку(ПараметрыОткрытияФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуЗаказа(ДанныеПоЗаказу)
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("РежимМастера", ?(ДанныеПоЗаказу.СостояниеИдентификатор = 0, 1, 2));
	ПараметрыОткрытияФормы.Вставить("ИдентификаторЗаказа", ДанныеПоЗаказу.Идентификатор);
	ПараметрыОткрытияФормы.Вставить("ДокументОснованиеДляДобавления", ДокументОснование);
	ПараметрыОткрытияФормы.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	
	Если ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьерика() Тогда
		
		ПараметрыОткрытияФормы.Вставить("СоответствиеСкладов", СоответствиеСкладов);
		ПараметрыОткрытияФормы.Вставить("ОпцияДоступнаКурьерика", ОпцияДоступнаКурьерика);
		ПараметрыОткрытияФормы.Вставить("КомментарийОпцияКурьерика", КомментарийОпцияКурьерика);
		ПараметрыОткрытияФормы.Вставить("БалансОпцияКурьерика", БалансОпцияКурьерика);
		ПараметрыОткрытияФормы.Вставить("СрокДействияОпцияКурьерика", СрокДействияОпцияКурьерика);
		ПараметрыОткрытияФормы.Вставить("ОтображатьУведомлениеПоОпцииКурьерика", ОтображатьУведомлениеПоОпцииКурьерика);
		
	КонецЕсли;
	
	ОткрытьФормуКарточкиЗаказаНаДоставку(ПараметрыОткрытияФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКарточкиЗаказаНаДоставку(ПараметрыОткрытияФормы)
	
	ПараметрыОткрытияФормы.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	
	СервисДоставкиКлиент.ОткрытьФормуКарточкиЗаказаНаДоставку(ПараметрыОткрытияФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораСостояний()
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыборСтатусовОтбораОкончание", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Представление", НСтр("ru = 'Выбор состояний'"));
	ПараметрыФормы.Вставить("ЗначенияДляВыбора", ОтборДеревоСостояний);
	ПараметрыФормы.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	
	ОткрытьФорму("Обработка.СервисДоставки.Форма.ВыборЗначенийВИерархическомСписке", ПараметрыФормы, ЭтотОбъект,,,,
		ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборСтатусовОтбораОкончание(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено 
		И Результат.ПолучитьЭлементы().Количество() > 0 Тогда
		
		ОтборСостояние.Очистить();
		
		ЭлементыДеревоСписка = ОтборДеревоСостояний.ПолучитьЭлементы();
		ЭлементыДеревоСписка.Очистить();
		
		ЭлементыДерева = Результат.ПолучитьЭлементы();
		
		Для Каждого ТекущийЭлементДерева Из ЭлементыДерева Цикл
			НовыйЭлементДерева = ЭлементыДеревоСписка.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйЭлементДерева, ТекущийЭлементДерева);
			
			ЭлементыВеткиДерева = ТекущийЭлементДерева.ПолучитьЭлементы();
			Если ЭлементыВеткиДерева.Количество() Тогда
				ЭлементыНовойВеткиДерева = НовыйЭлементДерева.ПолучитьЭлементы();
				Для Каждого ЭлементВеткиДерева Из ЭлементыВеткиДерева Цикл
					ЭлементНовойВеткиДерева = ЭлементыНовойВеткиДерева.Добавить();
					ЗаполнитьЗначенияСвойств(ЭлементНовойВеткиДерева, ЭлементВеткиДерева);
					
					Если ЭлементВеткиДерева.Пометка Тогда
						ОтборСостояние.Добавить(ЭлементВеткиДерева.Идентификатор, ЭлементВеткиДерева.Наименование);
					КонецЕсли;
					
				КонецЦикла;
			Иначе
				Если ТекущийЭлементДерева.Пометка Тогда
					ОтборСостояние.Добавить(ТекущийЭлементДерева.Идентификатор, ТекущийЭлементДерева.Наименование);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		СформироватьПредставлениеОтбораСостояния();
		
		ПолучитьЗаказыНаДоставку();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьРеквизитыАдреса(ИмяРеквизита, ЕстьОшибки, ВыводитьПредупреждения)
	
	СервисДоставкиСлужебный.ПроверитьУникальныйИдентификаторАдреса(ЭтотОбъект, ИмяРеквизита, ЕстьОшибки, ВыводитьПредупреждения);
	
	Возврат;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыАдреса(Значение)
	
	ПараметрыАдреса = СервисДоставки.НовыйПараметрыАдреса("АдресДоставки");
	ПараметрыАдреса.Владелец = Значение;
	
	Если ЗначениеЗаполнено(Значение) Тогда
		СервисДоставкиСлужебный.ЗаполнитьАдресПоПараметрам(ПараметрыАдреса);
	КонецЕсли;
	
	Возврат ПараметрыАдреса;
	
КонецФункции

&НаКлиенте
Процедура АдресНачалоВыбора(Элемент)

	ПараметрыВидаКонтактнойИнформации = ПараметрыВидаКонтактнойИнформации();
	
	Если Не ЗначениеЗаполнено(ПараметрыВидаКонтактнойИнформации) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыВидаКонтактнойИнформации.Вид) = Тип("Структура") Тогда
		ПараметрыВидаКонтактнойИнформации.Вид.НастройкиПроверки.ПроверятьКорректность = Ложь;
	КонецЕсли;
	
	ИмяРеквизита = Элемент.Имя;
	ИмяРеквизитаПредставление = ИмяРеквизита + "Представление";
	ИмяРеквизитаЗначенияПолей = ИмяРеквизита + "Значение";
	
	АдресПредставление = ЭтотОбъект[ИмяРеквизитаПредставление];
	АдресЗначенияПолей = ЭтотОбъект[ИмяРеквизитаЗначенияПолей];
	
	ПараметрыОткрытия = УправлениеКонтактнойИнформациейКлиент.ПараметрыФормыКонтактнойИнформации(
		ПараметрыВидаКонтактнойИнформации.Вид, АдресЗначенияПолей,
		АдресПредставление,, ПараметрыВидаКонтактнойИнформации.Тип);
		
	ТекстЗаголовка = "Адрес";
	Если СтрНайти(Элемент.Имя, "Отправитель") Тогда
		ТекстЗаголовка = ТекстЗаголовка + " отправления";
	ИначеЕсли СтрНайти(Элемент.Имя, "Получатель") Тогда
		ТекстЗаголовка = ТекстЗаголовка + " получения";
	КонецЕсли;
		
	ПараметрыОткрытия.Вставить("Заголовок", ТекстЗаголовка);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяРеквизита",	ИмяРеквизита);
	
	Оповещение = Новый ОписаниеОповещения("ОткрытьФормуКонтактнойИнформацииЗавершение", ЭтотОбъект, 
								ДополнительныеПараметры);
	
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия, ЭтотОбъект, Оповещение);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыВидаКонтактнойИнформации(ВидКонтактнойИнформацииСтрока="")
	
	Возврат СервисДоставкиСлужебный.ПараметрыВидаКонтактнойИнформации(ВидКонтактнойИнформацииСтрока);
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуКонтактнойИнформацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРеквизита = ДополнительныеПараметры.ИмяРеквизита;
		
	Если ТипЗнч(Результат) = Тип("Структура") Тогда 
		
		Результат.Свойство("Представление", ЭтотОбъект[ИмяРеквизита + "Представление"]);
		Результат.Свойство("Значение", ЭтотОбъект[ИмяРеквизита + "Значение"]);
		
		ОбработатьРеквизитыАдреса(ИмяРеквизита);
		
	КонецЕсли;
	
	ЗарегистрироватьИзменениеОтборов();

КонецПроцедуры

&НаСервере
Процедура ОбработатьРеквизитыАдреса(ИмяРеквизита)
	
	СервисДоставкиСлужебный.УстановитьЗначенияАдреса(ЭтотОбъект, ИмяРеквизита);
	ЕстьОшибки = Ложь;
	ПроверитьРеквизитыАдреса(ИмяРеквизита, ЕстьОшибки, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервал(ПериодИмя)
	
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период = ЭтотОбъект[ПериодИмя];
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяРеквизита", ПериодИмя);
	
	Оповещение = Новый ОписаниеОповещения(
		"УстановитьИнтервалЗавершение", 
		ЭтотОбъект, 
		ДополнительныеПараметры);
	
	Диалог.Показать(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервалЗавершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект[ДополнительныеПараметры.ИмяРеквизита] = ВыбранноеЗначение;
	 
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПараметрыОтбора()
	
	// Быстрые отборы.
	
	БыстрыеОтборы.Очистить();
	
	Если ЗначениеЗаполнено(ОтборОтправитель) Тогда
		БыстрыеОтборы.Добавить("ОтборОтправитель", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Отправитель: %1'"),
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтборОтправитель, "Наименование")));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборОтправительАдресПредставление) Тогда
		БыстрыеОтборы.Добавить("ОтборОтправительАдрес",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Откуда: %1'"), 
				ОтборОтправительАдресПредставление));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборПолучатель) Тогда
		БыстрыеОтборы.Добавить("ОтборПолучатель", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Получатель: %1'"),
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтборПолучатель, "Наименование")));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборПолучательАдресПредставление) Тогда
		БыстрыеОтборы.Добавить("ОтборПолучательАдрес", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Куда: %1'"),
			ОтборПолучательАдресПредставление));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборГрузоперевозчик) Тогда
		БыстрыеОтборы.Добавить("ОтборГрузоперевозчик", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Перевозчик: %1'"),
			ОтборГрузоперевозчикНаименование));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборСтатусОплаты) Тогда
		БыстрыеОтборы.Добавить("ОтборСтатусОплаты", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Оплата: %1'"),
			ОтборСтатусОплатыНаименование));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПериодДатаСоздания) Тогда
		БыстрыеОтборы.Добавить("ПериодДатаСоздания", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Дата создания: %1'"),
			ПериодДатаСоздания));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПериодДатаОтгрузки) Тогда
		БыстрыеОтборы.Добавить("ПериодДатаОтгрузки", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Дата отгрузки: %1'"),
			ПериодДатаОтгрузки));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПериодДатаДоставки) Тогда
		БыстрыеОтборы.Добавить("ПериодДатаДоставки", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Дата доставки: %1'"),
			ПериодДатаДоставки));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборНомерЗаказа) Тогда
		БыстрыеОтборы.Добавить("ОтборНомерЗаказа", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Номер заказа: %1'"),
			ОтборНомерЗаказа));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборДокументОснование) Тогда
		БыстрыеОтборы.Добавить("ОтборДокументОснование", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Документ-основание: %1'"),
			ОтборДокументОснование));
	КонецЕсли;

	Если ЗначениеЗаполнено(ОтборНаложенныйПлатеж) Тогда
		БыстрыеОтборы.Добавить("ОтборНаложенныйПлатеж", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Заказы: %1'"), 
			Элементы.ОтборНаложенныйПлатеж.СписокВыбора.НайтиПоЗначению(ОтборНаложенныйПлатеж)));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборНаложенныйПлатежПолучен) Тогда
		БыстрыеОтборы.Добавить("ОтборНаложенныйПлатежПолучен", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Наложенный платеж: %1'"), 
			Элементы.ОтборНаложенныйПлатежПолучен.СписокВыбора.НайтиПоЗначению(ОтборНаложенныйПлатежПолучен)));
	КонецЕсли;
		
	// Удаление старых элементов быстрых отборов.
	КоличествоЭлементов = Элементы.ГруппаОтборыОтображение.ПодчиненныеЭлементы.Количество();
	Для ОбратныйИндекс = 1 По КоличествоЭлементов Цикл
		ЭлементОтбора = Элементы.ГруппаОтборыОтображение.ПодчиненныеЭлементы[КоличествоЭлементов - ОбратныйИндекс];
		Если ЭлементОтбора.Видимость Тогда
			Элементы.Удалить(ЭлементОтбора);
		КонецЕсли;
	КонецЦикла;
	
	// Создание новых элементов быстрых отборов.
	Для Каждого ЭлементОтбора Из БыстрыеОтборы Цикл
		
		// Добавление пустой группы.
		НоваяГруппа = Элементы.Добавить("ГруппаБыстрогоОтбора_" + ЭлементОтбора.Значение, Тип("ГруппаФормы"), 
			Элементы.ГруппаОтборыОтображение);
		НоваяГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		НоваяГруппа.Отображение = ОтображениеОбычнойГруппы.Нет;
		НоваяГруппа.ОтображатьЗаголовок = Ложь;
		НоваяГруппа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		НоваяГруппа.Видимость = Истина;
		НоваяГруппа.ОтображатьОтступСлева = Ложь;
		НоваяГруппа.ЦветФона = WebЦвета.СветлоЖелтый;
		НоваяГруппа.ГоризонтальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Нет;
		
		НовыйЭлемент = Элементы.Добавить("ЗаголовокОтбора_" + ЭлементОтбора.Значение, Тип("ДекорацияФормы"), НоваяГруппа);
		
		// Разбитие на форматированную строку.
		
		НачалоПредставления = Лев(ЭлементОтбора.Представление, СтрНайти(ЭлементОтбора.Представление, ":"));
		КонецПредставления = Сред(ЭлементОтбора.Представление, СтрНайти(ЭлементОтбора.Представление, ":")+1);
		
		НовыйЭлемент.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru='%1 <span style=""color: ЦветВажнойНадписиБИП"">%2</span>'"),
			НачалоПредставления,
			КонецПредставления);
		НовыйЭлемент.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
		НовыйЭлемент.Гиперссылка = Истина;
		НовыйЭлемент.УстановитьДействие("Нажатие", "Подключаемый_Нажатие");
		
		НовыйЭлемент = Элементы.Добавить("ОчиститьОтбор_" + ЭлементОтбора.Значение, Тип("ДекорацияФормы"), НоваяГруппа);
		НовыйЭлемент.Вид = ВидДекорацииФормы.Картинка;
		НовыйЭлемент.Картинка = БиблиотекаКартинок.ПолеВводаОчистить;
		НовыйЭлемент.Гиперссылка = Истина;
		НовыйЭлемент.УстановитьДействие("Нажатие", "Подключаемый_Нажатие");
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Нажатие(Элемент)
	
	Если СтрНайти(Элемент.Имя, "ОчиститьОтбор_") Тогда
		
		// Очистка отбора.
		ИмяРеквизита = Сред(Элемент.Имя, СтрНайти(Элемент.Имя, "_") + 1);
		
		Если ИмяРеквизита = "ОтборДокументОснование" Тогда
			ОтборДокументОснование = Неопределено;
		ИначеЕсли ИмяРеквизита = "ОтборОтправитель" Тогда
			ОтборОтправитель = Неопределено;
		ИначеЕсли ИмяРеквизита = "ОтборОтправительАдрес" Тогда
			ОтборОтправительАдресПредставление = "";
			ОтборОтправительАдресЗначение = "";
			ОтборОтправительАдресВладелец = Неопределено;
		ИначеЕсли ИмяРеквизита = "ОтборПолучатель" Тогда
			ОтборПолучатель = Неопределено;
		ИначеЕсли ИмяРеквизита = "ОтборПолучательАдрес" Тогда
			ОтборПолучательАдресПредставление = "";
			ОтборПолучательАдресЗначение = "";
			ОтборПолучательАдресВладелец = Неопределено;
		ИначеЕсли ИмяРеквизита = "ОтборГрузоперевозчик" Тогда
			ОтборГрузоперевозчикНаименование = "";
			ОтборГрузоперевозчик = "";
		ИначеЕсли ИмяРеквизита = "ОтборСтатусОплаты" Тогда
			ОтборСтатусОплатыНаименование = "";
			ОтборСтатусОплаты = 0;
		ИначеЕсли ИмяРеквизита = "ОтборНаложенныйПлатеж" Тогда
			ОтборНаложенныйПлатеж = 0;
			ОтборНаложенныйПлатежНаименование = "";
		ИначеЕсли ИмяРеквизита = "ОтборНаложенныйПлатежПолучен" Тогда
			ОтборНаложенныйПлатежПолучен = 0;
			ОтборНаложенныйПлатежПолученНаименование = "";
		Иначе
			ЗначениеРеквизита = ЭтотОбъект[ИмяРеквизита];
			Если ТипЗнч(ЗначениеРеквизита) = Тип("Булево") Тогда
				ЭтотОбъект[ИмяРеквизита] = Ложь;
			ИначеЕсли ТипЗнч(ЗначениеРеквизита) = Тип("Число") Тогда
				ЭтотОбъект[ИмяРеквизита] = 0;
			ИначеЕсли ТипЗнч(ЗначениеРеквизита) = Тип("Строка") Тогда
				ЭтотОбъект[ИмяРеквизита] = "";
			ИначеЕсли ТипЗнч(ЗначениеРеквизита) = Тип("СписокЗначений") Тогда 
				Для Каждого ЭлементСписка Из ЭтотОбъект[ИмяРеквизита] Цикл
					ЭлементСписка.Пометка = Ложь;
				КонецЦикла;
			ИначеЕсли ТипЗнч(ЗначениеРеквизита) = Тип("СтандартныйПериод") Тогда
				ЭтотОбъект[ИмяРеквизита] = Неопределено;
			Иначе
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		ОтборыИзменение = Ложь;
		СформироватьНадписьОтбора();
		ПолучитьЗаказыНаДоставку();
		
	ИначеЕсли СтрНайти(Элемент.Имя, "ЗаголовокОтбора_") Тогда
		
		ИмяРеквизита = Сред(Элемент.Имя, СтрНайти(Элемент.Имя, "_") + 1);
		
		Если ИмяРеквизита = "ПериодДатаСоздания" Тогда
			ИмяРеквизита = "ПериодДатаСозданияДатаНачала";
		КонецЕсли;
		
		Если ИмяРеквизита = "ПериодДатаОтгрузки" Тогда
			ИмяРеквизита = "ПериодДатаОтгрузкиДатаНачала";
		КонецЕсли;
		
		Если ИмяРеквизита = "ПериодДатаДоставки" Тогда
			ИмяРеквизита = "ПериодДатаДоставкиДатаНачала";
		КонецЕсли;
		
		ТекущийЭлемент = Элементы[ИмяРеквизита];
		
	КонецЕсли;
	
	СформироватьПараметрыОтбора();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьИзменениеОтборов(ОтборыБылиИзменены = Истина)
	
	ОтборыИзменение = ОтборыБылиИзменены;
	СформироватьПараметрыОтбора();
	СброситьСтраницы();
	ОбновитьНавигационноеМеню(); 
	СформироватьНадписьОтбора();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокВыбораОтправитель()
	
	СписокВыбора = Элементы.ОтборОтправитель.СписокВыбора;
	СписокВыбора.Очистить();
	
	ЭтоОрганизация = ОтборРольОрганизации = 1;
	
	Элементы.ОтборОтправитель.КнопкаВыбора = ЭтоОрганизация;
	Элементы.ОтборОтправитель.КнопкаВыпадающегоСписка = Не ЭтоОрганизация;
	
	Если Элементы.ОтборОтправитель.КнопкаВыпадающегоСписка Тогда 
		СписокВыбора.Добавить(1, НСтр("ru='Наша организация...'"));
		СписокВыбора.Добавить(2, НСтр("ru='Контрагент...'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокВыбораПолучатель()
	
	СписокВыбора = Элементы.ОтборПолучатель.СписокВыбора;
	СписокВыбора.Очистить();
	
	ЭтоОрганизация = ОтборРольОрганизации = 2;
	
	Элементы.ОтборПолучатель.КнопкаВыбора = ЭтоОрганизация;
	Элементы.ОтборПолучатель.КнопкаВыпадающегоСписка = Не ЭтоОрганизация;
	
	Если Элементы.ОтборПолучатель.КнопкаВыпадающегоСписка Тогда 
		СписокВыбора.Добавить(1, НСтр("ru='Наша организация...'"));
		СписокВыбора.Добавить(2, НСтр("ru='Контрагент...'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокВыбораОтборОткуда()
	
	СписокВыбора = Элементы.ОтборОтправительАдрес.СписокВыбора;
	СписокВыбора.Очистить();
	
	СписокВыбора.Добавить(1, НСтр("ru='Наш склад...'"));
	
	Если ОтборРольОрганизации <> 1 Тогда 
		СписокВыбора.Добавить(2, НСтр("ru='Адрес контрагента...'"));
	КонецЕсли;
	
	СписокВыбора.Добавить(3, НСтр("ru='Произвольный адрес...'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокВыбораОтборКуда()
	
	СписокВыбора = Элементы.ОтборПолучательАдрес.СписокВыбора;
	СписокВыбора.Очистить();
	
	СписокВыбора.Добавить(1, НСтр("ru='Наш склад...'"));
	
	Если ОтборРольОрганизации <> 2 Тогда 
		СписокВыбора.Добавить(2, НСтр("ru='Адрес контрагента...'"));
	КонецЕсли;
	
	СписокВыбора.Добавить(3, НСтр("ru='Произвольный адрес...'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборЗначения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДополнительныеПараметры) Тогда
	
		ЭтотОбъект[ДополнительныеПараметры.ИмяРеквизита] = Результат;
		ОбработатьИзменениеЗначения(ДополнительныеПараметры.ИмяРеквизита);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеЗначения(ИмяРеквизита)
	
	ОчиститьСообщения();
	ОбработатьИзменениеРеквизитаФормыНаСервере(ИмяРеквизита);
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеРеквизитаФормыНаСервере(ИмяРеквизита)
	
	Если ИмяРеквизита = "ОтборОтправительАдресВладелец" 
		ИЛИ ИмяРеквизита = "ОтборПолучательАдресВладелец" Тогда
		
		ПараметрыРеквизита = ПараметрыАдреса(ЭтотОбъект[ИмяРеквизита]);
		ИмяЭлемента = СтрЗаменить(ИмяРеквизита, "Владелец", "");
		ОбработатьПараметры(ПараметрыРеквизита, ИмяЭлемента);
		
		ОбработатьРеквизитыАдреса(ИмяЭлемента);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПараметры(Параметры, Префикс = "")
	
	ПараметрыДляФормы = Новый Структура; 
	СервисДоставкиКлиентСервер.ЗаполнитьЛинейныеДанныеПоСтруктуре(Параметры, ПараметрыДляФормы, Префикс);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыДляФормы);
	
КонецПроцедуры

#Область ЗапросыКСервису

&НаКлиенте
Процедура ПолучитьЗаказыНаДоставку(ВыводитьОкноОжидания = Истина)
	
	ОчиститьСообщения();
	Отказ = Ложь;
	ПроверитьОтборы(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Список.Очистить();
	
	Если Страницы.Страница = -1 Тогда
		СброситьСтраницы();
		ОбновитьНавигационноеМеню(); 
	КонецЕсли;
	
	Элементы.ДекорацияСостояниеВыполненияЗапроса.Заголовок = "";
	
	Отказ = Ложь;
	ПараметрыДляПроверки = Новый Структура("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	ПараметрыДляПроверки.Вставить("ИмяФормы", "СписокЗаказов");
	
	СервисДоставкиКлиент.ПроверитьОрганизациюБизнесСетиСВопросом(ПараметрыДляПроверки, Отказ);
	Если Отказ Тогда
		Список.Очистить();
		СброситьСтраницы();
		ОбновитьНавигационноеМеню();
		Возврат;
	КонецЕсли;
	
	ПараметрыОперации = Новый Структура("ИмяПроцедуры, НаименованиеОперации, ВыводитьОкноОжидания");
	ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьЗаказыНаДоставку();
	ПараметрыОперации.НаименованиеОперации = НСтр("ru = 'Получение заказов на доставку.'");
	ПараметрыОперации.ВыводитьОкноОжидания = ВыводитьОкноОжидания;
	
	ВыполнитьЗапрос(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьГрузоперевозчиков()

	ПараметрыОперации = Новый Структура("ИмяПроцедуры, НаименованиеОперации, ВыводитьОкноОжидания");
	ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьГрузоперевозчиков();
	ПараметрыОперации.НаименованиеОперации = НСтр("ru = 'Получение списка грузоперевозчиков.'");
	ПараметрыОперации.ВыводитьОкноОжидания = Истина;
	
	ВыполнитьЗапрос(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНаложенныеПлатежи()
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	ПараметрыДляПроверки = Новый Структура("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	
	СервисДоставкиКлиент.ПроверитьОрганизациюБизнесСетиСВопросом(ПараметрыДляПроверки, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОперации = Новый Структура("ИмяПроцедуры, НаименованиеОперации, ВыводитьОкноОжидания");
	ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыВыполнитьЗагрузкуДанныхПоНаложеннымПлатежам();
	ПараметрыОперации.НаименованиеОперации = НСтр("ru = 'Загрузка наложенных платежей.'");
	ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
	ПараметрыОперации.Вставить("ЗапуститьВФоне", Истина);
	
	ВыполнитьЗапрос(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьОтборы(Отказ)
	
	Если ОтборОтправительАдресПредставление <> "" Тогда
		ПроверитьРеквизитыАдреса("ОтборОтправительАдрес", Отказ, Истина);
	КонецЕсли;
	
	Если ОтборПолучательАдресПредставление <> "" Тогда
		ПроверитьРеквизитыАдреса("ОтборПолучательАдрес", Отказ, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьЗаказНаДоставку(ПлатнаяОтмена = Ложь)
	
	ИдентификаторСтроки = Элементы.Список.ТекущаяСтрока;
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	ПараметрыОперации = Новый Структура("ИмяПроцедуры, НаименованиеОперации, ВыводитьОкноОжидания");
	Если ТекущиеДанные.ЭтоМультизаказ Тогда
		ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыОтменитьМультизаказНаДоставку();
	Иначе
		ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыОтменитьЗаказНаДоставку();
	КонецЕсли;
	ПараметрыОперации.НаименованиеОперации = НСтр("ru = 'Отмена заказа на доставку.'");
	ПараметрыОперации.ВыводитьОкноОжидания = Истина;
	
	// Добавим параметры из текущей строки списка
	ПараметрыЗапроса = Новый Структура();
	ПараметрыЗапроса.Вставить("ИдентификаторСтроки", ИдентификаторСтроки);
	ПараметрыЗапроса.Вставить("ПлатнаяОтмена", ПлатнаяОтмена);
	ПараметрыОперации.Вставить("ПараметрыЗапроса", ПараметрыЗапроса);
	
	ВыполнитьЗапрос(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСостояниеОпцияКурьерика()
	
	ПараметрыОперации = Новый Структура("ВыводитьОкноОжидания");
	ПараметрыОперации.Вставить("ИмяПроцедуры", СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьСостояниеОпцииКурьерика());
	ПараметрыОперации.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	ПараметрыОперации.Вставить("НаименованиеОперации", НСтр("ru = 'Получение информации по подписке.'"));
	
	ВыполнитьЗапрос(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСписокКурьеров()
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("ВыводитьОкноОжидания", Истина);
	ПараметрыОперации.Вставить("ИмяПроцедуры", СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьСписокКурьеров());
	ПараметрыОперации.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	ПараметрыОперации.Вставить("НаименованиеОперации", НСтр("ru = 'Получение списка курьеров.'"));
	
	ВыполнитьЗапрос(ПараметрыОперации);
	
КонецПроцедуры

#КонецОбласти

#Область ВыполнитьЗапрос

&НаКлиенте
Процедура ВыполнитьЗапрос(ПараметрыОперации)
	
	ИнтернетПоддержкаПодключена = Ложь;
	ОчиститьСообщения();
	
	ИмяФоновогоЗадания = "ФоновоеЗадание" + ПараметрыОперации.ИмяПроцедуры;
	ФоновоеЗадание = ВыполнитьЗапросВФоне(ИнтернетПоддержкаПодключена, ПараметрыОперации);
	ЭтотОбъект[ИмяФоновогоЗадания] = ФоновоеЗадание;
	
	Если ИнтернетПоддержкаПодключена = Ложь Тогда
		
		// Загрузка с проверкой подключения интернет-поддержки.
		Оповещение = Новый ОписаниеОповещения("ВыполнитьЗапросПродолжение", ЭтотОбъект, ПараметрыОперации);
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(Оповещение, ЭтотОбъект);
		Возврат;
		
	Иначе
		
		ПараметрыОперации.Вставить("ФоновоеЗадание", ФоновоеЗадание);
		ВыполнитьЗапросПродолжение(Истина, ПараметрыОперации);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗапросПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	ИмяФоновогоЗадания = "ФоновоеЗадание"+ ДополнительныеПараметры.ИмяПроцедуры;
	
	Если Результат = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Отсутствует подключение к Интернет-поддержке пользователей.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	ИначеЕсли ТипЗнч(Результат) = Тип("Структура")
		И Результат.Свойство("Логин") Тогда
		// Повторный вызов метода после подключения к Интернет-поддержке.
		ИнтернетПоддержкаПодключена = Ложь;
		ЭтотОбъект[ИмяФоновогоЗадания] = ВыполнитьЗапросВФоне(ИнтернетПоддержкаПодключена, ДополнительныеПараметры);
		ДополнительныеПараметры.Вставить("ФоновоеЗадание", ЭтотОбъект[ИмяФоновогоЗадания]);
	КонецЕсли;
	
	Если ЭтотОбъект[ИмяФоновогоЗадания] = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект[ИмяФоновогоЗадания].Статус = "Выполняется" Тогда
		
		ОжидатьЗавершениеВыполненияЗапроса(ДополнительныеПараметры);
		
	ИначеЕсли ЭтотОбъект[ИмяФоновогоЗадания].Статус = "Выполнено" Тогда
		
		ВыполнитьЗапросЗавершение(ЭтотОбъект[ИмяФоновогоЗадания], ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗавершениеВыполненияЗапроса(ПараметрыОперации)
	
	ВыводитьОкноОжидания = ?(ЗначениеЗаполнено(ПараметрыОперации.ВыводитьОкноОжидания), 
																	ПараметрыОперации.ВыводитьОкноОжидания,
																	Ложь);
	// Установка картинки длительной операции.
	Если Не ВыводитьОкноОжидания Тогда
		
		Если ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьЗаказыНаДоставку() Тогда
			
			Элементы.СписокОбновить.Картинка = БиблиотекаКартинок.СинхронизацияДанныхДлительнаяОперация;
			Элементы.ДекорацияСостояниеВыполненияЗапроса.Заголовок = НСтр("ru='Идет загрузка...'");
			
		ИначеЕсли ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьСостояния() Тогда
			
			Элементы.ГруппаОжиданияЗагрузкиСпискаСостояний.Видимость = Истина;
			
		ИначеЕсли ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыОтменитьЗаказНаДоставку()
			Или ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыОтменитьМультизаказНаДоставку() Тогда
			
			Элементы.СписокОбновить.Картинка = БиблиотекаКартинок.Обновить;
			
		ИначеЕсли ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыВыполнитьЗагрузкуДанныхПоНаложеннымПлатежам() Тогда
			
			СформироватьПредставленияПоНаложеннымПлатежам(Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Инициализация обработчик ожидания завершения.
	ИмяФоновогоЗадания = "ФоновоеЗадание" + ПараметрыОперации.ИмяПроцедуры;
	ФоновоеЗадание = ЭтотОбъект[ИмяФоновогоЗадания];
	ПараметрыОперации.Вставить("ФоновоеЗадание", ФоновоеЗадание);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ТекстСообщения = ПараметрыОперации.НаименованиеОперации;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Ложь;
	ПараметрыОжидания.ВыводитьОкноОжидания = ВыводитьОкноОжидания;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Ложь;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	ПараметрыОжидания.Вставить("ИдентификаторЗадания", ФоновоеЗадание.ИдентификаторЗадания);
	
	ОбработкаЗавершения = Новый ОписаниеОповещения("ВыполнитьЗапросЗавершение",
		ЭтотОбъект, ПараметрыОперации);
		
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, ОбработкаЗавершения, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗапросЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	// Инициализация.
	Отказ = Ложь;
	ТекстСообщения = "";
	ИмяФоновогоЗадания = "ФоновоеЗадание" + ДополнительныеПараметры.ИмяПроцедуры;
	ФоновоеЗадание = ЭтотОбъект[ИмяФоновогоЗадания];
	
	// Скрыть элементы ожидания на форме
	Элементы.СписокОбновить.Картинка = БиблиотекаКартинок.Обновить;
	Элементы.ГруппаОжиданияЗагрузкиСпискаСостояний.Видимость = Ложь;
	
	Если ДополнительныеПараметры.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыВыполнитьЗагрузкуДанныхПоНаложеннымПлатежам() Тогда
		
		СформироватьПредставленияПоНаложеннымПлатежам();
		
	КонецЕсли;
	
	// Вывод сообщений из фонового задания.
	СервисДоставкиКлиент.ОбработатьРезультатФоновогоЗадания(Результат, ДополнительныеПараметры, Отказ);
	Если Результат = Неопределено ИЛИ ФоновоеЗадание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка результата поиска.
	Если Не Отказ И Результат.Статус = "Выполнено" Тогда
		Если ЗначениеЗаполнено(Результат.АдресРезультата)
			И ЭтоАдресВременногоХранилища(Результат.АдресРезультата)
			И ДополнительныеПараметры.ФоновоеЗадание.ИдентификаторЗадания
			= ЭтотОбъект[ИмяФоновогоЗадания].ИдентификаторЗадания Тогда
			
			Если ДополнительныеПараметры.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьЗаказыНаДоставку() Тогда
				
				// Сохранение текущей строки для позиционирования после загрузки.
				ТекущиеДанныеСписка = Элементы.Список.ТекущиеДанные;
				
				// Загрузка результатов поиска.
				ЗагрузитьРезультатПолученияЗаказов(Результат.АдресРезультата);
				ФоновоеЗаданиеПолучитьЗаказыНаДоставку = Неопределено;
				
				// Подготовка данных для элементов постраничной выдачи данных.
				КоличествоСтрок = Список.Количество();
				Если КоличествоСтрок = 0 Тогда
					СостояниеВыполненияЗапроса = НСтр("ru = 'Заказы на доставку не найдены'");
				Иначе
					СостояниеВыполненияЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Отображается заказов на доставку: %1 из %2'"),
						Список.Количество(),
						Страницы.КоличествоСтрок);
				КонецЕсли;
				Элементы.ДекорацияСостояниеВыполненияЗапроса.Заголовок = СостояниеВыполненияЗапроса;
				
				ОбновитьНавигационноеМеню();
				
				ОтборыИзменение = Ложь;
				СформироватьНадписьОтбора();
				
				// Позиционирование на текущей строке списка.
				Если ТекущиеДанныеСписка <> Неопределено Тогда
					СтрокиСписка = Список.НайтиСтроки(Новый Структура("Идентификатор", ТекущиеДанныеСписка.Идентификатор));
					Если СтрокиСписка.Количество() Тогда
						Элементы.Список.ТекущаяСтрока = СтрокиСписка[0].ПолучитьИдентификатор();
					КонецЕсли;
				КонецЕсли;
				
				ЭтотОбъект[ИмяФоновогоЗадания] = Неопределено;
				
				Если ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьерика() Тогда
					
					КоличествоНовыхЗаказовКПовторнойОтправке = НайтиЗаказыТребующиеПовторнойОтправки();
					Если КоличествоНовыхЗаказовКПовторнойОтправке > 0 Тогда
						
						ПоказатьОповещениеПользователя(
							НСтр("ru = 'Заказы требуют отправки'"),,
							СтрШаблон(
								НСтр("ru = 'Появились заказы (%1), требующие повторной отправки'"),
								Формат(КоличествоНовыхЗаказовКПовторнойОтправке, НСтр("ru = 'ЧГ='"))),
							БиблиотекаКартинок.ПредупреждениеБЭД,
							СтатусОповещенияПользователя.Информация);
						
					КонецЕсли;
						
				КонецЕсли;
				
			ИначеЕсли ДополнительныеПараметры.ИмяПроцедуры
				= СервисДоставкиКлиентСервер.ИмяПроцедурыОтменитьЗаказНаДоставку() 
				ИЛИ ДополнительныеПараметры.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыОтменитьМультизаказНаДоставку() Тогда
				
				РезультатОтмены = 0;
				СуммаОтмены = 0;
				ЗагрузитьРезультатОтменыЗаказа(Результат.АдресРезультата, РезультатОтмены, СуммаОтмены);
				Если РезультатОтмены = 1 Тогда //Отменен
					ТекстПояснения = НСтр("ru='Заказ на доставку отменен.'");
					ПоказатьОповещениеПользователя(НСтр("ru='Отмена:'"),, ТекстПояснения, БиблиотекаКартинок.Информация32);
					
					Если Список.Количество() = 0 И Страницы.Страница > 1 Тогда
						Страницы.Страница = Страницы.Страница - 1;
						ПолучитьЗаказыНаДоставку();
					ИначеЕсли Список.Количество() <> Страницы.КоличествоСтрок Тогда
						ПолучитьЗаказыНаДоставку();
					КонецЕсли;
				ИначеЕсли РезультатОтмены = 2 Тогда //Возможна платная отмена
					
					ОписаниеОЗавершении = Новый ОписаниеОповещения("ЗавершениеОтменитьЗаказ", ЭтотОбъект);
					
					СписокКнопок = Новый СписокЗначений();
					СписокКнопок.Добавить(КодВозвратаДиалога.Повторить, НСтр("ru='Да'"));
					СписокКнопок.Добавить(КодВозвратаДиалога.Отмена);
					
					ТекстСообщения = НСтр("ru='Доступна только платная отмена заказа.'");
					Если СуммаОтмены > 0 Тогда
						ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru='Сумма отмены заказа %1.'"), СуммаОтмены);
					КонецЕсли;
						
					ТекстСообщения = ТекстСообщения + НСтр("ru='Отменить платно заказ?'");
					ПоказатьВопрос(ОписаниеОЗавершении, ТекстСообщения, СписокКнопок);
					
				ИначеЕсли РезультатОтмены = 3 Тогда //Отмена заказа уже не возможна
					
					ТекстПояснения = НСтр("ru='Заказ на доставку невозможно отменить.'");
					ПоказатьОповещениеПользователя(НСтр("ru='Отмена:'"),, ТекстПояснения, БиблиотекаКартинок.Информация32);
					
					Если Список.Количество() = 0 И Страницы.Страница > 1 Тогда
						Страницы.Страница = Страницы.Страница - 1;
						ПолучитьЗаказыНаДоставку();
					ИначеЕсли Список.Количество() <> Страницы.КоличествоСтрок Тогда
						ПолучитьЗаказыНаДоставку();
					КонецЕсли;
					
				КонецЕсли;
				ЭтотОбъект[ИмяФоновогоЗадания] = Неопределено;
				
			ИначеЕсли ДополнительныеПараметры.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьСостояния() Тогда
				
				// Загрузка результатов поиска.
				ЗагрузитьРезультатПолученияСпискаСостояний(Результат.АдресРезультата);
				ЭтотОбъект[ИмяФоновогоЗадания] = Неопределено;
				
			ИначеЕсли ДополнительныеПараметры.ИмяПроцедуры 
				= СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьГрузоперевозчиков() Тогда
				
				// Загрузка результатов поиска.
				ЗагрузитьРезультатПолученияСпискаПеревозчиков(Результат.АдресРезультата);
				ЭтотОбъект[ИмяФоновогоЗадания] = Неопределено;
				
			ИначеЕсли ДополнительныеПараметры.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьПунктыВыдачиКлиента() Тогда
				
				// Загрузка результатов поиска.
				ЗагрузитьРезультатПолученияСпискаПунктовВыдачиКлиента(Результат.АдресРезультата);
				ЭтотОбъект[ИмяФоновогоЗадания] = Неопределено;
				
			ИначеЕсли ДополнительныеПараметры.ИмяПроцедуры =
				СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьСостояниеОпцииКурьерика() Тогда
				
				ЗагрузитьРезультатПолучитьСостояниеОпцииКурьерика(Результат.АдресРезультата);
				
			ИначеЕсли ДополнительныеПараметры.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьСписокКурьеров() Тогда
				
				ЗагрузитьРезультатПолученияСпискаКурьеров(Результат.АдресРезультата);
				ЭтотОбъект[ИмяФоновогоЗадания] = Неопределено;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьНадписьОтбора()
	
	ТекстЗаголовка = НСтр("ru='Настроить отбор'");
	Элементы.ГруппаОтборы.Заголовок = ?(ОтборыИзменение, ТекстЗаголовка + "*", ТекстЗаголовка);
	
КонецПроцедуры

#КонецОбласти

#Область ВыполнитьЗапросВФоне

&НаСервере
Функция ВыполнитьЗапросВФоне(ИнтернетПоддержкаПодключена, ПараметрыОперации)
	
	// Проверка подключения Интернет-поддержки пользователей.
	ИнтернетПоддержкаПодключена = ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	Если Не ИнтернетПоддержкаПодключена Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Отказ = Ложь;
	ПараметрыЗапроса = ПараметрыЗапроса(ПараметрыОперации, Отказ);
	
	Если ПараметрыЗапроса.Свойство("ОрганизацияБизнесСетиСсылка") Тогда
		СервисДоставкиСлужебный.ПроверитьОрганизациюБизнесСети(ПараметрыЗапроса.ОрганизацияБизнесСетиСсылка, Отказ);
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИмяФоновогоЗадания = "ФоновоеЗадание" + ПараметрыОперации.ИмяПроцедуры;
	ФоновоеЗадание = ЭтотОбъект[ИмяФоновогоЗадания];
	Если ФоновоеЗадание <> Неопределено Тогда
		ОтменитьВыполнениеЗадания(ФоновоеЗадание.ИдентификаторЗадания);
	КонецЕсли;
	
	Задание = Новый Структура("ИмяПроцедуры, Наименование, ПараметрыПроцедуры");
	Задание.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1. %2'"),
		СервисДоставкиКлиентСервер.ПредставлениеТипаГрузоперевозки(ТипГрузоперевозки),
		ПараметрыОперации.НаименованиеОперации);
	Задание.ИмяПроцедуры = "СервисДоставки." + ПараметрыОперации.ИмяПроцедуры;
	Задание.ПараметрыПроцедуры = ПараметрыЗапроса;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = Задание.Наименование;
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	Если ПараметрыОперации.Свойство("ЗапуститьВФоне")
		И ТипЗнч(ПараметрыОперации.ЗапуститьВФоне) = Тип("Булево") Тогда
		ПараметрыВыполнения.ЗапуститьВФоне = ПараметрыОперации.ЗапуститьВФоне;
	КонецЕсли; 
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(Задание.ИмяПроцедуры,
		Задание.ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ОтменитьВыполнениеЗадания(ИдентификаторЗадания)
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
		ИдентификаторЗадания = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьЗаказыНаДоставкуВФоне()
	
	ПараметрыОперации = Новый Структура("ИмяПроцедуры, НаименованиеОперации, ВыводитьОкноОжидания");
	ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьЗаказыНаДоставку();
	ПараметрыОперации.НаименованиеОперации = НСтр("ru = 'Получение заказов на доставку.'");
	
	Возврат ВыполнитьЗапросВФоне(Ложь, ПараметрыОперации);
	
КонецФункции

&НаСервере
Функция ПолучитьСостоянияВФоне()
	
	ПараметрыОперации = Новый Структура("ИмяПроцедуры, НаименованиеОперации, ВыводитьОкноОжидания");
	ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьСостояния();
	ПараметрыОперации.НаименованиеОперации = НСтр("ru = 'Получение списка состояний заказа.'");
	
	Возврат ВыполнитьЗапросВФоне(Ложь, ПараметрыОперации);
	
КонецФункции

&НаСервере
Функция ПолучитьГрузоперевозчиковВФоне()
	
	ПараметрыОперации = Новый Структура("ИмяПроцедуры, НаименованиеОперации, ВыводитьОкноОжидания");
	ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьГрузоперевозчиков();
	ПараметрыОперации.НаименованиеОперации = НСтр("ru = 'Получение списка грузоперевозчиков.'");
	
	Возврат ВыполнитьЗапросВФоне(Ложь, ПараметрыОперации);
	
КонецФункции

&НаСервере
Функция ПолучитьПунктыВыдачиКлиентаВФоне()
	
	ПараметрыОперации = Новый Структура("ИмяПроцедуры, НаименованиеОперации, ВыводитьОкноОжидания");
	ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьПунктыВыдачиКлиента();
	ПараметрыОперации.НаименованиеОперации = НСтр("ru = 'Получение списка пунктов выдачи клиента.'");
	
	Возврат ВыполнитьЗапросВФоне(Ложь, ПараметрыОперации);
	
КонецФункции

&НаСервере
Функция ПолучитьСостояниеОпцииКурьерикаВФоне(Организация = Неопределено)
	
	ПараметрыОперации = Новый Структура("ВыводитьОкноОжидания");
	ПараметрыОперации.Вставить("ИмяПроцедуры", СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьСостояниеОпцииКурьерика());
	ПараметрыОперации.Вставить("НаименованиеОперации", НСтр("ru = 'Получение информации по подписке.'"));
	
	Если Организация <> Неопределено Тогда
		ПараметрыОперации.Вставить("ОрганизацияБизнесСетиСсылка", Организация);
	КонецЕсли;
	
	Возврат ВыполнитьЗапросВФоне(Ложь, ПараметрыОперации);
	
КонецФункции

&НаСервере
Функция ПолучитьКурьеровВФоне()
	
	ПараметрыОперации = Новый Структура("ИмяПроцедуры, НаименованиеОперации, ВыводитьОкноОжидания");
	ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьСписокКурьеров();
	ПараметрыОперации.НаименованиеОперации = НСтр("ru = 'Получение списка курьеров.'");
	
	Возврат ВыполнитьЗапросВФоне(Ложь, ПараметрыОперации);
	
КонецФункции

#КонецОбласти

#Область ПараметрыЗапроса

&НаСервере
Функция ПараметрыЗапроса(ПараметрыОперации, Отказ)
	
	ПараметрыЗапроса = Новый Структура();
	
	ИмяПроцедуры = ПараметрыОперации.ИмяПроцедуры;
	
	Если ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьЗаказыНаДоставку() Тогда
		ПараметрыЗапроса = ПараметрыЗапросаПолучитьЗаказыНаДоставку(ПараметрыОперации, Отказ);
	ИначеЕсли ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыОтменитьЗаказНаДоставку() Тогда
		ПараметрыЗапроса = ПараметрыЗапросаОтменитьЗаказНаДоставку(ПараметрыОперации, Отказ);
	ИначеЕсли ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыОтменитьМультизаказНаДоставку() Тогда
		ПараметрыЗапроса = ПараметрыЗапросаОтменитьМультизаказНаДоставку(ПараметрыОперации, Отказ);
	ИначеЕсли ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьСостояния() Тогда
		ПараметрыЗапроса = ПараметрыЗапросаПолучитьСостояния(ПараметрыОперации, Отказ);
	ИначеЕсли ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьГрузоперевозчиков() Тогда
		ПараметрыЗапроса = ПараметрыЗапросаПолучитьГрузоперевозчиков(ПараметрыОперации, Отказ);
	ИначеЕсли ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьПунктыВыдачиКлиента() Тогда
		ПараметрыЗапроса = ПараметрыЗапросаПолучитьПунктыВыдачиКлиента(ПараметрыОперации, Отказ);
	ИначеЕсли ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьСписокКурьеров() Тогда
		ПараметрыЗапроса = ПараметрыЗапросаПолучитьСписокКурьеров(ПараметрыОперации, Отказ);
	КонецЕсли;
	
	ПараметрыЗапроса.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

&НаСервере
Функция ПараметрыЗапросаПолучитьЗаказыНаДоставку(ПараметрыОперации, Отказ)
	
	ПараметрыЗапроса = СервисДоставки.НовыйПараметрыЗапросаПолучитьЗаказыНаДоставку();
	
	ПараметрыЗапроса.Страница = Страницы.Страница;
	ПараметрыЗапроса.РазмерСтраницы = Страницы.РазмерСтраницы;
	
	Если ЗначениеЗаполнено(ТипГрузоперевозки) Тогда
		ПараметрыЗапроса.ТипГрузоперевозки = ТипГрузоперевозки;
	Иначе
		ТекстОшибки = НСтр("ru='Не выбран тип грузоперевозки.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Отказ = Истина;
	КонецЕсли;
	
	ПараметрыЗапроса.ФильтрПоМультизаказам = ?(ПоказыватьТолькоМультизаказы,
											СервисДоставки.ФильтрПоМультизаказамОдиночныеИМультизаказы(),
											СервисДоставки.ФильтрПоМультизаказамВсеЗаказы());
	
	Если ЗначениеЗаполнено(ОтборРольОрганизации) Тогда
		ПараметрыЗапроса.Роль = ОтборРольОрганизации;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборГрузоперевозчик) Тогда
		ПараметрыЗапроса.Грузоперевозчик = ОтборГрузоперевозчик;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборСтатусОплаты) Тогда
		ПараметрыЗапроса.Оплата = ОтборСтатусОплаты;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборКурьер) Тогда
		ПараметрыЗапроса.Курьер = ОтборКурьер;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборНаложенныйПлатеж) Тогда
		ПараметрыЗапроса.НаложенныйПлатеж = ОтборНаложенныйПлатеж;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборНаложенныйПлатежПолучен) Тогда
		ПараметрыЗапроса.НаложенныйПлатежПолучен = ОтборНаложенныйПлатежПолучен;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборНомерЗаказа) Тогда
		ПараметрыЗапроса.НомерЗаказа = ОтборНомерЗаказа;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборДокументОснование) Тогда
		ПараметрыЗапроса.ДокументОснованиеИдентификатор = ОтборДокументОснование.УникальныйИдентификатор();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПериодДатаСоздания.ДатаНачала) Тогда
		ПараметрыЗапроса.ДатаСозданияОт = ПериодДатаСоздания.ДатаНачала;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПериодДатаСоздания.ДатаОкончания) Тогда
		ПараметрыЗапроса.ДатаСозданияДо = ПериодДатаСоздания.ДатаОкончания;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПериодДатаОтгрузки.ДатаНачала) Тогда
		ПараметрыЗапроса.ДатаОтгрузкиОт = ПериодДатаОтгрузки.ДатаНачала;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПериодДатаОтгрузки.ДатаОкончания) Тогда
		ПараметрыЗапроса.ДатаОтгрузкиДо = ПериодДатаОтгрузки.ДатаОкончания;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПериодДатаДоставки.ДатаНачала) Тогда
		ПараметрыЗапроса.ДатаДоставкиОт = ПериодДатаДоставки.ДатаНачала;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПериодДатаДоставки.ДатаОкончания) Тогда
		ПараметрыЗапроса.ДатаДоставкиДо = ПериодДатаДоставки.ДатаОкончания;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборОтправитель) Тогда
		НовыйЭлемент = ПараметрыЗапроса.Отправитель.Добавить();
		
		ПараметрыКонтрагента = СервисДоставки.НовыйПараметрыКонтрагента();
		ПараметрыКонтрагента.Ссылка = ОтборОтправитель;
		СервисДоставкиПереопределяемый.ЗаполнитьПараметрыКонтрагента(ПараметрыКонтрагента);
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, ПараметрыКонтрагента);
		
		Для каждого СтрокаОтправителя Из ПараметрыЗапроса.Отправитель Цикл
			Если СтрокаОтправителя.ЮрФизЛицо = 0 Тогда
				ТекстОшибки = НСтр("ru='Некорректное значение поля ""Отправитель"".
								|Отправитель может быть ИП, либо юридическим лицом.'");
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
				Отказ = Истина;
			ИначеЕсли СтрокаОтправителя.ЮрФизЛицо = 0 Тогда
				ТекстОшибки = НСтр("ru='Некорректное значение поля ""Отправитель"".
								|Отправитель не может быть физическим лицом.'");
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
				Отказ = Истина;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборПолучатель) Тогда
		НовыйЭлемент = ПараметрыЗапроса.Получатель.Добавить();
		
		ПараметрыКонтрагента = СервисДоставки.НовыйПараметрыКонтрагента();
		ПараметрыКонтрагента.Ссылка = ОтборПолучатель;
		СервисДоставкиПереопределяемый.ЗаполнитьПараметрыКонтрагента(ПараметрыКонтрагента);
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, ПараметрыКонтрагента);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборОтправительАдресЗначение) Тогда
		
		ПараметрыЗначения = СервисДоставки.ЗначениеИзСтрокиJSON(ОтборОтправительАдресЗначение);
		НовыйЭлемент = ПараметрыЗапроса.Откуда.Добавить();
		НовыйЭлемент.КодФИАС = ПараметрыЗначения.ID;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборПунктВыдачи) Тогда
		
		ПараметрыЗапроса.ПунктВыдачиКлиента = ОтборПунктВыдачи;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборПолучательАдресЗначение) Тогда
		
		ПараметрыЗначения = СервисДоставки.ЗначениеИзСтрокиJSON(ОтборПолучательАдресЗначение);
		НовыйЭлемент = ПараметрыЗапроса.Куда.Добавить();
		НовыйЭлемент.КодФИАС = ПараметрыЗначения.ID;
		
	КонецЕсли;
	
	Для Каждого ТекущееЗначение Из ОтборСостояние Цикл
		
		Если ТекущееЗначение.Значение > 100 Тогда
			КоллекцияСостояний = КоллекцияСостоянийПоИдентификатору(ТекущееЗначение.Значение);
			
			Для Каждого ТекущееСостояние Из КоллекцияСостояний Цикл
				НовоеСостояние = ПараметрыЗапроса.Состояние.Добавить();
				НовоеСостояние.Идентификатор = ТекущееСостояние;
			КонецЦикла;
			
			Прервать;
		Иначе
			НовоеСостояние = ПараметрыЗапроса.Состояние.Добавить();
			НовоеСостояние.Идентификатор = ТекущееЗначение.Значение;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

&НаСервере
Функция ПараметрыЗапросаОтменитьЗаказНаДоставку(ПараметрыОперации, Отказ)
	
	ПараметрыЗапроса = СервисДоставки.НовыйПараметрыЗапросаОтменитьЗаказНаДоставку();
	
	ТекущиеДанные = Список.НайтиПоИдентификатору(ПараметрыОперации.ПараметрыЗапроса.ИдентификаторСтроки);
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат ПараметрыЗапроса;
	КонецЕсли;
	
	ПараметрыЗапроса.Вставить("Идентификатор", ТекущиеДанные.Идентификатор);
	ПараметрыЗапроса.Вставить("ОтменитьЗаказПлатно", ?(ПараметрыОперации.ПараметрыЗапроса.ПлатнаяОтмена, "1", "0"));
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

&НаСервере
Функция ПараметрыЗапросаОтменитьМультизаказНаДоставку(ПараметрыОперации, Отказ)
	
	ПараметрыЗапроса = СервисДоставки.НовыйПараметрыЗапросаОтменитьМультизаказНаДоставку();
	
	ТекущиеДанные = Список.НайтиПоИдентификатору(ПараметрыОперации.ПараметрыЗапроса.ИдентификаторСтроки);
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат ПараметрыЗапроса;
	КонецЕсли;
	
	ПараметрыЗапроса.Вставить("Идентификатор", ТекущиеДанные.Идентификатор);
	ПараметрыЗапроса.Вставить("ОтменитьЗаказПлатно", ?(ПараметрыОперации.ПараметрыЗапроса.ПлатнаяОтмена, "1", "0"));
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

&НаСервере
Функция ПараметрыЗапросаПолучитьСостояния(ПараметрыОперации, Отказ)

	ПараметрыЗапроса = СервисДоставки.НовыйПараметрыЗапросаПолучитьСостояния();
	Возврат ПараметрыЗапроса;
	
КонецФункции

&НаСервере
Функция ПараметрыЗапросаПолучитьГрузоперевозчиков(ПараметрыОперации, Отказ)
	
	ПараметрыЗапроса = СервисДоставки.НовыйПараметрыЗапросаПолучитьГрузоперевозчиков();
	
	Если ЗначениеЗаполнено(ТипГрузоперевозки) Тогда
		ПараметрыЗапроса.ТипГрузоперевозки = ТипГрузоперевозки;
	Иначе
		ТекстОшибки = НСтр("ru='Не выбран тип грузоперевозки.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Отказ = Истина;
	КонецЕсли;
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

&НаСервере
Функция ПараметрыЗапросаПолучитьПунктыВыдачиКлиента(ПараметрыОперации, Отказ)
	
	ПараметрыЗапроса = СервисДоставки.НовыйПараметрыЗапросаПолучитьПунктыВыдачиКлиента();
	
	Если ЗначениеЗаполнено(ТипГрузоперевозки) Тогда
		ПараметрыЗапроса.ТипГрузоперевозки = ТипГрузоперевозки;
	Иначе
		ТекстОшибки = НСтр("ru='Не выбран тип грузоперевозки.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Отказ = Истина;
	КонецЕсли;
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

&НаСервере
Функция ПараметрыЗапросаПолучитьСписокКурьеров(ПараметрыОперации, Отказ)
	
	ПараметрыЗапроса = СервисДоставки.НовыйПараметрыЗапросаПолучитьСписокКурьеров();
	
	Если ЗначениеЗаполнено(ТипГрузоперевозки) Тогда
		ПараметрыЗапроса.ТипГрузоперевозки = ТипГрузоперевозки;
	Иначе
		ТекстОшибки = НСтр("ru='Не выбран тип грузоперевозки.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Отказ = Истина;
	КонецЕсли;
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

#КонецОбласти

#Область ЗагрузитьРезультаты

&НаСервере
Процедура ЗагрузитьРезультатПолученияЗаказов(АдресРезультата, ОперацияВыполнена = Истина)
	
	Если ЭтоАдресВременногоХранилища(АдресРезультата) Тогда
		
		Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
		Если ЗначениеЗаполнено(Результат) 
			И ТипЗнч(Результат) = Тип("Структура") Тогда
			
			Список.Очистить();
			Если Результат.Свойство("Список") Тогда
			
				Если ТипЗнч(Результат.Список) = Тип("Массив") Тогда
					
					ПараметрыОтбораЗаказов = Новый Структура;
					ПараметрыОтбораЗаказов.Вставить("Список", Результат.Список);
					
					Если ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьерика() Тогда
						ИдентификаторыЗаказовТребуетсяПовторнаяОтправка =
							РегистрыСведений.ДанныеЗаказовСервисДоставки.ЗаказыКПовторнойОтправке(
								ПараметрыОтбораЗаказов);
					Иначе
						ИдентификаторыЗаказовТребуетсяПовторнаяОтправка = Новый СписокЗначений();
					КонецЕсли;
					
					Для Каждого ТекущаяСтрока Из Результат.Список Цикл
						
						НоваяСтрока = Список.Добавить();
						
						ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
						
						Если ЗначениеЗаполнено(ТекущаяСтрока.ДатаСозданияЗаказа) Тогда
							НоваяСтрока.ДатаЗаказа = ТекущаяСтрока.ДатаЗаказа;
						КонецЕсли;
						
						Если Не ЗначениеЗаполнено(НоваяСтрока.ДатаОтгрузки) Тогда
							НоваяСтрока.ДатаОтгрузки = ТекущаяСтрока.ДатаОтгрузки;
						КонецЕсли;
						
						Если Не ЗначениеЗаполнено(НоваяСтрока.ДатаДоставки) Тогда
							НоваяСтрока.ДатаДоставки = ТекущаяСтрока.ДатаДоставки;
						КонецЕсли;
						
						Если ТекущаяСтрока.ЗаборОтАдреса Тогда
							НоваяСтрока.ВариантОтгрузки = НСтр("ru='От адреса'");
						Иначе
							НоваяСтрока.ВариантОтгрузки = НСтр("ru='От терминала'");
						КонецЕсли;
						
						Если ТекущаяСтрока.ДоставкаДоАдреса Тогда
							НоваяСтрока.ВариантДоставки = НСтр("ru='До адреса'");
						Иначе
							НоваяСтрока.ВариантДоставки = НСтр("ru='До терминала'");
						КонецЕсли;
						
						НоваяСтрока.ОтправительНаименование = ТекущаяСтрока.ОтправительНаименование;
						НоваяСтрока.ПолучательНаименование = ТекущаяСтрока.ПолучательНаименование;
						НоваяСтрока.ПлательщикНаименование = ТекущаяСтрока.ПлательщикНаименование;
						
						Если ЗначениеЗаполнено(ТекущаяСтрока.АдресОтгрузкиНаименование) Тогда
							НоваяСтрока.АдресОтгрузкиПредставление = ТекущаяСтрока.АдресОтгрузкиТипНаименование
								+ ": " + ТекущаяСтрока.АдресОтгрузкиНаименование;
						КонецЕсли;
						
						Если ЗначениеЗаполнено(ТекущаяСтрока.АдресДоставкиНаименование) Тогда
							НоваяСтрока.АдресДоставкиПредставление = ТекущаяСтрока.АдресДоставкиТипНаименование
								+ ": " + ТекущаяСтрока.АдресДоставкиНаименование;
						КонецЕсли;
						
						НоваяСтрока.КартинкаИдентификатор = 3;
						Если НоваяСтрока.ЭтоМультизаказ Тогда
							НоваяСтрока.КартинкаИдентификатор = 0;
						ИначеЕсли НоваяСтрока.МультизаказНомер <> "" Тогда
							НоваяСтрока.КартинкаИдентификатор = 5;
						КонецЕсли;

						НоваяСтрока.ПунктВыдачи = ТекущаяСтрока.ПунктВыдачиКлиентаНаименование;
						
						Если ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьерика() Тогда
							Если ТекущаяСтрока.НаложенныйПлатежВидОплаты = 2 Тогда
								НоваяСтрока.КОплатеНаличные = ТекущаяСтрока.СуммаНаложенногоПлатежа;
								НоваяСтрока.КОплатеКарта = ТекущаяСтрока.СуммаНаложенногоПлатежаДополнительный;
							Иначе
								НоваяСтрока.КОплатеНаличные = ТекущаяСтрока.СуммаНаложенногоПлатежаДополнительный;
								НоваяСтрока.КОплатеКарта = ТекущаяСтрока.СуммаНаложенногоПлатежа;
							КонецЕсли;
						КонецЕсли;
						
						СервисДоставкиПереопределяемый.ОбработатьРезультатСостоянияЗаказаНаДоставку(ТекущаяСтрока);
						
						НоваяСтрока.ТребуетсяПовторнаяОтправка = ИдентификаторыЗаказовТребуетсяПовторнаяОтправка.НайтиПоЗначению(
							НоваяСтрока.Идентификатор) <> Неопределено;
						
						ПараметрыСозданияДанныхЗаказа = СервисДоставкиКлиентСервер.НовыйПараметрыЗаписиДанныхЗаказаСервисаДоставки();
						ПараметрыСозданияДанныхЗаказа.ТипГрузоперевозки = ТипГрузоперевозки;
						ПараметрыСозданияДанныхЗаказа.Организация = ОрганизацияБизнесСетиСсылка;
						ПараметрыСозданияДанныхЗаказа.ДокументыОснования = НоваяСтрока.ДокументыОснования.ВыгрузитьЗначения();
						ПараметрыСозданияДанныхЗаказа.Идентификатор = НоваяСтрока.Идентификатор;
						ПараметрыСозданияДанныхЗаказа.Представление = СервисДоставкиКлиентСервер.ПредставлениеЗаказаНаДоставку(НоваяСтрока, Истина);
						
						РегистрыСведений.ДанныеЗаказовСервисДоставки.СоздатьОбновитьДанныеЗаказа(ПараметрыСозданияДанныхЗаказа);
						
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если Результат.Свойство("Страницы") Тогда
				ЗаполнитьЗначенияСвойств(Страницы,Результат.Страницы);
			КонецЕсли;
			
			СервисДоставки.ОбработатьБлокОшибок(Результат, ОперацияВыполнена);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультатОтменыЗаказа(АдресРезультата, РезультатВыполнения, СуммаОтмены)
	
	РезультатВыполнения = 0;
	ОперацияВыполнена = Истина;
	ДокументыОснования = Неопределено;
	
	Если ЭтоАдресВременногоХранилища(АдресРезультата) Тогда
		Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
		Если ЗначениеЗаполнено(Результат) 
			И ТипЗнч(Результат) = Тип("Структура") Тогда
			
			Если Результат.Свойство("Идентификатор") Тогда
				
				Если Результат.Свойство("ДокументОтменен")
					И Результат.ДокументОтменен = Истина Тогда
					Идентификатор = Результат.Идентификатор;
					НайденныеСтроки = Список.НайтиСтроки(Новый Структура("Идентификатор", Идентификатор));
					Если НайденныеСтроки.Количество() Тогда
						ДокументыОснования = НайденныеСтроки[0].ДокументыОснования;
						Список.Удалить(НайденныеСтроки[0]);
					КонецЕсли;
					РезультатВыполнения = 1;
				ИначеЕсли Результат.Свойство("ДоступнаПлатнаяОтмена")
					И Результат.ДоступнаПлатнаяОтмена = Истина Тогда
					РезультатВыполнения = 2;
					СуммаОтмены = Результат.СуммаПлатнойОтмены;
				ИначеЕсли Результат.Свойство("ДоступнаОтмена")
					И Не Результат.ДоступнаОтмена Тогда
					РезультатВыполнения = 3;
				КонецЕсли;
			Иначе
				СервисДоставки.ОбработатьБлокОшибок(Результат, ОперацияВыполнена);
			КонецЕсли;
			
		Иначе
			ОперацияВыполнена = Ложь;
		КонецЕсли;
	Иначе
		ОперацияВыполнена = Ложь;
	КонецЕсли;
	
	Если ОперацияВыполнена Тогда
		СервисДоставкиПереопределяемый.ОбработатьРезультатЗапросаОтменыЗаказаНаДоставку(Результат, ДокументыОснования);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультатПолученияСпискаСостояний(АдресРезультата, ОперацияВыполнена = Истина)
	
	Элементы.ГруппаОжиданияЗагрузкиСпискаСостояний.Видимость = Ложь;
	
	Если ЭтоАдресВременногоХранилища(АдресРезультата) Тогда
		Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
		Если ЗначениеЗаполнено(Результат) 
			И ТипЗнч(Результат) = Тип("Структура") Тогда
			
			Если Результат.Свойство("Список") Тогда
			
				ЭлементыДеревоСписка = ОтборДеревоСостояний.ПолучитьЭлементы();
				ЭлементыДеревоСписка.Очистить();
				
				ЭлементыСписка = Результат.Список;
				
				ТекущаяГруппа = "";
				Для Каждого ТекущийЭлементСписка Из ЭлементыСписка Цикл
					
					Если ТекущийЭлементСписка.НаименованиеГруппы = "" Тогда
						НовыйЭлементДерева = ЭлементыДеревоСписка.Добавить();
						
						НовыйЭлементДерева.Наименование = ТекущийЭлементСписка.Наименование;
						НовыйЭлементДерева.Идентификатор = ТекущийЭлементСписка.Идентификатор;
						
						ЭлементОтбора = ОтборСостояние.НайтиПоЗначению(НовыйЭлементДерева.Идентификатор);
						НовыйЭлементДерева.Пометка = (ЭлементОтбора <> Неопределено);
					Иначе
						Если ТекущаяГруппа <> ТекущийЭлементСписка.НаименованиеГруппы Тогда
							НовыйЭлементДерева = ЭлементыДеревоСписка.Добавить();
							НовыйЭлементДерева.Наименование = ТекущийЭлементСписка.НаименованиеГруппы;
							ТекущаяГруппа = НовыйЭлементДерева.Наименование;
						КонецЕсли;
							
						ЭлементыНовойВеткиДерева = НовыйЭлементДерева.ПолучитьЭлементы();
						ЭлементНовойВеткиДерева = ЭлементыНовойВеткиДерева.Добавить();
						
						ЭлементНовойВеткиДерева.Наименование = ТекущийЭлементСписка.Наименование;
						ЭлементНовойВеткиДерева.Идентификатор = ТекущийЭлементСписка.Идентификатор;
						
						ЭлементОтбора = ОтборСостояние.НайтиПоЗначению(ЭлементНовойВеткиДерева.Идентификатор);
						ЭлементНовойВеткиДерева.Пометка = (ЭлементОтбора <> Неопределено);
						
					КонецЕсли;
				КонецЦикла;
				
			Иначе
				ОперацияВыполнена = Ложь;
			КонецЕсли;
			
			СервисДоставки.ОбработатьБлокОшибок(Результат, ОперацияВыполнена);
			
		Иначе
			ОперацияВыполнена = Ложь;
		КонецЕсли;
	Иначе
		ОперацияВыполнена = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультатПолученияСпискаПеревозчиков(АдресРезультата)
	
	Элементы.ГруппаОжиданияЗагрузкиСпискаСостояний.Видимость = Ложь;
	
	Если ЭтоАдресВременногоХранилища(АдресРезультата) Тогда
		
		Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
		Если ЗначениеЗаполнено(Результат) 
			И ТипЗнч(Результат) = Тип("Структура") Тогда
			
			Если Результат.Свойство("Список") Тогда
				ЭлементыСписка = Результат.Список;
				СписокВыбора = Элементы.ОтборГрузоперевозчик.СписокВыбора;
				Для Каждого ТекущийЭлементСписка Из ЭлементыСписка Цикл
					
					СписокВыбора.Добавить(ТекущийЭлементСписка.Идентификатор, ТекущийЭлементСписка.Наименование);
					
					Если ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СДоставка() Тогда
						
						Элементы.ГруппаПереходНаНовыйТипАвторизации.Видимость = ТекущийЭлементСписка.Свойство("УстаревшийТипАвторизации")
							И ТекущийЭлементСписка.УстаревшийТипАвторизации;
							
						ПараметрыФормыУстаревшегоПодключения = Новый Структура;
						ПараметрыФормыУстаревшегоПодключения.Вставить("Авторизован", Ложь);
						ПараметрыФормыУстаревшегоПодключения.Вставить("ИдентификаторСервиса", ТекущийЭлементСписка.ИдентификаторСервиса);
						ПараметрыФормыУстаревшегоПодключения.Вставить("Организация", ОрганизацияБизнесСетиСсылка);
						ПараметрыФормыУстаревшегоПодключения.Вставить("Перевозчик", ТекущийЭлементСписка.Наименование);
						ПараметрыФормыУстаревшегоПодключения.Вставить("ТипАвторизации", ТекущийЭлементСписка.ТребуетсяАвторизация);
						ПараметрыФормыУстаревшегоПодключения.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
						ПараметрыФормыУстаревшегоПодключения.Вставить("УстаревшийТипАвторизации", ТекущийЭлементСписка.УстаревшийТипАвторизации);
						
					КонецЕсли;
					
				КонецЦикла;
				
			Иначе
				ОперацияВыполнена = Ложь;
			КонецЕсли;
			
			СервисДоставки.ОбработатьБлокОшибок(Результат, ОперацияВыполнена);
		
		Иначе
			ОперацияВыполнена = Ложь;
		КонецЕсли;
	Иначе
		ОперацияВыполнена = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультатПолученияСпискаПунктовВыдачиКлиента(АдресРезультата)
	
	Элементы.ГруппаОжиданияЗагрузкиСпискаСостояний.Видимость = Ложь;
	
	Если ЭтоАдресВременногоХранилища(АдресРезультата) Тогда
		
		Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
		Если ЗначениеЗаполнено(Результат) 
			И ТипЗнч(Результат) = Тип("Структура") Тогда
			
			Если Результат.Свойство("Список") Тогда
				
				СоответствиеСкладов.Очистить();
				
				ЭлементыСписка = Результат.Список;
				СписокВыбора = Элементы.ОтборПунктВыдачи.СписокВыбора;
				
				Для Каждого ТекущийЭлементСписка Из ЭлементыСписка Цикл
					
					НоваяСтрока = СоответствиеСкладов.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущийЭлементСписка);
					
					Если СписокВыбора.НайтиПоЗначению(НоваяСтрока.Идентификатор) = Неопределено Тогда
						
						СписокВыбора.Добавить(НоваяСтрока.Идентификатор,
							СтрШаблон("%1%2",
								НоваяСтрока.Наименование,
								?(НоваяСтрока.Склад.Пустая(), "", "(" + НоваяСтрока.Склад + ")")));
								
					КонецЕсли;
					
				КонецЦикла;
				
			Иначе
				ОперацияВыполнена = Ложь;
			КонецЕсли;
			
			СервисДоставки.ОбработатьБлокОшибок(Результат, ОперацияВыполнена);
			
		Иначе
			ОперацияВыполнена = Ложь;
		КонецЕсли;
		
	Иначе
		ОперацияВыполнена = Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТипГрузоперевозки", ТипГрузоперевозки);
	Запрос.УстановитьПараметр("МассивИдентификаторов", СоответствиеСкладов.Выгрузить().ВыгрузитьКолонку("Идентификатор"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИдентификаторОбъекта,
	|	Значение,
	|	ЗначениеПоУмолчанию
	|ИЗ
	|	РегистрСведений.СоответствиеОбъектовСервисовДоставки КАК ТаблицаСоответствий
	|ГДЕ
	|	ТипГрузоперевозки = &ТипГрузоперевозки
	|	И ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовСервисовДоставки.ПунктВыдачи)
	|	И ИдентификаторОбъекта В (&МассивИдентификаторов)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Идентификатор", Выборка.ИдентификаторОбъекта);
		
		ПоискСтрок = СоответствиеСкладов.НайтиСтроки(СтруктураОтбора);
		Если ЗначениеЗаполнено(ПоискСтрок) Тогда
			
			ПоискСтрок.Получить(0).ИспользоватьПоУмолчанию = Выборка.ЗначениеПоУмолчанию;
			ПоискСтрок.Получить(0).Склад = Выборка.Значение;
			ПоискСтрок.Получить(0).СкладАдрес = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(
				Выборка.Значение,
				Справочники.ВидыКонтактнойИнформации.АдресСклада);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультатПолучитьСостояниеОпцииКурьерика(АдресРезультата)
	
	ОперацияВыполнена = Ложь;
	
	Если ЭтоАдресВременногоХранилища(АдресРезультата) Тогда
		
		Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
		
		Если ЗначениеЗаполнено(Результат)
			И ТипЗнч(Результат) = Тип("Структура") Тогда
			
			Если Результат.Свойство("ОпцияДоступна") Тогда
				
				ОпцияДоступнаКурьерика = Результат.ОпцияДоступна;
				КомментарийОпцияКурьерика = Результат.Комментарий;
				БалансОпцияКурьерика = Результат.Баланс;
				СрокДействияОпцияКурьерика = ПрочитатьДатуJSON(Результат.СрокДействия, ФорматДатыJSON.ISO);
				
				СервисДоставкиВызовСервера.УстановитьЗаголовокОпцияКурьерика(ЭтотОбъект);
				УстановитьДоступностьЭлементовПоЗначениюОпции();
				
				ОперацияВыполнена = Истина;
				
			КонецЕсли;
			
			СервисДоставки.ОбработатьБлокОшибок(Результат, ОперацияВыполнена);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультатПолученияСпискаКурьеров(АдресРезультата)
	
	Если ЭтоАдресВременногоХранилища(АдресРезультата) Тогда
		Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
		Если ЗначениеЗаполнено(Результат) И ТипЗнч(Результат) = Тип("Структура") Тогда
			Если Результат.Свойство("Список") Тогда
				ЭлементыСписка = Результат.Список;
				СписокВыбора = Элементы.ОтборКурьер.СписокВыбора;
				СписокВыбора.Очистить();
				Для Каждого ТекущийЭлементСписка Из ЭлементыСписка Цикл
					СписокВыбора.Добавить(ТекущийЭлементСписка.Идентификатор, СокрЛП(ТекущийЭлементСписка.Наименование));
				КонецЦикла;
			Иначе
				ОперацияВыполнена = Ложь;
			КонецЕсли;
			СервисДоставки.ОбработатьБлокОшибок(Результат, ОперацияВыполнена);
		Иначе
			ОперацияВыполнена = Ложь;
		КонецЕсли;
	Иначе
		ОперацияВыполнена = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура СброситьСтраницы()
	
	Страницы.Вставить("Страница",          0); 
	Страницы.Вставить("КоличествоСтраниц", 0); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборТипаУчастникаГрузоперевозки(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = 1 Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФормуВыбора("ОрганизацияСервисДоставки", Элемент.Имя);
		
	ИначеЕсли ВыбранноеЗначение = 2 Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФормуВыбора("КонтрагентСервисДоставки", Элемент.Имя);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбора(ИмяСправочника, ИмяРеквизита, ПараметрыОтбора = Неопределено, 
	ИмяПроцедурыОбработки="ОбработатьВыборЗначения")
	
	Если ПараметрыОтбора = Неопределено Тогда
		ПараметрыОтбора = Новый Структура();
	КонецЕсли;
	
	ТекущееЗначениеРеквизита = ЭтотОбъект[ИмяРеквизита];
	
	ИмяФормыВыбора = СервисДоставкиВызовСервера.ИмяФормыВыбораПоОпределяемомуТипу(ИмяСправочника);
	
	Если ИмяФормыВыбора <> "" Тогда
		
		ПараметрыОткрытия = Новый Структура("ТекущаяСтрока", ТекущееЗначениеРеквизита);
		ПараметрыОткрытия.Вставить("Отбор", ПараметрыОтбора);
		ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
		
		СервисДоставкиКлиент.ПередОткрытиемФормыВыбора(ПараметрыОткрытия, ИмяСправочника);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(ИмяПроцедурыОбработки, ЭтотОбъект,
														Новый Структура("ИмяРеквизита", ИмяРеквизита));
		ОткрытьФорму(
			ИмяФормыВыбора,
			ПараметрыОткрытия,
			ЭтотОбъект,,,,ОписаниеОповещения,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УчастникГрузоперевозкиНачалоВыбора(Элемент, НомерРоли, СтандартнаяОбработка)
	
	Если Не Элемент.КнопкаВыбора Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеРеквизита = ЭтотОбъект[Элемент.Имя];
	
	Если ОтборРольОрганизации = НомерРоли Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФормуВыбора("ОрганизацияСервисДоставки", Элемент.Имя);
		
	ИначеЕсли ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
		
		СтандартнаяОбработка = Ложь;
		Если ОпределяемыйТипСодержитТипЗначения("ОрганизацияСервисДоставки", ЗначениеРеквизита) Тогда
			ОткрытьФормуВыбора("ОрганизацияСервисДоставки", Элемент.Имя);
		Иначе
			ОткрытьФормуВыбора("КонтрагентСервисДоставки", Элемент.Имя);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиПоНомеруСтраницы(НомерСтраницы)
	
	Если НомерСтраницы < 0
		ИЛИ (НомерСтраницы > (Страницы.КоличествоСтраниц))
		ИЛИ (НомерСтраницы = (Страницы.Страница)) Тогда
		Возврат;
	КонецЕсли;
	
	Страницы.Страница = НомерСтраницы;
	
	ПолучитьЗаказыНаДоставку();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПредставлениеОтбораСостояния()
	
	ОтборСостояниеПредставление = "";
	
	Для Каждого ТекущийЭлементСписка Из ОтборСостояние Цикл
		ОтборСостояниеПредставление = ОтборСостояниеПредставление + ?(ОтборСостояниеПредставление = "","",", ")
										+ ТекущийЭлементСписка.Представление;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция КоллекцияСостоянийПоИдентификатору(Идентификатор)
	
	Коллекция = Новый Массив();
	
	МассивКоллекций = Новый Массив();
	МассивКоллекций.Добавить("0"); //Все черновики
	МассивКоллекций.Добавить("1,2,3,21"); //Все к отгрузке
	МассивКоллекций.Добавить("4,5,6,8,22"); //Все отгруженные
	МассивКоллекций.Добавить("7"); //Все готовые к выдаче
	МассивКоллекций.Добавить("9"); //Все возвращаемые
	МассивКоллекций.Добавить("18"); //Все отмененные
	
	Индекс = Идентификатор-101;
	ТипЧисло = Новый ОписаниеТипов("Число");
	
	Если Индекс >= 0 И Индекс < МассивКоллекций.Количество() Тогда
		КоллекцияСтрока = МассивКоллекций[Индекс];
		КоллекцияВСтроках = СтрРазделить(КоллекцияСтрока,",");
		Для Каждого ТекущийЭлементСписка Из КоллекцияВСтроках Цикл
			Коллекция.Добавить(ТипЧисло.ПривестиЗначение(ТекущийЭлементСписка));
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Коллекция;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуГрузоперевозчика()
	
	ПараметрыОткрытияФормы = Новый Структура();
	ПараметрыОткрытияФормы.Вставить("Идентификатор", ОтборГрузоперевозчик);
	ПараметрыОткрытияФормы.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	ПараметрыОткрытияФормы.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	
	ОткрытьФорму("Обработка.СервисДоставки.Форма.КарточкаГрузоперевозчика", 
							ПараметрыОткрытияФормы,
							ЭтотОбъект,,,,,
							РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
							
КонецПроцедуры

&НаКлиенте
Функция НомерЗаказаНекорректный(ТекстНомераЗаказа)
	
	Результат = Ложь;
	
	Если Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ТекстНомераЗаказа) Тогда
		Результат = Истина;
	КонецЕсли;
	
	Если СтрДлина(ТекстНомераЗаказа) > 9 Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
&НаСервереБезКонтекста
Функция ОрганизацииБизнесСетиНаСервере()
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(СервисДоставкиСлужебный.ОрганизацииБизнесСети());
	
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультатВыбораЗаказаНаДоставку(ВыбраннаяСтрока)
	
	Если ОбработкаВыбора = СервисДоставкиКлиентСервер.ИмяПроцедурыДобавитьДокументОснованиеВВыбранныйЗаказНаДоставку() Тогда
		ОткрытьФормуЗаказа(Список.НайтиПоИдентификатору(ВыбраннаяСтрока));
		Закрыть();
	Иначе
		СервисДоставкиКлиентПереопределяемый.ОбработатьРезультатВыбораЗаказаНаДоставку(ЭтотОбъект, 
			Список.НайтиПоИдентификатору(ВыбраннаяСтрока),
			ОбработкаВыбора);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиСпискаПоУмолчанию()
	
	КлючОбщихНастроекФормы = "Обработка.СервисДоставки.Форма.СписокЗаказов";
	
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить(КлючОбщихНастроекФормы, "ОтборРольОрганизации", ОтборРольОрганизации);
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить(КлючОбщихНастроекФормы, "ОтборСостояние", ОтборСостояние);
	//@skip-warning
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить(КлючОбщихНастроекФормы, "ПериодДатаСоздания", ПериодДатаСоздания);
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить(КлючОбщихНастроекФормы, "РазмерСтраницы", РазмерСтраницы);
	
	КлючОбщихНастроекФормы = "Обработка.СервисДоставки.Форма.СписокЗаказов." + ТипГрузоперевозки;
	
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить(КлючОбщихНастроекФормы, "ПоказыватьТолькоМультизаказы", ПоказыватьТолькоМультизаказы);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементовПоЗначениюОпции()
	
	Элементы.ГруппаПодпискаНаСервис.Видимость = ОтображатьУведомлениеПоОпцииКурьерика;
	Элементы.ВыбратьСтрокуИзСписка.Доступность = ОпцияДоступнаКурьерика;
	Элементы.Отправить.Доступность = ОпцияДоступнаКурьерика;
	Элементы.ГруппаДействия.Доступность = ОпцияДоступнаКурьерика;
	Элементы.ГруппаСписокЗаказов.ТолькоПросмотр = Не ОпцияДоступнаКурьерика;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытииОкнаАвторизации(Результат, ДополнительныеПараметры) Экспорт
	
	ПолучитьГрузоперевозчиков();
	
КонецПроцедуры

&НаСервере
Функция НайтиЗаказыТребующиеПовторнойОтправки()
	
	ПараметрыОтбораЗаказов = Новый Структура;
	ПараметрыОтбораЗаказов.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	ПараметрыОтбораЗаказов.Вставить("Организация", ОрганизацияБизнесСетиСсылка);
	
	КоличествоЗаказовДоОбновления = СписокЗаказовТребуетсяПовторнаяОтправка.Количество();
	
	СписокЗаказовТребуетсяПовторнаяОтправка.Очистить();
	ОбщегоНазначенияКлиентСервер.ДополнитьСписок(
		СписокЗаказовТребуетсяПовторнаяОтправка,
		РегистрыСведений.ДанныеЗаказовСервисДоставки.ЗаказыКПовторнойОтправке(ПараметрыОтбораЗаказов));
		
	КоличествоЗаказовПослеОбновления = СписокЗаказовТребуетсяПовторнаяОтправка.Количество();
	
	Элементы.ДекорацияТребуетсяПовторнаяОтправка.Заголовок = СтрШаблон(
		НСтр("ru = 'Требуется повторная отправка (%1)'"),
		Формат(СписокЗаказовТребуетсяПовторнаяОтправка.Количество(), "ЧН=; ЧГ="));
	
	Элементы.ГруппаТребуетсяПовторнаяОтправка.Видимость = СписокЗаказовТребуетсяПовторнаяОтправка.Количество() > 0;
	
	Возврат КоличествоЗаказовПослеОбновления - КоличествоЗаказовДоОбновления;
	
КонецФункции

&НаКлиенте
Процедура ПослеПовторнойОтправкиЗаказов(Значение, ДополнительныеПараметры) Экспорт
	
	ПолучитьЗаказыНаДоставку();
	
КонецПроцедуры

#КонецОбласти
