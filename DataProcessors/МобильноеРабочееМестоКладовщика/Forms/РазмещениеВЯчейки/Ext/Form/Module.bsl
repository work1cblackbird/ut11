#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Ячейка                = Параметры.Ячейка;
	Склад                 = Параметры.Склад;
	Помещение             = Параметры.Помещение;
	ТипОперации           = Параметры.ТипОперации;
	ПоказыватьФотоТоваров = Параметры.ПоказыватьФотоТоваров;
	
	Если Параметры.Свойство("Режим") Тогда
		Режим = Параметры.Режим;
	КонецЕсли;
	
	Если Параметры.Свойство("Товары") Тогда
		
		ТоварыВЯчейке = Параметры.Товары;
		
		Для Каждого Строка Из ТоварыВЯчейке Цикл
			
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			Если ТипОперации = 1 Тогда
				НоваяСтрока.ЗаголовокФакт = НСтр("ru = 'Размещено'");
			ИначеЕсли ТипОперации = 2 Тогда
				НоваяСтрока.ЗаголовокФакт = НСтр("ru = 'Отобрано'");
			КонецЕсли;
			НоваяСтрока.Ячейка = Ячейка;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТипОперации = 3 Тогда
		
		Если Режим = "ПересчетРедактирование" Тогда
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Параметры);
		КонецЕсли;
		
		Если Параметры.Свойство("ЯчейкаЗамена") Тогда
			ЯчейкаЗамена = Параметры.ЯчейкаЗамена;
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		
		Если ИмяСобытия = "ScanData" И МенеджерОборудованияУТКлиент.ЕстьНеобработанноеСобытие() Тогда
			
			// Преобразуем предварительно к ожидаемому формату
			Если Параметр[1] = Неопределено Тогда
				Штрихкод = Параметр[0];
			Иначе
				Штрихкод = Параметр[1][1];
			КонецЕсли;
			
			ЭтоЯчейка = Ложь;
			ЭтоТовар = Ложь;
			
			РезультатПоискаЯчейки = НайтиЯчейкуСервер(Склад, Помещение, Штрихкод);
			ШтрихкодЯчейки = ЗначениеЗаполнено(РезультатПоискаЯчейки.Ячейка);
			
			Если ШтрихкодЯчейки Тогда
				Если РезультатПоискаЯчейки.Ячейка = Ячейка Тогда
					ЗакрытьФорму();
					Возврат;
				Иначе
					СообщениеПользователю = Новый СообщениеПользователю;
					СообщениеПользователю.Текст = НСтр("ru='Для перехода к следующей ячейке закончите работу с текущей'");
					СообщениеПользователю.Сообщить();
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			ПараметрыКарточкаТовара = НайтиТоварСервер(Штрихкод);
			
			Если НЕ ЗначениеЗаполнено(ПараметрыКарточкаТовара.Номенклатура) Тогда
				
				ТекстОшибки = СтрШаблон(НСтр("ru='Не найдена номенклатура со штрихкодом: %1'"), Штрихкод);
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
				
				Возврат;
			КонецЕсли;
			
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("Номенклатура",   ПараметрыКарточкаТовара.Номенклатура);
			СтруктураПоиска.Вставить("Характеристика", ПараметрыКарточкаТовара.Характеристика);
			СтруктураПоиска.Вставить("Упаковка",       ПараметрыКарточкаТовара.Упаковка);
			
			РезультатПоиска = Товары.НайтиСтроки(СтруктураПоиска);
			Если РезультатПоиска.Количество() > 0 Тогда
				
				Описание = Новый ОписаниеОповещения("ПодтвердитьКоличество", ЭтаФорма);
				
				ПараметрыФормы = Новый Структура;
				ПараметрыФормы.Вставить("Номенклатура",   РезультатПоиска[0].Номенклатура);
				ПараметрыФормы.Вставить("Характеристика", РезультатПоиска[0].Характеристика);
				ПараметрыФормы.Вставить("Серия",          РезультатПоиска[0].Серия);
				ПараметрыФормы.Вставить("Назначение",     РезультатПоиска[0].Назначение);
				ПараметрыФормы.Вставить("Упаковка",       РезультатПоиска[0].Упаковка);
				ПараметрыФормы.Вставить("Количество",     ПараметрыКарточкаТовара.Количество);
				ПараметрыФормы.Вставить("НомерСтроки",    Товары.Индекс(Элементы.Товары.ТекущиеДанные));
				ПараметрыФормы.Вставить("Склад",          Склад);
				ПараметрыФормы.Вставить("Помещение",      Помещение);
				
				Если ТипОперации = 1 Тогда
					ПараметрыФормы.Вставить("Режим", "РазмещениеВЯчейку");
				Иначе
					ПараметрыФормы.Вставить("Режим", "ОтборИзЯчейки");
				КонецЕсли;
				
				Если РезультатПоиска[0].ЯчейкаЗамена.Пустая() Тогда
					ПараметрыФормы.Вставить("Ячейка", РезультатПоиска[0].Ячейка);
				Иначе
					ПараметрыФормы.Вставить("Ячейка", РезультатПоиска[0].ЯчейкаЗамена);
				КонецЕсли;
				ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
				
				ОткрытьФорму(
				"Обработка.МобильноеРабочееМестоКладовщика.Форма.КарточкаТовара",ПараметрыФормы,
				ЭтаФорма,,,,Описание,
				РежимОткрытияОкнаФормы.Независимый);
				
			Иначе
				СообщениеПользователю = Новый СообщениеПользователю;
				ТекстСообщения = НСтр("ru='Не требуется %1 номенклатуры %2 в упаковке %3'");
				Если ТипОперации = 1 Тогда
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", НСтр("ru='размещение'"));
				Иначе
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", НСтр("ru='отбор'"));
				КонецЕсли;
				
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%2", ПараметрыКарточкаТовара.Номенклатура);
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%3", ПараметрыКарточкаТовара.Упаковка);
				
				СообщениеПользователю.Текст = ТекстСообщения;
				СообщениеПользователю.Сообщить();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Описание = Новый ОписаниеОповещения("ПодтвердитьКоличество", ЭтаФорма);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Номенклатура", Элемент.ТекущиеДанные.Номенклатура);
	ПараметрыФормы.Вставить("Характеристика", Элемент.ТекущиеДанные.Характеристика);
	ПараметрыФормы.Вставить("Серия", Элемент.ТекущиеДанные.Серия);
	ПараметрыФормы.Вставить("Назначение", Элемент.ТекущиеДанные.Назначение);
	ПараметрыФормы.Вставить("Упаковка", Элемент.ТекущиеДанные.Упаковка);
	ПараметрыФормы.Вставить("Количество", Элемент.ТекущиеДанные.КоличествоУпаковок);
	Если ТипОперации = 1 Тогда
		ПараметрыФормы.Вставить("Режим", "РедактированиеРазмещениеПоЯчейкам");
	ИначеЕсли ТипОперации = 2 Тогда
		ПараметрыФормы.Вставить("Режим", "РедактированиеОтборИзЯчейки");
	ИначеЕсли ТипОперации = 4 Тогда
		ПараметрыФормы.Вставить("Режим", "Пересчет");
	Иначе
		ПараметрыФормы.Вставить("Режим", "Просмотр");
	КонецЕсли;
	ПараметрыФормы.Вставить("НомерСтроки", Товары.Индекс(Элементы.Товары.ТекущиеДанные));
	ПараметрыФормы.Вставить("Склад", Склад);
	ПараметрыФормы.Вставить("Помещение", Помещение);
	Если Элемент.ТекущиеДанные.ЯчейкаЗамена.Пустая() Тогда
		ПараметрыФормы.Вставить("Ячейка", Элемент.ТекущиеДанные.Ячейка);
	Иначе
		ПараметрыФормы.Вставить("Ячейка", Элемент.ТекущиеДанные.ЯчейкаЗамена);
	КонецЕсли;
	ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
	
	ОткрытьФорму(
	"Обработка.МобильноеРабочееМестоКладовщика.Форма.КарточкаТовара",ПараметрыФормы,
	ЭтаФорма,,,,Описание,
	РежимОткрытияОкнаФормы.Независимый);
	
	
КонецПроцедуры

&НаКлиенте
Процедура Подтвердить(Команда)
	
	Если ТипОперации = 3 И НЕ ЗначениеЗаполнено(ЯчейкаЗамена) Тогда
		
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = НСтр("ru='Укажите ячейку для размещения'");
		СообщениеПользователю.Сообщить();
		
		Возврат;
		
	КонецЕсли;
	
	ЗакрытьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму()
	
	МассивРазмещений = Новый Массив;
	
	Для Каждого Строка Из Товары Цикл
		
		Структура = СтруктураТоваров();
		
		ЗаполнитьЗначенияСвойств(Структура, Строка);
		Структура.Вставить("КоличествоУпаковок", Строка.КоличествоУпаковокРазмещено);
		
		МассивРазмещений.Добавить(Структура);
		
	КонецЦикла;
	
	РезультатСборки = Новый Структура;
	РезультатСборки.Вставить("Ячейка", Ячейка);
	РезультатСборки.Вставить("МассивРазмещений", МассивРазмещений);
	РезультатСборки.Вставить("Режим", Режим);
	
	Закрыть(РезультатСборки);
	
КонецПроцедуры

&НаКлиенте
Процедура Блокировать(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ячейка", Ячейка);
	
	ОткрытьФорму(
	"Обработка.МобильноеРабочееМестоКладовщика.Форма.БлокировкаЯчейки",ПараметрыФормы,
	ЭтаФорма,,,,,
	РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура Сканировать(Команда)
	
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить("Ячейка", НСтр("ru = 'Ячейку'"));
	СписокЗначений.Добавить("Товар", НСтр("ru = 'Товар'"));
	СписокЗначений.Добавить("Отмена", НСтр("ru = 'Отмена'"));
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаВыбор", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Сканировать'"), СписокЗначений, 0);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Если ТипОперации = 1 Тогда
		ОбновитьПодсказку();
		Элементы.Подтвердить.Заголовок = НСтр("ru = 'Закончить размещение'");
		ЭтаФорма.Заголовок = НСтр("ru = 'Размещение в ячейку'");
	ИначеЕсли ТипОперации = 2 Тогда
		ОбновитьПодсказку();
		Элементы.Подтвердить.Заголовок = НСтр("ru = 'Закончить отбор'");
		ЭтаФорма.Заголовок             = НСтр("ru = 'Отбор из ячейки'");
	ИначеЕсли ТипОперации = 3 Тогда
		ОбновитьПодсказку();
		Элементы.Подтвердить.Заголовок             = НСтр("ru = 'Переместить'");
		ЭтаФорма.Заголовок                         = НСтр("ru = 'Перемещение'");
		Элементы.ЯчейкаЗамена.Видимость            = Истина;
		Элементы.ГруппаПодсказка.Видимость         = Ложь;
		Элементы.ТоварыЗаголовокФакт.Видимость     = Ложь;
		Элементы.ТоварыУпаковкаРазмещено.Видимость = Истина;
		Элементы.Ячейка.Заголовок                  = НСтр("ru = 'Ячейка отбор'");
		Элементы.ЯчейкаЗамена.Заголовок            = НСтр("ru = 'Ячейка размещение'");
	ИначеЕсли ТипОперации = 4 Тогда
		ОбновитьПодсказку();
		Элементы.Подтвердить.Заголовок = НСтр("ru = 'Закончить пересчет'");
		ЭтаФорма.Заголовок             = НСтр("ru = 'Пересчет в ячейке'");
	КонецЕсли;
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТоварыКоличествоУпаковокРазмещено");
	
	ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Товары.КоличествоУпаковок");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ЭлементОтбора.ПравоеЗначение = Новый ПолеКомпоновкиДанных("Товары.КоличествоУпаковокРазмещено");
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПроблема);
	
КонецПроцедуры

&НаКлиенте
Функция СтруктураТоваров()
	
	Структура = Новый Структура;
	Структура.Вставить("Номенклатура","");
	Структура.Вставить("Характеристика","");
	Структура.Вставить("Серия","");
	Структура.Вставить("Назначение","");
	Структура.Вставить("Упаковка","");
	Структура.Вставить("УпаковкаРазмещено","");
	Структура.Вставить("КоличествоУпаковок","");
	Структура.Вставить("КоличествоУпаковокРазмещено","");
	Структура.Вставить("Ячейка","");
	Структура.Вставить("ЯчейкаЗамена","");
	Структура.Вставить("НомерСтроки","");
	
	Возврат Структура;
	
КонецФункции

&НаКлиенте
Процедура РезультатЗаменыЯчейки(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЯчейкаЗамена = Результат;
	Элементы.Ячейка.Видимость = Ложь;
	Элементы.ЯчейкаЗамена.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПодсказку()
	
	Если ТипОперации = 1 Тогда
		Элементы.ЗаголовокПодсказка1.Заголовок = НСтр("ru = 'Размещено строк'");
	ИначеЕсли ТипОперации = 4 Тогда
		Элементы.ЗаголовокПодсказка1.Заголовок = НСтр("ru = 'Пересчитано'");
	Иначе
		Элементы.ЗаголовокПодсказка1.Заголовок = НСтр("ru = 'Отобрано строк'");
	КонецЕсли;
	
	План = Товары.Количество();
	ОтработаноСтрокБезОшибок = 0;
	Для Каждого Строка Из Товары Цикл
		Если Строка.КоличествоУпаковок = Строка.КоличествоУпаковокРазмещено Тогда
			ОтработаноСтрокБезОшибок = ОтработаноСтрокБезОшибок + 1;
		КонецЕсли;
	КонецЦикла;
		
	Элементы.ЗаголовокПодсказкаФакт.Заголовок = ОтработаноСтрокБезОшибок;
	Элементы.ЗаголовокПодсказкаПлан.Заголовок = План;
	Элементы.ЗаголовокПодсказкаРазница.Заголовок = План - ОтработаноСтрокБезОшибок;
	
	
КонецПроцедуры

&НаСервере
Процедура ПодтвердитьКоличество(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Режим = "РедактированиеРазмещениеПоЯчейкам" ИЛИ Результат.Режим = "РедактированиеОтборИзЯчейки" Тогда
		
		Литерал = "НомерСтроки";
		НомерСтроки = Результат[Литерал];
		
		Товары[НомерСтроки].КоличествоУпаковокРазмещено = Результат.КоличествоУпаковок;
		Если Товары[НомерСтроки].Ячейка <> Результат.Ячейка Тогда
			Товары[НомерСтроки].ЯчейкаЗамена = Результат.Ячейка;
		КонецЕсли;
	Иначе
		
		КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Результат.Упаковка, Результат.Номенклатура);
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Номенклатура", Результат.Номенклатура);
		СтруктураПоиска.Вставить("Характеристика", Результат.Характеристика);
		СтруктураПоиска.Вставить("Серия", Результат.Серия);
		СтруктураПоиска.Вставить("Упаковка", Результат.Упаковка);
		СтруктураПоиска.Вставить("Назначение", Результат.Назначение);
		СтруктураПоиска.Вставить("Ячейка", Результат.Ячейка);
		
		РезультатПоиска = Товары.НайтиСтроки(СтруктураПоиска);
		Если РезультатПоиска.Количество() > 0 Тогда
			РезультатПоиска[0].КоличествоУпаковокРазмещено = РезультатПоиска[0].КоличествоУпаковокРазмещено + Результат.КоличествоУпаковок;
		ИначеЕсли Результат.КоличествоУпаковок > 0 Тогда
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Результат,,"КоличествоУпаковок");
			НоваяСтрока.КоличествоУпаковокРазмещено = Результат.КоличествоУпаковок;
		КонецЕсли;
		
		Если Результат.Режим = "Перемещение" Тогда
			НоваяСтрока.Ячейка             = Ячейка;
			НоваяСтрока.ЯчейкаЗамена       = ЯчейкаЗамена;
			НоваяСтрока.КоличествоУпаковок = НоваяСтрока.КоличествоУпаковокРазмещено;
			НоваяСтрока.УпаковкаРазмещено  = НоваяСтрока.Упаковка;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбновитьПодсказку();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиТоварСервер(Штрихкод)
	Возврат Обработки.МобильноеРабочееМестоКладовщика.НайтиТоварПоШК(Штрихкод);
КонецФункции

&НаСервереБезКонтекста
Функция НайтиЯчейкуСервер(Склад, Помещение, Штрихкод)
	Возврат Обработки.МобильноеРабочееМестоКладовщика.НайтиЯчейкуСервер(Склад, Помещение, Штрихкод);
КонецФункции

&НаКлиенте
Процедура ПослеЗакрытияВопросаВыбор(Результат, Параметры) Экспорт
	
	Если Результат = "Ячейка" Тогда
		
		Описание = Новый ОписаниеОповещения("УстановитьЯчейку", ЭтаФорма);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ТипПоиска", "ПоискЯчейки");
		ПараметрыФормы.Вставить("Склад", Склад);
		ПараметрыФормы.Вставить("Помещение", Помещение);
		
		ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоКладовщика.Форма.СканированиеШтрихкода",ПараметрыФормы,
		ЭтаФорма,,,,Описание,
		РежимОткрытияОкнаФормы.Независимый);
		
	ИначеЕсли Результат = "Товар" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Описание = Новый ОписаниеОповещения("РезультатСканирования", ЭтаФорма);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ТипПоиска", "ПоискНоменклатуры");
		ПараметрыФормы.Вставить("Склад", Склад);
		ПараметрыФормы.Вставить("Помещение", Помещение);
		
		ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоКладовщика.Форма.СканированиеШтрихкода",ПараметрыФормы,
		ЭтаФорма,,,,Описание,
		РежимОткрытияОкнаФормы.Независимый);
		
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатСканирования(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Описание = Новый ОписаниеОповещения("ПодтвердитьКоличество", ЭтаФорма);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Номенклатура", Результат.Номенклатура);
	ПараметрыФормы.Вставить("Характеристика", Результат.Характеристика);
	Если Результат.Свойство("Серия") Тогда
		ПараметрыФормы.Вставить("Серия", Результат.Серия);
	Иначе
		ПараметрыФормы.Вставить("Серия", ПустаяСерия);
	КонецЕсли;
	ПараметрыФормы.Вставить("Упаковка", Результат.Упаковка);
	ПараметрыФормы.Вставить("Количество", 1);
	ПараметрыФормы.Вставить("Режим", "Перемещение");
	ПараметрыФормы.Вставить("НомерСтроки", 0);
	ПараметрыФормы.Вставить("Склад", Склад);
	ПараметрыФормы.Вставить("Помещение", Помещение);
	ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
	
	ОткрытьФорму(
	"Обработка.МобильноеРабочееМестоКладовщика.Форма.КарточкаТовара",ПараметрыФормы,
	ЭтаФорма,,,,Описание,
	РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЯчейку(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЯчейкаЗамена = Результат;
	
	Для Каждого Строка Из Товары Цикл
		Строка.ЯчейкаЗамена = ЯчейкаЗамена;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти