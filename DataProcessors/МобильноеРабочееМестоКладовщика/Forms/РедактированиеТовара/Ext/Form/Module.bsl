
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Номенклатура = Параметры.Номенклатура;
	ОбновитьФорму();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьНаСервере();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВесЕдиницаИзмеренияНажатие(Элемент, СтандартнаяОбработка)
	ЕдиницаИзмеренияНажатие("ВесЕдиницаИзмерения", СтандартнаяОбработка, "Вес");
КонецПроцедуры

&НаКлиенте
Процедура ОбъемЕдиницаИзмеренияНажатие(Элемент, СтандартнаяОбработка)
	ЕдиницаИзмеренияНажатие("ОбъемЕдиницаИзмерения", СтандартнаяОбработка, "Объем");
КонецПроцедуры

&НаКлиенте
Процедура ДлинаЕдиницаИзмеренияНажатие(Элемент, СтандартнаяОбработка)
	ЕдиницаИзмеренияНажатие("ДлинаЕдиницаИзмерения", СтандартнаяОбработка, "Длина");
КонецПроцедуры

&НаКлиенте
Процедура ПлощадьЕдиницаИзмеренияНажатие(Элемент, СтандартнаяОбработка)
	ЕдиницаИзмеренияНажатие("ПлощадьЕдиницаИзмерения", СтандартнаяОбработка, "Площадь");
КонецПроцедуры

&НаКлиенте
Процедура ВесЧислительПриИзменении(Элемент)
	УстановитьЕдиницуИзмеренияПоУмолчанию("Вес");
КонецПроцедуры

&НаКлиенте
Процедура ОбъемЧислительПриИзменении(Элемент)
	УстановитьЕдиницуИзмеренияПоУмолчанию("Объем");
КонецПроцедуры

&НаКлиенте
Процедура ДлинаЧислительПриИзменении(Элемент)
	УстановитьЕдиницуИзмеренияПоУмолчанию("Длина");
КонецПроцедуры

&НаКлиенте
Процедура ПлощадьЧислительПриИзменении(Элемент)
	УстановитьЕдиницуИзмеренияПоУмолчанию("Площадь");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьФорму()
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Номенклатура);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере()
	
	НоменклатураОбъект = Номенклатура.ПолучитьОбъект();
	ЗаполнитьЗначенияСвойств(НоменклатураОбъект, ЭтаФорма);
	
	Если НоменклатураОбъект.ВесЗнаменатель > 0 Тогда
		НоменклатураОбъект.ВесИспользовать = Истина;
	КонецЕсли;
	
	Если НоменклатураОбъект.ОбъемЗнаменатель > 0 Тогда
		НоменклатураОбъект.ОбъемИспользовать = Истина;
	КонецЕсли;
	
	Если НоменклатураОбъект.ДлинаЗнаменатель > 0 Тогда
		НоменклатураОбъект.ДлинаИспользовать = Истина;
	КонецЕсли;
	
	Если НоменклатураОбъект.ПлощадьЗнаменатель > 0 Тогда
		НоменклатураОбъект.ПлощадьИспользовать = Истина;
	КонецЕсли;
	
	НоменклатураОбъект.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияНажатие(ИмяПоля, СтандартнаяОбработка, ИмяМеры)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОповещения = Новый Структура("ИмяПоля, ИмяМеры", ИмяПоля, ИмяМеры);
	ОписаниеОповещения  = Новый ОписаниеОповещения("ЕдиницаИзмеренияНажатиеЗавершение", ЭтотОбъект, ПараметрыОповещения);
	
	ИмяТипаИзмеряемойВеличины = "Перечисление.ТипыИзмеряемыхВеличин." + ИмяМеры;
	ТипИзмеряемойВеличины     = ПредопределенноеЗначение(ИмяТипаИзмеряемойВеличины);
	
	Отбор          = Новый Структура("ТипИзмеряемойВеличины", ТипИзмеряемойВеличины);
	ПараметрыФормы = Новый Структура("Отбор,ТекущаяСтрока", Отбор, ЭтаФорма[ИмяПоля]);
	
	ОткрытьФорму("Справочник.УпаковкиЕдиницыИзмерения.ФормаВыбора",
				ПараметрыФормы,
				ЭтотОбъект,
				,
				,
				,
				ОписаниеОповещения,
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияНажатиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Модифицированность = Истина;
		
		ЭтаФорма[ДополнительныеПараметры.ИмяПоля] = Результат;
	КонецЕсли;
	
	Мера = ДополнительныеПараметры.ИмяМеры;
	СвойстваМеры = СвойстваМеры(Мера);
	
	ТекстПредставленияЗначения = НСтр("ru = 'Можно указывать количество в %ЕдиницаИзмеренияМеры% в документах'");
	ТекстПредставленияЗначения = СтрЗаменить(ТекстПредставленияЗначения,
	"%ЕдиницаИзмеренияМеры%",
	Строка(ЭтаФорма[СвойстваМеры.ЕдиницаИзмерения]));
	
КонецПроцедуры

// Описание
// 
// Параметры:
// 	Мера - Строка -
// Возвращаемое значение:
// 	Структура - Содержит ключи:
// 		* Использовать - Строка -
// 		* ЕдиницаИзмерения - Строка -
// 		* Числитель - Строка -
// 		* Знаменатель - Строка -
// 		* МожноУказыватьВДокументах - Строка -
// 		* МожноУказыватьВДокументахСтрока - Строка -
// 		* Отступ - Строка -
// 		* Надпись - Строка -
&НаКлиентеНаСервереБезКонтекста
Функция СвойстваМеры(Мера)
	
	СвойстваМеры = Новый Структура;
	СвойстваМеры.Вставить("Использовать");
	СвойстваМеры.Вставить("ЕдиницаИзмерения");
	СвойстваМеры.Вставить("Числитель");
	СвойстваМеры.Вставить("Знаменатель");
	СвойстваМеры.Вставить("МожноУказыватьВДокументах");
	СвойстваМеры.Вставить("МожноУказыватьВДокументахСтрока");
	СвойстваМеры.Вставить("Отступ");
	СвойстваМеры.Вставить("Надпись");
	
	Если Мера = "Объем" Тогда
		СвойстваМеры.Использовать = "ОбъемИспользовать";
		СвойстваМеры.ЕдиницаИзмерения = "ОбъемЕдиницаИзмерения";
		СвойстваМеры.Числитель = "ОбъемЧислитель";
		СвойстваМеры.Знаменатель = "ОбъемЗнаменатель";
		СвойстваМеры.МожноУказыватьВДокументах = "ОбъемМожноУказыватьВДокументах";
		СвойстваМеры.МожноУказыватьВДокументахСтрока = "ОбъемМожноУказыватьВДокументахСтрока";
		СвойстваМеры.Отступ = "ОбъемОтступ";
		СвойстваМеры.Надпись = "ОбъемНадпись";
	ИначеЕсли Мера = "Длина" Тогда
		СвойстваМеры.Использовать = "ДлинаИспользовать";
		СвойстваМеры.ЕдиницаИзмерения = "ДлинаЕдиницаИзмерения";
		СвойстваМеры.Числитель = "ДлинаЧислитель";
		СвойстваМеры.Знаменатель = "ДлинаЗнаменатель";
		СвойстваМеры.МожноУказыватьВДокументах = "ДлинаМожноУказыватьВДокументах";
		СвойстваМеры.МожноУказыватьВДокументахСтрока = "ДлинаМожноУказыватьВДокументахСтрока";
		СвойстваМеры.Отступ = "ДлинаОтступ";
		СвойстваМеры.Надпись = "ДлинаНадпись";
	ИначеЕсли Мера = "Ширина" Тогда
		СвойстваМеры.Использовать = "ШиринаИспользовать";
		СвойстваМеры.ЕдиницаИзмерения = "ШиринаЕдиницаИзмерения";
		СвойстваМеры.Числитель = "ШиринаЧислитель";
		СвойстваМеры.Знаменатель = "ШиринаЗнаменатель";
		СвойстваМеры.МожноУказыватьВДокументах = "ШиринаМожноУказыватьВДокументах";
		СвойстваМеры.МожноУказыватьВДокументахСтрока = "ШиринаМожноУказыватьВДокументахСтрока";
		СвойстваМеры.Отступ = "ШиринаОтступ";
		СвойстваМеры.Надпись = "ШиринаНадпись";
	ИначеЕсли Мера = "Вес" Тогда
		СвойстваМеры.Использовать = "ВесИспользовать";
		СвойстваМеры.ЕдиницаИзмерения = "ВесЕдиницаИзмерения";
		СвойстваМеры.Числитель = "ВесЧислитель";
		СвойстваМеры.Знаменатель = "ВесЗнаменатель";
		СвойстваМеры.МожноУказыватьВДокументах = "ВесМожноУказыватьВДокументах";
		СвойстваМеры.МожноУказыватьВДокументахСтрока = "ВесМожноУказыватьВДокументахСтрока";
		СвойстваМеры.Отступ = "ВесОтступ";
		СвойстваМеры.Надпись = "ВесНадпись";
	ИначеЕсли Мера = "Площадь" Тогда
		СвойстваМеры.Использовать = "ПлощадьИспользовать";
		СвойстваМеры.ЕдиницаИзмерения = "ПлощадьЕдиницаИзмерения";
		СвойстваМеры.Числитель = "ПлощадьЧислитель";
		СвойстваМеры.Знаменатель = "ПлощадьЗнаменатель";
		СвойстваМеры.МожноУказыватьВДокументах = "ПлощадьМожноУказыватьВДокументах";
		СвойстваМеры.МожноУказыватьВДокументахСтрока = "ПлощадьМожноУказыватьВДокументахСтрока";
		СвойстваМеры.Отступ = "ПлощадьОтступ";
		СвойстваМеры.Надпись = "ПлощадьНадпись";
	Иначе
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не определены свойства меры %1'"), Мера);
	КонецЕсли;
	
	Возврат СвойстваМеры;
	
КонецФункции

&НаСервере
Процедура УстановитьЕдиницуИзмеренияПоУмолчанию(Мера)
	#Если НЕ МобильныйАвтономныйСервер Тогда
	Если ЭтаФорма[Мера + "ЕдиницаИзмерения"].Пустая() Тогда
		ЭтаФорма[Мера + "ЕдиницаИзмерения"] = НоменклатураСервер.ЕдиницаИзмеренияПоУмолчанию(Мера);
	КонецЕсли;
	#КонецЕсли
КонецПроцедуры

#КонецОбласти