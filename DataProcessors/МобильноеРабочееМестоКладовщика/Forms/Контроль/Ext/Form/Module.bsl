
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивСтрок = Параметры.МассивСтрок;
	Склад       = Параметры.Склад;
	Помещение   = Параметры.Помещение;
	ПоказыватьФотоТоваров = Параметры.ПоказыватьФотоТоваров;
	
	ЗаполнитьФорму(МассивСтрок);
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Описание = Новый ОписаниеОповещения("ЗавершитьПодтверждение", ЭтаФорма);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Номенклатура", Элемент.ТекущиеДанные.Номенклатура);
	ПараметрыФормы.Вставить("Характеристика", Элемент.ТекущиеДанные.Характеристика);
	ПараметрыФормы.Вставить("Серия", Элемент.ТекущиеДанные.Серия);
	ПараметрыФормы.Вставить("Назначение", Элемент.ТекущиеДанные.Назначение);
	ПараметрыФормы.Вставить("Упаковка", Элемент.ТекущиеДанные.Упаковка);
	ПараметрыФормы.Вставить("Количество", Элемент.ТекущиеДанные.КоличествоУпаковокФакт);
	ПараметрыФормы.Вставить("Режим", "РедактированиеПересчет");
	ПараметрыФормы.Вставить("НомерСтроки", Элемент.ТекущаяСтрока);
	ПараметрыФормы.Вставить("Склад", Склад);
	ПараметрыФормы.Вставить("Помещение", Помещение);
	ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);

	ОткрытьФорму(
	"Обработка.МобильноеРабочееМестоКладовщика.Форма.КарточкаТовара",ПараметрыФормы,
	ЭтаФорма,,,,Описание,
	РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьПриемку(Команда)
	
	МассивСтрок = Новый Массив;
	Для Каждого Строка Из Товары Цикл

		
		СтруктураЗаполнения = ПолучитьСтруктуруЗаполненияКонтроля();
		ЗаполнитьЗначенияСвойств(СтруктураЗаполнения, Строка);
		МассивСтрок.Добавить(СтруктураЗаполнения);
		
	КонецЦикла;
	
	Закрыть(МассивСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура Сканировать(Команда)
	
	СтандартнаяОбработка = Ложь;
	
	Описание = Новый ОписаниеОповещения("РезультатСканирования", ЭтаФорма);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипПоиска", "ПоискНоменклатуры");
	ПараметрыФормы.Вставить("Склад", Склад);
	ПараметрыФормы.Вставить("Помещение", Помещение);
	
	ОткрытьФорму(
	"Обработка.МобильноеРабочееМестоКладовщика.Форма.СканированиеШтрихкода",ПараметрыФормы,
	ЭтаФорма,,,,Описание,
	РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыКоличествоУпаковокРазница.Имя);
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Товары.КоличествоУпаковокРазница");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = 0;
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПроблема); 
	
	ОбновитьПодсказку();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьФорму(МассивСтрок)
	
	Для Каждого Строка Из МассивСтрок Цикл
		
		НоваяСтрока = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.КоличествоУпаковокРазница = НоваяСтрока.КоличествоУпаковок;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьПодтверждение(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗавершитьПодтверждениеНаСервере(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ЗавершитьПодтверждениеНаСервере(Результат)
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Номенклатура", Результат.Номенклатура);
	СтруктураПоиска.Вставить("Характеристика", Результат.Характеристика);
	СтруктураПоиска.Вставить("Серия", Результат.Серия);
	СтруктураПоиска.Вставить("Упаковка", Результат.Упаковка);
	СтруктураПоиска.Вставить("Назначение", Результат.Назначение);
	
	КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Результат.Упаковка, Результат.Номенклатура);
	
	РезультатПоиска = Товары.НайтиСтроки(СтруктураПоиска);
	Если РезультатПоиска.Количество() > 0 Тогда
		
		Если Результат.Режим = "Пересчет" Тогда
			РезультатПоиска[0].КоличествоУпаковокФакт = РезультатПоиска[0].КоличествоУпаковокФакт + Результат.КоличествоУпаковок;
		Иначе
			РезультатПоиска[0].КоличествоУпаковокФакт = Результат.КоличествоУпаковок;
		КонецЕсли;
		
		РезультатПоиска[0].КоличествоФакт = РезультатПоиска[0].КоличествоУпаковокФакт * КоэффициентУпаковки;
		
		РезультатПоиска[0].КоличествоУпаковокРазница = РезультатПоиска[0].КоличествоУпаковок - РезультатПоиска[0].КоличествоУпаковокФакт;
		РезультатПоиска[0].КоличествоРазница = РезультатПоиска[0].КоличествоУпаковокРазница * КоэффициентУпаковки;
		
	КонецЕсли; 
	
	ОбновитьПодсказку();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаИнформация"] Тогда
		
		СтандартнаяОбработка = Ложь;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаКПоступлению"];
		
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьПодсказку()
	
	План = Товары.Итог("КоличествоУпаковок");
	Факт = Товары.Итог("КоличествоУпаковокФакт");
	Разница = План - Факт;
	
	Элементы.ЗаголовокПодсказкаФакт.Заголовок = Факт;
	Элементы.ЗаголовокПодсказкаПлан.Заголовок = План;
	Элементы.ЗаголовокПодсказкаРазница.Заголовок = Разница;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСтруктуруЗаполненияКонтроля()
	
	Структура = Новый Структура;
	Структура.Вставить("Номенклатура");
	Структура.Вставить("Характеристика");
	Структура.Вставить("Серия");
	Структура.Вставить("Упаковка");
	Структура.Вставить("КоличествоУпаковокФакт");
	Структура.Вставить("Назначение");
	Структура.Вставить("Действие");
	
	Возврат Структура;
	
КонецФункции

&НаКлиенте
Процедура РезультатСканирования(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Описание = Новый ОписаниеОповещения("ЗавершитьПодтверждение", ЭтаФорма);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Номенклатура", Результат.Номенклатура);
	ПараметрыФормы.Вставить("Характеристика", Результат.Характеристика);
	Если Результат.Свойство("Серия") Тогда
		ПараметрыФормы.Вставить("Серия", Результат.Серия);
	Иначе
		ПараметрыФормы.Вставить("Серия", ПустаяСерия);
	КонецЕсли;
	ПараметрыФормы.Вставить("Упаковка", Результат.Упаковка);
	ПараметрыФормы.Вставить("Количество", 1);
	ПараметрыФормы.Вставить("Режим", "Пересчет");
	ПараметрыФормы.Вставить("НомерСтроки", 0);
	ПараметрыФормы.Вставить("Склад", Склад);
	ПараметрыФормы.Вставить("Помещение", Помещение);
	
	ОткрытьФорму(
	"Обработка.МобильноеРабочееМестоКладовщика.Форма.КарточкаТовара",ПараметрыФормы,
	ЭтаФорма,,,,Описание,
	РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры


#КонецОбласти
