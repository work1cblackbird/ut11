#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	ЯчейкаОтбор = Параметры.ЯчейкаОтбор;
	Склад  = Параметры.Склад;
	
	Если Параметры.Свойство("Товары") Тогда
		
		Если Параметры.Товары.Количество() > 0 Тогда
			ЯчейкаОтбор = Параметры.Товары[0].ЯчейкаОтбор;
			ЯчейкаРазмещение = Параметры.Товары[0].ЯчейкаРазмещение;
		КонецЕсли;
		
		Для Каждого Строка Из Параметры.Товары Цикл
			
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Элементы.КарточкаТовара.Видимость Тогда
		
		Отказ = Истина;
		СтандартнаяОбработка = Ложь;
		
		Элементы.КРазмещению.Видимость = Истина;
		Элементы.КарточкаТовара.Видимость = Ложь;
		Элементы.Сканировать.Видимость = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Элементы.КРазмещению.Видимость = Ложь;
	Элементы.КарточкаТовара.Видимость = Истина;
	Элементы.Сканировать.Видимость = Ложь;
	
	Номенклатура       = Элемент.ТекущиеДанные.Номенклатура;
	Характеристика     = Элемент.ТекущиеДанные.Характеристика;
	Серия              = Элемент.ТекущиеДанные.Серия;
	Назначение         = Элемент.ТекущиеДанные.Назначение;
	Упаковка           = Элемент.ТекущиеДанные.Упаковка;
	КоличествоУпаковок = Элемент.ТекущиеДанные.КоличествоУпаковок;
	
	Если ЯчейкаОтбор <> Элемент.ТекущиеДанные.Ячейка Тогда
		
		ЯчейкаРазмещение = Элемент.ТекущиеДанные.ЯчейкаЗамена;
		Элементы.Ячейка.Видимость = Ложь;
		Элементы.ЯчейкаЗамена.Видимость = Истина;
		
	КонецЕсли;
	
	Элементы.Серия.Видимость = ЗначениеЗаполнено(Серия);
	Элементы.Характеристика.Видимость = ЗначениеЗаполнено(Характеристика);
	
КонецПроцедуры

&НаКлиенте
Процедура Подтвердить(Команда)
	
	Элементы.КРазмещению.Видимость = Истина;
	Элементы.КарточкаТовара.Видимость = Ложь;
	Элементы.Сканировать.Видимость = Истина;
	Элементы.Ячейка.Видимость = Истина;
	Элементы.ЯчейкаЗамена.Видимость = Ложь;
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Номенклатура", Номенклатура);
	СтруктураПоиска.Вставить("Характеристика", Характеристика);
	СтруктураПоиска.Вставить("Серия", Серия);
	СтруктураПоиска.Вставить("Упаковка", Упаковка);
	
	МассивНайденных = Товары.НайтиСтроки(СтруктураПоиска);
	Для Каждого Строка Из МассивНайденных Цикл
		Строка.КоличествоУпаковокРазмещено = КоличествоУпаковок;
		Если ЗначениеЗаполнено(ЯчейкаРазмещение) Тогда
			Строка.ЯчейкаЗамена = ЯчейкаРазмещение;
		КонецЕсли;
	КонецЦикла;
	
	ЯчейкаРазмещение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура Переместить(Команда)
	
	МассивРазмещений = Новый Массив;
	
	Для Каждого Строка Из Товары Цикл
		
		Структура = СтруктураТоваров();
		
		ЗаполнитьЗначенияСвойств(Структура, Строка);
		Структура.Вставить("КоличествоУпаковок", Строка.КоличествоУпаковокРазмещено);
		
		МассивРазмещений.Добавить(Структура);
		
	КонецЦикла;
	
	РезультатСборки = Новый Структура;
	РезультатСборки.Вставить("Ячейка", ЯчейкаОтбор);
	РезультатСборки.Вставить("МассивРазмещений", МассивРазмещений);
	
	Закрыть(РезультатСборки);
	
КонецПроцедуры

&НаКлиенте
Процедура УменьшитьКоличество(Команда)
	
	КоличествоУпаковок = КоличествоУпаковок - 1;
	
	Если КоличествоУпаковок < 0 Тогда
		КоличествоУпаковок = 0 ;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УвеличитьКоличество(Команда)
	КоличествоУпаковок = КоличествоУпаковок + 1;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Элементы.Количество.Шрифт = Новый Шрифт(, 16, Истина, , , );
	
КонецПроцедуры

&НаСервере
Функция СтруктураТоваров()
	
	Структура = Новый Структура;
	Структура.Вставить("Номенклатура","");
	Структура.Вставить("Характеристика","");
	Структура.Вставить("Серия","");
	Структура.Вставить("Упаковка","");
	Структура.Вставить("КоличествоУпаковок","");
	Структура.Вставить("Ячейка","");
	Структура.Вставить("ЯчейкаЗамена","");
	
	Возврат Структура;
	
КонецФункции

&НаКлиенте
Процедура РезультатЗаменыЯчейки(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЯчейкаРазмещение = Результат;
	Элементы.Ячейка.Видимость = Ложь;
	Элементы.ЯчейкаЗамена.Видимость = Истина;
	
КонецПроцедуры

#КонецОбласти