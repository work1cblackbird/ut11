
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	
	Если Параметры.Режим = "РазмещениеВЯчейку" ИЛИ Параметры.Режим = "РедактированиеРазмещениеПоЯчейкам" 
		ИЛИ Параметры.Режим = "Пересчет" ИЛИ Параметры.Режим = "РедактированиеПересчет" Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СкладскиеЯчейки.Ссылка КАК Ячейка
		|ПОМЕСТИТЬ ВТЯчейки
		|ИЗ
		|	Справочник.СкладскиеЯчейки КАК СкладскиеЯчейки
		|ГДЕ
		|	СкладскиеЯчейки.Владелец = &Склад
		|	И СкладскиеЯчейки.Помещение = &Помещение
		|	И НЕ СкладскиеЯчейки.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТЯчейки.Ячейка КАК Ячейка,
		|	ЕСТЬNULL(ТоварыВЯчейкахОстатки.ВНаличииОстаток, 0) КАК ВНаличииОстаток,
		|	ЕСТЬNULL(ТоварыВЯчейкахОстатки.Упаковка, """") КАК Упаковка
		|ИЗ
		|	ВТЯчейки КАК ВТЯчейки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыВЯчейках.Остатки(
		|				,
		|				Номенклатура = &Номенклатура
		|					И Характеристика = &Характеристика
		|					И Серия = &Серия) КАК ТоварыВЯчейкахОстатки
		|		ПО ВТЯчейки.Ячейка = ТоварыВЯчейкахОстатки.Ячейка
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТоварыВЯчейкахОстатки.ВНаличииОстаток УБЫВ,
		|	ТоварыВЯчейкахОстатки.Ячейка.ПорядокОбхода";
		
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыВЯчейкахОстатки.Ячейка КАК Ячейка,
		|	ТоварыВЯчейкахОстатки.Упаковка КАК Упаковка,
		|	ТоварыВЯчейкахОстатки.ВНаличииОстаток КАК ВНаличииОстаток
		|ИЗ
		|	РегистрНакопления.ТоварыВЯчейках.Остатки(
		|			,
		|			Номенклатура = &Номенклатура
		|				И Характеристика = &Характеристика
		|				И Серия = &Серия
		|				И Ячейка.Владелец = &Склад
		|				И Ячейка.Помещение = &Помещение) КАК ТоварыВЯчейкахОстатки
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТоварыВЯчейкахОстатки.Ячейка.ПорядокОбхода";
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Склад",              Параметры.Склад);
	Запрос.УстановитьПараметр("Помещение",          Параметры.Помещение);
	Запрос.УстановитьПараметр("Номенклатура",       Параметры.Номенклатура);
	Запрос.УстановитьПараметр("Характеристика",     Параметры.Характеристика);
	Запрос.УстановитьПараметр("Серия",              Параметры.Серия);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НоваяСтрока = Ячейки.Добавить();
		НоваяСтрока.Ячейка = ВыборкаДетальныеЗаписи.Ячейка;
		НоваяСтрока.Количество = ВыборкаДетальныеЗаписи.ВНаличииОстаток;
		НоваяСтрока.Упаковка = ВыборкаДетальныеЗаписи.Упаковка;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЯчейки

&НаКлиенте
Процедура ЯчейкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка) 
	
	СтандартнаяОбработка = Ложь;
	Закрыть(Элемент.ТекущиеДанные.Ячейка);
	
КонецПроцедуры

#КонецОбласти
