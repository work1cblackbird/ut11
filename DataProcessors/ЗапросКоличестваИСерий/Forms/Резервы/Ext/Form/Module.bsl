#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = ЗаголовокФормыПоПараметрам();
	
	Упаковка = Параметры.Упаковка;
	Если Не ЗначениеЗаполнено(Упаковка) Тогда
		Упаковка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Номенклатура, "ЕдиницаИзмерения");
	КонецЕсли;
	Элементы.СписокКоличество.Заголовок = СтрШаблон(НСтр("ru = 'Количество, %1'"), Упаковка);
	КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(
		Параметры.Упаковка,
		Параметры.Номенклатура);
		
	ТекстРезервировать = Строка(Перечисления.ВариантыОбеспечения.СоСклада);
	ТекстРезервироватьПоМере = Строка(Перечисления.ВариантыОбеспечения.РезервироватьПоМереПоступления);
	
	Если Не ЗначениеЗаполнено(КоэффициентУпаковки) Тогда
		КоэффициентУпаковки = 1;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список,
		"Номенклатура",
		Параметры.Номенклатура);
		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список,
		"Характеристика",
		Параметры.Характеристика);
		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список,
		"Склад",
		Параметры.Склад);
		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список,
		"Назначение",
		Параметры.Назначение);
		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список,
		"КоэффициентУпаковки",
		КоэффициентУпаковки);
		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список,
		"ТекстРезервировать",
		ТекстРезервировать);
		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список,
		"ТекстРезервироватьПоМере",
		ТекстРезервироватьПоМере);
		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список,
		"ЗаказыНаОтгрузку",
		Параметры.ЗаказыНаОтгрузку);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение( , Элементы.Список.ТекущиеДанные.Заказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЗаголовокФормыПоПараметрам()
	
	Текст = Неопределено;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов") Тогда
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьОбособленноеОбеспечениеЗаказов") Тогда
			
			Если ЗначениеЗаполнено(Параметры.Характеристика) Тогда
				
				Если ЗначениеЗаполнено(Параметры.Назначение) Тогда
					Текст = НСтр("ru = 'Резервы по обособленным заказам (Номенклатура: %1, Характеристика: %2, Склад: %3)'");
				Иначе
					Текст = НСтр("ru = 'Резервы по обособленным заказам (Номенклатура: %1, Характеристика: %2, Склад: %3)'");
				КонецЕсли;
				Текст = СтрШаблон(Текст, Параметры.Номенклатура, Параметры.Характеристика, Параметры.Склад);
				
			Иначе
				
				Если ЗначениеЗаполнено(Параметры.Назначение) Тогда
					Текст = НСтр("ru = 'Резервы по обособленным заказам (Номенклатура: %1, Склад: %2)'");
				Иначе
					Текст = НСтр("ru = 'Резервы по необособленным заказам (Номенклатура: %1, Склад: %2)'");
				КонецЕсли;
				Текст = СтрШаблон(Текст, Параметры.Номенклатура, Параметры.Склад);
				
			КонецЕсли;
			
		Иначе
			
			Если ЗначениеЗаполнено(Параметры.Характеристика) Тогда
				
				Текст = НСтр("ru = 'Резервы по заказам (Номенклатура: %1, Характеристика: %2, Склад: %3)'");
				Текст = СтрШаблон(Текст, Параметры.Номенклатура, Параметры.Характеристика, Параметры.Склад);
				
			Иначе
				
				Текст = НСтр("ru = 'Резервы по заказам (Номенклатура: %1, Склад: %2)'");
				Текст = СтрШаблон(Текст, Параметры.Номенклатура, Параметры.Склад);
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьОбособленноеОбеспечениеЗаказов") Тогда
			
			Если ЗначениеЗаполнено(Параметры.Характеристика) Тогда
				
				Если ЗначениеЗаполнено(Параметры.Назначение) Тогда
					Текст = НСтр("ru = 'Резервы по обособленным заказам (Номенклатура: %1, Характеристика: %2)'");
				Иначе
					Текст = НСтр("ru = 'Резервы по обособленным заказам (Номенклатура: %1, Характеристика: %2)'");
				КонецЕсли;
				Текст = СтрШаблон(Текст, Параметры.Номенклатура, Параметры.Характеристика);
				
			Иначе
				
				Если ЗначениеЗаполнено(Параметры.Назначение) Тогда
					Текст = НСтр("ru = 'Резервы по обособленным заказам (Номенклатура: %1)'");
				Иначе
					Текст = НСтр("ru = 'Резервы по необособленным заказам (Номенклатура: %1)'");
				КонецЕсли;
				Текст = СтрШаблон(Текст, Параметры.Номенклатура);
				
			КонецЕсли;
			
		Иначе
			
			Если ЗначениеЗаполнено(Параметры.Характеристика) Тогда
				
				Текст = НСтр("ru = 'Резервы по заказам (Номенклатура: %1, Характеристика: %2)'");
				Текст = СтрШаблон(Текст, Параметры.Номенклатура, Параметры.Характеристика);
				
			Иначе
				
				Текст = НСтр("ru = 'Резервы по заказам (Номенклатура: %1)'");
				Текст = СтрШаблон(Текст, Параметры.Номенклатура);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

#КонецОбласти
