#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЛичныйКабинет = Новый ФорматированнаяСтрока(
		НСтр("ru = 'Личный кабинет'"), , ЦветаСтиля.ЦветГиперссылкиГосИС, , "ПерейтиВЛичныйКабинет");
	Если Константы.РежимРаботыСТестовымКонтуромЗЕРНО.Получить() Тогда
		ЛичныйКабинетТест = Новый ФорматированнаяСтрока(
			НСтр("ru = '(Тестовый контур)'"), , ЦветаСтиля.ЦветГиперссылкиГосИС, , "ПерейтиВЛичныйКабинетТест");
		ПерейтиВЛичныйКабинет = Новый ФорматированнаяСтрока(ЛичныйКабинет, " ", ЛичныйКабинетТест);
	Иначе
		ПерейтиВЛичныйКабинет = ЛичныйКабинет;
	КонецЕсли;
	
	ЦветГиперссылки = ЦветаСтиля.ЦветГиперссылкиГосИС;
	ИнтеграцияИС.УстановитьВидимостьКомандыВыполнитьОбмен(ЭтотОбъект, Метаданные.ОпределяемыеТипы.ДокументыЗЕРНО, "ВыполнитьОбмен");
	
	ВосстановитьНастройкиФормы();
	ОбновитьСпискиДокументов();
	ПараметрыЛогирования = ПолучитьПараметрыЛогирования();
	
	НастроитьЭлементыФормыНаСервере();
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьПредставлениеЛогирования();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПерейтиВЛичныйКабинет" Тогда
		СтандартнаяОбработка = Ложь;
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://zerno.mcx.gov.ru/");
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ПерейтиВЛичныйКабинетТест" Тогда
		СтандартнаяОбработка = Ложь;
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://demo-zerno.fors.ru/");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = РаботаСКлассификаторамиКлиент.ИмяСобытияОповещенияОЗагрузки() Тогда
		ОтобразитьСостояниеОбмена();
	ИначеЕсли ИмяСобытия = "ЗЕРНО.ОбновитьКоличествоСообщенийКПодписанию" Тогда
		ОбновитьКоличествоСообщенийКПодписанию();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область ОтборПоОрганизации

&НаКлиенте
Процедура ОтборОрганизацииПриИзменении(Элемент)
	
	ИнтеграцияЗЕРНОКлиент.ОбработатьВыборОрганизаций(ЭтотОбъект, Организации, Ложь, "Отбор");
	ОбновитьСпискиДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнтеграцияЗЕРНОКлиент.ОткрытьФормуВыбораОрганизаций(ЭтотОбъект, "Отбор", , ОповещениеВыбораОрганизаций());
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацииОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнтеграцияЗЕРНОКлиент.ОбработатьВыборОрганизаций(ЭтотОбъект, Неопределено, Ложь, "Отбор");
	ОбновитьСпискиДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацииОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнтеграцияЗЕРНОКлиент.ОбработатьВыборОрганизаций(ЭтотОбъект, ВыбранноеЗначение, Ложь, "Отбор");
	ОбновитьСпискиДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	ИнтеграцияЗЕРНОКлиент.ОбработатьВыборОрганизаций(ЭтотОбъект, Организация, Ложь, "Отбор");
	ОбновитьСпискиДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнтеграцияЗЕРНОКлиент.ОткрытьФормуВыбораОрганизаций(ЭтотОбъект, "Отбор", , ОповещениеВыбораОрганизаций());
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнтеграцияЗЕРНОКлиент.ОбработатьВыборОрганизаций(ЭтотОбъект, Неопределено, Ложь, "Отбор");
	ОбновитьСпискиДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнтеграцияЗЕРНОКлиент.ОбработатьВыборОрганизаций(ЭтотОбъект, ВыбранноеЗначение, Ложь, "Отбор");
	ОбновитьСпискиДокументов();
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПредставлениеЛогированияЗапросовОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ОбработкаНавигационнойСсылкиЛогирования(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеЛогированияЗапросовРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ОбработкаНавигационнойСсылкиЛогирования(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныйПриИзменении(Элемент)
	
	ОбновитьСпискиДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьСостояниеОбменаНажатие(Элемент)
	
	Если ЕстьТолькоСообщенияОжидающиеОтправки Тогда
		
		ПараметрыОткрытияФормы = Неопределено;
		Если Организации.Количество() > 0 Тогда
			
			Отбор = Новый Структура;
			Отбор.Вставить("Организация", Организации.ВыгрузитьЗначения());
			
			ПараметрыОткрытияФормы = Новый Структура;
			ПараметрыОткрытияФормы.Вставить("Отбор", Отбор);
			
		КонецЕсли;
		
		ОткрытьФорму("РегистрСведений.ОчередьСообщенийЗЕРНО.ФормаСписка", ПараметрыОткрытияФормы, ЭтотОбъект);
		
	Иначе
		
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("Организации", Организации);
		
		ОткрытьФорму(
			"Обработка.ПанельОбменЗЕРНО.Форма.СостоянияОбмена",
			ПараметрыОткрытияФормы, ЭтотОбъект,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область ДокументыПодсистемы

#Область ПогашениеСДИЗ

&НаКлиенте
Процедура ОткрытьПогашениеСДИЗ(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ПогашениеСДИЗЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ПогашениеСДИЗЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеДействияИлиОжидания");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПогашениеСДИЗОформите(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ПогашениеСДИЗЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеСпискаРаспоряжений");
	ОткрытьФормуСпискаДокументов("Документ.ПогашениеСДИЗЗЕРНО.Форма.ФормаСпискаДокументов", Неопределено, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПогашениеСДИЗОтработайте(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ПогашениеСДИЗЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ПогашениеСДИЗЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеДействия");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПогашениеСДИЗОжидайте(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ПогашениеСДИЗЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ПогашениеСДИЗЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеОжидания");
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеПартий

&НаКлиенте
Процедура ОткрытьФормированиеПартий(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ФормированиеПартийЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ФормированиеПартийЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеДействияИлиОжидания");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормированиеПартийОформите(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ФормированиеПартийЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеСпискаРаспоряжений");
	ОткрытьФормуСпискаДокументов("Документ.ФормированиеПартийЗЕРНО.Форма.ФормаСпискаДокументов", Неопределено, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормированиеПартийОтработайте(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ФормированиеПартийЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ФормированиеПартийЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеДействия");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормированиеПартийОжидайте(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ФормированиеПартийЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ФормированиеПартийЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеОжидания");
	
КонецПроцедуры

#КонецОбласти

#Область СписаниеПартий

&НаКлиенте
Процедура ОткрытьСписаниеПартий(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.СписаниеПартийЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.СписаниеПартийЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеДействияИлиОжидания");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписаниеПартийОформите(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.СписаниеПартийЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеСпискаРаспоряжений");
	ОткрытьФормуСпискаДокументов("Документ.СписаниеПартийЗЕРНО.Форма.ФормаСпискаДокументов", Неопределено, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписаниеПартийОтработайте(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.СписаниеПартийЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.СписаниеПартийЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеДействия");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписаниеПартийОжидайте(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.СписаниеПартийЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.СписаниеПартийЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеОжидания");
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеПартийИзДругихПартий

&НаКлиенте
Процедура ОткрытьФормированиеПартийИзДругихПартий(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ФормированиеПартийИзДругихПартийЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ФормированиеПартийИзДругихПартийЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеДействияИлиОжидания");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормированиеПартийИзДругихПартийОформите(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ФормированиеПартийИзДругихПартийЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеСпискаРаспоряжений");
	ОткрытьФормуСпискаДокументов("Документ.ФормированиеПартийИзДругихПартийЗЕРНО.Форма.ФормаСпискаДокументов", Неопределено, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормированиеПартийИзДругихПартийОтработайте(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ФормированиеПартийИзДругихПартийЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ФормированиеПартийИзДругихПартийЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеДействия");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормированиеПартийИзДругихПартийОжидайте(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ФормированиеПартийИзДругихПартийЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ФормированиеПартийИзДругихПартийЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеОжидания");
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеПартийПриПроизводстве

&НаКлиенте
Процедура ОткрытьФормированиеПартийПриПроизводстве(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ФормированиеПартийПриПроизводствеЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ФормированиеПартийПриПроизводствеЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеДействияИлиОжидания");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормированиеПартийПриПроизводствеОформите(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ФормированиеПартийПриПроизводствеЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеСпискаРаспоряжений");
	ОткрытьФормуСпискаДокументов("Документ.ФормированиеПартийПриПроизводствеЗЕРНО.Форма.ФормаСпискаДокументов", Неопределено, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормированиеПартийПриПроизводствеОтработайте(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ФормированиеПартийПриПроизводствеЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ФормированиеПартийПриПроизводствеЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеДействия");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормированиеПартийПриПроизводствеОжидайте(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ФормированиеПартийПриПроизводствеЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ФормированиеПартийПриПроизводствеЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеОжидания");
	
КонецПроцедуры

#КонецОбласти

#Область ЗапросОстатковПартий

&НаКлиенте
Процедура ОткрытьЗапросОстатковПартий(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ЗапросОстатковПартийЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ЗапросОстатковПартийЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеДействияИлиОжидания");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗапросОстатковПартийОформите(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ЗапросОстатковПартийЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеСпискаРаспоряжений");
	ОткрытьФормуСпискаДокументов("Документ.ЗапросОстатковПартийЗЕРНО.Форма.ФормаСпискаДокументов", Неопределено, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗапросОстатковПартийОтработайте(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ЗапросОстатковПартийЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ЗапросОстатковПартийЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеДействия");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗапросОстатковПартийОжидайте(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ЗапросОстатковПартийЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ЗапросОстатковПартийЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеОжидания");
	
КонецПроцедуры

#КонецОбласти

#Область ОформлениеСДИЗ

&НаКлиенте
Процедура ОткрытьОформлениеСДИЗ(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ОформлениеСДИЗЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ОформлениеСДИЗЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеДействияИлиОжидания");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОформлениеСДИЗОформите(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ОформлениеСДИЗЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеСпискаРаспоряжений");
	ОткрытьФормуСпискаДокументов("Документ.ОформлениеСДИЗЗЕРНО.Форма.ФормаСпискаДокументов", Неопределено, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОформлениеСДИЗОтработайте(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ОформлениеСДИЗЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ОформлениеСДИЗЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеДействия");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОформлениеСДИЗОжидайте(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ОформлениеСДИЗЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ОформлениеСДИЗЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеОжидания");
	
КонецПроцедуры

#КонецОбласти

#Область ВнесениеСведенийОСобранномУрожае

&НаКлиенте
Процедура ОткрытьВнесениеСведенийОСобранномУрожае(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеДействияИлиОжидания");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВнесениеСведенийОСобранномУрожаеОформите(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеСпискаРаспоряжений");
	ОткрытьФормуСпискаДокументов("Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО.Форма.ФормаСпискаДокументов", Неопределено, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВнесениеСведенийОСобранномУрожаеОтработайте(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеДействия");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВнесениеСведенийОСобранномУрожаеОжидайте(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО.Форма.ФормаСпискаДокументов.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаДокументов("Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО.Форма.ФормаСпискаДокументов", "ВсеТребующиеОжидания");
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОткрытьУчитываемыеВидыПродукции(Команда)
	ПараметрыФормы = Новый Структура("Организации", Организации);
	ОткрытьФорму("Обработка.ПанельАдминистрированияЗЕРНО.Форма.УчитываемыеВидыПродукции", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор)
КонецПроцедуры

#КонецОбласти

#Область ОбъектыПодсистемы

&НаКлиенте
Процедура ПанельАдминистрирования(Команда)
	
	ПараметрыФормы = Новый Структура("Организации", Организации);
	ОткрытьФорму("Обработка.ПанельАдминистрированияЗЕРНО.Форма", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиЭлектроннойПодписиИШифрования(Команда)
	
	ОткрытьФорму("ОбщаяФорма.НастройкиЭлектроннойПодписиИШифрования",, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКлассификаторы(Команда)
	
	ОткрытьФорму("Обработка.ПанельОбменЗЕРНО.Форма.ВыборКлассификатора",, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСправочникСДИЗ(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Справочник.СДИЗЗЕРНО.Форма.ФормаСписка.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаСправочников(
		"Справочник.СДИЗЗЕРНО.Форма.ФормаСписка",
		ПредопределенноеЗначение("Перечисление.СтатусыСДИЗЗЕРНО.Оформлен"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСправочникРеестрМестФормированияПартий(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Справочник.РеестрМестФормированияПартийЗЕРНО.Форма.ФормаСписка.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаСправочников(
		"Справочник.РеестрМестФормированияПартийЗЕРНО.Форма.ФормаСписка",
		ПредопределенноеЗначение("Перечисление.СтатусыМестФормированияПартийЗЕРНО.Наполняется"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСправочникРезультатыИсследований(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Справочник.РезультатыИсследованийЗЕРНО.Форма.ФормаСписка.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаСправочников(
		"Справочник.РезультатыИсследованийЗЕРНО.Форма.ФормаСписка",
		ПредопределенноеЗначение("Перечисление.СтатусыРезультатовИсследованийЗЕРНО.Подписан"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСправочникАктыОтбораПроб(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Справочник.АктыОтбораПробЗЕРНО.Форма.ФормаСписка.ОткрытиеОсновногоСписка");
	ОткрытьФормуСпискаСправочников(
		"Справочник.АктыОтбораПробЗЕРНО.Форма.ФормаСписка",
		ПредопределенноеЗначение("Перечисление.СтатусыАктовОтбораПробЗЕРНО.Подписан"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОстаткиПартий(Команда)
	
	ОткрытьФормуСпискаСправочников(
		"РегистрНакопления.ОстаткиПартийЗЕРНО.Форма.ФормаОстатков");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРеестрЭлеваторов(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Справочник.РеестрЭлеваторовЗЕРНО.Форма.ФормаСписка.ОткрытиеСпискаЭлеваторов");
	
	ОткрытьФормуСпискаСправочников("Справочник.РеестрЭлеваторовЗЕРНО.Форма.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокКлючейАдресов(Команда)
	
	ДополнительныеПараметры = Новый Структура("ЗаголовокФормы", НСтр("ru = 'Местоположения партий'"));
	ОткрытьФормуСпискаСправочников(
		"Справочник.КлючиАдресовЗЕРНО.Форма.ФормаСписка",,
		ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокКлючейРеквизитовОрганизаций(Команда)
	
	ДополнительныеПараметры = Новый Структура("ЗаголовокФормы", НСтр("ru = 'Организации, осуществляющие продажу/хранение зерна'"));
	ОткрытьФормуСпискаСправочников(
		"Справочник.КлючиРеквизитовОрганизацийЗЕРНО.Форма.ФормаСписка",,
		ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСообщенияКПодписанию(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("РегистрСведений.ОчередьСообщенийЗЕРНО.Форма.СообщенияКПодписаниюЗЕРНО");
	
	ОткрытьФормуСпискаСправочников("РегистрСведений.ОчередьСообщенийЗЕРНО.Форма.СообщенияКПодписаниюЗЕРНО");
	
КонецПроцедуры

#КонецОбласти

#Область ПрикладныеОбъекты

&НаКлиенте
Процедура ОткрытьВидыНоменклатуры(Команда)
	
	ИнтеграцияИСКлиентПереопределяемый.ОткрытьФормуСпискаВидыНоменклатуры(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНоменклатуру(Команда)

	ИнтеграцияИСКлиентПереопределяемый.ОткрытьФормуСпискаНоменклатуры(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗемельныеУчастки(Команда)
	
	ИнтеграцияИСКлиент.ОткрытьФормуСпискаЗемельныхУчастков(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДоговорыХраненияПартий(Команда)
	
	ОткрытьФорму("Справочник.ДоговорыХраненияПартийЗЕРНО.ФормаСписка",, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКодыРАФПЗЕРНО(Команда)
	
	ОткрытьФорму("РегистрСведений.КодыРАФПЗЕРНО.ФормаСписка",, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьОбменОбработкаОжидания()
	ИнтеграцияЗЕРНОСлужебныйКлиент.ПродолжитьВыполнениеОбмена(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьСпискиДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбмен(Команда)
	
	ИнтеграцияЗЕРНОКлиент.ВыполнитьОбмен(
		ЭтотОбъект,
		ИнтеграцияЗЕРНОКлиент.ОрганизацииДляОбмена(ЭтотОбъект));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДействияСФормамиДокументов

&НаКлиенте
Процедура ОткрытьФормуСпискаДокументов(ИмяФормы,
                                       ДальнейшееДействие,
                                       ОткрытьРаспоряжения = Ложь)
	
	СтруктураБыстрогоОтбора = Новый Структура();
	ПараметрыФормы = Новый Структура;
	
	Если ОткрытьРаспоряжения Тогда
		ПараметрыФормы.Вставить("ОткрытьРаспоряжения", Истина);
	Иначе
		СтруктураБыстрогоОтбора.Вставить("ДальнейшееДействие", ДальнейшееДействие);
	КонецЕсли;
	
	ИмяПоляОтветственный = "Ответственный";
	ИмяПоляОрганизации = "Организации";
	ИмяПоляОрганизация = "Организация";
	ИмяПоляПредставления = "ОрганизацииПредставление";
	
	СтруктураБыстрогоОтбора.Вставить(ИмяПоляОтветственный, Ответственный);
	СтруктураБыстрогоОтбора.Вставить(ИмяПоляОрганизации,   Организации);
	СтруктураБыстрогоОтбора.Вставить(ИмяПоляОрганизация,   Организация);
	СтруктураБыстрогоОтбора.Вставить(ИмяПоляПредставления, ОрганизацииПредставление);
	
	ПараметрыФормы.Вставить("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора);
	ОткрытьФорму(ИмяФормы, ПараметрыФормы);

КонецПроцедуры

#КонецОбласти

#Область ДействияСФормамиСправочников

&НаКлиенте
Процедура ОткрытьФормуСпискаСправочников(ИмяФормы, Статус = Неопределено, ДополнительныеПараметры = Неопределено)
	
	СтруктураБыстрогоОтбора = Новый Структура();
	ПараметрыФормы = Новый Структура;
	
	ИмяПоляОрганизации   = "Организации";
	ИмяПоляОрганизация   = "Организация";
	ИмяПоляПредставления = "ОрганизацииПредставление";
	
	СтруктураБыстрогоОтбора.Вставить(ИмяПоляОрганизации,   Организации);
	СтруктураБыстрогоОтбора.Вставить(ИмяПоляОрганизация,   Организация);
	СтруктураБыстрогоОтбора.Вставить(ИмяПоляПредставления, ОрганизацииПредставление);
	
	СтруктураБыстрогоОтбора.Вставить("Статус", Статус);
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		Для Каждого КлючИЗначение Из ДополнительныеПараметры Цикл
			ПараметрыФормы.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
	КонецЕсли;
	
	ПараметрыФормы.Вставить("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора);
	ОткрытьФорму(ИмяФормы, ПараметрыФормы);

КонецПроцедуры

#КонецОбласти

#Область ДействияСНастройкамиФормы

&НаСервере
Процедура СохранитьНастройкиФормы()
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ПанельОбменЗЕРНО", "Ответственный",            Ответственный);
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ПанельОбменЗЕРНО", "Организация",              Организация);
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ПанельОбменЗЕРНО", "ОрганизацииПредставление", ОрганизацииПредставление);
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ПанельОбменЗЕРНО", "Организации",              Организации);
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройкиФормы()
	
	Организации              = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"ПанельОбменЗЕРНО", "Организации");
	Ответственный            = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"ПанельОбменЗЕРНО", "Ответственный", Ответственный);
	Организация              = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"ПанельОбменЗЕРНО", "Организация", Организация);
	ОрганизацииПредставление = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"ПанельОбменЗЕРНО", "ОрганизацииПредставление", ОрганизацииПредставление);
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеЭлементовФормы

&НаСервере
Процедура ОбновитьСпискиДокументов()
	
	ОбновитьСпискиДокументовПодсистемы();
	ОтобразитьСостояниеОбмена();
	ОбновитьКоличествоСообщенийКПодписанию();
	СобытияФормЗЕРНО.ДоступностьКомандыВыполнитьОбмен(ЭтотОбъект, "ВыполнитьОбмен");
	
	НастроитьНадписьУчитываемыеВидыПродукции();
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормыНаСервере()
	
	Элементы.ОткрытьВидыНоменклатуры.Видимость = ИнтеграцияИС.ВидыНоменклатурыИспользуются();
	
	НастроитьЭлементФормыРеестраЭлеваторов();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементФормыРеестраЭлеваторов()
	
	ДанныеТекущейВерсии = ИнтеграцияЗЕРНО.ДанныеЗагруженнойВерсииКлассификатора(Справочники.РеестрЭлеваторовЗЕРНО.ПустаяСсылка());
	
	ДанныеПодсказкиРеестраЭлеваторов = Новый Массив();
	ДанныеПодсказкиРеестраЭлеваторов.Добавить(
		Новый ФорматированнаяСтрока(
		НСтр("ru='Реестр организаций, осуществляющих в качестве предпринимательской деятельности хранение зерна и оказывающих связанные с хранением услуги'")));
	ДанныеПодсказкиРеестраЭлеваторов.Добавить(Символы.ПС);
	
	Если ЗначениеЗаполнено(ДанныеТекущейВерсии.Версия) Тогда
		ДанныеПодсказкиРеестраЭлеваторов.Добавить(
			Новый ФорматированнаяСтрока(
				СтрШаблон(НСтр("ru='Текущая версия %1 (%2)'"),
				ДанныеТекущейВерсии.Версия,
				ДанныеТекущейВерсии.ДатаОбновления)));
	КонецЕсли;
	
	Элементы.ОткрытьРеестрЭлеваторов.РасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(ДанныеПодсказкиРеестраЭлеваторов);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСпискиДокументовПодсистемы()
	
	// Заполним таблицу документов.
	ТаблицаДокументы = Новый ТаблицаЗначений;
	ТаблицаДокументы.Колонки.Добавить("Метаданные");
	ТаблицаДокументы.Колонки.Добавить("Заголовок");
	ТаблицаДокументы.Колонки.Добавить("Оформите");
	ТаблицаДокументы.Колонки.Добавить("Отработайте");
	ТаблицаДокументы.Колонки.Добавить("Ожидайте");
	ТаблицаДокументы.Колонки.Добавить("ЕстьПравоЧтение");
	ТаблицаДокументы.Колонки.Добавить("ЕстьПравоДобавление");
	ТаблицаДокументы.Колонки.Добавить("ЕстьПравоРедактирование");
	
	ДобавитьДокумент(
		ТаблицаДокументы,
		Метаданные.Документы.ПогашениеСДИЗЗЕРНО,
		НСтр("ru='Погашение СДИЗ'"),
		Истина,  // Оформите
		Истина,  // Отработайте
		Истина); // Ожидайте
	
	ДобавитьДокумент(
		ТаблицаДокументы,
		Метаданные.Документы.ФормированиеПартийЗЕРНО,
		НСтр("ru='Формирование партий'"),
		Истина,  // Оформите
		Истина,  // Отработайте
		Истина); // Ожидайте
	
	ДобавитьДокумент(
		ТаблицаДокументы,
		Метаданные.Документы.ФормированиеПартийИзДругихПартийЗЕРНО,
		НСтр("ru='Формирование партий из других партий'"),
		Ложь,    // Оформите
		Истина,  // Отработайте
		Истина); // Ожидайте
		
	ДобавитьДокумент(
		ТаблицаДокументы,
		Метаданные.Документы.ФормированиеПартийПриПроизводствеЗЕРНО,
		НСтр("ru='Формирование партий продуктов переработки зерна при производстве'"),
		Истина,  // Оформите
		Истина,  // Отработайте
		Истина); // Ожидайте
	
	ДобавитьДокумент(
		ТаблицаДокументы,
		Метаданные.Документы.СписаниеПартийЗЕРНО,
		НСтр("ru='Списание партий'"),
		Истина,  // Оформите
		Истина,  // Отработайте
		Истина); // Ожидайте
	
	ДобавитьДокумент(
		ТаблицаДокументы,
		Метаданные.Документы.ЗапросОстатковПартийЗЕРНО,
		НСтр("ru='Запросы остатков партий'"),
		Ложь,  // Оформите
		Истина,  // Отработайте
		Истина); // Ожидайте
	
	ДобавитьДокумент(
		ТаблицаДокументы,
		Метаданные.Документы.ОформлениеСДИЗЗЕРНО,
		НСтр("ru='Оформление СДИЗ'"),
		Истина,  // Оформите
		Истина,  // Отработайте
		Истина); // Ожидайте
	
	ДобавитьДокумент(
		ТаблицаДокументы,
		Метаданные.Документы.ВнесениеСведенийОСобранномУрожаеЗЕРНО,
		НСтр("ru='Внесение сведений о собранном урожае'"),
		Истина,  // Оформите
		Истина,  // Отработайте
		Истина); // Ожидайте
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организации",              Организации);
	Запрос.УстановитьПараметр("БезОтбораПоОрганизации",   Организации.Количество() = 0);
	Запрос.УстановитьПараметр("Ответственный",            ?(ЗначениеЗаполнено(Ответственный), Ответственный, Неопределено));
	Запрос.УстановитьПараметр("БезОтбораПоОтветственным", НЕ ЗначениеЗаполнено(Ответственный));
	
	Для Каждого ТекЭлемент Из ТаблицаДокументы Цикл
		
		Если НЕ ТекЭлемент.ЕстьПравоЧтение Тогда
			Элементы["Группа" + ТекЭлемент.Метаданные.Имя].Видимость = Ложь;
			Продолжить;
		КонецЕсли;
		
		МетаРеквизитОснование = ИнтеграцияИС.РеквизитДокументОснованиеДокументаИС(ТекЭлемент.Метаданные);
		
		Если МетаРеквизитОснование = Неопределено Тогда
			Запрос.УстановитьПараметр("ПустойДокументОснование", Неопределено);
		Иначе
			Запрос.УстановитьПараметр("ПустойДокументОснование", 
				ИнтеграцияИС.МассивПустыхЗначенийСоставногоТипа(МетаРеквизитОснование.Тип));
		КонецЕсли;
		
		// Сформируем текст запроса выборки количества документов для каждого действия.
		ТекстыЗапроса = Новый СписокЗначений;
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("Оформите",
			Новый Структура("Представление, Доступно",
				НСтр("ru='оформите'"),
				ТекЭлемент.ЕстьПравоДобавление));
		Если Не ТекЭлемент.Оформите Тогда
			СтруктураДействий.Оформите.Вставить("СкрытьЭлемент");
		КонецЕсли;
		СтруктураДействий.Вставить("Отработайте",
			Новый Структура("Представление, Доступно",
				НСтр("ru='отработайте'"),
				ТекЭлемент.ЕстьПравоРедактирование));
		СтруктураДействий.Вставить("Ожидайте",
			Новый Структура("Представление, Доступно",
				НСтр("ru='ожидайте'"),
				ТекЭлемент.ЕстьПравоЧтение));
		
		Если ТекЭлемент.Оформите Тогда
			ТекстыЗапроса.Добавить(
				ТекстЗапросаОформите(ТекЭлемент.Метаданные),
				ТекЭлемент.Метаданные.Имя + "Оформите");
		КонецЕсли;
		
		Если ТекЭлемент.Отработайте Тогда
			ТекстыЗапроса.Добавить(
				ТекстЗапросаОтработайте(ТекЭлемент.Метаданные),
				ТекЭлемент.Метаданные.Имя + "Отработайте");
		КонецЕсли;
		
		Если ТекЭлемент.Ожидайте Тогда
			ТекстыЗапроса.Добавить(
				ТекстЗапросаОжидайте(ТекЭлемент.Метаданные),
				ТекЭлемент.Метаданные.Имя + "Ожидайте");
		КонецЕсли;
		
		// Получим запросом количество документов.
		МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ТекЭлемент.Метаданные.ПолноеИмя());
		Запрос.УстановитьПараметр("ВсеТребующиеОжидания", МенеджерДокумента.ВсеТребующиеОжидания());
		Запрос.УстановитьПараметр("ВсеТребующиеДействия", МенеджерДокумента.ВсеТребующиеДействия());
		
		РезультатыЗапроса = ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, ТекстыЗапроса);
		
		ОбщееКоличество = 0;
		
		// Обновим тексты надписей действий с документом.
		Для Каждого КлючИЗначение Из СтруктураДействий Цикл
			
			ЭлементНадписи = Элементы.Найти("Открыть" + ТекЭлемент.Метаданные.Имя + КлючИЗначение.Ключ);
			
			Если ЭлементНадписи = Неопределено Тогда
				// Такое действие для документа не предусмотрено.
			ИначеЕсли КлючИЗначение.Значение.Свойство("СкрытьЭлемент") Тогда
				// Не определены соответствующие операции
				ЭлементНадписи.Видимость = Ложь;
				Элементы[ЭлементНадписи.Имя + "Декорация"].Заголовок = " ";
			ИначеЕсли Не ТекЭлемент[КлючИЗначение.Ключ] Тогда
				ВывестиПоказатель(
					0,
					ЭлементНадписи,
					КлючИЗначение.Значение.Представление,
					КлючИЗначение.Значение.Доступно);
			Иначе
				ОбщееКоличество = ОбщееКоличество + ВывестиПоказатель(
					РезультатыЗапроса[ТекЭлемент.Метаданные.Имя + КлючИЗначение.Ключ].Выбрать(),
					ЭлементНадписи,
					КлючИЗначение.Значение.Представление,
					КлючИЗначение.Значение.Доступно);
			КонецЕсли;
			
		КонецЦикла;
		
		// Обновим текст надписи самого документа.
		ВывестиПоказатель(
			ОбщееКоличество,
			Элементы["Открыть" + ТекЭлемент.Метаданные.Имя], 
			ТекЭлемент.Заголовок,
			Истина);
		
	КонецЦикла;
	
	СохранитьНастройкиФормы();
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСостояниеОбмена()
	
	РезультатыЗапроса = ИнтеграцияЗЕРНО.СостояниеОбмена(Организации);
	
	ПерваяПроблема = Неопределено;
	ЕщеПроблемы    = 0;
	
	ЕстьТолькоСообщенияОжидающиеОтправки = Ложь;
	
	ПроблемыКлассификаторов = ИнтеграцияЗЕРНО.ПроблемыКлассификаторовВПанелиОбмена();
	
	СтруктураОтбора = Новый Структура("Организация", Организации);
	Если ИнтеграцияЗЕРНО.НеСопоставленныеКлючиАдресов(СтруктураОтбора).Количество() Тогда
		Если ПерваяПроблема <> Неопределено Тогда
			ЕщеПроблемы = ЕщеПроблемы + 1;
		Иначе
			ПерваяПроблема = НСтр("ru = 'На остатках имеются партии с несопоставленным местоположением'");
			Элементы.ГруппаДекорацияСостояниеОбмена.ТекущаяСтраница = Элементы.СостояниеОбменаКрасный;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПроблемыКлассификаторов.ДоступноОбновлениеОписание) Тогда
		Если ПерваяПроблема <> Неопределено Тогда
			ЕщеПроблемы = ЕщеПроблемы + 1;
		Иначе
			ПерваяПроблема = ПроблемыКлассификаторов.ДоступноОбновлениеОписание;
			Элементы.ГруппаДекорацияСостояниеОбмена.ТекущаяСтраница = Элементы.СостояниеОбменаЖелтый;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПроблемыКлассификаторов.ТребуетсяЗагрузкаОписание) Тогда
		Если ПерваяПроблема <> Неопределено Тогда
			ЕщеПроблемы = ЕщеПроблемы + 1;
		Иначе
			ПерваяПроблема = ПроблемыКлассификаторов.ТребуетсяЗагрузкаОписание;
			Элементы.ГруппаДекорацияСостояниеОбмена.ТекущаяСтраница = Элементы.СостояниеОбменаЖелтый;
		КонецЕсли;
	КонецЕсли;
	
	Если Не СтандартныеПодсистемыСервер.ЭтоБазоваяВерсияКонфигурации()
		И Не ОбщегоНазначения.РазделениеВключено()
		И Не ИнтеграцияИС.ХранитьФайлыВТомахНаДиске() Тогда
		Если ПерваяПроблема <> Неопределено Тогда
			ЕщеПроблемы = ЕщеПроблемы + 1;
		Иначе
			ПерваяПроблема = НСтр("ru='Рекомендуется настроить хранение файлов в томах на диске'");
			Элементы.ГруппаДекорацияСостояниеОбмена.ТекущаяСтраница = Элементы.СостояниеОбменаЖелтый;
		КонецЕсли;
	КонецЕсли;
	
	Проблемы = РезультатыЗапроса["ДлительностьРегламентныхЗаданийОбмена"].Выбрать();
	Если Проблемы.Количество() > 0 Тогда
		Если ПерваяПроблема <> Неопределено Тогда
			ЕщеПроблемы = ЕщеПроблемы + 1;
		Иначе
			ПерваяПроблема = НСтр("ru='Длительность выполнения регламентного задания обмена превышает рекомендуемое значение'");
			Элементы.ГруппаДекорацияСостояниеОбмена.ТекущаяСтраница = Элементы.СостояниеОбменаЖелтый;
		КонецЕсли;
	КонецЕсли;
	
	ЕстьПроблема =  Не (РезультатыЗапроса["НастройкаРегламентногоОбменаЗагрузкиСДИЗЗерноСХТП"].Пустой()
		Или ИнтеграцияИС.РегЗаданияНастроены(РезультатыЗапроса, "НастройкаРегламентногоОбменаЗагрузкиСДИЗЗерноСХТП"));
	Если ЕстьПроблема Тогда
		Если ПерваяПроблема <> Неопределено Тогда
			ЕщеПроблемы = ЕщеПроблемы + 1;
		Иначе
			ПерваяПроблема = НСтр("ru='Рекомендуется настроить регламентное задание для загрузки СДИЗ (зерно)'");
			Элементы.ГруппаДекорацияСостояниеОбмена.ТекущаяСтраница = Элементы.СостояниеОбменаЖелтый;
		КонецЕсли;
	КонецЕсли;
	
	ЕстьПроблема = Не (РезультатыЗапроса["НастройкаРегламентногоОбменаЗагрузкиСДИЗППЗ"].Пустой()
		Или ИнтеграцияИС.РегЗаданияНастроены(РезультатыЗапроса, "НастройкаРегламентногоОбменаЗагрузкиСДИЗППЗ"));
	Если ЕстьПроблема Тогда
		Если ПерваяПроблема <> Неопределено Тогда
			ЕщеПроблемы = ЕщеПроблемы + 1;
		Иначе
			ПерваяПроблема = НСтр("ru='Рекомендуется настроить регламентное задание для загрузки СДИЗ (ППЗ)'");
			Элементы.ГруппаДекорацияСостояниеОбмена.ТекущаяСтраница = Элементы.СостояниеОбменаЖелтый;
		КонецЕсли;
	КонецЕсли;
	
	ЕстьПроблема = Не (РезультатыЗапроса["НастройкаРегламентногоОбменаЗагрузкиСДИЗЗерноЭлеватор"].Пустой()
		Или ИнтеграцияИС.РегЗаданияНастроены(РезультатыЗапроса, "НастройкаРегламентногоОбменаЗагрузкиСДИЗЗерноЭлеватор"));
	Если ЕстьПроблема Тогда
		Если ПерваяПроблема <> Неопределено Тогда
			ЕщеПроблемы = ЕщеПроблемы + 1;
		Иначе
			ПерваяПроблема =  НСтр("ru='Рекомендуется настроить регламентное задание для загрузки элеваторных СДИЗ'");
			Элементы.ГруппаДекорацияСостояниеОбмена.ТекущаяСтраница = Элементы.СостояниеОбменаЖелтый;
		КонецЕсли;
	КонецЕсли;
	
	ЕстьПроблема = Не (РезультатыЗапроса["НастройкаРегламентногоОбменаЗагрузкиПартийЗерноСобственныеПартии"].Пустой()
		Или ИнтеграцияИС.РегЗаданияНастроены(РезультатыЗапроса, "НастройкаРегламентногоОбменаЗагрузкиПартийЗерноСобственныеПартии"));
	Если ЕстьПроблема Тогда
		Если ПерваяПроблема <> Неопределено Тогда
			ЕщеПроблемы = ЕщеПроблемы + 1;
		Иначе
			ПерваяПроблема = НСтр("ru='Рекомендуется настроить регламентное задание для загрузки партий зерна'");
			Элементы.ГруппаДекорацияСостояниеОбмена.ТекущаяСтраница = Элементы.СостояниеОбменаЖелтый;
		КонецЕсли;
	КонецЕсли;
	
	ЕстьПроблема = Не (РезультатыЗапроса["НастройкаРегламентногоОбменаЗагрузкиОстатковПартий"].Пустой()
		Или ИнтеграцияИС.РегЗаданияНастроены(РезультатыЗапроса, "НастройкаРегламентногоОбменаЗагрузкиОстатковПартий"));
	Если ЕстьПроблема Тогда
		Если ПерваяПроблема <> Неопределено Тогда
			ЕщеПроблемы = ЕщеПроблемы + 1;
		Иначе
			ПерваяПроблема = НСтр("ru='Рекомендуется настроить регламентное задание для загрузки остатков партий зерна'");
			Элементы.ГруппаДекорацияСостояниеОбмена.ТекущаяСтраница = Элементы.СостояниеОбменаЖелтый;
		КонецЕсли;
	КонецЕсли;
	
	ЕстьПроблема = Не РезультатыЗапроса["ОтправкаСообщенийИзОбщейОчереди"].Пустой();
	Если ЕстьПроблема Тогда
		Если ПерваяПроблема <> Неопределено Тогда
			ЕщеПроблемы = ЕщеПроблемы + 1;
		Иначе
			ПерваяПроблема = НСтр("ru='Рекомендуется настроить регламентное задание для отправки сообщений из общей очереди'");
			Элементы.ГруппаДекорацияСостояниеОбмена.ТекущаяСтраница = Элементы.СостояниеОбменаЖелтый;
		КонецЕсли;
	КонецЕсли;
	
	ЕстьПроблема = ИнтеграцияИС.РегЗаданияНастроены(РезультатыЗапроса, "НастройкаСертификатаНаСервере", Ложь);
	Если ЕстьПроблема Тогда
		Если ПерваяПроблема <> Неопределено Тогда
			ЕщеПроблемы = ЕщеПроблемы + 1;
		Иначе
			ПерваяПроблема = НСтр("ru='Рекомендуется настроить сертификаты на сервере для автоматического подписания сообщений'");
			Элементы.ГруппаДекорацияСостояниеОбмена.ТекущаяСтраница = Элементы.СостояниеОбменаЖелтый;
		КонецЕсли;
	КонецЕсли;
	
	ЕстьПроблема = ИнтеграцияИС.РегЗаданияНастроены(РезультатыЗапроса, "НастройкаОтветственныхЗаПодписаниеСообщений", Ложь);
	Если ЕстьПроблема Тогда
		Если ПерваяПроблема <> Неопределено Тогда
			ЕщеПроблемы = ЕщеПроблемы + 1;
		Иначе
			ПерваяПроблема = НСтр("ru='Рекомендуется настроить ответственных за подписание сообщений'");
			Элементы.ГруппаДекорацияСостояниеОбмена.ТекущаяСтраница = Элементы.СостояниеОбменаЖелтый;
		КонецЕсли;
	КонецЕсли;
	
	Проблемы = РезультатыЗапроса["ЕстьСообщенияОжидающиеОтправки"].Выбрать();
	Если Проблемы.Количество() > 0 Тогда
		Если ПерваяПроблема <> Неопределено Тогда
			ЕщеПроблемы = ЕщеПроблемы + 1;
		Иначе
			Проблемы.Следующий();
			ПерваяПроблема = СтрШаблон(
				НСтр("ru = 'Есть сообщения (%1), ожидающие отправки в ФГИС ""Зерно"". Выполните обмен.'"),
				Проблемы.КоличествоСообщений);
			Элементы.ГруппаДекорацияСостояниеОбмена.ТекущаяСтраница = Элементы.СостояниеОбменаИнформация;
			ЕстьТолькоСообщенияОжидающиеОтправки = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ПерваяПроблема <> Неопределено Тогда
		
		Если ЕщеПроблемы > 0 Тогда
			ТекстЗаголовка = СтрШаблон(НСтр("ru='%1 ( + еще %2 )'"), ПерваяПроблема, ЕщеПроблемы);
			ЕстьТолькоСообщенияОжидающиеОтправки = Ложь;
		Иначе
			ТекстЗаголовка = ПерваяПроблема;
		КонецЕсли;
		
		Элементы.НадписьСостояниеОбмена.Заголовок = ТекстЗаголовка;
		Элементы.ГруппаДекорацияСостояниеОбмена.Видимость = Истина;
		Элементы.НадписьСостояниеОбмена.Видимость         = Истина;
		
	Иначе
		Элементы.ГруппаДекорацияСостояниеОбмена.Видимость = Ложь;
		Элементы.НадписьСостояниеОбмена.Видимость         = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКоличествоСообщенийКПодписанию()
	
	ДанныеСообщений = ИнтеграцияЗЕРНОВызовСервера.СообщенияВОчередиТребующиеПодписания(
		Организации.ВыгрузитьЗначения());
	
	КоличествоСообщений = 0;
	
	Для Каждого КлючИЗначение Из ДанныеСообщений.СообщенияКПодписанию Цикл
		КоличествоСообщений = КоличествоСообщений + КлючИЗначение.Значение.Количество();
	КонецЦикла;
	
	Если КоличествоСообщений Тогда
		Элементы.ОткрытьСообщенияКПодписанию.Заголовок = СтрШаблон(
			НСтр("ru = 'Сообщения к подписанию (%1)'"),
			Формат(КоличествоСообщений, "ЧН=0;"));
	Иначе
		Элементы.ОткрытьСообщенияКПодписанию.Заголовок = НСтр("ru = 'Сообщения к подписанию'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьДокумент(Таблица, ОбъектМетаданных, Заголовок, Оформите, Отработайте, Ожидайте)
	
	НоваяСтрока = Таблица.Добавить();
	НоваяСтрока.Метаданные  = ОбъектМетаданных;
	НоваяСтрока.Заголовок   = Заголовок;
	
	НоваяСтрока.ЕстьПравоЧтение         = ПравоДоступа("Чтение",         ОбъектМетаданных);
	НоваяСтрока.ЕстьПравоДобавление     = ПравоДоступа("Добавление",     ОбъектМетаданных);
	НоваяСтрока.ЕстьПравоРедактирование = ПравоДоступа("Редактирование", ОбъектМетаданных);
	
	НоваяСтрока.Оформите    = Оформите;
	НоваяСтрока.Отработайте = Отработайте;
	НоваяСтрока.Ожидайте    = Ожидайте;
	
КонецПроцедуры

&НаСервере
Функция ВывестиПоказатель(Выборка, Кнопка, ТекстПоказателя, ДействиеДоступно)
	
	Если ДействиеДоступно Тогда
		
		Если ТипЗнч(Выборка) = Тип("Число") Тогда
			КоличествоДокументов = Выборка;
		ИначеЕсли Выборка.Следующий() Тогда
			КоличествоДокументов = Выборка.КоличествоДокументов;
		Иначе
			КоличествоДокументов = 0;
		КонецЕсли;
		
		Если КоличествоДокументов > 0 Тогда
			ТекстЗаголовка = СтрШаблон(НСтр("ru = '%1 (%2)'"), ТекстПоказателя, КоличествоДокументов);
			ЦветТекста     = ЦветаСтиля.ЦветГиперссылкиГосИС;
		Иначе
			ТекстЗаголовка = ТекстПоказателя;
			ЦветТекста     = ЦветаСтиля.ЦветТекстаНеТребуетВниманияГосИС;
		КонецЕсли;
		
	Иначе
		
		КоличествоДокументов = 0;
		ТекстЗаголовка       = " ";
		ЦветТекста           = ЦветаСтиля.ЦветТекстаНеТребуетВниманияГосИС;
		
	КонецЕсли;
	
	Кнопка.Заголовок   = ТекстЗаголовка;
	Кнопка.ЦветТекста  = ЦветТекста;
	Кнопка.Видимость   = Истина;
	Кнопка.Доступность = НЕ ПустаяСтрока(ТекстЗаголовка);
	
	Возврат КоличествоДокументов;
	
КонецФункции

&НаСервере
Функция ТекстЗапросаОтработайте(МетаданныеДокумента)
	
	ТекстЗапроса = ТекстЗапросаКоличестваДокументовПоДальнейшемуДействию(
		МетаданныеДокумента,
		"&ВсеТребующиеДействия");
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ТекстЗапросаОжидайте(МетаданныеДокумента)
	
	ТекстЗапроса = ТекстЗапросаКоличестваДокументовПоДальнейшемуДействию(
		МетаданныеДокумента,
		"&ВсеТребующиеОжидания");
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ТекстЗапросаОформите(МетаданныеДокумента)
	
	Если МетаданныеДокумента = Метаданные.Документы.ПогашениеСДИЗЗЕРНО Тогда
		Возврат ТекстЗапросаКоличествоСДИЗ();
	КонецЕсли;
	
	МетаРеквизитОснование = ИнтеграцияИС.РеквизитДокументОснованиеДокументаИС(МетаданныеДокумента);
	
	Если МетаРеквизитОснование = Неопределено Тогда
		ТипыДокументаОснования = Новый Массив;
	Иначе
		ТипыДокументаОснования = МетаРеквизитОснование.Тип.Типы();
	КонецЕсли;
	СтрокиСоединенияДокументов = Новый Массив;
	СтрокиОтбораДокументов     = Новый Массив;
	
	Для Каждого ТипОснования Из ТипыДокументаОснования Цикл
		
		МетаданныеОснования = Метаданные.НайтиПоТипу(ТипОснования);
		
		Если НЕ ПравоДоступа("Чтение", МетаданныеОснования) Тогда
			Продолжить;
		КонецЕсли;
		
		// Добавим соединение с документом-основанием для RLS.
		СтрокиСоединенияДокументов.Добавить("	ЛЕВОЕ СОЕДИНЕНИЕ " + МетаданныеОснования.ПолноеИмя() + " КАК Основание" + МетаданныеОснования.Имя + "
			|		ПО Статусы.Основание = Основание" + МетаданныеОснования.Имя + ".Ссылка");
		
		СтрокиОтбораДокументов.Добавить("Основание" + МетаданныеОснования.Имя + ".Ссылка ЕСТЬ НЕ NULL");
		
	КонецЦикла;
	
	Если СтрокиОтбораДокументов.Количество() = 0 Тогда
		
		// Нет ни одного доступного документа-основания.
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	0 КАК КоличествоДокументов";
		
	Иначе
		
		ТекстСоединения = СтрСоединить(СтрокиСоединенияДокументов, Символы.ПС);
		ТекстОтбора 	= " И (" + СтрСоединить(СтрокиОтбораДокументов, Символы.ПС + "		ИЛИ ") + ")";
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Статусы.Основание) КАК КоличествоДокументов
		|ИЗ
		|	РегистрСведений.СтатусыОформленияДокументовЗЕРНО КАК Статусы
		|%2
		|ГДЕ
		|	Статусы.Документ = ЗНАЧЕНИЕ(Документ.%1.ПустаяСсылка)
		|	И Статусы.СтатусОформления В
		|		(ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовГосИС.НеОформлено),
		|		 ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовГосИС.ОформленоЧастично),
		|		 ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовГосИС.ЕстьОшибкиОформления),
		|		 ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовГосИС.ТребуетсяСопоставлениеНоменклатуры))
		|	И НЕ Статусы.Архивный
		|	И (&БезОтбораПоОрганизации ИЛИ Статусы.Контрагент В(&Организации))
		|	%3";
		
		ТекстЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстЗапроса,
			МетаданныеДокумента.Имя,
			ТекстСоединения,
			ТекстОтбора);
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ТекстЗапросаКоличествоСДИЗ()
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СДИЗ.Ссылка) КАК КоличествоДокументов
	|ИЗ
	|	Справочник.СДИЗЗЕРНО КАК СДИЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОформленияДокументовЗЕРНО КАК Статусы
	|		ПО СДИЗ.Ссылка = Статусы.Основание
	|ГДЕ
	|	СДИЗ.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыСДИЗЗЕРНО.Оформлен),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыСДИЗЗЕРНО.ОформленИПодтвержден))
	|	И НЕ СДИЗ.ПометкаУдаления
	|	И (&БезОтбораПоОрганизации ИЛИ Статусы.Контрагент В(&Организации))
	|	И НЕ ЕСТЬNULL(Статусы.Архивный, ЛОЖЬ)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ТекстЗапросаКоличестваДокументовПоДальнейшемуДействию(МетаданныеДокумента, ОтборДействия)
	
	МетаРеквизитОснование = ИнтеграцияИС.РеквизитДокументОснованиеДокументаИС(МетаданныеДокумента);
	
	ТекстСоединения = "";
	ТекстОтбора     = "";
	
	Если МетаРеквизитОснование <> Неопределено Тогда
		
		ТипыДокументаОснования     = МетаРеквизитОснование.Тип.Типы();
		СтрокиСоединенияДокументов = Новый Массив;
		СтрокиОтбораДокументов     = Новый Массив;
		ДокументИмеетОснования     = Ложь;
		
		Для Каждого ТипОснования Из ТипыДокументаОснования Цикл
			
			МетаданныеОснования = Метаданные.НайтиПоТипу(ТипОснования);
			
			Если МетаданныеОснования = Неопределено Тогда
				Продолжить
			ИначеЕсли Не Метаданные.Документы.Содержит(МетаданныеОснования) Тогда
				Продолжить;
			Иначе
				ДокументИмеетОснования = Истина;
			КонецЕсли;
			Если НЕ ПравоДоступа("Чтение", МетаданныеОснования) Тогда
				Продолжить;
			КонецЕсли;
			
			// Добавим соединение с документом-основанием для RLS.
			СтрокиСоединенияДокументов.Добавить(
				"	ЛЕВОЕ СОЕДИНЕНИЕ Документ." + МетаданныеОснования.Имя + " КАК Документ" + МетаданныеОснования.Имя + "
				|		ПО Документ.ДокументОснование = Документ" + МетаданныеОснования.Имя + ".Ссылка");
			
			СтрокиОтбораДокументов.Добавить("Документ" + МетаданныеОснования.Имя + ".Ссылка ЕСТЬ НЕ NULL");
			
		КонецЦикла;
		
		Если ДокументИмеетОснования Тогда
			
			Если СтрокиОтбораДокументов.Количество() = 0 Тогда
				ТекстСоединения = "";
			Иначе
				ТекстСоединения = СтрСоединить(СтрокиСоединенияДокументов, Символы.ПС);
			КонецЕсли;
			
			СтрокиОтбораДокументов.Добавить("Документ.ДокументОснование В (&ПустойДокументОснование)");
			ТекстОтбора = " И (" + СтрСоединить(СтрокиОтбораДокументов, Символы.ПС + "		ИЛИ ") + ")";
			
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыОбъектов.ОбъектСинхронизации) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыОбъектовСинхронизацииЗЕРНО КАК СтатусыОбъектов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.%1 КАК Документ
	|		ПО СтатусыОбъектов.ОбъектСинхронизации = Документ.Ссылка
	|%2
	|ГДЕ
	|	СтатусыОбъектов.ДальнейшееДействие1 В (%3)
	|	И НЕ Документ.ПометкаУдаления
	|	И (&БезОтбораПоОрганизации ИЛИ Документ.Организация В(&Организации))
	|	И (&БезОтбораПоОтветственным ИЛИ Документ.Ответственный = &Ответственный)
	|	%4";
	
	ТекстЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТекстЗапроса,
		МетаданныеДокумента.Имя,
		ТекстСоединения,
		ОтборДействия,
		ТекстОтбора);
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Процедура НастроитьНадписьУчитываемыеВидыПродукции()
	
	Если Организации.Количество() > 0 Тогда
		СписокОрганизации = Организации;
	Иначе
		СписокОрганизации = ОбщегоНазначенияИС.ДоступныеОрганизации();
	КонецЕсли;
	
	СохраненныеУчитываемыеВидыПродукции = Неопределено;
	АдресТаблицы = ИнтеграцияЗЕРНО.ПараметрыОптимизации().УчитываемыеВидыПродукции;
	Если Не ПустаяСтрока(АдресТаблицы) Тогда
		СохраненныеУчитываемыеВидыПродукции = ПолучитьИзВременногоХранилища(АдресТаблицы);
	КонецЕсли;
	
	ДанныеЗаголовка = Новый Массив();
	ДанныеЗаголовка.Добавить(
		Новый ФорматированнаяСтрока(
		НСтр("ru='Учитываемые виды продукции'"))
		);
	ДанныеЗаголовка.Добавить(
		Новый ФорматированнаяСтрока(" ")
		);
	
	МассивВидыПродукции = Новый Массив;
	
	Если СохраненныеУчитываемыеВидыПродукции <> Неопределено 
		И ТипЗнч(СохраненныеУчитываемыеВидыПродукции) = Тип("ТаблицаЗначений") Тогда
		
		ВидыПродукции = Новый Структура;
		ВидыПродукции.Вставить("УчетЗерна", НСтр("ru='зерно'"));
		ВидыПродукции.Вставить("УчетПродуктовПереработкиЗерна", НСтр("ru='ППЗ'"));
		ВидыПродукции.Вставить("ОрганизацияЯвляетсяЭлеватором", НСтр("ru='зерно на элеваторе'"));
		
		Для Каждого СтокаОрганизация Из СписокОрганизации Цикл
			
			Для Каждого Строка Из СохраненныеУчитываемыеВидыПродукции Цикл
				
				Если Строка.Организация <> СтокаОрганизация.Значение Тогда
					Продолжить;
				КонецЕсли;
				
				ЗаполнитьМассивИспользуемыеВидыПродукции(МассивВидыПродукции, ВидыПродукции, Строка);
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ПредставлениеСтроки = СформироватьПредставлениеСтрокиУчитываемыеВидыПродукции(МассивВидыПродукции);
	
	ПредставлениеСтроки = СтрШаблон("(%1)", ПредставлениеСтроки);
	
	ДанныеЗаголовка.Добавить(
		Новый ФорматированнаяСтрока(
		ПредставлениеСтроки));
	
	Элементы.ОткрытьУчитываемыеВидыПродукции.Заголовок = Новый ФорматированнаяСтрока(ДанныеЗаголовка);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьМассивИспользуемыеВидыПродукции(МассивВидыПродукции, ВидыПродукции, Строка)
	
	Для Каждого КлючИЗначение Из ВидыПродукции Цикл
		
		Если Строка[КлючИЗначение.Ключ] Тогда
			
			Если МассивВидыПродукции.Найти(КлючИЗначение.Значение) = Неопределено Тогда
				
				МассивВидыПродукции.Добавить(КлючИЗначение.Значение);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СформироватьПредставлениеСтрокиУчитываемыеВидыПродукции(МассивВидыПродукции)
	
	Если МассивВидыПродукции.Количество() = 0 Тогда
		
		ПредставлениеСтроки = НСтр("ru='не настроены'");
		
	Иначе
		
		ПредставлениеСтроки = СтрСоединить(МассивВидыПродукции, ", ");
		
	КонецЕсли;
	
	Возврат ПредставлениеСтроки;
	
КонецФункции

#КонецОбласти

#Область ОтборПоОрганизации

&НаКлиенте
Функция ОповещениеВыбораОрганизаций()
	
	Возврат Новый ОписаниеОповещения("ПослеВыбораОрганизации", ЭтотОбъект);
	
КонецФункции

&НаКлиенте
Процедура ПослеВыбораОрганизации(Результат, ДополнительныеПараметры) Экспорт
	
	ОбновитьСпискиДокументов();
	
КонецПроцедуры

#КонецОбласти

#Область ЛогированиеЗапросов

&НаСервере
Процедура ВключитьЛогированиеЗапросов(ПродолжительностьВМинутах)
	
	ПараметрыЛогирования = ЛогированиеЗапросовЗЕРНО.ПараметрыЛогированияЗапросов();
	ЛогированиеЗапросовИС.ВключитьЛогированиеЗапросов(ПараметрыЛогирования, ПродолжительностьВМинутах * 60);
	ЛогированиеЗапросовЗЕРНО.УстановитьПараметрыЛогированияЗапросов(ПараметрыЛогирования);
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьЛогированиеЗапросов()
	
	ЛогированиеЗапросовЗЕРНО.ОтключитьЛогированиеЗапросов();
	
	ПараметрыЛогирования = ПолучитьПараметрыЛогирования();
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьЛогЗапросов()
	
	ЛогированиеЗапросовИС.ОчиститьЛогЗапросов(ПолучитьПараметрыЛогирования());
	ЛогированиеЗапросовЗЕРНО.УстановитьПараметрыЛогированияЗапросов(ПараметрыЛогирования);
	ПараметрыЛогирования = ПолучитьПараметрыЛогирования();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПараметрыЛогирования()
	
	ПараметрыЛогирования = ЛогированиеЗапросовЗЕРНО.ПараметрыЛогированияЗапросов();
	ЛогированиеЗапросовИС.ЗаполнитьПараметрыЛогирования(ПараметрыЛогирования);
	
	Возврат ПараметрыЛогирования;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ОбработкаОжиданияЗавершенияЛогирования()
	
	ПараметрыЛогирования = ПолучитьПараметрыЛогирования();
	ОбновитьПредставлениеЛогирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылкиЛогирования(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ВключитьЛогЗапросов" Тогда
		
		СписокПродолжительностьЛога = Новый СписокЗначений();
		СписокПродолжительностьЛога.Добавить(5,  НСтр("ru='5 мин.'"));
		СписокПродолжительностьЛога.Добавить(10, НСтр("ru='10 мин.'"));
		СписокПродолжительностьЛога.Добавить(15, НСтр("ru='15 мин.'"));
		
		ПараметрыВыбораПродолжительности = Новый Структура;
		ПараметрыВыбораПродолжительности.Вставить("РежимПродления", Ложь);
		
		ОповещениеОВыбора = Новый ОписаниеОповещения("ВыборПродолжительностьЛогаЗавершение", ЭтотОбъект, ПараметрыВыбораПродолжительности);
		ПоказатьВыборИзСписка(ОповещениеОВыбора, СписокПродолжительностьЛога, Элемент);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьЛогЗапросов" Тогда
		
		ЛогированиеЗапросовИСКлиент.ОткрытьПросмотрЛогаЗапросов(УникальныйИдентификатор, "ЗЕРНО");
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ИзменитьВремяОкончанияЗаписиЛогов" Тогда
		
		СписокПродолжительностьЛога = Новый СписокЗначений();
		СписокПродолжительностьЛога.Добавить(5,  НСтр("ru='продлить на 5 мин.'"));
		СписокПродолжительностьЛога.Добавить(10, НСтр("ru='продлить на 10 мин.'"));
		СписокПродолжительностьЛога.Добавить(15, НСтр("ru='продлить на 15 мин.'"));
		
		ПараметрыВыбораПродолжительности = Новый Структура;
		ПараметрыВыбораПродолжительности.Вставить("РежимПродления", Истина);
		
		ОповещениеОВыбора = Новый ОписаниеОповещения("ВыборПродолжительностьЛогаЗавершение", ЭтотОбъект, ПараметрыВыбораПродолжительности);
		ПоказатьВыборИзСписка(ОповещениеОВыбора, СписокПродолжительностьЛога, Элемент);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОтключитьЗаписьЛогов" Тогда
		
		ОтключитьЛогированиеЗапросов();
		ОбновитьПредставлениеЛогирования();
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОчиститьЛогЗапросов" Тогда
		
		ОписаниеОповещенияВопросОчиститьЛогЗапросовЗавершение = Новый ОписаниеОповещения("ОписаниеОповещенияВопросОчиститьЛогЗапросовЗавершение", ЭтотОбъект);
		
		ПоказатьВопрос(
			ОписаниеОповещенияВопросОчиститьЛогЗапросовЗавершение,
			НСтр("ru='Очистить лог запросов?'"),
			РежимДиалогаВопрос.ДаНет,,
			КодВозвратаДиалога.Да);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПредставлениеЛогирования()
	
	ОтключитьОбработчикОжидания("Подключаемый_ОбработкаОжиданияЗавершенияЛогирования");
	
	ДанныеОсновнойЧасти = Новый Массив();
	ДанныеПодсказки     = Новый Массив();
	ДанныеПодсказки.Добавить(НСтр("ru='Логирование запросов позволяет получить подробную'"));
	ДанныеПодсказки.Добавить(" ");
	ДанныеПодсказки.Добавить(НСтр("ru='техническую информацию по взаимодействию с ФГИС ""Зерно""'"));
	ДанныеПодсказки.Добавить(" ");
	ДанныеПодсказки.Добавить(НСтр("ru='для расследования проблем и ошибок службами поддержки.'"));
	
	Если ПараметрыЛогирования.Включено Тогда
		
		Если ЗначениеЗаполнено(ПараметрыЛогирования.ОкончаниеЗаписи) Тогда
			ДатаЗавершенияЛогирования = ИнтеграцияИСКлиентСервер.ДатаИзУниверсальнойДаты(ПараметрыЛогирования.ОкончаниеЗаписи);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДатаЗавершенияЛогирования) Тогда
			
			ДанныеОсновнойЧасти.Добавить(НСтр("ru='Логирование запросов производится до'"));
			ДанныеОсновнойЧасти.Добавить(" ");
			
			Если НачалоДня(ТекущаяДата()) <> НачалоДня(ДатаЗавершенияЛогирования) Тогда
				ПредставлениеДаты = СтрШаблон(
					" (%1)",
					Формат(ДатаЗавершенияЛогирования, "ДФ=dd.MM.yyyy"));
			КонецЕсли;
			
			ПредставлениеВремени = СтрШаблон(
				"%1%2",
				Формат(ДатаЗавершенияЛогирования, "ДФ=HH:mm;"),
				ПредставлениеДаты);
			
			ДанныеОсновнойЧасти.Добавить(
				Новый ФорматированнаяСтрока(
					ПредставлениеВремени,,
					ЦветГиперссылки,,
					"ИзменитьВремяОкончанияЗаписиЛогов"));
			
			ПодключитьОбработчикОжидания(
				"Подключаемый_ОбработкаОжиданияЗавершенияЛогирования",
				(ДатаЗавершенияЛогирования - ТекущаяДата()) + 10,
				Истина);
			
		Иначе
			
			ДанныеОсновнойЧасти.Добавить(НСтр("ru='Запись логов запросов продолжается'"));
			
		КонецЕсли;
		
		ДанныеОсновнойЧасти.Добавить(" ");
		ДанныеОсновнойЧасти.Добавить(
			Новый ФорматированнаяСтрока(
				НСтр("ru='Отключить'"),,
				ЦветГиперссылки,,
				"ОтключитьЗаписьЛогов"));
		
	Иначе
		
		ДанныеОсновнойЧасти.Добавить(НСтр("ru='Запись логов запросов не производится.'"));
		
		ДанныеОсновнойЧасти.Добавить(" ");
		ДанныеОсновнойЧасти.Добавить(
			Новый ФорматированнаяСтрока(
				НСтр("ru='Включить'"),,
				ЦветГиперссылки,,
				"ВключитьЛогЗапросов"));
		
	КонецЕсли;
	
	Если ПараметрыЛогирования.Включено
		Или ЗначениеЗаполнено(ПараметрыЛогирования.ОкончаниеЗаписи) Тогда
		
		ДанныеПодсказки.Добавить(" ");
		
		ДанныеПодсказки.Добавить(
				Новый ФорматированнаяСтрока(
					НСтр("ru='Открыть'"),,
					ЦветГиперссылки,,
					"ОткрытьЛогЗапросов"));
		
		ДанныеПодсказки.Добавить(" ");
		
		ДанныеПодсказки.Добавить(
			Новый ФорматированнаяСтрока(
				НСтр("ru='Очистить'"),,
				ЦветГиперссылки,,
				"ОчиститьЛогЗапросов"));
		
	КонецЕсли;
	
	ПредставлениеЛогированияЗапросов = Новый ФорматированнаяСтрока(ДанныеОсновнойЧасти);
	Элементы.ПредставлениеЛогированияЗапросов.РасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(ДанныеПодсказки);
	
КонецПроцедуры

#КонецОбласти

#Область ОписанияОповещений

&НаКлиенте
Процедура ВыборПродолжительностьЛогаЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Продолжительность = ВыбранныйЭлемент.Значение;
	Если ДополнительныеПараметры.РежимПродления
		И ЗначениеЗаполнено(ПараметрыЛогирования.ОкончаниеЗаписи) Тогда
		ДатаОкончания = ИнтеграцияИСКлиентСервер.ДатаИзУниверсальнойДаты(ПараметрыЛогирования.ОкончаниеЗаписи);
		Если ДатаОкончания > ТекущаяДата() Тогда
			Продолжительность = Цел((КонецМинуты(ДатаОкончания) - КонецМинуты(ТекущаяДата())) / 60 + Продолжительность);
		КонецЕсли;
	КонецЕсли;
	
	ВключитьЛогированиеЗапросов(Продолжительность);
	ОбновитьПредставлениеЛогирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеОповещенияВопросОчиститьЛогЗапросовЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьЛогЗапросов();
	ОбновитьПредставлениеЛогирования();
	
	ПоказатьОповещениеПользователя(НСтр("ru='Лог запросов очищен.'"));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриЗавершенииОперации(Результат, ДополнительныеПараметры) Экспорт
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПанельСинхронизацииДанных(Команда)
	
	ИнтеграцияЗЕРНОКлиент.ОткрытьФормуСинхронизацияДанныхЗЕРНО(ЭтотОбъект);
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти