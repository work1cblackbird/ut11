

#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ЭтоЗакупки = СтрНайти(ПараметрыВыполненияКоманды.Источник.ИмяФормы, "Закупки");
	
	ЗаголовокФормыНастройкиСхемыКомпоновкиДанных = НСтр("ru = 'Отбор действующих договоров для автоматической постановки контрагентов на мониторинг 1СПАРК'");
	
	УникальныйИдентификатор = Новый УникальныйИдентификатор();
	
	Адреса = ПолучитьАдресаСхемыКомпоновкиДанныхВоВременномХранилище(УникальныйИдентификатор, ЭтоЗакупки);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресСхемыКомпоновкиДанных",                Адреса.СхемаКомпоновкиДанных);
	ПараметрыФормы.Вставить("АдресНастроекКомпоновкиДанных",             Адреса.НастройкиКомпоновкиДанных);
	ПараметрыФормы.Вставить("Заголовок",                                 ЗаголовокФормыНастройкиСхемыКомпоновкиДанных);
	ПараметрыФормы.Вставить("УникальныйИдентификатор",                   УникальныйИдентификатор);
	ПараметрыФормы.Вставить("НеНастраиватьУсловноеОформление",           Истина);
	ПараметрыФормы.Вставить("НеНастраиватьПорядок",                      Истина);
	ПараметрыФормы.Вставить("НеПомещатьНастройкиВСхемуКомпоновкиДанных", Истина);
	ПараметрыФормы.Вставить("НеРедактироватьСхемуКомпоновкиДанных",      Истина);
	ПараметрыФормы.Вставить("НеЗагружатьСхемуКомпоновкиДанныхИзФайла",   Истина);
	ПараметрыФормы.Вставить("НеНастраиватьВыбор",                        Истина);
	ПараметрыФормы.Вставить("НеНастраиватьПараметры",                    Истина);
	
	Оповещение = Новый ОписаниеОповещения("РедактироватьЗавершение", ЭтотОбъект, Новый Структура("ЭтоЗакупки", ЭтоЗакупки));
	ОткрытьФорму("ОбщаяФорма.УпрощеннаяНастройкаСхемыКомпоновкиДанных",
		ПараметрыФормы, 
		ПараметрыВыполненияКоманды.Источник,,,,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РедактироватьЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат <> Неопределено Тогда
		РедактироватьЗавершениеНаСервере(Результат, ДополнительныеПараметры.ЭтоЗакупки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РедактироватьЗавершениеНаСервере(Адрес, ЭтоЗакупки)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НастройкиКомпоновкиДанных = ПолучитьИзВременногоХранилища(Адрес);
	Если ЭтоЗакупки Тогда
		Константы.АвтоПостановкаПоставщиковНаМониторингСПАРКНастройка.Установить(Новый ХранилищеЗначения(НастройкиКомпоновкиДанных));
	Иначе
		Константы.АвтоПостановкаКлиентовНаМониторингСПАРКНастройка.Установить(Новый ХранилищеЗначения(НастройкиКомпоновкиДанных));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресаСхемыКомпоновкиДанныхВоВременномХранилище(УникальныйИдентификатор, ЭтоЗакупки)

	Адреса = Новый Структура("СхемаКомпоновкиДанных, НастройкиКомпоновкиДанных");
	
	СхемаКомпоновкиДанных = Обработки.НастройкаПостановкиКонтрагентовНаМониторингСПАРК.ПолучитьМакет("Макет");
	
	Адреса.СхемаКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	
	// Настройки
	Если ЭтоЗакупки Тогда
		ХранилищеНастроекКомпоновкиДанных = Константы.АвтоПостановкаПоставщиковНаМониторингСПАРКНастройка.Получить();
	Иначе
		ХранилищеНастроекКомпоновкиДанных = Константы.АвтоПостановкаКлиентовНаМониторингСПАРКНастройка.Получить();
	КонецЕсли;
	
	НастройкиКомпоновкиДанных = ХранилищеНастроекКомпоновкиДанных.Получить();
	Если ЗначениеЗаполнено(НастройкиКомпоновкиДанных) Тогда
		Адреса.НастройкиКомпоновкиДанных = ПоместитьВоВременноеХранилище(НастройкиКомпоновкиДанных, УникальныйИдентификатор);
	Иначе
		Адреса.НастройкиКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных.НастройкиПоУмолчанию, УникальныйИдентификатор);
	КонецЕсли;
	
	Возврат Адреса;

КонецФункции

#КонецОбласти

