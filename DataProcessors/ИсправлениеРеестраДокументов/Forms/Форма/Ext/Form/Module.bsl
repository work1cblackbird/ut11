
#Область ОбработчикиКомандФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОчисткаНесуществующихСсылок = Истина;
	ПереотражениеДокументов     = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОперацию(Команда)
	ИдентификаторЗадания = Неопределено;
	
	Если ОчисткаНесуществующихСсылок Тогда
		ПодключитьОбработчикОжидания("ОчиститьНесуществующиеСсылки", 0.1, Истина);
	ИначеЕсли ПереотражениеДокументов Тогда
		ПодключитьОбработчикОжидания("ПереотразитьДокументыВРеестре", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОчиститьНесуществующиеСсылки()
	
	ДлительнаяОперация = ОчиститьНесуществующиеСсылкиНаСервере();
	ИдентификаторЗадания = ДлительнаяОперация.ИдентификаторЗадания;
	
	ПараметрыОжидания  = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выполняется очистка реестра документов от несуществующих ссылок.'");
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОчисткаНесуществующихСсылокЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ОчиститьНесуществующиеСсылкиНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	СтруктураПараметров = Новый Структура;
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ИмяОбработки", СтрРазделить(ИмяФормы, ".")[1]);
	ПараметрыПроцедуры.Вставить("ИмяМетода", "ОчиститьНесуществующиеСсылкиВРеестреДокументовВФоне");
	ПараметрыПроцедуры.Вставить("ПараметрыВыполнения", СтруктураПараметров);
	ПараметрыПроцедуры.Вставить("ЭтоВнешняяОбработка", Ложь);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Очистка несуществующих ссылок в реестре документов'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("ДлительныеОперации.ВыполнитьПроцедуруМодуляОбъектаОбработки", 
		ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции
	
&НаКлиенте
Процедура ОчисткаНесуществующихСсылокЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	Иначе
		РезультатВыполнения = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		
		ШаблонСообщения = НСтр("ru = 'Завершена очистка несуществующих ссылок. Удалено записей: %1.'");
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
			РезультатВыполнения.КоличествоСсылок);
		Сообщение.Сообщить();
		
		Если ПереотражениеДокументов Тогда
			ПодключитьОбработчикОжидания("ПереотразитьДокументыВРеестре", 0.1, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереотразитьДокументыВРеестре()
	
	ДлительнаяОперация = ПереотразитьДокументыВРеестреНаСервере();
	ИдентификаторЗадания = ДлительнаяОперация.ИдентификаторЗадания;
	
	ПараметрыОжидания  = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выполняется отражение документов в реестре.'");
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПереотражениеДокументовВРеестреЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ПереотразитьДокументыВРеестреНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	СтруктураПараметров = Новый Структура;
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ИмяОбработки", СтрРазделить(ИмяФормы, ".")[1]);
	ПараметрыПроцедуры.Вставить("ИмяМетода", "ПереотразитьДокументыВРеестреДокументов");
	ПараметрыПроцедуры.Вставить("ПараметрыВыполнения", СтруктураПараметров);
	ПараметрыПроцедуры.Вставить("ЭтоВнешняяОбработка", Ложь);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Переотражение документов в реестре документов'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("ДлительныеОперации.ВыполнитьПроцедуруМодуляОбъектаОбработки", 
		ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции
	
&НаКлиенте
Процедура ПереотражениеДокументовВРеестреЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	Иначе
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Отражение документов в реестре завершено.'");
		Сообщение.Сообщить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти