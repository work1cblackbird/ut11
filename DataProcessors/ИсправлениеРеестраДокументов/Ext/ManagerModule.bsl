#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Очищает регистр сведений "Реестр документов" от битых ссылок.
// Возвращает информацию о количестве удаленных записей с битыми ссылками в регистре.
//
// Параметры:
//	ТипыДокументов - Массив Из Тип - типы документов, для которых выполняется очистка регистра.
//	РазмерПакета - Число - ограничение количества типов объектов метаданных в одном пакете запроса.
//
// Возвращаемое значение:
//	Структура - коллекция, содержащая следующие свойства:
//		* КоличествоСсылок - Число - количество удаленных записей с битыми ссылками.
//
Функция ОчиститьНесуществующиеСсылкиВРеестреДокументов(ТипыДокументов = Неопределено, РазмерПакета = 10) Экспорт
	
	Если ТипыДокументов = Неопределено Тогда
		ТипыДокументов = РегистрыСведений.РеестрДокументов.ТипыДокументовРеестра();
	КонецЕсли;
	
	Счетчик = 0;
	ТипыКОбработке       = Новый Массив;
	НесуществующиеСсылки = Новый Массив;
	Для Каждого ТипДокумента Из ТипыДокументов Цикл
		
		Счетчик = Счетчик + 1;
		ТипыКОбработке.Добавить(ТипДокумента);
		
		Если ТипыКОбработке.Количество() % РазмерПакета = 0 Или Счетчик = ТипыДокументов.Количество() Тогда
			ТекстЗапроса = ТекстЗапросаНесуществующихСсылокРеестраДокументов(ТипыКОбработке);
			Запрос = Новый Запрос(ТекстЗапроса);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НесуществующиеСсылки,
				Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
			
			ТипыКОбработке.Очистить();
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Ссылка Из НесуществующиеСсылки Цикл
		УдалитьНаборЗаписейВРеестре(Ссылка);
	КонецЦикла;
	
	РезультатВыполнения = Новый Структура;
	РезультатВыполнения.Вставить("КоличествоСсылок", НесуществующиеСсылки.Количество());
	
	Возврат РезультатВыполнения;
	
КонецФункции

// Возвращает текст запроса, получающий информацию о несуществующих ссылках в регистре сведений "Реестр документов".
//
// Параметры:
//	ТипыДокументов - Массив Из Тип - типы документов, для которых необходимо получить сведения в регистре.
//
// Возвращаемое значение:
//	Строка - текст запроса.
//
Функция ТекстЗапросаНесуществующихСсылокРеестраДокументов(ТипыДокументов) Экспорт
	
	ШаблонЗапроса = 
		"ВЫБРАТЬ
		|	РеестрДокументов.Ссылка
		|ИЗ
		|	РегистрСведений.РеестрДокументов КАК РеестрДокументов
		|ГДЕ
		|	&УсловиеСсылкиРеестра
		|	И НЕ ИСТИНА В
		|				(ВЫБРАТЬ
		|					ИСТИНА
		|				ИЗ
		|					&ТаблицаДокумента КАК Документ
		|				ГДЕ
		|					РеестрДокументов.Ссылка = Документ.Ссылка)";
	
	ШаблонОбъединения = "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|";
	
	ТекстЗапроса = "";
	Для Каждого ТипДокумента Из ТипыДокументов Цикл
		
		МетаданныеДокумента = Метаданные.НайтиПоТипу(ТипДокумента);
		ИмяДокумента        = МетаданныеДокумента.Имя;
		
		ТекстЗапроса = ТекстЗапроса + ?(ТекстЗапроса = "", "", ШаблонОбъединения)
			+ СтрЗаменить(ШаблонЗапроса, "&УсловиеСсылкиРеестра", 
				"РеестрДокументов.Ссылка ССЫЛКА Документ." + ИмяДокумента);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаДокумента", "Документ." + ИмяДокумента);
		
	КонецЦикла;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Удаляет набор записей в регистре сведений "Реестр документов" с отбором по переданной ссылке на документ.
//
// Параметры:
//	СсылкаНаДокумент - ДокументСсылка - ссылка на документ в информационной базе.
//
Процедура УдалитьНаборЗаписейВРеестре(СсылкаНаДокумент)
	
	НаборЗаписей = РегистрыСведений.РеестрДокументов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Ссылка.Установить(СсылкаНаДокумент);
	
	НаборЗаписей.ОбменДанными.Загрузка = Истина;
	
	// записываем набор записей
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли