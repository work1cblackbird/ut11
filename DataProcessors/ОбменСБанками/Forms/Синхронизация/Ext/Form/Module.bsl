
#Область ОписаниеПеременных

&НаКлиенте
Перем ТекущаяНастройкаОбмена;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ОбменСБанкамиСлужебныйВызовСервера.ПравоВыполненияОбмена() Тогда
		ТекстСообщения = НСтр("ru = 'Недостаточно прав для выполнения обмена с банком.
									|Синхронизация отменена.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;
	
	Для каждого ЭлементКоллекции Из Параметры.МассивНастроекОбмена Цикл
		НовСтрока = НастройкиОбмена.Добавить();
		НовСтрока.НастройкаОбмена = ЭлементКоллекции;
		ДатаПоследнейСинхронизации = ОбменСБанкамиСлужебный.ДатаПоследнейСинхронизации(ЭлементКоллекции);
		НовСтрока.ДатаПоследнейСинхронизации = ОбменДаннымиСервер.ОтносительнаяДатаСинхронизации(ДатаПоследнейСинхронизации);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	Настройки.Удалить("НастройкиОбмена");
	Если ЗапомнитьВыбор Тогда
		МассивВыбранныхНастроек = Новый Массив;
		Для каждого ЭлементКоллекции Из НастройкиОбмена Цикл
			Если ЭлементКоллекции.Выбран Тогда
				МассивВыбранныхНастроек.Добавить(ЭлементКоллекции.НастройкаОбмена);
			КонецЕсли;
		КонецЦикла;
	
		Настройки.Вставить("МассивВыбранныхНастроек", МассивВыбранныхНастроек);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	МассивВыбранныхНастроек = Настройки.Получить("МассивВыбранныхНастроек");
	
	Если МассивВыбранныхНастроек <> Неопределено Тогда
		Для каждого ЭлементКоллекции Из НастройкиОбмена Цикл
			Если МассивВыбранныхНастроек.Найти(ЭлементКоллекции.НастройкаОбмена) <> Неопределено Тогда
				ЭлементКоллекции.Выбран = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы Тогда
		ПриЗакрытииНаСервере(ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНастройкиОбмена

&НаКлиенте
Процедура НастройкиОбменаВыбранПриИзменении(Элемент)
	
	СохраняемыеВНастройкахДанныеМодифицированы = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьСинхронизацию(Команда)
	
	ОчиститьСообщения();
	Для каждого ЭлементКоллекции Из НастройкиОбмена Цикл
		ЭлементКоллекции.Выполнено = Ложь;
		ЭлементКоллекции.Картинка = Новый Картинка;
	КонецЦикла;

	Синхронизация()
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьФлажки(Команда)
	
	Для каждого ЭлементКоллекции Из НастройкиОбмена Цикл
		ЭлементКоллекции.Выбран = Ложь;
	КонецЦикла;
	
	СохраняемыеВНастройкахДанныеМодифицированы = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для каждого ЭлементКоллекции Из НастройкиОбмена Цикл
		ЭлементКоллекции.Выбран = Истина;
	КонецЦикла;
	
	СохраняемыеВНастройкахДанныеМодифицированы = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Инвертировать(Команда)
	
	Для каждого ЭлементКоллекции Из НастройкиОбмена Цикл
		ЭлементКоллекции.Выбран = НЕ ЭлементКоллекции.Выбран;
	КонецЦикла;
	
	СохраняемыеВНастройкахДанныеМодифицированы = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Синхронизация()
	
	Для каждого ЭлементКоллекции Из НастройкиОбмена Цикл
		Если ЭлементКоллекции.Выбран И Не ЭлементКоллекции.Выполнено Тогда
			ЭлементКоллекции.Выполнено = Истина;
			ЭлементКоллекции.Картинка = БиблиотекаКартинок.ДлительнаяОперация16;
			ТекущаяНастройкаОбмена = ЭлементКоллекции.НастройкаОбмена;
			Оповещение = Новый ОписаниеОповещения("ПослеСинхронизации", ЭтотОбъект);
			ПослеЗапускаДлительнойОперации = Новый ОписаниеОповещения("ПослеЗапускаДлительнойОперации", ЭтотОбъект);
			ОбменСБанкамиСлужебныйКлиент.СинхронизироватьСБанком(Оповещение, ТекущаяНастройкаОбмена, Ложь, ПослеЗапускаДлительнойОперации);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСинхронизации(Результат, ДополнительныеПараметры) Экспорт
	
	Для каждого ЭлементКоллекции Из НастройкиОбмена Цикл
		Если ЭлементКоллекции.НастройкаОбмена = ТекущаяНастройкаОбмена Тогда
			ЭлементКоллекции.ДатаПоследнейСинхронизации = ОтносительнаяДатаСинхронизации(ЭлементКоллекции.НастройкаОбмена);
			Если Результат Тогда
				ЭлементКоллекции.Картинка = БиблиотекаКартинок.Успешно;
			Иначе
				ЭлементКоллекции.Картинка = БиблиотекаКартинок.СостояниеОбменаДаннымиОшибка;
			КонецЕсли;
			
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ПодключитьОбработчикОжидания("Синхронизация", 0.1, Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОтносительнаяДатаСинхронизации(Знач НастройкаОбмена)
	
	ДатаПоследнейСинхронизации = ОбменСБанкамиСлужебный.ДатаПоследнейСинхронизации(НастройкаОбмена);
	Возврат ОбменДаннымиСервер.ОтносительнаяДатаСинхронизации(ДатаПоследнейСинхронизации);
	
КонецФункции

&НаКлиенте
Процедура НастройкиОбменаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущийЭлемент = Элементы.НастройкиОбменаНастройкаОбмена Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение( , Элемент.ТекущиеДанные.НастройкаОбмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗапускаДлительнойОперации(Результат, ДополнительныеПараметры) Экспорт
	
	ИдентификаторЗадания = Результат;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПриЗакрытииНаСервере(Знач ИдентификаторЗадания)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры


#КонецОбласти