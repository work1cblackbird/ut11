
#Область ОписаниеПеременных

&НаКлиенте
Перем ПараметрыОповещенияДляПродолженияВызова;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.Страницы.ПодчиненныеЭлементы.Основная;
	ТекстСообщения = НСтр("ru = 'Пожалуйста, подождите...'");
	Если Не ПустаяСтрока(Параметры.ТекстСообщения) Тогда
		ТекстСообщения = Параметры.ТекстСообщения + Символы.ПС + ТекстСообщения;
		Элементы.ДекорацияПоясняющийТекстДлительнойОперации.Заголовок = ТекстСообщения;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбменСБанкамиИдентификаторДлительнойОперации" Тогда
		ИдентификаторЗадания = Параметр;
	КонецЕсли;
	
	Если ИмяСобытия = "ОбменСБанкамиОкноОповещенияОбОперацияхЕстьОшибка" Тогда
		Если КлючУникальности = Параметр Тогда
			Элементы.Страницы.ТекущаяСтраница = Элементы.Страницы.ПодчиненныеЭлементы.Ошибка;
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяСобытия = "ОбменСБанкамиОкноОповещенияОбОперациях" Тогда
		Если КлючУникальности = Параметр Тогда
			Закрыть();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПодписатьДокументыКодомИзSMS_ПродолжитьВызов(Результат, ПолученныеДополнительныеПараметры) Экспорт
	
	ПараметрыОповещенияДляПродолженияВызова = ПолученныеДополнительныеПараметры;
	ПодключитьОбработчикОжидания("Подключаемый_ПодписатьДокументыКодомИзSMS", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПодписатьДокументыКодомИзSMS()
	
	Результат = Неопределено;
	ПодписатьДокументыКодомИзSMS = Новый ОписаниеОповещения(
		"ПодписатьДокументыКодомИзSMS", ОбменСБанкамиСлужебныйКлиент, ПараметрыОповещенияДляПродолженияВызова);
	ПараметрыОповещенияДляПродолженияВызова = Неопределено;
	ВыполнитьОбработкуОповещения(ПодписатьДокументыКодомИзSMS, Результат);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтменитьВыполнениеЗадания(ИдентификаторЗадания)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

#КонецОбласти