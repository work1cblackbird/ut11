
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// 1- для справочника "Форматы магазинов", 2 - для справочника "Склады"
	Параметры.Свойство("РежимФормы", РежимФормы);
	
	РазрешитьРедактированиеВМагазинеПоддерживаетсяСборкаЗаказов           = Истина;
	РазрешитьРедактированиеВМагазинеПоддерживаетсяДоставкаСвоимиКурьерами = Истина;
	РазрешитьРедактированиеДатаНачалаДоставкиСвоимиКурьерами              = Истина; 
	РазрешитьРедактированиеДатаНачалаСборкиЗаказов                        = Истина; 

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	УстановитьВидимостьДоступность();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РазрешитьРедактирование(Команда)

	Результат = Новый Массив;

	Если РазрешитьРедактированиеВМагазинеПоддерживаетсяСборкаЗаказов Тогда
		Результат.Добавить("ВМагазинеПоддерживаетсяСборкаЗаказов");
	КонецЕсли;
	
	Если РазрешитьРедактированиеВМагазинеПоддерживаетсяДоставкаСвоимиКурьерами Тогда
		Результат.Добавить("ВМагазинеПоддерживаетсяДоставкаСвоимиКурьерами");
	КонецЕсли;
	
	Если РежимФормы = 2 Тогда
		Если РазрешитьРедактированиеДатаНачалаДоставкиСвоимиКурьерами Тогда
			Результат.Добавить("ДатаНачалаДоставкиСвоимиКурьерами");
		КонецЕсли;
			
		Если РазрешитьРедактированиеДатаНачалаСборкиЗаказов Тогда
			Результат.Добавить("ДатаНачалаСборкиЗаказов");
		КонецЕсли;
	КонецЕсли;
	
	Закрыть(Результат);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьДоступность()
	
	ЭлементФормы = Элементы.Найти("ГруппаРазрешитьРедактированиеДатаНачалаСборкиЗаказов");
	Если ЭлементФормы <> Неопределено Тогда
		ЭлементФормы.Видимость = (РежимФормы = 2);
	КонецЕсли;
	
	ЭлементФормы = Элементы.Найти("ГруппаРазрешитьРедактированиеДатаНачалаДоставкиСвоимиКурьерами");
	Если ЭлементФормы <> Неопределено Тогда
		ЭлементФормы.Видимость = (РежимФормы = 2);
	КонецЕсли;
	
	ИспользоватьУправлениеДоставкой = ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеДоставкой");
	
	ЭлементФормы = Элементы.Найти("ГруппаДоставкаКурьерами");
	Если ЭлементФормы <> Неопределено Тогда
		ЭлементФормы.Видимость = ИспользоватьУправлениеДоставкой;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
