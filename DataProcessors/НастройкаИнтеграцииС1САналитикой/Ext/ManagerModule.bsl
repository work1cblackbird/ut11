#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Используемый в ИБ адрес сервера системы 1С:Аналитика
// (Функции для технического специалиста - Управление системой аналитики).
// 
// Возвращаемое значение:
//  Строка - Адрес сервера системы 1С:Аналитика.
//
Функция АдресСервера() Экспорт
	Возврат СистемаАналитики.ПолучитьАдресСервераСистемыАналитики();
КонецФункции

// Установить адрес подключения сервера системы 1С:Аналитика.
// 
// Параметры:
//  АдресСервера - Строка - Новый адрес сервера 1С:Аналитика.
// 
// Возвращаемое значение:
//  Булево - Новый адрес сервера 1С:Аналитика установлен успешно.
//
Функция УстановитьАдресСервера(АдресСервера) Экспорт
	
	АдресСервераТекущий = АдресСервера();
	СистемаАналитики.УстановитьАдресСервераСистемыАналитики(АдресСервера);
	
	СерверДоступен = СерверСистемыАналитикиДоступен();
	Если Не СерверСистемыАналитикиДоступен() Тогда
		СистемаАналитики.УстановитьАдресСервераСистемыАналитики(АдресСервераТекущий);
	КонецЕсли;
	
	Возврат СерверДоступен;
	
КонецФункции

// Проверяет адрес сервера системы 1С:Аналитика.
// 
// Параметры:
//  АдресСервера - Строка - Новый адрес сервера 1С:Аналитика.
// 
// Возвращаемое значение:
//  Булево - Новый адрес сервера 1С:Аналитика установлен успешно.
//
Функция ПроверитьДоступностьСервераАналитикиАдресу(АдресСервера) Экспорт

	АдресСервераТекущий = АдресСервера();
	СистемаАналитики.УстановитьАдресСервераСистемыАналитики(АдресСервера);
	
	СерверДоступен = СерверСистемыАналитикиДоступен();
	СистемаАналитики.УстановитьАдресСервераСистемыАналитики(АдресСервераТекущий);
	
	Возврат СерверДоступен;
	
КонецФункции

// Доступность сервера системы 1С:Аналитика.
// 
// Возвращаемое значение:
//  Булево - Сервер системы 1С:Аналитика доступен.
//
Функция СерверСистемыАналитикиДоступен() Экспорт
	
	Попытка
		СистемаАналитики.ПолучитьСоединение().ПолучитьСхему("objects");
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

// Очистить адрес сервера.
Процедура ОчиститьАдресСервера() Экспорт
	
	СистемаАналитики.УстановитьАдресСервераСистемыАналитики("");
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли