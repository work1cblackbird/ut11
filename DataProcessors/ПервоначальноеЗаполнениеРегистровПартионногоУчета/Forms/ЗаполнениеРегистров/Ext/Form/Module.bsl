#Область ОписаниеПеременных

&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Далее(Команда)
	
	Если Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаРезультат Тогда
		
		Если ПереходКНастройке Тогда
			ОткрытьФорму("Обработка.РегламентныеИФоновыеЗадания.Форма.РегламентныеИФоновыеЗадания"); 
		КонецЕсли;
		
		Закрыть();
		
	Иначе
		
		Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.ВыполняетсяЗаполнение;
		ПодключитьОбработчикОжидания("ВыполнитьЗаполнение", 1, Истина);
		
	КонецЕсли;
	
	УстановитьИзмененияВИнтерфейсе();

КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ЗавершитьФоновоеЗадание(ИдентификаторЗадания);
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьДанныеНаСервере(Параметры)
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		Обработки.ПервоначальноеЗаполнениеРегистровПартионногоУчета.ДополнитьДвиженияДокументов(Параметры, АдресХранилища);
		Результат = Новый Структура("ЗаданиеВыполнено,АдресХранилища", Истина, АдресХранилища);
	Иначе	
		НаименованиеЗадания = НСтр("ru = 'Проведение по регистрам партий'");
		
		Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
			УникальныйИдентификатор,
			"Обработки.ПервоначальноеЗаполнениеРегистровПартионногоУчета.ДополнитьДвиженияДокументов",
			Параметры, 
			НаименованиеЗадания);
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции

&НаКлиенте
Процедура ЗагрузитьРезультат()
	
	ПараметрыЗагрузки = ПолучитьИзВременногоХранилища(АдресХранилища);
	Источник = "";
	
	ИмяСобытия = НСтр("ru = 'Партионный учет сервер. Первоначальное заполнение данных документов по регистрам партий, НДС, сумм в валюте регл'");
	
	Если ПараметрыЗагрузки["ЗагрузкаВыполнена"] Тогда
		ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(ИмяСобытия,, 
			ПараметрыЗагрузки["ТекстСообщения"],, Истина);
	Иначе
		ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(ИмяСобытия, 
			"Ошибка", ПараметрыЗагрузки["ТекстСообщения"],, Истина);
		Элементы.ПоясняющийТекст.Заголовок = ПараметрыЗагрузки["ТекстСообщения"];
	КонецЕсли;

	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаРезультат;
		
	УстановитьИзмененияВИнтерфейсе();
	
КонецПроцедуры

// Унифицированная процедура проверки выполнения фонового задания
&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()
	
	Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда 
		ЗагрузитьРезультат();
	Иначе
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПроверитьВыполнениеЗадания", 
			ПараметрыОбработчикаОжидания.ТекущийИнтервал, 
			Истина);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

// В зависимости от текущей страницы устанавливает доступность тех или иных полей для пользователя.
//
&НаСервере
Процедура УстановитьИзмененияВИнтерфейсе()
	
	Если Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.ЗаполнениеРегистров Тогда
		Элементы.ФормаКнопкаДалее.Заголовок    = НСтр("ru ='Начать проведение >>'");
		Элементы.ФормаКнопкаОтмена.Доступность = Истина;
		Элементы.ФормаКнопкаДалее.Доступность  = Истина;
	ИначеЕсли Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.ВыполняетсяЗаполнение Тогда
		Элементы.ФормаКнопкаДалее.Видимость  = Ложь;
		Элементы.ФормаКнопкаОтмена.Доступность = Истина;
	Иначе
		Элементы.ФормаКнопкаОтмена.Видимость   = Ложь;
		Элементы.ФормаКнопкаДалее.Заголовок    = НСтр("ru ='Готово'");
		Элементы.ФормаКнопкаДалее.Видимость    = Истина;
		Элементы.ФормаКнопкаДалее.Доступность  = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗавершитьФоновоеЗадание(ИдентификаторЗадания)
	
	ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
	Если ФоновоеЗадание <> Неопределено Тогда
		ФоновоеЗадание.Отменить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанные()
	
	ПараметрыЗагрузки = Новый Соответствие;
	ПараметрыЗагрузки.Вставить("ТекстСообщения", "");
	ПараметрыЗагрузки.Вставить("ЗагрузкаВыполнена", Ложь);
	
	Результат = ПолучитьДанныеНаСервере(ПараметрыЗагрузки);
	
	АдресХранилища = Результат.АдресХранилища;
	Если НЕ Результат.ЗаданиеВыполнено Тогда
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
	Иначе
		ЗагрузитьРезультат();
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ВыполнитьЗаполнение()
	
	ЗаполнитьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПереходКНастройке = Истина;
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.ЗаполнениеРегистров;
	УстановитьИзмененияВИнтерфейсе();

КонецПроцедуры

#КонецОбласти
