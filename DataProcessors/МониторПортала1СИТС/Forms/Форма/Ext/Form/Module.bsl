///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не МониторПортала1СИТС.ДоступноИспользованиеМонитора() Тогда
		ВызватьИсключение НСтр("ru = 'Использование Монитора Портала 1С:ИТС недоступно в текущем режиме работы.'");
	КонецЕсли;
	
	ОбрабатыватьСобытиеПриОткрытии = Истина;
	ЭтоWindowsКлиент               = ОбщегоНазначения.ЭтоWindowsКлиент();
	
	ПараметрыОбработчикаПриОткрытии = Новый Структура;
	Если Не ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки() Тогда
		ПараметрыОбработчикаПриОткрытии.Вставить("ПодключитьИнтернетПоддержку", Истина);
		Возврат;
	Иначе
		ПараметрыОбработчикаПриОткрытии.Вставить("ПодключитьИнтернетПоддержку", Ложь);
	КонецЕсли;
	
	МониторПортала1СИТС.ПриСозданииНаСервере(ЭтотОбъект);
	
	ПараметрыСоздания = Новый Структура;
	ПараметрыСоздания.Вставить("СсылкиСмТакже", НовыйТаблицаСсылкиСмТакже());
	ИнтеграцияПодсистемБИП.ПриСозданииФормыМонитора(
		ЭтотОбъект,
		ПараметрыСоздания);
	МониторПортала1СИТСПереопределяемый.ПриСозданииФормыМонитора(
		ЭтотОбъект,
		ПараметрыСоздания);
	ЗаполнитьСсылкиСмТакже(ПараметрыСоздания.СсылкиСмТакже);
	
	ОчиститьКонтекстФормы();
	ИнициализироватьОписаниеЗаданий();
	
	НачатьОбновлениеСодержимогоМонитораНаСервере(Ложь, Параметры.ПриНачалеРаботы);
	
	ДоступнаОтправкаСообщенийТехПоддержке
		= ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СообщенияВСлужбуТехническойПоддержки");
	Элементы.ДекорацияСсылкаНаписатьВТехподдержку.Видимость = ДоступнаОтправкаСообщенийТехПоддержке;
	Если Не ДоступнаОтправкаСообщенийТехПоддержке Тогда
		Элементы.ДекорацияИПППродуктаНеОказываетсяОписание.Заголовок
			= НСтр("ru = 'Интернет-поддержка этого программного продукта не оказывается. Обратитесь к партнеру,
				|у которого вы приобрели эту программу.'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ОбрабатыватьСобытиеПриОткрытии Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыОбработчикаПриОткрытии.ПодключитьИнтернетПоддержку Тогда
		Отказ = Истина;
		МониторПортала1СИТСКлиент.ОткрытьМониторСПодключениемИнтернетПоддержки();
		Возврат;
	КонецЕсли;
	
	ПриНачалеЗадания("СМ");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы Тогда
		//@skip-warning
		ОтключитьОбработчикОжидания("Подключаемый_ПроверитьСостояниеЗаданий");
		ОтключитьОбработчикОжидания("Подключаемый_ОбработатьОжидание");
		Если ЕстьАктивныеЗадания(Задания) Тогда
			ОтменитьВыполнениеВсехЗаданий();
		КонецЕсли;
	КонецЕсли;
	
	ИнтеграцияПодсистемБИПКлиент.ПриЗакрытииФормыМонитора(
		ЭтотОбъект,
		ЗавершениеРаботы);
	МониторПортала1СИТСКлиентПереопределяемый.ПриЗакрытииФормыМонитора(
		ЭтотОбъект,
		ЗавершениеРаботы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИнтернетПоддержкаПодключена" Тогда
		НачатьОбновлениеСодержимогоМонитора();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказыватьПриНачалеРаботыПриИзменении(Элемент)
	
	НастройкаПоказПриСтартеПриИзмененииНаСервере(ПоказыватьПриНачалеРаботы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСсылкаЖурналРегистрацииНажатие(Элемент)
	
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", Новый Структура("Пользователь", ИмяПользователя()));
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСсылкаНастройкиПроксиНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаПроксиНаКлиенте", (ВРег(Лев(СтрокаСоединенияИнформационнойБазы(), 5)) = "FILE="));
	ПолучениеФайловИзИнтернетаКлиент.ОткрытьФормуПараметровПроксиСервера(ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСсылкаНаписатьВТехподдержкуНажатие(Элемент)
	
	ОтправитьСообщениеВТехПоддержку(
		НСтр("ru = 'Не удалось получить данные Монитора Портала 1С:ИТС.'")
		+ Символы.ПС
		+ ИнтернетПоддержкаПользователейКлиент.ТекстФорматированногоЗаголовка(
			Элементы.ДекорацияОшибкаЗаголовок.Заголовок)
		+ Символы.ПС
		+ ПодробноеОписаниеОшибкиДляТехПоддержки);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИПППродуктаНеОказываетсяОписаниеОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтправитьСообщениеВТехПоддержку(
		НСтр("ru = 'Интернет-поддержка программного продукта не оказывается.'"));
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьПоясненияПодключенияАвторизацияОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "action:openPortal" Тогда
		СтандартнаяОбработка = Ложь;
		ИнтернетПоддержкаПользователейКлиент.ОткрытьВебСтраницу(
			ИнтернетПоддержкаПользователейКлиентСервер.URLСтраницыСервисаLogin(
				,
				ИнтернетПоддержкаПользователейКлиент.НастройкиСоединенияССерверами()));
	КонецЕсли;
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ДекорацияНажатие(Элемент)
	
	ИнтеграцияПодсистемБИПКлиент.ПриНажатииДекорацииВФормеМонитора(
		ЭтотОбъект,
		Элемент);
	МониторПортала1СИТСКлиентПереопределяемый.ПриНажатииДекорацииВФормеМонитора(
		ЭтотОбъект,
		Элемент);
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбработатьНавигационнуюСсылку(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	ИнтеграцияПодсистемБИПКлиент.ОбработатьНавигационнуюСсылкуВФормеМонитора(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	МониторПортала1СИТСКлиентПереопределяемый.ОбработатьНавигационнуюСсылкуВФормеМонитора(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбработатьКоманду(Команда)
	
	ИнтеграцияПодсистемБИПКлиент.ОбработатьКомандуВФормеМонитора(
		ЭтотОбъект,
		Команда);
	МониторПортала1СИТСКлиентПереопределяемый.ОбработатьКомандуВФормеМонитора(
		ЭтотОбъект,
		Команда);
	
КонецПроцедуры

#Область БлокШапка

&НаКлиенте
Процедура ЛогинНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнтернетПоддержкаПользователейКлиент.ОткрытьЛичныйКабинетПользователя();
	
КонецПроцедуры

#КонецОбласти

#Область БлокУсловияСопровожденияВыполнены

&НаКлиенте
Процедура ДекорацияУсловияВыполненыДеталиНажатие(Элемент)
	
	ДекорацияУсловияВыполненыДеталиНажатиеНаСервере();
	ПриНачалеЗадания("СВУ");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКакОформитьДоговорИТСУсловияВыполненыСсылкаНажатие(Элемент)
	
	Элементы.ДекорацияКакОформитьДоговорИТСУсловияВыполненыИнструкция.Видимость =
		Не Элементы.ДекорацияКакОформитьДоговорИТСУсловияВыполненыИнструкция.Видимость;
	Элементы.ДекорацияКакОформитьДоговорИТСУсловияВыполненыСсылка.Заголовок =
		ЗаголовокГруппы(
			ЗаголовокКакПродлитьДействиеДоговора(),
			Элементы.ДекорацияКакОформитьДоговорИТСУсловияВыполненыИнструкция.Видимость);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКакАктивироватьСервисСопровожденияУсловияВыполненыСсылкаНажатие(Элемент)
	
	Элементы.ДекорацияКакАктивироватьСервисСопровожденияУсловияВыполненыИнструкция.Видимость =
		Не Элементы.ДекорацияКакАктивироватьСервисСопровожденияУсловияВыполненыИнструкция.Видимость;
	Элементы.ДекорацияКакАктивироватьСервисСопровожденияУсловияВыполненыСсылка.Заголовок =
		ЗаголовокГруппы(
			ЗаголовокКакПродлитьДействиеСервиса(),
			Элементы.ДекорацияКакАктивироватьСервисСопровожденияУсловияВыполненыИнструкция.Видимость);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКакОформитьДоговорИТСУсловияВыполненыИнструкцияОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	ОбработатьНавигационнуюСсылку(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКакАктивироватьСервисСопровожденияУсловияВыполненыИнструкцияОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	ОбработатьНавигационнуюСсылку(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область БлокНеВыполненыУсловияСопровождения

&НаКлиенте
Процедура ДекорацияПравилаСопровожденияППСсылкаНажатие(Элемент)
	
	Элементы.ГруппаНеВыполненыУсловияСопровожденияДетали.Видимость =
		Не Элементы.ГруппаНеВыполненыУсловияСопровожденияДетали.Видимость;
	Элементы.ДекорацияПравилаСопровожденияППСсылка.Заголовок =
		ЗаголовокГруппы(ЗаголовокПравилаСопровожденияПП(), Элементы.ГруппаНеВыполненыУсловияСопровожденияДетали.Видимость);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКакЗарегистрироватьПродуктСсылкаНажатие(Элемент)
	
	ИнтернетПоддержкаПользователейКлиент.ОткрытьСтраницуОфициальнаяПоддержка(
		"#register-portal");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКакЗаключитьДоговорИТССсылкаНажатие(Элемент)
	
	Элементы.ДекорацияКакЗаключитьДоговорИТСИнструкция.Видимость =
		Не Элементы.ДекорацияКакЗаключитьДоговорИТСИнструкция.Видимость;
	Элементы.ДекорацияКакЗаключитьДоговорИТССсылка.Заголовок =
		ЗаголовокГруппы(
			ЗаголовокКакОформитьДоговорИТС(),
			Элементы.ДекорацияКакЗаключитьДоговорИТСИнструкция.Видимость);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКакАктивироватьСервисСопровожденияСсылкаНажатие(Элемент)
	
	Элементы.ДекорацияКакАктивироватьСервисСопровожденияИнструкция.Видимость =
		Не Элементы.ДекорацияКакАктивироватьСервисСопровожденияИнструкция.Видимость;
	Элементы.ДекорацияКакАктивироватьСервисСопровожденияСсылка.Заголовок =
		ЗаголовокГруппы(
			ЗаголовокКакАктивироватьСервисСопровождения(),
			Элементы.ДекорацияКакАктивироватьСервисСопровожденияИнструкция.Видимость);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКакЗаключитьДоговорИТСИнструкцияОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	ОбработатьНавигационнуюСсылку(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКакАктивироватьСервисСопровожденияИнструкцияОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	ОбработатьНавигационнуюСсылку(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияДоговорИТССсылкаДеталиНажатие(Элемент)
	
	ДекорацияДоговорИТССсылкаДеталиНажатиеНаСервере();
	ПриНачалеЗадания("ИТСНУ");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСервисыСопровожденияДеталиСсылкаНажатие(Элемент)
	
	ДекорацияСервисыСопровожденияДеталиСсылкаНажатиеНаСервере();
	ПриНачалеЗадания("СервисыСопровожденияНевыполненныеУсловия");
	
КонецПроцедуры

#КонецОбласти

#Область БлокДоговоры1С

&НаКлиенте
Процедура ДекорацияКакПродлитьСервис1ССсылкаНажатие(Элемент)
	
	Элементы.ДекорацияКакПродлитьСервис1СИнструкция.Видимость =
		Не Элементы.ДекорацияКакПродлитьСервис1СИнструкция.Видимость;
	Элементы.ДекорацияКакПродлитьСервис1ССсылка.Заголовок =
		ЗаголовокГруппы(
			ЗаголовокКакПродлитьСрокДействияСервиса(),
			Элементы.ДекорацияКакПродлитьСервис1СИнструкция.Видимость);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСервисы1СДеталиСсылкаНажатие(Элемент)
	
	ДекорацияДоговорыНаСервисыДеталиСсылкаНажатиеНаСервере();
	ПриНачалеЗадания("Сервисы1С");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКакПродлитьСервис1СИнструкцияОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	ОбработатьНавигационнуюСсылку(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКакПодключитьСервис1ССсылкаНажатие(Элемент)
	
	Элементы.ДекорацияКакПодключитьСервис1СИнструкция.Видимость =
		Не Элементы.ДекорацияКакПодключитьСервис1СИнструкция.Видимость;
	Элементы.ДекорацияКакПодключитьСервис1ССсылка.Заголовок =
		ЗаголовокГруппы(
			ЗаголовокКакОформитьДоговорыНаСервисы(),
			Элементы.ДекорацияКакПодключитьСервис1СИнструкция.Видимость);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСервисы1СПояснениеОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	ОбработатьНавигационнуюСсылку(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКакПодключитьСервис1СИнструкцияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ОбработатьНавигационнуюСсылку(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область БлокОбновлениеПрограммы

&НаКлиенте
Процедура ДекорацияИнформацияОбОбновленииОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	ОбработатьНавигационнуюСсылку(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияУстановитьОбновлениеНажатие(Элемент)
	
	МодульПолучениеОбновленийПрограммыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПолучениеОбновленийПрограммыКлиент");
	МодульПолучениеОбновленийПрограммыКлиент.ОбновитьПрограмму();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОбновлениеПрограммыДеталиСсылкаНажатие(Элемент)
	
	ДекорацияОбновлениеПрограммыДеталиСсылкаНажатиеНаСервере();
	ПриНачалеЗадания("ИнформацияОбОбновлении");
	
КонецПроцедуры

#КонецОбласти

#Область ПредупрежденияСервисов1С

&НаКлиенте
Процедура ДекорацияПредупрежденияСервисов1СДеталиСсылкаНажатие(Элемент)
	
	ДекорацияПредупрежденияСервисовДеталиСсылкаНажатиеНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СмТакже

&НаКлиенте
Процедура ДекорацияСмТакжеКолонкаОдинОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	ОбработатьНавигационнуюСсылку(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСмТакжеКолонкаДваОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	ОбработатьНавигационнуюСсылку(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПовторитьПопыткуПодключения(Команда)
	
	// Проверка заполнения данных авторизации
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОшибка
		И Элементы.ГруппаПодключениеКПорталу.Видимость
		И Не ЗаполнениеЛогинаИПароляКорректно() Тогда
		
		Возврат;
		
	КонецЕсли;
	
	НачатьОбновлениеСодержимогоМонитора();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСодержимое(Команда)
	
	НачатьОбновлениеСодержимогоМонитора();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбщегоНазначения

&НаСервереБезКонтекста
Процедура НастройкаПоказПриСтартеПриИзмененииНаСервере(Знач НастройкаПоказПриСтарте)
	
	МониторПортала1СИТС.СохранитьНастройкиПоказПриСтарте(НастройкаПоказПриСтарте);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСообщениеВТехПоддержку(ТекстСообщения)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СообщенияВСлужбуТехническойПоддержки") Тогда
		
		МодульСообщенияВСлужбуТехническойПоддержкиКлиентСервер =
			ОбщегоНазначенияКлиент.ОбщийМодуль("СообщенияВСлужбуТехническойПоддержкиКлиентСервер");
		ДанныеСообщения = МодульСообщенияВСлужбуТехническойПоддержкиКлиентСервер.ДанныеСообщения();
		ДанныеСообщения.Получатель = "webIts";
		ДанныеСообщения.Тема       = НСтр("ru = 'Интернет-поддержка. Монитор Портала 1С:ИТС'");
		ДанныеСообщения.Сообщение  = ТекстСообщения;
		
		МодульСообщенияВСлужбуТехническойПоддержкиКлиентСервер.ЗаполнитьНастройкиПоискаГотовогоОтвета(
			ТекстСообщения,
			ДанныеСообщения.НастройкиПоискаГотовогоОтвета);
		
		МодульСообщенияВСлужбуТехническойПоддержкиКлиент =
			ОбщегоНазначенияКлиент.ОбщийМодуль("СообщенияВСлужбуТехническойПоддержкиКлиент");
		МодульСообщенияВСлужбуТехническойПоддержкиКлиент.ОтправитьСообщение(
			ДанныеСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНавигационнуюСсылку(Элемент, Ссылка, СтандартнаяОбработка)
	
	СсылкаНРег = НРег(СокрЛП(Ссылка));
	Если СтрНачинаетсяС(СсылкаНРег, "action:openportal") Тогда
		
		СтандартнаяОбработка = Ложь;
		ИнтернетПоддержкаПользователейКлиент.ОткрытьГлавнуюСтраницуПортала();
		
	ИначеЕсли СтрНачинаетсяС(СсылкаНРег, "action:profile") Тогда
		
		СтандартнаяОбработка = Ложь;
		ИнтернетПоддержкаПользователейКлиент.ОткрытьЛичныйКабинетПользователя();
		
	ИначеЕсли СтрНачинаетсяС(СсылкаНРег, "action:techsuppmonitorproblem") Тогда
		
		СтандартнаяОбработка = Ложь;
		ТекстСообщения = НСтр("ru = 'При использовании Монитора Портала 1С:ИТС возникли следующие проблемы:
			|<Опишите проблемы>'");
		ОтправитьСообщениеВТехПоддержку(ТекстСообщения);
		
	ИначеЕсли СтрНачинаетсяС(СсылкаНРег, "http://") Или СтрНачинаетсяС(СсылкаНРег, "https://") Тогда
		
		СтандартнаяОбработка = Ложь;
		ИнтернетПоддержкаПользователейКлиент.ОткрытьВебСтраницуСДополнительнымиПараметрами(Ссылка);
		
	ИначеЕсли СтрНачинаетсяС(СсылкаНРег, "mailto:") Тогда
		
		СтандартнаяОбработка = Ложь;
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(Ссылка);
		
	Иначе
		
		ИнтеграцияПодсистемБИПКлиент.ОбработатьНавигационнуюСсылкуВФормеМонитора(
			ЭтотОбъект,
			Элемент,
			Ссылка,
			СтандартнаяОбработка);
		МониторПортала1СИТСКлиентПереопределяемый.ОбработатьНавигационнуюСсылкуВФормеМонитора(
			ЭтотОбъект,
			Элемент,
			Ссылка,
			СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция МаркированныйСписокСПериодами(ДанныеСписка, СписокСервисов1С)
	
	Если ДанныеСписка = Неопределено
		Или ДанныеСписка.serviceContracts = Неопределено
		Или ДанныеСписка.serviceContracts.Количество() = 0 Тогда
		Возврат НСтр("ru = '<Нет данных>'");
	КонецЕсли;
	
	СписокДоговоров = ДанныеСписка.serviceContracts;
	
	//@skip-check use-non-recommended-method
	ТекДата       = ТекущаяДата(); // АПК:143 используется для расчета смещения.
	ТекДатаСеанса = ТекущаяДатаСеанса();
	Смещение = (ТекДатаСеанса - ТекДата);
	
	ЧастиСтроки = Новый Массив;
	Для ИндексЭлемента = 0 По СписокДоговоров.ВГраница() Цикл
		
		ЭлементСписка = СписокДоговоров[ИндексЭлемента];
		ЧастиСтроки.Добавить(
			?(ИндексЭлемента > 0, Символы.ПС, "")
			+ "● "
			+ ЭлементСписка.name
			+ " ");
		
		// Приведение времени сервера ко времени сеанса.
		ТекущийПериод = ЭлементСписка.period;
		ДатаОт = (ТекущийПериод.from + Смещение);
		ДатаПо = (ТекущийПериод.to + Смещение);
		ЧастиСтроки.Добавить(
			?(СписокСервисов1С И (ДатаОт < ТекДатаСеанса),
				"",
				НСтр("ru = 'с'") + " " + Формат(ДатаОт, "ДЛФ=D") + " ")
			+ ?(СписокСервисов1С, НСтр("ru = 'до'"), НСтр("ru = 'по'"))
			+ " "
			+ ?(ТекущийПериод.warning,
				"<span style=""color:ПредупреждениеМониторПорталаЦвет"">" + Формат(ДатаПо, "ДЛФ=D") + "</span>",
				Формат(ДатаПо, "ДЛФ=D")));
		
	КонецЦикла;
	
	Возврат СтроковыеФункции.ФорматированнаяСтрока(СтрСоединить(ЧастиСтроки));
	
КонецФункции

&НаСервереБезКонтекста
Функция МаркированныйФорматированныйСписокСсылокСмТакже(ТаблицаСсылок, НомерКолонки)
	
	СтрокиТаблицы = ТаблицаСсылок.НайтиСтроки(Новый Структура("Колонка", НомерКолонки));
	Если СтрокиТаблицы.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ЧастиСтроки = Новый Массив;
	Для Итератор = 0 По СтрокиТаблицы.ВГраница() Цикл
		СтрокаТаблицы = СтрокиТаблицы[Итератор];
		ШаблонСтроки = "<span style=""color: ЦветГиперссылкиМониторПортала"">%1  ● <a href=""%2"">%3</a></span>";
		ЧастиСтроки.Добавить(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСтроки,
				?(Итератор > 0, Символы.ПС, ""),
				СтрокаТаблицы.Ссылка,
				СтрокаТаблицы.Заголовок));
	КонецЦикла;
	
	Возврат СтроковыеФункции.ФорматированнаяСтрока(СтрСоединить(ЧастиСтроки));
	
КонецФункции

&НаСервере
Процедура ЗаполнитьЗаголовокИзМакета(ЭлементДекорация, ИмяМакета)
	
	Если ТипЗнч(ЭлементДекорация.Заголовок) <> Тип("ФорматированнаяСтрока") Тогда
		ЭлементДекорация.Заголовок = ТекстИнструкцииИзМакета(ИмяМакета, Домен);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьКонтекстФормы()
	
	ПодробноеОписаниеОшибкиДляТехПоддержки = "";
	
	Элементы.ДекорацияДоговорИТССписокНУ.Заголовок                             = "";
	Элементы.ДекорацияСервисыСопровожденияСписокНевыполненныеУсловия.Заголовок = "";
	
	Элементы.ДекорацияДоговорИТССписок.Заголовок           = "";
	Элементы.ДекорацияСервисыСопровожденияСписок.Заголовок = "";
	
	Элементы.ДекорацияСервисы1ССписок.Заголовок = "";
	
	ОчиститьТекстыИнструкций();
	
	ДанныеМонитора = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьТекстыИнструкций()
	
	Элементы.ДекорацияКакЗаключитьДоговорИТСИнструкция.Заголовок                = "";
	Элементы.ДекорацияКакАктивироватьСервисСопровожденияИнструкция.Заголовок    = "";
	Элементы.ДекорацияКакОформитьДоговорИТСУсловияВыполненыИнструкция.Заголовок = "";
	Элементы.ДекорацияКакПодключитьСервис1СИнструкция.Заголовок            = "";
	Элементы.ДекорацияКакПродлитьСервис1СИнструкция.Заголовок = "";
	Элементы.ДекорацияКакАктивироватьСервисСопровожденияУсловияВыполненыИнструкция.Заголовок = "";
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокГруппы(Заголовок, Развернута)
	Возврат Заголовок + " " + ?(Развернута, "▼", "►");
КонецФункции

&НаСервереБезКонтекста
Функция ТекстИнструкцииИзМакета(ИмяМакета, Домен)
	
	Макет = Обработки.МониторПортала1СИТС.ПолучитьМакет(ИмяМакета + ?(Домен = 0, "_RU", "_EU"));
	ФДок = Новый ФорматированныйДокумент;
	ФДок.УстановитьHTML(Макет.ПолучитьТекст(), Новый Структура);
	Возврат ФДок.ПолучитьФорматированнуюСтроку();
	
КонецФункции

&НаСервереБезКонтекста
Процедура СохранитьОтпечатокДанныхМонитора(Логин, ДанныеМонитора)
	
	Если ДанныеМонитора.supportConditionsImplStatus <> Неопределено
		И ДанныеМонитора.supportConditionsImplStatus.blockStatus <> "200"
		И ДанныеМонитора.supportConditionsImplStatus.blockStatus <> 200
		Или ДанныеМонитора.serviceContractsStatus <> Неопределено
		И ДанныеМонитора.serviceContractsStatus.blockStatus <> "200"
		И ДанныеМонитора.serviceContractsStatus.blockStatus <> 200
		Или ДанныеМонитора.updateInfo <> Неопределено
		И ДанныеМонитора.updateInfo.blockStatus <> "200"
		И ДанныеМонитора.updateInfo.blockStatus <> 200
		Или ДанныеМонитора.patchesInfo <> Неопределено
		И ДанныеМонитора.patchesInfo.blockStatus <> "200"
		И ДанныеМонитора.patchesInfo.blockStatus <> 200 Тогда
		// При ошибке получения данных не сохранять отпечаток.
		Возврат;
	КонецЕсли;
	
	МониторПортала1СИТС.СохранитьОтпечатокДанныхМонитора(Логин, ДанныеМонитора);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьОжидание()
	
	ИнтеграцияПодсистемБИПКлиент.ПриВыполненииОбработчикаОжиданияВФормеМонитора(
		ЭтотОбъект);
	МониторПортала1СИТСКлиентПереопределяемый.ПриВыполненииОбработчикаОжиданияВФормеМонитора(
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция ЗаполнениеЛогинаИПароляКорректно()
	
	Результат = ИнтернетПоддержкаПользователейКлиентСервер.ПроверитьДанныеАутентификации(
		Новый Структура("Логин, Пароль", Логин, ПарольИПП));
	
	Если Результат.Отказ Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			Результат.СообщениеОбОшибке,
			,
			?(Результат.Поле = "Пароль", "ПарольИПП", "ЛогинИПП"));
	КонецЕсли;
	
	Возврат Не Результат.Отказ;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиПолученияДанных

&НаСервере
Процедура ИнициализироватьОписаниеЗаданий()
	
	ИдентификаторЭтойФормы = УникальныйИдентификатор;
	
	НоваяСтрока = Задания.Добавить();
	НоваяСтрока.Имя             = "СМ"; // Содержимое монитора.
	НоваяСтрока.АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, ИдентификаторЭтойФормы);
	
	НоваяСтрока = Задания.Добавить();
	НоваяСтрока.Имя             = "СВУ"; // Содержимое блока выполненных условий.
	НоваяСтрока.АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, ИдентификаторЭтойФормы);
	
	НоваяСтрока = Задания.Добавить();
	НоваяСтрока.Имя             = "ИТСНУ"; // Договор 1С:ИТС в блоке невыполненных условий сопровождения.
	НоваяСтрока.АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, ИдентификаторЭтойФормы);
	
	НоваяСтрока = Задания.Добавить();
	// Сервисы сопровождения в блоке невыполненных условий сопровождения.
	НоваяСтрока.Имя             = "СервисыСопровожденияНевыполненныеУсловия";
	НоваяСтрока.АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, ИдентификаторЭтойФормы);
	
	НоваяСтрока = Задания.Добавить();
	НоваяСтрока.Имя             = "Сервисы1С"; // Сервисы 1С.
	НоваяСтрока.АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, ИдентификаторЭтойФормы);
	
	НоваяСтрока = Задания.Добавить();
	НоваяСтрока.Имя             = "ИнформацияОбОбновлении"; // Обновление программы.
	НоваяСтрока.АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, ИдентификаторЭтойФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриНачалеЗадания(ИмяЗадания)
	
	Если ЕстьАктивныеЗадания(Задания, ИмяЗадания) Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			ОписательЗадания(Задания, ИмяЗадания).ДлительнаяОперация,
			Новый ОписаниеОповещения("ПриЗавершенииЗадания", ЭтотОбъект, ИмяЗадания),
			ПараметрыОжидания);

	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОписательЗадания(ТаблицаЗаданий, Имя)
	
	Для Каждого СтрокаЗадания Из ТаблицаЗаданий Цикл
		Если СтрокаЗадания.Имя = Имя Тогда
			Возврат СтрокаЗадания;
		КонецЕсли;
	КонецЦикла;
	
	ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Неизвестное имя задания (%1).'"),
		Имя);
	
КонецФункции

&НаСервере
Процедура ОтменитьВыполнениеВсехЗаданий()
	
	ПустойИдентификатор = Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000");
	Для Каждого СтрокаЗадания Из Задания Цикл
		Если ЗначениеЗаполнено(СтрокаЗадания.Идентификатор) Тогда
			ДлительныеОперации.ОтменитьВыполнениеЗадания(СтрокаЗадания.Идентификатор);
			СтрокаЗадания.Идентификатор = ПустойИдентификатор;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОтменитьЗадание(Имя)
	
	Для Каждого СтрокаЗадания Из Задания Цикл
		Если СтрокаЗадания.Имя = Имя Тогда
			Если ЗначениеЗаполнено(СтрокаЗадания.Идентификатор) Тогда
				ДлительныеОперации.ОтменитьВыполнениеЗадания(СтрокаЗадания.Идентификатор);
				СтрокаЗадания.Идентификатор = Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000");
			КонецЕсли;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЕстьАктивныеЗадания(ТаблицаЗаданий, ИмяЗадания = Неопределено)
	
	Для Каждого СтрокаЗадания Из ТаблицаЗаданий Цикл
		Если ЗначениеЗаполнено(СтрокаЗадания.Идентификатор)
			И (ИмяЗадания = Неопределено
			Или СтрокаЗадания.Имя = ИмяЗадания) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура ПриЗавершенииЗадания(Результат, ИмяЗадания) Экспорт
	
	Если Не Открыта() Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Очистить параметры в описателе задания.
	ОписательЗадания = ОписательЗадания(Задания, ИмяЗадания);
	ОписательЗадания.ДлительнаяОперация = Неопределено;
	ОписательЗадания.Идентификатор      = Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000");
	
	Если ИмяЗадания = "СМ" Тогда
		Если ПустаяСтрока(Результат.КраткоеПредставлениеОшибки) Тогда
			ПриПолученииСодержимогоМонитора();
		Иначе
			ОтобразитьСостояниеОшибка(
				Результат.КраткоеПредставлениеОшибки,
				Результат.ПодробноеПредставлениеОшибки,
				Ложь);
		КонецЕсли;
	ИначеЕсли ИмяЗадания = "СВУ" Тогда
		Если ПустаяСтрока(Результат.КраткоеПредставлениеОшибки) Тогда
			ПриПолученииДеталиУсловияВыполнены();
		Иначе
			ОтобразитьОшибкаПолученияДеталиУсловияВыполнены(Результат.КраткоеПредставлениеОшибки);
		КонецЕсли;
	ИначеЕсли ИмяЗадания = "ИТСНУ" Тогда
		Если ПустаяСтрока(Результат.КраткоеПредставлениеОшибки) Тогда
			ПриПолученииДеталиИТСНУ();
		Иначе
			ОтобразитьОшибкаПолученияДеталиИТСНУ(Результат.КраткоеПредставлениеОшибки);
		КонецЕсли;
	ИначеЕсли ИмяЗадания = "СервисыСопровожденияНевыполненныеУсловия" Тогда
		Если ПустаяСтрока(Результат.КраткоеПредставлениеОшибки) Тогда
			ПриПолученииДеталиСервисыСопровожденияНевыполненныеУсловия();
		Иначе
			ОтобразитьОшибкаПолученияДеталиСервисыСопровожденияНевыполненныеУсловия(
				Результат.КраткоеПредставлениеОшибки);
		КонецЕсли;
	ИначеЕсли ИмяЗадания = "Сервисы1С" Тогда
		Если ПустаяСтрока(Результат.КраткоеПредставлениеОшибки) Тогда
			ПриПолученииДеталиСервисы1С();
		Иначе
			ОтобразитьОшибкаПолученияДеталиСервисы1С(Результат.КраткоеПредставлениеОшибки);
		КонецЕсли;
	ИначеЕсли ИмяЗадания = "ИнформацияОбОбновлении" Тогда
		Если ПустаяСтрока(Результат.КраткоеПредставлениеОшибки) Тогда
			ПриПолученииДеталиИнформацияОбОбновлении();
		Иначе
			ОтобразитьОшибкаПолученияДеталиИнформацияОбОбновлении(Результат.КраткоеПредставлениеОшибки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Главное

&НаСервере
Процедура ОтобразитьСодержимоеМонитора(РезультатОперацииСервиса)
	
	// Видимость была отключена в НачатьОбновлениеСодержимогоМонитораНаСервере().
	Элементы.СтраницаСодержимое.Видимость = Истина;
	
	ДанныеМонитора = РезультатОперацииСервиса.Данные;
	Логин = РезультатОперацииСервиса.Логин;
	
	Если Домен <> РезультатОперацииСервиса.Домен Тогда
		Домен = РезультатОперацииСервиса.Домен;
		// При смене домена тексты инструкций должны быть очищены
		// и заполнены заново, т.к. зависят от домена.
		ОчиститьТекстыИнструкций();
	КонецЕсли;
	
	СохранитьОтпечатокДанныхМонитора(Логин, ДанныеМонитора);
	
	ОтобразитьБлокШапка();
	
	Если ДанныеМонитора.supportConditionsImplStatus.blockStatus <> "200"
		И ДанныеМонитора.supportConditionsImplStatus.blockStatus <> 200 Тогда
		Элементы.ГруппаУсловияСопровожденияОшибка.Видимость      = Истина;
		Элементы.ГруппаУсловияСопровожденияВыполнены.Видимость   = Ложь;
		Элементы.ГруппаНеВыполненыУсловияСопровождения.Видимость = Ложь;
	ИначеЕсли ДанныеМонитора.supportConditionsImplStatus.status = "NOT_COMPLIED" Тогда
		ОтобразитьБлокНеВыполненыУсловияСопровождения();
	Иначе
		ОтобразитьБлокУсловияСопровожденияВыполнены();
	КонецЕсли;
	
	Если ДанныеМонитора.serviceContractsStatus.blockStatus <> "200"
		И ДанныеМонитора.serviceContractsStatus.blockStatus <> 200 Тогда
		Элементы.ДекорацияКартинкаБлокДоговорыНаСервисы.Картинка = БиблиотекаКартинок.ИнтернетПоддержкаВнимание;
		Элементы.ДекорацияСервисы1ССообщениеОбОшибке.Видимость   = Истина;
		Элементы.ГруппаСервисы1ССодержимое.Видимость             = Ложь;
	Иначе
		ОтобразитьБлокДоговорыНаСервисы();
	КонецЕсли;
	
	Если ДанныеМонитора.updateInfo <> Неопределено
		И ДанныеМонитора.updateInfo.blockStatus <> "200"
		И ДанныеМонитора.updateInfo.blockStatus <> 200
		Или ДанныеМонитора.patchesInfo <> Неопределено
		И ДанныеМонитора.patchesInfo.blockStatus <> "200"
		И ДанныеМонитора.patchesInfo.blockStatus <> 200 Тогда
		Элементы.ДекорацияСтатусБлокаОбновлениеПрограммы.Картинка = БиблиотекаКартинок.ИнтернетПоддержкаВнимание;
		Элементы.ДекорацияОбновлениеСообщениеОбОшибке.Видимость   = Истина;
		Элементы.ГруппаОбновлениеСодержимое.Видимость             = Ложь;
	Иначе
		ОтобразитьБлокОбновлениеПрограммы();
	КонецЕсли;
	
	ОтобразитьБлокПредупрежденияСервисов();
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаСодержимое;
	
	ИнтеграцияПодсистемБИП.ОтобразитьДополнительныеДанныеМонитора(
		ЭтотОбъект,
		РезультатОперацииСервиса.ДополнительныеДанные);
	МониторПортала1СИТСПереопределяемый.ОтобразитьДополнительныеДанныеМонитора(
		ЭтотОбъект,
		РезультатОперацииСервиса.ДополнительныеДанные);
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСостояниеОшибка(
		Знач КраткоеОписаниеОшибки,
		Знач ПодробноеОписаниеОшибки,
		Знач ОшибкаСоединения = Истина,
		Знач ПоказатьПоляАутентификации = Ложь)
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОшибка;
	Если ОшибкаСоединения Тогда
		Элементы.ДекорацияОшибкаЗаголовок.Заголовок = НСтр("ru = 'Не удалось подключиться к сервису'");
		Элементы.ДекорацияРекомендацияСетеваяОшибка.Видимость = Истина;
		Элементы.ДекорацияКраткоеОписаниеОшибки.Заголовок = КраткоеОписаниеОшибки;
	Иначе
		Элементы.ДекорацияОшибкаЗаголовок.Заголовок = НСтр("ru = 'Ошибка'");
		Элементы.ДекорацияРекомендацияСетеваяОшибка.Видимость = Ложь;
		Элементы.ДекорацияКраткоеОписаниеОшибки.Заголовок =
			НСтр("ru = 'Возникла ошибка при получении информации из сервиса Монитора Портала 1С:ИТС:'")
			+ Символы.ПС
			+ КраткоеОписаниеОшибки;
	КонецЕсли;
	
	Элементы.ГруппаПодключениеКПорталу.Видимость = ПоказатьПоляАутентификации;
	
	Элементы.ДекорацияСсылкаЖурналРегистрации.Видимость =
		ПравоДоступа("Просмотр", Метаданные.Обработки.ЖурналРегистрации);
	
	Элементы.ДекорацияСсылкаНастройкиПрокси.Видимость =
		ОшибкаСоединения
		И (ОбщегоНазначения.ИнформационнаяБазаФайловая()
		Или ПравоДоступа("Просмотр", Метаданные.ОбщиеФормы.ПараметрыПроксиСервера));
	
	ПодробноеОписаниеОшибкиДляТехПоддержки = ПодробноеОписаниеОшибки;
	Элементы.ПовторитьПопыткуПодключения.КнопкаПоУмолчанию = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСостояниеИПППродуктаНеОказывается()
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПоддержкаПродуктаНеОказывается;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьОбновлениеСодержимогоМонитора()
	
	ОчиститьКонтекстФормы();
	ОтключитьОбработчикОжидания("Подключаемый_ОбработатьОжидание");
	ИнтеграцияПодсистемБИПКлиент.ПередПолучениемДанныхМонитора(
		ЭтотОбъект);
	МониторПортала1СИТСКлиентПереопределяемый.ПередПолучениемДанныхМонитора(
		ЭтотОбъект);
	НачатьОбновлениеСодержимогоМонитораНаСервере();
	ПриНачалеЗадания("СМ");
	
КонецПроцедуры

&НаСервере
Процедура НачатьОбновлениеСодержимогоМонитораНаСервере(
		Знач ОжидатьЗавершения = Истина,
		Знач ТолькоФоновыйРежим = Истина)
	
	МониторПортала1СИТС.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Подготовка к получению данных Монитора'"));
	
	ОтменитьВыполнениеВсехЗаданий();
	
	// Сохранение данных авторизации
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОшибка
		И Элементы.ГруппаПодключениеКПорталу.Видимость Тогда
		
		РезультатПроверки = ИнтернетПоддержкаПользователей.ПроверитьЛогинИПароль(
			Логин,
			ПарольИПП);
		
		Если РезультатПроверки.Результат Тогда
			
			УстановитьПривилегированныйРежим(Истина);
			ИнтернетПоддержкаПользователей.СохранитьДанныеАутентификации(
				Новый Структура("Логин,Пароль", Логин, ПарольИПП));
			УстановитьПривилегированныйРежим(Ложь);
			
		Иначе
			
			ОбщегоНазначения.СообщитьПользователю(РезультатПроверки.СообщениеОбОшибке);
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Исключить отображение полосы прокрутки в форме.
	Элементы.СтраницаСодержимое.Видимость = Ложь;
	
	Описатель = ОписательЗадания(Задания, "СМ");
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	Если Не ОжидатьЗавершения Тогда
		ПараметрыВыполненияВФоне.ОжидатьЗавершение = 0;
	КонецЕсли;
	
	ПараметрыВыполненияВФоне.КлючФоновогоЗадания = "ПолучениеИнформацииМонитора"
		+ Строка(Новый УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.АдресРезультата     = Описатель.АдресРезультата;
	ПараметрыВыполненияВФоне.ЗапуститьВФоне      = ТолькоФоновыйРежим;
	
	ПараметрыПолученияДополнительныхДанных = Неопределено;
	ИнтеграцияПодсистемБИП.ПередПолучениемДанныхМонитора(
		ЭтотОбъект,
		ПараметрыПолученияДополнительныхДанных);
	МониторПортала1СИТСПереопределяемый.ПередПолучениемДанныхМонитора(
		ЭтотОбъект,
		ПараметрыПолученияДополнительныхДанных);
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ПараметрыПолученияДополнительныхДанных", ПараметрыПолученияДополнительныхДанных);
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.МониторПортала1СИТС.ПолучитьДанныеМонитора",
		ПараметрыЗадания,
		ПараметрыВыполненияВФоне);
	Описатель.ДлительнаяОперация = РезультатВыполнения;
	
	Если РезультатВыполнения.Статус = "Выполняется" Тогда
		
		Описатель.Идентификатор = РезультатВыполнения.ИдентификаторЗадания;
		ОтобразитьСостояниеДлительнаяОперация();
		
	ИначеЕсли РезультатВыполнения.Статус = "Выполнено" Тогда
		
		ПриПолученииСодержимогоМонитора();
		
	ИначеЕсли РезультатВыполнения.Статус = "Отменено" Тогда
		
		МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(
			НСтр("ru = 'Не удалось отобразить данные Монитора. Задание получение данных Монитора отменено администратором.'"));
		ОтобразитьСостояниеОшибка(НСтр("ru = 'Задание отменено администратором.'"), "", Ложь);
		
	Иначе
		
		// Ошибка.
		СообщениеЖурнала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить данные Монитора.
				|%1'"),
			РезультатВыполнения.ПодробноеПредставлениеОшибки);
		МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурнала);
		ОтобразитьСостояниеОшибка(
			РезультатВыполнения.КраткоеПредставлениеОшибки,
			РезультатВыполнения.ПодробноеПредставлениеОшибки,
			Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСостояниеДлительнаяОперация()
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаДлительнаяОперация;
	
КонецПроцедуры

&НаСервере
Процедура ПриПолученииСодержимогоМонитора()
	
	Описатель = ОписательЗадания(Задания, "СМ");
	РезультатОперацииСервиса = ПолучитьИзВременногоХранилища(Описатель.АдресРезультата);
	Если ПустаяСтрока(РезультатОперацииСервиса.ИмяОшибки) Тогда
		
		ДанныеМонитора = РезультатОперацииСервиса.Данные;
		Если Не ДанныеМонитора.authResult.authenticated Тогда
			
			СообщениеЖурнала = НСтр("ru = 'Не удалось отобразить содержимое Монитора.
				|Некорректные данные аутентификации пользователя Интернет-поддержки.'");
			МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурнала);
			ОтобразитьСостояниеОшибка(
				НСтр("ru = 'Некорректные данные аутентификации пользователя Интернет-поддержки.
					|Не подключена Интернет-поддержка пользователей.'"),
				СообщениеЖурнала,
				Ложь,
				Истина);
			
		ИначеЕсли ДанныеМонитора.programName = Неопределено Тогда
			
			ОтобразитьСостояниеИПППродуктаНеОказывается();
			
		Иначе
			
			Попытка
				ОтобразитьСодержимоеМонитора(РезультатОперацииСервиса);
			Исключение
				ИнформацияОшибка = ИнформацияОбОшибке();
				СообщениеЖурнала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось отобразить содержимое Монитора.
						|%1'"),
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОшибка));
				МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурнала);
				ОтобразитьСостояниеОшибка(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не удалось отобразить содержимое Монитора.
							|%1'"),
						ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОшибка)),
					СообщениеЖурнала,
					Ложь);
				Возврат;
			КонецПопытки;
			
			ОписаниеОшибкиДанных = МониторПортала1СИТС.ОписаниеОшибкиПолученияДанныхМонитора(
				РезультатОперацииСервиса.Данные);
			Если Не ПустаяСтрока(ОписаниеОшибкиДанных) Тогда
				МониторПортала1СИТС.ЗаписатьПредупреждениеВЖурналРегистрации(
					НСтр("ru = 'Не удалось получить часть данных Монитора.'")
					+ Символы.ПС
					+ ОписаниеОшибкиДанных);
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли РезультатОперацииСервиса.ИмяОшибки = "ОшибкаСоединения" Тогда
		
		ОтобразитьСостояниеОшибка(
			РезультатОперацииСервиса.СообщениеОбОшибке,
			РезультатОперацииСервиса.ИнформацияОбОшибке,
			Истина);
		
	ИначеЕсли РезультатОперацииСервиса.ИмяОшибки = "НеЗаполненыДанныеАутентификации"
		Или РезультатОперацииСервиса.ИмяОшибки = "НекорректныеДанныеАутентификации" Тогда
		
		СообщениеЖурнала = НСтр("ru = 'Не удалось отобразить содержимое Монитора.
			|Не подключена Интернет-поддержка пользователей.'");
		МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурнала);
		ОтобразитьСостояниеОшибка(
			НСтр("ru = 'Некорректные данные аутентификации пользователя Интернет-поддержки.
				|Не подключена Интернет-поддержка пользователей.'"),
			СообщениеЖурнала,
			Ложь,
			Истина);
		
	Иначе
		
		// Все остальные ошибки.
		ОтобразитьСостояниеОшибка(
			РезультатОперацииСервиса.СообщениеОбОшибке,
			РезультатОперацииСервиса.ИнформацияОбОшибке,
			Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокКакОформитьДоговорИТС()
	Возврат НСтр("ru = 'Как оформить договор 1С:ИТС?'");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокКакАктивироватьСервисСопровождения()
	Возврат НСтр("ru = 'Как активировать сервис сопровождения конфигурации?'");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокКакПродлитьДействиеДоговора()
	Возврат НСтр("ru = 'Как продлить действие договора?'");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокКакПродлитьСрокДействияСервиса()
	Возврат НСтр("ru = 'Как продлить срок действия сервиса?'");
КонецФункции

&НаСервере
Процедура ПроверитьОбработатьИзмененииСостоянияВыполненияУсловийСопровождения(НовоеСостояниеУсловий)
	
	ТекущееСостояниеУсловий = ДанныеМонитора.supportConditionsImplStatus;
	СостояниеВыполненияИзменилось = Ложь;
	
	Если ТекущееСостояниеУсловий.status = "OK" Или ТекущееСостояниеУсловий.status = "WARNING" Тогда
		
		Если НовоеСостояниеУсловий.status = "NOT_COMPLIED" Тогда
			
			ДанныеМонитора.supportConditionsImplStatus = НовоеСостояниеУсловий;
			ОтобразитьБлокНеВыполненыУсловияСопровождения();
			СостояниеВыполненияИзменилось = Истина;
			
		ИначеЕсли ТекущееСостояниеУсловий.status <> НовоеСостояниеУсловий.status
			Или ТекущееСостояниеУсловий.itsStatus <> НовоеСостояниеУсловий.itsStatus
			Или ТекущееСостояниеУсловий.supportServiceActivationStatus <> НовоеСостояниеУсловий.supportServiceActivationStatus Тогда
			
			ДанныеМонитора.supportConditionsImplStatus = НовоеСостояниеУсловий;
			ОтобразитьОбщийСтатусВыполненыУсловия(ДанныеМонитора.supportConditionsImplStatus);
			СостояниеВыполненияИзменилось = Истина;
			
		КонецЕсли;
		
	ИначеЕсли ТекущееСостояниеУсловий.status = "NOT_COMPLIED" Тогда
		
		Если (НовоеСостояниеУсловий.status = "OK" Или НовоеСостояниеУсловий.status = "WARNING") Тогда
			
			ДанныеМонитора.supportConditionsImplStatus = НовоеСостояниеУсловий;
			ОтобразитьБлокУсловияСопровожденияВыполнены();
			СостояниеВыполненияИзменилось = Истина;
			
		Иначе
			
			// Отработка изменения отдельных пунктов условий.
			Если ТекущееСостояниеУсловий.productRegistrationStatus <> НовоеСостояниеУсловий.productRegistrationStatus Тогда
				ТекущееСостояниеУсловий.productRegistrationStatus = НовоеСостояниеУсловий.productRegistrationStatus;
				ОтобразитьРегистрацияПрограммногоПродуктаНеВыполненыУсловия(ТекущееСостояниеУсловий);
				СостояниеВыполненияИзменилось = Истина;
			КонецЕсли;
			
			Если ТекущееСостояниеУсловий.itsStatus <> НовоеСостояниеУсловий.itsStatus
				И Не (ТекущееСостояниеУсловий.itsStatus = "OK"
				И НовоеСостояниеУсловий.itsStatus = "WARNING"
				Или ТекущееСостояниеУсловий.itsStatus = "WARNING"
				И НовоеСостояниеУсловий.itsStatus = "OK") Тогда
				// Изменение "OK" на "WARNING" (и наоборот) не влияет на отображение.
				ТекущееСостояниеУсловий.itsStatus = НовоеСостояниеУсловий.itsStatus;
				ОтобразитьДоговорИТСНеВыполненыУсловия(ТекущееСостояниеУсловий);
				СостояниеВыполненияИзменилось = Истина;
			КонецЕсли;
			
			Если ТекущееСостояниеУсловий.supportServiceActivationStatus <> НовоеСостояниеУсловий.supportServiceActivationStatus
				И Не (ТекущееСостояниеУсловий.supportServiceActivationStatus = "OK"
				И НовоеСостояниеУсловий.supportServiceActivationStatus = "WARNING"
				Или ТекущееСостояниеУсловий.supportServiceActivationStatus = "WARNING"
				И НовоеСостояниеУсловий.supportServiceActivationStatus = "OK") Тогда
				// Изменение "OK" на "WARNING" (и наоборот) не влияет на отображение.
				ТекущееСостояниеУсловий.supportServiceActivationStatus = НовоеСостояниеУсловий.supportServiceActivationStatus;
				ОтобразитьСервисыСопровожденияНеВыполненыУсловия(ТекущееСостояниеУсловий);
				СостояниеВыполненияИзменилось = Истина;
			КонецЕсли;
			
			Если СостояниеВыполненияИзменилось Тогда
				ОтобразитьБлокНеВыполненыУсловияСопровожденияЗавершение();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если СостояниеВыполненияИзменилось Тогда
		СохранитьОтпечатокДанныхМонитора(Логин, ДанныеМонитора);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НовыйТаблицаСсылкиСмТакже()
	
	Результат = Новый ТаблицаЗначений;
	ТипСтрока = Новый ОписаниеТипов("Строка");
	Результат.Колонки.Добавить("Ссылка"   , ТипСтрока);
	Результат.Колонки.Добавить("Заголовок", ТипСтрока);
	Результат.Колонки.Добавить("Колонка"  , Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(1, 0, ДопустимыйЗнак.Неотрицательный)));
	
	СтрокаСсылки = Результат.Добавить();
	СтрокаСсылки.Ссылка    = "action:OpenPortal";
	СтрокаСсылки.Заголовок = НСтр("ru = 'Портал 1С:ИТС'");
	СтрокаСсылки.Колонка   = 1;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СообщенияВСлужбуТехническойПоддержки") Тогда
		СтрокаСсылки = Результат.Добавить();
		СтрокаСсылки.Ссылка    = "action:TechSuppMonitorProblem";
		СтрокаСсылки.Заголовок = НСтр("ru = 'Сообщить в техподдержку о проблеме в Мониторе'");
		СтрокаСсылки.Колонка   = 1;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСсылкиСмТакже(ТаблицаСсылки)
	
	Если ТаблицаСсылки.Количество() = 0 Тогда
		Элементы.ГруппаСмТакже.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаСсылки Из ТаблицаСсылки Цикл
		Если СтрокаСсылки.Колонка <> 1 И СтрокаСсылки.Колонка <> 2 Тогда
			СтрокаСсылки.Колонка = 1;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ДекорацияСмТакжеКолонкаОдин.Заголовок =
		МаркированныйФорматированныйСписокСсылокСмТакже(ТаблицаСсылки, 1);
	Элементы.ДекорацияСмТакжеКолонкаДва.Заголовок =
		МаркированныйФорматированныйСписокСсылокСмТакже(ТаблицаСсылки, 2);
	
КонецПроцедуры

#КонецОбласти

#Область БлокШапка

&НаСервере
Процедура ОтобразитьБлокШапка()
	
	Элементы.ДекорацияНазваниеПП.Заголовок = ДанныеМонитора.programName;
	Элементы.ДекорацияВремя.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Данные на %1'"),
		Формат(ТекущаяДатаСеанса(), "ДЛФ=DT"));
	
КонецПроцедуры

#КонецОбласти

#Область БлокУсловияСопровожденияВыполнены

&НаСервере
Процедура ОтобразитьБлокУсловияСопровожденияВыполнены()
	
	// Приведение к начальному отображению.
	Элементы.ГруппаУсловияСопровожденияОшибка.Видимость      = Ложь;
	Элементы.ГруппаНеВыполненыУсловияСопровождения.Видимость = Ложь;
	Элементы.ГруппаУсловияСопровожденияВыполнены.Видимость   = Истина;
	Элементы.ГруппаУсловияВыполненыДетали.Видимость          = Ложь;
	
	ВыполнениеУсловий = ДанныеМонитора.supportConditionsImplStatus;
	ОтобразитьОбщийСтатусВыполненыУсловия(ВыполнениеУсловий);
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьОбщийСтатусВыполненыУсловия(ВыполнениеУсловий)
	
	Если Не ЗначениеЗаполнено(ВыполнениеУсловий.itsStatus)
		И Не ЗначениеЗаполнено(ВыполнениеУсловий.supportServiceActivationStatus) Тогда
		
		// Нет данных для просмотра.
		Элементы.ДекорацияУсловияВыполненыКартинка.Картинка = БиблиотекаКартинок.Успешно32;
		Элементы.ДекорацияУсловияВыполненыДетали.Видимость  = Ложь;
		
	Иначе
		
		Элементы.ДекорацияУсловияВыполненыДетали.Видимость = Истина;
		Если ВыполнениеУсловий.status = "WARNING" Тогда
			Элементы.ДекорацияУсловияВыполненыКартинка.Картинка = БиблиотекаКартинок.ИнтернетПоддержкаВнимание;
			Элементы.ДекорацияУсловияВыполненыДетали.Заголовок =
				ЗаголовокГруппы(ЗаголовокОбратитеВнимание(), Элементы.ГруппаУсловияВыполненыДетали.Видимость);
		Иначе
			Элементы.ДекорацияУсловияВыполненыКартинка.Картинка = БиблиотекаКартинок.Успешно32;
			Элементы.ДекорацияУсловияВыполненыДетали.Заголовок =
				ЗаголовокГруппы(ЗаголовокПосмотретьДетали(), Элементы.ГруппаУсловияВыполненыДетали.Видимость);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДекорацияУсловияВыполненыДеталиНажатиеНаСервере()
	
	Элементы.ГруппаУсловияВыполненыДетали.Видимость =
		Не Элементы.ГруппаУсловияВыполненыДетали.Видимость;
	Элементы.ДекорацияУсловияВыполненыДетали.Заголовок =
		ЗаголовокГруппы(ЗаголовокПосмотретьДетали(), Элементы.ГруппаУсловияВыполненыДетали.Видимость);
	
	Если ДанныеМонитора.supportConditionsImplStatus.status = "WARNING" Тогда
		Элементы.ДекорацияУсловияВыполненыДетали.Заголовок =
			ЗаголовокГруппы(ЗаголовокОбратитеВнимание(), Элементы.ГруппаУсловияВыполненыДетали.Видимость);
	Иначе
		Элементы.ДекорацияУсловияВыполненыДетали.Заголовок =
			ЗаголовокГруппы(ЗаголовокПосмотретьДетали(), Элементы.ГруппаУсловияВыполненыДетали.Видимость);
	КонецЕсли;
	
	Если Элементы.ГруппаУсловияВыполненыДетали.Видимость Тогда
		НачатьПолучениеДеталиУсловияВыполненыНаСервере();
	Иначе
		ОтменитьЗадание("СВУ");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НачатьПолучениеДеталиУсловияВыполненыНаСервере()
	
	МониторПортала1СИТС.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Подготовка к получению деталей выполненных условий сопровождения.'"));
	
	Описатель = ОписательЗадания(Задания, "СВУ");
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.КлючФоновогоЗадания = "ПолучениеИнформацииМонитораУсловияСопровождения"
		+ Строка(Новый УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.АдресРезультата = Описатель.АдресРезультата;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Логин", Логин);
	ПараметрыЗадания.Вставить("Договоры1СИТС",
		ЗначениеЗаполнено(ДанныеМонитора.supportConditionsImplStatus.itsStatus));
	ПараметрыЗадания.Вставить("АктивацияСервисовСопровождения",
		ЗначениеЗаполнено(ДанныеМонитора.supportConditionsImplStatus.supportServiceActivationStatus));
	ПараметрыЗадания.Вставить("ДоговорыНаСервисы", Ложь);
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.МониторПортала1СИТС.ПолучитьДеталиДанныхМонитора",
		ПараметрыЗадания,
		ПараметрыВыполненияВФоне);
	Описатель.ДлительнаяОперация = РезультатВыполнения;
	
	Если РезультатВыполнения.Статус = "Выполняется" Тогда
		
		Описатель.Идентификатор = РезультатВыполнения.ИдентификаторЗадания;
		ОтобразитьСостояниеДлительнаяОперацияУсловияВыполнены();
		
	ИначеЕсли РезультатВыполнения.Статус = "Выполнено" Тогда
		
		ПриПолученииДеталиУсловияВыполнены();
		
	ИначеЕсли РезультатВыполнения.Статус = "Отменено" Тогда
		
		МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(
			НСтр("ru = 'Не удалось отобразить детали выполненных условий сопровождения. Задание получение данных отменено.'"));
		ОтобразитьОшибкаПолученияДеталиУсловияВыполнены(НСтр("ru = 'Задание отменено.'"));
		
	Иначе
		
		// Ошибка.
		СообщениеЖурнала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить детали выполненных условий.
				|%1'"),
			РезультатВыполнения.ПодробноеПредставлениеОшибки);
		МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурнала);
		ОтобразитьОшибкаПолученияДеталиУсловияВыполнены(РезультатВыполнения.КраткоеПредставлениеОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСостояниеДлительнаяОперацияУсловияВыполнены()
	
	Элементы.ГруппаВыполненоДоговорИТС.Видимость           = Ложь;
	Элементы.ГруппаВыполненоСервисыСопровождения.Видимость = Ложь;
	Элементы.ДекорацияВУОшибка.Заголовок                   = "";
	
	Элементы.СтраницыДлительнаяОперацияУсловияВыполнены.Видимость = Истина;
	Элементы.СтраницыДлительнаяОперацияУсловияВыполнены.ТекущаяСтраница =
		Элементы.СтраницаВыполненныеУсловияДлительнаяОперация;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьОшибкаПолученияДеталиУсловияВыполнены(Знач СообщениеОбОшибке)
	
	Элементы.СтраницыДлительнаяОперацияУсловияВыполнены.ТекущаяСтраница = Элементы.СтраницаВыполненныеУсловияОшибка;
	Элементы.ДекорацияВУОшибка.Заголовок = СообщениеОбОшибке;
	
	Элементы.СтраницыДлительнаяОперацияУсловияВыполнены.Видимость = Истина;
	Элементы.ГруппаВыполненоДоговорИТС.Видимость           = Ложь;
	Элементы.ГруппаВыполненоСервисыСопровождения.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПриПолученииДеталиУсловияВыполнены()
	
	Описатель = ОписательЗадания(Задания, "СВУ");
	РезультатОперацииСервиса = ПолучитьИзВременногоХранилища(Описатель.АдресРезультата);
	Если ПустаяСтрока(РезультатОперацииСервиса.ИмяОшибки) Тогда
		
		Данные = РезультатОперацииСервиса.Данные;
		
		Попытка
			ОтобразитьДеталиВыполненныеУсловия(Данные);
		Исключение
			
			ИнформацияОшибка = ИнформацияОбОшибке();
			СообщениеЖурнала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось отобразить детали выполненных условий сопровождения.
					|%1'"),
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОшибка));
			МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурнала);
			ОтобразитьОшибкаПолученияДеталиУсловияВыполнены(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка отображения данных.
						|%1'"),
					ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОшибка)));
			Возврат;
			
		КонецПопытки;
		
		ПроверитьОбработатьИзмененииСостоянияВыполненияУсловийСопровождения(Данные.supportConditionsImplStatus);
		
	Иначе
		
		ОтобразитьОшибкаПолученияДеталиУсловияВыполнены(РезультатОперацииСервиса.СообщениеОбОшибке);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьДеталиВыполненныеУсловия(Данные)
	
	ВыполнениеУсловий = Данные.supportConditionsImplStatus;
	
	Элементы.СтраницыДлительнаяОперацияУсловияВыполнены.Видимость = Ложь;
	
	Если Не ЗначениеЗаполнено(ВыполнениеУсловий.itsStatus) Тогда
		
		Элементы.ГруппаВыполненоДоговорИТС.Видимость = Ложь;
		
	Иначе
		
		Элементы.ГруппаВыполненоДоговорИТС.Видимость = Истина;
		
		НетДанныхДляОтображения =
			(Данные.itsContracts.serviceContracts = Неопределено
			Или Данные.itsContracts.serviceContracts.Количество() = 0);
		
		Если ВыполнениеУсловий.itsStatus = "WARNING" Тогда
			Если НетДанныхДляОтображения Тогда
				Элементы.ДекорацияЗаголовокДоговорИТСВУ.Заголовок =
					НСтр("ru = 'Договор 1С:ИТС заканчивается'");
			Иначе
				Элементы.ДекорацияЗаголовокДоговорИТСВУ.Заголовок =
					НСтр("ru = 'Договор 1С:ИТС заканчивается:'");
			КонецЕсли;
		Иначе
			Если НетДанныхДляОтображения Тогда
				Элементы.ДекорацияЗаголовокДоговорИТСВУ.Заголовок =
					НСтр("ru = 'Договор 1С:ИТС'");
			Иначе
				Элементы.ДекорацияЗаголовокДоговорИТСВУ.Заголовок =
					НСтр("ru = 'Договор 1С:ИТС:'");
			КонецЕсли;
		КонецЕсли;
		
		Элементы.ДекорацияДоговорИТССписок.Заголовок =
			МаркированныйСписокСПериодами(
				Данные.itsContracts,
				Ложь);
		
		Если НетДанныхДляОтображения Тогда
			Элементы.ДекорацияКакОформитьДоговорИТСУсловияВыполненыСсылка.Видимость = Ложь;
		Иначе
			Элементы.ДекорацияКакОформитьДоговорИТСУсловияВыполненыСсылка.Видимость = Истина;
			Элементы.ДекорацияКакОформитьДоговорИТСУсловияВыполненыСсылка.Заголовок =
				ЗаголовокГруппы(
					ЗаголовокКакПродлитьДействиеДоговора(),
					Ложь);
			ЗаполнитьЗаголовокИзМакета(
				Элементы.ДекорацияКакОформитьДоговорИТСУсловияВыполненыИнструкция,
				"ИнструкцияОформитьДоговорИТС");
		КонецЕсли;
		
		Элементы.ДекорацияКакОформитьДоговорИТСУсловияВыполненыИнструкция.Видимость = Ложь;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыполнениеУсловий.supportServiceActivationStatus) Тогда
		
		Элементы.ГруппаВыполненоСервисыСопровождения.Видимость = Ложь;
		
	Иначе
		
		Элементы.ГруппаВыполненоСервисыСопровождения.Видимость = Истина;
		
		НетДанныхДляОтображения =
			(Данные.supportServiceActivations.serviceContracts = Неопределено
			Или Данные.supportServiceActivations.serviceContracts.Количество() = 0);
		
		Если ВыполнениеУсловий.supportServiceActivationStatus = "WARNING" Тогда
			Если НетДанныхДляОтображения Тогда
				Элементы.ДекорацияЗаголовокСервисыСопровожденияВУ.Заголовок =
					НСтр("ru = 'Срок сервиса сопровождения конфигурации заканчивается'");
			Иначе
				Элементы.ДекорацияЗаголовокСервисыСопровожденияВУ.Заголовок =
					НСтр("ru = 'Срок сервиса сопровождения конфигурации заканчивается:'");
			КонецЕсли;
		Иначе
			Если НетДанныхДляОтображения Тогда
				Элементы.ДекорацияЗаголовокСервисыСопровожденияВУ.Заголовок =
					НСтр("ru = 'Сервис сопровождения конфигурации'");
			Иначе
				Элементы.ДекорацияЗаголовокСервисыСопровожденияВУ.Заголовок =
					НСтр("ru = 'Активирован сервис сопровождения конфигурации:'");
			КонецЕсли;
		КонецЕсли;
		
		Элементы.ДекорацияСервисыСопровожденияСписок.Заголовок =
			МаркированныйСписокСПериодами(
				Данные.supportServiceActivations,
				Ложь);
		
		Если НетДанныхДляОтображения Тогда
			Элементы.ДекорацияКакАктивироватьСервисСопровожденияУсловияВыполненыСсылка.Видимость = Ложь;
		Иначе
			Элементы.ДекорацияКакАктивироватьСервисСопровожденияУсловияВыполненыСсылка.Видимость = Истина;
			Элементы.ДекорацияКакАктивироватьСервисСопровожденияУсловияВыполненыСсылка.Заголовок =
				ЗаголовокГруппы(
					ЗаголовокКакПродлитьДействиеСервиса(),
					Ложь);
			ЗаполнитьЗаголовокИзМакета(
				Элементы.ДекорацияКакАктивироватьСервисСопровожденияУсловияВыполненыИнструкция,
				"ИнструкцияАктивироватьСервисСопровождения");
		КонецЕсли;
		
		Элементы.ДекорацияКакАктивироватьСервисСопровожденияУсловияВыполненыИнструкция.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокОбратитеВнимание()
	Возврат НСтр("ru = 'Обратите внимание'");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокПосмотретьДетали()
	Возврат НСтр("ru = 'Посмотреть детали'");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокКакПродлитьДействиеСервиса()
	Возврат НСтр("ru = 'Как продлить действие сервиса?'");
КонецФункции

#КонецОбласти

#Область БлокНеВыполненыУсловияСопровождения

&НаСервере
Процедура ОтобразитьБлокНеВыполненыУсловияСопровождения()
	
	СостояниеВыполненияУсловий = ДанныеМонитора.supportConditionsImplStatus;
	
	ОтобразитьРегистрацияПрограммногоПродуктаНеВыполненыУсловия(СостояниеВыполненияУсловий);
	ОтобразитьДоговорИТСНеВыполненыУсловия(СостояниеВыполненияУсловий);
	ОтобразитьСервисыСопровожденияНеВыполненыУсловия(СостояниеВыполненияУсловий);
	
	ОтобразитьБлокНеВыполненыУсловияСопровожденияЗавершение();
	
	Элементы.ГруппаУсловияСопровожденияОшибка.Видимость      = Ложь;
	Элементы.ГруппаУсловияСопровожденияВыполнены.Видимость   = Ложь;
	Элементы.ГруппаНеВыполненыУсловияСопровождения.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьРегистрацияПрограммногоПродуктаНеВыполненыУсловия(СостояниеВыполненияУсловий)
	
	Если СостояниеВыполненияУсловий.productRegistrationStatus <> "NOT_REGISTERED" Тогда
		Элементы.ГруппаНеВыполненоРегистрационныйНомер.Видимость = Ложь;
	Иначе
		Элементы.ГруппаНеВыполненоРегистрационныйНомер.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьДоговорИТСНеВыполненыУсловия(СостояниеВыполненияУсловий)
	
	Если СостояниеВыполненияУсловий.itsStatus = Неопределено Тогда
		
		Элементы.ГруппаНеВыполненоИТС.Видимость                   = Ложь;
		Элементы.ГруппаВыполненоИТСНевыполненныеУсловия.Видимость = Ложь;
		
	ИначеЕсли СостояниеВыполненияУсловий.itsStatus = "NO_CONTRACT" Тогда
		
		Элементы.ГруппаНеВыполненоИТС.Видимость                   = Истина;
		Элементы.ГруппаВыполненоИТСНевыполненныеУсловия.Видимость = Ложь;
		Элементы.ДекорацияКакЗаключитьДоговорИТССсылка.Заголовок =
			ЗаголовокГруппы(ЗаголовокКакОформитьДоговорИТС(), Ложь);
		Элементы.ДекорацияКакЗаключитьДоговорИТСИнструкция.Видимость = Ложь;
		ЗаполнитьЗаголовокИзМакета(
			Элементы.ДекорацияКакЗаключитьДоговорИТСИнструкция,
			"ИнструкцияОформитьДоговорИТС");
		
	Иначе
		
		Элементы.ГруппаНеВыполненоИТС.Видимость = Ложь;
		Элементы.ГруппаВыполненоИТСНевыполненныеУсловия.Видимость = Истина;
		Элементы.ГруппаИТСНевыполненныеУсловия.Видимость = Ложь;
		Элементы.ДекорацияДоговорИТССсылкаДетали.Заголовок =
			ЗаголовокГруппы(ЗаголовокСписокДоговоров(), Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСервисыСопровожденияНеВыполненыУсловия(СостояниеВыполненияУсловий)
	
	Если СостояниеВыполненияУсловий.supportServiceActivationStatus = Неопределено Тогда
		
		Элементы.ГруппаНеВыполненоСервисыСопровождения.Видимость                   = Ложь;
		Элементы.ГруппаВыполненоСервисыСопровожденияНевыполненныеУсловия.Видимость = Ложь;
		
	ИначеЕсли СостояниеВыполненияУсловий.supportServiceActivationStatus = "NO_ACTIVATIONS" Тогда
		
		Элементы.ГруппаНеВыполненоСервисыСопровождения.Видимость                   = Истина;
		Элементы.ГруппаВыполненоСервисыСопровожденияНевыполненныеУсловия.Видимость = Ложь;
		Элементы.ДекорацияКакАктивироватьСервисСопровожденияСсылка.Заголовок =
			ЗаголовокГруппы(ЗаголовокКакАктивироватьСервисСопровождения(), Ложь);
		Элементы.ДекорацияКакАктивироватьСервисСопровожденияИнструкция.Видимость = Ложь;
		ЗаполнитьЗаголовокИзМакета(
			Элементы.ДекорацияКакАктивироватьСервисСопровожденияИнструкция,
			"ИнструкцияАктивироватьСервисСопровождения");
		
	Иначе
		
		Элементы.ГруппаНеВыполненоСервисыСопровождения.Видимость = Ложь;
		Элементы.ГруппаВыполненоСервисыСопровожденияНевыполненныеУсловия.Видимость = Истина;
		Элементы.ГруппаСервисыСопровожденияНевыполненныеУсловия.Видимость = Ложь;
		Элементы.ДекорацияСервисыСопровожденияДеталиСсылка.Заголовок =
			ЗаголовокГруппы(ЗаголовокСписокСервисов(), Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьБлокНеВыполненыУсловияСопровожденияЗавершение()
	
	Элементы.ГруппаНеВыполненыУсловияСопровожденияДетали.Видимость =
		Элементы.ГруппаНеВыполненоРегистрационныйНомер.Видимость
		Или Элементы.ГруппаНеВыполненоИТС.Видимость
		Или Элементы.ГруппаНеВыполненоСервисыСопровождения.Видимость
		Или Элементы.ГруппаВыполненоИТСНевыполненныеУсловия.Видимость
		Или Элементы.ГруппаВыполненоСервисыСопровожденияНевыполненныеУсловия.Видимость;
	Элементы.ДекорацияПравилаСопровожденияППСсылка.Видимость =
		Элементы.ГруппаНеВыполненыУсловияСопровожденияДетали.Видимость;
	Если Элементы.ДекорацияПравилаСопровожденияППСсылка.Видимость Тогда
		Элементы.ДекорацияПравилаСопровожденияППСсылка.Заголовок =
			ЗаголовокГруппы(ЗаголовокПравилаСопровожденияПП(), Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДекорацияДоговорИТССсылкаДеталиНажатиеНаСервере()
	
	Элементы.ГруппаИТСНевыполненныеУсловия.Видимость = Не Элементы.ГруппаИТСНевыполненныеУсловия.Видимость;
	Элементы.ДекорацияДоговорИТССсылкаДетали.Заголовок =
		ЗаголовокГруппы(ЗаголовокСписокДоговоров(), Элементы.ГруппаИТСНевыполненныеУсловия.Видимость);
	
	Если Элементы.ГруппаИТСНевыполненныеУсловия.Видимость Тогда
		НачатьПолучениеДеталиИТСНУНаСервере();
	Иначе
		ОтменитьЗадание("ИТСНУ");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДекорацияСервисыСопровожденияДеталиСсылкаНажатиеНаСервере()
	
	Элементы.ГруппаСервисыСопровожденияНевыполненныеУсловия.Видимость =
		Не Элементы.ГруппаСервисыСопровожденияНевыполненныеУсловия.Видимость;
	Элементы.ДекорацияСервисыСопровожденияДеталиСсылка.Заголовок =
		ЗаголовокГруппы(ЗаголовокСписокСервисов(), Элементы.ГруппаСервисыСопровожденияНевыполненныеУсловия.Видимость);
	
	Если Элементы.ГруппаСервисыСопровожденияНевыполненныеУсловия.Видимость Тогда
		НачатьПолучениеДеталиСервисыСопровожденияНевыполненныеУсловияНаСервере();
	Иначе
		ОтменитьЗадание("СервисыСопровожденияНевыполненныеУсловия");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НачатьПолучениеДеталиИТСНУНаСервере()
	
	МониторПортала1СИТС.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Подготовка к получению деталей договора ИТС невыполненных условий.'"));
	
	Описатель = ОписательЗадания(Задания, "ИТСНУ");
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.КлючФоновогоЗадания = "ПолучениеИнформацииМонитораДоговорыИТС"
		+ Строка(Новый УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.АдресРезультата = Описатель.АдресРезультата;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Логин", Логин);
	ПараметрыЗадания.Вставить("Договоры1СИТС"                 , Истина);
	ПараметрыЗадания.Вставить("АктивацияСервисовСопровождения", Ложь);
	ПараметрыЗадания.Вставить("ДоговорыНаСервисы"             , Ложь);
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.МониторПортала1СИТС.ПолучитьДеталиДанныхМонитора",
		ПараметрыЗадания,
		ПараметрыВыполненияВФоне);
	Описатель.ДлительнаяОперация = РезультатВыполнения;
	
	Если РезультатВыполнения.Статус = "Выполняется" Тогда
		
		Описатель.Идентификатор = РезультатВыполнения.ИдентификаторЗадания;
		ОтобразитьСостояниеДлительнаяОперацияИТСНУ();
		
	ИначеЕсли РезультатВыполнения.Статус = "Выполнено" Тогда
		
		ПриПолученииДеталиИТСНУ();
		
	ИначеЕсли РезультатВыполнения.Статус = "Отменено" Тогда
		
		МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(
			НСтр("ru = 'Не удалось отобразить детали договора ИТС невыполненных условий сопровождения. Задание получение данных отменено.'"));
		ОтобразитьОшибкаПолученияДеталиИТСНУ(НСтр("ru = 'Задание отменено.'"));
		
	Иначе
		
		// Ошибка.
		СообщениеЖурнала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить детали договора ИТС невыполненных условий.
				|%1'"),
			РезультатВыполнения.ПодробноеПредставлениеОшибки);
		МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурнала);
		ОтобразитьОшибкаПолученияДеталиИТСНУ(РезультатВыполнения.КраткоеПредставлениеОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСостояниеДлительнаяОперацияИТСНУ()
	
	Элементы.ДекорацияДоговорИТССписокНУ.Видимость = Ложь;
	Элементы.ДекорацияИТСНУОшибка.Заголовок        = "";
	
	Элементы.СтраницыДлительнаяОперацияИТСНеВыполненыУсловия.Видимость = Истина;
	Элементы.СтраницыДлительнаяОперацияИТСНеВыполненыУсловия.ТекущаяСтраница =
		Элементы.СтраницаИТСНеВыполненныеУсловияДлительнаяОперация;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьОшибкаПолученияДеталиИТСНУ(Знач СообщениеОбОшибке)
	
	Элементы.СтраницыДлительнаяОперацияИТСНеВыполненыУсловия.ТекущаяСтраница =
		Элементы.СтраницаИТСНеВыполненныеУсловияОшибка;
	Элементы.ДекорацияИТСНУОшибка.Заголовок = СообщениеОбОшибке;
	
	Элементы.СтраницыДлительнаяОперацияИТСНеВыполненыУсловия.Видимость = Истина;
	Элементы.ДекорацияДоговорИТССписокНУ.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПриПолученииДеталиИТСНУ()
	
	Описатель = ОписательЗадания(Задания, "ИТСНУ");
	РезультатОперацииСервиса = ПолучитьИзВременногоХранилища(Описатель.АдресРезультата);
	Если ПустаяСтрока(РезультатОперацииСервиса.ИмяОшибки) Тогда
		
		Данные = РезультатОперацииСервиса.Данные;
		
		Попытка
			ОтобразитьДеталиИТСНУ(Данные);
		Исключение
			
			ИнформацияОшибка = ИнформацияОбОшибке();
			СообщениеЖурнала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось отобразить детали договора ИТС невыполненных условий сопровождения.
					|%1'"),
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОшибка));
			МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурнала);
			ОтобразитьОшибкаПолученияДеталиИТСНУ(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка отображения данных.
						|%1'"),
					ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОшибка)));
			Возврат;
			
		КонецПопытки;
		
		ПроверитьОбработатьИзмененииСостоянияВыполненияУсловийСопровождения(Данные.supportConditionsImplStatus);
		
	Иначе
		
		ОтобразитьОшибкаПолученияДеталиИТСНУ(РезультатОперацииСервиса.СообщениеОбОшибке);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьДеталиИТСНУ(Данные)
	
	ВыполнениеУсловий = Данные.supportConditionsImplStatus;
	
	Элементы.СтраницыДлительнаяОперацияИТСНеВыполненыУсловия.Видимость = Ложь;
	
	Если Не ЗначениеЗаполнено(ВыполнениеУсловий.itsStatus) Тогда
		Элементы.ГруппаИТСНевыполненныеУсловия.Видимость = Ложь;
	Иначе
		Элементы.ДекорацияДоговорИТССписокНУ.Видимость = Истина;
		Элементы.ДекорацияДоговорИТССписокНУ.Заголовок = МаркированныйСписокСПериодами(
			Данные.itsContracts,
			Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НачатьПолучениеДеталиСервисыСопровожденияНевыполненныеУсловияНаСервере()
	
	МониторПортала1СИТС.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Подготовка к получению деталей сервисов сопровождения невыполненных условий.'"));
	
	Описатель = ОписательЗадания(Задания, "СервисыСопровожденияНевыполненныеУсловия");
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.КлючФоновогоЗадания = "ПолучениеИнформацииМонитораСервисыСопровождения"
		+ Строка(Новый УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.АдресРезультата = Описатель.АдресРезультата;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Логин"                         , Логин);
	ПараметрыЗадания.Вставить("Договоры1СИТС"                 , Ложь);
	ПараметрыЗадания.Вставить("АктивацияСервисовСопровождения", Истина);
	ПараметрыЗадания.Вставить("ДоговорыНаСервисы"             , Ложь);
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.МониторПортала1СИТС.ПолучитьДеталиДанныхМонитора",
		ПараметрыЗадания,
		ПараметрыВыполненияВФоне);
	Описатель.ДлительнаяОперация = РезультатВыполнения;
	
	Если РезультатВыполнения.Статус = "Выполняется" Тогда
		
		Описатель.Идентификатор = РезультатВыполнения.ИдентификаторЗадания;
		ОтобразитьСостояниеДлительнаяОперацияСервисыСопровожденияНевыполненныеУсловия();
		
	ИначеЕсли РезультатВыполнения.Статус = "Выполнено" Тогда
		
		ПриПолученииДеталиСервисыСопровожденияНевыполненныеУсловия();
		
	ИначеЕсли РезультатВыполнения.Статус = "Отменено" Тогда
		
		МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(
			НСтр("ru = 'Не удалось отобразить детали сервисов сопровождения невыполненных условий сопровождения. Задание получение данных отменено.'"));
		ОтобразитьОшибкаПолученияДеталиИТСНУ(НСтр("ru = 'Задание отменено.'"));
		
	Иначе
		
		// Ошибка.
		СообщениеЖурнала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить детали сервисов сопровождения невыполненных условий.
				|%1'"),
			РезультатВыполнения.ПодробноеПредставлениеОшибки);
		МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурнала);
		ОтобразитьОшибкаПолученияДеталиСервисыСопровожденияНевыполненныеУсловия(
			РезультатВыполнения.КраткоеПредставлениеОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСостояниеДлительнаяОперацияСервисыСопровожденияНевыполненныеУсловия()
	
	Элементы.ДекорацияСервисыСопровожденияСписокНевыполненныеУсловия.Видимость = Ложь;
	Элементы.ДекорацияСервисыСопровожденияНевыполненныеУсловияОшибка.Заголовок = "";
	
	Элементы.СтраницыДлительнаяОперацияСервисыСопровожденияНевыполненныеУсловия.Видимость = Истина;
	Элементы.СтраницыДлительнаяОперацияСервисыСопровожденияНевыполненныеУсловия.ТекущаяСтраница =
		Элементы.СтраницаСервисыСопровожденияНевыполненныеУсловияДлительнаяОперация;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьОшибкаПолученияДеталиСервисыСопровожденияНевыполненныеУсловия(Знач СообщениеОбОшибке)
	
	Элементы.СтраницыДлительнаяОперацияСервисыСопровожденияНевыполненныеУсловия.ТекущаяСтраница =
		Элементы.СтраницаСервисыСопровожденияНевыполненныеУсловияОшибка;
	Элементы.ДекорацияСервисыСопровожденияНевыполненныеУсловияОшибка.Заголовок = СообщениеОбОшибке;
	
	Элементы.СтраницыДлительнаяОперацияСервисыСопровожденияНевыполненныеУсловия.Видимость = Истина;
	Элементы.ДекорацияСервисыСопровожденияСписокНевыполненныеУсловия.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПриПолученииДеталиСервисыСопровожденияНевыполненныеУсловия()
	
	Описатель = ОписательЗадания(Задания, "СервисыСопровожденияНевыполненныеУсловия");
	РезультатОперацииСервиса = ПолучитьИзВременногоХранилища(Описатель.АдресРезультата);
	Если ПустаяСтрока(РезультатОперацииСервиса.ИмяОшибки) Тогда
		
		Данные = РезультатОперацииСервиса.Данные;
		
		Попытка
			ОтобразитьДеталиСервисыСопровожденияНевыполненныеУсловия(Данные);
		Исключение
			
			ИнформацияОшибка = ИнформацияОбОшибке();
			СообщениеЖурнала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось отобразить детали сервисов сопровождения невыполненных условий сопровождения.
					|%1'"),
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОшибка));
			МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурнала);
			ОтобразитьОшибкаПолученияДеталиСервисыСопровожденияНевыполненныеУсловия(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка отображения данных.
						|%1'"),
					ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОшибка)));
			Возврат;
			
		КонецПопытки;
		
		ПроверитьОбработатьИзмененииСостоянияВыполненияУсловийСопровождения(
			Данные.supportConditionsImplStatus);
		
	Иначе
		
		ОтобразитьОшибкаПолученияДеталиСервисыСопровожденияНевыполненныеУсловия(
			РезультатОперацииСервиса.СообщениеОбОшибке);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьДеталиСервисыСопровожденияНевыполненныеУсловия(Данные)
	
	ВыполнениеУсловий = Данные.supportConditionsImplStatus;
	
	Элементы.СтраницыДлительнаяОперацияСервисыСопровожденияНевыполненныеУсловия.Видимость = Ложь;
	
	Если Не ЗначениеЗаполнено(ВыполнениеУсловий.supportServiceActivationStatus) Тогда
		Элементы.ГруппаСервисыСопровожденияНевыполненныеУсловия.Видимость = Ложь;
	Иначе
		Элементы.ДекорацияСервисыСопровожденияСписокНевыполненныеУсловия.Видимость = Истина;
		Элементы.ДекорацияСервисыСопровожденияСписокНевыполненныеУсловия.Заголовок = МаркированныйСписокСПериодами(
			Данные.supportServiceActivations,
			Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокПравилаСопровожденияПП()
	Возврат НСтр("ru = 'Правила сопровождения программного продукта'");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокСписокДоговоров()
	Возврат НСтр("ru = 'Список договоров'");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокСписокСервисов()
	Возврат НСтр("ru = 'Список сервисов сопровождения'");
КонецФункции

#КонецОбласти

#Область БлокДоговорыНаСервисы

&НаСервере
Процедура ОтобразитьБлокДоговорыНаСервисы()
	
	Элементы.ДекорацияСервисы1ССообщениеОбОшибке.Видимость = Ложь;
	Элементы.ГруппаСервисы1ССодержимое.Видимость = Истина;
	Элементы.ГруппаСервисы1СДетали.Видимость = Ложь;
	
	Если ДанныеМонитора.serviceContractsStatus.status = "NO_CONTRACTS" Тогда
		
		// Нет договоров на сервисы.
		Элементы.ДекорацияКартинкаБлокДоговорыНаСервисы.Картинка =
			БиблиотекаКартинок.ИнтернетПоддержкаВнимание;
		Элементы.ДекорацияСервисы1СДеталиСсылка.Видимость = Ложь;
		
		Элементы.ДекорацияКакПодключитьСервис1ССсылка.Видимость = Истина;
		Элементы.ДекорацияКакПодключитьСервис1СИнструкция.Видимость = Ложь;
		Элементы.ДекорацияКакПодключитьСервис1ССсылка.Заголовок =
			ЗаголовокГруппы(
				ЗаголовокКакОформитьДоговорыНаСервисы(),
				Ложь);
		
		ЗаполнитьЗаголовокИзМакета(
			Элементы.ДекорацияКакПодключитьСервис1СИнструкция,
			"ИнструкцияКакПодключитьСервисы1С");
		
	Иначе
		
		Элементы.ДекорацияКакПодключитьСервис1ССсылка.Видимость = Ложь;
		Элементы.ДекорацияКакПодключитьСервис1СИнструкция.Видимость = Ложь;
		
		ОтобразитьОбщееСостояниеДоговоровНаСервисы();
		
		ЗаполнитьЗаголовокИзМакета(
			Элементы.ДекорацияКакПродлитьСервис1СИнструкция,
			"ИнструкцияКакПродлитьСервис1С");
		Элементы.ДекорацияКакПродлитьСервис1СИнструкция.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьОбщееСостояниеДоговоровНаСервисы()
	
	Если ДанныеМонитора.serviceContractsStatus.status = "WARNING" Тогда
		Элементы.ДекорацияКартинкаБлокДоговорыНаСервисы.Картинка =
			БиблиотекаКартинок.ИнтернетПоддержкаВнимание;
		Элементы.ДекорацияСервисы1СДеталиСсылка.Заголовок =
			ЗаголовокГруппы(
				ЗаголовокЗаканчиваетсяСрокДействияСервиса(),
				Элементы.ГруппаСервисы1СДетали.Видимость);
	Иначе
		Элементы.ДекорацияКартинкаБлокДоговорыНаСервисы.Картинка =
			БиблиотекаКартинок.Успешно32;
		Элементы.ДекорацияСервисы1СДеталиСсылка.Заголовок =
			ЗаголовокГруппы(
				ЗаголовокЕстьДействующиеДоговорыНаСервисы(),
				Элементы.ГруппаСервисы1СДетали.Видимость);
	КонецЕсли;
	
	Элементы.ДекорацияСервисы1СДеталиСсылка.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ДекорацияДоговорыНаСервисыДеталиСсылкаНажатиеНаСервере()
	
	Элементы.ГруппаСервисы1СДетали.Видимость =
		Не Элементы.ГруппаСервисы1СДетали.Видимость;
	Если ДанныеМонитора.serviceContractsStatus.status = "WARNING" Тогда
		Элементы.ДекорацияСервисы1СДеталиСсылка.Заголовок =
			ЗаголовокГруппы(
				ЗаголовокЗаканчиваетсяСрокДействияСервиса(),
				Элементы.ГруппаСервисы1СДетали.Видимость);
	Иначе
		Элементы.ДекорацияСервисы1СДеталиСсылка.Заголовок =
			ЗаголовокГруппы(
				ЗаголовокЕстьДействующиеДоговорыНаСервисы(),
				Элементы.ГруппаСервисы1СДетали.Видимость);
	КонецЕсли;
	
	Если Элементы.ГруппаСервисы1СДетали.Видимость Тогда
		НачатьПолучениеДеталиСервисы1СНаСервере();
	Иначе
		ОтменитьЗадание("Сервисы1С");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НачатьПолучениеДеталиСервисы1СНаСервере()
	
	МониторПортала1СИТС.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Подготовка к получению деталей договоров на сервисы.'"));
	
	Описатель = ОписательЗадания(Задания, "Сервисы1С");
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.КлючФоновогоЗадания = "ПолучениеИнформацииМонитораДоговорыНаСервисы"
		+ Строка(Новый УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.АдресРезультата = Описатель.АдресРезультата;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Логин"                         , Логин);
	ПараметрыЗадания.Вставить("Договоры1СИТС"                 , Ложь);
	ПараметрыЗадания.Вставить("АктивацияСервисовСопровождения", Ложь);
	ПараметрыЗадания.Вставить("ДоговорыНаСервисы"             , Истина);
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.МониторПортала1СИТС.ПолучитьДеталиДанныхМонитора",
		ПараметрыЗадания,
		ПараметрыВыполненияВФоне);
	Описатель.ДлительнаяОперация = РезультатВыполнения;
	
	Если РезультатВыполнения.Статус = "Выполняется" Тогда
		
		Описатель.Идентификатор = РезультатВыполнения.ИдентификаторЗадания;
		ОтобразитьСостояниеДлительнаяОперацияСервисы1С();
		
	ИначеЕсли РезультатВыполнения.Статус = "Выполнено" Тогда
		
		ПриПолученииДеталиСервисы1С();
		
	ИначеЕсли РезультатВыполнения.Статус = "Отменено" Тогда
		
		МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(
			НСтр("ru = 'Не удалось отобразить детали договоров на сервисы. Задание получение данных отменено.'"));
		ОтобразитьОшибкаПолученияДеталиСервисы1С(НСтр("ru = 'Задание отменено.'"));
		
	Иначе
		
		// Ошибка.
		СообщениеЖурнала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить детали договоров на сервисы.
				|%1'"),
			РезультатВыполнения.ПодробноеПредставлениеОшибки);
		МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурнала);
		ОтобразитьОшибкаПолученияДеталиСервисы1С(РезультатВыполнения.КраткоеПредставлениеОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСостояниеДлительнаяОперацияСервисы1С()
	
	Элементы.ГруппаСервисы1СДеталиСодержимое.Видимость = Ложь;
	Элементы.ДекорацияСервисы1СОшибка.Заголовок = "";
	
	Элементы.СтраницыДлительнаяОперацияСервисы1С.Видимость = Истина;
	Элементы.СтраницыДлительнаяОперацияСервисы1С.ТекущаяСтраница =
		Элементы.СтраницаСервисы1СДлительнаяОперация;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьОшибкаПолученияДеталиСервисы1С(Знач СообщениеОбОшибке)
	
	Элементы.СтраницыДлительнаяОперацияСервисы1С.ТекущаяСтраница = Элементы.СтраницаСервисы1СОшибка;
	Элементы.ДекорацияСервисы1СОшибка.Заголовок = СообщениеОбОшибке;
	
	Элементы.СтраницыДлительнаяОперацияСервисы1С.Видимость = Истина;
	Элементы.ГруппаСервисы1СДеталиСодержимое.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПриПолученииДеталиСервисы1С()
	
	Описатель = ОписательЗадания(Задания, "Сервисы1С");
	РезультатОперацииСервиса = ПолучитьИзВременногоХранилища(Описатель.АдресРезультата);
	Если ПустаяСтрока(РезультатОперацииСервиса.ИмяОшибки) Тогда
		
		Данные = РезультатОперацииСервиса.Данные;
		
		Попытка
			ОтобразитьДеталиСервисы1С(Данные);
		Исключение
			ИнформацияОшибка = ИнформацияОбОшибке();
			СообщениеЖурнала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось отобразить детали договоров на сервисы.
					|%1'"),
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОшибка));
			МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурнала);
			ОтобразитьОшибкаПолученияДеталиСервисы1С(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка отображения данных.
						|%1'"),
					ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОшибка)));
		КонецПопытки;
		
		ПроверитьОбработатьИзменениеСостоянияДоговоровНаСервисы(Данные.serviceContractsStatus.status);
		
	Иначе
		
		ОтобразитьОшибкаПолученияДеталиСервисы1С(РезультатОперацииСервиса.СообщениеОбОшибке);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьОбработатьИзменениеСостоянияДоговоровНаСервисы(НовоеСостояниеДоговоров)
	
	ТекущееСостояниеДоговоров = ДанныеМонитора.serviceContractsStatus.status;
	СостояниеИзменилось       = Ложь;
	
	Если ТекущееСостояниеДоговоров = "OK" Или ТекущееСостояниеДоговоров = "WARNING" Тогда
		
		ДанныеМонитора.serviceContractsStatus.status = НовоеСостояниеДоговоров;
		Если Не ЗначениеЗаполнено(НовоеСостояниеДоговоров) Тогда
			ОтобразитьБлокДоговорыНаСервисы();
			СостояниеИзменилось = Истина;
		ИначеЕсли ТекущееСостояниеДоговоров <> НовоеСостояниеДоговоров Тогда
			ОтобразитьОбщееСостояниеДоговоровНаСервисы();
			СостояниеИзменилось = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если СостояниеИзменилось Тогда
		СохранитьОтпечатокДанныхМонитора(Логин, ДанныеМонитора);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьДеталиСервисы1С(Данные)
	
	Элементы.СтраницыДлительнаяОперацияСервисы1С.Видимость = Ложь;
	
	Если Данные.serviceContractsStatus.status = "NO_CONTRACTS" Тогда
		Элементы.ГруппаСервисы1СДетали.Видимость = Ложь;
	Иначе
		Элементы.ГруппаСервисы1СДеталиСодержимое.Видимость = Истина;
		Элементы.ДекорацияСервисы1ССписок.Заголовок = МаркированныйСписокСПериодами(
			Данные.serviceContracts,
			Истина);
		Элементы.ДекорацияКакПродлитьСервис1ССсылка.Заголовок =
			ЗаголовокГруппы(ЗаголовокКакПродлитьСрокДействияСервиса(), Ложь);
		Элементы.ДекорацияКакПродлитьСервис1СИнструкция.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокЕстьДействующиеДоговорыНаСервисы()
	Возврат НСтр("ru = 'Срок действия сервисов'");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокКакОформитьДоговорыНаСервисы()
	Возврат НСтр("ru = 'Как подключить сервисы 1С?'");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокЗаканчиваетсяСрокДействияСервиса()
	Возврат НСтр("ru = 'Заканчивается срок действия сервиса'");
КонецФункции

#КонецОбласти

#Область БлокОбновлениеПрограммы

&НаСервере
Процедура ОтобразитьБлокОбновлениеПрограммы()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПолучениеОбновленийПрограммы") Тогда
		
		МодульПолучениеОбновленийПрограммы = ОбщегоНазначения.ОбщийМодуль("ПолучениеОбновленийПрограммы");
		ПараметрыПолученияОбновлений       = МодульПолучениеОбновленийПрограммы.СлужебнаяПараметрыПолученияОбновлений();
		Если Не МодульПолучениеОбновленийПрограммы.ДоступноИспользованиеОбновленияПрограммы(Ложь, Ложь)
			Или (Не ПараметрыПолученияОбновлений.ПолучатьОбновленияКонфигурации
			И Не ПараметрыПолученияОбновлений.ПолучатьИсправления
			И Не ПараметрыПолученияОбновлений.ПолучатьОбновленияПлатформы) Тогда
			
			Элементы.ГруппаОбновлениеПрограммы.Видимость = Ложь;
			Возврат;
			
		КонецЕсли;
		
	Иначе
		Элементы.ГруппаОбновлениеПрограммы.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ЕстьНеобязательныеОбновления = Ложь;
	
	Элементы.ДекорацияОбновлениеСообщениеОбОшибке.Видимость = Ложь;
	Элементы.ГруппаОбновлениеСодержимое.Видимость           = Истина;
	
	Элементы.ДекорацияОбновлениеПрограммыДеталиСсылка.Видимость = Истина;
	Элементы.ГруппаИнформацияОбОбновленииДетали.Видимость       = Ложь;
	
	НастроитьОтображениеБлокаОбновлениеПрограммы();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОтображениеБлокаОбновлениеПрограммы()
	
	ДоступнаУстановкаИсправлений = ДанныеМонитора.patchesInfo <> Неопределено
		И (ДанныеМонитора.patchesInfo.blockStatus = "200"
		Или ДанныеМонитора.patchesInfo.blockStatus = 200)
		И ДанныеМонитора.patchesInfo.patchesAvailable;
	
	Если ДанныеМонитора.ДоступноОбновление Тогда
		
		Если ДанныеМонитора.updateInfo <> Неопределено
			И (ДанныеМонитора.updateInfo.blockStatus = "200"
			Или ДанныеМонитора.updateInfo.blockStatus = 200)
			И Не ДанныеМонитора.updateInfo.updateAvailable
			И АвтоматическаяЗагрузкаИсправленийВключена() Тогда
			Элементы.ДекорацияСтатусБлокаОбновлениеПрограммы.Картинка = БиблиотекаКартинок.Успешно32;
		Иначе
			Элементы.ДекорацияСтатусБлокаОбновлениеПрограммы.Картинка = БиблиотекаКартинок.ИнтернетПоддержкаВнимание;
		КонецЕсли;
		
		Элементы.ДекорацияОбновлениеПрограммыДеталиСсылка.Заголовок =
			ЗаголовокГруппы(ЗаголовокДоступноОбновлениеПрограммы(), Ложь);
		
		МодульПолучениеОбновленийПрограммы = ОбщегоНазначения.ОбщийМодуль("ПолучениеОбновленийПрограммы");
		Если МодульПолучениеОбновленийПрограммы.ДоступноИспользованиеОбновленияПрограммы(Истина, Ложь) Тогда
			Элементы.ДекорацияУстановитьОбновление.Видимость                       = ЭтоWindowsКлиент
				Или ДоступнаУстановкаИсправлений;
			Элементы.ДекорацияНадписьОбновлениеОбратитесьКАдминистратору.Видимость = Ложь;
		Иначе
			Элементы.ДекорацияУстановитьОбновление.Видимость                       = Ложь;
			Элементы.ДекорацияНадписьОбновлениеОбратитесьКАдминистратору.Видимость = Истина;
		КонецЕсли;
		
	Иначе
		
		Элементы.ДекорацияСтатусБлокаОбновлениеПрограммы.Картинка =
			БиблиотекаКартинок.Успешно32;
		
		Элементы.ДекорацияОбновлениеПрограммыДеталиСсылка.Заголовок =
			ЗаголовокГруппы(ЗаголовокУстановленаАктуальнаяВерсияПрограммы(), Ложь);
		
		ТекстОбновлений = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1
				|Нет доступных обновлений для текущей версии конфигурации <b>%2</b>.'"),
			ДанныеМонитора.programName,
			ИнтернетПоддержкаПользователей.ВерсияКонфигурации());
		
		Элементы.ДекорацияИнформацияОбОбновлении.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(ТекстОбновлений);
		
		МодульПолучениеОбновленийПрограммы = ОбщегоНазначения.ОбщийМодуль("ПолучениеОбновленийПрограммы");
		
		Элементы.СтраницыДлительнаяОперацияОбновлениеПрограммы.Видимость       = Ложь;
		Элементы.ГруппаИнформацияОбОбновлении.Видимость                        = Истина;
		Элементы.ДекорацияУстановитьОбновление.Видимость                       = (ЭтоWindowsКлиент
			И ЕстьНеобязательныеОбновления
			Или ДоступнаУстановкаИсправлений)
			И МодульПолучениеОбновленийПрограммы.ДоступноИспользованиеОбновленияПрограммы(Истина, Ложь);
		Элементы.ДекорацияНадписьОбновлениеОбратитесьКАдминистратору.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НачатьПолучениеДеталиИнформацияОбОбновленииНаСервере()
	
	МониторПортала1СИТС.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Подготовка к получению деталей информации о доступном обновлении.'"));
	
	Описатель = ОписательЗадания(Задания, "ИнформацияОбОбновлении");
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.КлючФоновогоЗадания = "ПолучениеИнформацииМонитораИнформацияОбОбновлении"
		+ Строка(Новый УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.АдресРезультата = Описатель.АдресРезультата;
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.МониторПортала1СИТС.ПолучитьДеталиИнформацияОДоступномОбновлении",
		Неопределено,
		ПараметрыВыполненияВФоне);
	Описатель.ДлительнаяОперация = РезультатВыполнения;
	
	Если РезультатВыполнения.Статус = "Выполняется" Тогда
		
		Описатель.Идентификатор = РезультатВыполнения.ИдентификаторЗадания;
		ОтобразитьСостояниеДлительнаяОперацияИнформацияОбОбновлении();
		
	ИначеЕсли РезультатВыполнения.Статус = "Выполнено" Тогда
		
		ПриПолученииДеталиИнформацияОбОбновлении();
		
	ИначеЕсли РезультатВыполнения.Статус = "Отменено" Тогда
		
		МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(
			НСтр("ru = 'Не удалось отобразить детали информации о доступном обновлении. Задание получение данных отменено.'"));
		ОтобразитьОшибкаПолученияДеталиИнформацияОбОбновлении(НСтр("ru = 'Задание отменено.'"));
		
	Иначе
		
		// Ошибка.
		СообщениеЖурнала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить детали информации о доступном обновлении.
				|%1'"),
			РезультатВыполнения.ПодробноеПредставлениеОшибки);
		МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурнала);
		ОтобразитьОшибкаПолученияДеталиИнформацияОбОбновлении(РезультатВыполнения.КраткоеПредставлениеОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСостояниеДлительнаяОперацияИнформацияОбОбновлении()
	
	Элементы.ГруппаИнформацияОбОбновлении.Видимость = Ложь;
	Элементы.ДекорацияОбновлениеПрограммыОшибка.Заголовок = "";
	
	Элементы.СтраницыДлительнаяОперацияОбновлениеПрограммы.Видимость = Истина;
	Элементы.СтраницыДлительнаяОперацияОбновлениеПрограммы.ТекущаяСтраница =
		Элементы.СтраницаОбновлениеПрограммыДлительнаяОперация;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьОшибкаПолученияДеталиИнформацияОбОбновлении(Знач СообщениеОбОшибке)
	
	Элементы.СтраницыДлительнаяОперацияОбновлениеПрограммы.ТекущаяСтраница =
		Элементы.СтраницаОбновлениеПрограммыОшибка;
	Элементы.ДекорацияОбновлениеПрограммыОшибка.Заголовок =
		СтроковыеФункции.ФорматированнаяСтрока(СообщениеОбОшибке);
	
	Элементы.СтраницыДлительнаяОперацияОбновлениеПрограммы.Видимость = Истина;
	Элементы.ГруппаИнформацияОбОбновлении.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПриПолученииДеталиИнформацияОбОбновлении()
	
	Описатель = ОписательЗадания(Задания, "ИнформацияОбОбновлении");
	ИнформацияОбОбновлении = ПолучитьИзВременногоХранилища(Описатель.АдресРезультата);
	Если ПустаяСтрока(ИнформацияОбОбновлении.ИмяОшибки) Тогда
		
		Попытка
			ОтобразитьДеталиИнформацияОбОбновлении(ИнформацияОбОбновлении);
		Исключение
			ИнформацияОшибка = ИнформацияОбОшибке();
			СообщениеЖурнала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось отобразить информацию об обновлении.
					|%1'"),
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОшибка));
			МониторПортала1СИТС.ЗаписатьОшибкуВЖурналРегистрации(СообщениеЖурнала);
			ОтобразитьОшибкаПолученияДеталиИнформацияОбОбновлении(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка отображения данных.
						|%1'"),
					ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОшибка)));
		КонецПопытки;
		
	Иначе
		
		ОтобразитьОшибкаПолученияДеталиИнформацияОбОбновлении(ИнформацияОбОбновлении.Сообщение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьДеталиИнформацияОбОбновлении(Данные)
	
	Элементы.СтраницыДлительнаяОперацияОбновлениеПрограммы.Видимость = Ложь;
	Элементы.ГруппаИнформацияОбОбновлении.Видимость                  = Истина;
	
	ТекущаяВерсия   = ИнтернетПоддержкаПользователей.ВерсияКонфигурации();
	ТекстОбновлений = Новый Массив();
	ТекстОбновлений.Добавить(ДанныеМонитора.programName);
	
	Если ДанныеМонитора.ДоступноОбновление Тогда
		
		ТекстОбновлений.Добавить(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для текущей версии конфигурации <b>%1</b> доступны обновления:'"),
				ТекущаяВерсия));
		
		// Добавление информации о наличии доступных исправлений (патчей) для текущей версии
		Если Данные.Исправления <> Неопределено Тогда
			
			ОтборСтрок            = Новый Структура("ДляТекущейВерсии", Истина);
			КоличествоИсправлений = Данные.Исправления.НайтиСтроки(ОтборСтрок).Количество();
			
			Если КоличествоИсправлений > 0 Тогда
				ТекстОбновлений.Добавить(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = '%3● Исправления (патчи) для версии <b>%1</b>: <b>%2</b>'"),
						ТекущаяВерсия,
						Строка(КоличествоИсправлений),
						"  "));
			КонецЕсли;
			
		КонецЕсли;
		
		// Добавление информации о наличии доступных обновлений на версии с длительной поддержкой
		Для Каждого ДополнительнаяВерсия Из Данные.ДополнительныеВерсии Цикл
			ТекстОбновлений.Добавить(
				ТекстНовойВерсииКонфигурации(ДополнительнаяВерсия.Конфигурация));
		КонецЦикла;
		
		// Добавление информации о доступном обновлений конфигурации на актуальную версию
		Если Данные.Конфигурация <> Неопределено
			И Данные.Конфигурация.ДоступноОбновление Тогда
			
			ТекстОбновлений.Добавить(
				ТекстНовойВерсииКонфигурации(Данные.Конфигурация));
			
		// Добавление информации о доступном обновлении платформы для текущей версии конфигурации
		ИначеЕсли Данные.Платформа <> Неопределено
			И Данные.Платформа.ДоступноОбновление Тогда
			
			Если Данные.Платформа.ОбязательностьУстановки < 2 Тогда
				ТекстОбновлений.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '%1● Платформа 1С:Предприятие <b>%2</b>. Рекомендуется установить.'"),
					"  ",
					Данные.Платформа.Версия));
			Иначе
				ТекстОбновлений.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '%1● Платформа 1С:Предприятие <b>%2</b>'"),
					"  ",
					Данные.Платформа.Версия));
			КонецЕсли;
			
		КонецЕсли;
		
		ДоступноОбновление = (Данные.Конфигурация <> Неопределено
			И Данные.Конфигурация.ДоступноОбновление
			Или Данные.Исправления <> Неопределено
			И Данные.Исправления.Количество() > 0
			Или Данные.Платформа <> Неопределено
			И Данные.Платформа.ДоступноОбновление);
		
		Если ДоступноОбновление <> ДанныеМонитора.ДоступноОбновление Тогда
			ДанныеМонитора.ДоступноОбновление = ДоступноОбновление;
			НастроитьОтображениеБлокаОбновлениеПрограммы();
			СохранитьОтпечатокДанныхМонитора(Логин, ДанныеМонитора);
		КонецЕсли;
		
	Иначе
		
		// Добавление информации о наличии доступных исправлений (патчей) для текущей версии
		КоличествоИсправлений = 0;
		Если Данные.Исправления <> Неопределено Тогда
			ОтборСтрок            = Новый Структура("ДляТекущейВерсии", Истина);
			КоличествоИсправлений = Данные.Исправления.НайтиСтроки(ОтборСтрок).Количество();
		КонецЕсли;
		
		Если КоличествоИсправлений > 0 Тогда
			
			ТекстОбновлений.Добавить(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Для текущей версии конфигурации <b>%1</b> доступны обновления:'"),
					ТекущаяВерсия));
			ТекстОбновлений.Добавить(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%3● Исправления (патчи) для версии <b>%1</b>: <b>%2</b>'"),
				ТекущаяВерсия,
				Строка(КоличествоИсправлений),
				"  "));
			
		Иначе
			ТекстОбновлений.Добавить(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Нет доступных обновлений для текущей версии конфигурации <b>%1</b>.'"),
				ТекущаяВерсия));
		КонецЕсли;
		
		Если Данные.Конфигурация <> Неопределено
			И Данные.Конфигурация.ДоступноОбновление Тогда
			
			ЕстьНеобязательныеОбновления = Истина;
			
			ТекстОбновлений.Добавить(
				НСтр("ru = 'Для использования нового функционала доступны обновления:'"));
			
			Для Каждого ДополнительнаяВерсия Из Данные.ДополнительныеВерсии Цикл
				ТекстОбновлений.Добавить(
					ТекстНовойВерсииКонфигурации(ДополнительнаяВерсия.Конфигурация));
			КонецЦикла;
			
			ТекстОбновлений.Добавить(
				ТекстНовойВерсииКонфигурации(Данные.Конфигурация));
			
			НастроитьОтображениеБлокаОбновлениеПрограммы();
			
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ДекорацияИнформацияОбОбновлении.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
		СтрСоединить(ТекстОбновлений, Символы.ПС));
	
КонецПроцедуры

&НаСервере
Процедура ДекорацияОбновлениеПрограммыДеталиСсылкаНажатиеНаСервере()
	
	Элементы.ГруппаИнформацияОбОбновленииДетали.Видимость =
		Не Элементы.ГруппаИнформацияОбОбновленииДетали.Видимость;
	
	Если ДанныеМонитора.ДоступноОбновление Тогда
		Элементы.ДекорацияОбновлениеПрограммыДеталиСсылка.Заголовок =
			ЗаголовокГруппы(
				ЗаголовокДоступноОбновлениеПрограммы(),
				Элементы.ГруппаИнформацияОбОбновленииДетали.Видимость);
	Иначе
		Элементы.ДекорацияОбновлениеПрограммыДеталиСсылка.Заголовок =
			ЗаголовокГруппы(
				ЗаголовокУстановленаАктуальнаяВерсияПрограммы(),
				Элементы.ГруппаИнформацияОбОбновленииДетали.Видимость);
	КонецЕсли;
	
	Если ДанныеМонитора.ДоступноОбновление
		Или ДанныеМонитора.ИнформироватьОВыходеВерсийДлительнойПоддержки Тогда
		
		Если Элементы.ГруппаИнформацияОбОбновленииДетали.Видимость Тогда
			НачатьПолучениеДеталиИнформацияОбОбновленииНаСервере();
		Иначе
			ОтменитьЗадание("ИнформацияОбОбновлении");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстНовойВерсииКонфигурации(ОбновлениеКонфигурации)
	
	Если ПустаяСтрока(ОбновлениеКонфигурации.URLНовоеВВерсии) Тогда
		Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%2● Версия <b>%1</b>'"),
			ОбновлениеКонфигурации.Версия,
			"  ");
	Иначе
		Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%3● Версия <b>%1</b>   <a href=""%2"">Новое в версии</a>'"),
			ОбновлениеКонфигурации.Версия,
			ОбновлениеКонфигурации.URLНовоеВВерсии,
			"  ");
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокДоступноОбновлениеПрограммы()
	Возврат НСтр("ru = 'Доступно обновление программы'");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокУстановленаАктуальнаяВерсияПрограммы()
	Возврат НСтр("ru = 'Установлена актуальная версия программы'");
КонецФункции

&НаСервереБезКонтекста
Функция АвтоматическаяЗагрузкаИсправленийВключена()
	
	Возврат ПолучениеОбновленийПрограммы.АвтоматическаяЗагрузкаИсправленийВключена();
	
КонецФункции

#КонецОбласти

#Область ПредупрежденияСервисов

&НаСервере
Процедура ОтобразитьБлокПредупрежденияСервисов()
	
	Элементы.ДекорацияПредупрежденияСервисов1ССообщениеОбОшибке.Видимость = Ложь;
	Элементы.ГруппаПредупрежденияСервисов1ССодержимое.Видимость = Истина;
	Элементы.ГруппаПредупрежденияСервисов1СДетали.Видимость = Ложь;
	
	ОтобразитьОбщееСостояниеПредупрежденийСервисов();
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьОбщееСостояниеПредупрежденийСервисов()
	
	Если ДанныеМонитора.ПредупрежденияСервисов.ЕстьПредупреждения Тогда
		Элементы.ДекорацияКартинкаБлокПредупрежденияСервисов.Картинка =
			БиблиотекаКартинок.ИнтернетПоддержкаВнимание;
		Элементы.ДекорацияПредупрежденияСервисов1СДеталиСсылка.Заголовок =
			ЗаголовокГруппы(
				ЗаголовокПолученыНовыеПредупрежденияСервисов(),
				Элементы.ГруппаПредупрежденияСервисов1СДетали.Видимость);
	Иначе
		Элементы.ДекорацияКартинкаБлокПредупрежденияСервисов.Картинка =
			БиблиотекаКартинок.Успешно32;
		Элементы.ДекорацияПредупрежденияСервисов1СДеталиСсылка.Заголовок =
			ЗаголовокГруппы(
				ЗаголовокПредупрежденияСервисовОтсутствуют(),
				Элементы.ГруппаПредупрежденияСервисов1СДетали.Видимость);
	КонецЕсли;
	
	Элементы.ДекорацияПредупрежденияСервисов1СДеталиСсылка.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ДекорацияПредупрежденияСервисовДеталиСсылкаНажатиеНаСервере()
	
	Элементы.ГруппаПредупрежденияСервисов1СДетали.Видимость =
		Не Элементы.ГруппаПредупрежденияСервисов1СДетали.Видимость;
	Если ДанныеМонитора.ПредупрежденияСервисов.ЕстьПредупреждения Тогда
		Элементы.ДекорацияПредупрежденияСервисов1СДеталиСсылка.Заголовок =
			ЗаголовокГруппы(
				ЗаголовокПолученыНовыеПредупрежденияСервисов(),
				Элементы.ГруппаПредупрежденияСервисов1СДетали.Видимость);
	Иначе
		Элементы.ДекорацияПредупрежденияСервисов1СДеталиСсылка.Заголовок =
			ЗаголовокГруппы(
				ЗаголовокПредупрежденияСервисовОтсутствуют(),
				Элементы.ГруппаПредупрежденияСервисов1СДетали.Видимость);
	КонецЕсли;
	
	Если Элементы.ГруппаПредупрежденияСервисов1СДетали.Видимость Тогда
		ОтобразитьДеталиПредупрежденийСервисов1С(
			ДанныеМонитора.ПредупрежденияСервисов.ДанныеПредупреждений);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьДеталиПредупрежденийСервисов1С(Данные)
	
	Элементы.ГруппаПредупрежденияСервисов1СДеталиСодержимое.Видимость = Истина;
	Элементы.ДекорацияПредупрежденияСервисов1ССписок.Заголовок = МаркированныйСписокПредупреждений(
			Данные);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция МаркированныйСписокПредупреждений(ДанныеСписка)
	
	Если ДанныеСписка = Неопределено
		Или ДанныеСписка.Количество() = 0 Тогда
		Возврат НСтр("ru = '<Нет предупреждений>'");
	КонецЕсли;
	
	ТекстыПредупреждений = МониторПортала1СИТС.ТекстыПредупреждений(ДанныеСписка);
	
	ЧастиСтроки = Новый Массив;
	Для ИндексЭлемента = 0 По ТекстыПредупреждений.ВГраница() Цикл
		
		ЭлементСписка = ТекстыПредупреждений[ИндексЭлемента];
		ЧастиСтроки.Добавить(
			?(ИндексЭлемента > 0, Символы.ПС, "")
			+ "● "
			+ ЭлементСписка);
		
	КонецЦикла;
	
	Возврат СтроковыеФункции.ФорматированнаяСтрока(
		СтрСоединить(ЧастиСтроки));
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокПолученыНовыеПредупрежденияСервисов()
	Возврат НСтр("ru = 'Получены предупреждения'");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокПредупрежденияСервисовОтсутствуют()
	Возврат НСтр("ru = 'Предупреждения отсутствуют'");
КонецФункции

#КонецОбласти

#КонецОбласти
