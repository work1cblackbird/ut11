#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
 
	ВыбранныйФайл = Параметры.ВыбранныйФайл; 	
	ПараметрыФормыЭД = Параметры.ПараметрыФормыЭД;
	
	Если Параметры.Свойство("РежимСправкаПоШаблону") Тогда
		Заголовок = НСтр("ru = 'Формат шаблона договора для ЭДО'");
		Элементы.ДекорацияПредупреждение.Заголовок = 
			СтрШаблон(НСтр("ru = 'Шаблон договора ""%1"" невозможно автоматически преобразовать в формат PDF для отправки по ЭДО, т.к. данный шаблон формируется во внешнем редакторе.
			|
			|Сейчас можно сохранить сформированую печатную форму на 
			|основе шаблона, преобразовать её в формат PDF средствами 
			|внешнего редактора и, прикрепив файл к договору или выбрав с диска, отправить по ЭДО.'"),Параметры.ИмяШаблона);
		Элементы.ГруппаКомандыГиперссылка.Видимость = Ложь; 
		Элементы.ГруппаКомандыШаблон.Видимость = Истина;
		Элементы.Сохранить.КнопкаПоУмолчанию = Истина;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияГиперссылкаНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ПараметрыФормыЭД", ПараметрыФормыЭД);
	ПараметрыФормы.Вставить("ВыбранныйФайл", ВыбранныйФайл);   

	ОткрытьФорму("Обработка.ФорматДоговорнойДокумент101XML.Форма.ФормаСправкиПомощникПДФ_А3ДляЭД", ПараметрыФормы,,,,, ЭтаФорма.ОписаниеОповещенияОЗакрытии);
	
	Закрыть();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть(Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	СохранитьПечатнуюФормуВФайл();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СохранитьПечатнуюФормуВФайл()
		
	ДополнительныеПараметры = Новый Структура("АдресДокумента, ОбработчикЗавершения, ИмяФайлаДляОткрытия");
	ДополнительныеПараметры.АдресДокумента = ПоместитьВоВременноеХранилище(ВыбранныйФайл.ДвоичныеДанные);
	ДополнительныеПараметры.ОбработчикЗавершения= Неопределено;
	ДополнительныеПараметры.ИмяФайлаДляОткрытия	=  ВыбранныйФайл.ИмяФайла;
	
	Оповещение = Новый ОписаниеОповещения("ОткрытьОфисныеДокументыПослеПодключенияРасширения", ЭтотОбъект, ДополнительныеПараметры);
	ТекстСообщения = НСтр("ru = 'Для печати документа необходимо установить расширение для работы с 1С:Предприятием.'");
	ФайловаяСистемаКлиент.ПодключитьРасширениеДляРаботыСФайлами(Оповещение, ТекстСообщения);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОфисныеДокументыПослеПодключенияРасширения(РасширениеПодключено, Контекст) Экспорт 
	
	Если РасширениеПодключено Тогда
		Оповещение = Новый ОписаниеОповещения("ОткрытьОфисныйДокументПослеПолученияВременногоКаталога", ЭтотОбъект, Контекст);
		ФайловаяСистемаКлиент.СоздатьВременныйКаталог(Оповещение);
	Иначе
		ПараметрыСохранения = ФайловаяСистемаКлиент.ПараметрыСохраненияФайлов();
		ПараметрыСохранения.Диалог.Заголовок = НСтр("ru = 'Выбор папки для сохранения печатной формы'");
		
		МассивПередаваемыхФайлов = Новый Массив;
		
		МассивПередаваемыхФайлов.Добавить(Новый ОписаниеПередаваемогоФайла(Контекст.ИмяФайлаДляОткрытия, Контекст.АдресДокумента));
				
		ФайловаяСистемаКлиент.СохранитьФайлы(Новый ОписаниеОповещения, МассивПередаваемыхФайлов, ПараметрыСохранения);
	КонецЕсли;  
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОфисныйДокументПослеПолученияВременногоКаталога(ИмяКаталога, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ОткрытьПослеСохранениеУКлиента", ЭтотОбъект, ДополнительныеПараметры.ОбработчикЗавершения);
	ПараметрыСохранения = ФайловаяСистемаКлиент.ПараметрыСохраненияФайла();
	ПараметрыСохранения.Интерактивно = Ложь;
	
	ИмяФайлаДляОткрытия = ИмяКаталога+"\"+ДополнительныеПараметры.ИмяФайлаДляОткрытия;
	ФайловаяСистемаКлиент.СохранитьФайл(Оповещение, ДополнительныеПараметры.АдресДокумента, ИмяФайлаДляОткрытия, ПараметрыСохранения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПослеСохранениеУКлиента(ПолученныеФайлы, ОбработчикЗавершения) Экспорт 
	
	Если ПолученныеФайлы <> Неопределено Тогда		
		ПутьКФайлу  = ПолученныеФайлы[0].ПолноеИмя;
		ИмяФайла	= ПолученныеФайлы[0].Имя;
		
		ПараметрыЗавершения = Новый Структура;
		ПараметрыЗавершения.Вставить("ОбработчикЗавершения", ОбработчикЗавершения);
		ПараметрыЗавершения.Вставить("ПутьКФайлу", ПутьКФайлу);
		ПараметрыЗавершения.Вставить("ИмяФайла", ИмяФайла);
		
		ФайлНаДиске = Новый Файл(ПутьКФайлу);
		ОписаниеПослеУстановкиТолькоЧтения = Новый ОписаниеОповещения("ОткрытьФайлЗавершение", ЭтотОбъект, ПараметрыЗавершения);
		ФайлНаДиске.НачатьУстановкуТолькоЧтения(ОписаниеПослеУстановкиТолькоЧтения, Не ДоступноРедактированиеПечатнойФормы());
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОбработчикЗавершения); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлЗавершение(ПараметрыЗавершения) Экспорт
	
	ОбработчикЗавершения = ПараметрыЗавершения.ОбработчикЗавершения;
	ПутьКФайлу = ПараметрыЗавершения.ПутьКФайлу;
	ИмяФайла = ПараметрыЗавершения.ИмяФайла;
		
	ПараметрыОткрытияФайла = ФайловаяСистемаКлиент.ПараметрыОткрытияФайла();
	ФайловаяСистемаКлиент.ОткрытьФайл(ПутьКФайлу, ОбработчикЗавершения, ИмяФайла, ПараметрыОткрытияФайла);
	
КонецПроцедуры

&НаСервере
Функция ДоступноРедактированиеПечатнойФормы() 
	
	Возврат Пользователи.РолиДоступны("РедактированиеПечатныхФорм"); 
	
КонецФункции

#КонецОбласти
