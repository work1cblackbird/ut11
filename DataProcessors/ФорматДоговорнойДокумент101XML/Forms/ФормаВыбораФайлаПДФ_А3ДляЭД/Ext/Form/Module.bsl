#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РежимВыбораФайла = "ПечатнаяФормаДокумента";
	Если Параметры.Свойство("ПараметрыФормыЭД") Тогда  
		
		ОбъектУчета = Параметры.ПараметрыФормыЭД.ОбъектУчета;
		ПараметрыФормыЭД = Параметры.ПараметрыФормыЭД;
		ЗаполнитьСписокШаблоновДоговоров(ОбъектУчета); 
		
	КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПродолжитьКоманда(Команда) 
		
	Если РежимВыбораФайла = "СДиска" Тогда
		Элементы.Продолжить.Доступность = Ложь;
		Элементы.ГруппаРежимВыбора.Доступность = Ложь;
		
		// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
		ОбработчикЗавершения = Новый ОписаниеОповещения("ДобавлениеФайлаСДискаЗавершение", ЭтотОбъект);
		
		ПараметрыЗагрузки = Новый Структура("МаксимальныйРазмер, ФильтрДиалогаВыбора, НеОткрыватьКарточку");
		ПараметрыЗагрузки.ФильтрДиалогаВыбора = НСтр("ru = 'Файл PDF'") + " (*.pdf)|*.pdf";
		ПараметрыЗагрузки.МаксимальныйРазмер = 0;
		ПараметрыЗагрузки.НеОткрыватьКарточку = Истина;
		
		РаботаСФайламиКлиент.ДобавитьФайл(ОбработчикЗавершения, ОбъектУчета, ЭтотОбъект, 2, ПараметрыЗагрузки);
		
	ИначеЕсли РежимВыбораФайла = "ПрисоединенныйФайл" Тогда
		Элементы.Продолжить.Доступность = Ложь;
		Элементы.ГруппаРежимВыбора.Доступность = Ложь;
		ОбработчикЗавершения = Новый ОписаниеОповещения("ВыборПрисоединенногоФайлаЗавершение", ЭтотОбъект);
		ИнтерфейсДокументовЭДОУТКлиент.ОткрытьФормуВыбораПрисоединенногоФайлаДоговорногоДокумента(ПараметрыФормыЭД, ОбработчикЗавершения);
		// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами					
	ИначеЕсли РежимВыбораФайла = "ПечатнаяФормаДокумента" Тогда 
		Если ЗначениеЗаполнено(ТекущийШаблонДоговора) Тогда 
			ДлительнаяОперация = НачатьФормированиеПечатныхФорм(ТекущийШаблонДоговора);
			Контекст = Новый Структура("ИменаМакетов", ТекущийШаблонДоговора);
			ОповещениеОЗавершении = Новый ОписаниеОповещения("ПриЗавершенииФормированияПечатныхФорм", ЭтотОбъект, Контекст);
			ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания());
		Иначе
			ОчиститьСообщения();
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не выбран шаблон договора.";
			Сообщение.Поле="ТекущийШаблонДоговора";
			Сообщение.ПутьКДанным = "ТекущийШаблонДоговора";
			Сообщение.Сообщить();
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокШаблоновДоговоров(СсылкаНаДоговор) 
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Печать") Тогда
		Возврат;
	КонецЕсли;
	МодульУправлениеПечатью = ОбщегоНазначения.ОбщийМодуль("УправлениеПечатью");
	
	СписокОбъектов = Новый Массив;
	СписокОбъектов.Добавить(СсылкаНаДоговор.Метаданные());
	
	КомандыПечати = МодульУправлениеПечатью.КомандыПечатиФормы("Справочник.ДоговорыКонтрагентов.Форма.ФормаЭлемента", СписокОбъектов);
	
	Для Каждого ПечатнаяФорма Из КомандыПечати Цикл  
		ШаблоныДоговоров.Добавить(ПечатнаяФорма.Идентификатор, ПечатнаяФорма.Представление);
		Элементы.ТекущийШаблонДоговора.СписокВыбора.Добавить(ПечатнаяФорма.Идентификатор, ПечатнаяФорма.Представление);
	КонецЦикла; 
		
КонецПроцедуры

&НаСервере
Функция НачатьФормированиеПечатныхФорм(ИменаМакетов)
	
	УникальныйИдентификаторХранилища = Новый УникальныйИдентификатор;
	
	ПараметрКоманды = Новый Массив();
	ПараметрКоманды.Добавить(ОбъектУчета); 
	
	ПараметрыПечати = Новый Структура();
	ПараметрыПечати.Вставить("Идентификатор", ИменаМакетов);
	ПараметрыПечати.Вставить("МенеджерПечати", "УправлениеПечатью");
	ПараметрыПечати.Вставить("ОбъектыПечати", ПараметрКоманды);
	ДополнительныеПараметрыСтруктура = Новый Структура("ПодписьИПечать",	
		ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ПечатьДокументовOfficeOpen", "ПодписьИПечать", Ложь));
	ПараметрыПечати.Вставить("ДополнительныеПараметры", ДополнительныеПараметрыСтруктура);
	
	СтруктураПараметров = ПараметрыОткрытияФормыПечати();
	СтруктураПараметров.УникальныйИдентификаторХранилища = УникальныйИдентификаторХранилища; 
	СтруктураПараметров.ИсточникДанных = Неопределено;
	СтруктураПараметров.ИмяМенеджераПечати = "УправлениеПечатью";  
	СтруктураПараметров.ПараметрКоманды = ПараметрКоманды;	
	СтруктураПараметров.ПараметрыПечати = ПараметрыПечати; 
		
	СтруктураПараметров.Вставить("ИменаМакетов", ИменаМакетов);

	Возврат УправлениеПечатьюВызовСервера.НачатьФормированиеПечатныхФорм(СтруктураПараметров); 
	
КонецФункции

&НаКлиенте
Процедура ПриЗавершенииФормированияПечатныхФорм(Результат, Знач Контекст) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если Результат.Статус = "Ошибка" Тогда
			ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
		КонецЕсли;
		СтруктураРезультата = ПолучитьРезультатФоновогоЗадания(Результат);
		КоллекцияПечатныхФорм = СтруктураРезультата.КоллекцияПечатныхФорм; 
		
		ИмяШаблона = КоллекцияПечатныхФорм[0].СинонимМакета;
			
		Если КоллекцияПечатныхФорм[0].ОфисныеДокументы <> Неопределено Тогда
			СохранитьФайлПоШаблону(Истина, ИмяШаблона);
		Иначе
			СохранитьФайлПоШаблону(Ложь, ИмяШаблона);
		КонецЕсли;
		
	КонецЕсли; 			

КонецПроцедуры 

&НаКлиенте
Процедура СохранитьФайлПоШаблону(ОфисныйДокумент, ИмяШаблона)

	ДлительнаяОперация = НачатьСохранениеФайлаПоШаблону();
	Контекст = Новый Структура("ОфисныйДокумент, ИмяШаблона", ОфисныйДокумент, ИмяШаблона);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПриЗавершенииСохраненияФайла", ЭтотОбъект, Контекст);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания()); 

КонецПроцедуры

&НаСервере
Функция НачатьСохранениеФайлаПоШаблону()

	СписокОбъектовМетаданные = Новый Массив();
	СписокОбъектовМетаданные.Добавить(ОбъектУчета.Метаданные());	
	КомандыПечати = УправлениеПечатью.КомандыПечатиФормы("Справочник.ДоговорыКонтрагентов.Форма.ФормаЭлемента", СписокОбъектовМетаданные);
	КомандаПечати = КомандыПечати.НайтиСтроки(Новый Структура("Идентификатор", ТекущийШаблонДоговора));

	СписокОбъектов = Новый Массив();
	СписокОбъектов.Добавить(ОбъектУчета);
	
	Результат = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(КомандаПечати[0]);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификаторХранилища);
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "УправлениеПечатью.НапечататьВФайл",
		Результат, СписокОбъектов, УправлениеПечатью.НастройкиСохранения());
			
КонецФункции 

&НаКлиенте
Процедура ПриЗавершенииСохраненияФайла(Результат, Знач Контекст) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если Результат.Статус = "Ошибка" Тогда
			ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
		КонецЕсли;
		
		СтруктураРезультата = ПолучитьРезультатФоновогоЗадания(Результат);
		ДобавленныйФайл = СтруктураРезультата; 
		
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("РезультатВыбора",ДобавленныйФайл);
		ОбработчикЗавершения = Новый ОписаниеОповещения("ПоказСправкиЗавершение", ЭтотОбъект, ДопПараметры);
		
		// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
		Если Контекст.ОфисныйДокумент = Истина Тогда 
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("ПараметрыФормыЭД", ПараметрыФормыЭД);
			ПараметрыФормы.Вставить("РежимСправкаПоШаблону", Истина);
			ПараметрыФормы.Вставить("ИмяШаблона", Контекст.ИмяШаблона);
			ИнтерфейсДокументовЭДОУТКлиент.ПоказатьСправкуПоФорматуДоговорногоДокумента(ДобавленныйФайл, ПараметрыФормы, ОбработчикЗавершения);
		Иначе
			ИнтерфейсДокументовЭДОУТКлиент.ПоказатьСправкуПоФорматуДоговорногоДокумента(ДобавленныйФайл, ПараметрыФормыЭД, ОбработчикЗавершения);
		КонецЕсли;
		// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
		
	КонецЕсли; 			

КонецПроцедуры 

&НаСервере
Функция ПолучитьРезультатФоновогоЗадания(Результат)
	
	РезультатФоновойОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Если ТипЗнч(РезультатФоновойОперации) = Тип("Структура") Тогда		
		ТаблицаПечатныхФорм = РезультатФоновойОперации.КоллекцияПечатныхФорм;
		ОфисныеДокументы = РезультатФоновойОперации.ОфисныеДокументы;
		ОбъектыПечати = РезультатФоновойОперации.ОбъектыПечати;
		
		Для Каждого ПечатнаяФорма Из ТаблицаПечатныхФорм Цикл
			ОфисныеДокументыНовыеАдреса = Новый Соответствие();
			Если ЗначениеЗаполнено(ПечатнаяФорма.ОфисныеДокументы) Тогда
				Для Каждого ОфисныйДокумент Из ПечатнаяФорма.ОфисныеДокументы Цикл
					ОфисныеДокументыНовыеАдреса.Вставить(ПоместитьВоВременноеХранилище(ОфисныеДокументы[ОфисныйДокумент.Ключ], УникальныйИдентификаторХранилища), ОфисныйДокумент.Значение);
				КонецЦикла;
				ПечатнаяФорма.ОфисныеДокументы = ОфисныеДокументыНовыеАдреса;
			КонецЕсли;
		КонецЦикла;
		СтруктураРезультата = Новый Структура("КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода");
		СтруктураРезультата.КоллекцияПечатныхФорм = ТаблицаПечатныхФорм;
		СтруктураРезультата.ОбъектыПечати = ОбъектыПечати;
		СтруктураРезультата.ПараметрыВывода = РезультатФоновойОперации.ПараметрыВывода;
		
		Возврат СтруктураРезультата;
	Иначе		
		ДобавленныйФайлСтруктура = Новый Структура("ИмяФайла, ДвоичныеДанные");
		ДобавленныйФайлСтруктура.ИмяФайла = РезультатФоновойОперации[0].ИмяФайла;
		ДобавленныйФайлСтруктура.ДвоичныеДанные = РезультатФоновойОперации[0].ДвоичныеДанные;  
	
		Возврат ДобавленныйФайлСтруктура;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПараметрыОткрытияФормыПечати()
	ПараметрыОткрытия = Новый Структура("ИмяМенеджераПечати,ИменаМакетов,ПараметрКоманды,ПараметрыПечати,УникальныйИдентификаторХранилища,
	|ИсточникДанных,КоллекцияПечатныхФорм,ПараметрыИсточника,ТекущийЯзык,ВладелецФормы,ПараметрыВывода");
	ПараметрыОткрытия.Вставить("ОбъектыПечати", Новый СписокЗначений);
	Возврат ПараметрыОткрытия;
КонецФункции  

&НаКлиенте
Функция ПараметрыОжидания()
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект.ВладелецФормы);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Подготовка печатных форм.'");
	ПараметрыОжидания.ОповещениеПользователя.Показать = Ложь;
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.Интервал = 0;
	ПараметрыОжидания.ВыводитьСообщения = Ложь;
	Возврат ПараметрыОжидания;

КонецФункции

&НаКлиенте
Процедура ДобавлениеФайлаСДискаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.Продолжить.Доступность = Ложь;
	Элементы.ГруппаРежимВыбора.Доступность = Ложь;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("РезультатВыбора",Результат);
	ОбработчикЗавершения = Новый ОписаниеОповещения("ПоказСправкиЗавершение", ЭтотОбъект, ДопПараметры);
	ИнтерфейсДокументовЭДОУТКлиент.ПоказатьСправкуПоФорматуДоговорногоДокумента(Результат, ПараметрыФормыЭД, ОбработчикЗавершения);
	
КонецПроцедуры 

&НаКлиенте
Процедура ВыборПрисоединенногоФайлаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.Продолжить.Доступность = Ложь;
	Элементы.ГруппаРежимВыбора.Доступность = Ложь;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеФайла = РаботаСФайламиКлиент.ДанныеФайла(Результат);
	
	Если ДанныеФайла.ФайлРедактируется Тогда
			ТекстСообщения = Нстр("ru = 'Выбран файл, который редактируется. 
		|Невозможно выбрать для отправки редактируемый файл.'");
			
		ОбработчикЗавершения = Новый ОписаниеОповещения("ВыборПрисоединенногоФайлаЗавершение", ЭтотОбъект);	
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("ОбработчикЗавершения", ОбработчикЗавершения);
		ПараметрыОповещения.Вставить("ПараметрыФормыЭД", ПараметрыФормыЭД);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПоказПредупрежденияОРедактированииЗавершение", ЭтотОбъект, ПараметрыОповещения);
		ПоказатьПредупреждение(ОписаниеОповещения, ТекстСообщения,, Нстр("ru = 'Файл договорного документа PDF/A3'"));
		Возврат;
	Иначе
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("РезультатВыбора",Результат);
		ОбработчикЗавершения = Новый ОписаниеОповещения("ПоказСправкиЗавершение", ЭтотОбъект, ДопПараметры);
		ИнтерфейсДокументовЭДОУТКлиент.ПоказатьСправкуПоФорматуДоговорногоДокумента(Результат, ПараметрыФормыЭД, ОбработчикЗавершения);
	КонецЕсли;	
		
КонецПроцедуры

&НаКлиенте
Процедура ПоказПредупрежденияОРедактированииЗавершение(ДополнительныеПараметры) Экспорт
	
	ИнтерфейсДокументовЭДОУТКлиент.ОткрытьФормуВыбораПрисоединенногоФайлаДоговорногоДокумента(ДополнительныеПараметры.ПараметрыФормыЭД,
		ДополнительныеПараметры.ОбработчикЗавершения);
	
КонецПроцедуры   
	
&НаКлиенте
Процедура ПоказСправкиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДополнительныеПараметры.РезультатВыбора) = Тип("Структура")
		И Не ДополнительныеПараметры.РезультатВыбора.Свойство("ФайлСсылка") Тогда
		ПараметрыФайла = РаботаСФайламиСлужебныйКлиентСервер.ПараметрыДобавленияФайла();
		ПараметрыФайла.ВладелецФайлов = ПараметрыФормыЭД.ОбъектУчета;
		ПараметрыФайла.Автор = ПользователиКлиент.ТекущийПользователь();

		ПараметрыФайла.ИмяБезРасширения = ДополнительныеПараметры.РезультатВыбора.ИмяФайла;
		Адрес = ПоместитьВоВременноеХранилище(ДополнительныеПараметры.РезультатВыбора.ДвоичныеДанные);
		
		ФайлСсылка = РаботаСФайламиСлужебныйВызовСервера.ДобавитьФайл(ПараметрыФайла, Адрес);  
		Оповестить("ДобавленФайлКЭДОДоговорныйДокумент", ФайлСсылка, ПараметрыФормыЭД.ОбъектУчета);
		ДополнительныеПараметры.Вставить("ФайлСсылка", ФайлСсылка); 
	КонецЕсли;
	
	Если ТипЗнч(ДополнительныеПараметры.РезультатВыбора) = Тип("СправочникСсылка.ДоговорыКонтрагентовПрисоединенныеФайлы") Тогда 
		Оповестить("ДобавленФайлКЭДОДоговорныйДокумент", ДополнительныеПараметры.РезультатВыбора, ПараметрыФормыЭД.ОбъектУчета);
	КонецЕсли;
	
	Закрыть(ДополнительныеПараметры);
	
КонецПроцедуры	
	
// Параметры:
//  ПараметрыПодготовки - см. ПараметрыПодготовкиФайла
//
&НаКлиенте
Процедура ЗапроситьИПодготовитьФайл(ПараметрыПодготовки) Экспорт // АПК:78 - исключение для вызова клиентских методов.
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ИнтерфейсДокументовЭДОУТКлиент.ЗапроситьФайлПДФ_А3(ПараметрыПодготовки);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
		
КонецПроцедуры

// Процедура-"заглушка", для возможности переопределения формы выбора файла договорного документа
&НаКлиенте
Процедура ЗагрузитьИзФайлаЗавершение(ПомещенныйФайл, ПараметрыФормы) Экспорт // АПК:78 - исключение для вызова клиентских методов.
		
	Возврат;
	
КонецПроцедуры

// Процедура-"заглушка", для возможности переопределения формы выбора файла договорного документа
&НаКлиенте
Процедура ЗапроситьФайлЗавершение(ПомещенныйФайл, ПараметрыФормы) Экспорт // АПК:78 - исключение для вызова клиентских методов.
		
	Возврат;
	
КонецПроцедуры

// Возвращаемое значение:
//  Структура:
//   * ОбработчикЗавершения - Неопределено - Значение по умолчанию
//                          - ОписаниеОповещения - Обработчик, который будет вызван после подготовки файла
//   * Данные               - Неопределено - значение по умолчанию.
//                          - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО - объект, к которому готовится файл.
//                          - Структура:
//                            * ОбъектУчета - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО - объект, к которому
//                                                                                                готовится файл.
//    * Формат               - Строка - текстовое описание текущего формата обена эл. договорами.
//
&НаКлиенте
Функция ПараметрыПодготовкиФайла() Экспорт // АПК:78 - исключение для вызова клиентских методов.
	
	ПараметрыПодготовки = Новый Структура;
	ПараметрыПодготовки.Вставить("ОбработчикЗавершения", Неопределено);
	ПараметрыПодготовки.Вставить("Данные",               Неопределено);	
	ПараметрыПодготовки.Вставить("Формат", Неопределено);

	Возврат ПараметрыПодготовки;
	
КонецФункции

#КонецОбласти 