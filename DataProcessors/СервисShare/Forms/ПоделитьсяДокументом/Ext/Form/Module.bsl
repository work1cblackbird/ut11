
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("ПараметрыДокумента") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ПараметрыДокумента.ДанныеОпубликованногоДокумента);

	ПодключенаЭлектроннаяПочта = ОбщегоНазначения.ПодсистемаСуществует(
		"СтандартныеПодсистемы.РаботаСПочтовымиСообщениями");

	Если Не ПодключенаЭлектроннаяПочта Тогда
		Элементы.ОтправитьСсылкуПоПочте.Видимость = Ложь;
	Иначе
		ОтправлятьПисьмаВФорматеHTML = ОтправлятьПисьмаВФорматеHTML();
		ОбменСКонтрагентамиПереопределяемый.АдресЭлектроннойПочтыКонтрагента(Контрагент, ЭлектронныйАдресКонтрагента);
	КонецЕсли;
	
	КонтактнаяИнформация = Новый Массив;
	ИнтеграцияShareПереопределяемый.КонтактныеЛицаСНомеромТелефона(Контрагент, КонтактнаяИнформация);

	Для Каждого Контакт Из КонтактнаяИнформация Цикл
		Если ПустаяСтрока(Контакт.НомерТелефонаБезКодов) Тогда
			Продолжить;
		КонецЕсли;
		СписокКонтактнойИнформации.Добавить(Контакт, СтрШаблон("%1 (%2%3)", Контакт.ВладелецКонтакта,
			Контакт.КодСтраны, Контакт.НомерТелефонаБезКодов), , БиблиотекаКартинок.ПользовательСистемыВзаимодействия);
	КонецЦикла;

	Если СписокКонтактнойИнформации.Количество() Тогда
		СписокКонтактнойИнформации.Добавить(Неопределено, НСтр("ru = 'Выбрать получателя в WhatsApp'",
			ОбщегоНазначения.КодОсновногоЯзыка()), , БиблиотекаКартинок.WhatsappShare);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПерейтиПоСсылкеНажатие(Элемент)

	ПерейтиПоНавигационнойСсылке(СсылкаДляСкачиванияДокумента);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СкопироватьВБуферОбмена(Команда)
	
	БуферОбмена = "";
	
	ПодключитьОбработчикОжидания("КопироватьСсылкуВБуфер", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСсылкуПоПочте(Команда)
	
	РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыОтправкиЭлектронногоПисьма());
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСсылкуВТелеграм(Команда)

	ПерейтиПоНавигационнойСсылке(СтрокаЗапросаВТелеграм());

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСсылкуВWhatsapp(Команда)
		
	Если СписокКонтактнойИнформации.Количество() = 0 Тогда
		ОтправитьСообщениеПолучателюЧерезWhatsapp(ИнтеграцияShareКлиентСервер.НовыйКонтактнаяИнформация(), Неопределено);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ОтправитьСообщениеПолучателюЧерезWhatsapp", ЭтотОбъект);
	ПоказатьВыборИзМеню(Оповещение, СписокКонтактнойИнформации, Элементы.ОтправитьСсылкуВWhatsapp);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура КопироватьСсылкуВБуфер()
	
	// Необходимо вызывать переменную через ЭтотОбъект, иначе может не скопироваться значение.
	ЭтотОбъект.БуферОбмена = СтрШаблон(
	"<!DOCTYPE html>
	|<html>
	|	<body onload='copy()'>
	|		<input id='input' type='text'/>
	|		<script>
	|			function copy() {
	|				var text = '%1';
	|				var ua = navigator.userAgent;
	|				if (ua.search(/MSIE/) > 0 || ua.search(/Trident/) > 0) {
	|					window.clipboardData.setData('Text', text);
	|				} else {
	|					var copyText = document.getElementById('input');
	|					copyText.value = text;
	|					copyText.select();
	|					document.execCommand('copy');
	|				}
	|			}
	|		</script>
	|	</body>
	|</html>", СсылкаДляСкачиванияДокумента);
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Ссылка получена'"),, 
		НСтр("ru = 'Ссылка скопирована в буфер обмена'"));
		
КонецПроцедуры

&НаКлиенте
Функция СтрокаЗапросаВТелеграм()
	
	ТекстСообщения = СтрЗаменить(ПредставлениеДокумента, " ", "%20");
	
	Возврат СтрШаблон("https://t.me/share/url?url= %1&text= %2",
		СсылкаДляСкачиванияДокумента,
		ТекстСообщения);
	
КонецФункции

&НаКлиенте
Функция СтрокаЗапросаВWhatsapp(Знач КонтактПолучателя)
	
	ТекстСообщения = СтрШаблон("%1 %2",
		СсылкаДляСкачиванияДокумента,
		СтрЗаменить(ПредставлениеДокумента, " ", "%20"));
		
	Если ТипЗнч(КонтактПолучателя) = Тип("ЭлементСпискаЗначений") Тогда
		КонтактПолучателя = КонтактПолучателя.Значение;
	КонецЕсли;
	
	Если КонтактПолучателя <> Неопределено
		И Не ПустаяСтрока(КонтактПолучателя.НомерТелефонаБезКодов) Тогда
		СтрокаЗапроса = СтрШаблон("https://api.whatsapp.com/send?phone=%1&text=%2",
			СтрШаблон("%1%2", КонтактПолучателя.КодСтраны, КонтактПолучателя.НомерТелефонаБезКодов),
			ТекстСообщения);
	Иначе
		СтрокаЗапроса = СтрШаблон("https://api.whatsapp.com/send/?text=%1", ТекстСообщения);
	КонецЕсли;
		
	Возврат СтрокаЗапроса;
	
КонецФункции

&НаКлиенте
Функция ПараметрыОтправкиЭлектронногоПисьма()
	
	ПараметрыДокумента   = ИнтеграцияShareКлиентСервер.НовыеДанныеОпубликованногоДокумента();
	ЗаполнитьЗначенияСвойств(ПараметрыДокумента, ЭтотОбъект);

	ПараметрыОтправки = РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма();
	
	ТекстТемы = СтрШаблон(НСтр("ru = 'Ссылка для скачивания документа %1 от %2'",
		ОбщегоНазначенияКлиент.КодОсновногоЯзыка()), ПредставлениеДокумента, ОрганизацияНаименование);
	
	ТекстСообщения = ТекстПисьмаHTML();
	
	Если ОтправлятьПисьмаВФорматеHTML Тогда
		ТекстСообщения = Новый Структура("ТекстHTML, СтруктураВложений",
			ТекстСообщения, Новый Структура());
	КонецЕсли;
	
	ПараметрыОтправки.Получатель = ЭлектронныйАдресКонтрагента;
	ПараметрыОтправки.Предмет    = ЭлектронныйДокумент;
	ПараметрыОтправки.Тема       = ТекстТемы;
	ПараметрыОтправки.Текст      = ТекстСообщения;
	
	ИнтеграцияShareКлиентПереопределяемый.ПриОпределенииПараметровОтправкиЭлектронногоПисьма(ПараметрыОтправки, ПараметрыДокумента);
	
	Возврат ПараметрыОтправки;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОтправлятьПисьмаВФорматеHTML()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Взаимодействия") Тогда
		
		ИмяФункциональнойОпции = "ОтправлятьПисьмаВФорматеHTML";
		
		Возврат ПолучитьФункциональнуюОпцию(ИмяФункциональнойОпции);
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Функция ТекстПисьмаHTML()
	
	HTMLСтрока = СтрШаблон(
	"<html>
	|<head>
	|<meta http-equiv=""Content-Type"" content=""text/html; charset=utf-8"" />
	|<meta http-equiv=""X-UA-Compatible"" content=""IE=Edge"" />
	|<meta name=""format-detection"" content=""telephone=no"" />
	|</head>
	|<body>
	|<p>Здравствуйте, %1.</p>
	|<p></p>
	|<p>Высылаю Вам электронный документ:</p>
	|<p><strong>%2</strong> на сумму %3 %4</p>
	|<p></p>
	|<p>Данный документ Вы можете просмотреть или загрузить к себе в программу 1С по ссылке:</p>
	|<p></p>
	|<p><strong><a href=""%5"">%6</a></strong></p>
	|
	|</body>
	|</html>", КонтрагентНаименование, ПредставлениеДокумента, СуммаДокумента, ВалютаДокумента,
		СсылкаДляСкачиванияДокумента, СсылкаДляСкачиванияДокумента);
	
	Возврат СтрШаблон(НСтр("ru = '%1'", ОбщегоНазначенияКлиент.КодОсновногоЯзыка()), HTMLСтрока);
	
КонецФункции

&НаКлиенте
Процедура ОтправитьСообщениеПолучателюЧерезWhatsapp(КонтактПолучателя, ДополнительныеПараметры) Экспорт
	
	Если КонтактПолучателя = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПерейтиПоНавигационнойСсылке(СтрокаЗапросаВWhatsapp(КонтактПолучателя));

КонецПроцедуры

#КонецОбласти
