
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПустаяСтрока(Параметры.Заголовок) Тогда
		Заголовок = НСтр("ru = 'Выбранные значения'");
	Иначе
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	Если ТипЗнч(Параметры.ТипПеречисления) <> Тип("Тип") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	МетаданныеПеречисления = Метаданные.НайтиПоТипу(Параметры.ТипПеречисления);
	
	Если Метаданные.НайтиПоТипу(Параметры.ТипПеречисления) = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Для Каждого ЗначениеПеречисленияМетаданные Из МетаданныеПеречисления.ЗначенияПеречисления Цикл
		
		ЗначениеПеречисления = Перечисления[МетаданныеПеречисления.Имя][ЗначениеПеречисленияМетаданные.Имя];
		
		Если Параметры.ИсключаемыеЗначения.Количество() > 0 Тогда
			
			Если Параметры.ИсключаемыеЗначения.НайтиПоЗначению(ЗначениеПеречисления) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		ЭлементСписка = Список.Добавить();
		
		ЭлементСписка.Значение      = ЗначениеПеречисления;
		ЭлементСписка.Представление = ЗначениеПеречисленияМетаданные.Синоним;
		
		Если Параметры.СписокОтбор.НайтиПоЗначению(ЭлементСписка.Значение) <> Неопределено Тогда
			
			ЭлементСписка.Пометка = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ВыбранныеЗначения = Новый СписокЗначений;
	
	Для Каждого ЭлементСписка Из Список Цикл
		Если ЭлементСписка.Пометка Тогда
			ВыбранныеЗначения.Добавить(ЭлементСписка.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(ВыбранныеЗначения);
	
КонецПроцедуры

#КонецОбласти