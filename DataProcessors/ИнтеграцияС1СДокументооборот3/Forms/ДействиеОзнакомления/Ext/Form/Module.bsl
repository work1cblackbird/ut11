#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборот3.ПриСозданииНаСервереФормыДействия(ЭтотОбъект, Параметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПроверитьПодключение();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "УдалениеУчастниковДействия" И Источник = УникальныйИдентификатор Тогда
		ОбновитьПредставленияВДеревеУчастников(Участники);
		УстановитьДоступностьЭлементовФормы();
		
	ИначеЕсли ИмяСобытия = "ОбновитьПредставленияВДеревеУчастников" И Источник = УникальныйИдентификатор Тогда
		ОбновитьПредставленияВДеревеУчастников(Участники);
		УстановитьДоступностьЭлементовФормы();
		ИнтеграцияС1СДокументооборот3Клиент.УчастникиДействияПриАктивизацииСтроки(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстПредупреждения = "";
		ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы,, ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АвторНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнтеграцияС1СДокументооборот3Клиент.ВыбратьСотрудникаИзДереваПодразделений("Автор", ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура АвторАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ДанныеДляАвтоПодбора(
			"DMEmployee",
			ДанныеВыбора,
			Текст,
			СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АвторОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ДанныеДляАвтоПодбора(
			"DMEmployee",
			ДанныеВыбора,
			Текст,
			СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
				"Автор",
				ДанныеВыбора[0].Значение,
				СтандартнаяОбработка,
				ЭтотОбъект);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АвторОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
		"Автор",
		ВыбранноеЗначение,
		СтандартнаяОбработка,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура АвторОчистка(Элемент, СтандартнаяОбработка)
	
	Автор = "";
	АвторID = "";
	АвторТип = "";
	АвторПредставление = "";
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкиАвторизацииНажатие(Элемент)
	
	Оповещение = Новый ОписаниеОповещения("ДекорацияНастройкиАвторизацииНажатиеЗавершение", ЭтотОбъект);
	ИмяФормыПараметров = "Обработка.ИнтеграцияС1СДокументооборотБазоваяФункциональность.Форма.АвторизацияВ1СДокументооборот";
	
	ОткрытьФорму(ИмяФормыПараметров,, ЭтотОбъект,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкиАвторизацииНажатиеЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Истина Тогда
		ПриПодключении();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеОтложенногоНачалаВыполненияНажатие(Элемент, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборот3Клиент.ОписаниеОтложенногоНачалаВыполненияДействияНажатие(
		ЭтотОбъект,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУчастники

&НаКлиенте
Процедура УчастникиПриИзменении(Элемент)
	
	ОбновитьПредставленияВДеревеУчастников(Участники);
	
КонецПроцедуры

&НаКлиенте
Процедура УчастникиПриАктивизацииСтроки(Элемент)
	
	УстановитьДоступностьЭлементовФормы();
	ИнтеграцияС1СДокументооборот3Клиент.УчастникиДействияПриАктивизацииСтроки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УчастникиПередУдалением(Элемент, Отказ)
	
	ИнтеграцияС1СДокументооборот3Клиент.УчастникиДействияПередУдалением(ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура УчастникиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ПараметрыДобавленияУчастниковДействия = ИнтеграцияС1СДокументооборот3Клиент.ПараметрыДобавленияУчастниковДействия();
	ПараметрыДобавленияУчастниковДействия.СЭтапами = Ложь;
	ПараметрыДобавленияУчастниковДействия.ВозможныДочерниеИсполнители = Ложь;
	ПараметрыДобавленияУчастниковДействия.МожноДобавлятьЭтапы = Ложь;
	ПараметрыДобавленияУчастниковДействия.МожноТолькоДобавлятьУчастников = МожноТолькоДобавлятьУчастников;
	ПараметрыДобавленияУчастниковДействия.ФункцияУчастника = "Examinated";
	ИнтеграцияС1СДокументооборот3Клиент.УчастникиДействияПередНачаломДобавления(
		ЭтотОбъект,
		Отказ,
		Копирование,
		ПараметрыДобавленияУчастниковДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура УчастникиПередНачаломИзменения(Элемент, Отказ)
	
	ИнтеграцияС1СДокументооборот3Клиент.УчастникиДействияПередНачаломИзменения(ЭтотОбъект, Отказ);
	
	ТекущиеДанные = Элементы.Участники.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено
			И ИсточникДанныхАктивен
			И ЗначениеЗаполнено(ТекущиеДанные.ИсточникДанныхID) Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УчастникиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Участники.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если ТекущиеДанные <> Неопределено Тогда
		ОткрыватьОсобоеНаименованиеИОписание = Не ТекущиеДанные.ЭтоЭтап;
		РазрешениеОбщее = Неопределено;
		Если Разрешения.Количество() > 0 Тогда
			РазрешениеОбщее = Разрешения[0].Разрешение;
		КонецЕсли;
		ТолькоПросмотрОсобогоНаименованияИОписания = ТолькоПросмотр
			Или (РазрешениеОбщее = "Forbidden")
			Или (РазрешениеОбщее = Неопределено)
			Или (МожноТолькоДобавлятьУчастников И ТекущиеДанные.Недоступно);
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборот3Клиент.УчастникиДействияВыбор(
		ЭтотОбъект,
		ВыбраннаяСтрока,
		Поле,
		СтандартнаяОбработка,
		ОткрыватьОсобоеНаименованиеИОписание,
		ТолькоПросмотрОсобогоНаименованияИОписания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеСтрокиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборот3Клиент.ПредставлениеУчастникаДействияНачалоВыбора(
		Элемент,
		ДанныеВыбора,
		СтандартнаяОбработка,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеСтрокиАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборот3Клиент.ПредставлениеУчастникаДействияАвтоПодбор(
		Элемент,
		Текст,
		ДанныеВыбора,
		ПараметрыПолученияДанных,
		Ожидание,
		СтандартнаяОбработка,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеСтрокиОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборот3Клиент.ПредставлениеУчастникаДействияОкончаниеВводаТекста(
		Элемент,
		Текст,
		ДанныеВыбора,
		ПараметрыПолученияДанных,
		СтандартнаяОбработка,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеСтрокиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборот3Клиент.ПредставлениеУчастникаДействияОбработкаВыбора(
		Элемент,
		ВыбранноеЗначение,
		СтандартнаяОбработка,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеСтрокиОчистка(Элемент, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборот3Клиент.ПредставлениеУчастникаДействияОчистка(
		Элемент,
		СтандартнаяОбработка,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СрокПредставлениеПриИзменении(Элемент)
	
	ИнтеграцияС1СДокументооборот3Клиент.СрокВыполненияДействияУчастникомПриИзменении(ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СрокПредставлениеРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборот3Клиент.СрокВыполненияДействияУчастникомРегулирование(
		ЭтотОбъект,
		Направление,
		СтандартнаяОбработка,
		Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеПриИзменении(Элемент)
	
	ОбновитьПолеЕстьОсобоеНаименованиеОписание(Элементы.Участники.ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьФорму(Истина, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	ЗаписатьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьДиалог(Команда)
	
	Закрыть(КодВозвратаДиалога.ОК);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(КодВозвратаДиалога.Отмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьЗадержку(Команда)
	
	ИнтеграцияС1СДокументооборот3Клиент.ОчиститьЗадержкуДействия(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НаправитьНовымУчастникам(Команда)
	
	ИсполнениеДействияСНовымиУчастниками = Истина;
	ТолькоПросмотр = Ложь;
	Элементы.ЗаписатьИЗакрыть.Видимость = Истина;
	Элементы.ЗаписатьИЗакрыть.КнопкаПоУмолчанию = Истина;
	УстановитьДоступностьЭлементовФормы();
	
	ВариантыУстановкиСрока = ИнтеграцияС1СДокументооборот3КлиентСервер.ВариантыУстановкиСрокаИсполнения();
	
	// Сразу добавим новую строку.
	СтрокиУчастников = Участники.ПолучитьЭлементы();
	СтрокаУчастника = СтрокиУчастников.Добавить();
	СтрокаУчастника.ПредставлениеСтроки =
		ИнтеграцияС1СДокументооборот3КлиентСервер.ПредставлениеУчастника(СтрокаУчастника);
	
	ИндексПредыдущегоЭлемента = СтрокиУчастников.Количество() - 2;
	Если ИндексПредыдущегоЭлемента >= 0 Тогда
		СтрокаУчастника.ВариантУстановкиСрока = СтрокиУчастников[ИндексПредыдущегоЭлемента].ВариантУстановкиСрока;
		Если СтрокаУчастника.ВариантУстановкиСрока = ВариантыУстановкиСрока.ТочныйСрок Тогда
			СтрокаУчастника.Срок = СтрокиУчастников[ИндексПредыдущегоЭлемента].Срок;
		Иначе
			СтрокаУчастника.СрокДни = СтрокиУчастников[ИндексПредыдущегоЭлемента].СрокДни;
			СтрокаУчастника.СрокЧасы = СтрокиУчастников[ИндексПредыдущегоЭлемента].СрокЧасы;
			СтрокаУчастника.СрокМинуты = СтрокиУчастников[ИндексПредыдущегоЭлемента].СрокМинуты;
		КонецЕсли;
	Иначе
		СтрокаУчастника.ВариантУстановкиСрока = ВариантыУстановкиСрока.ОтносительныйСрок;
	КонецЕсли;
	
	СтрокаУчастника.Функция = "Examinated";
	Элементы.Участники.ТекущаяСтрока = СтрокаУчастника.ПолучитьИдентификатор();
	
	ЗаполнитьПредставлениеСроков();
	
	Элементы.НаправитьНовымУчастникам.Видимость = Ложь;
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Защищенный(Команда)
	
	ИнтеграцияС1СДокументооборот3Клиент.ИзменитьПризнакУчастникаДействияЗащищенный(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Клиент

// Заполняет представление сроков в карточке действия.
//
&НаКлиенте
Процедура ЗаполнитьПредставлениеСроков() Экспорт
	
	Для Каждого ЭлементУчастник Из Участники.ПолучитьЭлементы() Цикл
		ЭлементУчастник.СрокПредставление =
			ИнтеграцияС1СДокументооборот3КлиентСервер.ПредставлениеСрокаИсполнения(
				ЭлементУчастник.Срок,
				ЭлементУчастник.СрокДни,
				ЭлементУчастник.СрокЧасы,
				ЭлементУчастник.СрокМинуты,
				ИспользоватьДатуИВремяВСрокахЗадач,
				ЭлементУчастник.ВариантУстановкиСрока);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Ответ, ПараметрыОповещения) Экспорт
	
	ЗаписатьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьЭлементовФормы() Экспорт
	
	Если Элементы.Участники.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Участники.ТекущиеДанные;
	
	Индекс = Участники.ПолучитьЭлементы().Индекс(ТекущиеДанные);
	ЭтоПервыйЭлемент = (Индекс = 0);
	
	ТолькоПросмотрСтроки = (ТекущиеДанные.Состояние = "Completed")
		Или (ИсточникДанныхАктивен И ЗначениеЗаполнено(ТекущиеДанные.ИсточникДанныхID))
		Или (МожноТолькоДобавлятьУчастников И ТекущиеДанные.Недоступно);
	
	Элементы.ПредставлениеСтроки.ТолькоПросмотр = ТолькоПросмотрСтроки;
	Элементы.СрокПредставление.ТолькоПросмотр = ТолькоПросмотрСтроки
		Или МожноТолькоДобавлятьУчастников И Не ЭтоПервыйЭлемент;
	Элементы.ЕстьОсобоеНаименованиеОписание.ТолькоПросмотр = ТолькоПросмотрСтроки;
	Элементы.Описание.ТолькоПросмотр = ТолькоПросмотрСтроки;
	
	Элементы.ПредставлениеСтроки.КнопкаВыбора = Истина;
	
	Элементы.Удалить.Доступность = Не ТолькоПросмотрСтроки;
	Элементы.КонтекстУдалить.Доступность = Не ТолькоПросмотрСтроки;
	
КонецПроцедуры

#КонецОбласти

#Область Сервер

&НаСервере
Процедура ОбработатьОтветВебСервисаНаПолучениеДействия(ДействиеXDTO)
	
	// Заполним общие данные действия.
	ИнтеграцияС1СДокументооборот3.ОбработатьОтветВебСервисаНаПолучениеДействия(ЭтотОбъект, ДействиеXDTO);
	
	// Заполним данные действия ознакомления.
	ПодписыватьУЭП = ДействиеXDTO.signWithDS;
	ИспользоватьОзнакомлениеСУЭП = ДействиеXDTO.useExaminationsWithEnhancedDS;
	ОжидатьЗавершения = ДействиеXDTO.waitForCompletion;
	СостояниеДействия = "";
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ДействиеXDTO, "state") Тогда
		СостояниеДействия = ДействиеXDTO.state;
	КонецЕсли;
	
	// Обновим форму действия.
	ОбновитьДеревоУчастниковПоОбъекту(ДействиеXDTO);
	ИнтеграцияС1СДокументооборот3.УстановитьВидимостьУсловийУчастниковДействия(ЭтотОбъект, ДействиеXDTO);
	ИнтеграцияС1СДокументооборот3.ЗаполнитьСостоянияИРезультатыВФормеДействия(ЭтотОбъект, ДействиеXDTO);
	ИнтеграцияС1СДокументооборот3.УстановитьДоступностьИзмененияУчастниковДействия(ЭтотОбъект, Разрешения);
	
	ЕстьНастройкаДействия =
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ДействиеXDTO, "actionSettings")
		И ЗначениеЗаполнено(ДействиеXDTO.actionSettings.objectID.id);
	Элементы.ОжидатьЗавершения.Доступность = Не ЕстьНастройкаДействия;
	Элементы.ПодписыватьУЭП.Видимость = ИспользоватьОзнакомлениеСУЭП И ДействиеXDTO.target.objectID.type = "DMDocument";
	Элементы.ПодписыватьУЭП.Доступность = Не ЕстьНастройкаДействия;
	
	ИнтеграцияС1СДокументооборот3.УстановитьВидимостьНаправленияНовымУчастникамДействия(
		ЭтотОбъект,
		СостояниеДействия,
		ДействиеXDTO.permissionRows.rows);
	
КонецПроцедуры

// Заполняет дерево участников по объекту.
//
&НаСервере
Процедура ОбновитьДеревоУчастниковПоОбъекту(ДействиеXDTO)
	
	ЭлементыУчастники = Участники.ПолучитьЭлементы();
	ЭлементыУчастники.Очистить();
	
	МаксимальноеЧислоУчастниковДляПоказа = ДействиеXDTO.maximumNumberOfParticipantsToShow;
	ПолноеКоличество = ДействиеXDTO.numberOfParticipants;
	ТекущееКоличество = 0;
	
	Для Каждого Участник Из ДействиеXDTO.participantRows.rows Цикл
		
		ЭлементУчастник = ЭлементыУчастники.Добавить();
		ТекущееКоличество = ТекущееКоличество + 1;
		ИнтеграцияС1СДокументооборот3.ЗаполнитьСтрокуУчастникаДействияИзXDTO(ЭлементУчастник, Участник);
		
		ЭлементУчастник.Функция = "Examinated";
		ОбновитьПолеЕстьОсобоеНаименованиеОписание(ЭлементУчастник);
		ЭлементУчастник.Недоступно = МожноТолькоДобавлятьУчастников;
		ИсточникДанныхАктивен = Участник.dataSourceIsActive;
		
		Если ТолькоПросмотр И ТекущееКоличество >= МаксимальноеЧислоУчастниковДляПоказа Тогда
			
			Если ПолноеКоличество = 0 Тогда
				ПолноеКоличество = ДействиеXDTO.participantRows.rows.Количество();
			КонецЕсли;
			
			ДопОписаниеОзнакомиться = СтрШаблон(
				НСтр("ru = 'и др., всего %1, см ""Ход ознакомления"".'"),
				ПолноеКоличество);
			ЭлементУчастник.ДопОписаниеОзнакомиться = СтрШаблон(" %1 ", ДопОписаниеОзнакомиться);
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбновитьПредставленияВДеревеУчастников(Участники);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОбъектПоДеревуУчастников(Прокси, ДействиеXDTO)
	
	ДействиеXDTO.participantRows = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(
		Прокси,
		"DMActionExaminationParticipantRows");
	УчастникиXDTO = ДействиеXDTO.participantRows.rows; // СписокXDTO
	
	Для Каждого ЭлементУчастник Из Участники.ПолучитьЭлементы() Цикл
		
		Если Не ЗначениеЗаполнено(ЭлементУчастник.УчастникID) Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтрокаXDTO Из УчастникиXDTO Цикл
			Если ЭлементУчастник.Описание = СтрокаXDTO.description
					И ЭлементУчастник.УчастникID = СтрокаXDTO.participant.objectID.id
					И ЭлементУчастник.УчастникТип = СтрокаXDTO.participant.objectID.type
					И ((ЭлементУчастник.Защищенный = Истина И СтрокаXDTO.protected = Ложь)
						Или ЭлементУчастник.Защищенный = Ложь) Тогда
				ВызватьИсключение СтрШаблон(
					НСтр("ru = 'Повторяется участник ознакомления: %1.
						|Удалите дубли, укажите особое описание или установите участнику признак ""Защищенный""'"),
					ЭлементУчастник.УчастникПредставление);
			КонецЕсли;
		КонецЦикла;
		
		УчастникXDTO = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(
			Прокси,
			"DMActionExaminationParticipantRow");
		ИнтеграцияС1СДокументооборот3.ЗаполнитьСтрокуXDTOИзУчастникаДействия(Прокси, УчастникXDTO, ЭлементУчастник);
		УчастникиXDTO.Добавить(УчастникXDTO);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПолеЕстьОсобоеНаименованиеОписание(Строка)
	
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Строка.ЕстьОсобоеНаименованиеОписание = ЗначениеЗаполнено(Строка.Описание);
	
КонецПроцедуры

// Обновляет представления строк в дереве участников.
//
// Параметры:
//   Участники - ДанныеФормыДерево - дерево с участниками.
//
&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПредставленияВДеревеУчастников(Участники)
	
	Для Каждого ЭлементУчастник Из Участники.ПолучитьЭлементы() Цикл
		ЭлементУчастник.ПредставлениеСтроки = ИнтеграцияС1СДокументооборот3КлиентСервер.ПредставлениеУчастника(
			ЭлементУчастник,
			ЭлементУчастник.ДопОписаниеОзнакомиться);
	КонецЦикла;
	
КонецПроцедуры

// Формирует объект XDTO для записи в Документообороте по данным из формы действия.
//
// Возвращаемое значение:
//   Строка - строковое представление объекта XDTO типа, наследующего DMAction.
//
&НаСервере
Функция ПодготовитьДействиеДляЗаписи()
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	ДействиеXDTO = ИнтеграцияС1СДокументооборот3.ПодготовитьДействиеДляЗаписи(Прокси, ЭтотОбъект);
	
	ДействиеXDTO.signWithDS = ПодписыватьУЭП;
	
	ОбновитьОбъектПоДеревуУчастников(Прокси, ДействиеXDTO);
	
	ИнтеграцияС1СДокументооборот3.ЗаполнитьТаблицуУчастниковДействия(ЭтотОбъект.УчастникиПередЗаписью, ДействиеXDTO);
	
	Возврат ИнтеграцияС1СДокументооборотБазоваяФункциональность.ОбъектXDTOВСтроку(Прокси, ДействиеXDTO);
	
КонецФункции

#КонецОбласти

#Область Подключение

// Проверяет подключение к ДО, выводя окно авторизации, если необходимо, и изменяя форму согласно результату.
//
&НаКлиенте
Процедура ПроверитьПодключение()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьПодключениеЗавершение", ЭтотОбъект);
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ПроверитьПодключение(
		ОписаниеОповещения,
		ЭтотОбъект,,
		Ложь,
		Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодключениеЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Истина Тогда
		ПриПодключении();
	Иначе // Не удалось подключиться к ДО.
		ОбработатьФормуСогласноВерсииСервиса();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПодключении()
	
	Если ОбработатьФормуСогласноВерсииСервиса() Тогда
		ОбновитьФорму();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбработатьФормуСогласноВерсииСервиса() Экспорт
	
	ВерсияСервиса = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВерсияСервиса();
	
	Если Не ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СервисДоступен(ВерсияСервиса) Тогда
		Элементы.ГруппаСтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаДокументооборотНедоступен;
		Возврат Ложь;
	КонецЕсли;
	
	ФормаОбработанаУспешно = Истина;
	
	Попытка
		
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ДоступенФункционалВерсииСервиса("3.0.13.29") Тогда
			
			Элементы.ГруппаСтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаДокументооборотДоступен;
			
			НастройкиДокументооборота = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьНастройки();
			ИспользоватьДатуИВремяВСрокахЗадач = НастройкиДокументооборота.ИспользоватьДатуИВремяВСрокахЗадач;
			
		Иначе
			
			Элементы.ГруппаСтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаВерсияНеПоддерживается;
			ФормаОбработанаУспешно = Ложь;
			
		КонецЕсли;
		
	Исключение
		
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.НужноОбработатьФорму(ИнформацияОбОшибке()) Тогда
			ОбработатьФормуСогласноВерсииСервиса();
		КонецЕсли;
		
	КонецПопытки;
	
	Возврат ФормаОбработанаУспешно;
	
КонецФункции

#КонецОбласти

#Область ОбновитьФорму

&НаКлиенте
Процедура ОбновитьФорму(ВыводитьОкноОжидания = Ложь, СкрыватьИнтерфейс = Истина)
	
	Если Не ВыводитьОкноОжидания И Не СкрыватьИнтерфейс Тогда
		Элементы.СтраницаДокументооборотДоступен.Доступность = Ложь;
	КонецЕсли;
	
	Колонки = Новый Массив;
	Если РежимДиалога Тогда
		Колонки.Добавить("dialogueMode");
	КонецЕсли;
	ДлительнаяОперация = ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ПолучитьОбъектАсинхронно(
		УникальныйИдентификатор,
		Тип,
		ID,
		Колонки);
	ПараметрыОповещения = Новый Структура("ВыводитьОкноОжидания, СкрыватьИнтерфейс",
		ВыводитьОкноОжидания,
		СкрыватьИнтерфейс);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбновитьФормуЗавершение", ЭтотОбъект, ПараметрыОповещения);
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьЗапросАсинхронно(
		ЭтотОбъект,
		ДлительнаяОперация,
		ОповещениеОЗавершении,
		ВыводитьОкноОжидания,,
		СкрыватьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФормуЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если Не ПараметрыОповещения.ВыводитьОкноОжидания И Не ПараметрыОповещения.СкрыватьИнтерфейс Тогда
		Элементы.СтраницаДокументооборотДоступен.Доступность = Истина;
	КонецЕсли;
	
	ОбработатьФорму = Ложь;
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ПроверитьОшибкиПриОбновленииФормы(
		ЭтотОбъект,
		Результат,
		ОбработатьФорму);
	Если ОбработатьФорму Тогда
		ОбработатьФормуСогласноВерсииСервиса();
	КонецЕсли;
	
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОперацияВыполнена(Результат, ОбработатьФорму) Тогда
		ОбновлениеНаСервереЗавершение(Результат.РезультатДлительнойОперации);
		ИнтеграцияС1СДокументооборот3Клиент.ПриОткрытииФормыДействия(ЭтотОбъект);
	ИначеЕсли ОбработатьФорму Тогда
		ОбработатьФормуСогласноВерсииСервиса();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновлениеНаСервереЗавершение(ДействиеXDTOСтрока)
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	ДействиеXDTO = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СтрокаВОбъектXDTO(Прокси, ДействиеXDTOСтрока);
	ОбработатьОтветВебСервисаНаПолучениеДействия(ДействиеXDTO);
	
КонецПроцедуры

#КонецОбласти

#Область ЗаписатьОбъект

&НаКлиенте
Процедура ПередЗаписью(Отказ)
	
	Если ИсполнениеДействияСНовымиУчастниками
			И Не ИнтеграцияС1СДокументооборот3КлиентСервер.УчастникиДействияИзмененыВКарточке(ЭтотОбъект) Тогда
		ПоказатьПредупреждение(,
			НСтр("ru = 'Новые участники не добавлены.
				|Для запуска нового ознакомления добавьте участников.'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть()
	
	Если Не ЗначениеЗаполнено(ID) Или Не ЗначениеЗаполнено(Тип) Тогда
		Закрыть(КодВозвратаДиалога.Отмена);
		Возврат;
	КонецЕсли;
	
	Если Не Модифицированность Тогда
		Если РежимДиалога Тогда
			Закрыть(КодВозвратаДиалога.ОК);
		Иначе
			Закрыть(КодВозвратаДиалога.Отмена);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ДействиеXDTOСтрока = ПодготовитьДействиеДляЗаписи();
	
	Отказ = Ложь;
	ПередЗаписью(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборот3Клиент.ПроверитьУчастниковИЗаписатьДействие(
		ДействиеXDTOСтрока,
		ЭтотОбъект,
		Новый ОписаниеОповещения("ЗаписатьИЗакрытьЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрытьЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	ОбработатьФорму = Ложь;
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОперацияВыполнена(Результат, ОбработатьФорму) Тогда
		Модифицированность = Ложь;
		ДанныеОбработки = Новый Структура("ПредметID, ПредметТип", ОсновнойПредметID, ОсновнойПредметТип);
		Оповестить("Документооборот_ДействиеСОбработкой", Новый Структура("ДанныеОбработки", ДанныеОбработки), ЭтотОбъект);
		Закрыть();
	ИначеЕсли ОбработатьФорму Тогда
		ОбработатьФормуСогласноВерсииСервиса();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти