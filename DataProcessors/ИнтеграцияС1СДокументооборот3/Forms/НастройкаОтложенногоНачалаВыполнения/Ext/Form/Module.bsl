#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = Параметры.Заголовок;
	ОтложенноеНачалоДней = Параметры.Дни;
	ОтложенноеНачалоЧасов = Параметры.Часы;
	ОтложеннаяДата = Параметры.ОтложеннаяДата;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтложенныйСтартДнейПриИзменении(Элемент)
	
	ОтложеннаяДата = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтложенныйСтартЧасовПриИзменении(Элемент)
	
	ОтложеннаяДата = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтложеннаяДатаПриИзменении(Элемент)
	
	ОтложенноеНачалоДней = 0;
	ОтложенноеНачалоЧасов = 0;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Если ЗначениеЗаполнено(ОтложеннаяДата) И ОтложеннаяДата <= ТекущаяДатаСеансаНаСервере() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Выбранная дата меньше текущей.
				|Укажите корректную дату отложенного выполнения.'"),, ОтложеннаяДата);
		Возврат;
	КонецЕсли;
	
	ДанныеВозврата = Новый Структура;
	ДанныеВозврата.Вставить("Дни", ОтложенноеНачалоДней);
	ДанныеВозврата.Вставить("Часы", ОтложенноеНачалоЧасов);
	ДанныеВозврата.Вставить("ОтложеннаяДата", ОтложеннаяДата);
	Закрыть(ДанныеВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ТекущаяДатаСеансаНаСервере()
	
	Возврат ТекущаяДатаСеанса();
	
КонецФункции

#КонецОбласти