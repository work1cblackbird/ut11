
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УчетнаяЗапись        = Параметры.УчетнаяЗапись;
	ИмяОбъектаМетаданных = Параметры.ИмяОбъектаМетаданных;
	ИмяТабличнойЧасти    = Параметры.ИмяТабличнойЧасти;
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Параметры.ДополнительныеПараметры) = Тип("Структура")
			И Параметры.ДополнительныеПараметры.Свойство("ЗначенияПараметровСсылкиНаОбъект") Тогда
		ДанныеУчетнойЗаписи = Параметры.ДополнительныеПараметры.ЗначенияПараметровСсылкиНаОбъект;
	Иначе
		ДанныеУчетнойЗаписи = ИнтеграцияСЯндексМаркетВызовСервера.ДанныеУчетнойЗаписиЯндексМаркет(Параметры.УчетнаяЗапись);
	КонецЕсли;
	
	КонстантыСервиса      = ИнтеграцияСЯндексМаркетКлиентСервер.КонстантыСервиса();
	СпособОтраженияПродаж = ДанныеУчетнойЗаписи.СпособОтраженияПродаж;
	
	Если СпособОтраженияПродаж = Неопределено Тогда
		СпособОтраженияПродаж = КонстантыСервиса.СпособОтраженияПродажРеализация;
	КонецЕсли;
	
	Если СпособОтраженияПродаж = КонстантыСервиса.СпособОтраженияПродажРеализация Тогда
		СхемаКомпоновкиДанных = Обработки.УправлениеПродажамиНаЯндексМаркет.ПолучитьМакет("СводныйОтчетДляСпособаРеализация");
	ИначеЕсли СпособОтраженияПродаж = КонстантыСервиса.СпособОтраженияПродажКомиссия Тогда
		СхемаКомпоновкиДанных = Обработки.УправлениеПродажамиНаЯндексМаркет.ПолучитьМакет("СводныйОтчетДляСпособаКомиссия");
	Иначе
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЗначениеПараметра = СхемаКомпоновкиДанных.Параметры.Найти("СопоставленныеДанные");
	ЗначениеПараметра.Значение = Параметры.ТаблицаСопоставленияДанных.Выгрузить().Скопировать();
	
	КомпоновкаДанныхСервер.УстановитьПараметрСКД(СхемаКомпоновкиДанных, 
		"ЗагруженныеОбъекты", 
		Параметры.ЗагруженныеОбъекты, 
		Истина);
	КомпоновкаДанныхСервер.УстановитьПараметрСКД(СхемаКомпоновкиДанных, 
		"Организация", 
		ДанныеУчетнойЗаписи.Организация, 
		Истина);
	
	Если СпособОтраженияПродаж = КонстантыСервиса.СпособОтраженияПродажРеализация Тогда
		КомпоновкаДанныхСервер.УстановитьПараметрСКД(СхемаКомпоновкиДанных, 
			"Склад", 
			ДанныеУчетнойЗаписи.СкладОтгрузки, 
			Истина);
		КомпоновкаДанныхСервер.УстановитьПараметрСКД(СхемаКомпоновкиДанных, 
			"КомментарийНевыкупТоваров",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '(Яндекс Маркет) %1. Невыкуп товаров'"),
				ДанныеУчетнойЗаписи.Представление) + "%", Истина);
		
	Иначе
		КомпоновкаДанныхСервер.УстановитьПараметрСКД(СхемаКомпоновкиДанных, 
			"Партнер", 
			ДанныеУчетнойЗаписи.Партнер, 
			Истина);
		КомпоновкаДанныхСервер.УстановитьПараметрСКД(СхемаКомпоновкиДанных, 
			"Контрагент", 
			ДанныеУчетнойЗаписи.Контрагент, 
			Истина);
	КонецЕсли;
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	КомпоновщикНастроек.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, 
		КомпоновщикНастроек.ПолучитьНастройки(), 
		,,
		Тип("ГенераторМакетаКомпоновкиДанных"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,,, Истина);
	
	Результат.Очистить();
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(Результат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры

#КонецОбласти
