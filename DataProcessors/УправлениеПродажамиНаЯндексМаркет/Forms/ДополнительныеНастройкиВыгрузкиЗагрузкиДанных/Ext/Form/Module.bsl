
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УчетнаяЗапись        = Параметры.УчетнаяЗапись;
	ВариантНастроек      = Параметры.ВариантНастроек;
	ИмяОбъектаМетаданных = Параметры.ИмяОбъектаМетаданных;
	ИмяТабличнойЧасти    = Параметры.ИмяТабличнойЧасти;
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Заголовок = Параметры.ЗаголовокФормы;
		
	Если ВариантНастроек = "ДанныеОРеализованныхТоварах" Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаДанныеОРеализованныхТоварах;
		
		Организация           = Параметры.ЗначенияПараметровСсылкиНаОбъект.Организация;
		СхемаРаботы           = Параметры.ЗначенияПараметровСсылкиНаОбъект.СхемаРаботы;
		СпособОтраженияПродаж = Параметры.ЗначенияПараметровСсылкиНаОбъект.СпособОтраженияПродаж;
		СкладОтгрузки         = Параметры.ЗначенияПараметровСсылкиНаОбъект.СкладОтгрузки;
		Партнер               = Параметры.ЗначенияПараметровСсылкиНаОбъект.Партнер;
		Контрагент            = Параметры.ЗначенияПараметровСсылкиНаОбъект.Контрагент;
		ДоговорВзаимозачеты   = Параметры.ЗначенияПараметровСсылкиНаОбъект.ДоговорВзаимозачеты;
		СоглашениеКомиссия    = Параметры.ЗначенияПараметровСсылкиНаОбъект.СоглашениеКомиссия;
		ДоговорКомиссия       = Параметры.ЗначенияПараметровСсылкиНаОбъект.ДоговорКомиссия;
		РозничныйПартнер      = Параметры.ЗначенияПараметровСсылкиНаОбъект.РозничныйПартнер;
		РозничныйКонтрагент   = Параметры.ЗначенияПараметровСсылкиНаОбъект.РозничныйКонтрагент;
		СоглашениеРеализация  = Параметры.ЗначенияПараметровСсылкиНаОбъект.СоглашениеРеализация;
		ДоговорРеализация     = Параметры.ЗначенияПараметровСсылкиНаОбъект.ДоговорРеализация;
		
	Иначе
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Открытие формы невозможно. Не указан вариант настроек.'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;    
	
	УстановитьВидимостьДоступность(Элементы.Страницы.ТекущаяСтраница.Имя);
	
КонецПроцедуры   

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НаборКонстант" 
			И (Источник = "ИспользоватьОтгрузкуБезПереходаПраваСобственности"
				Или Источник = "ИспользоватьКорректировкиРеализаций"
				Или Источник = "ИспользоватьКомиссиюПриПродажах" 
				Или Источник = "ИспользоватьПартнеровКакКонтрагентов"
				Или Источник = "ИспользоватьСоглашенияСКлиентами"
				Или Источник = "ИспользоватьДоговорыСКлиентами") Тогда
		Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаДанныеОРеализованныхТоварах Тогда
			УстановитьВидимостьДоступность(Элементы.Страницы.ТекущаяСтраница.Имя);
		КонецЕсли;
	
	ИначеЕсли ИмяСобытия = "Запись_Партнеры" 
				И (Параметр = Партнер
					Или Параметр = РозничныйПартнер) Тогда
		Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаДанныеОРеализованныхТоварах Тогда
			УстановитьВидимостьДоступность(Элементы.Страницы.ТекущаяСтраница.Имя);
		КонецЕсли;
	
	ИначеЕсли ИмяСобытия = "Запись_Контрагенты" 
				И (Параметр = Контрагент
					Или Параметр = РозничныйКонтрагент) Тогда
		Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаДанныеОРеализованныхТоварах Тогда
			УстановитьВидимостьДоступность(Элементы.Страницы.ТекущаяСтраница.Имя);
		КонецЕсли;
	
	ИначеЕсли ИмяСобытия = "ТорговыеПредложения_ПослеЗаписи" Тогда
		Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаДанныеОРеализованныхТоварах Тогда
			УстановитьВидимостьДоступность(Элементы.Страницы.ТекущаяСтраница.Имя);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособОтраженияПродажРеализацияПриИзменении(Элемент)

	УстановитьВидимостьДоступность(Элементы.СтраницаДанныеОРеализованныхТоварах.Имя);

КонецПроцедуры

&НаКлиенте
Процедура СпособОтраженияПродажКомиссияПриИзменении(Элемент)

	УстановитьВидимостьДоступность(Элементы.СтраницаДанныеОРеализованныхТоварах.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ПартнерПриИзменении(Элемент)
	
	ЗаполнитьУсловияПродажПоУмолчанию(Истина);
	УстановитьВидимостьДоступность(Элементы.Страницы.ТекущаяСтраница.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ЗаполнитьДоговорПоУмолчанию(Истина);
	УстановитьВидимостьДоступность(Элементы.Страницы.ТекущаяСтраница.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорВзаимозачетыСоздание(Элемент, СтандартнаяОбработка)
	
	ПродажиКлиент.ОткрытьФормуСозданияДоговора(ЭтотОбъект, 
		Элемент, 
		"ДоговорВзаимозачеты", 
		Партнер, 
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоглашениеКомиссияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(СоглашениеКомиссия) Тогда
		ЗаполнитьУсловияПродажПоСоглашению(Истина);
	КонецЕсли;
	
	УстановитьВидимостьДоступность(Элементы.Страницы.ТекущаяСтраница.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура СоглашениеКомиссияНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ДанныеФормыСтруктура = Новый Структура;
	ДанныеФормыСтруктура.Вставить("Партнер",    Партнер);
	ДанныеФормыСтруктура.Вставить("Контрагент", Контрагент);
	
	ПараметрыВыбораСоглашения = ПродажиКлиент.ПараметрыНачалаВыбораСоглашенияСКлиентом();
	
	ПараметрыВыбораСоглашения.Элемент               = Элемент;
	ПараметрыВыбораСоглашения.Организация           = Организация;
	ПараметрыВыбораСоглашения.Партнер               = Партнер;
	ПараметрыВыбораСоглашения.Документ              = СоглашениеКомиссия;
	ПараметрыВыбораСоглашения.ДатаДокумента         = ОбщегоНазначенияУТВызовСервера.ДатаСеанса();
	ПараметрыВыбораСоглашения.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию");
	ПараметрыВыбораСоглашения.КомиссионныеПродажи25 = Истина;
	ПараметрыВыбораСоглашения.ДанныеФормыСтруктура  = ДанныеФормыСтруктура;
	
	ПараметрыВыбора = Новый Массив;
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Организация",           Организация));
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ХозяйственнаяОперация", ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию")));
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
	ПродажиКлиент.НачалоВыбораСоглашенияСКлиентом(ПараметрыВыбораСоглашения, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКомиссияСоздание(Элемент, СтандартнаяОбработка)
	
	ПродажиКлиент.ОткрытьФормуСозданияДоговора(ЭтотОбъект, 
		Элемент, 
		"ДоговорКомиссия", 
		Партнер, 
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РозничныйПартнерПриИзменении(Элемент)
	
	ЗаполнитьУсловияПродажПоУмолчанию(Ложь);
	УстановитьВидимостьДоступность(Элементы.Страницы.ТекущаяСтраница.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура РозничныйКонтрагентПриИзменении(Элемент)
	
	ЗаполнитьДоговорПоУмолчанию(Ложь);
	УстановитьВидимостьДоступность(Элементы.Страницы.ТекущаяСтраница.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура СоглашениеРеализацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(СоглашениеРеализация) Тогда
		ЗаполнитьУсловияПродажПоСоглашению(Ложь);
	КонецЕсли;
	
	УстановитьВидимостьДоступность(Элементы.Страницы.ТекущаяСтраница.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура СоглашениеРеализацияНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)

	ДанныеФормыСтруктура = Новый Структура;
	ДанныеФормыСтруктура.Вставить("Партнер",    РозничныйПартнер);
	ДанныеФормыСтруктура.Вставить("Контрагент", РозничныйКонтрагент);
	
	ПараметрыВыбораСоглашения = ПродажиКлиент.ПараметрыНачалаВыбораСоглашенияСКлиентом();
	
	ПараметрыВыбораСоглашения.Элемент               = Элемент;
	ПараметрыВыбораСоглашения.Организация           = Организация;
	ПараметрыВыбораСоглашения.Партнер               = РозничныйПартнер;
	ПараметрыВыбораСоглашения.Документ              = СоглашениеРеализация;
	ПараметрыВыбораСоглашения.ДатаДокумента         = ОбщегоНазначенияУТВызовСервера.ДатаСеанса();
	ПараметрыВыбораСоглашения.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.РеализацияКлиенту");
	ПараметрыВыбораСоглашения.ДанныеФормыСтруктура  = ДанныеФормыСтруктура;
	
	ПараметрыВыбора = Новый Массив;
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Организация",           Организация));
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ХозяйственнаяОперация", ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.РеализацияКлиенту")));
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
	ПродажиКлиент.НачалоВыбораСоглашенияСКлиентом(ПараметрыВыбораСоглашения, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорРеализацияСоздание(Элемент, СтандартнаяОбработка)
	
	ПродажиКлиент.ОткрытьФормуСозданияДоговора(ЭтотОбъект, 
		Элемент, 
		"ДоговорРеализация", 
		РозничныйПартнер, 
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	
	Результат = Новый Структура;
	
	Если ВариантНастроек = "ДанныеОРеализованныхТоварах" Тогда
		ДополнительныеНастройки = Новый Структура;
		ДополнительныеНастройки.Вставить("СпособОтраженияПродаж", СпособОтраженияПродаж);
		ДополнительныеНастройки.Вставить("СкладОтгрузки",         СкладОтгрузки);
		ДополнительныеНастройки.Вставить("ДоговорВзаимозачеты",   ДоговорВзаимозачеты);
		ДополнительныеНастройки.Вставить("РозничныйПартнер",      РозничныйПартнер);
		ДополнительныеНастройки.Вставить("РозничныйКонтрагент",   РозничныйКонтрагент);
		ДополнительныеНастройки.Вставить("СоглашениеРеализация",  СоглашениеРеализация);
		ДополнительныеНастройки.Вставить("ДоговорРеализация",     ДоговорРеализация);
		
		Результат.Вставить("Организация",             Организация);
		Результат.Вставить("СхемаРаботы",             СхемаРаботы);
		Результат.Вставить("Партнер",                 Партнер);
		Результат.Вставить("Контрагент",              Контрагент);
		Результат.Вставить("СоглашениеКомиссия",      СоглашениеКомиссия);
		Результат.Вставить("ДоговорКомиссия",         ДоговорКомиссия);
		Результат.Вставить("ДополнительныеНастройки", ДополнительныеНастройки);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Результат, ДополнительныеНастройки, Истина);
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьДоступность(ТекущийЭлементИмя = Неопределено)
	
	Если ТекущийЭлементИмя = Элементы.СтраницаДанныеОРеализованныхТоварах.Имя Тогда
		УстановитьПривилегированныйРежим(Истина);
		ИспользоватьОтгрузкуБезПереходаПраваСобственности = ПолучитьФункциональнуюОпцию("ИспользоватьОтгрузкуБезПереходаПраваСобственности");
		ИспользоватьКорректировкиРеализаций               = ПолучитьФункциональнуюОпцию("ИспользоватьКорректировкиРеализаций");
		ИспользоватьКомиссиюПриПродажах                   = ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриПродажах");
		ИспользоватьПартнеровКакКонтрагентов              = ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов");
		ИспользоватьСоглашенияСКлиентами                  = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами");
		ИспользоватьДоговорыСКлиентами                    = ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами");
		УстановитьПривилегированныйРежим(Ложь);
		
		КонстантыСервиса                 = ИнтеграцияСЯндексМаркетКлиентСервер.КонстантыСервиса();
		СпособОтраженияПродаж_Реализация = КонстантыСервиса.СпособОтраженияПродажРеализация;
		СпособОтраженияПродаж_Комиссия   = КонстантыСервиса.СпособОтраженияПродажКомиссия;
		
		Элементы.СпособОтраженияПродажРеализация.Доступность                   = ИспользоватьОтгрузкуБезПереходаПраваСобственности;
		Элементы.ГруппаСпособОтраженияПродажРеализацияНеДоступен.Видимость     = Не ИспользоватьОтгрузкуБезПереходаПраваСобственности;
		Элементы.ГруппаСпособОтраженияПродажРеализацияОграничения.Видимость    = ИспользоватьОтгрузкуБезПереходаПраваСобственности 
																					И Не ИспользоватьКорректировкиРеализаций;
		Элементы.СпособОтраженияПродажКомиссия.Доступность                     = ИспользоватьКомиссиюПриПродажах;
		Элементы.ГруппаСпособОтраженияПродажКомиссияНеДоступен.Видимость       = Не ИспользоватьКомиссиюПриПродажах;
		
		Элементы.ГруппаСкладОтгрузки.Видимость                                 = (СпособОтраженияПродаж = СпособОтраженияПродаж_Реализация);
		
		Элементы.ДекорацияСпособОтраженияПродажКомментарийРеализация.Видимость = (СпособОтраженияПродаж = СпособОтраженияПродаж_Реализация);
		Элементы.ДекорацияСпособОтраженияПродажКомментарийКомиссия.Видимость   = (СпособОтраженияПродаж = СпособОтраженияПродаж_Комиссия);
		
		Элементы.ГруппаСведенияОЮридическомЛице.Доступность  = ИспользоватьОтгрузкуБезПереходаПраваСобственности 
																		И (СпособОтраженияПродаж = СпособОтраженияПродаж_Реализация)
																	Или ИспользоватьКомиссиюПриПродажах 
																		И (СпособОтраженияПродаж = СпособОтраженияПродаж_Комиссия);
		Элементы.ДекорацияПартнер.Заголовок                  = ?(СпособОтраженияПродаж = СпособОтраженияПродаж_Реализация, 
																	НСтр("ru = 'Партнер:'"), 
																	НСтр("ru = 'Комиссионер:'"));
		Элементы.ДекорацияПартнер.Ширина                     = ?(СпособОтраженияПродаж = СпособОтраженияПродаж_Реализация, 8, 9);
		Элементы.Партнер.Ширина                              = ?(СпособОтраженияПродаж = СпособОтраженияПродаж_Реализация, 26, 25);
		Элементы.ГруппаКонтрагент.Видимость                  = Не ИспользоватьПартнеровКакКонтрагентов;
		Элементы.ДекорацияКонтрагент.Ширина                  = ?(СпособОтраженияПродаж = СпособОтраженияПродаж_Реализация, 8, 9);
		Элементы.Контрагент.Ширина                           = ?(СпособОтраженияПродаж = СпособОтраженияПродаж_Реализация, 26, 25);
		Элементы.ГруппаДоговорВзаимозачеты.Видимость         = (СпособОтраженияПродаж = СпособОтраженияПродаж_Реализация);
		Элементы.ГруппаСоглашениеДоговорКомиссия.Видимость   = (СпособОтраженияПродаж = СпособОтраженияПродаж_Комиссия);
		Элементы.ГруппаСоглашениеКомиссия.Видимость          = ИспользоватьСоглашенияСКлиентами;
		Элементы.ГруппаДоговорКомиссия.Доступность           = ЗначениеЗаполнено(Организация)
																И ЗначениеЗаполнено(Партнер)
																И (ЗначениеЗаполнено(Контрагент)
																	Или ИспользоватьПартнеровКакКонтрагентов);
		Элементы.ГруппаДоговорКомиссия.Видимость             = ИспользоватьДоговорыСКлиентами
																И (ИспользоватьСоглашенияСКлиентами
																		И ЗначениеЗаполнено(СоглашениеКомиссия)
																		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СоглашениеКомиссия, "ИспользуютсяДоговорыКонтрагентов")
																	Или Не ИспользоватьСоглашенияСКлиентами);

		Элементы.ГруппаСведенияОРозничномПокупателе.Видимость   = (СпособОтраженияПродаж = СпособОтраженияПродаж_Реализация);
		Элементы.ГруппаСведенияОРозничномПокупателе.Доступность = ИспользоватьОтгрузкуБезПереходаПраваСобственности;
		Элементы.ГруппаРозничныйКонтрагент.Видимость            = Не ИспользоватьПартнеровКакКонтрагентов;
		Элементы.ГруппаСоглашениеРеализация.Видимость           = ИспользоватьСоглашенияСКлиентами;
		Элементы.ГруппаДоговорРеализация.Доступность            = ЗначениеЗаполнено(Организация)
																	И ЗначениеЗаполнено(РозничныйПартнер)
																	И (ЗначениеЗаполнено(РозничныйКонтрагент)
																		Или ИспользоватьПартнеровКакКонтрагентов);
		Элементы.ГруппаДоговорРеализация.Видимость              = ИспользоватьДоговорыСКлиентами
																	И (ИспользоватьСоглашенияСКлиентами
																		И ЗначениеЗаполнено(СоглашениеРеализация)
																		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СоглашениеРеализация, "ИспользуютсяДоговорыКонтрагентов")
																			Или Не ИспользоватьСоглашенияСКлиентами);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУсловияПродажПоУмолчанию(Комиссия = Истина)
	
	ИспользоватьСоглашенияСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами");
	РеквизитПартнер                  = ?(Комиссия, Партнер, РозничныйПартнер);
	РеквизитКонтрагент               = ?(Комиссия, Контрагент, РозничныйКонтрагент);
	РеквизитСоглашение               = ?(Комиссия, СоглашениеКомиссия, СоглашениеРеализация);
	ХозяйственнаяОперация            = ?(Комиссия, Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию, Перечисления.ХозяйственныеОперации.РеализацияКлиенту);

	Если ЗначениеЗаполнено(РеквизитПартнер) Или Не ИспользоватьСоглашенияСКлиентами Тогда
		ПараметрыОтбора = Новый Структура();
		ПараметрыОтбора.Вставить("ХозяйственныеОперации", ХозяйственнаяОперация);
		ПараметрыОтбора.Вставить("ВыбранноеСоглашение",   РеквизитСоглашение);
		ПараметрыОтбора.Вставить("КомиссионныеПродажи25", Комиссия);
		
		УсловияПродажПоУмолчанию = ПродажиСервер.ПолучитьУсловияПродажПоУмолчанию(РеквизитПартнер, ПараметрыОтбора);
		
		Если УсловияПродажПоУмолчанию <> Неопределено Тогда
			Если Не ИспользоватьСоглашенияСКлиентами 
					Или (РеквизитСоглашение <> УсловияПродажПоУмолчанию.Соглашение 
							И ЗначениеЗаполнено(УсловияПродажПоУмолчанию.Соглашение)) Тогда
				РеквизитСоглашение = УсловияПродажПоУмолчанию.Соглашение;
				ЗаполнитьУсловияПродаж(УсловияПродажПоУмолчанию);
			Иначе
				РеквизитСоглашение = УсловияПродажПоУмолчанию.Соглашение;
			КонецЕсли;
			
			Если Не УсловияПродажПоУмолчанию.Типовое
					 И ЗначениеЗаполнено(УсловияПродажПоУмолчанию.Контрагент) Тогда
				РеквизитКонтрагент = УсловияПродажПоУмолчанию.Контрагент;
			Иначе
				ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(РеквизитПартнер, РеквизитКонтрагент);
			КонецЕсли;
			
		Иначе
			ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(РеквизитПартнер, РеквизитКонтрагент);
			РеквизитСоглашение = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если Комиссия Тогда
		Контрагент         = РеквизитКонтрагент;
		СоглашениеКомиссия = РеквизитСоглашение;
	Иначе
		РозничныйКонтрагент  = РеквизитКонтрагент;
		СоглашениеРеализация = РеквизитСоглашение;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУсловияПродаж(Знач УсловияПродаж, Комиссия = Истина)
	
	Если УсловияПродаж <> Неопределено Тогда                        
		РеквизитПартнер    = ?(Комиссия, Партнер, РозничныйПартнер);
		РеквизитКонтрагент = ?(Комиссия, Контрагент, РозничныйКонтрагент);
		РеквизитСоглашение = ?(Комиссия, СоглашениеКомиссия, СоглашениеРеализация);
		РеквизитДоговор    = ?(Комиссия, ДоговорКомиссия, ДоговорРеализация);
		
		Если Не УсловияПродаж.Типовое
				 И ЗначениеЗаполнено(УсловияПродаж.Контрагент) Тогда
			РеквизитКонтрагент = УсловияПродаж.Контрагент;
		Иначе
			ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(РеквизитПартнер, РеквизитКонтрагент);
		КонецЕсли;
		
		Если УсловияПродаж.ИспользуютсяДоговорыКонтрагентов <> Неопределено 
				И УсловияПродаж.ИспользуютсяДоговорыКонтрагентов Тогда
			Объект = Новый Структура;
			Объект.Вставить("Организация", Организация);
			Объект.Вставить("Партнер",     РеквизитПартнер);
			Объект.Вставить("Контрагент",  РеквизитКонтрагент);
			Объект.Вставить("Соглашение",  РеквизитСоглашение);
			Объект.Вставить("Договор",     РеквизитДоговор);
			
			РеквизитДоговор = ПродажиСервер.ПолучитьДоговорПоУмолчанию(
				Объект,
				Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию,
				,
				,
				УсловияПродаж.НоваяСхемаКомиссии);
		КонецЕсли;
	КонецЕсли;
	
	Если Комиссия Тогда
		Контрагент      = РеквизитКонтрагент;
		ДоговорКомиссия = РеквизитДоговор;
	Иначе
		РозничныйКонтрагент = РеквизитКонтрагент;
		ДоговорРеализация   = РеквизитДоговор;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУсловияПродажПоСоглашению(Комиссия = Истина)
	
	УстановитьПривилегированныйРежим(Истина);
	
	РеквизитСоглашение = ?(Комиссия, СоглашениеКомиссия, СоглашениеРеализация);
	УсловияПродаж      = ПродажиСервер.ПолучитьУсловияПродаж(РеквизитСоглашение);
	
	Если Организация <> Неопределено Тогда
		УсловияПродаж.Организация = Организация;
	КонецЕсли;
	
	ЗаполнитьУсловияПродаж(УсловияПродаж, Комиссия);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДоговорПоУмолчанию(Комиссия = Истина)
	
	РеквизитПартнер    = ?(Комиссия, Партнер, РозничныйПартнер);
	РеквизитКонтрагент = ?(Комиссия, Контрагент, РозничныйКонтрагент);
	РеквизитСоглашение = ?(Комиссия, СоглашениеКомиссия, СоглашениеРеализация);
	РеквизитДоговор    = ?(Комиссия, ДоговорКомиссия, ДоговорРеализация);
	
	Если ЗначениеЗаполнено(РеквизитКонтрагент) Тогда
		Объект = Новый Структура;
		Объект.Вставить("Организация", Организация);
		Объект.Вставить("Партнер",     РеквизитПартнер);
		Объект.Вставить("Контрагент",  РеквизитКонтрагент);
		Объект.Вставить("Соглашение",  РеквизитСоглашение);
		Объект.Вставить("Договор",     РеквизитДоговор);
		
		РеквизитДоговор = ПродажиСервер.ПолучитьДоговорПоУмолчанию(
			Объект,
			Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию,
			,
			,
			Истина);
	КонецЕсли;
	
	Если Комиссия Тогда
		ДоговорКомиссия = РеквизитДоговор;
	Иначе
		ДоговорРеализация = РеквизитДоговор;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
