
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура XMLФайлНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораФайла.Фильтр                      = НСтр("ru='XML файл (*.xml)|*.xml'");
	ДиалогВыбораФайла.Заголовок                   = НСтр("ru='Выберите XML файл'");
	ДиалогВыбораФайла.ПредварительныйПросмотр     = Ложь;
	ДиалогВыбораФайла.Расширение                  = "xml";
	ДиалогВыбораФайла.ИндексФильтра               = 0;
	ДиалогВыбораФайла.ПолноеИмяФайла              = Элемент.ТекстРедактирования;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("XMLФайлПоказатьДиалогЗавершение", ЭтотОбъект);
	ДиалогВыбораФайла.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура XMLФайлОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(Элемент.ТекстРедактирования) Тогда
		Возврат;
	КонецЕсли;
	
	Файл = Новый Файл;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"XMLФайлИнициализация",
		ЭтотОбъект,
		Новый Структура("Открытие, ИмяФайла", Истина, Элемент.ТекстРедактирования));
	
	Файл.НачатьИнициализацию(ОписаниеОповещения, Элемент.ТекстРедактирования);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Файл = Новый Файл;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"XMLФайлИнициализация",
		ЭтотОбъект,
		Новый Структура("Открытие, ИмяФайла", Ложь, XMLФайл));
	
	Файл.НачатьИнициализацию(ОписаниеОповещения, XMLФайл);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьЗавершение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	ЗагрузитьСервер(Адрес);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура ЗагрузитьСервер(Адрес)
	
	Обработки.ЗагрузкаОтчетаБанкаПоЭквайрингу.ЗагрузитьXMLФайлВИнформационнуюБазуСервер(
		Объект, XMLФайл, ПроводитьДокументыПриЗагрузке, Адрес);
	
КонецПроцедуры

&НаКлиенте
Процедура XMLФайлПоказатьДиалогЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	XMLФайл = ВыбранныеФайлы[0];
	
КонецПроцедуры

&НаКлиенте
Процедура XMLФайлИнициализация(Файл, ДополнительныеПараметры) Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("XMLФайлПроверкаСуществования", ЭтотОбъект, ДополнительныеПараметры);
	Файл.НачатьПроверкуСуществования(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура XMLФайлПроверкаСуществования(Существует, ДополнительныеПараметры) Экспорт
	
	Если НЕ Существует Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Не найден XML файл с данными!'"),,, Элементы.XMLФайл.Имя);
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Открытие Тогда
		
		ОткрытьФорму(
			"Обработка.ЗагрузкаОтчетаБанкаПоЭквайрингу.Форма.ФормаXMLДокумента",
			Новый Структура("XMLФайл", ДополнительныеПараметры.ИмяФайла),
			ЭтаФорма);
		
	Иначе // загрузка
		
		Адрес = "";
		НачатьПомещениеФайла(
			Новый ОписаниеОповещения("ЗагрузитьЗавершение", ЭтотОбъект, Новый Структура("Адрес", Адрес)),
			Адрес,
			ДополнительныеПараметры.ИмяФайла,
			Ложь,
			Новый УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
