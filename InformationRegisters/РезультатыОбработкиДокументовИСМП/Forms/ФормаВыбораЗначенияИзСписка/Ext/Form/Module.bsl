
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Заголовок = Параметры.Заголовок;
	Для Каждого Элемент Из Параметры.ВсеЗначения Цикл
		Строка = ДанныеВыбора.Добавить();
		Строка.Значение      = Элемент.Значение;
		Строка.Представление = ?(ЗначениеЗаполнено(Элемент.Представление), Элемент.Представление, Элемент.Значение);
		Строка.Выбрана       = (Параметры.ВыбранныеЗначения.Найти(Строка.Значение) <> Неопределено);
	КонецЦикла;
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	
	Для Каждого СтрокаТЧ Из ДанныеВыбора Цикл
		СтрокаТЧ.Выбрана = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьВсе(Команда)
	
	Для Каждого СтрокаТЧ Из ДанныеВыбора Цикл
		СтрокаТЧ.Выбрана = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	Значения        = Новый Массив;
	РезультатВыбора = Новый Структура("Значения, СохраненыНастройки", Значения, СохраненыНастройки);
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Выбрана", Истина);
	НайденныеСтроки = ДанныеВыбора.НайтиСтроки(ПараметрыОтбора);
	
	Для Каждого СтрокаТЧ Из НайденныеСтроки Цикл
		РезультатВыбора.Значения.Добавить(СтрокаТЧ.Значение);
	КонецЦикла;
	
	Закрыть(РезультатВыбора);
	
КонецПроцедуры

#КонецОбласти
