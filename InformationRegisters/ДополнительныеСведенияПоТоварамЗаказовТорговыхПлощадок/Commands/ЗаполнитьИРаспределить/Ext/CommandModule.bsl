#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	ОчиститьСообщения();
	
	ЕстьУчетнаяЗапись =
		ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ПараметрыВыполненияКоманды.Источник, "УчетнаяЗапись");
	Если Не ЕстьУчетнаяЗапись Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не предусмотрено заполнение сведений по товарам из этого источника.'"));
		Возврат;
	КонецЕсли;
	
	Форма = ПараметрыВыполненияКоманды.Источник;
	
	Заказ       = Неопределено;
	НомерЗаказа = "";
	
	Если Форма.ИмяФормы = "Обработка.УправлениеПродажамиНаOzon.Форма.СписокЗаказов" Тогда
		ИдентификаторыВыделенныхСтрок = Форма.Элементы.ТаблицаЗаказов.ВыделенныеСтроки;
		ДанныеЗаказа = ПолучитьДанныеЗаказов(ИдентификаторыВыделенныхСтрок, Форма.ТаблицаЗаказов);
		Заказ        = ДанныеЗаказа.Заказ;
		НомерЗаказа  = ДанныеЗаказа.НомерЗаказа;
		
	ИначеЕсли Форма.ИмяФормы = "Обработка.УправлениеПродажамиНаOzon.Форма.СоставЗаказа" Тогда
		Заказ       = Форма.ДокументЗаказа;
		НомерЗаказа = Форма.НомерЗаказа;
		
	Иначе
		КлючиВыделенныхСтрок = Форма.Элементы.Список.ВыделенныеСтроки;
		ДанныеЗаказа = ПолучитьДанныеЗаказов(КлючиВыделенныхСтрок);
		Заказ        = ДанныеЗаказа.Заказ;
		НомерЗаказа  = ДанныеЗаказа.НомерЗаказа;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Заказ) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("УчетнаяЗапись", ПараметрыВыполненияКоманды.Источник.УчетнаяЗапись);
	ПараметрыФормы.Вставить("Заказ",         Заказ);
	ПараметрыФормы.Вставить("НомерЗаказа",   НомерЗаказа);
	
	ОткрытьФорму(
		"РегистрСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.Форма.ЗаполнениеДанныхЭкземпляров",
		ПараметрыФормы,
		ЭтотОбъект,
		Заказ);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПолучитьДанныеЗаказов(ВыделенныеСтроки, Таблица = Неопределено)

	Заказ       = Неопределено;
	НомерЗаказа = "";
	
	ДанныеЗаказа = Новый Структура;
	ДанныеЗаказа.Вставить("Заказ",       Заказ);
	ДанныеЗаказа.Вставить("НомерЗаказа", НомерЗаказа);
	
	ЗагруженныеЗаказы   = Новый Соответствие;
	НезагруженныеЗаказы = Новый Соответствие;
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		Если ТипЗнч(ВыделеннаяСтрока) = Тип("РегистрСведенийКлючЗаписи.ЗаказыТорговыхПлощадок") Тогда
			ДанныеСтроки = ДанныеЗаказаИзРегистраНаСервере(ВыделеннаяСтрока);
		ИначеЕсли ТипЗнч(ВыделеннаяСтрока) = Тип("Число") И Таблица <> Неопределено Тогда
			ДанныеСтроки = Таблица.НайтиПоИдентификатору(ВыделеннаяСтрока);
		КонецЕсли;
		
		Если ДанныеСтроки <> Неопределено Тогда
			Заказ       = ДанныеСтроки.ДокументЗаказа;
			НомерЗаказа = ДанныеСтроки.НомерЗаказа;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Заказ) Тогда
			НезагруженныеЗаказы.Вставить(НомерЗаказа);
		Иначе
			ЗагруженныеЗаказы.Вставить(НомерЗаказа, Заказ);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ЗагруженныйЗаказ Из ЗагруженныеЗаказы Цикл
		Если НезагруженныеЗаказы[ЗагруженныйЗаказ.Ключ] <> Неопределено Тогда
			НезагруженныеЗаказы.Удалить(ЗагруженныйЗаказ.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	Если ЗагруженныеЗаказы.Количество() > 1 Тогда
		ТекстСообщения =
			НСтр("ru = 'Не предусмотрено заполнение сведений по товарам для нескольких заказов.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
		Возврат ДанныеЗаказа;
	КонецЕсли;
	
	Если НезагруженныеЗаказы.Количество() > 0 Тогда
		ТекстСообщения =
			НСтр("ru = 'Не предусмотрено заполнение сведений по товарам для незагруженного заказа.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	ИначеЕсли ЗагруженныеЗаказы.Количество() = 0 Тогда
		ТекстСообщения =
			НСтр("ru = 'Нет заказов для заполнения сведений по товарам.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Если ЗагруженныеЗаказы.Количество() = 1 Тогда
		ДанныеЗаказа.Заказ       = Заказ;
		ДанныеЗаказа.НомерЗаказа = НомерЗаказа;
	КонецЕсли;
	
	Возврат ДанныеЗаказа;

КонецФункции

&НаСервере
Функция ДанныеЗаказаИзРегистраНаСервере(КлючЗаписи)

	ДанныеЗаказа = Новый Структура;
	ДанныеЗаказа.Вставить("ДокументЗаказа", Неопределено);
	ДанныеЗаказа.Вставить("НомерЗаказа",    "");
	
	ДанныеСтроки = РегистрыСведений.ЗаказыТорговыхПлощадок.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, КлючЗаписи);
	ДанныеСтроки.Прочитать();
	
	Если ДанныеСтроки.Выбран() Тогда
		ДанныеЗаказа.ДокументЗаказа = ДанныеСтроки.Заказ;
		ДанныеЗаказа.НомерЗаказа    = ДанныеСтроки.НомерЗаказа;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ДанныеЗаказа;

КонецФункции

#КонецОбласти
