#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	// Проверка ОбменДанными.Загрузка не требуется, т.к. данный объект в РИБ при записи должен создавать задания.
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НДССостояниеРеализации0.ДокументРеализации КАК ДокументРеализации,
	|	НДССостояниеРеализации0.Организация КАК Организация,
	|	НДССостояниеРеализации0.ДатаПодтверждения КАК ДатаПодтверждения,
	|	НДССостояниеРеализации0.Состояние КАК Состояние
	|ПОМЕСТИТЬ НДССостояниеРеализации0ПередЗаписью
	|ИЗ
	|	РегистрСведений.НДССостояниеРеализации0 КАК НДССостояниеРеализации0
	|ГДЕ
	|	НДССостояниеРеализации0.Регистратор = &Регистратор
	|";
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Если ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Иначе
		ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	КонецЕсли;
	
	Запрос.Выполнить();
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	// Проверка ОбменДанными.Загрузка не требуется, т.к. данный объект в РИБ при записи должен создавать задания.
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.ДатаПодтверждения    КАК ДатаПодтверждения,
	|	Таблица.ДокументРеализации   КАК ДокументРеализации,
	|	Таблица.Организация          КАК Организация
	|ПОМЕСТИТЬ НДССостояниеРеализации0Изменения
	|ИЗ
	|	(ВЫБРАТЬ
	|		НДССостояниеРеализации0.ДокументРеализации КАК ДокументРеализации,
	|		НДССостояниеРеализации0.ДатаПодтверждения КАК ДатаПодтверждения,
	|		НДССостояниеРеализации0.Организация КАК Организация,
	|		ВЫБОР
	|			КОГДА НДССостояниеРеализации0.Состояние = ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ОжидаетсяПодтверждение)
	|				ТОГДА 1
	|			КОГДА НДССостояниеРеализации0.Состояние = ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ПодтвержденаРеализация0)
	|				ТОГДА 2
	|			КОГДА НДССостояниеРеализации0.Состояние = ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.НеПодтвержденаРеализация0)
	|				ТОГДА 3
	|			КОГДА НДССостояниеРеализации0.Состояние = ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ПодтверждениеНеТребуется)
	|				ТОГДА 4
	|		КОНЕЦ КАК Состояние
	|	ИЗ
	|		НДССостояниеРеализации0ПередЗаписью КАК НДССостояниеРеализации0
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НДССостояниеРеализации0.ДокументРеализации КАК ДокументРеализации,
	|		НДССостояниеРеализации0.ДатаПодтверждения КАК ДатаПодтверждения,
	|		НДССостояниеРеализации0.Организация КАК Организация,
	|		-(ВЫБОР
	|			КОГДА НДССостояниеРеализации0.Состояние = ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ОжидаетсяПодтверждение)
	|				ТОГДА 1
	|			КОГДА НДССостояниеРеализации0.Состояние = ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ПодтвержденаРеализация0)
	|				ТОГДА 2
	|			КОГДА НДССостояниеРеализации0.Состояние = ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.НеПодтвержденаРеализация0)
	|				ТОГДА 3
	|			КОГДА НДССостояниеРеализации0.Состояние = ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ПодтверждениеНеТребуется)
	|				ТОГДА 4
	|		КОНЕЦ) КАК Состояние
	|	ИЗ
	|		РегистрСведений.НДССостояниеРеализации0 КАК НДССостояниеРеализации0
	|	ГДЕ
	|		НДССостояниеРеализации0.Регистратор = &Регистратор) Таблица
	|ГДЕ
	|	Таблица.ДатаПодтверждения <> ДАТАВРЕМЯ(1,1,1)
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.ДатаПодтверждения,
	|	Таблица.ДокументРеализации,
	|	Таблица.Организация
	|
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Состояние) <> 0
	|;
	|
	|///////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(Таблица.ДатаПодтверждения, МЕСЯЦ) КАК Месяц,
	|	Таблица.Организация                             КАК Организация,
	|	НДСПредъявленный.СчетФактура                    КАК СчетФактура
	|ИЗ
	|	НДССостояниеРеализации0Изменения КАК Таблица
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|	ПО
	|		Таблица.Организация = НДСПредъявленный.Организация
	|		И Таблица.ДокументРеализации = НДСПредъявленный.РеализацияЭкспорт
	|;
	|
	|///////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(Таблица.ДатаПодтверждения, МЕСЯЦ) КАК Месяц,
	|	Таблица.Организация                             КАК Организация,
	|	НДСПредъявленный.СчетФактура                    КАК СчетФактура
	|ПОМЕСТИТЬ ИмяВТ
	|ИЗ
	|	НДССостояниеРеализации0Изменения КАК Таблица
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|	ПО
	|		Таблица.Организация = НДСПредъявленный.Организация
	|		И Таблица.ДокументРеализации = НДСПредъявленный.РеализацияЭкспорт
	|";
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Если ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
		
		ИмяВТ = Метаданные().Имя + "_ЗаданияКФормированиюДвиженийПоНДС";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяВт", ИмяВТ);
	
		Выборка = Запрос.ВыполнитьПакет()[2].Выбрать();
		ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(
			ДополнительныеСвойства,
			ИмяВТ,
			Выборка.Следующий() И Выборка.Количество > 0);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПОМЕСТИТЬ ИмяВт", "");
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
		Выборка = Запрос.Выполнить().Выбрать();
		РегистрыСведений.ЗаданияКФормированиюДвиженийПоНДС.СоздатьЗаписиРегистраПоДаннымВыборки(Выборка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
