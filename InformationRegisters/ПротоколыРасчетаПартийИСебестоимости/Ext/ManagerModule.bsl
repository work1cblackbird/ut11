#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Возвращает протокол расчета.
//
// Параметры:
// Идентификатор - Строка -
// Период - Дата -
// ПериодРасчета - Дата -
//
// Возвращаемое значение:
// 	Структура - Описание:
// * Протокол - ТекстовыйДокумент -
// * Заголовок - Строка -
//
Функция ПолучитьТекстПротокола(Идентификатор, Период = Неопределено, ПериодРасчета = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НЕ ЗначениеЗаполнено(Период) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Т.Период
		|ИЗ
		|	РегистрСведений.ПротоколыРасчетаПартийИСебестоимости КАК Т
		|ГДЕ
		|	Т.Идентификатор = &Идентификатор
		|	И &ПериодРасчета В (Т.ПериодРасчета, НЕОПРЕДЕЛЕНО)";
		
		Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
		Запрос.УстановитьПараметр("ПериодРасчета", ПериодРасчета);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Количество() = 0 Тогда
			
			ВызватьИсключение НСтр("ru='Протокола расчета не найден. Возможно протокол был удален.'");
			
		КонецЕсли;
		
		Выборка.Следующий();
		
		ПериодЗаписи = Выборка.Период;
		
	Иначе
		ПериодЗаписи = Период;
	КонецЕсли;
	
	// Прочитаем запись протокола
	ЗаписьПротокола = РегистрыСведений.ПротоколыРасчетаПартийИСебестоимости.СоздатьМенеджерЗаписи();
	ЗаписьПротокола.Период		  = ПериодЗаписи;
	ЗаписьПротокола.Идентификатор = Идентификатор;
	
	ЗаписьПротокола.Прочитать();
	
	Если ЗаписьПротокола.Выбран() Тогда
		
		// Получим протокол в формате текстового документа
		Протокол = ЗаписьПротокола.Протокол.Получить();
		
		Если ТипЗнч(Протокол) <> Тип("ТекстовыйДокумент") Тогда
			ВызватьИсключение НСтр("ru='Ошибка получения текста протокола расчета.'");
		КонецЕсли;
		
	Иначе
		
		ВызватьИсключение НСтр("ru='Протокола расчета не найден. Возможно протокол был удален.'");
		
	КонецЕсли;
	
	Возврат Протокол;
	
КонецФункции

#КонецОбласти

#КонецЕсли
