
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Параметры.Период) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	МассивПротоколов = Новый Массив;
	ПротоколыРасчета = ПолучитьПротоколыРасчета();
	
	Если ЗначениеЗаполнено(ПротоколыРасчета) Тогда
		
		Для Каждого КлючИЗначение Из ПротоколыРасчета Цикл
			ТекущийПротокол = ПолучитьПротоколРасчета(КлючИЗначение.Ключ, Параметры.Период);
			МассивПротоколов.Добавить(Новый ФиксированнаяСтруктура(ТекущийПротокол));
		КонецЦикла;
		
	КонецЕсли;
	
	ПротоколыДляПросмотра = Новый ФиксированныйМассив(МассивПротоколов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Отказ = Истина;
	
	Для Каждого ЭлементКоллекции Из ПротоколыДляПросмотра Цикл
		
		ТекущийПротокол = Новый Структура("Протокол, Заголовок");
		ЗаполнитьЗначенияСвойств(ТекущийПротокол, ЭлементКоллекции);
		
		ТекущийПротокол.Протокол.Показать(ТекущийПротокол.Заголовок);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьПротоколыРасчета()
	
	Возврат РегистрыСведений.ОшибкиРасчетаПартийИСебестоимости.ПолучитьПротоколыРасчета(
		Параметры.Период,
		Параметры.МассивОрганизаций);
		
КонецФункции

&НаСервере
// Возвращает протокол расчета.
//
Функция ПолучитьПротоколРасчета(Идентификатор, Период)
	
	Заголовок = РасчетСебестоимостиПротоколРасчета.ЗаголовокПротоколаПриВыводе(Период);
	Протокол  = РегистрыСведений.ПротоколыРасчетаПартийИСебестоимости.ПолучитьТекстПротокола(Идентификатор, , Период);
	
	Возврат Новый Структура("Протокол, Заголовок", Протокол, Заголовок);
	
КонецФункции

#КонецОбласти
