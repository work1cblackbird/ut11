#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ОрганизацияСсылка)
		И ЗначениеЗаполнено(Параметры.БанкСсылка) Тогда
		
		ЗаписьПоСсылке = РегистрыСведений.ПодключенияКБанкамBankFeeds.СоздатьМенеджерЗаписи();
		ЗаписьПоСсылке.Организация = Параметры.ОрганизацияСсылка;
		ЗаписьПоСсылке.Банк = Параметры.БанкСсылка;
		ЗаписьПоСсылке.Прочитать();
		
		Если ЗначениеЗаполнено(ЗаписьПоСсылке.Организация)
			И ЗначениеЗаполнено(ЗаписьПоСсылке.Банк) Тогда
			ЗначениеВДанныеФормы(ЗаписьПоСсылке, Запись);
		Иначе
			Запись.Организация = Параметры.ОрганизацияСсылка;
			Запись.Банк = Параметры.БанкСсылка;
		КонецЕсли;

	Иначе

		ЗаписьПоСсылке = РегистрыСведений.ПодключенияКБанкамBankFeeds.СоздатьМенеджерЗаписи();
		ЗаписьПоСсылке.Организация = Запись.Организация;
		ЗаписьПоСсылке.Банк = Запись.Банк;
		ЗаписьПоСсылке.Прочитать();
		
	КонецЕсли;
	
	Если ЗаписьПоСсылке <> Неопределено Тогда

		РеквизитыРегистрации = ЗаписьПоСсылке.РеквизитыРегистрации.Получить();  
		
		Если ЗначениеЗаполнено(РеквизитыРегистрации) Тогда
		
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыРегистрации);
		
		КонецЕсли;	

	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗапуститьПроверкуСтатусаСоединенияИСогласия();
	
КонецПроцедуры

#КонецОбласти
		  
#Область ОбработчикиКомандФормы
		  
&НаКлиенте
Процедура Подключить(Команда)
	
	ПараметрыЗаполнения = Новый Структура();
	ПараметрыЗаполнения.Вставить("Организация", Запись.Организация);
	ПараметрыЗаполнения.Вставить("Банк", Запись.Банк);
	ПараметрыЗаполнения.Вставить("НастройкиСервиса", Запись.НастройкиСервиса);
	ПараметрыЗаполнения.Вставить("РеквизитыРегистрации", ПрочитатьРеквизитыРегистрацииНаСервере(ПараметрыЗаполнения));
	ПараметрыЗаполнения.Вставить("ТолькоНовоеПодключение", Истина);
	ПараметрыЗаполнения.Вставить("email", Запись.email);

	ОбменССервисомBankFeedsКлиент.ПодключитьСервисНаКлиенте(ПараметрыЗаполнения, ЭтотОбъект);	
	
КонецПроцедуры

&НаКлиенте
Процедура Отключить(Команда)
	
    Закрыть(Новый Структура("ОтключитьBankFeeds", Истина));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСоединение(Команда)
	
	ПараметрыЗаполнения = Новый Структура();
	ПараметрыЗаполнения.Вставить("Организация", Запись.Организация);
	ПараметрыЗаполнения.Вставить("Банк", Запись.Банк);
	ПараметрыЗаполнения.Вставить("РеквизитыРегистрации", ПрочитатьРеквизитыРегистрацииНаСервере(ПараметрыЗаполнения));
	ПараметрыЗаполнения.Вставить("НастройкиСервиса", Запись.НастройкиСервиса);
	ПараметрыЗаполнения.Вставить("email", Запись.email);
	ПараметрыЗаполнения.Вставить("ЭтоОбновлениеСоединения", Истина);

	ОбменССервисомBankFeedsКлиент.ОбновитьСоединениеСервисаНаКлиенте(ПараметрыЗаполнения, ЭтотОбъект);	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтозватьСогласие(Команда)
	
	ПараметрыЗаполнения = Новый Структура();
	ПараметрыЗаполнения.Вставить("Организация", Запись.Организация);
	ПараметрыЗаполнения.Вставить("Банк", Запись.Банк);
	ПараметрыЗаполнения.Вставить("НастройкиСервиса", Запись.НастройкиСервиса);
	ПараметрыЗаполнения.Вставить("email", Запись.email);

	ОбменССервисомBankFeedsКлиент.ОтозватьСогласиеПоСоединениюСервисаНаКлиенте(ПараметрыЗаполнения, ЭтотОбъект);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСтатусСоединенияИСогласия(Команда)
	
	ЗапуститьПроверкуСтатусаСоединенияИСогласия();	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗапуститьПроверкуСтатусаСоединенияИСогласия() Экспорт
	
	ВыполняетсяПроверкаСтатусов = Истина;
	УправлениеФормой();
	
	ПараметрыЗаполнения = Новый Структура();
	ПараметрыЗаполнения.Вставить("Организация", Запись.Организация);
	ПараметрыЗаполнения.Вставить("Банк", Запись.Банк);
	ПараметрыЗаполнения.Вставить("НастройкиСервиса", Запись.НастройкиСервиса);
	ПараметрыЗаполнения.Вставить("email", Запись.email);

	ОбменССервисомBankFeedsКлиент.ПроверитьСтатусыСоединенияИСогласияCервисаНаКлиенте(ПараметрыЗаполнения, ЭтотОбъект);	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДаннымиФормуАвторизацииНаСервисе(СсылкаНаАвторизациюВСервисе, ПараметрыЗаполненияЗапроса) Экспорт
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ПослеСозданияНастройкиОбменаBankFeeds", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", ПараметрыЗаполненияЗапроса.Организация);
	ПараметрыФормы.Вставить("Банк", ПараметрыЗаполненияЗапроса.Банк);
	ПараметрыФормы.Вставить("СсылкаНаАвторизацию", СсылкаНаАвторизациюВСервисе); 
	ПараметрыФормы.Вставить("ПараметрыЗаполнения", ПараметрыЗаполненияЗапроса); 
	ПараметрыФормы.Вставить("СоздатьСоединениеСуществующегоСоединения", Истина); 
	
	ОткрытьФорму("РегистрСведений.ПодключенияКБанкамBankFeeds.Форма.ФормаПодключения", ПараметрыФормы, , , , ,
		ОбработчикОповещения);
	
КонецПроцедуры	

&НаКлиенте
Процедура ПослеСозданияНастройкиОбменаBankFeeds(НастройкаОбмена, Параметры) Экспорт

	ЗапуститьПроверкуСтатусаСоединенияИСогласия();
			
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеПодключенияКСервису(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РезультатОтветаОтСервиса = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Если Результат.Статус = "Ошибка" Тогда
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка подключения по организации %1, к банку %2. Подключение не выполнено'"),
			Запись.Организация, Запись.Банк);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
	ИначеЕсли РезультатОтветаОтСервиса.СтатусОтвета = ОбменССервисомBankFeedsВызовСервера.СтатусыОтветов().ОтветНеПолучен Тогда
		
		Для Каждого ТекущаяОшибка Из РезультатОтветаОтСервиса.МассивОшибок Цикл
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка: %1. Подключение счета не выполнено!'"), ТекущаяОшибка);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			
		КонецЦикла;	
		
	ИначеЕсли РезультатОтветаОтСервиса.СтатусОтвета = ОбменССервисомBankFeedsВызовСервера.СтатусыОтветов().ОтветПолучен Тогда

		Если РезультатОтветаОтСервиса.ИдентификаторСчета <> "" Тогда

			Закрыть(Новый Структура("НомерСчета", РезультатОтветаОтСервиса.ИдентификаторСчета));
		
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Подключение по организации %1, к банку %2 выполнено.'"),
				Запись.Организация, Запись.Банк);
			
			ПоказатьОповещениеПользователя(НСтр("ru = 'Подключение к сервису'"),,ТекстСообщения,,СтатусОповещенияПользователя.Важное);
			
		Иначе     
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Подключение по организации %1, к банку %2 не выполнено. Не найден счет!'"),
				Запись.Организация, Запись.Банк);
			ПоказатьПредупреждение(,ТекстСообщения,,НСтр("ru = 'Подключение к сервису'"));
			
		КонецЕсли;	
		
	Иначе
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка подключения по организации %1, к банку %2. Подключение не выполнено'"),
			Запись.Организация, Запись.Банк);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;	
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПрочитатьРеквизитыРегистрацииНаСервере(ПараметрыЗаполнения)
	
	Возврат ОбменССервисомBankFeedsСервер.ПрочитатьРеквизитыРегистрацииНаСервере(ПараметрыЗаполнения);
	
КонецФункции	

&НаКлиенте
Процедура УправлениеФормой() Экспорт
	
	Элементы.ГруппаОбновлениеСтатуса.Видимость = ВыполняетсяПроверкаСтатусов;
	Элементы.ГруппаИнформацияОПодключении.Видимость = НЕ ВыполняетсяПроверкаСтатусов;
	
	Если СоедниенениеИСогласиеАктивно Тогда

		Элементы.ИнформацияОПодключении.ЦветТекста = WebЦвета.Зеленый;
		
	Иначе
		
		Элементы.ИнформацияОПодключении.ЦветТекста = WebЦвета.Красный;
		
	КонецЕсли;
	
	СтатусОжидания =   НСтр("ru = 'Проверка подключения...'");
	
КонецПроцедуры	

#КонецОбласти

