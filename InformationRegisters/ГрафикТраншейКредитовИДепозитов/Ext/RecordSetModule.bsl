#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Итог = 0;
	Для каждого СтрокаНабора Из ЭтотОбъект Цикл
		Итог = Итог + СтрокаНабора.Сумма;
		СтрокаНабора.СуммаИтог = Итог;
	КонецЦикла;
	
	СФормироватьТаблицуОбъектовОплаты();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	РегистрыСведений.ГрафикПлатежей.РассчитатьГрафикПлатежейПоФинансовымИнструментам(
		ДополнительныеСвойства.ТаблицаОбъектовОплаты.ВыгрузитьКолонку("ОбъектОплаты"));
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Для Каждого СтрокаНабора Из ЭтотОбъект Цикл
			ЗаполнитьЗначенияСвойств(СтрокаНабора, ДанныеЗаполнения);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Процедура СФормироватьТаблицуОбъектовОплаты()
	
	Договор = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Отбор.ВариантГрафика.Значение, "Владелец");
	
	ТаблицаОбъектовОплаты = Новый ТаблицаЗначений;
	ТаблицаОбъектовОплаты.Колонки.Добавить("ОбъектОплаты");
	ТаблицаОбъектовОплаты.Добавить().ОбъектОплаты = Договор;
	
	ДополнительныеСвойства.Вставить("ТаблицаОбъектовОплаты", ТаблицаОбъектовОплаты);
	
КонецПроцедуры

#КонецЕсли