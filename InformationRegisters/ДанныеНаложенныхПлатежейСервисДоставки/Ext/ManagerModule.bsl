
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Новый отбор записей.
// 
// Возвращаемое значение:
//  Структура - Отбор для удаления:
// * ОрганизацияБизнесСети - Неопределено -
// * СписокИдентификаторов - Неопределено -
// * УдалитьОбработанные - Булево -
Функция НовыйОтборЗаписей() Экспорт
	
	ОтборДляУдаления = Новый Структура();
	ОтборДляУдаления.Вставить("ОрганизацияБизнесСети", Неопределено);
	ОтборДляУдаления.Вставить("СписокИдентификаторов", Неопределено);
	ОтборДляУдаления.Вставить("УдалитьОбработанные", Ложь);

	Возврат ОтборДляУдаления
	
КонецФункции

// Удалить записи по отбору.
// 
// Параметры:
//  ОтборДляУдаления - Структура - Отбор для удаления:
// * ОрганизацияБизнесСети - Неопределено -
// * СписокИдентификаторов - Неопределено -
// * УдалитьОбработанные - Булево -
Процедура УдалитьЗаписиПоОтбору(ОтборДляУдаления) Экспорт
	
	МассивЗамен = Новый Массив();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеДляОбработкиПоНаложеннымПлатежам.ОрганизацияБизнесСети,
		|	ДанныеДляОбработкиПоНаложеннымПлатежам.ИдентификаторДокумента
		|ИЗ
		|	РегистрСведений.ДанныеНаложенныхПлатежейСервисДоставки КАК ДанныеДляОбработкиПоНаложеннымПлатежам
		|ГДЕ
		|	&УсловияОтбора";

	Если ОтборДляУдаления.ОрганизацияБизнесСети <> Неопределено Тогда
		МассивЗамен.Добавить("ДанныеДляОбработкиПоНаложеннымПлатежам.ОрганизацияБизнесСети = &ОрганизацияБизнесСети");
		Запрос.УстановитьПараметр("ОрганизацияБизнесСети", ОтборДляУдаления.ОрганизацияБизнесСети);
	КонецЕсли;
	
	Если ОтборДляУдаления.СписокИдентификаторов <> Неопределено Тогда
		МассивЗамен.Добавить("ДанныеДляОбработкиПоНаложеннымПлатежам.ИдентификаторДокумента В (&СписокИдентификаторов)");
		Запрос.УстановитьПараметр("СписокИдентификаторов", ОтборДляУдаления.СписокИдентификаторов);
	КонецЕсли;
	
	Если ОтборДляУдаления.УдалитьОбработанные = Истина Тогда
		МассивЗамен.Добавить("ДанныеДляОбработкиПоНаложеннымПлатежам.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОбработкиНаложенныхПлатежейСервисДоставки.Удалить)");
	КонецЕсли;

	Если МассивЗамен.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловияОтбора", СтрСоединить(МассивЗамен, " И "));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		УдаляемаяЗапись = РегистрыСведений.ДанныеНаложенныхПлатежейСервисДоставки.СоздатьМенеджерЗаписи();
		УдаляемаяЗапись.ОрганизацияБизнесСети = ВыборкаДетальныеЗаписи.ОрганизацияБизнесСети;
		УдаляемаяЗапись.ИдентификаторДокумента = ВыборкаДетальныеЗаписи.ИдентификаторДокумента;
		
		УдаляемаяЗапись.Удалить();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли