
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Запись, ЭтотОбъект);

	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.ВариантАнализа, "Владелец.ЦелевойТренд")
		= Перечисления.ВидыЦелевыхТрендовПоказателей.Диапазон Тогда
		Запись.ВидЦелевогоЗначения = Перечисления.ВидыЦелевыхЗначенийВариантовАнализа.Диапазон;
	Иначе
		Запись.ВидЦелевогоЗначения = Перечисления.ВидыЦелевыхЗначенийВариантовАнализа.Значение;
	КонецЕсли;
	
	УстановитьВидимостьЭлементовФормы();
	
	УстановитьЕдиницыИзмеренияОтклонений();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ВидыЦелевыхЗначенийВариантовАнализа = Перечисления.ВидыЦелевыхЗначенийВариантовАнализа;
	
	Если ТекущийОбъект.ВидЦелевогоЗначения = ВидыЦелевыхЗначенийВариантовАнализа.Значение Тогда
		ТекущийОбъект.ЦелевойДиапазонМинимум = 0;
		ТекущийОбъект.ЦелевойДиапазонМаксимум = 0;
		
	ИначеЕсли Запись.ВидЦелевогоЗначения = ВидыЦелевыхЗначенийВариантовАнализа.Диапазон Тогда
		ТекущийОбъект.ЦелевоеЗначение = 0;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантАнализаПриИзменении(Элемент)
	
	УстановитьЕдиницыИзмеренияОтклонений();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОтклоненияОтЦелевогоЗначенияПриИзменении(Элемент)
	
	УстановитьЕдиницыИзмеренияОтклонений();
	
	ПересчитатьПриведенныеОтклонения();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЦелевогоЗначенияПриИзменении(Элемент)
	
	УстановитьВидимостьЭлементовФормы();
	
	ПересчитатьПриведенныеОтклонения();
	
КонецПроцедуры

&НаКлиенте
Процедура ГраничноеНегативноеОтклонениеПриИзменении(Элемент)
	
	Запись.ПриведенноеГраничноеНегативноеОтклонение = ПриведенноеГраничноеНегативноеОтклонение();
	
КонецПроцедуры

&НаКлиенте
Процедура ГраничноеПозитивноеОтклонениеПриИзменении(Элемент)
	
	Запись.ПриведенноеГраничноеПозитивноеОтклонение = ПриведенноеГраничноеПозитивноеОтклонение();
	
КонецПроцедуры

&НаКлиенте
Процедура ЦелевойДиапазонМаксимумПриИзменении(Элемент)
	
	ПересчитатьПриведенныеОтклонения();
	
КонецПроцедуры

&НаКлиенте
Процедура ЦелевойДиапазонМинимумПриИзменении(Элемент)
	
	ПересчитатьПриведенныеОтклонения();
	
КонецПроцедуры

&НаКлиенте
Процедура ЦелевоеЗначениеПриИзменении(Элемент)
	
	ПересчитатьПриведенныеОтклонения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПриИзмененииРеквизитов

&НаСервере 
Процедура ПересчитатьПриведенныеОтклонения()
	
	Запись.ПриведенноеГраничноеПозитивноеОтклонение = ПриведенноеГраничноеПозитивноеОтклонение();
	Запись.ПриведенноеГраничноеНегативноеОтклонение = ПриведенноеГраничноеНегативноеОтклонение();
	
КонецПроцедуры

&НаСервере 
Функция ПриведенноеГраничноеНегативноеОтклонение()
	
	ПриведенноеЗначение = 0;
	ВидыЦелевыхТрендовПоказателей = Перечисления.ВидыЦелевыхТрендовПоказателей;
	Тренд = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.ВариантАнализа, "Владелец.ЦелевойТренд");
	ВидыЦелевыхЗначений = Перечисления.ВидыЦелевыхЗначенийВариантовАнализа;
	ВидЦелевогоЗначения = Запись.ВидЦелевогоЗначения;
	ВидыОтклоненийОтЦелевыхЗначений = Перечисления.ВидыОтклоненийОтЦелевыхЗначенийПоказателей;
	ВидОтклоненияОтЦелевогоЗначения = Запись.ВидОтклоненияОтЦелевогоЗначения;
	
	Если Тренд = ВидыЦелевыхТрендовПоказателей.Максимизация Тогда
		Если ВидЦелевогоЗначения = ВидыЦелевыхЗначений.Значение Тогда
			Если ВидОтклоненияОтЦелевогоЗначения = ВидыОтклоненийОтЦелевыхЗначений.Абсолютное Тогда
				ПриведенноеЗначение = Запись.ЦелевоеЗначение - Запись.ГраничноеНегативноеОтклонение;
			Иначе
				ПриведенноеЗначение = Запись.ЦелевоеЗначение*(100 - Запись.ГраничноеНегативноеОтклонение)/100;
			КонецЕсли;
			
		Иначе
			Если ВидОтклоненияОтЦелевогоЗначения = ВидыОтклоненийОтЦелевыхЗначений.Абсолютное Тогда
				ПриведенноеЗначение = Запись.ЦелевойДиапазонМинимум - Запись.ГраничноеНегативноеОтклонение;
			Иначе
				ПриведенноеЗначение = Запись.ЦелевойДиапазонМинимум*(100 - Запись.ГраничноеНегативноеОтклонение)/100;
			КонецЕсли;
			
		КонецЕсли;
	ИначеЕсли Тренд = ВидыЦелевыхТрендовПоказателей.Минимизация Тогда
		Если ВидЦелевогоЗначения = ВидыЦелевыхЗначений.Значение Тогда
			Если ВидОтклоненияОтЦелевогоЗначения = ВидыОтклоненийОтЦелевыхЗначений.Абсолютное Тогда
				ПриведенноеЗначение = Запись.ЦелевоеЗначение + Запись.ГраничноеНегативноеОтклонение;
			Иначе
				ПриведенноеЗначение = Запись.ЦелевоеЗначение*(100 + Запись.ГраничноеНегативноеОтклонение)/100;
			КонецЕсли;
			
		Иначе
			Если ВидОтклоненияОтЦелевогоЗначения = ВидыОтклоненийОтЦелевыхЗначений.Абсолютное Тогда
				ПриведенноеЗначение = Запись.ЦелевойДиапазонМаксимум + Запись.ГраничноеНегативноеОтклонение;
			Иначе
				ПриведенноеЗначение = Запись.ЦелевойДиапазонМаксимум*(100 + Запись.ГраничноеНегативноеОтклонение)/100;
			КонецЕсли;
			
		КонецЕсли;
	ИначеЕсли Тренд = ВидыЦелевыхТрендовПоказателей.Диапазон Тогда
		Если ВидОтклоненияОтЦелевогоЗначения = ВидыОтклоненийОтЦелевыхЗначений.Абсолютное Тогда
			ПриведенноеЗначение = Запись.ЦелевойДиапазонМинимум - Запись.ГраничноеНегативноеОтклонение;
		Иначе
			ПриведенноеЗначение = Запись.ЦелевойДиапазонМинимум*(100 - Запись.ГраничноеНегативноеОтклонение)/100;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПриведенноеЗначение;
	
КонецФункции

&НаСервере 
Функция ПриведенноеГраничноеПозитивноеОтклонение()
	
	ПриведенноеЗначение = 0;
	ВидыЦелевыхТрендовПоказателей = Перечисления.ВидыЦелевыхТрендовПоказателей;
	Тренд = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.ВариантАнализа, "Владелец.ЦелевойТренд");
	ВидыЦелевыхЗначений = Перечисления.ВидыЦелевыхЗначенийВариантовАнализа;
	ВидЦелевогоЗначения = Запись.ВидЦелевогоЗначения;
	ВидыОтклоненийОтЦелевыхЗначений = Перечисления.ВидыОтклоненийОтЦелевыхЗначенийПоказателей;
	ВидОтклоненияОтЦелевогоЗначения = Запись.ВидОтклоненияОтЦелевогоЗначения;
	
	Если Тренд = ВидыЦелевыхТрендовПоказателей.Максимизация Тогда
		Если ВидЦелевогоЗначения = ВидыЦелевыхЗначений.Значение Тогда
			Если ВидОтклоненияОтЦелевогоЗначения = ВидыОтклоненийОтЦелевыхЗначений.Абсолютное Тогда
				ПриведенноеЗначение = Запись.ЦелевоеЗначение + Запись.ГраничноеПозитивноеОтклонение;
			Иначе
				ПриведенноеЗначение = Запись.ЦелевоеЗначение*(100 + Запись.ГраничноеПозитивноеОтклонение)/100;
			КонецЕсли;
			
		Иначе
			Если ВидОтклоненияОтЦелевогоЗначения = ВидыОтклоненийОтЦелевыхЗначений.Абсолютное Тогда
				ПриведенноеЗначение = Запись.ЦелевойДиапазонМаксимум + Запись.ГраничноеПозитивноеОтклонение;
			Иначе
				ПриведенноеЗначение = Запись.ЦелевойДиапазонМаксимум*(100 + Запись.ГраничноеПозитивноеОтклонение)/100;
			КонецЕсли;
			
		КонецЕсли;
	ИначеЕсли Тренд = ВидыЦелевыхТрендовПоказателей.Минимизация Тогда
		Если ВидЦелевогоЗначения = ВидыЦелевыхЗначений.Значение Тогда
			Если ВидОтклоненияОтЦелевогоЗначения = ВидыОтклоненийОтЦелевыхЗначений.Абсолютное Тогда
				ПриведенноеЗначение = Запись.ЦелевоеЗначение - Запись.ГраничноеПозитивноеОтклонение;
			Иначе
				ПриведенноеЗначение = Запись.ЦелевоеЗначение*(100 - Запись.ГраничноеПозитивноеОтклонение)/100;
			КонецЕсли;
			
		Иначе
			Если ВидОтклоненияОтЦелевогоЗначения = ВидыОтклоненийОтЦелевыхЗначений.Абсолютное Тогда
				ПриведенноеЗначение = Запись.ЦелевойДиапазонМинимум - Запись.ГраничноеПозитивноеОтклонение;
			Иначе
				ПриведенноеЗначение = Запись.ЦелевойДиапазонМинимум*(100 - Запись.ГраничноеПозитивноеОтклонение)/100;
			КонецЕсли;
			
		КонецЕсли;
	ИначеЕсли Тренд = ВидыЦелевыхТрендовПоказателей.Диапазон Тогда
		Если ВидОтклоненияОтЦелевогоЗначения = ВидыОтклоненийОтЦелевыхЗначений.Абсолютное Тогда
			ПриведенноеЗначение = Запись.ЦелевойДиапазонМаксимум + Запись.ГраничноеПозитивноеОтклонение;
		Иначе
			ПриведенноеЗначение = Запись.ЦелевойДиапазонМаксимум*(100 + Запись.ГраничноеПозитивноеОтклонение)/100;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПриведенноеЗначение;
	
КонецФункции

&НаСервере 
Процедура УстановитьВидимостьЭлементовФормы()
	
	// Видимость полей диапазона
	ВидыЦелевыхЗначенийВариантовАнализа = Перечисления.ВидыЦелевыхЗначенийВариантовАнализа;
	
	Если Запись.ВидЦелевогоЗначения = ВидыЦелевыхЗначенийВариантовАнализа.Значение Тогда
		ВидимостьПолейДиапазона = Ложь;
	ИначеЕсли Запись.ВидЦелевогоЗначения = ВидыЦелевыхЗначенийВариантовАнализа.Диапазон Тогда
		ВидимостьПолейДиапазона = Истина;
	КонецЕсли;
	
	Элементы.ГруппаЦелевоеЗначение.Видимость = НЕ ВидимостьПолейДиапазона;
	Элементы.ГруппаЦелевойДиапазон.Видимость = ВидимостьПолейДиапазона;
	
КонецПроцедуры

&НаСервере 
Процедура УстановитьЕдиницыИзмеренияОтклонений()
	
	Размерность = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.ВариантАнализа, "Владелец.Размерность");
	ВидыОтклоненийОтЦелевыхЗначенийПоказателей = Перечисления.ВидыОтклоненийОтЦелевыхЗначенийПоказателей;
	
	Если Запись.ВидОтклоненияОтЦелевогоЗначения = ВидыОтклоненийОтЦелевыхЗначенийПоказателей.Абсолютное Тогда
		Элементы.ГраничноеПозитивноеОтклонениеЕдИзм.Заголовок = Размерность;
		Элементы.ГраничноеНегативноеОтклонениеЕдИзм.Заголовок = Размерность;
	Иначе
		Элементы.ГраничноеПозитивноеОтклонениеЕдИзм.Заголовок = "%";
		Элементы.ГраничноеНегативноеОтклонениеЕдИзм.Заголовок = "%";
	КонецЕсли;
	
	Элементы.ЦелевоеЗначениеЕдИзм.Заголовок = Размерность;
	Элементы.ЦелевойДиапазонМаксимумЕдИзм.Заголовок = Размерность;
	Элементы.ЦелевойДиапазонМинимумЕдИзм.Заголовок = Размерность;
	
	Элементы.ПриведенноеГраничноеПозитивноеОтклонениеЕдИзм.Заголовок = Размерность;
	Элементы.ПриведенноеГраничноеНегативноеОтклонениеЕдИзм.Заголовок = Размерность;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
