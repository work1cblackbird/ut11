// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Ищет доверенность-владельца правила проверки МЧД.
// 
// Параметры:
//  Правило - СправочникСсылка.ПравилаПроверкиПолномочийМЧД
// 
// Возвращаемое значение:
//  - СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов
//  - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//  - Неопределено
//  
Функция Доверенность(Правило) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Правило) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПравилаПроверкиПолномочийПоМЧД.Доверенность КАК Доверенность
		|ИЗ
		|	РегистрСведений.ПравилаПроверкиПолномочийПоМЧД КАК ПравилаПроверкиПолномочийПоМЧД
		|ГДЕ
		|	ПравилаПроверкиПолномочийПоМЧД.ПравилоПроверки = &Правило";
	
	Запрос.УстановитьПараметр("Правило", Правило);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Доверенность;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Ищет правило проверки МЧД.
// 
// Параметры:
//  МЧД - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//  	- СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов
//  	- СправочникСсылка.МЧД003
// 
// Возвращаемое значение:
//  Структура:
//   * Правило - СправочникСсылка.ПравилаПроверкиПолномочийМЧД
//   * Используется - Булево
//  
Функция ПравилоПроверки(МЧД) Экспорт
	
	Структура = Новый Структура("Правило, Используется", Справочники.ПравилаПроверкиПолномочийМЧД.ПустаяСсылка(), Ложь);
	
	Если НЕ ЗначениеЗаполнено(МЧД) Тогда
		Возврат Структура;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПравилаПроверкиПолномочийПоМЧД.ПравилоПроверки,
	|	ПравилаПроверкиПолномочийПоМЧД.Используется
	|ИЗ
	|	РегистрСведений.ПравилаПроверкиПолномочийПоМЧД КАК ПравилаПроверкиПолномочийПоМЧД
	|ГДЕ
	|	ПравилаПроверкиПолномочийПоМЧД.Доверенность = &МЧД";
	
	Запрос.УстановитьПараметр("МЧД", МЧД);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Структура.Правило = Выборка.ПравилоПроверки;
		Структура.Используется = Выборка.Используется;
	КонецЕсли;
	
	Возврат Структура;
	
КонецФункции

#КонецОбласти

#КонецЕсли
