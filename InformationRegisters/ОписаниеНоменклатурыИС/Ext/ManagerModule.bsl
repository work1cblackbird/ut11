
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Сохраняет описание номенклатуры
//
// Параметры:
//   ДанныеОписания    - см. ОбщегоНазначенияИС.ИнициализироватьДанныеОписанияНоменклатуры.
//   СохраненныеДанные - см. ОбщегоНазначенияИС.ИнициализироватьДанныеОписанияНоменклатуры.
Процедура УстановитьОписание(ДанныеОписания, СохраненныеДанные = Неопределено) Экспорт

	УстановитьПривилегированныйРежим(Истина);

	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Номенклатура.Установить(ДанныеОписания.Номенклатура, Истина);

	Если СохраненныеДанные = Неопределено Тогда
		НаборЗаписей.Прочитать();
	КонецЕсли;
	
	ЕстьЗаписьВРегистре = НаборЗаписей.Выбран();
	
	Если ЕстьЗаписьВРегистре И СохраненныеДанные = Неопределено Тогда

		Если НаборЗаписей.Количество() Тогда
			ЗаписьНабора = НаборЗаписей[0];
		Иначе
			ЗаписьНабора = НаборЗаписей.Добавить();
			ЗаписьНабора.Номенклатура = ДанныеОписания.Номенклатура;
		КонецЕсли;

		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "ЕмкостьПотребительскойУпаковки")
			И ДанныеОписания.ЕмкостьПотребительскойУпаковки <> Неопределено Тогда
			ЗаписьНабора.ЕмкостьПотребительскойУпаковки = ДанныеОписания.ЕмкостьПотребительскойУпаковки;
		КонецЕсли;

		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "КоличествоВПотребительскойУпаковке")
			И ДанныеОписания.КоличествоВПотребительскойУпаковке <> Неопределено Тогда
			ЗаписьНабора.КоличествоВПотребительскойУпаковке = ДанныеОписания.КоличествоВПотребительскойУпаковке;
		КонецЕсли;

		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "ВариантЧастичногоВыбытия")
			И ДанныеОписания.ВариантЧастичногоВыбытия <> Неопределено Тогда
			ЗаписьНабора.ВариантЧастичногоВыбытия = ДанныеОписания.ВариантЧастичногоВыбытия;
		КонецЕсли;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "ВариантИспользованияЕдиницыХранения")
			И ДанныеОписания.ВариантИспользованияЕдиницыХранения <> Неопределено Тогда
			ЗаписьНабора.ВариантИспользованияЕдиницыХранения = ДанныеОписания.ВариантИспользованияЕдиницыХранения;
		КонецЕсли;

		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "ПотребительскаяУпаковка")
			И ДанныеОписания.ПотребительскаяУпаковка <> Неопределено Тогда
			ЗаписьНабора.ПотребительскаяУпаковка = ДанныеОписания.ПотребительскаяУпаковка;
		КонецЕсли;

		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "УпаковкаЧастичногоВыбытия")
			И ДанныеОписания.УпаковкаЧастичногоВыбытия <> Неопределено Тогда
			ЗаписьНабора.УпаковкаЧастичногоВыбытия = ДанныеОписания.УпаковкаЧастичногоВыбытия;
		КонецЕсли;

	ИначеЕсли СохраненныеДанные <> Неопределено Тогда

		ЗаписьНабора = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(ЗаписьНабора, ДанныеОписания);

		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "ЕмкостьПотребительскойУпаковки")
			И ДанныеОписания.ЕмкостьПотребительскойУпаковки = Неопределено Тогда
			ЗаписьНабора.ЕмкостьПотребительскойУпаковки = СохраненныеДанные.ЕмкостьПотребительскойУпаковки;
		КонецЕсли;

		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "КоличествоВПотребительскойУпаковке")
			И ДанныеОписания.КоличествоВПотребительскойУпаковке = Неопределено Тогда
			ЗаписьНабора.КоличествоВПотребительскойУпаковке = СохраненныеДанные.КоличествоВПотребительскойУпаковке;
		КонецЕсли;

		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "ВариантЧастичногоВыбытия")
			И ДанныеОписания.ВариантЧастичногоВыбытия = Неопределено Тогда
			ЗаписьНабора.ВариантЧастичногоВыбытия = СохраненныеДанные.ВариантЧастичногоВыбытия;
		КонецЕсли;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "ВариантИспользованияЕдиницыХранения")
			И ДанныеОписания.ВариантИспользованияЕдиницыХранения = Неопределено Тогда
			ЗаписьНабора.ВариантИспользованияЕдиницыХранения = СохраненныеДанные.ВариантИспользованияЕдиницыХранения;
		КонецЕсли;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "ПотребительскаяУпаковка")
			И ДанныеОписания.ПотребительскаяУпаковка = Неопределено Тогда
			ЗаписьНабора.ПотребительскаяУпаковка = СохраненныеДанные.ПотребительскаяУпаковка;
		КонецЕсли;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеОписания, "УпаковкаЧастичногоВыбытия")
			И ДанныеОписания.УпаковкаЧастичногоВыбытия = Неопределено Тогда
			ЗаписьНабора.УпаковкаЧастичногоВыбытия = СохраненныеДанные.УпаковкаЧастичногоВыбытия;
		КонецЕсли;
		
	Иначе

		ЗаписьНабора = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(ЗаписьНабора, ДанныеОписания);

	КонецЕсли;
	
	Если (ЗаписьНабора.ВариантИспользованияЕдиницыХранения = Перечисления.ВариантыИспользованияЕдиницыХраненияИС.ПотребительскаяУпаковка
		И Не ЗначениеЗаполнено(ЗаписьНабора.ВариантЧастичногоВыбытия))
		Или (ЗаписьНабора.ВариантИспользованияЕдиницыХранения = Перечисления.ВариантыИспользованияЕдиницыХраненияИС.ЗаданУпаковками
			И Не ЗначениеЗаполнено(ЗаписьНабора.ПотребительскаяУпаковка)
			И Не ЗначениеЗаполнено(ЗаписьНабора.УпаковкаЧастичногоВыбытия)) Тогда
		НаборЗаписей.Очистить();
	КонецЕсли;
	
	Если НаборЗаписей.Количество() = 0 И Не ЕстьЗаписьВРегистре Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей.Записать();

КонецПроцедуры

#КонецОбласти

#КонецЕсли

