///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ДанныеНастроекПоКассе(Касса) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиКассОФД.НастройкаПодключения КАК НастройкаПодключения,
	|	НастройкиКассОФД.РегистрационныйНомерККТ КАК РегистрационныйНомерККТ,
	|	НастройкиКассОФД.ЗаводскойНомерФН КАК ЗаводскойНомерФН,
	|	НастройкиКассОФД.Магазин КАК Магазин,
	|	НастройкиКассОФД.ПредставлениеКассы КАК ПредставлениеКассы,
	|	НастройкиКассОФД.СценарийИспользования КАК СценарийИспользования
	|ИЗ
	|	РегистрСведений.НастройкиКассОФД КАК НастройкиКассОФД
	|ГДЕ
	|	НастройкиКассОФД.Касса = &Касса";
	
	Запрос.УстановитьПараметр("Касса", Касса);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		Результат = Новый Структура;
		
		Результат.Вставить("НастройкаПодключения",    ВыборкаДетальныеЗаписи.НастройкаПодключения);
		Результат.Вставить("РегистрационныйНомерККТ", ВыборкаДетальныеЗаписи.РегистрационныйНомерККТ);
		Результат.Вставить("ЗаводскойНомерФН",        ВыборкаДетальныеЗаписи.ЗаводскойНомерФН);
		Результат.Вставить("Магазин",                 ВыборкаДетальныеЗаписи.Магазин);
		Результат.Вставить("ПредставлениеКассы",      ВыборкаДетальныеЗаписи.ПредставлениеКассы);
		Результат.Вставить("СценарийИспользования",   ВыборкаДетальныеЗаписи.СценарийИспользования);
		
		Возврат Результат;
		
	КонецЕсли;
	
КонецФункции

Процедура ЗаписатьНастройкиКассы(Касса, НастройкиКассы) Экспорт
	
	Запись = РегистрыСведений.НастройкиКассОФД.СоздатьМенеджерЗаписи();
	
	Запись.Касса = Касса;
	ЗаполнитьЗначенияСвойств(Запись, НастройкиКассы);
	
	Запись.Записать();
	
КонецПроцедуры

Процедура УдалитьНастройкиКассы(Касса) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиКассОФД");
		ЭлементБлокировки.УстановитьЗначение("Касса", Касса);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Запись = РегистрыСведений.НастройкиКассОФД.СоздатьМенеджерЗаписи();
		Запись.Касса = Касса;
		Запись.Удалить();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При удалении параметров подключения возникли ошибки:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ОФДСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ТекстИсключения,
			Истина);
		
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли