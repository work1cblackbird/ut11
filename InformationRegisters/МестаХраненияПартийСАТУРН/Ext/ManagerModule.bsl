#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Записывает движения документа в регистр.
// 
// Параметры:
//  ДополнительныеСвойства - Структура - Дополнительные свойства:
//   * ТаблицыДляДвижений - Структура - таблицы движений документа:
//     ** ТаблицаМестаХраненияПартийСАТУРН - см. ПустаяТаблицаМестаХраненияПартийСАТУРН
//  Движения - Структура - движения документа по регистрам:
//    * МестаХраненияПартийСАТУРН - РегистрСведенийНаборЗаписей.МестаХраненияПартийСАТУРН - движения документа
//  Отказ - Булево - Отказ от проведения / записи движений
Процедура ОтразитьДвижения(ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	Если Отказ Или ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаМестаХраненияПартийСАТУРН.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДвиженияОстаткиПродукцииСАТУРН = Движения.МестаХраненияПартийСАТУРН;
	ДвиженияОстаткиПродукцииСАТУРН.Записывать = Истина;
	ДвиженияОстаткиПродукцииСАТУРН.Загрузить(ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаМестаХраненияПартийСАТУРН);
	
КонецПроцедуры

// Обновить движения при изменении статуса документа.
// 
// Параметры:
//  ДокументСсылка - ДокументСсылка - регистратор текущего регистра
Процедура ОбновитьДвиженияПриИзмененииСтатусаДокумента(ДокументСсылка) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
	
	ДополнительныеСвойства = Новый Структура;
	ИнтеграцияИС.ИнициализироватьДополнительныеСвойстваДляПроведения(ДокументСсылка, ДополнительныеСвойства);
	Документы[ДополнительныеСвойства.ДляПроведения.МетаданныеДокумента.Имя].ИнициализироватьДанныеДокумента(
		ДокументСсылка, ДополнительныеСвойства, "МестаХраненияПартийСАТУРН");
	НаборЗаписей.Загрузить(ДополнительныеСвойства.ТаблицыДляДвижений["ТаблицаМестаХраненияПартийСАТУРН"]);
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Пустая таблица остатки продукции САТУРН.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Пустая таблица остатки продукции САТУРН
Функция ПустаяТаблицаМестаХраненияПартийСАТУРН() Экспорт
	Возврат СоздатьНаборЗаписей().Выгрузить();
КонецФункции

#КонецОбласти

#КонецЕсли
