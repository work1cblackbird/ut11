// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Параметры:
//  Организации - Неопределено
//              - Массив из ОпределяемыйТип.Организация
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
//  * Идентификатор - Строка
//  * Организация - ОпределяемыйТип.Организация
Функция Выгрузить(Организации = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	УчетныеЗаписиОблачногоЭДО.Идентификатор,
		|	УчетныеЗаписиОблачногоЭДО.Организация
		|ИЗ
		|	РегистрСведений.УчетныеЗаписиОблачногоЭДО КАК УчетныеЗаписиОблачногоЭДО";
	
	Если ЗначениеЗаполнено(Организации) Тогда
		Запрос.Текст = Запрос.Текст + "
			|ГДЕ
			|	УчетныеЗаписиОблачногоЭДО.Организация В (&Организации)";
		Запрос.УстановитьПараметр("Организации", Организации);
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Параметры:
//  Организация - ОпределяемыйТип.Организация
// 
// Возвращаемое значение:
//  Строка
Функция ИдентификаторУчетнойЗаписи(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	УчетныеЗаписиОблачногоЭДО.Идентификатор
		|ИЗ
		|	РегистрСведений.УчетныеЗаписиОблачногоЭДО КАК УчетныеЗаписиОблачногоЭДО
		|ГДЕ
		|	УчетныеЗаписиОблачногоЭДО.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Идентификатор;
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

// Параметры:
//  Организация - ОпределяемыйТип.Организация
// 
// Возвращаемое значение:
//  Булево
Функция ОрганизацияПодключена(Организация) Экспорт
	ИдентификаторОрганизации = ИдентификаторУчетнойЗаписи(Организация);
	Возврат ЗначениеЗаполнено(ИдентификаторОрганизации);
КонецФункции

#КонецОбласти

#КонецЕсли
