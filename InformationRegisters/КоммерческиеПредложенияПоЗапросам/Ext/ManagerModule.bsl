
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Процедура выполняет привязку КП к ЗКП
//
Процедура СопоставитьПоступившееПредложение(Знач ЗапросКоммерческихПредложений, Знач КоммерческоеПредложениеПоставщика) Экспорт
	
	Если Не ЗначениеЗаполнено(ЗапросКоммерческихПредложений)
			Или Не ЗначениеЗаполнено(КоммерческоеПредложениеПоставщика) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		
		МенеджерЗаписи = РегистрыСведений.КоммерческиеПредложенияПоЗапросам.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ЗапросКоммерческихПредложений = ЗапросКоммерческихПредложений;
		МенеджерЗаписи.КоммерческоеПредложениеПоставщика = КоммерческоеПредложениеПоставщика;
		МенеджерЗаписи.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		Информация = ИнформацияОбОшибке();
		
		ЭлектронноеВзаимодействие.ОбработатьОшибку(НСтр("ru = 'Сопоставление поступившего предложения с запросом КП'"), ПодробноеПредставлениеОшибки(Информация));
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли