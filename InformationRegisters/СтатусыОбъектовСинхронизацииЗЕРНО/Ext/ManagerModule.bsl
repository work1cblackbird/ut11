
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает структуру значений по умолчанию для документа для движений.
//
// Возвращаемое значение:
//	Структура - значения по умолчанию
//
Функция ЗначенияПоУмолчанию(СсылкаНаОформляемыйДокумент, ИдентификаторСтроки = "") Экспорт
	
	СтруктураЗначенияПоУмолчанию = Новый Структура;
	
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(СсылкаНаОформляемыйДокумент);
	
	СтруктураЗначенияПоУмолчанию.Вставить("ОбъектСинхронизации", СсылкаНаОформляемыйДокумент);
	СтруктураЗначенияПоУмолчанию.Вставить("Статус",   МенеджерОбъекта.СтатусПоУмолчанию());
	СтруктураЗначенияПоУмолчанию.Вставить("ИдентификаторСтроки", ИдентификаторСтроки);
	
	СтекДействий = Новый Массив;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ОбъектРасчета", СсылкаНаОформляемыйДокумент);
	
	ДальнейшееДействиеПоУмолчанию = МенеджерОбъекта.ДальнейшееДействиеПоУмолчанию(СтруктураПараметров);
	
	Если ТипЗнч(ДальнейшееДействиеПоУмолчанию) = Тип("Массив") Тогда
		Для Каждого Действие Из ДальнейшееДействиеПоУмолчанию Цикл
			СтекДействий.Добавить(Действие);
		КонецЦикла;
	Иначе
		СтекДействий.Добавить(ДальнейшееДействиеПоУмолчанию);
	КонецЕсли;
	
	СтекДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.НеТребуется);
	СтекДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.НеТребуется);
	
	МассивКлючейДальнейшихДействий = Новый Массив;
	МассивКлючейДальнейшихДействий.Добавить("ДальнейшееДействие1");
	МассивКлючейДальнейшихДействий.Добавить("ДальнейшееДействие2");
	МассивКлючейДальнейшихДействий.Добавить("ДальнейшееДействие3");
	
	Для Каждого ДальнейшееДействиеКлюч Из МассивКлючейДальнейшихДействий Цикл
		СтруктураЗначенияПоУмолчанию.Вставить(ДальнейшееДействиеКлюч, СтекДействий.Получить(0));
		СтекДействий.Удалить(0);
	КонецЦикла;
	
	Возврат СтруктураЗначенияПоУмолчанию;
	
КонецФункции

// Осуществляет запись в регистр по переданным данным.
//
// Параметры:
//  ДанныеЗаписи - данные для записи в регистр
//
Процедура ВыполнитьЗаписьВРегистр(ДанныеЗаписи) Экспорт
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ДанныеЗаписи);
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Функция ОбработатьНаборЗаписей(НаборЗаписей, ПараметрыОбновления, ЭтоНовый = Истина) Экспорт
	
	НовыйСтатус   = Неопределено;
	СтарыйСтатус  = Неопределено;
	ЗаписатьНабор = Неопределено;
	
	Для Каждого ЗаписьНабора Из НаборЗаписей Цикл
		
		ИдентификаторСтроки = "";
		Если ПараметрыОбновления.Свойство("ИдентификаторСтроки", ИдентификаторСтроки)
			И ЗначениеЗаполнено(ИдентификаторСтроки) Тогда
			Если ЗаписьНабора.ИдентификаторСтроки <> ИдентификаторСтроки Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗаписьНабора.Статус <> ПараметрыОбновления.НовыйСтатус Или ЭтоНовый Тогда
			СтарыйСтатус = ЗаписьНабора.Статус;
			ЗаписьНабора.Статус = ПараметрыОбновления.НовыйСтатус;
			НовыйСтатус = ПараметрыОбновления.НовыйСтатус;
			ЗаписатьНабор = Истина;
		КонецЕсли;
		Если ЗаписьНабора.ДальнейшееДействие1 <> ПараметрыОбновления.ДальнейшееДействие1 Тогда
			ЗаписьНабора.ДальнейшееДействие1 = ПараметрыОбновления.ДальнейшееДействие1;
			ЗаписатьНабор = Истина;
		КонецЕсли;
		Если ЗаписьНабора.ДальнейшееДействие2 <> ПараметрыОбновления.ДальнейшееДействие2 Тогда
			ЗаписьНабора.ДальнейшееДействие2 = ПараметрыОбновления.ДальнейшееДействие2;
			ЗаписатьНабор = Истина;
		КонецЕсли;
		Если ЗаписьНабора.ДальнейшееДействие3 <> ПараметрыОбновления.ДальнейшееДействие3 Тогда
			ЗаписьНабора.ДальнейшееДействие3 = ПараметрыОбновления.ДальнейшееДействие3;
			ЗаписатьНабор = Истина;
		КонецЕсли;
		Если (ПараметрыОбновления.ИдентификаторЗаявки <> Неопределено 
			И ЗаписьНабора.ИдентификаторЗаявки <> ПараметрыОбновления.ИдентификаторЗаявки)
			Или ЭтоНовый Тогда
			ЗаписьНабора.ИдентификаторЗаявки = ПараметрыОбновления.ИдентификаторЗаявки;
			ЗаписатьНабор = Истина;
		КонецЕсли;
	КонецЦикла;
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("НовыйСтатус",   НовыйСтатус);
	ВозвращаемоеЗначение.Вставить("СтарыйСтатус",  СтарыйСтатус);
	ВозвращаемоеЗначение.Вставить("ЗаписатьНабор", ЗаписатьНабор);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Дополнить параметры обновления статуса.
//
// Параметры:
//  ПараметрыОбновленияСтатуса - Структура - см. функцию ИнтеграцияЗЕРНОСлужебныйКлиентСервер.ПараметрыОбновленияСтатуса().
// 
// Возвращаемое значение:
//  Структура - см. функцию ИнтеграцияЗЕРНОСлужебныйКлиентСервер.ПараметрыОбновленияСтатуса().
//
Функция ДополнитьПараметрыОбновленияСтатуса(ПараметрыОбновленияСтатуса = Неопределено) Экспорт
	
	Если ПараметрыОбновленияСтатуса = Неопределено Тогда
		
		ПараметрыОбновленияСтатуса = ИнтеграцияЗЕРНОСлужебныйКлиентСервер.ПараметрыОбновленияСтатуса();
		
	КонецЕсли;
	
	Возврат ПараметрыОбновленияСтатуса;
	
КонецФункции

// Изменяет и возвращает статус документа ЗЕРНО.
//
// Параметры:
//  Документ - ДокументСсылка - Документ ЗЕРНО.
//  ПараметрыОбновления - Структура:
//   * НовыйСтатус - ПеречислениеСсылка.СтатусыПартийЗЕРНО, ПеречислениеСсылка.СтатусыОбработкиФормированиеПартийЗЕРНО - Новый статус.
//   * ИдентификаторСтроки - Строка - Идентификато строки документа
//   * ДальнейшееДействие1 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЗЕРНО - Дальнейшее действие 1.
//   * ДальнейшееДействие2 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЗЕРНО - Дальнейшее действие 2.
//   * ДальнейшееДействие3 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЗЕРНО - Дальнейшее действие 3.
//  ДополнительныеПараметры - Структура - см. функцию ИнтеграцияЗЕРНОСлужебныйКлиентСервер.ПараметрыОбновленияСтатуса().
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыПартийЗЕРНО, ПеречислениеСсылка.СтатусыОбработкиФормированиеПартийЗЕРНО - новый статус документа ЗЕРНО.
//
Функция ОбновитьСтатус(Документ, ПараметрыОбновления, ДополнительныеПараметры = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ПараметрыОбновления = Неопределено Тогда
		ДанныеЗаписи = ЗначенияПоУмолчанию(Документ, ПараметрыОбновления.ИдентификаторСтроки);
		ВыполнитьЗаписьВРегистр(ДанныеЗаписи);
		Возврат ДанныеЗаписи.Статус;
	КонецЕсли;
	
	Если ПараметрыОбновления.НовыйСтатус = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка изменения статуса'");
	КонецЕсли;
	
	ЭтоНовый = Ложь;
	НаборЗаписей = НаборЗаписей(Документ, ПараметрыОбновления.ИдентификаторСтроки, ЭтоНовый);
	ЗаписатьНабор = НаборЗаписей.Модифицированность();
	
	Результат = ОбработатьНаборЗаписей(НаборЗаписей, ПараметрыОбновления, ЭтоНовый);
	Если Результат.ЗаписатьНабор <> Неопределено Тогда
		ЗаписатьНабор = Результат.ЗаписатьНабор;
	КонецЕсли;
	НовыйСтатус  = Результат.НовыйСтатус;
	СтарыйСтатус = Результат.СтарыйСтатус;
	
	Если ЗаписатьНабор Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		НаборЗаписей.Записать();
		
		Если СтарыйСтатус <> НовыйСтатус Тогда
			
			ПолноеИмя = Документ.Метаданные().ПолноеИмя();
			МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
			
			ПараметрыОбновленияСтатуса = ДополнитьПараметрыОбновленияСтатуса(ДополнительныеПараметры);
			
			МенеджерОбъекта.ПриИзмененииСтатусаДокумента(
				Документ,
				СтарыйСтатус, 
				НовыйСтатус,
				ПараметрыОбновленияСтатуса);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат НовыйСтатус;
	
КонецФункции

Процедура УдалитьСтатусыПоИдентификатору(Документ, ИдентификаторыИзмененныхСтрок) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = СоздатьНаборЗаписей();
	Для Каждого ИдентификаторСтроки Из ИдентификаторыИзмененныхСтрок Цикл
		Набор.Отбор.ОбъектСинхронизации.Установить(Документ);
		Набор.Отбор.ИдентификаторСтроки.Установить(ИдентификаторСтроки);
		Набор.Записать();
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьСтатус(Документ, ИдентификаторСтроки, ПараметрыОбновления, ДополнительныеПараметры = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ОбъектСинхронизации.Установить(Документ);
	ЗначенияПоУмолчанию = ЗначенияПоУмолчанию(Документ, ИдентификаторСтроки);
	ЗаписьНабора = НаборЗаписей.Добавить();
	ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗначенияПоУмолчанию);
	ОбработатьНаборЗаписей(НаборЗаписей, ПараметрыОбновления);
	НаборЗаписей.Записать(Истина);
	
	ПолноеИмя = Документ.Метаданные().ПолноеИмя();
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
	
	ПараметрыОбновленияСтатуса = ДополнитьПараметрыОбновленияСтатуса(ДополнительныеПараметры);
	
	МенеджерОбъекта.ПриИзмененииСтатусаДокумента(
		Документ,
		Неопределено, ПараметрыОбновления.НовыйСтатус,
		ПараметрыОбновленияСтатуса);
	
КонецПроцедуры

Функция ТекущееСостояние(ОбъектСинхронизации) Экспорт
	
	ВозвращаемоеЗначение = ЗначенияПоУмолчанию(ОбъектСинхронизации);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СтатусыОбъектовСинхронизацииЗЕРНО.ОбъектСинхронизации,
	|	СтатусыОбъектовСинхронизацииЗЕРНО.Статус,
	|	СтатусыОбъектовСинхронизацииЗЕРНО.ДальнейшееДействие1,
	|	СтатусыОбъектовСинхронизацииЗЕРНО.ДальнейшееДействие2,
	|	СтатусыОбъектовСинхронизацииЗЕРНО.ДальнейшееДействие3
	|ИЗ
	|	РегистрСведений.СтатусыОбъектовСинхронизацииЗЕРНО КАК СтатусыОбъектовСинхронизацииЗЕРНО
	|ГДЕ
	|	СтатусыОбъектовСинхронизацииЗЕРНО.ОбъектСинхронизации = &ОбъектСинхронизации
	|	И СтатусыОбъектовСинхронизацииЗЕРНО.ИдентификаторСтроки = """"");
	
	Запрос.УстановитьПараметр("ОбъектСинхронизации", ОбъектСинхронизации);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(ВозвращаемоеЗначение, Выборка);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Рассчитать статусы.
// 
// Параметры:
//  ДокументСсылка Документ ссылка
//  СтатусОбработки Статус обработки
//  Статусы Статусы
// 
// Возвращаемое значение:
//  Неопределено, См. Воз - Рассчитать статусы:
// * НовыйСтатус - ПеречислениеСсылка.СтатусыОбработкиФормированиеПартийЗЕРНО, ПеречислениеСсылка.СтатусыОбработкиЗапросаОстатковПартийЗЕРНО, ПеречислениеСсылка.СтатусыОбработкиМестФормированияПартийЗЕРНО, ПеречислениеСсылка.СтатусыОбработкиОформлениеСДИЗЗЕРНО, ПеречислениеСсылка.СтатусыОбработкиСообщенийЗЕРНО, ПеречислениеСсылка.СтатусыОбработкиСведенийОСобранномУрожаеЗЕРНО, ПеречислениеСсылка.СтатусыОбработкиВнесениеСведенийОСобранномУрожаеЗЕРНО, ПеречислениеСсылка.СтатусыОбработкиСписанияПартииЗЕРНО, ПеречислениеСсылка.СтатусыОбработкиПогашенияСДИЗЗЕРНО -
// * ИдентификаторСтроки - Неопределено -
// * ИдентификаторЗаявки - Неопределено -
// * ДальнейшееДействие1 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЗЕРНО -
// * ДальнейшееДействие2 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЗЕРНО -
// * ДальнейшееДействие3 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЗЕРНО -
Функция РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, Статусы) Экспорт
	
	Если СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийЗЕРНО.ЗаявкаОтклонена
		Или СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийЗЕРНО.Ошибка Тогда
		
		ДальнейшиеДействия = Новый Массив;
		Для Каждого Действие Из Статусы.ОшибкаДействия Цикл
			ДальнейшиеДействия.Добавить(Действие);
		КонецЦикла;
		
		Возврат ВозвращаемоеЗначениеДальнейшиеДействияСтатус(Статусы.Ошибка, ДальнейшиеДействия);
		
	ИначеЕсли СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийЗЕРНО.ЗаявкаОбрабатывается Тогда
		
		Если Статусы.Обрабатывается = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДальнейшиеДействия = Новый Массив;
		Для Каждого Действие Из Статусы.ОбрабатываетсяДействия Цикл
			ДальнейшиеДействия.Добавить(Действие);
		КонецЦикла;
		
		Возврат ВозвращаемоеЗначениеДальнейшиеДействияСтатус(Статусы.Обрабатывается, ДальнейшиеДействия);
		
	ИначеЕсли СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийЗЕРНО.ЗаявкаВыполнена
		Или СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийЗЕРНО.ЗаявкаПринята
		Или СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийЗЕРНО.ЗаявкаПроигнорирована Тогда
		
		ДальнейшиеДействия = Новый Массив;
		Для Каждого Действие Из Статусы.ПринятДействия Цикл
			ДальнейшиеДействия.Добавить(Действие);
		КонецЦикла;
		
		Возврат ВозвращаемоеЗначениеДальнейшиеДействияСтатус(Статусы.Принят, ДальнейшиеДействия);
		
	Иначе
		
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Неизвестный статус обработки: %1'"), СтатусОбработки);
		
	КонецЕсли;
	
КонецФункции

Функция РассчитатьСтатусыКПередаче(ДокументСсылка, НовыйСтатус) Экспорт
	
	РеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Организация, Подразделение");
	
	ИспользоватьАвтоматическийОбмен = ИнтеграцияЗЕРНОПовтИсп.ИспользоватьАвтоматическийОбменДанными(
		РеквизитыОбъекта.Организация,
		РеквизитыОбъекта.Подразделение);
	
	ДальнейшиеДействия = Новый Массив;
	Если ИспользоватьАвтоматическийОбмен Тогда
		ДальнейшиеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.ОжидайтеПередачуДанныхРегламентнымЗаданием);
	Иначе
		ДальнейшиеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.ВыполнитеОбмен);
		ДальнейшиеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.ОтменитеПередачуДанных);
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначениеДальнейшиеДействияСтатус(НовыйСтатус, ДальнейшиеДействия);
	
КонецФункции

Функция ДальнейшиеДействия(Статусы) Экспорт
	
	ВозвращаемоеЗначение = Новый Массив;
	
	Если Статусы.ДальнейшееДействие1 <> Перечисления.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.НеТребуется Тогда
		ВозвращаемоеЗначение.Добавить(Статусы.ДальнейшееДействие1);
	КонецЕсли;
	Если Статусы.ДальнейшееДействие2 <> Перечисления.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.НеТребуется Тогда
		ВозвращаемоеЗначение.Добавить(Статусы.ДальнейшееДействие2);
	КонецЕсли;
	Если Статусы.ДальнейшееДействие3 <> Перечисления.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.НеТребуется Тогда
		ВозвращаемоеЗначение.Добавить(Статусы.ДальнейшееДействие3);
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Выполняет запись в регистр дальнейших действий, обозначающих архивирование документов.
// 
// Параметры:
// 	ДокументыКАрхивированию - Массив из ОпределяемыйТип.ДокументыЗЕРНО - Документы для архивирования.
// Возвращаемое значение:
// 	Массив из Структура - (см. ИнтеграцияЗЕРНОСлужебный.СтруктураИзменения()).
//
Функция Архивировать(ДокументыКАрхивированию) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Изменения = Новый Массив;
	
	Для Каждого ДокументСсылка Из ДокументыКАрхивированию Цикл
		
		ЗначенияЗаписи = ТекущееСостояние(ДокументСсылка);
		ЗначенияЗаписи.ДальнейшееДействие1 = Перечисления.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.НеТребуется;
		ЗначенияЗаписи.ДальнейшееДействие2 = Перечисления.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.НеТребуется;
		ЗначенияЗаписи.ДальнейшееДействие3 = Перечисления.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.НеТребуется;
		
		Если Не ПравоДоступа("Изменение", ДокументСсылка.Метаданные()) Тогда
			ВызватьИсключение НСтр("ru = 'Недостаточно прав доступа для очистки дальнейших действий по документу'");
		КонецЕсли;
	
		НачатьТранзакцию();
		
		УстановитьПривилегированныйРежим(Истина);
		Попытка
			
			ВыполнитьЗаписьВРегистр(ЗначенияЗаписи);
			
			ДокументОснование = Неопределено;
			Если ДокументСсылка.Метаданные().Реквизиты.Найти("ДокументОснование") <> Неопределено Тогда
				ДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ДокументОснование");
			КонецЕсли;
			
			Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ОчередьСообщенийЗЕРНО.Сообщение КАК Сообщение
			|ИЗ
			|	РегистрСведений.ОчередьСообщенийЗЕРНО КАК ОчередьСообщенийЗЕРНО
			|ГДЕ
			|	ОчередьСообщенийЗЕРНО.СсылкаНаОбъект = &Документ");
			Запрос.Параметры.Вставить("Документ", ДокументСсылка);
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				НаборЗаписей = РегистрыСведений.ОчередьСообщенийЗЕРНО.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Сообщение.Установить(Выборка.Сообщение, Истина);
				НаборЗаписей.Записать();
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
			
			ТекстОшибки = "";
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстОшибки = СтрШаблон(
				НСтр("ru = 'При архивировании документа %1 возникла ошибка:
				           |Текст ошибки: %2'"),
				ДокументСсылка,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ИнтеграцияЗЕРНОСлужебный.ЗаписатьОшибкуВЖурналРегистрации(ТекстОшибки);
			
			ТекстОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			
		КонецПопытки;
		УстановитьПривилегированныйРежим(Ложь);
		
		СтрокаРезультата = ИнтеграцияЗЕРНОСлужебный.СтруктураИзменения();
		СтрокаРезультата.ДокументОснование = ДокументОснование;
		СтрокаРезультата.ТекстОшибки       = ТекстОшибки;
		СтрокаРезультата.Объект.Добавить(ДокументСсылка);
		
		Изменения.Добавить(СтрокаРезультата);
		
	КонецЦикла;
	
	Возврат Изменения;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НаборЗаписей(ОбъектСинхронизации, ИдентификаторСтроки, ЭтоНовый)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СтатусыОбъектовСинхронизацииЗЕРНО.ОбъектСинхронизации КАК ОбъектСинхронизации
	|ИЗ
	|	РегистрСведений.СтатусыОбъектовСинхронизацииЗЕРНО КАК СтатусыОбъектовСинхронизацииЗЕРНО
	|ГДЕ
	|	СтатусыОбъектовСинхронизацииЗЕРНО.ОбъектСинхронизации = &ОбъектСинхронизации
	|	И СтатусыОбъектовСинхронизацииЗЕРНО.ИдентификаторСтроки = &ИдентификаторСтроки");
	
	Запрос.УстановитьПараметр("ОбъектСинхронизации", ОбъектСинхронизации);
	Запрос.УстановитьПараметр("ИдентификаторСтроки", ИдентификаторСтроки);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЭтоНовый = Запрос.Выполнить().Пустой();
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ОбъектСинхронизации.Установить(ОбъектСинхронизации, Истина);
	НаборЗаписей.Отбор.ИдентификаторСтроки.Установить(ИдентификаторСтроки, Истина);
	
	Если ЭтоНовый Тогда
		
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ЗначенияПоУмолчанию(ОбъектСинхронизации, ИдентификаторСтроки));
		
	Иначе
		
		НаборЗаписей.Прочитать();
		
	КонецЕсли;
	
	Возврат НаборЗаписей;
	
КонецФункции

Функция СтруктураСтатусы() Экспорт
	
	Статусы = Новый Структура;
	Статусы.Вставить("Принят");
	Статусы.Вставить("Обрабатывается");
	Статусы.Вставить("Ошибка");
	
	Статусы.Вставить("ОшибкаДействия",         Новый Массив);
	Статусы.Вставить("ПринятДействия",         Новый Массив);
	Статусы.Вставить("ОбрабатываетсяДействия", Новый Массив);
	
	Возврат Статусы;
	
КонецФункции

// Возвращаемое значение дальнейшие действия статус.
// 
// Параметры:
//  Статус - См. РегистрСведений.СтатусыОбъектовСинхронизацииЗЕРНО.Статус
//  ДальнейшиеДействия - Массив Из ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЗЕРНО - Дальнейшие действия
// 
// Возвращаемое значение:
//  Структура - Возвращаемое значение дальнейшие действия статус:
// * НовыйСтатус - См. РегистрСведений.СтатусыОбъектовСинхронизацииЗЕРНО.Статус
// * ИдентификаторСтроки - Неопределено, ОпределяемыйТип.УникальныйИдентификаторИС -
// * ИдентификаторЗаявки - Неопределено, Строка -
// * ДальнейшееДействие1 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЗЕРНО -
// * ДальнейшееДействие2 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЗЕРНО -
// * ДальнейшееДействие3 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЗЕРНО -
Функция ВозвращаемоеЗначениеДальнейшиеДействияСтатус(Статус, ДальнейшиеДействия) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("НовыйСтатус",         Статус);
	ВозвращаемоеЗначение.Вставить("ИдентификаторСтроки", Неопределено);
	ВозвращаемоеЗначение.Вставить("ИдентификаторЗаявки", Неопределено);
	ВозвращаемоеЗначение.Вставить("ДальнейшееДействие1", Перечисления.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.НеТребуется);
	ВозвращаемоеЗначение.Вставить("ДальнейшееДействие2", Перечисления.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.НеТребуется);
	ВозвращаемоеЗначение.Вставить("ДальнейшееДействие3", Перечисления.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.НеТребуется);
	
	Индекс = 1;
	Для Каждого ДальнейшееДействие Из ДальнейшиеДействия Цикл
		ВозвращаемоеЗначение["ДальнейшееДействие" + Индекс] = ДальнейшееДействие;
		Индекс = Индекс + 1;
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

#КонецЕсли