
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.ОтборСписка) Тогда
		УстановитьОтборыДинамическогоСписка(Параметры.ОтборСписка);
	КонецЕсли;
	Если Не ПустаяСтрока(Параметры.Код) Тогда
		КлючЗаписи = РегистрыСведений.ПереченьСпециальныхОбстоятельствДокументаЭДО.СоздатьКлючЗаписи(
			Новый Структура("Код", Параметры.Код));
		Элементы.Список.ТекущаяСтрока = КлючЗаписи;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ПараметрЗакрытия = Новый Структура("Код, Наименование");
	ЗаполнитьЗначенияСвойств(ПараметрЗакрытия, Элемент.ТекущиеДанные);
	Если ЗначениеЗаполнено(ПараметрЗакрытия.Код) Тогда
		Закрыть(ПараметрЗакрытия);
	Иначе
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборыДинамическогоСписка(Отбор)
	Для Каждого ПараметрОтбора Из Отбор Цикл
		ИмяПоля = ПараметрОтбора.Ключ;
		Если ПараметрОтбора.Значение.Свойство("ВидСравнения") Тогда
			ВидСравненияОтбора = ПараметрОтбора.Значение.ВидСравнения;
		Иначе
			ВидСравненияОтбора = Неопределено;
		КонецЕсли;
		ПравоеЗначение = ПараметрОтбора.Значение.ЗначениеОтбора;
		Если Не ПустаяСтрока(ПравоеЗначение) Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список, ИмяПоля, ПравоеЗначение, ВидСравненияОтбора);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти