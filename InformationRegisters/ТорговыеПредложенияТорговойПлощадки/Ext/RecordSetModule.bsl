#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	
	ОбновитьСтатистикуКоличестваОбъектовПоКатегориям(Замещение);
	
	Для Каждого ЗаписьНабора Из ЭтотОбъект Цикл
		Если ЗаписьНабора.КратностьУпаковки = 0 Тогда
			ЗаписьНабора.КратностьУпаковки = 1;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьСтатистикуКоличестваОбъектовПоКатегориям(Замещение)

	Если НЕ ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.РаботаСНоменклатурой") Тогда
		Возврат;
	ИначеЕсли ДополнительныеСвойства.Свойство("ОтключитьОбновлениеСтатистики") Тогда
		Возврат;
	КонецЕсли; 
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбщийМодульРаботаСНоменклатуройСлужебный = ОбщегоНазначения.ОбщийМодуль("РаботаСНоменклатуройСлужебный");
	ТаблицаОтличий = ТорговыеПредложенияСлужебный.ТаблицаОтличийДляСтатистики();
	
	Для каждого Запись Из ЭтотОбъект Цикл
		
		Если Запись.ОтключитьПубликацию Тогда
			Продолжить;
		КонецЕсли; 
		
		НоваяСтрока = ТаблицаОтличий.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Запись);
		НоваяСтрока.Количество = 1;
	
	КонецЦикла; 
	
	Если Замещение Тогда
	
		ПредыдущийНаборЗаписей = РегистрыСведений.ТорговыеПредложенияТорговойПлощадки.СоздатьНаборЗаписей();
		
		Для каждого ЭлементОтбора Из Отбор Цикл
			ЗаполнитьЗначенияСвойств(ПредыдущийНаборЗаписей.Отбор[ЭлементОтбора.Имя], ЭлементОтбора);
		КонецЦикла;
		
		ПредыдущийНаборЗаписей.Прочитать();
		
		Для каждого Запись Из ПредыдущийНаборЗаписей Цикл
		
			Если Запись.ОтключитьПубликацию Тогда
				Продолжить;
			КонецЕсли; 
			
			НоваяСтрока = ТаблицаОтличий.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Запись);
			НоваяСтрока.Количество = -1;
		
		КонецЦикла; 
	
	КонецЕсли; 
	
	ОбщийМодульРаботаСНоменклатуройСлужебный.СкорректироватьСтатистику(ТаблицаОтличий);
	
КонецПроцедуры

#КонецОбласти

#Иначе
	
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
	
#КонецЕсли