#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Очищает напоминания текущего пользователя.
//
Процедура ОчиститьНапоминанияТекущегоПользователя() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Пользователь.Установить(Пользователи.ТекущийПользователь());
	Набор.Записать();
	
КонецПроцедуры

// Откладывает напоминания текущего пользователя на указанное время.
//
// Параметры:
//  ВремяВСекундах - Число - количество секунд, насколько от текущего времени отложить напоминания пользователя.
//
Процедура ОтложитьНапоминанияТекущегоПользователя(ВремяВСекундах) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Пользователь.Установить(ТекущийПользователь);
	
	Запись = Набор.Добавить();
	Запись.Пользователь    = ТекущийПользователь;
	Запись.СрокНапоминания = ТекущаяУниверсальнаяДата() + ВремяВСекундах;
	
	Набор.Записать();
	
КонецПроцедуры

// Срок напоминания текущего пользователя.
// 
// Возвращаемое значение:
//  Дата - Срок напоминания текущего пользователя
Функция СрокНапоминанияТекущегоПользователя() Экспорт
	
	СрокНапоминания = Дата(1,1,1);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СрокНапоминания
		|ИЗ
		|	РегистрСведений.НапоминанияПользователяИСМП
		|ГДЕ
		|	Пользователь = &Пользователь";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователи.АвторизованныйПользователь());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		СрокНапоминания = Выборка.СрокНапоминания;
	КонецЕсли;
	
	Возврат СрокНапоминания;
	
КонецФункции

#КонецОбласти

#КонецЕсли