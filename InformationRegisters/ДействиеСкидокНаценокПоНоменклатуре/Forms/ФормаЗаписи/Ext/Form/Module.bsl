
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Запись, ЭтотОбъект);
	
	ДанныеФормы = Новый Структура;
	ДанныеФормы.Вставить("Источник");
	ДанныеФормы.Вставить("Номенклатура");
	ДанныеФормы.Вставить("Характеристика");
	
	Если Параметры.Ключ.Источник <> Неопределено Тогда
		ДанныеФормы.Источник = Параметры.Ключ.Источник;
	КонецЕсли;
	Если Параметры.ЗначенияЗаполнения.Свойство("Источник")
		И Параметры.ЗначенияЗаполнения.Источник <> Неопределено Тогда
		ДанныеФормы.Источник = Параметры.ЗначенияЗаполнения.Источник;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Ключ.Номенклатура) Тогда
		ДанныеФормы.Номенклатура = Параметры.Ключ.Номенклатура;
	КонецЕсли;
	Если Параметры.ЗначенияЗаполнения.Свойство("Номенклатура")
		И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Номенклатура) Тогда
		ДанныеФормы.Номенклатура = Параметры.ЗначенияЗаполнения.Номенклатура;
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.Ключ.Характеристика) Тогда
		ДанныеФормы.Характеристика = Параметры.Ключ.Характеристика;
	КонецЕсли;
	Если Параметры.ЗначенияЗаполнения.Свойство("Характеристика")
		И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Характеристика) Тогда
		ДанныеФормы.Характеристика = Параметры.ЗначенияЗаполнения.Характеристика;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеФормы.Источник) Тогда
		Элементы.Источник.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеФормы.Номенклатура) Тогда
		Элементы.Номенклатура.ТолькоПросмотр = Истина;
		Элементы.Характеристика.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Если Элементы.Источник.ТолькоПросмотр И Элементы.Номенклатура.ТолькоПросмотр Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРедактированиеЗапрещено;
		Элементы.ХарактеристикаГиперссылка.Видимость = ЗначениеЗаполнено(ДанныеФормы.Характеристика);
		Элементы.Характеристика.Видимость = ЗначениеЗаполнено(ДанныеФормы.Характеристика);
	КонецЕсли;
	
	Если Элементы.Источник.ТолькоПросмотр Тогда
		
		Если ТипЗнч(ДанныеФормы.Источник) = Тип("СправочникСсылка.СкидкиНаценки") Тогда
			Элементы.ИсточникГиперссылка.Заголовок = НСтр("ru = 'Скидка (наценка)'");
		ИначеЕсли ТипЗнч(ДанныеФормы.Источник) = Тип("СправочникСсылка.УсловияПредоставленияСкидокНаценок") Тогда
			Элементы.ИсточникГиперссылка.Заголовок = НСтр("ru = 'Условие предоставления'");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	МассивИсточники = Новый Массив;
	МассивИсточники.Добавить(Запись.Источник);
	
	Параметр = Новый Структура;
	Параметр.Вставить("Источник", МассивИсточники);
	
	Оповестить("Запись_ДействиеСкидокНаценокПоНоменклатуре", Параметр, ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не ЗначениеЗаполнено(ТекущийОбъект.Ответственный) Тогда
		ТекущийОбъект.Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти