#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	МассивНепроверяемыхРеквизитов.Добавить("ДатаОбязательнойМаркировки");
	МассивНепроверяемыхРеквизитов.Добавить("ДатаОбязательногоВключенияРазрешительногоРежима");
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	МассивТГОбязательнойОнлайнПроверки = ОбщегоНазначенияИСМПКлиентСерверПовтИсп.ВидыПродукцийОбязательнойОнлайнПроверкиПередРозничнойПродажей();
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		Если Не Запись.ВестиУчетПродукции Тогда
			Продолжить;
		КонецЕсли;
		
		Если Запись.ДатаОбязательнойМаркировки = '00010101' Тогда
			ТекстОшибки = НСтр("ru = 'Для маркируемой продукции ""%1"" не указана дата обязательной маркировки'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, Запись.ВидПродукции);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,, "ДатаОбязательнойМаркировки", "Запись", Отказ);
		КонецЕсли;
		
		Если Запись.ДатаОбязательногоВключенияРазрешительногоРежима = '00010101'
			И МассивТГОбязательнойОнлайнПроверки.Найти(Запись.ВидПродукции) <> Неопределено Тогда
			ТекстОшибки = НСтр("ru = 'Для маркируемой продукции ""%1"" не указана дата включения обязательного разрешительного режима розничной проверки'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, Запись.ВидПродукции);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,, "ДатаОбязательногоВключенияРазрешительногоРежима", "Запись", Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеУчитываемыеТГОбязательнойОнлайнПроверки = ОбщегоНазначенияИСМПВызовСервера.СписокУчитываемойПродукцииТребующейОбязательнойПроверкиПриПродажеВРозницу();
	
	Если ТекущиеУчитываемыеТГОбязательнойОнлайнПроверки.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	МассивТГОбязательнойОнлайнПроверки             = ОбщегоНазначенияИСМПКлиентСерверПовтИсп.ВидыПродукцийОбязательнойОнлайнПроверкиПередРозничнойПродажей();
	ЗапуститьПервоеВключениеТГОбязательнойПроверки = Ложь;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		Если МассивТГОбязательнойОнлайнПроверки.Найти(Запись.ВидПродукции) <> Неопределено И Запись.ВестиУчетПродукции Тогда
			
			ЗапуститьПервоеВключениеТГОбязательнойПроверки = Истина;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗапуститьПервоеВключениеТГОбязательнойПроверки Тогда
		ДополнительныеСвойства.Вставить("ЗапуститьФоновоеОбновлениеНастроекРазрешительногоРежима", Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли