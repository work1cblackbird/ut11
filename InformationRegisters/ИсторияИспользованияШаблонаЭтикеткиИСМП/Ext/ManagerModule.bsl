#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ИзменениеДанных

Функция ИнициализироватьТаблицуДляЗаписиДанных() Экспорт
	
	ТаблицаДляЗаписиДанных = Новый ТаблицаЗначений;
	ТаблицаДляЗаписиДанных.Колонки.Добавить("Номенклатура",     Метаданные.ОпределяемыеТипы.Номенклатура.Тип);
	ТаблицаДляЗаписиДанных.Колонки.Добавить("Характеристика",   Метаданные.ОпределяемыеТипы.ХарактеристикаНоменклатуры.Тип);
	ТаблицаДляЗаписиДанных.Колонки.Добавить("GTIN",             Метаданные.ОпределяемыеТипы.GTIN.Тип);
	ТаблицаДляЗаписиДанных.Колонки.Добавить("Шаблон",           Новый ОписаниеТипов("ПеречислениеСсылка.ШаблоныКодовМаркировкиСУЗ"));
	ТаблицаДляЗаписиДанных.Колонки.Добавить("ШаблонЭтикетки",   Метаданные.ОпределяемыеТипы.ШаблонЭтикеткиИС.Тип);
	
	Возврат ТаблицаДляЗаписиДанных;
	
КонецФункции

Процедура ЗаписатьДанные(ТаблицаДляЗаписиДанных) Экспорт
	
	Если ТаблицаДляЗаписиДанных.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	БлокировкаЗаписей = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаЗаписей.Добавить("РегистрСведений.ИсторияИспользованияШаблонаЭтикеткиИСМП");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ТаблицаДляЗаписиДанных;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура",   "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика", "Характеристика");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("GTIN",           "GTIN");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Шаблон",         "Шаблон");
	
	НачатьТранзакцию();
	
	Попытка
		
		БлокировкаЗаписей.Заблокировать();
		
		Для Каждого СтрокаДанных Из ТаблицаДляЗаписиДанных Цикл
			Запись = РегистрыСведений.ИсторияИспользованияШаблонаЭтикеткиИСМП.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Запись, СтрокаДанных);
			Запись.Записать();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли