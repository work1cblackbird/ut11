
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("ЗаполнениеСкладаТорговойПлощадки") Тогда
		РежимФормы = "ЗаполнениеСкладаТорговойПлощадки";
		Заголовок = НСтр("ru = 'Склад торговой площадки'");

		Элементы.УчетнаяЗаписьМаркетплейса.ТолькоПросмотр = Истина;
		Элементы.ВидОбъектаМаркетплейса.Видимость = Ложь;
		Элементы.ИдентификаторВладельцаОбъектаМаркетплейса.Видимость = Ложь;
		Элементы.Объект1С.Видимость = Ложь;
		Элементы.ДатаАктуальности.Видимость = Ложь;

		Элементы.Переместить(Элементы.НаименованиеОбъектаМаркетплейса, ЭтотОбъект, Элементы.ИдентификаторОбъектаМаркетплейса);
		Элементы.НаименованиеОбъектаМаркетплейса.Заголовок = НСтр("ru = 'Наименование склада'");
		Элементы.ИдентификаторОбъектаМаркетплейса.Заголовок = НСтр("ru = 'Идентификатор склада'");
		Элементы.ИдентификаторОбъектаМаркетплейса.АвтоОтметкаНезаполненного = Истина;
		Элементы.ДополнительныеСведения.Заголовок = НСтр("ru = 'Адрес склада, дополнительные сведения'");

		Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
			Запись.ИдентификаторВладельцаОбъектаМаркетплейса = "FBO";
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	ПараметрыЗаписи.Вставить("ЭтоНовый", Запись.ИсходныйКлючЗаписи.Пустой());
	ЗаполнитьИдентификаторОбъекта();

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ЭтоНовый", ПараметрыЗаписи.ЭтоНовый);
	ПараметрыОповещения.Вставить("Запись",   Запись);

	Оповестить("ЗаписаноСоответствиеОбъектовТорговойПлощадки", ПараметрыОповещения, ВладелецФормы);

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеОбъектаМаркетплейсаПриИзменении(Элемент)

	ЗаполнитьИдентификаторОбъекта();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьИдентификаторОбъекта()

	Если РежимФормы = "ЗаполнениеСкладаТорговойПлощадки" И ПустаяСтрока(Запись.ИдентификаторОбъектаМаркетплейса) Тогда
		Запись.ИдентификаторОбъектаМаркетплейса = СтрЗаменить(Запись.НаименованиеОбъектаМаркетплейса, " ", "");
	КонецЕсли;

КонецПроцедуры

#КонецОбласти