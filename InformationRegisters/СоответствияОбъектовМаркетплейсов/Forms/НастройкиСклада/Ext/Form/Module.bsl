
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ИдентификаторСклада = Параметры.ИдентификаторСклада;
	УчетнаяЗапись       = Параметры.УчетнаяЗапись;
	Партнер             = Параметры.Партнер;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиСклада(Параметры.УчетнаяЗапись, Параметры.ИдентификаторСклада));
	
	ЭлементыДляЗаполнения = Новый Массив;
	ЭлементыДляЗаполнения.Добавить(Элементы.ВремяДоставкиС);
	ЭлементыДляЗаполнения.Добавить(Элементы.ВремяДоставкиПо);
	ДоставкаТоваровКлиентСервер.ЗаполнитьСписокВыбораПоляВремени(ЭлементыДляЗаполнения);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	НастроитьОтображениеЭлементов();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособМестоДоставкиПеревозчикПриИзменении(Элемент)

	НастроитьОтображениеЭлементов();
	ЗаполнитьУстановитьРеквизитыДоставкиСервер(Элемент.Имя);

КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиПеревозчикаПриИзменении(Элемент)

	ИменаРеквизитовАдресовДоставки = ДоставкаТоваровКлиентСервер.ИменаРеквизитовАдресовДоставки("АдресДоставкиПеревозчика");
	
	ДоставкаТоваровКлиент.ПриИзмененииПредставленияАдреса(
		Элемент,
		ЭтотОбъект[ИменаРеквизитовАдресовДоставки.ИмяРеквизитаАдресаДоставки],
		ЭтотОбъект[ИменаРеквизитовАдресовДоставки.ИмяРеквизитаАдресаДоставкиЗначенияПолей]);

КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиПеревозчикаОчистка(Элемент, СтандартнаяОбработка)

	ИменаРеквизитовАдресовДоставки = ДоставкаТоваровКлиентСервер.ИменаРеквизитовАдресовДоставки("АдресДоставкиПеревозчика");
	
	ДоставкаТоваровКлиент.ОчиститьПредставлениеАдреса(
		ЭтотОбъект[ИменаРеквизитовАдресовДоставки.ИмяРеквизитаАдресаДоставки],
		ЭтотОбъект[ИменаРеквизитовАдресовДоставки.ИмяРеквизитаАдресаДоставкиЗначенияПолей],
		ЭтотОбъект[ИменаРеквизитовАдресовДоставки.ИмяРеквизитаАдресаДоставкиЗначение]);

КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиПеревозчикаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ИменаРеквизитовАдресовДоставки = ДоставкаТоваровКлиентСервер.ИменаРеквизитовАдресовДоставки("АдресДоставкиПеревозчика");
	
	ДоставкаТоваровКлиент.ОткрытьФормуВыбораАдресаИОбработатьРезультат(
		Элемент,
		ЭтотОбъект,
		ИменаРеквизитовАдресовДоставки,
		СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиПеревозчикаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	АдресДоставкиЗначение = "";
	АдресДоставкиПеревозчикаЗначение = "";
	
	АдресДоставкиПеревозчикаПриИзменении(Элемент);
	
	ДоставкаТоваровКлиент.АдресДоставкиОбработкаВыбора(Элементы, ЭтотОбъект, Элемент.Имя, ВыбранноеЗначение);
	
	Модифицированность = Истина;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)

	Отказ = Ложь;
	ЗаписатьНастройкиНаСервере(Отказ);
	
	Если Не Отказ Тогда
		Оповестить("ЗаписаныНастройкиСкладаFBS", ИдентификаторСклада, УчетнаяЗапись);
		Закрыть();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция НастройкиСклада(УчетнаяЗапись, ИдентификаторСклада)

	Возврат ИнтеграцияСМаркетплейсамиСервер.НастройкиСклада(УчетнаяЗапись, ИдентификаторСклада);

КонецФункции

&НаКлиенте
Процедура НастроитьОтображениеЭлементов()

	Если СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.СиламиПеревозчика") Тогда
		Элементы.СтраницыПеревозчик.ТекущаяСтраница = Элементы.СтраницаПеревозчик;
		Элементы.СтраницыДоставки.ТекущаяСтраница = Элементы.СтраницаАдресДоставкиПусто;
	ИначеЕсли СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.СиламиПеревозчикаДоПунктаПередачи")
				Или СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.СиламиПеревозчикаПоАдресу") Тогда
		Элементы.СтраницыПеревозчик.ТекущаяСтраница = Элементы.СтраницаПеревозчик;
		Элементы.СтраницыДоставки.ТекущаяСтраница = Элементы.СтраницаДоставкаПеревозчикомСокращенно;
	ИначеЕсли СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.ДоКлиентаКурьером") Тогда
		Элементы.СтраницыПеревозчик.ТекущаяСтраница = Элементы.СтраницаПеревозчик;
		Элементы.СтраницыДоставки.ТекущаяСтраница = Элементы.СтраницаАдресДоставкиПусто;
	ИначеЕсли СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.ДоКлиентаКурьером") Тогда
		Элементы.СтраницыПеревозчик.ТекущаяСтраница = Элементы.СтраницаКурьер;
		Элементы.СтраницыДоставки.ТекущаяСтраница = Элементы.СтраницаАдресДоставкиПусто;
	Иначе
		Элементы.СтраницыПеревозчик.ТекущаяСтраница = Элементы.СтраницаПеревозчикПусто;
		Элементы.СтраницыДоставки.ТекущаяСтраница = Элементы.СтраницаАдресДоставкиПусто;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУстановитьРеквизитыДоставкиСервер(ИмяЭлементаФормы);

	ДоставкаТоваров.ЗаполнитьРеквизитыДоставки(Элементы, ИмяЭлементаФормы, ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкиНаСервере(Отказ)

	Результат = ИнтеграцияСМаркетплейсамиСервер.СохранитьНастройкиСклада(УчетнаяЗапись, ИдентификаторСклада, ЭтотОбъект);
	
	Если Не Результат.Выполнено Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Не ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
		ОбщегоНазначения.СообщитьПользователю(Результат.ОписаниеОшибки);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти