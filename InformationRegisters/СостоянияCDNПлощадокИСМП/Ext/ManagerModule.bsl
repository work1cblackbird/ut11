
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает разделение адреса площадки на сервер, порт и защищенное соединение
// 
// Параметры:
//  АдресПлощадки - Строка - полный адрес площадки, http://....ru
// 
// Возвращаемое значение:
//  Структура - структура адреса:
// * Сервер - Строка - сервер, данные без http:// и порта
// * Порт - Число - порт
// * ЗащищенноеСоединение - Булево - Истина, если https://
Функция РазобратьАдресПлощадкиНаСерверИПорт(Знач АдресПлощадки) Экспорт
	
	СтруктураВозврата = Новый Структура();
	СтруктураВозврата.Вставить("Сервер",         "");
	СтруктураВозврата.Вставить("Порт",                 0);
	СтруктураВозврата.Вставить("ЗащищенноеСоединение", Ложь);
	
	УдалитьНачалоСтрокиПодключения = Ложь;
	
	Если СтрНачинаетсяС(ВРег(АдресПлощадки), "HTTPS://") Тогда
		
		СтруктураВозврата.ЗащищенноеСоединение = Истина;
		УдалитьНачалоСтрокиПодключения         = Истина;
		
	ИначеЕсли СтрНачинаетсяС(ВРег(АдресПлощадки), "HTTP://") Тогда
		
		СтруктураВозврата.ЗащищенноеСоединение = Ложь;
		УдалитьНачалоСтрокиПодключения         = Истина;
		
	КонецЕсли;
	
	Если УдалитьНачалоСтрокиПодключения Тогда
		
		Если СтруктураВозврата.ЗащищенноеСоединение Тогда
			АдресПлощадки = Прав(АдресПлощадки, СтрДлина(АдресПлощадки) - 8);
		Иначе
			АдресПлощадки = Прав(АдресПлощадки, СтрДлина(АдресПлощадки) - 7);
		КонецЕсли;
		
	КонецЕсли;
	
	МассивСлов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(АдресПлощадки, ":");
	
	Если МассивСлов.Количество() > 1 Тогда
		
		ОписаниеТипаЧисло = ОбщегоНазначения.ОписаниеТипаЧисло(0);
		СтруктураВозврата.Порт = ОписаниеТипаЧисло.ПривестиЗначение(МассивСлов[МассивСлов.Количество() - 1]);
		
		МассивСлов.Удалить(МассивСлов.Количество() - 1);
		
		СтруктураВозврата.Сервер = СтрСоединить(МассивСлов, ":");
	
	ИначеЕсли МассивСлов.Количество() = 1 Тогда
		
		СтруктураВозврата.Сервер = АдресПлощадки;
		СтруктураВозврата.Порт   = 443;
		
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

#КонецОбласти

#КонецЕсли
