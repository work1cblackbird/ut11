
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ПриЧтенииСозданииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	УстановитьВидимостьДоступность();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступность()
	
	ТекущаяДата = ТекущаяУниверсальнаяДата();
	
	Если Запись.ДатаНедоступнаДоУниверсальная < ТекущаяДата Тогда
		
		Элементы.СтраницыСтатусПлощадки.ТекущаяСтраница = Элементы.СтраницаПлощадкаДоступна;
		Активна                                         = Истина;
		
	Иначе
		
		Элементы.СтраницыСтатусПлощадки.ТекущаяСтраница = Элементы.СтраницаПлощадкаЗаблокирована;
		
		ШаблонНадписи = НСтр("ru = 'Площадка недоступна до %1.'");
		
		Если НачалоДня(Запись.ДатаНедоступнаДоУниверсальная) = НачалоДня(ТекущаяДата) Тогда
			ДатаНедоступности = Формат(МестноеВремя(Запись.ДатаНедоступнаДоУниверсальная), "ДФ=HH:mm:ss;");
		Иначе
			ДатаНедоступности = Формат(МестноеВремя(Запись.ДатаНедоступнаДоУниверсальная), "ДФ='dd.MM.yyyy HH:mm:ss';");
		КонецЕсли;
		
		Элементы.НадписьСтатусПлощадкиПлощадкиНедоступна.Заголовок = СтрШаблон(ШаблонНадписи, Формат(ДатаНедоступности));
		Активна                                                    = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
