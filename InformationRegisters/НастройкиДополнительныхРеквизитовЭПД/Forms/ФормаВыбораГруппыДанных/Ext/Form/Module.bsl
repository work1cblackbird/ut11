
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ТипДокумента", ТипДокумента);
	
	ДеревоЗначений = ОбменСГИСЭПД.ДеревоГруппДанныхДляДополнительныхПолей(ТипДокумента);
	
	ЗначениеВРеквизитФормы(ДеревоЗначений, "Дерево");
	
	ТекущаяГруппа = Неопределено;
	Параметры.Свойство("ГруппаДанных", ТекущаяГруппа);
	
	Если ТекущаяГруппа <> Неопределено Тогда
		СтрокаДерева = НайтиСтрокуДереваРекурсивно(Дерево, "Путь", ТекущаяГруппа);
		Если СтрокаДерева <> Неопределено Тогда
			Элементы.Дерево.ТекущаяСтрока = СтрокаДерева.ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьТехническиеПоля(Команда)
	
	Элементы.ДеревоПуть.Видимость = Не Элементы.ДеревоПуть.Видимость;
	Элементы.ДеревоУзел.Видимость = Не Элементы.ДеревоУзел.Видимость;
	
	Элементы.ДеревоПоказатьТехническиеПоля.Заголовок = ?(Элементы.ДеревоПуть.Видимость, 
		НСтр("ru='Скрыть технические поля'"), 
		НСтр("ru='Показать технические поля'"));
		
КонецПроцедуры


&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыбраннаяСтрока = Элементы.Дерево.ВыделенныеСтроки.Получить(0);
	
	ТекущиеДанные = Дерево.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	Результат = Новый Структура;
	Результат.Вставить("ГруппаДанных", ТекущиеДанные.Путь);
	Результат.Вставить("Узел", ТекущиеДанные.Узел);
	
	Закрыть(Результат);
	
КонецПроцедуры


&НаКлиенте
Процедура ДеревоВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Дерево.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	Результат = Новый Структура;
	Результат.Вставить("ГруппаДанных", ТекущиеДанные.Путь);
	Результат.Вставить("Узел", ТекущиеДанные.Узел);
	
	Закрыть(Результат);

КонецПроцедуры

&НаСервере
Функция НайтиСтрокуДереваРекурсивно(ДеревоРодитель, ИмяПоля, Значение)
	
	Для Каждого СтрокаДерева Из ДеревоРодитель.ПолучитьЭлементы() Цикл
		Если СтрокаДерева[ИмяПоля] = Значение Тогда
			Возврат СтрокаДерева;	
		Иначе
			НайденнаяСтрока = НайтиСтрокуДереваРекурсивно(СтрокаДерева, ИмяПоля, Значение);
			Если НайденнаяСтрока <> Неопределено Тогда
				Возврат НайденнаяСтрока;
			КонецЕсли;		
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции
