
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Номенклатура = Параметры.Номенклатура;
	Характеристика = Параметры.Характеристика;
	Назначение = Параметры.Назначение;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетДефицитовПлановПоЭтапам.Дефицит КАК Дефицит,
	|	РасчетДефицитовПлановПоЭтапам.Приход КАК Приход	
	|ИЗ
	|	РегистрСведений.РасчетДефицитовПлановПоЭтапам КАК РасчетДефицитовПлановПоЭтапам
	|ГДЕ
	|	РасчетДефицитовПлановПоЭтапам.Характеристика = &Характеристика
	|	И РасчетДефицитовПлановПоЭтапам.Номенклатура = &Номенклатура
	|	И РасчетДефицитовПлановПоЭтапам.Назначение = &Назначение
	|	И РасчетДефицитовПлановПоЭтапам.ВидПлана = &ВидПлана
	|	И РасчетДефицитовПлановПоЭтапам.ПериодПланирования = &ПериодПланирования";
	Запрос.УстановитьПараметр("Номенклатура", Параметры.Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Параметры.Характеристика);
	Запрос.УстановитьПараметр("Назначение", Параметры.Назначение);
	Запрос.УстановитьПараметр("ВидПлана", Параметры.ВидПлана);
	Запрос.УстановитьПараметр("ПериодПланирования", Параметры.Период);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Потребность = Выборка.Дефицит + Выборка.Приход;
	Иначе
		//Отладка, когда не расчиталось можно взять потребность с этапа
		Потребность = 0;
	КонецЕсли;
	
	Если ТипЗнч(Параметры.План) = Тип("ДокументСсылка.ПланЗакупок") Тогда
		Элементы.Обеспечить.Заголовок = НСтр("ru='Закупить'");
		Заголовок = НСтр("ru='Корректировка плана закупки'");
	ИначеЕсли ТипЗнч(Параметры.План) = Тип("ДокументСсылка.ПланСборкиРазборки") Тогда
		Элементы.Обеспечить.Заголовок = НСтр("ru='Собрать'");
		Заголовок = НСтр("ru='Корректировка плана сборки (разборки'");
	Иначе
		Элементы.Обеспечить.Заголовок = НСтр("ru='Произвести'");
	КонецЕсли;
		
	
	Обеспечить = Параметры.Обеспечить;
	
	Комментарий = Параметры.Комментарий;
	Если Параметры.ЗаданИнтервалПотребности Тогда
		ИнтервалПотребностиОт = Параметры.ИнтервалПотребностиОт;
		ИнтервалПотребностиДо = Параметры.ИнтервалПотребностиДо;
		ОграничитьИнтервалОт = ЗначениеЗаполнено(ИнтервалПотребностиОт);
		ОграничитьИнтервалДо = ЗначениеЗаполнено(ИнтервалПотребностиДо);
	Иначе
		ИнтервалПотребностиОт = Потребность;
		ИнтервалПотребностиДо = Потребность;
		ОграничитьИнтервалДо = Истина;
		ОграничитьИнтервалОт = Истина;
	КонецЕсли;
	
	ОбновитьДоступностьЭлементов(ЭтаФорма);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДоступностьЭлементов(Форма)
	
	Форма.Элементы.ИнтервалПотребностиОт.Доступность = Форма.ОграничитьИнтервалОт;
	Форма.Элементы.ИнтервалПотребностиДо.Доступность = Форма.ОграничитьИнтервалДо;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбеспечитьПриИзменении(Элемент)
	
	Если Обеспечить = 0 Тогда
		ОграничитьИнтервалОт = Ложь;
		ОграничитьИнтервалДо = Ложь;
	КонецЕсли;
	
	Если ОграничитьИнтервалОт Тогда
		ИнтервалПотребностиОт = Мин(Обеспечить, ИнтервалПотребностиОт);
	КонецЕсли;
	Если ОграничитьИнтервалДо Тогда
			ИнтервалПотребностиДо = Макс(Обеспечить,ИнтервалПотребностиДо);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалПотребностиОтПриИзменении(Элемент)
	ОграничитьИнтервалОт = ЗначениеЗаполнено(ИнтервалПотребностиОт);
	ОбновитьДоступностьЭлементов(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалПотребностиДоПриИзменении(Элемент)
	ОграничитьИнтервалДо = ЗначениеЗаполнено(ИнтервалПотребностиДо);
	ОбновитьДоступностьЭлементов(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОграничитьИнтервалДоПриИзменении(Элемент)
	ОбновитьДоступностьЭлементов(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОграничитьИнтервалОтПриИзменении(Элемент)
	ОбновитьДоступностьЭлементов(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКорректировку(Команда)
	
	Если Потребность = ИнтервалПотребностиОт 
		И Потребность = ИнтервалПотребностиДо
		И ОграничитьИнтервалОт = Истина
		И ОграничитьИнтервалДо = Истина Тогда
		Закрыть(Неопределено);
	Иначе
		СтруктураНастроек = Новый Структура();
		СтруктураНастроек.Вставить("УстановитьКорректировку", Истина);
		СтруктураНастроек.Вставить("Обеспечить", Обеспечить);
		СтруктураНастроек.Вставить("ИнтервалПотребностиОт", ?(ОграничитьИнтервалОт, ИнтервалПотребностиОт, 0));
		СтруктураНастроек.Вставить("ИнтервалПотребностиДо",?(ОграничитьИнтервалДо, ИнтервалПотребностиДо, 0));
		СтруктураНастроек.Вставить("Комментарий", Комментарий);
		
		Закрыть(СтруктураНастроек);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьКорректировку(Команда)
	СтруктураНастроек = Новый Структура();
	СтруктураНастроек.Вставить("УстановитьКорректировку", Ложь);
	
	Закрыть(СтруктураНастроек);
КонецПроцедуры
