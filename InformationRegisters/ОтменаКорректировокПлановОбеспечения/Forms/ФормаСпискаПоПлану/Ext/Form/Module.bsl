
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.План) Тогда
		
		План = Параметры.План; 
		АвтоЗаголовок = Ложь;
		
		Заголовок = СтрШаблон(
			НСтр("ru = 'Отмененные корректировки по плану: %1'"),
			Параметры.План);
		
	КонецЕсли;
	
	Элементы.Назначение.Видимость = Параметры.ПоказыватьНазначение;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", ВыбраннаяСтрока);
	ПараметрыФормы.Вставить("ПоказыватьНазначение", Элементы.Назначение.Видимость);
	
	ОткрытьФорму(
		"РегистрСведений.ОтменаКорректировокПлановОбеспечения.Форма.ФормаЗаписиПросмотр",
		ПараметрыФормы,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоммндФормы

&НаКлиенте
Процедура ОчиститьСписок(Команда)
	ОчиститьСписокНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОчиститьСписокНаСервере()
	
	НаборЗаписей = РегистрыСведений.ОтменаКорректировокПлановОбеспечения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.План.Установить(План);
	НаборЗаписей.Записать();
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти
