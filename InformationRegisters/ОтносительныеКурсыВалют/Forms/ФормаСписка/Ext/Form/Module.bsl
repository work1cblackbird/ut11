#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДоступноИзменениеВалют = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ОтносительныеКурсыВалют);
	
	Если ДоступноИзменениеВалют Тогда
		Элементы.СписокЗагрузитьИзТаблицы.Видимость = Истина;
		Элементы.ФормаЗагрузитьКурсыВалютЦБРФ.Видимость = Метаданные.Обработки.Найти("ЗагрузкаКурсовВалют") <> Неопределено;
		Элементы.ФормаЗагрузитьКурсыВалютЕЦБ.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьЗагрузкуКурсовВалютЕЦБ");
	Иначе
		Элементы.СписокГруппаЗагрузить.Видимость = Ложь;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют") Тогда
		ОтборБазоваяВалюта = Справочники.Валюты.ПолучитьВалютуПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ЗагрузкаОтносительныхКурсовВалют"
		Или ИмяСобытия = "Запись_ЗагрузкаКурсовВалют" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьИзТаблицы(Команда)
	
	ОткрытьФорму("РегистрСведений.ОтносительныеКурсыВалют.Форма.ФормаЗагрузки", , ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборБазоваяВалютаПриИзменении(Элемент)
	УстановитьОтборСписка(ОтборБазоваяВалюта, "БазоваяВалюта")
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборСписка(Отбор, ИмяПоля)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		ИмяПоля,
		Отбор,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(Отбор));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьКурсыВалютЕЦБ(Команда)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОткрытиеИзСписка", Истина);
	ОткрытьФорму("Обработка.ЗагрузкаКурсовВалютЕЦБ.Форма.Форма", ПараметрыФормы, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьКурсыВалютЦБРФ(Команда)
	
	ИмяФормыОткрытия = ИмяФормыЗагрузкиВалютЦБРФ();
	
	Если Не ПустаяСтрока(ИмяФормыОткрытия) Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ОткрытиеИзСписка", Истина);
		ОткрытьФорму(ИмяФормыОткрытия, ПараметрыФормы, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ИмяФормыЗагрузкиВалютЦБРФ()
	
	ИмяФормыОбработки = "";
	
	МетаданныеОбработка = Метаданные.Обработки.Найти("ЗагрузкаКурсовВалют");
	Если МетаданныеОбработка <> Неопределено Тогда
		ИмяФормыОбработки = МетаданныеОбработка.ОсновнаяФорма.ПолноеИмя();
	КонецЕсли;
	
	Возврат ИмяФормыОбработки;
	
КонецФункции

#КонецОбласти