
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Элементы.ВыполнитьПоискАссоциаций.Видимость  = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НоменклатураПродаваемаяСовместно);
	Элементы.НастройкаПоискаАссоциаций.Видимость = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НоменклатураПродаваемаяСовместно);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ВариантДобавленияОтбор = Настройки.Получить("ВариантДобавленияОтбор");
	ВариантАнализаОтбор = Настройки.Получить("ВариантАнализаОтбор");
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ВариантАнализа", ВариантАнализаОтбор, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(ВариантАнализаОтбор));
	Если ВариантДобавленияОтбор = "" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ДобавленоАвтоматически", Неопределено, ВидСравненияКомпоновкиДанных.Равно,, Ложь);
	ИначеЕсли ВариантДобавленияОтбор = "ДобавленоАвтоматически" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ДобавленоАвтоматически", Истина, ВидСравненияКомпоновкиДанных.Равно,, Истина);
	ИначеЕсли ВариантДобавленияОтбор = "ДобавленоВРучную" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ДобавленоАвтоматически", Ложь, ВидСравненияКомпоновкиДанных.Равно,, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Настроить(Команда)
	
	ОткрытьФорму("РегистрСведений.НоменклатураПродаваемаяСовместно.Форма.НастройкаПоискаАссоциаций", Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПоискАссоциаций(Команда)
	
	ОбновитьДанныеОНоменклатуреПродаваемойСовместно();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантДобавленияПриИзменении(Элемент)
	
	Если ВариантДобавленияОтбор = "" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ДобавленоАвтоматически", Неопределено, ВидСравненияКомпоновкиДанных.Равно,, Ложь);
	ИначеЕсли ВариантДобавленияОтбор = "ДобавленоАвтоматически" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ДобавленоАвтоматически", Истина, ВидСравненияКомпоновкиДанных.Равно,, Истина);
	ИначеЕсли ВариантДобавленияОтбор = "ДобавленоВРучную" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ДобавленоАвтоматически", Ложь, ВидСравненияКомпоновкиДанных.Равно,, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантАнализаОтборПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ВариантАнализа", ВариантАнализаОтбор, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(ВариантАнализаОтбор));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура ОбновитьДанныеОНоменклатуреПродаваемойСовместно()
	
	НоменклатураПродаваемаяСовместно.ОбновитьДанныеОНоменклатуреПродаваемойСовместно();
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
