
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция определяет применение ЕНВД для розничной торговли.
//
// Параметры:
//	Организация - СправочникСсылка.Организации - Организация, для которой определяется применение ЕНВД
//	Склад - СправочникСсылка.Склады - Розничный склад, для которого определяется применение ЕНВД
//	Дата - Дата - Дата определения применения ЕНВД.
//
// Возвращаемое значение:
//	Булево - признак применения ЕНВД в розничной торговле.
//
Функция РозничнаяТорговляОблагаетсяЕНВД(Организация, Склад, Дата) Экспорт
	
	РозничнаяТорговляОблагаетсяЕНВД = Ложь;
	
	//++ Локализация
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ИСТИНА КАК РозничнаяТорговляОблагаетсяЕНВД
	|ИЗ
	|	РегистрСведений.ПримененияЕНВД.СрезПоследних(&Дата,
	|		Организация = &Организация
	|		И Склад = &Склад
	|	) КАК ПримененияЕНВД
	|ГДЕ
	|	ПримененияЕНВД.РозничнаяТорговляОблагаетсяЕНВД
	|");
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	Выборка = запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		РозничнаяТорговляОблагаетсяЕНВД = Выборка.РозничнаяТорговляОблагаетсяЕНВД;
	КонецЕсли;
	//-- Локализация
	
	Возврат РозничнаяТорговляОблагаетсяЕНВД;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Склад)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли
