#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает текущий формат магазина установленного на складе
//
// Параметры:
//  Склад  - СправочникСсылка.Склады - Склад для которого необходимо получить формат
//  НаДату  - Дата - Необязательный. Дата, на которую получается формат, по умолчанию - текущая дата.
//
// Возвращаемое значение:
//   СправочникСсылка.ФорматыМагазинов   - текущий формат магазина для склада.
//
Функция ТекущийФормат(Склад, НаДату = Неопределено) Экспорт 
	
	Если НаДату = Неопределено Тогда
		НаДату = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИсторияИзмененияФорматовМагазиновСрезПоследних.ФорматМагазина
	|ИЗ
	|	РегистрСведений.ИсторияИзмененияФорматовМагазинов.СрезПоследних(&НаДату, Склад = &Склад) КАК ИсторияИзмененияФорматовМагазиновСрезПоследних";
	
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("НаДату", НаДату);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.ФорматМагазина;
	КонецЕсли;
	
	Возврат Справочники.ФорматыМагазинов.ПустаяСсылка();

КонецФункции
 
// Возвращает текущий формат магазина установленного на складе
//
// Параметры:
//  Склад  - СправочникСсылка.Склады - Склад для которого необходимо получить формат
//  НаДату  - Дата - Необязательный. Дата, на которую получается формат, по умолчанию - текущая дата.
//
// Возвращаемое значение:
//   СправочникСсылка.ФорматыМагазинов   - текущий формат магазина для склада.
//
Функция ТекущийНастройкиАссортимента(Склад, НаДату = Неопределено) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НаДату = Неопределено Тогда
		НаДату = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Настройки = Новый Структура;
	Настройки.Вставить("КонтролироватьАссортимент", Ложь);
	Настройки.Вставить("ФорматМагазина", Справочники.ФорматыМагазинов.ПустаяСсылка());
	Настройки.Вставить("РозничныеЦеныИзФорматаМагазина", Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИсторияИзмененияФорматовМагазиновСрезПоследних.ФорматМагазина,
	|	ИсторияИзмененияФорматовМагазиновСрезПоследних.КонтролироватьАссортимент,
	|	ИсторияИзмененияФорматовМагазиновСрезПоследних.РозничныеЦеныИзФорматаМагазина
	|ИЗ
	|	РегистрСведений.ИсторияИзмененияФорматовМагазинов.СрезПоследних(&НаДату, Склад = &Склад) КАК
	|		ИсторияИзмененияФорматовМагазиновСрезПоследних";
	
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("НаДату", НаДату);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Настройки, ВыборкаДетальныеЗаписи);
	КонецЕсли;
	
	Возврат Настройки;

КонецФункции

#КонецОбласти

#КонецЕсли