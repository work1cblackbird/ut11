///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Получает фискальные данные по документу.
//
// Параметры
//  ДокументОперации - ОпределяемыйТип.ДокументОФДБИП - документ созданный по данным сервиса ОФД.
//
// Возвращаемое значение:
//  Массив Из Структура - данные заказа на оплату:
//    *ФискальныйНомер - Число - фискальный номер документа;
//    *НомерСмены - Число - номер смены чека;
//    *НомерЧекаЗаСмену - Число - номер чека за смену;
//    *Касса - ОпределяемыйТип.КассаОФДБИП - касса на которой была выполнена операция;
//    *ЗаводскойНомерФН - Строка - заводской номер фискального накопителя;
//    *РегистрационныйНомерККТ - Строка - регистрационный номер ККТ.
//
Функция ФискальныеДанныеПоДокументу(ДокументОперации) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДокументыОФД.НомерСмены КАК НомерСмены,
		|	ДокументыОФД.НомерЧекаЗаСмену КАК НомерЧекаЗаСмену,
		|	ДокументыОФД.Касса КАК Касса,
		|	ДокументыОФД.ЗаводскойНомерФН КАК ЗаводскойНомерФН,
		|	ДокументыОФД.РегистрационныйНомерККТ КАК РегистрационныйНомерККТ,
		|	ДокументыОФД.ФискальныйНомер КАК ФискальныйНомер
		|ИЗ
		|	РегистрСведений.ДокументыОФД КАК ДокументыОФД
		|ГДЕ
		|	ДокументыОФД.ДокументОперации = &ДокументОперации
		|ИТОГИ ПО
		|	Касса,
		|	ЗаводскойНомерФН";
	
	Запрос.УстановитьПараметр("ДокументОперации", ДокументОперации);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаПоКассе = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПереченьФискальныхДанных = Новый Массив;
	
	Пока ВыборкаПоКассе.Следующий() Цикл
		
		ВыборкаПоФискальномуНомеру = ВыборкаПоКассе.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
		
		Пока ВыборкаПоФискальномуНомеру.Следующий() Цикл
			
			ФискальныеНомераЧеков = Новый Массив;
			
			ДанныеКассы = Новый Структура;
			ДанныеКассы.Вставить("Касса", ВыборкаПоФискальномуНомеру.Касса);
			ДанныеКассы.Вставить("ЗаводскойНомерФН", ВыборкаПоФискальномуНомеру.ЗаводскойНомерФН);
			
			ВыборкаДетальныеЗаписи = ВыборкаПоФискальномуНомеру.Выбрать();
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				
				ФискальныеНомераЧеков.Добавить(ВыборкаДетальныеЗаписи.ФискальныйНомер);
				
			КонецЦикла;
			
			ДанныеКассы.Вставить("ФискальныеНомераЧеков", ФискальныеНомераЧеков);
			
			ПереченьФискальныхДанных.Добавить(ДанныеКассы);
			
			КонецЦикла;
		
	КонецЦикла;
	
	Возврат ПереченьФискальныхДанных;
	
КонецФункции

// Записывает соответствие документов фискальным данным из сервиса ОФД.
//
// Параметры:
//  Касса - ОпределяемыйТип.КассаОФДБИП - касса по которой выполняется запрос.
//  РезультатЗагрузки - Массив из Соответствие - Описывает соответствие документов фискальным данным:
//    * Ключ - ОпределяемыйТип.ДокументОФДБИП - документ созданный по данным сервиса ОФД;
//    * Значение - ТаблицаЗначений - содержит перечень фискальных данных по которым был создан документ.
//      см. ОФД.НовыйОписаниеТаблицыФискальныхДанных
//
Процедура СохранитьДанныеЗагрузки(Касса, РезультатЗагрузки) Экспорт
	
	ДанныеНастроекПоКассе = РегистрыСведений.НастройкиКассОФД.ДанныеНастроекПоКассе(Касса);
	
	Для Каждого ДанныеПоДокументу Из РезультатЗагрузки Цикл
		
		Для Каждого ФискальныеДанные Из ДанныеПоДокументу.Значение Цикл
			
			Запись = РегистрыСведений.ДокументыОФД.СоздатьМенеджерЗаписи();
			Запись.ДокументОперации        = ДанныеПоДокументу.Ключ;
			Запись.НомерСмены              = ФискальныеДанные.НомерСмены;
			Запись.НомерЧекаЗаСмену        = ФискальныеДанные.НомерЧекаЗаСмену;
			Запись.Касса                   = Касса;
			Запись.ЗаводскойНомерФН        = ДанныеНастроекПоКассе.ЗаводскойНомерФН;
			Запись.РегистрационныйНомерККТ = ДанныеНастроекПоКассе.РегистрационныйНомерККТ;
			Запись.ФискальныйНомер         = ФискальныеДанные.ФискальныйНомер;
			Запись.Записать();
			
		КонецЦикла;
		
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли