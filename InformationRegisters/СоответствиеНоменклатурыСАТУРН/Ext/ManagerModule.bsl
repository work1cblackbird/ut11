#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Сворачивает остатки по партиям
//
Процедура СверткаРегистраСоответствиеНоменклатурыСАТУРН() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоответствиеНоменклатуры.Номенклатура КАК Номенклатура,
	|	СоответствиеНоменклатуры.Характеристика КАК Характеристика,
	|	СоответствиеНоменклатуры.ПАТ КАК ПАТ,
	|	СоответствиеНоменклатуры.Партия КАК Партия
	|ИЗ
	|	РегистрСведений.СоответствиеНоменклатурыСАТУРН КАК СоответствиеНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиПартийСАТУРН.Остатки() КАК ОстаткиПартий
	|		ПО СоответствиеНоменклатуры.Партия = ОстаткиПартий.Партия
	|ГДЕ
	|	СоответствиеНоменклатуры.Партия <> ЗНАЧЕНИЕ(Справочник.ПартииСАТУРН.ПустаяСсылка)
	|	И ОстаткиПартий.Партия ЕСТЬ NULL
	|СГРУППИРОВАТЬ ПО
	|	СоответствиеНоменклатуры.Номенклатура,
	|	СоответствиеНоменклатуры.Характеристика,
	|	СоответствиеНоменклатуры.ПАТ,
	|	СоответствиеНоменклатуры.Партия
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	ПАТ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПредыдущаяНоменклатура = Неопределено;
	ПредыдущаяХарактеристика = Неопределено;
	ПредыдущийПАТ = Неопределено;
	
	Пока Выборка.Следующий() Цикл
		
		Если ПредыдущаяНоменклатура <> Выборка.Номенклатура
			Или ПредыдущаяХарактеристика <> Выборка.Характеристика
			Или ПредыдущийПАТ <> Выборка.ПАТ Тогда
			
			Если ПредыдущаяНоменклатура <> Неопределено Тогда
				ЗаписатьСвернутуюЗапись(ПредыдущаяНоменклатура, ПредыдущаяХарактеристика, ПредыдущийПАТ);
			КонецЕсли;
			
			ПредыдущаяНоменклатура = Выборка.Номенклатура;
			ПредыдущаяХарактеристика = Выборка.Характеристика;
			ПредыдущийПАТ = Выборка.ПАТ;
			
		КонецЕсли;
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Номенклатура.Установить(Выборка.Номенклатура, Истина);
		НаборЗаписей.Отбор.Характеристика.Установить(Выборка.Характеристика, Истина);
		НаборЗаписей.Отбор.ПАТ.Установить(Выборка.ПАТ, Истина);
		НаборЗаписей.Отбор.Партия.Установить(Выборка.Партия, Истина);
		
		Попытка
			НаборЗаписей.Записать();
		Исключение
			
			ТекстОшибки = НСтр("ru = 'При записи соответствия номенклатуры произошла ошибка:
			                         |%1'");
			
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(ТекстОшибки, ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ПредыдущаяНоменклатура <> Неопределено Тогда
		ЗаписатьСвернутуюЗапись(ПредыдущаяНоменклатура, ПредыдущаяХарактеристика, ПредыдущийПАТ);
	КонецЕсли;
	
КонецПроцедуры

#Область Серии

//Имена реквизитов, от значений которых зависят параметры указания серий
//
//	Возвращаемое значение:
//		Строка - имена реквизитов, перечисленные через запятую
//
Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	
	Возврат ИнтеграцияИС.ИменаРеквизитовДляЗаполненияПараметровУказанияСерий(Метаданные.РегистрыСведений.СоответствиеНоменклатурыСАТУРН);
	
КонецФункции

// Возвращает параметры указания серий для товаров, указанных в документе.
//
// Параметры:
//  Объект	 - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий.
// 
// Возвращаемое значение:
//  (см. ИнтеграцияИСПереопределяемый.ЗаполнитьПараметрыУказанияСерий) - параметры указания серий
//
Функция ПараметрыУказанияСерий(Объект) Экспорт
	
	Возврат ИнтеграцияИС.ПараметрыУказанияСерий(Метаданные.РегистрыСведений.СоответствиеНоменклатурыСАТУРН, Объект);
	
КонецФункции

// Возвращает текст запроса для расчета статусов указания серий
// Параметры:
//   ПараметрыУказанияСерий - (см. ИнтеграцияИСПереопределяемый.ЗаполнитьПараметрыУказанияСерий) - параметры указания серий
// Возвращаемое значение:
//   Строка - текст запроса
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий) Экспорт
	
	Возврат ИнтеграцияИС.ТекстЗапросаЗаполненияСтатусовУказанияСерий(Метаданные.РегистрыСведений.СоответствиеНоменклатурыСАТУРН, ПараметрыУказанияСерий);

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаписатьСвернутуюЗапись(Номенклатура, Характеристика, ПАТ)
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Номенклатура.Установить(Номенклатура, Истина);
	НаборЗаписей.Отбор.Характеристика.Установить(Характеристика, Истина);
	НаборЗаписей.Отбор.ПАТ.Установить(ПАТ, Истина);
	НаборЗаписей.Отбор.Партия.Установить(Справочники.ПартииСАТУРН.ПустаяСсылка(), Истина);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Номенклатура   = Номенклатура;
	НоваяЗапись.Характеристика = Характеристика;
	НоваяЗапись.ПАТ            = ПАТ;
	
	Попытка
		НаборЗаписей.Записать();
	Исключение
		
		ТекстОшибки = НСтр("ru = 'При записи соответствия номенклатуры произошла ошибка:
		|%1'");
		
		ОбщегоНазначения.СообщитьПользователю(
		СтрШаблон(ТекстОшибки, ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
