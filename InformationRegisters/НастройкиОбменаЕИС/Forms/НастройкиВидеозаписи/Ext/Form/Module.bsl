#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Организация", Неопределено);
	ВсеДокументы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Документы", Неопределено);
	Если ВсеДокументы <> Неопределено И НЕ ЗначениеЗаполнено(Организация) Тогда
		ВсеДокументы = ЭлектронноеАктированиеЕИСГИСНР.НастройкиОбменаДокументовДляГИСНР(ВсеДокументы);
		Для Каждого СтрокаКлюча Из ВсеДокументы Цикл
			Организация = СтрокаКлюча.Значение.Организация;
			ПоказатьФлажок = Истина;
			Прервать;
		КонецЦикла;
	КонецЕсли;
	
	ОбменСГИСНРВключен = ПрочитатьТекущиеНастройки().ОбменСГИСНРВключен;
	Состояние_Длительность = 60;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НомерПортаПоУмолчанию = ЭлектронноеАктированиеЕИСГИСНРКлиент.НомерПортаПоУмолчанию();
	Состояние_НомерПорта = ФорматНомераПорта(ЭлектронноеАктированиеЕИСГИСНРКлиент.ПараметрыСеансаПлагина().Порт);
	Состояние_МаксимальныйРазмер = 1024;

	УправлениеЭлементамиФормы(Истина);

	ОбновитьСостояниеПлагинаКлиент(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Состояние_Изменилось ИЛИ Состояние_ИзменилсяПорт Тогда
		ОповеститьОбИзмененииВсех();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = ЭлектронноеАктированиеЕИСГИСНРКлиент.ИмяОповещенияИзменениеНастройкиПлагина() Тогда
		ОбновитьСостояниеПлагинаКлиент(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Состояние_ПараметрыВидеоЗаписиОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	Если НЕ ЭлектронноеАктированиеЕИСГИСНРКлиент.ОткрытьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Состояние_НомерПортаПриИзменении(Элемент)
	
	ИзменилсяПорт();
	
КонецПроцедуры

&НаКлиенте
Процедура Состояние_НомерПортаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Состояние_ИзменилсяПорт = Истина;
	Модифицированность = Истина;

	Состояние_НомерПорта = ФорматНомераПорта(НомерПортаПоумолчанию);
	ИзменилсяПорт();
	
КонецПроцедуры

&НаКлиенте
Процедура Состояние_МаксимальныйРазмерПриИзменении(Элемент)
	
	Состояние_Изменилось = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Состояние_ДлительностьПриИзменении(Элемент)

	Состояние_Изменилось = Истина;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	СохранитьКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("НомерПорта", Состояние_НомерПорта);
	ОткрытьФорму("РегистрСведений.НастройкиОбменаЕИС.Форма.ДиагностикаВидеозаписи", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьЗакрыть(Команда)

	СохранитьКлиент(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОстановитьЗапись(Команда)
	
	ЭлектронноеАктированиеЕИСГИСНРКлиент.ПараметрыСеансаПлагина().ВидеоЗаписьПрервалась
		= ОбщегоНазначенияКлиент.ДатаУниверсальная();
	ЭлектронноеАктированиеЕИСГИСНРКлиент.ВидеоЗаписьОтключить(Неопределено);
	ОбновитьСостояниеПлагинаКлиент(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИзменилсяПорт()
	
	ТекущееЗначение = 0;
	Если ЗначениеЗаполнено(Состояние_НомерПорта) Тогда
		ТекущееЗначение = Число(Состояние_НомерПорта);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекущееЗначение) Тогда
		ТекущееЗначение = Неопределено;
	КонецЕсли;
	
	Состояние_ИзменилсяПорт = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКлиент(Сразу = Ложь)
	
	ПараметрыЦикла = Новый Структура;
	ПараметрыЦикла.Вставить("ЗакрытьСразу", Сразу);
	
	Если Состояние_ИзменилсяПорт Тогда
		ЭлектронноеАктированиеЕИСГИСНРКлиент.ЗаписатьНастройкиСеанса("Порт", Число(Состояние_НомерПорта));
		ЭлектронноеАктированиеЕИСГИСНРКлиент.ПараметрыСеансаПлагина().Порт = Число(Состояние_НомерПорта);
		ОповещениеЗавершения = Новый ОписаниеОповещения("СохранитьКлиентПродолжение", ЭтотОбъект, ПараметрыЦикла);
		ЭлектронноеАктированиеЕИСГИСНРКлиент.ВидеоЗаписьИнициализация(ОповещениеЗавершения);
	Иначе
		СохранитьКлиентПродолжение(Неопределено, ПараметрыЦикла);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКлиентПродолжение(РезультатВызова = Неопределено, ПараметрыЦикла = Неопределено) Экспорт
	
	Если РезультатВызова <> Неопределено Тогда
		ОбновитьСостояниеПлагинаЗавершение(РезультатВызова, ПараметрыЦикла.ЗакрытьСразу);
	КонецЕсли;
	
	Если ПлагинПроблемы = 1 И Состояние_Изменилось Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Не удалось подключиться к плагину видеозаписи.'"));
		Возврат;
	КонецЕсли;
	
	Если Состояние_Изменилось Тогда
		ЭлектронноеАктированиеЕИСГИСНРКлиент.ВидеоЗаписьУстановитьМаксимальныйРазмер(
			Неопределено,
			Состояние_МаксимальныйРазмер);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		СохранитьНастройки();
	КонецЕсли;
	
	Модифицированность = Ложь;
	
	Если ПараметрыЦикла.ЗакрытьСразу Тогда
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояниеПлагинаКлиент(ОповеститьФормы = Истина, ОповещениеЗавершения = Неопределено)
	
	ОбновитьСостояниеПлагина(ОповещениеЗавершения, ОповеститьФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояниеПлагина(РезультатВызова = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОбработкаСостояния = Новый ОписаниеОповещения(
		"ОбновитьСостояниеПлагинаЗавершение",
		ЭтотОбъект,
		ДополнительныеПараметры);
	ЭлектронноеАктированиеЕИСГИСНРКлиент.ВидеоЗаписьИнициализация(ОбработкаСостояния);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояниеПлагинаЗавершение(РезультатВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова <> Неопределено Тогда
		СостояниеПлагина = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(РезультатВызова);
		СостояниеПлагина.Вставить("СлужебнаяФорма", Неопределено);
	КонецЕсли;
	
	ПлагинПроблемы = РезультатВызова.ВидПроблемы;
	
	Если ДополнительныеПараметры = Истина Тогда
		ОповеститьОбИзмененииВсех();
	КонецЕсли;
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОбИзмененииВсех()
	
	Оповестить(
		ЭлектронноеАктированиеЕИСГИСНРКлиент.ИмяОповещенияИзменениеНастройкиПлагина(),
		Неопределено,
		Неопределено);

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьМаксимальныйРазмерЗавершение(РезультатВызова, ДополнительныеПараметры) Экспорт
	
	ОбновитьСостояниеПлагинаКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеЭлементамиФормы(НачальноеЗаполнение = Ложь)
	
	УправлениеЭлементамиФормыСервер(НачальноеЗаполнение);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭлементамиФормыСервер(НачальноеЗаполнение = Ложь)
	
	Элементы.ГруппаПервая.Видимость = ПлагинПроблемы <> 0;
	СостояниеПодключения = ЭлектронноеАктированиеЕИСГИСНР.ОписаниеПроблемПлагина(ПлагинПроблемы, Истина);
	Элементы.ДекорацияНастройкиВидеоЗаписи.Заголовок = СостояниеПодключения.Текст;
	Элементы.ГруппаПервая.ЦветФона = СостояниеПодключения.Фон;
	Элементы.ОстановитьЗапись.Видимость = НЕ НачальноеЗаполнение И ПлагинПроблемы = 0
				И НЕ ЗначениеСостояниеПлагина("ВидеозаписьДоступна", Истина);

	Элементы.ОбменСГИСНРВключен.Видимость = ПоказатьФлажок;
	Элементы.Состояние_Длительность.Видимость = ЗначениеЗаполнено(Организация);
	
	Состояние_МаксимальныйРазмер = ЗначениеСостояниеПлагина("МаксимальныйРазмер", 2048);
	Если Состояние_МаксимальныйРазмер = 0 Тогда
		Состояние_МаксимальныйРазмер = 2048;
	КонецЕсли;
	
	Если НЕ НачальноеЗаполнение Тогда
		НомерПорта = ЗначениеСостояниеПлагина("Порт");
		Если НЕ ЗначениеЗаполнено(НомерПорта) Тогда
			НомерПорта = НомерПортаПоУмолчанию;
		КонецЕсли;
		Состояние_НомерПорта = ФорматНомераПорта(НомерПорта);
	КонецЕсли;
	
	Состояние_СписокВидео = Текст_Ролики(ПлагинПроблемы = 0);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ФорматНомераПорта(ТекущийНомер)
	
	Возврат Формат(ТекущийНомер, "ЧГ=0");
	
КонецФункции

&НаСервере
Функция Текст_Ролики(ЕстьПлагин)
	
	Результат = НСтр("ru = '<a href = ""Видеоролики"">Открыть</a>'");
	
	Возврат СтроковыеФункции.ФорматированнаяСтрока(Результат);
	
КонецФункции

&НаСервере
Функция ЗначениеСостояниеПлагина(ИмяПараметра, ЗначениеПоУмолчанию = Неопределено)
	
	Результат = ЗначениеПоУмолчанию;
	
	Если СостояниеПлагина <> Неопределено Тогда
		Результат = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СостояниеПлагина, ИмяПараметра, ЗначениеПоУмолчанию);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПрочитатьТекущиеНастройки()
	
	Результат = Новый Структура("ОбменСГИСНРВключен", Истина);
	Если НЕ ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиОбменаЕИС) Тогда
		Организация = Неопределено;
		Возврат Результат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	НастройкиОбменаЕИС.ВидеозаписьГИСНРВключена КАК ВидеозаписьГИСНРВключена
	|ИЗ
	|	РегистрСведений.НастройкиОбменаЕИС КАК НастройкиОбменаЕИС
	|ГДЕ
	|	НастройкиОбменаЕИС.Организация = &Организация";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Организация", Организация);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура СохранитьНастройки()
	
	Если НЕ Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	// Сохранить длительность видеозаписи
	
	ТекущаяЗапись = РегистрыСведений.НастройкиОбменаЕИС.СоздатьМенеджерЗаписи();
	ТекущаяЗапись.Организация = Организация;
	ТекущаяЗапись.Прочитать();
	
	Если ТекущаяЗапись.Выбран() Тогда
		ТекущаяЗапись.Организация = Организация;
		ТекущаяЗапись.ВидеозаписьГИСНРВключена = ОбменСГИСНРВключен;
		ТекущаяЗапись.ДлительностьВидеозаписи = Состояние_Длительность;
		ТекущаяЗапись.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти