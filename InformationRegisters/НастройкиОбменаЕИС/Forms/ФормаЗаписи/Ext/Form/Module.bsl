#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Поддерживается обмен только от имени поставщика.
	Если НЕ ЗначениеЗаполнено(Запись.ТипОрганизации) Тогда
		Запись.ТипОрганизации = Перечисления.ВидыУчастниковЭлектронногоАктированияЕИС.Поставщик;
	КонецЕсли;
	
	ИспользуетсяНесколькоОрганизаций = ЭлектронноеАктированиеЕИСВызовСервера.ИспользуетсяНесколькоОрганизаций();
	
	Если НЕ ИспользуетсяНесколькоОрганизаций И НЕ ЗначениеЗаполнено(Запись.Организация) Тогда
		Запись.Организация = ЭлектронноеАктированиеЕИС.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	ЭлектронноеАктированиеЕИСВызовСервера.
		СкрытьЭлементыФормыПриИспользованииОднойОрганизации(ЭтотОбъект, "Организация");

	ВидеозаписьГИСНРБылаВключена = Запись.ВидеозаписьГИСНРВключена;

	УправлениеДоступностьюЭУ();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьСостояниеПлагина();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = ЭлектронноеАктированиеЕИСГИСНРКлиент.ИмяОповещенияИзменениеНастройкиПлагина() Тогда
		ОбновитьСостояниеПлагина();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьМастерНастроек(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ОткрытьМастерНастроекЗавершение", ЭтотОбъект);
	ЭлектронноеАктированиеЕИСКлиент.ОткрытьМастерНастроек(Запись.Организация, УникальныйИдентификатор, Оповещение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОбменСГИСНРВключенПриИзменении(Элемент)
	
	Запись.ВидеозаписьГИСНРВключена = Запись.ВидеозаписьГИСНРВключена И Запись.ОбменСГИСНРВключен;
	УправлениеДоступностьюЭУ();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидеоЗаписьГИСНРВключенаПриИзменении(Элемент)

	УправлениеДоступностьюЭУ();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВидеоЗаписьОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Запись.Организация);
	
	Если НЕ ЭлектронноеАктированиеЕИСГИСНРКлиент.ОткрытьНавигационнуюСсылку(
		НавигационнаяСсылкаФорматированнойСтроки,
		ПараметрыФормы) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьСостояниеПлагина()

	ОбработкаСостояния = Новый ОписаниеОповещения("СостояниеПодключенногоПлагина", ЭтотОбъект);
	ЭлектронноеАктированиеЕИСГИСНРКлиент.ВидеоЗаписьИнициализация(ОбработкаСостояния);
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеПодключенногоПлагина(РезультатВызова, ДополнительныеПараметры) Экспорт
	
	ПлагинПроблемы = РезультатВызова.ВидПроблемы;
	УправлениеДоступностьюЭУ();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоОрганизацияПоставщик(ТипОрганизации)
	
	Возврат ТипОрганизации = Перечисления.ВидыУчастниковЭлектронногоАктированияЕИС.Поставщик;
	
КонецФункции

&НаСервере
Процедура УправлениеДоступностьюЭУ()
	
	Элементы.РегистрационныйНомерЕРУЗ.Видимость = ЭтоОрганизацияПоставщик(Запись.ТипОрганизации);
	
	СостояниеПодключенияПлагина
		= ЭлектронноеАктированиеЕИСГИСНР.ОписаниеПроблемПлагина(ПлагинПроблемы, Ложь, НЕ ВидеозаписьГИСНРБылаВключена);
	ЭлектронноеАктированиеЕИСГИСНР.ДобавитьПодсказкуДляГИСНР(Элементы.ДекорацияЗаголовокГИСНР);
	Элементы.ДекорацияНастройкиВидеоЗаписи.Заголовок = СостояниеПодключенияПлагина.Текст;
	Элементы.ГруппаВидеоЗаписьГоризонтально1.Доступность = Запись.ВидеозаписьГИСНРВключена;
	Элементы.ГруппаВидеоЗаписьГоризонтально1.ЦветФона = СостояниеПодключенияПлагина.Фон;
	Элементы.ВидеоЗаписьГИСНРВключена.Доступность = Запись.ОбменСГИСНРВключен;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьМастерНастроекЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) И Результат Тогда
		Прочитать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

