#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НомерПорта = Параметры.НомерПорта;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НачатьПроверку();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ПоказатьВопрос Тогда
		ПроверкаОтключитьЗапись();
	КонецЕсли;
	РезультатПроверки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияРезультатОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	Если НЕ ЭлектронноеАктированиеЕИСГИСНРКлиент.ОткрытьНавигационнуюСсылку(
		НавигационнаяСсылкаФорматированнойСтроки) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Повторить(Команда)
	
	НачатьПроверку();
	
КонецПроцедуры

&НаКлиенте
Процедура РамкаНевидна(Команда)

	ОшибкаВидеозаписи = НСтр("ru = 'Ошибка запуска видеозаписи через плагин'");
	ПроверкаОтключитьЗапись();

КонецПроцедуры

&НаКлиенте
Процедура РамкаВидна(Команда)

	ПроверкаОтключитьЗапись();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НачатьПроверку()
	
	СостояниеПлагина = Неопределено;
	ПроверкаНачалась = Истина;
	ПоказатьВопрос = Ложь;
	ОшибкаВидеозаписи = "";
	Элементы.ГруппаОтвет.Доступность = Истина;

	Если НЕ ПустаяСтрока(НомерПорта) Тогда
		ТекущиеНастройки = ЭлектронноеАктированиеЕИСГИСНРКлиент.ПараметрыСеансаПлагина();
		ТекущиеНастройки.Порт = Число(НомерПорта);
	КонецЕсли;

	УправлениеЭлементамиФормы();
	ОбработкаСостояния = Новый ОписаниеОповещения("НачатьПроверкуСостояние", ЭтотОбъект, Неопределено);
	ЭлектронноеАктированиеЕИСГИСНРКлиент.ВидеоЗаписьИнициализация(ОбработкаСостояния);

КонецПроцедуры

&НаКлиенте
Процедура НачатьПроверкуСостояние(РезультатВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова <> Неопределено Тогда
		СостояниеПлагина = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(РезультатВызова);
		СостояниеПлагина.Вставить("СлужебнаяФорма", Неопределено);
	КонецЕсли;
	
	Если ДополнительныеПараметры = Истина И СостояниеПлагина <> Неопределено Тогда
		РезультатПроверки();
	КонецЕсли;
	
	ЕстьПлагин = ЗначениеСостояниеПлагина("ПлагинОбнаружен", Ложь);
	
	Если ЗначениеСостояниеПлагина("ВидеозаписьДоступна", Ложь) Тогда
		ОбработкаСостояния = Новый ОписаниеОповещения("НачатьПроверкуЗапись", ЭтотОбъект, ДополнительныеПараметры);
		ЭлектронноеАктированиеЕИСГИСНРКлиент.ВидеоЗаписьВключить(ОбработкаСостояния, Истина);
	Иначе
		ОкончитьПроверку();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПроверкуЗапись(РезультатВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова.Ошибка Тогда
		ОшибкаВидеозаписи = РезультатВызова.ТекстОшибки;
		ОкончитьПроверку();
	Иначе
		ПоказатьВопрос = Истина;
		УправлениеЭлементамиФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаОтключитьЗапись(БезОбработки = Ложь)
	
	ОбработкаСостояния = Неопределено;
	Если НЕ БезОбработки Тогда
		ОбработкаСостояния = Новый ОписаниеОповещения("ПроверкаОтключитьЗаписьЗавершение", ЭтотОбъект, Неопределено);
	КонецЕсли;
	
	ЭлектронноеАктированиеЕИСГИСНРКлиент.ВидеоЗаписьОтключить(ОбработкаСостояния);
	Элементы.ГруппаОтвет.Доступность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаОтключитьЗаписьЗавершение(РезультатВызова, ДополнительныеПараметры) Экспорт
	
	ОкончитьПроверку();
	
КонецПроцедуры

&НаКлиенте
Процедура ОкончитьПроверку()
	
	ПроверкаНачалась = Ложь;
	ПоказатьВопрос = Ложь;
	
	ТекущиеНастройки = ЭлектронноеАктированиеЕИСГИСНРКлиент.ПараметрыСеансаПлагина();
	ТекущиеНастройки.Порт = Неопределено;
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатПроверки()
	
	Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура УправлениеЭлементамиФормы()

	Элементы.ДекорацияОжиданиеКартинка.Видимость = ПроверкаНачалась;
	Элементы.ГруппаПроверка.Видимость = ПроверкаНачалась И ПоказатьВопрос;
	Элементы.ГруппаРезультат.Видимость = НЕ ПроверкаНачалась;
	Если НЕ ПроверкаНачалась Тогда
		СостояниеПодключения = Текст_Плагин();
		Элементы.ДекорацияРезультат.Заголовок = СостояниеПодключения.Заголовок;
		Элементы.ДекорацияКартинка.Картинка = СостояниеПодключения.Картинка;
		Элементы.ДекорацияСодержание.Заголовок = СостояниеПодключения.Текст;
	КонецЕсли;
	
	Если ПоказатьВопрос Тогда
		Элементы.РамкаВидна.КнопкаПоУмолчанию = Истина;
	Иначе
		Элементы.Закрыть.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	Элементы.ФормаПовторить.Видимость = ЗначениеСостояниеПлагина("Ошибка", Ложь) = Истина;
	
КонецПроцедуры

&НаСервере
Функция Текст_Плагин()
	
	Результат = Новый Структура;
	Результат.Вставить("Заголовок", "");
	Результат.Вставить("Текст", "");
	Результат.Вставить("Картинка", Новый Картинка());
	
	СодержимоеОшибки = "";
	ЗаголовокСодержимого = "";
	ТекстОшибки = ОшибкаВидеозаписи;
	Если ЗначениеСостояниеПлагина("Ошибка", Ложь) Тогда
		ТекстОшибки = СокрЛП(ТекстОшибки + Символы.ПС + ЗначениеСостояниеПлагина("ТекстОшибки", ""));
	КонецЕсли;
	
	ЗаголовокСодержимого = НСтр("ru = '<b>В процессе проверки возникла ошибка:</b>'");
	ТекстОписания = "";
	
	Если НЕ ЕстьПлагин Тогда
		ЗаголовокСодержимого = НСтр("ru = '<b>Не удалось подключиться к плагину ГИС НР</b>'");
		СодержимоеОшибки = НСтр("ru = 'Убедитесь, что'") + ":";
		Если ОбщегоНазначения.ЭтоMacOSКлиент() 
			или ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
			СодержимоеОшибки = НСтр("ru = '
			| - плагин предназначен для работы в ОС Windows и ОС Linux;'");
		КонецЕсли;
		СодержимоеОшибки = СодержимоеОшибки + НСтр("ru = '
		| - установлена версия плагина не ниже 6.4.2.0, скачать последнюю версию плагина можно на <a href = ""Скачать"">странице</a>;
		| - плагин запущен и отображается в области уведомлений операционной системы;
		| - указан корректный номер ТСР-порта службы плагина.'");
		
	ИначеЕсли ЗначениеЗаполнено(ТекстОшибки) И ЗначениеСостояниеПлагина("Ошибка", Ложь) Тогда
		ЗаголовокСодержимого = НСтр("ru = '<b>В плагине ГИС НР возникла ошибка</b>'");
		СодержимоеОшибки = НСтр("ru = 'Убедитесь, что:
		| - проверьте настройки плагина ГИС НР на рабочем месте пользователя;
		| - при необходимости перезапустите плагин из операционной системы;
		| - версия плагина не ниже 6.4.2.0, скачать последнюю версию плагина можно на <a href = ""Скачать"">странице</a>;'")
		+ Символы.ПС + " - " + ЗначениеСостояниеПлагина("ТекстОшибки", "");
		
	ИначеЕсли ЗначениеЗаполнено(ТекстОшибки) Тогда
		СодержимоеОшибки = ТекстОшибки;
		
	ИначеЕсли ЗначениеСостояниеПлагина("ВидеозаписьДоступна", Ложь) = Ложь Тогда
		СодержимоеОшибки = НСтр("ru = 'Видеозапись уже запущена в плагине'");
		
	ИначеЕсли ЗначениеСостояниеПлагина("СертификатВыбран", Ложь) = Ложь Тогда
		СодержимоеОшибки = СодержимоеОшибки + Символы.ПС
				+ НСтр("ru = 'В настройках плагина не указан сертификат. 
					|<a href = ""Сертификат"">Укажите</a> сертификат ЭП для работы плагина.'");
				
	ИначеЕсли ЗначениеСостояниеПлагина("ОшибкаЭП", Ложь) = Истина Тогда
		СодержимоеОшибки = СодержимоеОшибки + Символы.ПС 
				+ НСтр("ru = 'Обнаружены проблемы формирования электронной подписи.
					|Проверьте или <a href = ""Сертификат"">измените</a> сертификат ЭП плагина.'");
				
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СодержимоеОшибки) Тогда
		ТекстОписания = ОформлениеСтроки(СодержимоеОшибки, 1);
		Результат.Картинка = ЭлектронноеАктированиеЕИСГИСНР.ПолучитьКартинкуБиблиотеки("Ошибка32");
	Иначе
		ЗаголовокСодержимого = НСтр("ru = '<b>Проблемы не обнаружены</b>'");
		Результат.Картинка = ЭлектронноеАктированиеЕИСГИСНР.ПолучитьКартинкуБиблиотеки("Успешно32");
	КонецЕсли;
	
	Результат.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(ЗаголовокСодержимого);
	Результат.Текст = СтроковыеФункции.ФорматированнаяСтрока(ТекстОписания);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ЗначениеСостояниеПлагина(ИмяПараметра, ЗначениеПоУмолчанию = Неопределено)
	
	Результат = ЗначениеПоУмолчанию;
	
	Если СостояниеПлагина <> Неопределено Тогда
		Результат = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СостояниеПлагина, ИмяПараметра, ЗначениеПоУмолчанию);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОформлениеСтроки(Содержимое, Статус)
	
	Если Статус = 0 Тогда
		Результат = "<span style=""color: ЦветТекстаВажнаяНадписьЭлАктированиеЕИС"">" + Содержимое + "</span>";
	ИначеЕсли Статус = 1 Тогда
		Результат = "<span>" + Содержимое + "</span>";
	ИначеЕсли Статус = 3 Тогда
		Результат = "<b>" + Содержимое + "</b>";
	Иначе
		Результат = "<span style=""color: ЦветТекстаНеважнаяНадписьЭлАктированиеЕИС"">" + Содержимое + "</span>";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
