#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ширина > 0 Тогда
		Ширина = Параметры.Ширина;
	КонецЕсли;
	
	Режим = Параметры.Режим;
	ЗаголовокФормы = Параметры.ЗаголовокФормы;
	Элементы.ТекстОписания.Заголовок = Параметры.ТекстФормы;
	
	Для Каждого СтрокаСписка Из Параметры.СписокКоманд Цикл
		НоваяСтрока = СписокКоманд.Добавить();
		НоваяСтрока.Идентификатор = "ИД_" + СтрЗаменить(Строка(Новый УникальныйИдентификатор()), "-", "");
		НоваяСтрока.ПоУмолчанию = СтрокаСписка.Пометка;
		НоваяСтрока.Картинка = СтрокаСписка.Картинка;
		НоваяСтрока.Представление = СтрокаСписка.Представление;
		НоваяСтрока.Результат = СтрокаСписка.Значение;
	КонецЦикла;
	
	ЗаполнитьЭлементыФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТекстОписанияОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыЦикла = Новый Структура("Ссылка", НавигационнаяСсылкаФорматированнойСтроки);
	ОповещениеПродолжения = Новый ОписаниеОповещения("ОшибкаНавигационнойСсылки", ЭтотОбъект, ПараметрыЦикла);
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки, ОповещениеПродолжения);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_КомандаФормы(Команда)
	
	Результат = Неопределено;
	НашлиСтроки = СписокКоманд.НайтиСтроки(Новый Структура("Идентификатор", Команда.Имя));
	Если НашлиСтроки.Количество() > 0 Тогда
		Результат = НашлиСтроки[0].Результат;
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОшибкаНавигационнойСсылки(РезультатВызова, ПараметрыЦикла) Экспорт
	
	Если РезультатВызова = Ложь И ОписаниеОповещенияОЗакрытии <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии, ПараметрыЦикла.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЭлементыФормы()
	
	ЕстьКоманды = СписокКоманд.Количество() > 0;
	
	ТекущееВыравнивание = ГоризонтальноеПоложениеЭлемента.Центр;
	Картинка = БиблиотекаКартинок.ДиалогИнформация;
	НовыйЗаголовок = НСтр("ru = 'Информация'");
	Если Режим = "Ошибка" Тогда
		Картинка = БиблиотекаКартинок.ДиалогСтоп;
		НовыйЗаголовок = НСтр("ru = 'Возникла ошибка'");
		Если НЕ ЕстьКоманды Тогда
			ТекущееВыравнивание = ГоризонтальноеПоложениеЭлемента.Лево;
		КонецЕсли;
	ИначеЕсли Режим = "Предупреждение" Тогда
		НовыйЗаголовок = НСтр("ru = 'Предупреждение'");
		Картинка = БиблиотекаКартинок.ДиалогВосклицание;
		ТекущееВыравнивание = ГоризонтальноеПоложениеЭлемента.Центр;
	ИначеЕсли Режим = "Вопрос" Тогда
		НовыйЗаголовок = НСтр("ru = 'Вопрос'");
		Картинка = БиблиотекаКартинок.ДиалогВопрос;
		ТекущееВыравнивание = ГоризонтальноеПоложениеЭлемента.Центр;
	КонецЕсли;
	Элементы.ДекорацияКартинка.Картинка = Картинка;
	Элементы.ГруппаКоманды.ГоризонтальноеПоложение = ТекущееВыравнивание;
	Элементы.КнопкаЗакрыть.Видимость = НЕ ЕстьКоманды;
	Заголовок = ?(ПустаяСтрока(ЗаголовокФормы), НовыйЗаголовок, ЗаголовокФормы);
	
	ПервыйЭлемент = Истина;
	Для Каждого СтрокаТаблицы Из СписокКоманд Цикл
		
		НоваяКоманда = Команды.Добавить(СтрокаТаблицы.Идентификатор);
		НоваяКоманда.Действие = "Подключаемый_КомандаФормы";
		НоваяКоманда.Заголовок = СтрокаТаблицы.Представление;
		НоваяКоманда.Картинка = СтрокаТаблицы.Картинка;
		Если НоваяКоманда.Картинка.Вид <> ВидКартинки.Пустая Тогда
			НоваяКоманда.Отображение = ОтображениеКнопки.КартинкаИТекст;
		КонецЕсли;
		
		НовыйЭлемент = Элементы.Добавить(СтрокаТаблицы.Идентификатор, Тип("КнопкаФормы"), Элементы.ГруппаКоманды);
		НовыйЭлемент.ИмяКоманды = СтрокаТаблицы.Идентификатор;
		НовыйЭлемент.КнопкаПоУмолчанию = ПервыйЭлемент ИЛИ СтрокаТаблицы.ПоУмолчанию;
		ПервыйЭлемент = Ложь;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
