#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает таблицу кодов ТНВЭД, соответствующих переданным кодам ОКПД2.
//
// Параметры:
//  КодыОКПД2 - Строка, Массив из Строка - коды ОКПД2, для которых требуется получить ТНВЭД.
//
// Возвращаемое значение:
//  ТаблицаЗначений:
//   * ОКПД2 - Строка - Исходный ОКПД2.
//   * КодТНВЭД - Строка - Код ТН ВЭД.
//   * Представление - Строка - представление ТН ВЭД.
Функция ДанныеТНВЭДПоОКПД2(КодыОКПД2) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВидыСельскохозяйственныхКультурЗЕРНО.ОКПД2             КАК ОКПД2,
	|	ВидыСельскохозяйственныхКультурЗЕРНО.КодТНВЭД          КАК КодТНВЭД,
	|	ВидыСельскохозяйственныхКультурЗЕРНО.ТНВЭДНаименование КАК Представление
	|ИЗ
	|	РегистрСведений.ВидыСельскохозяйственныхКультурЗЕРНО КАК ВидыСельскохозяйственныхКультурЗЕРНО
	|ГДЕ
	|	ВидыСельскохозяйственныхКультурЗЕРНО.ОКПД2 В (&ОКПД2)
	|	И (ВидыСельскохозяйственныхКультурЗЕРНО.ДействуетПо >= &Дата
	|		Или ВидыСельскохозяйственныхКультурЗЕРНО.ДействуетПо = ДатаВремя(1,1,1))
	|";
	
	Запрос.УстановитьПараметр("ОКПД2", КодыОКПД2);
	Запрос.УстановитьПараметр("Дата", ТекущаяДатаСеанса());
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Для Каждого СтрокаРезультата Из Результат Цикл
		СтрокаРезультата.Представление = ИнтеграцияЗЕРНОКлиентСервер.ПредставлениеКодТНВЭД(
			СтрокаРезультата.Представление, СтрокаРезультата.КодТНВЭД);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
