
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ЗаполнитьНастройкиПечатиОбъектов();
	
	Если НЕ ПраваПользователяПовтИсп.ДобавлениеИзменениеНастройкиПечатиОбъектов()
	 И НЕ ПраваПользователяПовтИсп.СохранениеНастроекПечатиОбъектовПоУмолчанию() Тогда
		Элементы.НастройкиПечатиОбъектовДобавить.Видимость = Ложь;
		Элементы.НастройкиПечатиОбъектовУдалить.Видимость  = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НастройкиПечатиОбъектов" Тогда
		ЗаполнитьНастройкиПечатиОбъектов();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНастройкиПечатиОбъектов

&НаКлиенте
Процедура НастройкиПечатиОбъектовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Перем КлючЗаписи;
	
	Если Элемент.ТекущийЭлемент = Элементы.НастройкиПечатиОбъектовПредставление И
		Элементы.НастройкиПечатиОбъектов.ТекущиеДанные <> Неопределено И 
		Элементы.НастройкиПечатиОбъектов.ТекущиеДанные.ИндексКартинки = 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ТипОбъекта  = "";
		Организация = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
		Партнер     = ПредопределенноеЗначение("Справочник.Партнеры.ПустаяСсылка");
		
		ТекущиеДанные = Элементы.НастройкиПечатиОбъектов.ТекущиеДанные;
		
		Если ТипЗнч(ТекущиеДанные.Настройка) = Тип("Строка") Тогда
			ТипОбъекта = ТекущиеДанные.Настройка;
		ИначеЕсли ТипЗнч(ТекущиеДанные.Настройка) = Тип("СправочникСсылка.Организации") Тогда
			ТипОбъекта  = ТекущиеДанные.ПолучитьРодителя().Настройка;
			Организация = ТекущиеДанные.Настройка;
		ИначеЕсли ТипЗнч(ТекущиеДанные.Настройка) = Тип("СправочникСсылка.Партнеры") Тогда
			ТипОбъекта  = ТекущиеДанные.ПолучитьРодителя().ПолучитьРодителя().Настройка;
			Организация = ТекущиеДанные.ПолучитьРодителя().Настройка;
			Партнер     = ТекущиеДанные.Настройка;
		КонецЕсли;
		
		ДанныеКлючаЗаписи = Новый Структура();
		ДанныеКлючаЗаписи.Вставить("ТипОбъекта",  ТипОбъекта);
		ДанныеКлючаЗаписи.Вставить("Организация", Организация);
		ДанныеКлючаЗаписи.Вставить("Партнер",     Партнер);
		
		Если ЗаписьСуществует(ДанныеКлючаЗаписи) Тогда
			МассивПараметровКонструктора = Новый Массив();
			МассивПараметровКонструктора.Добавить(ДанныеКлючаЗаписи);
			
			КлючЗаписи = Новый("РегистрСведенийКлючЗаписи.НастройкиПечатиОбъектов", МассивПараметровКонструктора);
		КонецЕсли;
		
		ОткрытьФорму(
			"РегистрСведений.НастройкиПечатиОбъектов.ФормаЗаписи",
			Новый Структура("Ключ,ЗначенияЗаполнения",
			КлючЗаписи, ДанныеКлючаЗаписи),,,,, Неопределено, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Добавить(Команда)
	
	ОткрытьФорму("РегистрСведений.НастройкиПечатиОбъектов.ФормаЗаписи",,,,,, Неопределено, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	
	ТекущиеДанные = Элементы.НастройкиПечатиОбъектов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Укажите настройку печати для удаления'"));
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ИндексКартинки = 1 Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Невозможно удалить группировку'"));
		Возврат;
	КонецЕсли;
	
	СписокКнопок = Новый СписокЗначений();
	СписокКнопок.Добавить("Удалить", НСтр("ru = 'Удалить'"));
	СписокКнопок.Добавить("НеУдалять", НСтр("ru = 'Не удалять'"));
	
	ОтветНаВопрос = Неопределено;

	
	ПоказатьВопрос(Новый ОписаниеОповещения("УдалитьЗавершение", ЭтотОбъект, Новый Структура("ТекущиеДанные", ТекущиеДанные)), НСтр("ru = 'Удалить настройку печати?'"), СписокКнопок);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    ТекущиеДанные = ДополнительныеПараметры.ТекущиеДанные;
    
    
    ОтветНаВопрос = РезультатВопроса;
    
    Если ОтветНаВопрос = "НеУдалять" Тогда
        Возврат;
    КонецЕсли;
    
    ОчиститьСообщения();
    УдалитьНастройкуПечатиСервер(ТекущиеДанные.ПолучитьИдентификатор());

КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗаполнитьНастройкиПечатиОбъектов();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНастройкиПечатиОбъектов()
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	НастройкиПечатиОбъектов.ТипОбъекта  КАК ТипОбъекта,
	|	НастройкиПечатиОбъектов.Организация КАК Организация,
	|	НастройкиПечатиОбъектов.Партнер     КАК Партнер
	|ИЗ
	|	РегистрСведений.НастройкиПечатиОбъектов КАК НастройкиПечатиОбъектов
	|УПОРЯДОЧИТЬ ПО
	|	ТипОбъекта,
	|	Организация,
	|	Партнер
	|ИТОГИ ПО
	|	ТипОбъекта,
	|	Организация,
	|	Партнер");
	
	РезультатЗапроса = Запрос.Выполнить();
	НастройкиПечатиОбъектов.ПолучитьЭлементы().Очистить();
	ДеревоЗначений = РеквизитФормыВЗначение("НастройкиПечатиОбъектов");
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ВыборкаТипОбъекта = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаТипОбъекта.Следующий() Цикл
			
			СтрокаТипОбъекта = ДеревоЗначений.Строки.Добавить();
			СтрокаТипОбъекта.Настройка = ВыборкаТипОбъекта.ТипОбъекта;
			СтрокаТипОбъекта.Представление = Метаданные.НайтиПоПолномуИмени(ВыборкаТипОбъекта.ТипОбъекта).Представление();
			СтрокаТипОбъекта.ИндексКартинки = 1;
			
			ВыборкаОрганизация = ВыборкаТипОбъекта.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаОрганизация.Следующий() Цикл
				
				Если Не ЗначениеЗаполнено(ВыборкаОрганизация.Организация) Тогда
					СтрокаТипОбъекта.ИндексКартинки = 0;
					Продолжить;
				КонецЕсли;
				
				СтрокаОрганизация = СтрокаТипОбъекта.Строки.Добавить();
				СтрокаОрганизация.Настройка = ВыборкаОрганизация.Организация;
				СтрокаОрганизация.Представление = ВыборкаОрганизация.Организация;
				СтрокаОрганизация.ИндексКартинки = 1;
				
				ВыборкаПартнер = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ВыборкаПартнер.Следующий() Цикл
					
					Если Не ЗначениеЗаполнено(ВыборкаПартнер.Партнер) Тогда
						СтрокаОрганизация.ИндексКартинки = 0;
						Продолжить;
					КонецЕсли;
					
					СтрокаПартнер = СтрокаОрганизация.Строки.Добавить();
					СтрокаПартнер.Настройка = ВыборкаПартнер.Партнер;
					СтрокаПартнер.Представление = ВыборкаПартнер.Партнер;
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
	КонецЕсли;

	ЗначениеВРеквизитФормы(ДеревоЗначений, "НастройкиПечатиОбъектов");
	
КонецПроцедуры

&НаСервере
Функция УдалитьНастройкуПечатиСервер(ИдентификаторСтроки)
	
	ТипОбъекта  = "";
	Организация = Справочники.Организации.ПустаяСсылка();
	Партнер     = Справочники.Партнеры.ПустаяСсылка();
	
	ТекущиеДанные = НастройкиПечатиОбъектов.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(ТекущиеДанные.Настройка) = Тип("Строка") Тогда
		Если Не ПраваПользователяПовтИсп.СохранениеНастроекПечатиОбъектовПоУмолчанию() Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Нет прав на удаление настройки печати'"));
			Возврат Ложь;
		Иначе
			ТипОбъекта = ТекущиеДанные.Настройка;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ТекущиеДанные.Настройка) = Тип("СправочникСсылка.Организации") Тогда
		Если Не ПраваПользователяПовтИсп.СохранениеНастроекПечатиОбъектовПоУмолчанию() Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Нет прав на удаление настройки печати по организации'"));
			Возврат Ложь;
		Иначе
			ТипОбъекта  = ТекущиеДанные.ПолучитьРодителя().Настройка;
			Организация = ТекущиеДанные.Настройка;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ТекущиеДанные.Настройка) = Тип("СправочникСсылка.Партнеры") Тогда
		Если Не ПраваПользователяПовтИсп.ДобавлениеИзменениеНастройкиПечатиОбъектов() Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Нет прав на удаление настройки печати по партнеру'"));
			Возврат Ложь;
		Иначе
			ТипОбъекта  = ТекущиеДанные.ПолучитьРодителя().ПолучитьРодителя().Настройка;
			Организация = ТекущиеДанные.ПолучитьРодителя().Настройка;
			Партнер     = ТекущиеДанные.Настройка;
		КонецЕсли;
	КонецЕсли;
	
	ДанныеКлючаЗаписи = Новый Структура();
	ДанныеКлючаЗаписи.Вставить("ТипОбъекта",  ТипОбъекта);
	ДанныеКлючаЗаписи.Вставить("Организация", Организация);
	ДанныеКлючаЗаписи.Вставить("Партнер",     Партнер);
	
	Если ЗаписьСуществует(ДанныеКлючаЗаписи) Тогда
		
		МассивПараметровКонструктора = Новый Массив();
		МассивПараметровКонструктора.Добавить(ДанныеКлючаЗаписи);
		РегистрыСведений.НастройкиПечатиОбъектов.УдалитьНастройкиПечатиОбъекта(ТипОбъекта, Организация, Партнер);
		ЗаполнитьНастройкиПечатиОбъектов();
		
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ЗаписьСуществует(ДанныеКлючаЗаписи)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	НастройкиУчетныхЗаписейЭлектроннойПочты.ТипОбъекта
	|ИЗ
	|	РегистрСведений.НастройкиПечатиОбъектов КАК НастройкиУчетныхЗаписейЭлектроннойПочты
	|ГДЕ
	|	НастройкиУчетныхЗаписейЭлектроннойПочты.ТипОбъекта = &ТипОбъекта
	|	И НастройкиУчетныхЗаписейЭлектроннойПочты.Организация = &Организация
	|	И НастройкиУчетныхЗаписейЭлектроннойПочты.Партнер = &Партнер
	|");
	
	Запрос.УстановитьПараметр("ТипОбъекта",  ДанныеКлючаЗаписи.ТипОбъекта);
	Запрос.УстановитьПараметр("Организация", ДанныеКлючаЗаписи.Организация);
	Запрос.УстановитьПараметр("Партнер",     ДанныеКлючаЗаписи.Партнер);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти
