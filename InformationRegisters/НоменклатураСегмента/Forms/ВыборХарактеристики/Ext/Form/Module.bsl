
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если НЕ Параметры.Свойство("ТаблицаХарактеристик") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Параметры.ТолькоПросмотр = Истина Тогда
		
		Элементы.Характеристики.ТолькоПросмотр              = Истина;
		Элементы.ХарактеристикиУстановитьФлажки.Доступность = Ложь;
		Элементы.ХарактеристикиСнятьФлажки.Доступность      = Ложь;
		
		Заголовок = НСтр("ru = 'Характеристики входящие в сегмент'");
		Элементы.Выбрать.Заголовок = НСтр("ru = 'Закрыть'");
		
	Иначе
		
		ОткрытаДляВыбора = Истина;
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу( Параметры.ТаблицаХарактеристик, Характеристики);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если ОткрытаДляВыбора Тогда
		
		Закрыть(Характеристики);
		
	Иначе
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	УстановитьСнятьФлажки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	УстановитьСнятьФлажки(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьСнятьФлажки(Установить)

	Для каждого СтрокаТаблицы Из Характеристики Цикл
	
		СтрокаТаблицы.ВходитВСегмент = Установить;
	
	КонецЦикла;

КонецПроцедуры // УстановитьСнятьФлажки()

#КонецОбласти
