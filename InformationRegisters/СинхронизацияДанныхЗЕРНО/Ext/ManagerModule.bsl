
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Установить дату выполнения синхронизации.
// 
// Параметры:
//  ПараметрыЗаписи - Структура - содержит ключи:
//    * Организация - ОпределяемыйТип.Организация.
//    * Подразделение - ОпределяемыйТип.Подразделение.
//    * ДатаСинхронизации - Дата.
//    * ИмяЗапроса - ПеречислениеСсылка.ИмяЗапросаЗЕРНО.
Процедура УстановитьДатуВыполненияСинхронизации(ПараметрыЗаписи) Экспорт
	
	Организация       = ПараметрыЗаписи.Организация;
	Подразделение     = ПараметрыЗаписи.Организация;
	ДатаСинхронизации = ПараметрыЗаписи.ДатаСинхронизации;
	ИмяЗапроса        = ПараметрыЗаписи.ИмяЗапроса;
	
	Если Организация = Неопределено Тогда
		Организация = ОбщегоНазначенияИС.ПустоеЗначениеОпределяемогоТипа("Организация");
	КонецЕсли;
	
	Если Подразделение = Неопределено Тогда
		Подразделение = ОбщегоНазначенияИС.ПустоеЗначениеОпределяемогоТипа("Подразделение");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИмяЗапроса.Установить(ПараметрыЗаписи.ИмяЗапроса);
	НаборЗаписей.Отбор.Организация.Установить(Организация);
	НаборЗаписей.Отбор.Подразделение.Установить(Подразделение);
	
	Если ДатаСинхронизации <> Неопределено И ДатаСинхронизации > '00010101' Тогда
		
		ЗаписьНабора = НаборЗаписей.Добавить();
		ЗаписьНабора.ИмяЗапроса        = ИмяЗапроса;
		ЗаписьНабора.ДатаСинхронизации = ДатаСинхронизации;
		ЗаписьНабора.Организация       = Организация;
		ЗаписьНабора.Подразделение     = Подразделение;
		ЗаписьНабора.ДатаОбмена        = ТекущаяУниверсальнаяДата();
		
		НаборЗаписей.Записать();
		
	Иначе
		
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Выбран()
			И НаборЗаписей.Количество() > 0 Тогда
			
			Для Каждого ЗаписьНабора Из НаборЗаписей Цикл
				ЗаписьНабора.ДатаОбмена = ТекущаяУниверсальнаяДата();
			КонецЦикла;
			
			НаборЗаписей.Записать();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли