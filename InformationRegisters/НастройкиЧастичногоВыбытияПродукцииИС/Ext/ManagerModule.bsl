#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьОписание(Номенклатура, Характеристика) Экспорт
	
	ВозвращаемоеЗначение = Новый  Структура;
	ВозвращаемоеЗначение.Вставить("НоменклатураНастроена",           Ложь);
	ВозвращаемоеЗначение.Вставить("НоменклатураЧастичногоВыбытия",   ОбщегоНазначенияИС.ПустоеЗначениеОпределяемогоТипа("Номенклатура"));
	ВозвращаемоеЗначение.Вставить("ХарактеристикаЧастичногоВыбытия", ОбщегоНазначенияИС.ПустоеЗначениеОпределяемогоТипа("ХарактеристикаНоменклатуры"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИСТИНА                                                                КАК НоменклатураНастроена,
	|	НаборНастройкиЧастичногоВыбытия.НоменклатураЧастичногоВыбытия   КАК НоменклатураЧастичногоВыбытия,
	|	НаборНастройкиЧастичногоВыбытия.ХарактеристикаЧастичногоВыбытия КАК ХарактеристикаЧастичногоВыбытия
	|ИЗ
	|	РегистрСведений.НастройкиЧастичногоВыбытияПродукцииИС КАК НаборНастройкиЧастичногоВыбытия
	|ГДЕ
	|	НаборНастройкиЧастичногоВыбытия.Номенклатура = &Номенклатура
	|	И НаборНастройкиЧастичногоВыбытия.Характеристика = &Характеристика";
	
	Запрос.УстановитьПараметр("Номенклатура",   Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ВозвращаемоеЗначение, ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ЭтоНастроенноеВыбытие(Номенклатура, Характеристика, НоменклатураЧастичногоВыбытия, ХарактеристикаЧастичногоВыбытия) Экспорт
	
	Описание = ПолучитьОписание(Номенклатура, Характеристика);
	
	Возврат (Описание.НоменклатураНастроена
		И Описание.НоменклатураЧастичногоВыбытия = НоменклатураЧастичногоВыбытия
		И Описание.ХарактеристикаЧастичногоВыбытия = ХарактеристикаЧастичногоВыбытия);
	
КонецФункции

Функция ОсновнаяНоменклатураЧастичногоВыбытия(Номенклатура) Экспорт
	
	Если Не ЗначениеЗаполнено(Номенклатура) Тогда
		Таблица = Новый ТаблицаЗначений;
		Таблица.Колонки.Добавить("Номенклатура");
		Возврат Таблица;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НастройкиЧастичногоВыбытия.Номенклатура                Как Номенклатура,
		|	ПРЕДСТАВЛЕНИЕ(НастройкиЧастичногоВыбытия.Номенклатура) КАК НоменклатураПредставление
		|ИЗ
		|	РегистрСведений.НастройкиЧастичногоВыбытияПродукцииИС КАК НастройкиЧастичногоВыбытия
		|ГДЕ
		|	НастройкиЧастичногоВыбытия.НоменклатураЧастичногоВыбытия = &НоменклатураЧастичногоВыбытия";
	
	Запрос.УстановитьПараметр("НоменклатураЧастичногоВыбытия", Номенклатура);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	РезультатЗапроса.Сортировать("НоменклатураПредставление");
	
	Возврат РезультатЗапроса;
	
КонецФункции

Функция ДанныеНоменклатурыЧастичногоВыбытия(Номенклатура) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("НоменклатураЧастичногоВыбытия");
	Результат.Вставить("НоменклатураЧастичногоВыбытияПредставление");
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Номенклатура", Метаданные.ОпределяемыеТипы.Номенклатура.Тип);
	Таблица.Колонки.Добавить("НоменклатураПредставление", Новый ОписаниеТипов("Строка"));
	Результат.Вставить("ОсновнаяНоменклатура", Таблица);
	
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НастройкиЧастичногоВыбытия.Номенклатура                КАК Номенклатура,
		|	ПРЕДСТАВЛЕНИЕ(НастройкиЧастичногоВыбытия.Номенклатура) КАК НоменклатураПредставление
		|ИЗ
		|	РегистрСведений.НастройкиЧастичногоВыбытияПродукцииИС КАК НастройкиЧастичногоВыбытия
		|ГДЕ
		|	НастройкиЧастичногоВыбытия.НоменклатураЧастичногоВыбытия = &Номенклатура
		|;
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиЧастичногоВыбытия.НоменклатураЧастичногоВыбытия                КАК НоменклатураЧастичногоВыбытия,
		|	ПРЕДСТАВЛЕНИЕ(НастройкиЧастичногоВыбытия.НоменклатураЧастичногоВыбытия) КАК НоменклатураЧастичногоВыбытияПредставление
		|ИЗ
		|	РегистрСведений.НастройкиЧастичногоВыбытияПродукцииИС КАК НастройкиЧастичногоВыбытия
		|ГДЕ
		|	НастройкиЧастичногоВыбытия.Номенклатура = &Номенклатура";
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		Выборка = РезультатЗапроса[0].Выбрать();
		Пока Выборка.Следующий() Цикл
			Строка = Результат.ОсновнаяНоменклатура.Добавить();
			ЗаполнитьЗначенияСвойств(Строка, Выборка);
		КонецЦикла;
		
		Если Результат.ОсновнаяНоменклатура.Количество() > 1 Тогда
			Результат.ОсновнаяНоменклатура.Сортировать("НоменклатураПредставление");
		КонецЕсли;
		
		Выборка = РезультатЗапроса[1].Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(Результат, Выборка);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция НастроеннаяНоменклатураЧастичногоВыбытия(СписокНоменклатуры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиЧастичногоВыбытияПродукцииИС.Номенклатура                    КАК Номенклатура,
		|	НастройкиЧастичногоВыбытияПродукцииИС.Характеристика                  КАК Характеристика,
		|	НастройкиЧастичногоВыбытияПродукцииИС.НоменклатураЧастичногоВыбытия   КАК НоменклатураЧастичногоВыбытия,
		|	НастройкиЧастичногоВыбытияПродукцииИС.ХарактеристикаЧастичногоВыбытия КАК ХарактеристикаЧастичногоВыбытия
		|ИЗ
		|	РегистрСведений.НастройкиЧастичногоВыбытияПродукцииИС КАК НастройкиЧастичногоВыбытияПродукцииИС
		|ГДЕ
		|	НастройкиЧастичногоВыбытияПродукцииИС.Номенклатура В (&СписокНоменклатуры)";
	
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	ТаблицаДанных.Индексы.Добавить("Номенклатура,Характеристика");
	
	Возврат ТаблицаДанных;
	
КонецФункции

#КонецОбласти

#КонецЕсли