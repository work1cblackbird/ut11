
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Запись, ЭтотОбъект);
	
	Если ЗначениеЗаполнено(Запись.Договор) Тогда
		УстановитьДоступностьПоДоговору();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	УстановитьДоступностьПоДоговору();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ДатаИзменения = ТекущаяДатаСеанса();
	ТекущийОбъект.АвторИзменения = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	СтавкиДоговора = Новый Структура("Договор, Процент, Комиссия");
	ЗаполнитьЗначенияСвойств(СтавкиДоговора, Запись);
	ПараметрыЗаписи = Новый Структура("СтавкиДоговора", СтавкиДоговора);
	Оповестить("Запись_ПроцентныеСтавкиФинансовыхИнструментов", ПараметрыЗаписи, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДоговорПриИзменении(Элемент)
	
	УстановитьДоступностьПоДоговору();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура УстановитьДоступностьПоДоговору()
	
	Если ТипЗнч(Запись.Договор) = Тип("СправочникСсылка.ДоговорыКредитовИДепозитов") Тогда
		
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Запись.Договор, "Статус, ТипКомиссии, ВалютаВзаиморасчетов");
		
		Элементы.НадписьВалюта.Заголовок = Строка(РеквизитыДоговора.ВалютаВзаиморасчетов);
		
		Если РеквизитыДоговора.ТипКомиссии = Перечисления.ТипыКомиссииКредитовИДепозитов.Сумма Тогда
			Элементы.СтраницыКомиссия.ТекущаяСтраница = Элементы["Суммой"];
			Элементы.СтраницыКомиссия.Видимость = Истина;
		ИначеЕсли РеквизитыДоговора.ТипКомиссии = Перечисления.ТипыКомиссииКредитовИДепозитов.Процент Тогда
			Элементы.СтраницыКомиссия.ТекущаяСтраница = Элементы["Процентом"];
			Элементы.СтраницыКомиссия.Видимость = Истина;
		Иначе
			Элементы.СтраницыКомиссия.Видимость = Ложь;
		КонецЕсли;
	Иначе
		
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Запись.Договор, "Статус");
	КонецЕсли;
	
	ДоговорЗакрыт = (РеквизитыДоговора.Статус = Перечисления.СтатусыДоговоровКонтрагентов.Закрыт);
	Элементы.НадписьДоговорЗакрыт.Видимость = ДоговорЗакрыт;
	ТолькоПросмотр = ДоговорЗакрыт;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
