#Область ОписаниеПеременных

&НаКлиенте
Перем ОбработчикТаймера; // Обработка оповещения для обратного вызова

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Повторять = Параметры.Повторять;
	Идентификатор = Параметры.Идентификатор;
	КоличествоСекунд = Параметры.КоличествоСекунд;
	ОписаниеОперации = Новый ФорматированнаяСтрока(Параметры.ОписаниеОперации);
	Открывать = Параметры.Открывать;
	
	Элементы.ДекорацияОписание.Заголовок = ОписаниеОперации;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не Открывать Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ОтключитьТаймер("", Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Источник <> ЭлектронноеАктированиеЕИСГИСНРКлиент.ИдентификаторТаймера() Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяСобытия = "ВключитьТаймер" Тогда
		Повторять = Параметр.Повторять;
		Идентификатор = Параметр.Идентификатор;
		КоличествоСекунд = Параметр.КоличествоСекунд;
		ОбработчикТаймера = Параметр.Обработчик;
		ОписаниеОперации = Новый ФорматированнаяСтрока(Параметр.ОписаниеОперации);
		Элементы.ДекорацияОписание.Заголовок = ОписаниеОперации;
		ВключитьТаймер();
	ИначеЕсли ИмяСобытия = "ОтключитьТаймер" Тогда
		ОтключитьТаймер(Параметр);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ВключитьТаймер()
	
	Результат = Идентификатор = ЭлектронноеАктированиеЕИСГИСНРКлиент.ИдентификаторТаймера();
	
	Если Результат И ОбработчикТаймера <> Неопределено Тогда
		ПодключитьОбработчикОжидания("ОчереднойТикТаймера", КоличествоСекунд, НЕ Повторять);
	КонецЕсли;
	
	Если Открыта() Тогда
		Активизировать();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОтключитьТаймер(Параметр, ПриЗакрытии = Ложь)
	
	ОтключитьОбработчикОжидания("ОчереднойТикТаймера");
	
	Если Параметр <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОбработчикТаймера, Параметр);
	КонецЕсли;
	
	Если Открыта() И НЕ ПриЗакрытии Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчереднойТикТаймера()
	
	ВыполнитьОбработкуОповещения(ОбработчикТаймера, Истина);
	Если НЕ Повторять Тогда
		ОтключитьТаймер("", Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
