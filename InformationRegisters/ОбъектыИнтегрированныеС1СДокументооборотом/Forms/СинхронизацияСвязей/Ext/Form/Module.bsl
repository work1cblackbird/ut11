#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СокращенноеНаименованиеКонфигурации =
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.СокращенноеНаименованиеКонфигурации();
	Если ЗначениеЗаполнено(СокращенноеНаименованиеКонфигурации) Тогда
		ШаблонПояснения = НСтр(
			"ru = 'В базу 1С:Документооборот будут добавлены недостающие ссылки на объекты %1.
			|В базу %1 будут добавлены недостающие ссылки на объекты 1С:Документооборота.
			|Внимание, операция может занять продолжительное время. Во время выполнения операции бесшовная интеграция с 1С:Документооборотом будет ограничена.'");
		
		Элементы.Пояснение.Заголовок = СтрШаблон(ШаблонПояснения, СокращенноеНаименованиеКонфигурации);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПроверитьПодключение();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИнтеграцияС1СДокументооборотом_УспешноеПодключение" И Источник <> ЭтотОбъект Тогда
		ПриПодключении();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияНастройкиАвторизацииНажатие(Элемент)
	
	Оповещение = Новый ОписаниеОповещения("ДекорацияНастройкиАвторизацииНажатиеЗавершение", ЭтотОбъект);
	ИмяФормыПараметров = "Обработка.ИнтеграцияС1СДокументооборотБазоваяФункциональность.Форма.АвторизацияВ1СДокументооборот";
	
	ОткрытьФорму(ИмяФормыПараметров,, ЭтотОбъект,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкиАвторизацииНажатиеЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Истина Тогда
		ПриПодключении();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Синхронизировать(Команда)
	
	Если ОперацияЗавершена Тогда
		Закрыть(Новый Структура("ЛишниеСвязиДО, ЛишниеСвязиИС", ЛишниеСвязиДО, ЛишниеСвязиИС));
		Возврат;
	КонецЕсли;
	
	ЛишниеСвязиДО.Очистить();
	ЛишниеСвязиИС.Очистить();
	ОперацияЗавершена = Ложь;
	
	НачатьСинхронизацию();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область НачатьСинхронизацию

&НаКлиенте
Процедура НачатьСинхронизацию()
	
	ДлительнаяОперация = НачатьСинхронизациюНаСервере();
	
	Элементы.Синхронизировать.Доступность = Ложь;
	ТекущееСостояние = НСтр("ru = 'Подготовка данных для выполнения операции'");
	Прогресс = 0;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("НачатьСинхронизациюЗавершение", ЭтотОбъект);
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьЗапросАсинхронно(
		ЭтотОбъект,
		ДлительнаяОперация,
		ОповещениеОЗавершении,,,
		Ложь);
	
КонецПроцедуры

&НаСервере
Функция НачатьСинхронизациюНаСервере()
	
	Попытка
		
		Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
		
		Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(
			Прокси,
			"DMClearFeedbackMarkInObjectLinksRequest");
		
		Возврат ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапросАсинхронно(
			Прокси,
			Запрос,
			УникальныйИдентификатор,
			НСтр("ru = 'Подготовка данных для синхронизации связей'"));
		
	Исключение
		
		ОбработатьИсключение(ИнформацияОбОшибке());
		
	КонецПопытки;
	
КонецФункции

&НаКлиенте
Процедура НачатьСинхронизациюЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Элементы.Синхронизировать.Доступность = Истина;
	
	Если Результат = Неопределено Тогда
		ТекущееСостояние = "";
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатНачалаСинхронизации = НачатьСинхронизациюНаСервереЗавершение(Результат.РезультатДлительнойОперации);
		Если РезультатНачалаСинхронизации.Успешно Тогда
			ВыполнитьСинхронизацию();
		Иначе
			ОбработатьИсключение(РезультатНачалаСинхронизации.ТекстПредупреждения);
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		
		ОбработатьИсключение(Результат.КраткоеПредставлениеОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НачатьСинхронизациюНаСервереЗавершение(ОтветНаЗапросСтрока)
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	ОтветНаЗапрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СтрокаВОбъектXDTO(
		Прокси,
		ОтветНаЗапросСтрока);
	
	Результат = Новый Структура("Успешно, ТекстПредупреждения", Истина, "");
	
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьТип(Прокси, ОтветНаЗапрос, "DMError") Тогда
		Результат.Успешно = Ложь;
		Результат.ТекстПредупреждения = СтрШаблон(
			"%1
			|
			|%2",
			СокрЛП(ОтветНаЗапрос.subject),
			СокрЛП(ОтветНаЗапрос.description));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ВыполнитьСинхронизацию

&НаКлиенте
Процедура ВыполнитьСинхронизацию()
	
	ИнтегрированныеОбъекты = ИнтегрированныеОбъекты();
	КоличествоОбъектов = ИнтегрированныеОбъекты.Количество();
	
	Если КоличествоОбъектов = 0 Тогда
		ЗавершитьСинхронизацию();
		Возврат;
	КонецЕсли;
	
	Индекс = 0;
	РазмерПорции = 500;
	
	СинхронизироватьПорциюОбъектов(ИнтегрированныеОбъекты, КоличествоОбъектов, Индекс, РазмерПорции);
	
КонецПроцедуры

&НаКлиенте
Процедура СинхронизироватьПорциюОбъектов(ИнтегрированныеОбъекты, КоличествоОбъектов, Индекс, РазмерПорции)
	
	Прогресс = Окр((100 * Индекс) / КоличествоОбъектов);
	
	Если Индекс = КоличествоОбъектов Тогда
		ЗавершитьСинхронизацию();
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = СинхронизироватьПорциюОбъектовНаСервере(
		ИнтегрированныеОбъекты,
		КоличествоОбъектов,
		Индекс,
		РазмерПорции);
	
	Элементы.Синхронизировать.Доступность = Ложь;
	ТекущееСостояние = НСтр("ru = 'Выполнение операции'");
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ИнтегрированныеОбъекты", ИнтегрированныеОбъекты);
	ПараметрыОповещения.Вставить("КоличествоОбъектов", КоличествоОбъектов);
	ПараметрыОповещения.Вставить("Индекс", Индекс);
	ПараметрыОповещения.Вставить("РазмерПорции", РазмерПорции);
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"СинхронизироватьПорциюОбъектовЗавершение",
		ЭтотОбъект,
		ПараметрыОповещения);
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьЗапросАсинхронно(
		ЭтотОбъект,
		ДлительнаяОперация,
		ОповещениеОЗавершении,,,
		Ложь);
	
КонецПроцедуры

&НаСервере
Функция СинхронизироватьПорциюОбъектовНаСервере(ИнтегрированныеОбъекты, КоличествоОбъектов, Индекс, РазмерПорции)
	
	Попытка
		
		Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
		
		Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(
			Прокси,
			"DMSynchronizeObjectLinksRequest");
		СвязиXDTO = Запрос.links; // СписокXDTO
		
		Для Индекс = Индекс По (Индекс + РазмерПорции - 1) Цикл
			Если Индекс > (КоличествоОбъектов - 1) Тогда
				Прервать;
			КонецЕсли;
			
			СвязьИС = ИнтегрированныеОбъекты[Индекс];
			
			СвязиXDTO.Добавить(
				ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьDMLink(
					Прокси,
					СвязьИС.ИдентификаторОбъектаДО,
					СвязьИС.ТипОбъектаДО,
					СвязьИС.ОбъектИС));
		КонецЦикла;
		
		Возврат ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапросАсинхронно(
			Прокси,
			Запрос,
			УникальныйИдентификатор,
			НСтр("ru = 'Синхронизация связей'"));
		
	Исключение
		
		ОбработатьИсключение(ИнформацияОбОшибке());
		
	КонецПопытки;
	
КонецФункции

// Обработчик оповещения о завершении длительной операции.
//
// Параметры:
//   РезультатЗапроса - см. ДлительныеОперации.ОперацияВыполнена
//   ПараметрыОповещения - Структура:
//     * ИнтегрированныеОбъекты - Массив из Структура:
//       ** ОбъектИС - ЛюбаяСсылка
//       ** ИдентификаторОбъектаДО - Строка
//       ** ТипОбъектаДО - Строка
//     * КоличествоОбъектов - Число
//     * Индекс - Число
//     * РазмерПорции - Число
//
&НаКлиенте
Процедура СинхронизироватьПорциюОбъектовЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Элементы.Синхронизировать.Доступность = Истина;
	
	Если Результат = Неопределено Тогда
		ТекущееСостояние = "";
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатСинхронизации = СинхронизироватьПорциюОбъектовНаСервереЗавершение(Результат.РезультатДлительнойОперации);
		Если РезультатСинхронизации.Успешно Тогда
			СинхронизироватьПорциюОбъектов(
				ПараметрыОповещения.ИнтегрированныеОбъекты,
				ПараметрыОповещения.КоличествоОбъектов,
				ПараметрыОповещения.Индекс,
				ПараметрыОповещения.РазмерПорции);
		Иначе
			ОбработатьИсключение(РезультатСинхронизации.ТекстПредупреждения);
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		
		ОбработатьИсключение(Результат.КраткоеПредставлениеОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СинхронизироватьПорциюОбъектовНаСервереЗавершение(ОтветНаЗапросСтрока)
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	ОтветНаЗапрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СтрокаВОбъектXDTO(
		Прокси,
		ОтветНаЗапросСтрока);
	
	Результат = Новый Структура("Успешно, ТекстПредупреждения", Истина, "");
	
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьТип(Прокси, ОтветНаЗапрос, "DMError") Тогда
		Результат.Успешно = Ложь;
		Результат.ТекстПредупреждения = СтрШаблон(
			"%1
			|
			|%2",
			СокрЛП(ОтветНаЗапрос.subject),
			СокрЛП(ОтветНаЗапрос.description));
	Иначе
		Для Каждого СвязьXDTO Из ОтветНаЗапрос.brokenLinks Цикл
			СвязьИС = ЛишниеСвязиИС.Добавить();
			СвязьИС.ОбъектИС = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СсылкаИзUUID(
				СвязьXDTO.ownerObject.type,
				СвязьXDTO.ownerObject.id);
			СвязьИС.ИдентификаторОбъектаДО = СвязьXDTO.linkedObject.id;
			СвязьИС.ТипОбъектаДО = СвязьXDTO.linkedObject.type;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ЗавершитьСинхронизацию

&НаКлиенте
Процедура ЗавершитьСинхронизацию()
	
	ДлительнаяОперация = ЗавершитьСинхронизациюНаСервере();
	
	Элементы.Синхронизировать.Доступность = Ложь;
	ТекущееСостояние = НСтр("ru = 'Завершение выполнения операции'");
	Прогресс = 100;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗавершитьСинхронизациюЗавершение", ЭтотОбъект);
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьЗапросАсинхронно(
		ЭтотОбъект,
		ДлительнаяОперация,
		ОповещениеОЗавершении,,,
		Ложь);
	
КонецПроцедуры

&НаСервере
Функция ЗавершитьСинхронизациюНаСервере()
	
	Попытка
		
		Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
		
		Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(
			Прокси,
			"DMGetObjectLinksWithoutFeedbackMarkRequest");
		
		Возврат ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапросАсинхронно(
			Прокси,
			Запрос,
			УникальныйИдентификатор,
			НСтр("ru = 'Завершение синхронизации связей'"));
		
	Исключение
		
		ОбработатьИсключение(ИнформацияОбОшибке());
		
	КонецПопытки;
	
КонецФункции

&НаКлиенте
Процедура ЗавершитьСинхронизациюЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Элементы.Синхронизировать.Доступность = Истина;
	
	Если Результат = Неопределено Тогда
		ТекущееСостояние = "";
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатЗавершенияСинхронизации = ЗавершитьСинхронизациюНаСервереЗавершение(Результат.РезультатДлительнойОперации);
		Если РезультатЗавершенияСинхронизации.Успешно Тогда
			ОперацияЗавершена = Истина;
			ТекущееСостояние = НСтр("ru = 'Синхронизация успешно выполнена'");
			Элементы.Синхронизировать.Заголовок = НСтр("ru = 'Завершить'");
		Иначе
			ОбработатьИсключение(РезультатЗавершенияСинхронизации.ТекстПредупреждения);
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		
		ОбработатьИсключение(Результат.КраткоеПредставлениеОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗавершитьСинхронизациюНаСервереЗавершение(ОтветНаЗапросСтрока)
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	ОтветНаЗапрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СтрокаВОбъектXDTO(
		Прокси,
		ОтветНаЗапросСтрока);
	
	Результат = Новый Структура("Успешно, ТекстПредупреждения", Истина, "");
	
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьТип(Прокси, ОтветНаЗапрос, "DMError") Тогда
		Результат.Успешно = Ложь;
		Результат.ТекстПредупреждения = СтрШаблон(
			"%1
			|
			|%2",
			СокрЛП(ОтветНаЗапрос.subject),
			СокрЛП(ОтветНаЗапрос.description));
	Иначе
		
		Для Каждого СвязьXDTO Из ОтветНаЗапрос.links Цикл
			
			ОбъектИСНеНайден = Ложь;
			
			Попытка
				СсылкаНаОбъектИС = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СсылкаИзUUID(
					СвязьXDTO.ownerObject.type,
					СвязьXDTO.ownerObject.ID);
			Исключение
				ОбъектИСНеНайден = Истина;
			КонецПопытки;
			
			Если (ОбъектИСНеНайден = Ложь)
					И (СсылкаНаОбъектИС = Неопределено
						Или Не ОбщегоНазначения.СсылкаСуществует(СсылкаНаОбъектИС)) Тогда
				ОбъектИСНеНайден = Истина;
			КонецЕсли;
			
			Если ОбъектИСНеНайден Тогда
				СвязьДО = ЛишниеСвязиДО.Добавить();
				СвязьДО.ОбъектДО = СвязьXDTO.linkedObject.navigationRef;
				СвязьДО.ПредставлениеОбъектаДО = СвязьXDTO.linkedObject.presentation;
				СвязьДО.ИдентификаторОбъектаДО = СвязьXDTO.linkedObject.id;
				СвязьДО.ТипОбъектаДО = СвязьXDTO.linkedObject.type;
				СвязьДО.ИдентификаторОбъектаИС = СвязьXDTO.ownerObject.id;
				СвязьДО.ТипОбъектаИС = СвязьXDTO.ownerObject.type;
				Продолжить;
			КонецЕсли;
			
			НачатьТранзакцию();
			Попытка
				БлокировкаДанных = Новый БлокировкаДанных;
				ЭлементБлокировкиДанных = БлокировкаДанных.Добавить(
					"РегистрСведений.ОбъектыИнтегрированныеС1СДокументооборотом");
				ЭлементБлокировкиДанных.УстановитьЗначение("ТипОбъектаДО", СвязьXDTO.linkedObject.type);
				ЭлементБлокировкиДанных.УстановитьЗначение("ИдентификаторОбъектаДО", СвязьXDTO.linkedObject.id);
				ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
				БлокировкаДанных.Заблокировать();
				
				НаборЗаписей = РегистрыСведений.ОбъектыИнтегрированныеС1СДокументооборотом.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.ТипОбъектаДО.Установить(СвязьXDTO.linkedObject.type);
				НаборЗаписей.Отбор.ИдентификаторОбъектаДО.Установить(СвязьXDTO.linkedObject.id);
				НаборЗаписей.Прочитать();
				Для Каждого Запись Из НаборЗаписей Цикл
					Если Не ЗначениеЗаполнено(Запись.Объект) Тогда
						// Существующий объект ДО не должен ссылаться на пустой объект ИС.
						НаборЗаписей.Удалить(Запись);
					КонецЕсли;
				КонецЦикла;
				НаборЗаписей.Записать();
				
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаписатьОшибку(
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ВызватьИсключение;
			КонецПопытки;
			
			РегистрыСведений.ОбъектыИнтегрированныеС1СДокументооборотом.ДобавитьСвязь(
				СвязьXDTO.linkedObject.id,
				СвязьXDTO.linkedObject.type,
				СсылкаНаОбъектИС,
				Ложь);
			
		КонецЦикла;
		
		НачатьТранзакцию();
		Попытка
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить(
				"РегистрСведений.ОбъектыИнтегрированныеС1СДокументооборотом");
			ЭлементБлокировкиДанных.УстановитьЗначение("Объект", Неопределено);
			ЭлементБлокировкиДанных.УстановитьЗначение("ТипОбъектаДО", "");
			ЭлементБлокировкиДанных.УстановитьЗначение("ИдентификаторОбъектаДО", "");
			ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
			БлокировкаДанных.Заблокировать();
			
			ЗапросПустыеСвязи = Новый Запрос(
				"ВЫБРАТЬ
				|	ОбъектыИнтегрированныеС1СДокументооборотом.Объект КАК Объект,
				|	ОбъектыИнтегрированныеС1СДокументооборотом.ТипОбъектаДО КАК ТипОбъектаДО,
				|	ОбъектыИнтегрированныеС1СДокументооборотом.ИдентификаторОбъектаДО КАК ИдентификаторОбъектаДО
				|ИЗ
				|	РегистрСведений.ОбъектыИнтегрированныеС1СДокументооборотом КАК ОбъектыИнтегрированныеС1СДокументооборотом
				|ГДЕ
				|	(ОбъектыИнтегрированныеС1СДокументооборотом.Объект = НЕОПРЕДЕЛЕНО
				|			ИЛИ ОбъектыИнтегрированныеС1СДокументооборотом.ТипОбъектаДО = """"
				|			ИЛИ ОбъектыИнтегрированныеС1СДокументооборотом.ИдентификаторОбъектаДО = """")");
			Выборка = ЗапросПустыеСвязи.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				Если Не ЗначениеЗаполнено(Выборка.ИдентификаторОбъектаДО) Или Не ЗначениеЗаполнено(Выборка.ТипОбъектаДО) Тогда
					Если ЗначениеЗаполнено(Выборка.Объект) Тогда
						СвязьИС = ЛишниеСвязиИС.Добавить();
						СвязьИС.ОбъектИС = Выборка.Объект;
						СвязьИС.ИдентификаторОбъектаДО = Выборка.ИдентификаторОбъектаДО;
						СвязьИС.ТипОбъектаДО = Выборка.ТипОбъектаДО;
					Иначе
						// Невосстановимая связь.
						Запись = РегистрыСведений.ОбъектыИнтегрированныеС1СДокументооборотом.СоздатьМенеджерЗаписи();
						ЗаполнитьЗначенияСвойств(Запись, Выборка);
						Запись.Удалить();
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаписатьОшибку(
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ВызватьИсключение;
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

&НаСервере
Функция ИнтегрированныеОбъекты()
	
	ИнтегрированныеОбъекты = Новый Массив;
	
	ЗапросИнтегрированныеОбъекты = Новый Запрос(
		"ВЫБРАТЬ
		|	ОбъектыИнтегрированныеС1СДокументооборотом.Объект КАК Объект,
		|	ОбъектыИнтегрированныеС1СДокументооборотом.ТипОбъектаДО КАК ТипОбъектаДО,
		|	ОбъектыИнтегрированныеС1СДокументооборотом.ИдентификаторОбъектаДО КАК ИдентификаторОбъектаДО
		|ИЗ
		|	РегистрСведений.ОбъектыИнтегрированныеС1СДокументооборотом КАК ОбъектыИнтегрированныеС1СДокументооборотом
		|ГДЕ
		|	(ОбъектыИнтегрированныеС1СДокументооборотом.ТипОбъектаДО = ""DMDocument""
		|			ИЛИ ОбъектыИнтегрированныеС1СДокументооборотом.ТипОбъектаДО = ""DMInternalDocument""
		|			ИЛИ ОбъектыИнтегрированныеС1СДокументооборотом.ТипОбъектаДО = ""DMIncomingDocument""
		|			ИЛИ ОбъектыИнтегрированныеС1СДокументооборотом.ТипОбъектаДО = ""DMOutgoingDocument""
		|			ИЛИ ОбъектыИнтегрированныеС1СДокументооборотом.ТипОбъектаДО = ""DMCorrespondent""
		|			ИЛИ ОбъектыИнтегрированныеС1СДокументооборотом.ТипОбъектаДО = ""DMMeeting"")
		|	И ОбъектыИнтегрированныеС1СДокументооборотом.ИдентификаторОбъектаДО <> """"");
	
	ТипВсеСсылки = Метаданные.ОпределяемыеТипы.ИнтеграцияС1СДокументооборотВсеСсылкиПереопределяемый.Тип;
	
	Выборка = ЗапросИнтегрированныеОбъекты.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Не ЗначениеЗаполнено(Выборка.Объект) Или Не ТипВсеСсылки.СодержитТип(ТипЗнч(Выборка.Объект)) Тогда
			Продолжить;
		КонецЕсли;
		
		ИнтегрированныеОбъекты.Добавить(
			СвязьИС(
				Выборка.Объект,
				Выборка.ИдентификаторОбъектаДО,
				Выборка.ТипОбъектаДО));
	КонецЦикла;
	
	Возврат ИнтегрированныеОбъекты;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СвязьИС(ОбъектИС, ИдентификаторОбъектаДО, ТипОбъектаДО)
	
	СвязьИС = Новый Структура;
	СвязьИС.Вставить("ОбъектИС", ОбъектИС);
	СвязьИС.Вставить("ИдентификаторОбъектаДО", ИдентификаторОбъектаДО);
	СвязьИС.Вставить("ТипОбъектаДО", ТипОбъектаДО);
	
	Возврат СвязьИС;
	
КонецФункции

// Проверяет подключение к ДО, выводя окно авторизации, если необходимо, и изменяя форму согласно результату.
//
&НаКлиенте
Процедура ПроверитьПодключение()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьПодключениеЗавершение", ЭтотОбъект);
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ПроверитьПодключение(
		ОписаниеОповещения,
		ЭтотОбъект,,
		Ложь,
		Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодключениеЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Истина Тогда
		ПриПодключении();
	Иначе // Не удалось подключиться к ДО.
		ОбработатьФормуСогласноВерсииСервиса();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПодключении()
	
	ОбработатьФормуСогласноВерсииСервиса();
	
КонецПроцедуры

&НаСервере
Функция ОбработатьФормуСогласноВерсииСервиса()
	
	ВерсияСервиса = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВерсияСервиса();
	
	Если Не ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СервисДоступен(ВерсияСервиса) Тогда
		Элементы.ГруппаСтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаДокументооборотНедоступен;
		Возврат Ложь;
	КонецЕсли;
	
	ФормаОбработанаУспешно = Истина;
	
	Попытка
		
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ДоступенФункционалВерсииСервиса("3.0.11.25") Тогда
			
			Элементы.ГруппаСтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаДокументооборотДоступен;
			
		Иначе
			
			Элементы.ГруппаСтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаВерсияНеПоддерживается;
			ФормаОбработанаУспешно = Ложь;
			
		КонецЕсли;
		
	Исключение
		
		ОбработатьИсключение(ИнформацияОбОшибке());
		
	КонецПопытки;
	
	Возврат ФормаОбработанаУспешно;
	
КонецФункции

&НаСервере
Процедура ОбработатьИсключение(Знач ИнформацияОбОшибке)
	
	ТекущееСостояние = "";
	Прогресс = 0;
	
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.НужноОбработатьФорму(ИнформацияОбОшибке) Тогда
		ОбработатьФормуСогласноВерсииСервиса();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти