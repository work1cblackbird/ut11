// @strict-types
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		Запись.ДатаВыгрузки = Неопределено;
	КонецЕсли;

	ВосстановитьНастройки();

	Если Не ЗначениеЗаполнено(ВыбранныеПоляПредставления) Тогда
		ВыбранныеПоляПредставления = "Наименование, Характеристика";
	КонецЕсли;

	ИнтеграцияСЭлектроннымиТорговымиПлощадкамиПереопределяемый.ПриСозданииНаСервере(ЭтаФорма, Отказ,
		СтандартнаяОбработка);

	УстановитьУсловноеОформление();

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ИнтеграцияСЭлектроннымиТорговымиПлощадкамиПереопределяемый.ПриОпределенииОбязательностиЗаполненияХарактеристики(ТекущийОбъект, Элементы.Характеристика, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Запись.Номенклатура) Тогда
		Элементы.Характеристика.Доступность = Истина;
		ОбработатьСтрокуДанныхВыгрузки();
	Иначе
		Элементы.Характеристика.Доступность = Ложь;
		Запись.ПредставлениеНоменклатуры = "";
		Запись.Статус = ПредопределенноеЗначение("Перечисление.СтатусыОбъектовЭТП.КПубликации");
		Запись.ДатаВыгрузки = Неопределено;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ХарактеристикаПриИзменении(Элемент)

	ОбработатьСтрокуДанныхВыгрузки();

КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеНоменклатурыПриИзменении(Элемент)

	Запись.Статус = ПредопределенноеЗначение("Перечисление.СтатусыОбъектовЭТП.КПубликации");
	Запись.ДатаВыгрузки = Неопределено;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбработатьСтрокуДанныхВыгрузки()

	ВыбранныеПоля = СтрРазделить(ВыбранныеПоляПредставления, ", ", Ложь); // Массив из Строка

	РеквизитыСтрокой = "";
	Для Каждого Поле Из ВыбранныеПоля Цикл
		Если Поле <> "Характеристика" Тогда
			РеквизитыСтрокой = ?(ПустаяСтрока(РеквизитыСтрокой), Поле, РеквизитыСтрокой + ", " + Поле);
		КонецЕсли;
	КонецЦикла;

	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Запись.Номенклатура, РеквизитыСтрокой);

	МассивПредставления = Новый Массив; // Массив из Строка
	Значение = "";
	Для Каждого Поле Из ВыбранныеПоля Цикл
		Если Поле = "Характеристика" Тогда
			Значение = Строка(Запись.Характеристика);
		Иначе
			Реквизиты.Свойство(Поле, Значение);
		КонецЕсли;
		Если ЗначениеЗаполнено(Значение) Тогда
			МассивПредставления.Добавить(Значение);
		ИначеЕсли СтрНайти(Поле, "Наименование") > 0 Тогда
			МассивПредставления.Добавить(Строка(Запись.Номенклатура));
		КонецЕсли;
	КонецЦикла;
	НовоеПредставление = СтрСоединить(МассивПредставления, ", ");
	Если Запись.ПредставлениеНоменклатуры <> НовоеПредставление Тогда
		Запись.ПредставлениеНоменклатуры = НовоеПредставление;
		Запись.Статус = Перечисления.СтатусыОбъектовЭТП.КПубликации;
		Запись.ДатаВыгрузки = Неопределено;
	КонецЕсли;
	
	Если ТекущийЭлемент = Элементы.Номенклатура Тогда
		ИнтеграцияСЭлектроннымиТорговымиПлощадкамиПереопределяемый.ПриОпределенииОбязательностиЗаполненияХарактеристики(
		Запись, Элементы.Характеристика);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройки()

	Если Не Пользователи.РолиДоступны("СохранениеДанныхПользователя") Тогда
		Возврат;
	КонецЕсли;

	ЗначенияНастроек = ХранилищеОбщихНастроек.Загрузить("Обработка.УправлениеВыгрузкамиВБидзаар.Форма.ВыгрузкаДанных");

	Если ТипЗнч(ЗначенияНастроек) = Тип("Структура") Тогда
		ЗначенияНастроек.Свойство("ВыбранныеПоляПредставления", ВыбранныеПоляПредставления);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	Элементы.УчетнаяЗапись.ТолькоПросмотр = ЗначениеЗаполнено(Запись.УчетнаяЗапись);

КонецПроцедуры

#КонецОбласти