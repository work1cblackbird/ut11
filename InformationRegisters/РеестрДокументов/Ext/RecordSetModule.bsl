#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		ЗаполнитьИдентификаторыЗаписей(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	ЗаполнитьИдентификаторыЗаписей(ЭтотОбъект);
	
	Если Не ПривилегированныйРежим()
		И Не Пользователи.ЭтоПолноправныйПользователь() Тогда
		Отказ = Истина;
	КонецЕсли;

	Если ДополнительныеСвойства.Свойство("МенеджерВременныхТаблиц") Тогда // это проведение или удаление проведения
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	РеестрДокументов.ХозяйственнаяОперация    КАК ХозяйственнаяОперация,
			|	РеестрДокументов.Организация              КАК Организация,
			|	РеестрДокументов.Партнер                  КАК Партнер,
			|	РеестрДокументов.МестоХранения            КАК МестоХранения,
			|	РеестрДокументов.Контрагент               КАК Контрагент,
			|	РеестрДокументов.Подразделение            КАК Подразделение,
			|	РеестрДокументов.ДатаДокументаИБ          КАК ДатаДокументаИБ,
			|	РеестрДокументов.Ссылка                   КАК Ссылка,
			|	РеестрДокументов.НомерДокументаИБ         КАК НомерДокументаИБ,
			|	РеестрДокументов.Статус                   КАК Статус,
			|	РеестрДокументов.Ответственный            КАК Ответственный,
			|	РеестрДокументов.ДополнительнаяЗапись     КАК ДополнительнаяЗапись,
			|	РеестрДокументов.Проведен                 КАК Проведен,
			|	РеестрДокументов.ПометкаУдаления          КАК ПометкаУдаления,
			|	РеестрДокументов.ДатаПервичногоДокумента  КАК ДатаПервичногоДокумента,
			|	РеестрДокументов.НомерПервичногоДокумента КАК НомерПервичногоДокумента,
			|	РеестрДокументов.Сумма                    КАК Сумма,
			|	РеестрДокументов.Валюта                   КАК Валюта,
			|	РеестрДокументов.Договор                  КАК Договор,
			|	РеестрДокументов.НаправлениеДеятельности  КАК НаправлениеДеятельности,
			|	РеестрДокументов.ДатаОтраженияВУчете      КАК ДатаОтраженияВУчете,
			|	РеестрДокументов.СторноИсправление        КАК СторноИсправление,
			|	РеестрДокументов.СторнируемыйДокумент     КАК СторнируемыйДокумент,
			|	РеестрДокументов.ИсправляемыйДокумент     КАК ИсправляемыйДокумент,
			|	РеестрДокументов.Приоритет                КАК Приоритет
			|ПОМЕСТИТЬ РеестрДокументовПередЗаписью
			|ИЗ
			|	РегистрСведений.РеестрДокументов КАК РеестрДокументов
			|ГДЕ
			|	РеестрДокументов.Ссылка = &Ссылка");
		
		Запрос.УстановитьПараметр("Ссылка", Отбор.Ссылка.Значение);
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("МенеджерВременныхТаблиц") Тогда // это проведение или удаление проведения
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	РеестрДокументовИзменения.ХозяйственнаяОперация    КАК ХозяйственнаяОперация,
			|	РеестрДокументовИзменения.Организация              КАК Организация,
			|	РеестрДокументовИзменения.Партнер                  КАК Партнер,
			|	РеестрДокументовИзменения.МестоХранения            КАК МестоХранения,
			|	РеестрДокументовИзменения.Контрагент               КАК Контрагент,
			|	РеестрДокументовИзменения.Подразделение            КАК Подразделение,
			|	РеестрДокументовИзменения.ДатаДокументаИБ          КАК ДатаДокументаИБ,
			|	РеестрДокументовИзменения.Ссылка                   КАК Ссылка,
			|
			|	МАКСИМУМ(ВЫБОР КОГДА РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.НомерДокументаИБ
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК НомерДокументаИБДоИзменения,
			|	МАКСИМУМ(ВЫБОР КОГДА РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.Статус
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК СтатусДоИзменения,
			|	МАКСИМУМ(ВЫБОР КОГДА РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.Ответственный
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК ОтветственныйДоИзменения,
			|	МАКСИМУМ(ВЫБОР КОГДА РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.ДополнительнаяЗапись
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК ДополнительнаяЗаписьДоИзменения,
			|	МАКСИМУМ(ВЫБОР КОГДА РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.Проведен
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК ПроведенДоИзменения,
			|	МАКСИМУМ(ВЫБОР КОГДА РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.ПометкаУдаления
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК ПометкаУдаленияДоИзменения,
			|	МАКСИМУМ(ВЫБОР КОГДА РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.ДатаПервичногоДокумента
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК ДатаПервичногоДокументаДоИзменения,
			|	МАКСИМУМ(ВЫБОР КОГДА РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.НомерПервичногоДокумента
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК НомерПервичногоДокументаДоИзменения,
			|	МАКСИМУМ(ВЫБОР КОГДА РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.Сумма
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК СуммаДоИзменения,
			|	МАКСИМУМ(ВЫБОР КОГДА РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.Валюта
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК ВалютаДоИзменения,
			|	МАКСИМУМ(ВЫБОР КОГДА РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.Договор
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК ДоговорДоИзменения,
			|	МАКСИМУМ(ВЫБОР КОГДА РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.НаправлениеДеятельности
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК НаправлениеДеятельностиДоИзменения,
			|	МАКСИМУМ(ВЫБОР КОГДА РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.ДатаОтраженияВУчете
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК ДатаОтраженияВУчетеДоИзменения,
			|	МАКСИМУМ(ВЫБОР КОГДА РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.СторноИсправление
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК СторноИсправлениеДоИзменения,
			|	МАКСИМУМ(ВЫБОР КОГДА РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.СторнируемыйДокумент
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК СторнируемыйДокументДоИзменения,
			|	МАКСИМУМ(ВЫБОР КОГДА РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.ИсправляемыйДокумент
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК ИсправляемыйДокументДоИзменения,
			|	МАКСИМУМ(ВЫБОР КОГДА РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.Приоритет
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК ПриоритетДоИзменения,
			|
			|	МАКСИМУМ(ВЫБОР КОГДА НЕ РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.НомерДокументаИБ
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК НомерДокументаИБ,
			|	МАКСИМУМ(ВЫБОР КОГДА НЕ РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.Статус
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК Статус,
			|	МАКСИМУМ(ВЫБОР КОГДА НЕ РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.Ответственный
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК Ответственный,
			|	МАКСИМУМ(ВЫБОР КОГДА НЕ РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.ДополнительнаяЗапись
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК ДополнительнаяЗапись,
			|	МАКСИМУМ(ВЫБОР КОГДА НЕ РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.Проведен
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК Проведен,
			|	МАКСИМУМ(ВЫБОР КОГДА НЕ РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.ПометкаУдаления
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК ПометкаУдаления,
			|	МАКСИМУМ(ВЫБОР КОГДА НЕ РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.ДатаПервичногоДокумента
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК ДатаПервичногоДокумента,
			|	МАКСИМУМ(ВЫБОР КОГДА НЕ РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.НомерПервичногоДокумента
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК НомерПервичногоДокумента,
			|	МАКСИМУМ(ВЫБОР КОГДА НЕ РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.Сумма
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК Сумма,
			|	МАКСИМУМ(ВЫБОР КОГДА НЕ РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.Валюта
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК Валюта,
			|	МАКСИМУМ(ВЫБОР КОГДА НЕ РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.Договор
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК Договор,
			|	МАКСИМУМ(ВЫБОР КОГДА НЕ РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.НаправлениеДеятельности
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК НаправлениеДеятельности,
			|	МАКСИМУМ(ВЫБОР КОГДА НЕ РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.ДатаОтраженияВУчете
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК ДатаОтраженияВУчете,
			|	МАКСИМУМ(ВЫБОР КОГДА НЕ РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.СторноИсправление
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК СторноИсправление,
			|	МАКСИМУМ(ВЫБОР КОГДА НЕ РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.СторнируемыйДокумент
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК СторнируемыйДокумент,
			|	МАКСИМУМ(ВЫБОР КОГДА НЕ РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.ИсправляемыйДокумент
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК ИсправляемыйДокумент,
			|	МАКСИМУМ(ВЫБОР КОГДА НЕ РеестрДокументовИзменения.ДоИзменения ТОГДА
			|					РеестрДокументовИзменения.Приоритет
			|				ИНАЧЕ
			|			NULL
			|		КОНЕЦ) КАК Приоритет
			|ПОМЕСТИТЬ РеестрДокументовИзменения
			|ИЗ (
			|	ВЫБРАТЬ
			|		ЛОЖЬ                                      КАК ДоИзменения,
			|		РеестрДокументов.ХозяйственнаяОперация    КАК ХозяйственнаяОперация,
			|		РеестрДокументов.Организация              КАК Организация,
			|		РеестрДокументов.Партнер                  КАК Партнер,
			|		РеестрДокументов.МестоХранения            КАК МестоХранения,
			|		РеестрДокументов.Контрагент               КАК Контрагент,
			|		РеестрДокументов.Подразделение            КАК Подразделение,
			|		РеестрДокументов.ДатаДокументаИБ          КАК ДатаДокументаИБ,
			|		РеестрДокументов.Ссылка                   КАК Ссылка,
			|		РеестрДокументов.НомерДокументаИБ         КАК НомерДокументаИБ,
			|		РеестрДокументов.Статус                   КАК Статус,
			|		РеестрДокументов.Ответственный            КАК Ответственный,
			|		РеестрДокументов.ДополнительнаяЗапись     КАК ДополнительнаяЗапись,
			|		РеестрДокументов.Проведен                 КАК Проведен,
			|		РеестрДокументов.ПометкаУдаления          КАК ПометкаУдаления,
			|		РеестрДокументов.ДатаПервичногоДокумента  КАК ДатаПервичногоДокумента,
			|		РеестрДокументов.НомерПервичногоДокумента КАК НомерПервичногоДокумента,
			|		РеестрДокументов.Сумма                    КАК Сумма,
			|		РеестрДокументов.Валюта                   КАК Валюта,
			|		РеестрДокументов.Договор                  КАК Договор,
			|		РеестрДокументов.НаправлениеДеятельности  КАК НаправлениеДеятельности,
			|		РеестрДокументов.ДатаОтраженияВУчете      КАК ДатаОтраженияВУчете,
			|		РеестрДокументов.СторноИсправление        КАК СторноИсправление,
			|		РеестрДокументов.СторнируемыйДокумент     КАК СторнируемыйДокумент,
			|		РеестрДокументов.ИсправляемыйДокумент     КАК ИсправляемыйДокумент,
			|		РеестрДокументов.Приоритет                КАК Приоритет
			|	ИЗ
			|		РегистрСведений.РеестрДокументов КАК РеестрДокументов
			|	ГДЕ
			|		РеестрДокументов.Ссылка = &Ссылка
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ИСТИНА                                    КАК ДоИзменения,
			|		РеестрДокументов.ХозяйственнаяОперация    КАК ХозяйственнаяОперация,
			|		РеестрДокументов.Организация              КАК Организация,
			|		РеестрДокументов.Партнер                  КАК Партнер,
			|		РеестрДокументов.МестоХранения            КАК МестоХранения,
			|		РеестрДокументов.Контрагент               КАК Контрагент,
			|		РеестрДокументов.Подразделение            КАК Подразделение,
			|		РеестрДокументов.ДатаДокументаИБ          КАК ДатаДокументаИБ,
			|		РеестрДокументов.Ссылка                   КАК Ссылка,
			|		РеестрДокументов.НомерДокументаИБ         КАК НомерДокументаИБ,
			|		РеестрДокументов.Статус                   КАК Статус,
			|		РеестрДокументов.Ответственный            КАК Ответственный,
			|		РеестрДокументов.ДополнительнаяЗапись     КАК ДополнительнаяЗапись,
			|		РеестрДокументов.Проведен                 КАК Проведен,
			|		РеестрДокументов.ПометкаУдаления          КАК ПометкаУдаления,
			|		РеестрДокументов.ДатаПервичногоДокумента  КАК ДатаПервичногоДокумента,
			|		РеестрДокументов.НомерПервичногоДокумента КАК НомерПервичногоДокумента,
			|		РеестрДокументов.Сумма                    КАК Сумма,
			|		РеестрДокументов.Валюта                   КАК Валюта,
			|		РеестрДокументов.Договор                  КАК Договор,
			|		РеестрДокументов.НаправлениеДеятельности  КАК НаправлениеДеятельности,
			|		РеестрДокументов.ДатаОтраженияВУчете      КАК ДатаОтраженияВУчете,
			|		РеестрДокументов.СторноИсправление        КАК СторноИсправление,
			|		РеестрДокументов.СторнируемыйДокумент     КАК СторнируемыйДокумент,
			|		РеестрДокументов.ИсправляемыйДокумент     КАК ИсправляемыйДокумент,
			|		РеестрДокументов.Приоритет                КАК Приоритет
			|	ИЗ
			|		РеестрДокументовПередЗаписью КАК РеестрДокументов) КАК РеестрДокументовИзменения
			|СГРУППИРОВАТЬ ПО
			|	РеестрДокументовИзменения.ХозяйственнаяОперация,
			|	РеестрДокументовИзменения.Организация,
			|	РеестрДокументовИзменения.Партнер,
			|	РеестрДокументовИзменения.МестоХранения,
			|	РеестрДокументовИзменения.Контрагент,
			|	РеестрДокументовИзменения.Подразделение,
			|	РеестрДокументовИзменения.ДатаДокументаИБ,
			|	РеестрДокументовИзменения.Ссылка
			|;
			|УНИЧТОЖИТЬ РеестрДокументовПередЗаписью");
		
		Запрос.УстановитьПараметр("Ссылка", Отбор.Ссылка.Значение);
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Параметры:
//  ТаблицаРеестрДокументов - ТаблицаЗначений - 
Процедура ЗагрузитьСОбработкой(ТаблицаРеестрДокументов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаРеестрДокументов.ТипСсылки КАК ТипСсылки,
	|	ТаблицаРеестрДокументов.Организация КАК Организация,
	|	ТаблицаРеестрДокументов.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ТаблицаРеестрДокументов.Партнер КАК Партнер,
	|	ВЫБОР
	|		КОГДА ТаблицаРеестрДокументов.Контрагент = НЕОПРЕДЕЛЕНО
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КлючиРеестраДокументов.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаРеестрДокументов.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ТаблицаРеестрДокументов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаРеестрДокументов.ДополнительнаяЗапись КАК ДополнительнаяЗапись,
	|	ТаблицаРеестрДокументов.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ТаблицаРеестрДокументов.МестоХранения = НЕОПРЕДЕЛЕНО
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КлючиРеестраДокументов.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаРеестрДокументов.МестоХранения
	|	КОНЕЦ КАК МестоХранения,
	|	ТаблицаРеестрДокументов.ДатаДокументаИБ КАК ДатаДокументаИБ,
	|	ТаблицаРеестрДокументов.Ссылка КАК Ссылка,
	|	ТаблицаРеестрДокументов.РазделительЗаписи КАК РазделительЗаписи,
	|	ТаблицаРеестрДокументов.НомерДокументаИБ КАК НомерДокументаИБ,
	|	ТаблицаРеестрДокументов.Статус КАК Статус,
	|	ТаблицаРеестрДокументов.Ответственный КАК Ответственный,
	|	ТаблицаРеестрДокументов.Автор КАК Автор,
	|	ТаблицаРеестрДокументов.Дополнительно КАК Дополнительно,
	|	ТаблицаРеестрДокументов.Комментарий КАК Комментарий,
	|	ТаблицаРеестрДокументов.Проведен КАК Проведен,
	|	ТаблицаРеестрДокументов.ПометкаУдаления КАК ПометкаУдаления,
	|	ТаблицаРеестрДокументов.ДатаПервичногоДокумента КАК ДатаПервичногоДокумента,
	|	ТаблицаРеестрДокументов.НомерПервичногоДокумента КАК НомерПервичногоДокумента,
	|	ТаблицаРеестрДокументов.НаименованиеПервичногоДокумента КАК НаименованиеПервичногоДокумента,
	|	ТаблицаРеестрДокументов.Сумма КАК Сумма,
	|	ТаблицаРеестрДокументов.Валюта КАК Валюта,
	|	ТаблицаРеестрДокументов.ДатаОтраженияВУчете КАК ДатаОтраженияВУчете,
	|	ТаблицаРеестрДокументов.Договор КАК Договор,
	|	ТаблицаРеестрДокументов.СторноИсправление КАК СторноИсправление,
	|	ТаблицаРеестрДокументов.СторнируемыйДокумент КАК СторнируемыйДокумент,
	|	ТаблицаРеестрДокументов.ИсправляемыйДокумент КАК ИсправляемыйДокумент,
	|	ТаблицаРеестрДокументов.Приоритет
	|ПОМЕСТИТЬ ТаблицаРеестрДокументов
	|ИЗ
	|	&ТаблицаРеестрДокументов КАК ТаблицаРеестрДокументов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(КлючиРеестраДокументов.Ссылка) КАК Ссылка,
	|	КлючиРеестраДокументов.Ключ КАК Ключ
	|ПОМЕСТИТЬ ТаблицаСгруппированныхКлючей
	|ИЗ
	|	Справочник.КлючиРеестраДокументов КАК КлючиРеестраДокументов
	|ГДЕ
	|	КлючиРеестраДокументов.Ключ В(&КлючиОтбора)
	|
	|СГРУППИРОВАТЬ ПО
	|	КлючиРеестраДокументов.Ключ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ключ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРеестрДокументов.ТипСсылки КАК ТипСсылки,
	|	ТаблицаРеестрДокументов.Организация КАК Организация,
	|	ТаблицаРеестрДокументов.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ТаблицаРеестрДокументов.Партнер КАК Партнер,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаРеестрДокументов.Контрагент) = ТИП(Справочник.КлючиРеестраДокументов)
	|			ТОГДА ТаблицаРеестрДокументов.Контрагент
	|		ИНАЧЕ ЕСТЬNULL(КлючиРеестраДокументовКонтрагент.Ссылка, ЗНАЧЕНИЕ(Справочник.КлючиРеестраДокументов.ПустаяСсылка))
	|	КОНЕЦ КАК Контрагент,
	|	ТаблицаРеестрДокументов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаРеестрДокументов.ДополнительнаяЗапись КАК ДополнительнаяЗапись,
	|	ТаблицаРеестрДокументов.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаРеестрДокументов.МестоХранения) = ТИП(Справочник.КлючиРеестраДокументов)
	|			ТОГДА ТаблицаРеестрДокументов.МестоХранения
	|		ИНАЧЕ ЕСТЬNULL(КлючиРеестраДокументовМестоХранения.Ссылка, ЗНАЧЕНИЕ(Справочник.КлючиРеестраДокументов.ПустаяСсылка))
	|	КОНЕЦ КАК МестоХранения,
	|	ТаблицаРеестрДокументов.ДатаДокументаИБ КАК ДатаДокументаИБ,
	|	ТаблицаРеестрДокументов.Ссылка КАК Ссылка,
	|	ТаблицаРеестрДокументов.РазделительЗаписи КАК РазделительЗаписи,
	|	ТаблицаРеестрДокументов.НомерДокументаИБ КАК НомерДокументаИБ,
	|	ТаблицаРеестрДокументов.Статус КАК Статус,
	|	ТаблицаРеестрДокументов.Ответственный КАК Ответственный,
	|	ТаблицаРеестрДокументов.Автор КАК Автор,
	|	ТаблицаРеестрДокументов.Дополнительно КАК Дополнительно,
	|	ТаблицаРеестрДокументов.Комментарий КАК Комментарий,
	|	ТаблицаРеестрДокументов.Проведен КАК Проведен,
	|	ТаблицаРеестрДокументов.ПометкаУдаления КАК ПометкаУдаления,
	|	ТаблицаРеестрДокументов.ДатаПервичногоДокумента КАК ДатаПервичногоДокумента,
	|	ТаблицаРеестрДокументов.НомерПервичногоДокумента КАК НомерПервичногоДокумента,
	|	ТаблицаРеестрДокументов.НаименованиеПервичногоДокумента КАК НаименованиеПервичногоДокумента,
	|	ТаблицаРеестрДокументов.Сумма КАК Сумма,
	|	ТаблицаРеестрДокументов.Валюта КАК Валюта,
	|	ТаблицаРеестрДокументов.ДатаОтраженияВУчете КАК ДатаОтраженияВУчете,
	|	ТаблицаРеестрДокументов.Договор КАК Договор,
	|	ТаблицаРеестрДокументов.Приоритет КАК Приоритет,
	|	ТаблицаРеестрДокументов.СторноИсправление КАК СторноИсправление,
	|	ТаблицаРеестрДокументов.СторнируемыйДокумент КАК СторнируемыйДокумент,
	|	ТаблицаРеестрДокументов.ИсправляемыйДокумент КАК ИсправляемыйДокумент,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаРеестрДокументов.Контрагент) = ТИП(Справочник.КлючиРеестраДокументов)
	|			ТОГДА ЛОЖЬ
	|		КОГДА НЕ ТаблицаРеестрДокументов.Контрагент В (&ПустыеЗначенияКлючей)
	|		И КлючиРеестраДокументовКонтрагент.Ссылка ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьОшибкаПустойКлючКонтрагент,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаРеестрДокументов.МестоХранения) = ТИП(Справочник.КлючиРеестраДокументов)
	|			ТОГДА ЛОЖЬ
	|		КОГДА НЕ ТаблицаРеестрДокументов.МестоХранения В (&ПустыеЗначенияКлючей)
	|		И КлючиРеестраДокументовМестоХранения.Ссылка ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьОшибкаПустойКлючМестоХранения,
	|	ТаблицаРеестрДокументов.МестоХранения КАК МестоХраненияИзТаблицы,
	|	ТаблицаРеестрДокументов.Контрагент КАК КонтрагентИзТаблицы,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(36)) КАК ИдентификаторЗаписи
	|ПОМЕСТИТЬ ТаблицаРеестрДокументовСКлючами
	|ИЗ
	|	ТаблицаРеестрДокументов КАК ТаблицаРеестрДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСгруппированныхКлючей КАК КлючиРеестраДокументовМестоХранения
	|		ПО ТаблицаРеестрДокументов.МестоХранения = КлючиРеестраДокументовМестоХранения.Ключ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСгруппированныхКлючей КАК КлючиРеестраДокументовКонтрагент
	|		ПО ТаблицаРеестрДокументов.Контрагент = КлючиРеестраДокументовКонтрагент.Ключ
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаРеестрДокументов.Ссылка КАК Ссылка,
	|	РеестрДокументов.ДополнительнаяЗапись КАК ДополнительнаяЗапись,
	|	РеестрДокументов.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
	|	ЛОЖЬ КАК Обработано
	|ПОМЕСТИТЬ ИдентификаторыДопЗаписей
	|ИЗ ТаблицаРеестрДокументов КАК ТаблицаРеестрДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументов
	|		ПО ТаблицаРеестрДокументов.Ссылка = РеестрДокументов.Ссылка
	|		И РеестрДокументов.ДополнительнаяЗапись = ТаблицаРеестрДокументов.ДополнительнаяЗапись
	|		И РеестрДокументов.ИдентификаторЗаписи <> """"
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРеестрДокументовСКлючами.ЕстьОшибкаПустойКлючКонтрагент КАК ЕстьОшибкаПустойКлючКонтрагент,
	|	ТаблицаРеестрДокументовСКлючами.ЕстьОшибкаПустойКлючМестоХранения КАК ЕстьОшибкаПустойКлючМестоХранения,
	|	ТаблицаРеестрДокументовСКлючами.МестоХраненияИзТаблицы КАК МестоХраненияИзТаблицы,
	|	ТаблицаРеестрДокументовСКлючами.КонтрагентИзТаблицы КАК КонтрагентИзТаблицы
	|ИЗ
	|	ТаблицаРеестрДокументовСКлючами КАК ТаблицаРеестрДокументовСКлючами
	|ГДЕ
	|	(ТаблицаРеестрДокументовСКлючами.ЕстьОшибкаПустойКлючКонтрагент
	|	ИЛИ ТаблицаРеестрДокументовСКлючами.ЕстьОшибкаПустойКлючМестоХранения)";
	
	Для каждого Измерение Из Метаданные.РегистрыСведений.РеестрДокументов.Измерения Цикл
		
		Колонка = ТаблицаРеестрДокументов.Колонки.Найти(Измерение.Имя);
		
		Если Колонка <> Неопределено Тогда
			Если Колонка.ТипЗначения = Новый ОписаниеТипов("Неопределено")
				Или Колонка.ТипЗначения = Новый ОписаниеТипов("Null") Тогда
				ТаблицаРеестрДокументов.Колонки.Удалить(Колонка);
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		ТаблицаРеестрДокументов.Колонки.Добавить(Измерение.Имя, Измерение.Тип);
		
	КонецЦикла;
	
	Для каждого Ресурс Из Метаданные.РегистрыСведений.РеестрДокументов.Ресурсы Цикл
		
		Колонка = ТаблицаРеестрДокументов.Колонки.Найти(Ресурс.Имя);
		
		Если Колонка <> Неопределено Тогда
			Если Колонка.ТипЗначения = Новый ОписаниеТипов("Неопределено")
				Или Колонка.ТипЗначения = Новый ОписаниеТипов("Null") Тогда
				ТаблицаРеестрДокументов.Колонки.Удалить(Колонка);
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		ТаблицаРеестрДокументов.Колонки.Добавить(Ресурс.Имя, Ресурс.Тип);
		
	КонецЦикла;
	
	ПустыеЗначенияКлючей = Новый Массив;
	ПустыеЗначенияКлючей.Добавить(Неопределено);
	ПустыеЗначенияКлючей.Добавить(Null);
	
	Для Каждого ТипЗначенияКлюча из Метаданные.Справочники.КлючиРеестраДокументов.Реквизиты.Ключ.Тип.Типы() Цикл
		ПустыеЗначенияКлючей.Добавить(ПредопределенноеЗначение(Метаданные.НайтиПоТипу(ТипЗначенияКлюча).ПолноеИмя() + ".ПустаяСсылка"));
	КонецЦикла;
	
	ТаблицаКлючейРеестра = ТаблицаРеестрДокументов.Скопировать(, "Контрагент, МестоХранения");
	ТаблицаКлючейРеестра.Свернуть("Контрагент, МестоХранения");
	
	КлючиОтбора = Новый Массив;
	
	Для Каждого СтрокаКлючей Из ТаблицаКлючейРеестра Цикл
		КонтрагентКлюч = ?(СтрокаКлючей.Контрагент = Неопределено,
							Справочники.КлючиРеестраДокументов.ПустаяСсылка(),
							СтрокаКлючей.Контрагент);
		
		Если КлючиОтбора.Найти(КонтрагентКлюч) = Неопределено Тогда
			КлючиОтбора.Добавить(КонтрагентКлюч);
		КонецЕсли;

		МестоХраненияКлюч = ?(СтрокаКлючей.МестоХранения = Неопределено,
								Справочники.КлючиРеестраДокументов.ПустаяСсылка(),
								СтрокаКлючей.МестоХранения);
								
		Если КлючиОтбора.Найти(МестоХраненияКлюч) = Неопределено Тогда
			КлючиОтбора.Добавить(МестоХраненияКлюч);
		КонецЕсли;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ПустыеЗначенияКлючей",		ПустыеЗначенияКлючей);
	Запрос.УстановитьПараметр("ТаблицаРеестрДокументов",	ТаблицаРеестрДокументов);
	Запрос.УстановитьПараметр("КлючиОтбора",				КлючиОтбора);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	
	Ошибки = РезультатыЗапроса[4].Выбрать();
	
	Пока Ошибки.Следующий() Цикл
		
		Если ОбщегоНазначения.ЭтоПодчиненныйУзелРИБ() Тогда
			
			ТекстСообщения = НСтр("ru = 'Для значения ""%Значение%"" не найден ключ реестра документов. Обратитесь к администратору.'");
			
			Если Ошибки.ЕстьОшибкаПустойКлючКонтрагент Тогда
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Значение%", Ошибки.КонтрагентИзТаблицы);
			КонецЕсли;
			
			Если Ошибки.ЕстьОшибкаПустойКлючМестоХранения Тогда
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Значение%", Ошибки.МестоХраненияИзТаблицы);
			КонецЕсли;
			
			ВызватьИсключение ТекстСообщения;
		Иначе
			
			Если Ошибки.ЕстьОшибкаПустойКлючКонтрагент Тогда
				Справочники.КлючиРеестраДокументов.СоздатьОбновитьКлючиРеестра(,Ошибки.КонтрагентИзТаблицы);
			КонецЕсли;
			
			Если Ошибки.ЕстьОшибкаПустойКлючМестоХранения Тогда
				Справочники.КлючиРеестраДокументов.СоздатьОбновитьКлючиРеестра(,Ошибки.МестоХраненияИзТаблицы);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Если Ошибки.Количество() > 0 Тогда
		РезультатыЗапроса = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	КонецЕсли;
	
	ТаблицаРеестрДокументовКЗагрузке = РезультатыЗапроса[2].Выгрузить();
	ТаблицаИдентификаторыЗаписей = РезультатыЗапроса[3].Выгрузить();
	ТаблицаИдентификаторыЗаписей.Индексы.Добавить("Ссылка, ДополнительнаяЗапись, Обработано");
	
	Если ТаблицаИдентификаторыЗаписей.Количество() > 0 Тогда
		
		СтруктураОтбораИдентификаторы = Новый Структура;
		
		Для Каждого ТекущаяСтрокаРегистра Из ТаблицаРеестрДокументовКЗагрузке Цикл
		
			СтруктураОтбораИдентификаторы.Вставить("Ссылка", ТекущаяСтрокаРегистра.Ссылка);
			СтруктураОтбораИдентификаторы.Вставить("ДополнительнаяЗапись", ТекущаяСтрокаРегистра.ДополнительнаяЗапись);
			СтруктураОтбораИдентификаторы.Вставить("Обработано", Ложь);

			ИдентификаторыНайденныеСтроки = ТаблицаИдентификаторыЗаписей.НайтиСтроки(СтруктураОтбораИдентификаторы);
			Если ИдентификаторыНайденныеСтроки.Количество() > 0 Тогда

				ТекущаяСтрокаРегистра.ИдентификаторЗаписи = ИдентификаторыНайденныеСтроки[0].ИдентификаторЗаписи;
				ИдентификаторыНайденныеСтроки[0].Обработано = Истина;

			КонецЕсли;

		КонецЦикла;
	КонецЕсли;
	
	Загрузить(ТаблицаРеестрДокументовКЗагрузке);
	
КонецПроцедуры

// Заполнение служебного измерения "ИдентификаторЗаписи"
// При обмене и при записи документов заполнение данного измерения - обязательное, т.к. оно входит в состав ключевых
// полей в динамических списках журналов документов.
// 
// Параметры:
//  НаборЗаписей - РегистрСведенийНаборЗаписей.РеестрДокументов - Набор записей
//
Процедура ЗаполнитьИдентификаторыЗаписей(НаборЗаписей)
	
	Для Каждого Запись Из НаборЗаписей Цикл
		Если Не ЗначениеЗаполнено(Запись.ИдентификаторЗаписи) Тогда
			Запись.ИдентификаторЗаписи = Строка(Новый УникальныйИдентификатор);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
