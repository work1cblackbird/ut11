#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ЗаполнитьЗамещенныеИЗамещающиеПланы();
	Если Параметры.НавигационнаяСсылкаФорматированнойСтроки = "Замещенные" Тогда
		Элементы.ЗамещающиеИЗамещенныеПланы.ТекущаяСтраница = Элементы.СтраницаЗамещенныеПланы;
	КонецЕсли;
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗамещающиеПланыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(,Элементы.ЗамещающиеПланы.ТекущиеДанные.ЗамещаемыйПлан);
КонецПроцедуры

&НаКлиенте
Процедура ЗамещенныеПланыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(,Элементы.ЗамещенныеПланы.ТекущиеДанные.ЗамещенныйПлан);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьЗамещенныеИЗамещающиеПланы()
	
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Параметры.Ссылка);
	Запрос = МенеджерОбъекта.ЗапросЗамещенныеИЗамещающиеПланы(Параметры);
	
	ЗапросПакет = Запрос.ВыполнитьПакет();
	ТЗЗамещающиеПланы = ЗапросПакет[0].Выгрузить();
	ТЗЗамещенныеПланы = ЗапросПакет[1].Выгрузить();
		
	Для Каждого Строка Из ТЗЗамещающиеПланы Цикл
		НоваяСтрока = ЗамещающиеПланы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.ПериодЗамещения.ДатаНачала = Строка.НачалоПериода;
		НоваяСтрока.ПериодЗамещения.ДатаОкончания = Строка.ОкончаниеПериода;
	КонецЦикла;
	
	Для Каждого Строка Из ТЗЗамещенныеПланы Цикл
		НоваяСтрока = ЗамещенныеПланы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.ПериодЗамещения.ДатаНачала = Строка.НачалоПериода;
		НоваяСтрока.ПериодЗамещения.ДатаОкончания = Строка.ОкончаниеПериода;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

КонецПроцедуры

#КонецОбласти