
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Номенклатура") Тогда
		ОтборНоменклатура = Параметры.Номенклатура;
	КонецЕсли;
	
	ТекстЗапроса = Список.ТекстЗапроса;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
								"&ТекстЗапросаКоэффициентУпаковки",
								Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
									"ПрогнозыПереопределяемый.Упаковка",
									"ПрогнозыПереопределяемый.Номенклатура"));
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СвойстваСписка.ТекстЗапроса					= ТекстЗапроса;
	СвойстваСписка.ОсновнаяТаблица				= "РегистрСведений.ПрогнозыРасходаУпаковок";
	СвойстваСписка.ДинамическоеСчитываниеДанных	= Истина;
	
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список, СвойстваСписка);
	
	НормативноеКоличествоЗапаса = "Все";
	
	УстановитьОтборы();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если ЗначениеЗаполнено(Склад) И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад, "ЭтоГруппа") Тогда
		Склад = Неопределено;
	КонецЕсли;
	УстановитьОтборы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	УстановитьОтборы();
КонецПроцедуры

&НаКлиенте
Процедура ПомещениеПриИзменении(Элемент)
	УстановитьОтборы();
КонецПроцедуры

&НаКлиенте
Процедура НормативноеКоличествоЗапаса1ПриИзменении(Элемент)
	УстановитьОтборы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИспользоватьРекомендуемоеНормативноеКоличествоЗапаса(Команда)
	МассивЗаписей = Элементы.Список.ВыделенныеСтроки;
	
	Если МассивЗаписей.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Выделите строки, в которых нужно установить использование рекомендуемого нормативного запаса.'");
		ПоказатьПредупреждение(Неопределено, ТекстСообщения);
		Возврат;
	Иначе
		ТекстВопроса = НСтр("ru = 'При выполнении операции в выделенных строках будет очищено нормативное количество запаса, назначенное вручную. Продолжить?'");
		Ответ = Неопределено;

		ПоказатьВопрос(Новый ОписаниеОповещения("ИспользоватьРекомендуемоеНормативноеКоличествоЗапасаЗавершение", ЭтотОбъект, Новый Структура("МассивЗаписей", МассивЗаписей)), ТекстВопроса,РежимДиалогаВопрос.ДаНет);
        Возврат;
	КонецЕсли;
	
	ИспользоватьРекомендуемоеНормативноеКоличествоЗапасаСервер(МассивЗаписей);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьРекомендуемоеНормативноеКоличествоЗапасаЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    МассивЗаписей = ДополнительныеПараметры.МассивЗаписей;
    
    
    Ответ = РезультатВопроса;
    
    Если Ответ <> КодВозвратаДиалога.Да Тогда
        Возврат;
    КонецЕсли;
    
    ИспользоватьРекомендуемоеНормативноеКоличествоЗапасаСервер(МассивЗаписей);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Номенклатура.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Характеристика.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Серия.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Упаковка.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НормативноеКоличествоЗапаса.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НормативноеКоличествоЗапасаРекомендуемое.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВероятностьОтгрузкиВТечениеДня.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВероятностьОтгрузкиВТечениеДня.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СреднедневноеПотребление.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СреднеквадратическоеОтклонение.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.УчаствуетВПодпитке");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);

КонецПроцедуры

#Область ПриИзмененииРеквизитов

&НаСервере
Процедура УстановитьОтборы()
	
	ТолькоЗначенияНазначенныеВручную = (НормативноеКоличествоЗапаса = "НазначенныеВручную");
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Помещение", Помещение, ВидСравненияКомпоновкиДанных.Равно,, Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Склад", Склад, ВидСравненияКомпоновкиДанных.Равно,, Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "НазначеноПользователем", ТолькоЗначенияНазначенныеВручную, ВидСравненияКомпоновкиДанных.Равно,, ТолькоЗначенияНазначенныеВручную, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
	Если ЗначениеЗаполнено(ОтборНоменклатура) Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Номенклатура", ОтборНоменклатура,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		
	КонецЕсли;
	
	УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Склад", Склад));
	
	Если СкладыСервер.ИспользоватьПрогнозРасходаУпаковок(Склад, Помещение) Тогда
		Элементы.ДекорацияИспользоватьПрогнозРасходаУпаковок.Заголовок = "";
		Элементы.Список.ИзменятьСоставСтрок = Истина;
	ИначеЕсли ЗначениеЗаполнено(Склад)
		И Не СкладыСервер.ИспользоватьСкладскиеПомещения(Склад,,Ложь)
		Или ЗначениеЗаполнено(Помещение) Тогда
		
		ТекстЗаголовка = НСтр("ru = 'На складе ""%Склад%"" не используется прогнозирование расхода упаковок.'");
		ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%Склад%", СкладыСервер.ПолучитьПредставлениеСклада(Склад, Помещение));
		
		Элементы.ДекорацияИспользоватьПрогнозРасходаУпаковок.Заголовок = ТекстЗаголовка;
		Элементы.Список.ИзменятьСоставСтрок = Ложь;
	Иначе
		Элементы.ДекорацияИспользоватьПрогнозРасходаУпаковок.Заголовок = "";
		Элементы.Список.ИзменятьСоставСтрок = Ложь;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ИспользоватьРекомендуемоеНормативноеКоличествоЗапасаСервер(МассивЗаписей)
	
	Для Каждого СтрМас Из МассивЗаписей Цикл
		Если ТипЗнч(СтрМас) <> Тип("РегистрСведенийКлючЗаписи.ПрогнозыРасходаУпаковок")
			Тогда
			Продолжить;
		КонецЕсли;
		
		Набор = РегистрыСведений.ПрогнозыРасходаУпаковок.СоздатьНаборЗаписей();
		Набор.Отбор.Склад.Установить(СтрМас.Склад);
		Набор.Отбор.Помещение.Установить(СтрМас.Помещение);
		Набор.Отбор.Номенклатура.Установить(СтрМас.Номенклатура);
		Набор.Отбор.Характеристика.Установить(СтрМас.Характеристика);
		Набор.Отбор.Серия.Установить(СтрМас.Серия);
		Набор.Отбор.Упаковка.Установить(СтрМас.Упаковка);
		
		Набор.Прочитать();
		
		Если Набор.Количество() > 0 Тогда
			Если Не Набор[0].НазначеноПользователем Тогда
				Продолжить;
			КонецЕсли;
		Иначе
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Набор[0];
		НоваяСтрока.НазначеноПользователем      = Ложь;
		НоваяСтрока.НормативноеКоличествоЗапаса = 0;
		
		Набор.Записать();
	КонецЦикла;
	
	Элементы.Список.Обновить();
КонецПроцедуры

#КонецОбласти

#КонецОбласти
