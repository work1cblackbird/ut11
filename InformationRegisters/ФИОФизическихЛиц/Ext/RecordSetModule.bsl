#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если НЕ ДополнительныеСвойства.Свойство("НеФормироватьНаименование") Тогда
		
		// Вызывается не из формы элемента физического лица
		ТаблицаФизЛиц = ЭтотОбъект.Выгрузить();
		ТаблицаФизЛиц.Свернуть("ФизическоеЛицо", "");
		
		ОбновитьНаименованияФизЛиц(ТаблицаФизЛиц.ВыгрузитьКолонку("ФизическоеЛицо"));	
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Перезаполняет поле Наименование указанных физлиц по данным регистра.
//
Процедура ОбновитьНаименованияФизЛиц(МассивФизЛиц)
	
 	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивФизЛиц", МассивФизЛиц);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФИОФизическихЛицСрезПоследних.ФизическоеЛицо,
	|	ВЫРАЗИТЬ(ФИОФизическихЛицСрезПоследних.Фамилия + ВЫБОР
	|			КОГДА ФИОФизическихЛицСрезПоследних.Имя = """"
	|				ТОГДА """"
	|			ИНАЧЕ "" "" + ФИОФизическихЛицСрезПоследних.Имя
	|		КОНЕЦ + ВЫБОР
	|			КОГДА ФИОФизическихЛицСрезПоследних.Отчество = """"
	|				ТОГДА """"
	|			ИНАЧЕ "" "" + ФИОФизическихЛицСрезПоследних.Отчество
	|		КОНЕЦ КАК СТРОКА(50)) КАК НовоеНаименование
	|ПОМЕСТИТЬ ВТФИОФизЛицПоследнее
	|ИЗ
	|	РегистрСведений.ФИОФизическихЛиц.СрезПоследних(, ФизическоеЛицо В (&МассивФизЛиц)) КАК ФИОФизическихЛицСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФИОФизЛицПоследнее.ФизическоеЛицо,
	|	ФИОФизЛицПоследнее.НовоеНаименование
	|ИЗ
	|	ВТФИОФизЛицПоследнее КАК ФИОФизЛицПоследнее
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ПО ФИОФизЛицПоследнее.ФизическоеЛицо = ФизическиеЛица.Ссылка
	|			И ФИОФизЛицПоследнее.НовоеНаименование <> ФизическиеЛица.Наименование";
	
	ДанныеФИО = Запрос.Выполнить().Выбрать();
	
	Пока ДанныеФИО.Следующий() Цикл
		
		ФизЛицоОбъект = ДанныеФИО.ФизическоеЛицо.ПолучитьОбъект();
		
		Попытка 
			ФизЛицоОбъект.Заблокировать();
		Исключение
			
			ТекстИсключенияЗаписи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось изменить имя физического лица %1.
				|Возможно, данные сотрудника редактируются другим пользователем'"),
				ФизЛицоОбъект.Наименование);
			ВызватьИсключение ТекстИсключенияЗаписи;
			
		КонецПопытки;
		
		ФизЛицоОбъект.Наименование = ДанныеФИО.НовоеНаименование;
		
		ФизЛицоОбъект.Записать();
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
