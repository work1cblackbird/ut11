#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВиртуальныеПоля = Новый Массив();
	ВиртуальныеПоля.Добавить("РезультатПроверкиОИСМ");
	ВиртуальныеПоля.Добавить("ТипКода");
	Список.УстановитьОграниченияИспользованияВГруппировке(ВиртуальныеПоля);
	Список.УстановитьОграниченияИспользованияВОтборе(ВиртуальныеПоля);
	Список.УстановитьОграниченияИспользованияВПорядке(ВиртуальныеПоля);
	
	ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИдентификаторФискальнойОперации");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ЭлементОтбора.ПравоеЗначение = "";
	ЭлементОтбора.Использование = Ложь;
	
	ТипыОборудований = МенеджерОборудованияКлиентСервер.ПараметрыТипыОборудования();
	ТипыОборудований.СканерШтрихкода = Истина;
	МенеджерОборудования.ПриСозданииНаСервере(ЭтотОбъект, ТипыОборудований);
	
	ТипыОборудования = МенеджерОборудованияКлиентСервер.ПараметрыТипыОборудования();
	ТипыОборудования.СканерШтрихкода = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Источник = "ПодключаемоеОборудование"  И ВводДоступен() Тогда
		Если ТипЗнч(Параметр) = Тип("Массив") И Параметр.Количество()>0 Тогда
			Если ИмяСобытия = "ScanDataBase64" Тогда
				КодМаркировкиBase64 = Параметр[0];
			ИначеЕсли ИмяСобытия = "ScanData" Тогда
				КодМаркировкиBase64 = ОбщегоНазначенияБПОКлиентСервер.ШтрихкодВBase64(Параметр[0]);
			Иначе
				Возврат;
			КонецЕсли;
		ИначеЕсли ТипЗнч(Параметр) = Тип("Структура") Тогда
			Если ИмяСобытия = "ScanDataBase64" Тогда
				КодМаркировкиBase64 = Параметр.Штрихкод;
			ИначеЕсли ИмяСобытия = "ScanData" Тогда
				КодМаркировкиBase64 = ОбщегоНазначенияБПОКлиентСервер.ШтрихкодВBase64(Параметр.Штрихкод);
			Иначе
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		КлючЗаписи = НайтиПоКодуМаркировки(КодМаркировкиBase64);
		Если КлючЗаписи <> Неопределено Тогда
			ОткрытьФорму("РегистрСведений.ОперацииПроверкиКМ.ФормаЗаписи", Новый Структура("Ключ", КлючЗаписи));
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(, ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказыватьФискализированныеПриИзменении(Элемент)
	
	Список.Отбор.Элементы[0].Использование = ПоказыватьТолькоФискализированные;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	МенеджерОборудованияМаркировка.ПриПолученииДанныхСпискаОперацийПроверкиКМ(ИмяЭлемента, Настройки, Строки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция НайтиПоКодуМаркировки(КодМаркировки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОперацииПроверкиКМ.ИдентификаторСессии КАК ИдентификаторСессии,
		|	ОперацииПроверкиКМ.ИдентификаторЗапроса КАК ИдентификаторЗапроса,
		|	ОперацииПроверкиКМ.ИдентификаторФискальнойОперации КАК ИдентификаторФискальнойОперации,
		|	ОперацииПроверкиКМ.ГодМесяц КАК ГодМесяц,
		|	ОперацииПроверкиКМ.КодМаркировки КАК КодМаркировки
		|ИЗ
		|	РегистрСведений.ОперацииПроверкиКМ КАК ОперацииПроверкиКМ
		|ГДЕ
		|	ОперацииПроверкиКМ.КодМаркировки = &КодМаркировки";
	
	Запрос.УстановитьПараметр("КодМаркировки", КодМаркировки);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	ОтборПоКодуМаркировки = Новый Структура();
	ОтборПоКодуМаркировки.Вставить("ИдентификаторСессии", Выборка.ИдентификаторСессии);
	ОтборПоКодуМаркировки.Вставить("ИдентификаторЗапроса", Выборка.ИдентификаторЗапроса);
	ОтборПоКодуМаркировки.Вставить("ИдентификаторФискальнойОперации", Выборка.ИдентификаторФискальнойОперации);
	ОтборПоКодуМаркировки.Вставить("ГодМесяц", Выборка.ГодМесяц);
	ОтборПоКодуМаркировки.Вставить("КодМаркировки", Выборка.КодМаркировки);
	
	Возврат РегистрыСведений.ОперацииПроверкиКМ.СоздатьКлючЗаписи(ОтборПоКодуМаркировки);
КонецФункции

#КонецОбласти




