
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьИнформациюОПродажахТоваровЧерезТорговуюПлощадку(Команда)
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбновитьИнформациюОПродажахТоваровЧерезТорговуюПлощадкуЗавершение", ЭтотОбъект,);
	ОткрытьФорму("РегистрСведений.ТоварыНаСкладахТорговыхПлощадокЗаДень.Форма.НастройкаОбновленияИнформацииОПродажахТоваров",, ЭтотОбъект,,,, ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьИнформациюОПродажахТоваровЧерезТорговуюПлощадкуЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено 
		 И ЗначениеЗаполнено(РезультатЗакрытия.УчетнаяЗаписьТорговойПлощадки) Тогда
		ОчиститьСообщения();
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбновитьИнформациюОПродажахТоваровЧерезТорговуюПлощадкуЗавершениеФоновогоЗадания", ЭтотОбъект);
		ДлительнаяОперация    = ОбновитьИнформациюОПродажахТоваровЧерезТорговуюПлощадкуНаСервере(
									РезультатЗакрытия.УчетнаяЗаписьТорговойПлощадки,
									РезультатЗакрытия.НачалоПериода,
									РезультатЗакрытия.ОкончаниеПериода);
		
		Если ДлительнаяОперация.Статус = "Выполнено" Тогда
			ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, ДлительнаяОперация);
			
		Иначе
			ПараметрыОжидания 					               = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
			ПараметрыОжидания.ВыводитьОкноОжидания             = Истина;
			ПараметрыОжидания.ТекстСообщения                   = НСтр("ru = 'Обновление информации о продажах товаров через торговую площадку.'");
			ПараметрыОжидания.ОповещениеПользователя.Показать  = Истина;
			ПараметрыОжидания.ОповещениеПользователя.Текст 	   = НСтр("ru = 'Ozon'");
			ПараметрыОжидания.ОповещениеПользователя.Пояснение = НСтр("ru = 'Обновление информации о продажах товаров через торговую площадку завершено.'");
			ПараметрыОжидания.ОповещениеПользователя.Картинка  = БиблиотекаКартинок.ЛоготипOzon1;
			
			ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбновитьИнформациюОПродажахТоваровЧерезТорговуюПлощадкуНаСервере(УчетнаяЗаписьТорговойПлощадки, НачалоПериода, ОкончаниеПериода)
	
	ПараметрыВыполнения 							 = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение            = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания  = НСтр("ru = 'Ozon. Обновление информации о продажах товаров через торговую площадку.'");
	ПараметрыВыполнения.ЗапуститьВФоне               = Истина;
	ПараметрыВыполнения.ПрерватьВыполнениеЕслиОшибка = Истина;

	ИмяМетода = "ИнтеграцияСМаркетплейсомOzonСервер.ОбновитьИнформациюОПродажахТоваровЧерезТорговуюПлощадку";

	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, 
		ИмяМетода, 
		УчетнаяЗаписьТорговойПлощадки,
		НачалоПериода, 
		ОкончаниеПериода);
	
КонецФункции

&НаКлиенте
Процедура ОбновитьИнформациюОПродажахТоваровЧерезТорговуюПлощадкуЗавершениеФоновогоЗадания(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Если Результат.Статус = "Ошибка" Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ПодробноеПредставлениеОшибки);
		
		ИначеЕсли Результат.Статус = "Выполнено" 
			 		И Результат.Свойство("АдресРезультата") Тогда
			Ошибка = ИнтеграцияСМаркетплейсомOzonВызовСервера.ПолучитьРезультатВыполненияФоновогоЗадания(Результат.АдресРезультата);
			
			Если Не ПустаяСтрока(Ошибка.КодОшибки) Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(Ошибка.ОписаниеОшибки);
			Иначе
				Элементы.Список.Обновить();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти