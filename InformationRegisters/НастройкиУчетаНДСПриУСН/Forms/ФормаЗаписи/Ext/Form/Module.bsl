#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КлючЗаписи = Неопределено;
	ИмяРегистра = Метаданные.РегистрыСведений.НастройкиУчетаНДСПриУСН.Имя;
	ТолькоПросмотр = НЕ ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиУчетаНДСПриУСН); 
	
	Параметры.Свойство("Организация", Организация);
	Элементы.Организация.Видимость = НЕ ЗначениеЗаполнено(Организация);
	Заголовок = Заголовок + " " + Строка(Организация);
	
	ЭтоФормаЗаписи = Параметры.Свойство("Ключ", КлючЗаписи);
	
	Если Параметры.Свойство("ЗначениеКопирования") И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		СкопироватьЗаписьРегистра(Параметры.ЗначениеКопирования);
	Иначе
		ПрочитатьЗаписьРегистра(КлючЗаписи);
	КонецЕсли; 
	
	ОписаниеЛимитовПримененияСтавокНДС();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаИзмененияПриИзменении(Элемент)
	ДатаИзменения = НачалоМесяца(ДатаИзменения);
	ТекстВопроса  = НСтр("ru = 'Создать новую настройку на %1?'");
	ТекстВопроса  = СтрШаблон(ТекстВопроса, Формат(ДатаИзменения,"ДЛФ=D"));
	ПоказатьВопрос(Новый ОписаниеОповещения("ДатаИзмененияПриИзмененииЗавершение", ЭтотОбъект), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	Организация = Запись.Организация;
	ПрочитатьЗаписьРегистраПриИзменииРеквизита(,ДатаИзменения);
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеИсторииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если НавигационнаяСсылкаФорматированнойСтроки = "ИсторияИзменений" Тогда
		Если Модифицированность Тогда
			ЗадатьВопросФормаМодифицирована("ОткрытьИсториюИзмененийПродолжение");
		Иначе
			ОткрытьИсториюИзменений();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Если ЗаписатьИзменения(Истина) Тогда
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	ЗаписатьИзменения();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаКлиенте
Процедура ЗадатьВопросФормаМодифицирована(ИмяОповещения)
	
	ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
	Оповещение = Новый ОписаниеОповещения(ИмяОповещения, ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаИзмененияПриИзмененииЗавершение(Ответ, ДополнительныеПараметры) Экспорт  
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		ПрочитатьЗаписьРегистраПриИзменииРеквизита(,ДатаИзменения);
	ИначеЕсли Ответ = КодВозвратаДиалога.Да Тогда
		ПрочитатьЗаписьРегистраПриИзменииРеквизита(,ДатаИзменения, Истина);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СкопироватьЗаписьРегистра(ЗначениеКопирования)
	НастройкиНалоговУчетныхПолитик.СкопироватьУчетнуюПолитику(ЭтотОбъект, ЗначениеКопирования, ИмяРегистра);
	Копирование = Истина;
	Модифицированность = Истина;
		
КонецПроцедуры 

&НаСервере
Процедура ПрочитатьЗаписьРегистра(КлючЗаписи = Неопределено, ПериодЗаписи = Неопределено, СоздатьНовую = Ложь)
	
	НастройкиНалоговУчетныхПолитик.ПрочитатьЗаписьРегистра(ЭтотОбъект, 
		ИмяРегистра,
		Организация,
		СоздатьНовую,
		КлючЗаписи,
		ПериодЗаписи); 
		
	Если ЗначениеЗаполнено(Запись.СтавкаНДС) Тогда
		СтавкаНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.СтавкаНДС, "ПеречислениеСтавкаНДС");
	Иначе
		СтавкаНДС = Перечисления.СтавкиНДС.ПустаяСсылка();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьЗаписьРегистраПриИзменииРеквизита(КлючЗаписи = Неопределено, ПериодЗаписи = Неопределено, СоздатьНовую = Ложь)
	Если НЕ Копирование Тогда
		ПрочитатьЗаписьРегистра(КлючЗаписи, ПериодЗаписи, СоздатьНовую);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьПослеЗаписи()
	
	ПараметрыОповещения = Новый Структура("Организация, Период", Запись.Организация, Запись.Период);
	ИмяСобытия = "Запись_" + ИмяРегистра;
	Оповестить(ИмяСобытия, ПараметрыОповещения);
	
КонецПроцедуры

&НаСервере
Функция ЗаписатьИзмененияНаСервере(Закрытие = Ложь) 
	
	Если ЗначениеЗаполнено(СтавкаНДС) Тогда
		Если СтавкаНДС = Перечисления.СтавкиНДС.БезНДС Тогда  
			Запись.ОсновноеНалогообложение = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС;
			Запись.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
		Иначе 
			Запись.ОсновноеНалогообложение = Перечисления.ТипыНалогообложенияНДС.ЛьготноеНалогообложениеНДСНаУСН;
			Запись.СтавкаНДС = УчетНДСРФКлиентСерверПовтИсп.СтавкаНДСПоЗначениюПеречисления(СтавкаНДС, Перечисления.ТипыНалогообложенияНДС.ЛьготноеНалогообложениеНДСНаУСН);
		КонецЕсли;
	Иначе
		Запись.ОсновноеНалогообложение = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС;
		Запись.СтавкаНДС = Справочники.СтавкиНДС.ПустаяСсылка(); 
	КонецЕсли;
	ЗаписьУспешна = НастройкиНалоговУчетныхПолитик.ЗаписатьИзменениеЗаписейРегистра(ЭтотОбъект, Закрытие);
	Возврат ЗаписьУспешна;
	
КонецФункции

&НаКлиенте
Функция ЗаписатьИзменения(Закрытие = Ложь)  
	
	ОчиститьСообщения();
	ЗаписьУспешна = ЗаписатьИзмененияНаСервере(Закрытие);
	Если ЗаписьУспешна Тогда
		ОповеститьПослеЗаписи();
	КонецЕсли;
	Возврат ЗаписьУспешна; 
	
КонецФункции

&НаКлиенте
Процедура ОткрытьИсториюИзменений()
	ОткрытьФорму("РегистрСведений.НастройкиУчетаНДСПриУСН.Форма.РедактированиеИстории",
		Новый Структура("ТолькоПросмотр, Организация", ТолькоПросмотр, Организация),
		ЭтотОбъект,,,,
		Новый ОписаниеОповещения("ОткрытьИсториюИзмененийЗавершение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИсториюИзмененийПродолжение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Нет Тогда
		ОткрытьИсториюИзменений();
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда
		Если НЕ ЗаписатьИзменения(Ложь) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИсториюИзмененийЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если ЗначениеЗаполнено(Результат) Тогда
		ПрочитатьЗаписьРегистра(Результат);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОписаниеЛимитовПримененияСтавокНДС()
	
	СтрокаШаблон = Элементы.СтавкаНДСБезНДС.Подсказка;
	ЛимитСтавкаБезНДС = УчетНДСРФКлиентСервер.ЛимитДоходовДляПримененияСтавокНДСПриУСН(Перечисления.СтавкиНДС.БезНДС, ДатаИзменения);
	Элементы.СтавкаНДСБезНДС.Подсказка = СтрШаблон(СтрокаШаблон,ЛимитСтавкаБезНДС);
	
	ЛимитСтавка5 = УчетНДСРФКлиентСервер.ЛимитДоходовДляПримененияСтавокНДСПриУСН(Перечисления.СтавкиНДС.НДС5, ДатаИзменения);
	СтрокаШаблон = Элементы.СтавкаНДС5.Подсказка;
	Элементы.СтавкаНДС5.Подсказка = СтрШаблон(СтрокаШаблон, ЛимитСтавкаБезНДС, ЛимитСтавка5);
	
	ЛимитСтавка7 = УчетНДСРФКлиентСервер.ЛимитДоходовДляПримененияСтавокНДСПриУСН(Перечисления.СтавкиНДС.НДС7, ДатаИзменения);
	СтрокаШаблон = Элементы.СтавкаНДС7.Подсказка;
	Элементы.СтавкаНДС7.Подсказка = СтрШаблон(СтрокаШаблон, ЛимитСтавка5, ЛимитСтавка7);
	
	СтрокаШаблон = Элементы.ПрочиеСтавкиНДС.Подсказка;
	Элементы.ПрочиеСтавкиНДС.Подсказка = СтрШаблон(СтрокаШаблон, ЛимитСтавкаБезНДС, ЛимитСтавка7);
	
КонецПроцедуры

#КонецОбласти
