
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	ДоступноИзменениеТокена = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ДанныеКлючаСессииИСМП);
	
	Элементы.Токен.ТолькоПросмотр       = Не ДоступноИзменениеТокена;
	Элементы.ДействуетДо.ТолькоПросмотр = Не ДоступноИзменениеТокена;
	Если ДоступноИзменениеТокена Тогда
		ЗаголовокКоманды = НСтр("ru = 'Записать и закрыть'");
	Иначе
		ЗаголовокКоманды = НСтр("ru = 'Закрыть'");
	КонецЕсли;
	Элементы.ФормаЗакрытьФорму.Заголовок = ЗаголовокКоманды;
	
	ТипТокенаАвторизации   = Параметры.ТипТокенаАвторизации;
	Организация            = Параметры.Организация;
	ПроизводственныйОбъект = Параметры.ПроизводственныйОбъект;
	
	Если ЗначениеЗаполнено(ТипТокенаАвторизации) Тогда
		ПолучитьТокен();
	КонецЕсли;
	
	Элементы.ТипТокенаАвторизации.ТолькоПросмотр   = Не Параметры.СозданиеТокена;
	Элементы.Организация.ТолькоПросмотр            = Не Параметры.СозданиеТокена;
	Элементы.ПроизводственныйОбъект.ТолькоПросмотр = Не Параметры.СозданиеТокена;
	
	Если Параметры.СозданиеТокена Тогда
		Организация            = Неопределено;
		ПроизводственныйОбъект = Неопределено;
	КонецЕсли;
	
	УправлениеЭлементамиФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Модифицированность = Не ЗначениеЗаполнено(Организация);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипТокенаАвторизацииПриИзменении(Элемент)
	
	УправлениеЭлементамиФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТокенПриИзменении(Элемент)
	
	РасшифроватьТокен(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Если Модифицированность Тогда
		Если Не ПроверитьЗаполнениеДанных() Тогда
			Возврат;
		КонецЕсли;
		ЗаписатьТокен();
		ПараметрЗакрытия = Истина;
	Иначе
		ПараметрЗакрытия = Неопределено;
	КонецЕсли;
	
	Закрыть(ПараметрЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// РасшифрованныйТокен
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.РасшифрованныйТокен.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ОшибкаРасшифровкиТокена");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаТребуетВниманияГосИС);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЭлементамиФормы(Форма)
	
	ЭтоТокенСУЗ = Форма.ТипТокенаАвторизации = ПредопределенноеЗначение("Перечисление.ТипыТокеновАвторизации.СУЗ");
	
	Форма.Элементы.ПроизводственныйОбъект.Видимость = ЭтоТокенСУЗ;
	Форма.Элементы.ДействуетДо.Видимость            = ЭтоТокенСУЗ;
	Форма.Элементы.РасшифрованныйТокен.Видимость    = Не ЭтоТокенСУЗ;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьТокен()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеКлючаСессии = ИнтерфейсАвторизацииИСМПСлужебный.ПолучитьСохраненныеДанныеКлючаСессии(
		ИнтерфейсИСМПОбщегоНазначенияКлиентСервер.ИмяДанныхКлючаСессии(ТипТокенаАвторизации));
	Если ДанныеКлючаСессии = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Данные = ДанныеКлючаСессии[Организация];
	
	Если Данные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипТокенаАвторизации = Перечисления.ТипыТокеновАвторизации.СУЗ Тогда
		Данные = Данные[ПроизводственныйОбъект];
		Если Данные = Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Токен = Данные.КлючСессии;
	ДействуетДо = МестноеВремя(Данные.ДействуетДо, ЧасовойПоясСеанса());
	РасшифроватьТокен(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РасшифроватьТокен(Форма)
	
	Если Форма.ТипТокенаАвторизации = ПредопределенноеЗначение("Перечисление.ТипыТокеновАвторизации.СУЗ")
		Или Форма.ТипТокенаАвторизации = ПредопределенноеЗначение("Перечисление.ТипыТокеновАвторизации.ИСМПРозница") Тогда
		Форма.ОшибкаРасшифровкиТокена = Ложь;
		Форма.РасшифрованныйТокен     = "";
	Иначе
		РезультатРасшифровкиТокена    = РасшифроватьТокенJWT(Форма.Токен);
		Форма.ОшибкаРасшифровкиТокена = РезультатРасшифровкиТокена.ОшибкаРасшифровки;
		Форма.РасшифрованныйТокен     = РезультатРасшифровкиТокена.РезультатРасшифровки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РасшифроватьТокенJWT(Токен)
	
	Результат = Новый Структура;
	Результат.Вставить("ОшибкаРасшифровки", Истина);
	Результат.Вставить("РезультатРасшифровки",
		НСтр("ru = 'Не удалось расшифровать данные токена'"));
	
	Если ПустаяСтрока(Токен) Тогда
		Результат.РезультатРасшифровки = НСтр("ru = 'Токен не указан'");
		Возврат Результат;
	КонецЕсли;
	
	Попытка
		РезультатРазбора = ОбщегоНазначенияИСМПСлужебный.РасшифроватьТокенJWT(Токен);
	Исключение
		Возврат Результат;
	КонецПопытки;
	
	Если РезультатРазбора.Данные = Неопределено Тогда
		Если ЗначениеЗаполнено(РезультатРазбора.ТекстОшибки) Тогда
			Результат.РезультатРасшифровки = РезультатРазбора.ТекстОшибки;
		КонецЕсли;
		Возврат Результат;
	КонецЕсли;
	
	Результат.ОшибкаРасшифровки    = Ложь;
	Результат.РезультатРасшифровки = ОбщегоНазначенияИСМП.ОбъектВТекстJSON(РезультатРазбора.Данные);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПроверитьЗаполнениеДанных()
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(ТипТокенаАвторизации) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Тип токена"" не заполнено'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ТипТокенаАвторизации", , Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Организация"" не заполнено'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Организация", , Отказ);
	КонецЕсли;
	
	Если ТипТокенаАвторизации = ПредопределенноеЗначение("Перечисление.ТипыТокеновАвторизации.СУЗ")
		И Не ЗначениеЗаполнено(ПроизводственныйОбъект) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Производственный объект"" не заполнено'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ПроизводственныйОбъект", , Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Токен) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Токен"" не заполнено'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Токен", , Отказ);
	ИначеЕсли ОшибкаРасшифровкиТокена Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Токен"" заполнено не верно'");;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Токен", , Отказ);
	КонецЕсли;
	
	Если ТипТокенаАвторизации = ПредопределенноеЗначение("Перечисление.ТипыТокеновАвторизации.СУЗ")
		И Не ЗначениеЗаполнено(ДействуетДо) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Действует до"" не заполнено'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ДействуетДо", , Отказ);
	КонецЕсли;
	
	Возврат Не Отказ;
	
КонецФункции

&НаСервере
Процедура ЗаписатьТокен()
	
	Если ТипТокенаАвторизации = Перечисления.ТипыТокеновАвторизации.СУЗ Тогда
		ТокенДействуетДо = УниверсальноеВремя(ДействуетДо, ЧасовойПоясСеанса());
	ИначеЕсли ТипТокенаАвторизации = Перечисления.ТипыТокеновАвторизации.ИСМПРозница Тогда
		// фиксированная дата действия токена розницы
		ТокенДействуетДо = Дата(2025, 03, 01);
	Иначе
		ТокенДействуетДо = Дата(1,1,1);
		РезультатРабора = ОбщегоНазначенияИСМП.ТекстJSONВОбъект(РасшифрованныйТокен);
		Если РезультатРабора <> Неопределено Тогда
			ТокенДействуетДо = ОбщегоНазначенияИСКлиентСервер.ДатаИзСтрокиUNIX(РезультатРабора.exp, 1);
		КонецЕсли;
	КонецЕсли;

	ПараметрыКлючаСессии = ИнтерфейсАвторизацииИСМПВызовСервера.ПараметрыКлючаСессии();
	ПараметрыКлючаСессии.КлючСессии  = Токен;
	ПараметрыКлючаСессии.ДействуетДо = ТокенДействуетДо;
	
	ПараметрыЗапроса = ИнтерфейсАвторизацииИСМПВызовСервера.ПараметрыЗапросаКлючаСессии(ТипТокенаАвторизации);
	ПараметрыЗапроса.Организация            = Организация;
	ПараметрыЗапроса.ПроизводственныйОбъект = ПроизводственныйОбъект;
	
	ИнтерфейсАвторизацииИСМПВызовСервера.УстановитьКлючСессии(ПараметрыЗапроса, ПараметрыКлючаСессии);
	
КонецПроцедуры

#КонецОбласти
