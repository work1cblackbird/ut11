///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Получает исторические данные заказа на оплату по идентификатору.
//
// Параметры
//  Идентификатор - Строка - идентификатор заказа на оплату.
//
// Возвращаемое значение:
//  Структура - данные заказа на оплату:
//    *ДатаОперации - Дата - дата заказа в программе;
//    *СуммаОперации - Число - сумма операции;
//    *НазначениеПлатежа - Строка - назначение для СБП;
//    *ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП - объект оплаты в информационной базе;
//    *ИдентификаторМерчанта - Строка - идентификатор торговой точки для которой была сформирован заказ.
//
Функция ДанныеОперацииСБППоИдентификатору(Идентификатор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеОперацийСБПc2b.ДатаОперации КАК ДатаОперации,
		|	ДанныеОперацийСБПc2b.СуммаОперации КАК СуммаОперации,
		|	ДанныеОперацийСБПc2b.НазначениеПлатежа КАК НазначениеПлатежа,
		|	ДанныеОперацийСБПc2b.ДокументОперации КАК ДокументОперации,
		|	ДанныеОперацийСБПc2b.ИдентификаторМерчанта КАК ИдентификаторМерчанта,
		|	ДанныеОперацийСБПc2b.Оплата КАК Оплата,
		|	ДанныеОперацийСБПc2b.КассоваяСсылка КАК КассоваяСсылка,
		|	ИдентификаторыОперацийСБПc2b.НастройкаПодключения КАК НастройкаПодключения,
		|	ВЫБОР
		|		КОГДА ИдентификаторыОперацийСБПc2b.ИдентификаторУчастника <> """"
		|			ТОГДА ИдентификаторыОперацийСБПc2b.ИдентификаторУчастника
		|		ИНАЧЕ ИдентификаторыОперацийСБПc2b.НастройкаПодключения.ИдентификаторУчастника
		|	КОНЕЦ КАК ИдентификаторУчастника
		|ИЗ
		|	РегистрСведений.ДанныеОперацийСБПc2b КАК ДанныеОперацийСБПc2b
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИдентификаторыОперацийСБПc2b КАК ИдентификаторыОперацийСБПc2b
		|		ПО ДанныеОперацийСБПc2b.Идентификатор = ИдентификаторыОперацийСБПc2b.Идентификатор
		|ГДЕ
		|	ДанныеОперацийСБПc2b.Идентификатор = &Идентификатор";
	
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Результат = Новый Структура;
		Результат.Вставить("ДатаОперации", ВыборкаДетальныеЗаписи.ДатаОперации);
		Результат.Вставить("СуммаОперации", ВыборкаДетальныеЗаписи.СуммаОперации);
		Результат.Вставить("НазначениеПлатежа", ВыборкаДетальныеЗаписи.НазначениеПлатежа);
		Результат.Вставить("ДокументОперации", ВыборкаДетальныеЗаписи.ДокументОперации);
		Результат.Вставить("ИдентификаторМерчанта", ВыборкаДетальныеЗаписи.ИдентификаторМерчанта);
		Результат.Вставить("Оплата", ВыборкаДетальныеЗаписи.Оплата);
		Результат.Вставить("КассоваяСсылка", ВыборкаДетальныеЗаписи.КассоваяСсылка);
		Результат.Вставить("НастройкаПодключения", ВыборкаДетальныеЗаписи.НастройкаПодключения);
		Результат.Вставить("ИдентификаторУчастника", ВыборкаДетальныеЗаписи.ИдентификаторУчастника);
		Возврат Результат;
	КонецЕсли;
	
КонецФункции

// Записывает данные оплаты в информационную базу.
//
// Параметры
//  Идентификатор - Строка - идентификатор заказа на оплату;
//  ЗаказНаОплату - Структура - данные заказа на оплату:
//    *ДатаОплаты - Дата - дата заказа в программе;
//    *СуммаОплаты - Число - сумма оплаты;
//    *НазначениеПлатежа - Строка - назначение для СБП;
//  ДокументОплаты - ОпределяемыйТип.ДокументОперацииСБП - объект оплаты в информационной базе;
//  КассоваяСсылка - Строка - идентификатор кассовой ссылки;
//  ИдентификаторМерчанта - Строка - идентификатор торговой точки для которой была сформирован заказ.
//
Процедура СохранитьДанныеОплаты(
		Идентификатор,
		ЗаказНаОплату,
		КассоваяСсылка,
		ДокументОплаты,
		ИдентификаторМерчанта) Экспорт
	
	Запись = РегистрыСведений.ДанныеОперацийСБПc2b.СоздатьМенеджерЗаписи();
	Запись.Идентификатор = Идентификатор;
	Запись.ДокументОперации  = ДокументОплаты;
	Запись.ДатаОперации = ЗаказНаОплату.ДатаОплаты;
	Запись.СуммаОперации = ЗаказНаОплату.СуммаОплаты;
	Запись.НазначениеПлатежа = ЗаказНаОплату.НазначениеПлатежа;
	Запись.ИдентификаторМерчанта = ИдентификаторМерчанта;
	Запись.КассоваяСсылка = КассоваяСсылка;
	Запись.Оплата = Истина;
	Запись.Записать();
	
КонецПроцедуры

// Записывает данные возврата в информационную базу.
//
// Параметры
//  Идентификатор - Строка - идентификатор заказа на оплату;
//  ЗаказНаВозврат - Структура - данные заказа на возврат:
//    *ДатаВозврата - Дата - дата возврата в программе;
//    *СуммаВозврата - Число - сумма возврата;
//    *НазначениеПлатежа - Строка - назначение для СБП;
//  ДокументВозврата - ОпределяемыйТип.ДокументОперацииСБП - объект оплаты в информационной базе;
//  ИдентификаторМерчанта - Строка - идентификатор торговой точки для которой была сформирован заказ.
//
Процедура СохранитьДанныеВозврата(
		Идентификатор,
		ЗаказНаВозврат,
		ДокументВозврата,
		ИдентификаторМерчанта) Экспорт
	
	Запись = РегистрыСведений.ДанныеОперацийСБПc2b.СоздатьМенеджерЗаписи();
	Запись.Идентификатор = Идентификатор;
	Запись.ДокументОперации = ДокументВозврата;
	Запись.ДатаОперации = ЗаказНаВозврат.ДатаВозврата;
	Запись.СуммаОперации = ЗаказНаВозврат.СуммаВозврата;
	Запись.ИдентификаторМерчанта = ИдентификаторМерчанта;
	Запись.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли