#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьМногострановойУчет") И ЭтоНоваяЗапись() Тогда
		Для Каждого Запись Из ЭтотОбъект Цикл
			Запись.Страна = ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана();
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьМногострановойУчет") И ЭтоНоваяЗапись() Тогда
		Константы.ОсновнаяСтрана.ПроверитьЗаполнение();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьМногострановойУчет") Тогда
		СинхронизироватьНачальныеДанныеПоСтране();
	КонецЕсли;
	
	АткуализироватьСтавкуНДСНоменклатуры();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Номенклатура

Процедура АткуализироватьСтавкуНДСНоменклатуры()
	
	Если ДополнительныеСвойства.Свойство("ПропуститьАктуализациюСтавкиНДСНоменклатуры") Тогда
		Возврат;
	КонецЕсли;
	
	ОсновнаяСтрана = ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана();
	
	Если Количество() Тогда
		
		ТаблицаЗаписей = Выгрузить();
		
		Блокировка = Новый БлокировкаДанных();
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СтавкиНДСНоменклатуры");
		ЭлементБлокировки.УстановитьЗначение("Страна", ОсновнаяСтрана);
		ЭлементБлокировки.ИсточникДанных = ТаблицаЗаписей;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СтавкиНДСНоменклатуры.Период КАК Период,
		|	СтавкиНДСНоменклатуры.Номенклатура КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА СтавкиНДСНоменклатуры.Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
		|			ТОГДА &ОсновнаяСтрана
		|		ИНАЧЕ СтавкиНДСНоменклатуры.Страна
		|	КОНЕЦ КАК Страна,
		|	СтавкиНДСНоменклатуры.СтавкаНДС КАК СтавкаНДС
		|ПОМЕСТИТЬ ЗаписываемыеСтавкиНДСНоменклатуры
		|ИЗ
		|	&СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
		|ГДЕ
		|	(СтавкиНДСНоменклатуры.Страна = &ОсновнаяСтрана
		|			ИЛИ СтавкиНДСНоменклатуры.Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	Номенклатура,
		|	Страна,
		|	СтавкаНДС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АктуальныеСтавки.Номенклатура КАК Номенклатура,
		|	АктуальныеСтавки.СтавкаНДС КАК СтавкаНДС
		|ИЗ
		|	ЗаписываемыеСтавкиНДСНоменклатуры КАК ЗаписываемыеСтавкиНДСНоменклатуры
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНДСНоменклатуры.СрезПоследних КАК АктуальныеСтавки
		|		ПО ЗаписываемыеСтавкиНДСНоменклатуры.Период = АктуальныеСтавки.Период
		|			И ЗаписываемыеСтавкиНДСНоменклатуры.Номенклатура = АктуальныеСтавки.Номенклатура
		|			И ЗаписываемыеСтавкиНДСНоменклатуры.Страна = АктуальныеСтавки.Страна
		|			И ЗаписываемыеСтавкиНДСНоменклатуры.СтавкаНДС = АктуальныеСтавки.СтавкаНДС";
		
		Запрос.УстановитьПараметр("СтавкиНДСНоменклатуры", ТаблицаЗаписей);
		Запрос.УстановитьПараметр("ОсновнаяСтрана", ОсновнаяСтрана);
		
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			НоменклатураОбъект = ПолучитьОбъектНомеклатурыСПроверкой(Отбор.Номенклатура.Значение);
			Если НоменклатураОбъект = Неопределено Тогда
				Продолжить;;
			КонецЕсли;
			
			ЗаблокироватьИЗаписатьНоменклатуру(НоменклатураОбъект, Выборка.СтавкаНДС);
			
		КонецЦикла;
		
	Иначе
		
		НоменклатураОбъект = ПолучитьОбъектНомеклатурыСПроверкой(Отбор.Номенклатура.Значение);
		Если НоменклатураОбъект = Неопределено Тогда
			Возврат;
		КонецЕсли;

		Дата = НачалоМесяца(ТекущаяДатаСеанса());
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	1 КАК Приоритет,
		|	СтавкиНДСНоменклатуры.СтавкаНДС КАК СтавкаНДС
		|ИЗ
		|	РегистрСведений.СтавкиНДСНоменклатуры.СрезПоследних(&Дата, Номенклатура = &Номенклатура
		|	И Страна = &ОсновнаяСтрана) КАК СтавкиНДСНоменклатуры
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2 КАК Приоритет,
		|	СтавкиНДСНоменклатуры.СтавкаНДС КАК СтавкаНДС
		|ИЗ
		|	РегистрСведений.СтавкиНДСНоменклатуры.СрезПоследних(&Дата, Номенклатура = &Номенклатура
		|	И Страна = &ПустаяСтрана) КАК СтавкиНДСНоменклатуры
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3 КАК Приоритет,
		|	ОсновныеСтавкиНДС.СтавкаНДС КАК СтавкаНДС
		|ИЗ
		|	РегистрСведений.ОсновныеСтавкиНДС.СрезПоследних(&Дата, Страна = &ОсновнаяСтрана ИЛИ Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)) КАК ОсновныеСтавкиНДС
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет";
		
		Запрос.УстановитьПараметр("Дата", Дата);
		Запрос.УстановитьПараметр("Номенклатура", НоменклатураОбъект.Ссылка);
		Запрос.УстановитьПараметр("ОсновнаяСтрана", ОсновнаяСтрана);
		Запрос.УстановитьПараметр("ПустаяСтрана", Справочники.СтраныМира.ПустаяСсылка());
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаблокироватьИЗаписатьНоменклатуру(НоменклатураОбъект, Выборка.СтавкаНДС);
			Если Выборка.Приоритет = 3 Тогда
				
				НаборЗаписей = РегистрыСведений.СтавкиНДСНоменклатуры.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Период.Установить(Дата);
				НаборЗаписей.Отбор.Номенклатура.Установить(НоменклатураОбъект.Ссылка);
				НаборЗаписей.Отбор.Страна.Установить(ОсновнаяСтрана);
				
				НоваяЗапись = НаборЗаписей.Добавить();
				НоваяЗапись.Период = Дата;
				НоваяЗапись.Номенклатура = НоменклатураОбъект.Ссылка;
				НоваяЗапись.Страна = ОсновнаяСтрана;
				НоваяЗапись.СтавкаНДС = Выборка.СтавкаНДС;
				
				НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьАктуализациюСтавкиНДСНоменклатуры");
				НаборЗаписей.Записать();
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ЗаблокироватьИЗаписатьНоменклатуру(НоменклатураОбъект, СтавкаНДС)
	
	Если НоменклатураОбъект.СтавкаНДС = СтавкаНДС Тогда
		Возврат;
	КонецЕсли;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Справочник.Номенклатура");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Ссылка", НоменклатураОбъект.Ссылка);
	
	НоменклатураОбъект.СтавкаНДС = СтавкаНДС;
	НоменклатураОбъект.ОбменДанными.Загрузка = Истина;
	
	НоменклатураОбъект.Записать();
	
КонецПроцедуры

Функция ПолучитьОбъектНомеклатурыСПроверкой(НоменклатураСсылка)
	
	Если Не ЗначениеЗаполнено(НоменклатураСсылка) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат НоменклатураСсылка.ПолучитьОбъект();
	
КонецФункции

#КонецОбласти

Процедура СинхронизироватьНачальныеДанныеПоСтране()
	
	ПустаяСтрана = Справочники.СтраныМира.ПустаяСсылка();
	
	Блокировка = Новый БлокировкаДанных();
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СтавкиНДСНоменклатуры");
	ЭлементБлокировки.ИсточникДанных = Выгрузить();
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтавкиНДСНоменклатуры.Период КАК Период,
		|	СтавкиНДСНоменклатуры.Номенклатура КАК Номенклатура,
		|	СтавкиНДСНоменклатуры.СтавкаНДС КАК СтавкаНДС
		|ИЗ
		|	РегистрСведений.СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
		|ГДЕ
		|	СтавкиНДСНоменклатуры.Номенклатура В(&Номенклатура)
		|	И СтавкиНДСНоменклатуры.Страна = &ПустаяСтрана";
	
	Запрос.УстановитьПараметр("Номенклатура", ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("ПустаяСтрана", ПустаяСтрана);
	
	РезультатЗапроса = Запрос.Выполнить();
		
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НаборЗаписей = РегистрыСведений.СтавкиНДСНоменклатуры.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
			НаборЗаписей.Отбор.Номенклатура.Установить(Выборка.Номенклатура);
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			НаборЗаписей.Записать();
			
			НаборЗаписей = РегистрыСведений.СтавкиНДСНоменклатуры.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
			НаборЗаписей.Отбор.Номенклатура.Установить(Выборка.Номенклатура);
			НаборЗаписей.Отбор.Страна.Установить(ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана());
			
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Период = Выборка.Период;
			НоваяЗапись.Номенклатура = Выборка.Номенклатура;
			НоваяЗапись.Страна = ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана();
			НоваяЗапись.СтавкаНДС = Выборка.СтавкаНДС;
			
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			НаборЗаписей.Записать();
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ЭтоНоваяЗапись()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтавкиНДСНоменклатуры.Период КАК Период
		|ИЗ
		|	РегистрСведений.СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
		|ГДЕ
		|	СтавкиНДСНоменклатуры.Период В(&Период)
		|	И СтавкиНДСНоменклатуры.Номенклатура В(&Номенклатура)
		|	И СтавкиНДСНоменклатуры.Страна В(&Страна)";
	
	Запрос.УстановитьПараметр("Номенклатура", ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("Период", ВыгрузитьКолонку("Период"));
	Запрос.УстановитьПараметр("Страна", ВыгрузитьКолонку("Страна"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли