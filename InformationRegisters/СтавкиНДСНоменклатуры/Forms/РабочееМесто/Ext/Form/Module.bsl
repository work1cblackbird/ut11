#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	
	Страна = ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана();
	Период = ТекущаяДатаСеанса;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокНоменклатуры, "Страна", Страна);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокНоменклатуры, "Период", ?(Не ЗначениеЗаполнено(Период), ТекущаяДатаСеанса, КонецДня(Период)));
	
	ОбновитьИнформациюПоСтавкам();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_Номенклатура" Тогда
		ОбновитьИнформациюПоСтавкам();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокНоменклатуры, "Период", ?(Не ЗначениеЗаполнено(Период), ТекущаяДатаСеанса, КонецДня(Период)));
	ОбновитьИнформациюПоСтавкам();
КонецПроцедуры

&НаКлиенте
Процедура СтранаПриИзменении(Элемент)
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокНоменклатуры, "Страна", Страна);
	ОбновитьИнформациюПоСтавкам();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьСтавкуНДСНоменклатурыПоТекущемуОтбору(Команда)
	
	НачатьУстановкуСтавокНДС(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтавкуНДСНоменклатурыПоВыделеннымСтрокам(Команда)
	
	ВыделенныеСтроки = Элементы.СписокНоменклатуры.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Для продолжения необходимо выделить строки'"));
		Возврат;
	КонецЕсли;
	
	СписокВыделенныхСтрок.Очистить();
	Для Каждого Элемент Из ВыделенныеСтроки Цикл
		СписокВыделенныхСтрок.Добавить(Элемент);
	КонецЦикла;
	
	НачатьУстановкуСтавокНДС(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокНоменклатурыСтавкаНДС.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокНоменклатуры.ЭтоГруппа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не требуется>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	
	// 
	
	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокНоменклатурыСтавкаНДС.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокНоменклатуры.СтавкаНДС");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокНоменклатуры.ЭтоГруппа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не указана>'"));
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИнформациюПоСтавкам()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СтавкиНДСНоменклатуры.СтавкаНДС, ЕСТЬNULL(ОсновныеСтавкиНДС.СтавкаНДС, ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка))) КАК СтавкаНДС,
	|	КОЛИЧЕСТВО(1) КАК Количество
	|ИЗ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНДСНоменклатуры.СрезПоследних(&Период, Страна = &Страна) КАК СтавкиНДСНоменклатуры
	|		ПО СправочникНоменклатура.Ссылка = СтавкиНДСНоменклатуры.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСтавкиНДС.СрезПоследних(&Период, Страна = &Страна) КАК ОсновныеСтавкиНДС
	|		ПО (ИСТИНА)
	|ГДЕ
	|	СправочникНоменклатура.ЭтоГруппа = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(СтавкиНДСНоменклатуры.СтавкаНДС, ЕСТЬNULL(ОсновныеСтавкиНДС.СтавкаНДС, ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)))";
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Страна", Страна);
	
	СписокСтрок = Новый Массив;
	СписокСтрок.Добавить(НСтр("ru='Всего номенклатуры со ставками:'"));
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Не ЗначениеЗаполнено(Выборка.СтавкаНДС) Тогда
			СтавкаНДС = НСтр("ru='<не указана>'");
		Иначе
			СтавкаНДС = Выборка.СтавкаНДС;
		КонецЕсли;
		СписокСтрок.Добавить(СтрШаблон(НСтр("ru='%1 - %2 шт.'"), СтавкаНДС, Выборка.Количество));
	КонецЦикла;
	
	ИнформацияПоСтавкам = Новый ФорматированнаяСтрока(СтрСоединить(СписокСтрок, " "), , ЦветаСтиля.ТекстИнформационнойНадписи);
	
КонецПроцедуры

&НаСервере
Функция УстановитьСтавкуНДСНоменклатурыПоТекущемуОтборуНаСервере(Результат)
	
	Схема = Элементы.СписокНоменклатуры.ПолучитьИсполняемуюСхемуКомпоновкиДанных();
	Настройки = Элементы.СписокНоменклатуры.ПолучитьИсполняемыеНастройкиКомпоновкиДанных();
	
	ИмяМетода = "РегистрыСведений.СтавкиНДСНоменклатуры.УстановитьСтавкуНДСНоменклатурыПоТекущемуОтбору";
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Установка ставок НДС номенклатуры по выделенным строкам'");
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(НастройкиЗапуска, ИмяМетода, Схема, Настройки, Результат);
	
КонецФункции

&НаСервере
Функция УстановитьСтавкуНДСНоменклатурыПоВыделеннымСтрокамНаСервере(СписокНоменклатуры, Результат)
	
	ИмяМетода = "РегистрыСведений.СтавкиНДСНоменклатуры.УстановитьСтавкуНДСНоменклатурыПоВыделеннымСтрокам";
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Установка ставок НДС номенклатуры по выделенным строкам'");
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(НастройкиЗапуска, ИмяМетода, СписокНоменклатуры, Результат);
	
КонецФункции

&НаКлиенте
Процедура НачатьУстановкуСтавокНДС(ПоВыделеннымСтрокам)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Период", Период);
	ПараметрыФормы.Вставить("Страна", Страна);
	ПараметрыФормы.Вставить("ПоВыделеннымСтрокам", ПоВыделеннымСтрокам);
	
	ОткрытьФорму("РегистрСведений.СтавкиНДСНоменклатуры.Форма.ПараметрыУстановкиСтавокНДС", ПараметрыФормы,,,,,
		Новый ОписаниеОповещения("ПараметрыУстановкиСтавокНДСЗавершение", ЭтотОбъект),);
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыУстановкиСтавокНДСЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Если Результат.ПоВыделеннымСтрокам Тогда
			ДлительнаяОперация = УстановитьСтавкуНДСНоменклатурыПоВыделеннымСтрокамНаСервере(СписокВыделенныхСтрок, Результат);
		Иначе
			ДлительнаяОперация = УстановитьСтавкуНДСНоменклатурыПоТекущемуОтборуНаСервере(Результат);
		КонецЕсли;
		Если ДлительнаяОперация <> Неопределено Тогда
			ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗавершенияЗавершенияУстановкиСтавокНДС", ЭтотОбъект);
			ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
			ПараметрыОжидания.ВыводитьСообщения = Истина;
			ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗавершенияЗавершенияУстановкиСтавокНДС(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		Если Результат.Свойство("Сообщения") И Результат.Сообщения <> Неопределено Тогда
			Для каждого Сообщение Из Результат.Сообщения Цикл
				Сообщение.Сообщить();
			КонецЦикла;
		КонецЕсли;
		
		Если Результат.Свойство("Статус") Тогда
			Если Результат.Статус = "Ошибка" Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.КраткоеПредставлениеОшибки);
			ИначеЕсли Результат.Статус = "Выполнено" Тогда
				Элементы.СписокНоменклатуры.Обновить();
				ПоказатьОповещениеПользователя(НСтр("ru='Ставки НДС номенклатуры установлены'"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
