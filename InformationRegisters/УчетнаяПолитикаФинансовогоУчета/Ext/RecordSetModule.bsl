#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Отказ 
	И ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов")
	И Отбор.Период.Использование 
	И Отбор.Организация.Использование Тогда
		
		ВыключеноДисконтирование = Истина;
		Для Каждого Запись Из ЭтотОбъект Цикл
			Если Запись.УчетДисконтированнойКредиторскойЗадолженностиПоставщикам Тогда
					ВыключеноДисконтирование = Ложь;
			КонецЕсли;
		КонецЦикла;
		Если ВыключеноДисконтирование Тогда 
			// Проверяем, если было включено дисконтирование - создадим регламентное задание по пересчету процентов
			Запрос = Новый Запрос(РегистрыСведений.УчетнаяПолитикаФинансовогоУчета.ТекстЗапросаДействующиеПараметрыНалоговУчетныхПолитик());
			Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
			Организации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Отбор.Организация.Значение);
			НастройкиНалоговУчетныхПолитик.ДополнитьМенеджерВременныхТаблицГоловнымиОрганизациями(Запрос.МенеджерВременныхТаблиц, Организации);
			Запрос.УстановитьПараметр("Период", Отбор.Период.Значение);
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Если Выборка.УчетнаяПолитикаСуществует
					И Выборка.УчетДисконтированнойКредиторскойЗадолженностиПоставщикам Тогда
					РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьЗаписьРегистра(
						Отбор.Период.Значение
						,
						,
						Отбор.Организация.Значение,
						Перечисления.ОперацииЗакрытияМесяца.РасчетПроцентныхРасходовДисконтирования);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Отказ И Количество() = 0 Тогда
		
		НаборЗаписей = РегистрыСведений.СтавкиДисконтирования.СоздатьНаборЗаписей();
		Если Отбор.Организация.Использование Тогда
			НаборЗаписей.Отбор.Организация.Установить(Отбор.Организация.Значение);
		КонецЕсли;
		Если Отбор.Период.Использование Тогда
			НаборЗаписей.Отбор.Период.Установить(Отбор.Период.Значение);
		КонецЕсли;
		НаборЗаписей.Записать(Замещение);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли