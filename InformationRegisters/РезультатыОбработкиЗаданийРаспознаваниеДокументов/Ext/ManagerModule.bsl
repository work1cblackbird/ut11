#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ЗаписатьРезультатОбработкиДокумента(ИдентификаторЗадания, РезультатыОбработки) Экспорт
	
	Для Каждого ИдентификаторФайла Из РезультатыОбработки.ФайлыДокумента Цикл
		Запись = РегистрыСведений.РезультатыОбработкиЗаданийРаспознаваниеДокументов.СоздатьМенеджерЗаписи();
		Запись.ИдентификаторРезультата = РезультатыОбработки.ИдентификаторРезультата;
		Запись.ИдентификаторФайла = ИдентификаторФайла;
		Запись.ИдентификаторЗадания = ИдентификаторЗадания;
		Запись.ИменаФайлов = РезультатыОбработки.ИменаФайлов;
		Запись.Записать();
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьИдФайловПоИдРезультата(ИдентификаторРезультата) Экспорт
	
	ИдентификаторыФайлов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РезультатыОбработкиЗаданийРаспознаваниеДокументов.ИдентификаторФайла КАК ИдентификаторФайла
		|ИЗ
		|	РегистрСведений.РезультатыОбработкиЗаданийРаспознаваниеДокументов КАК РезультатыОбработкиЗаданийРаспознаваниеДокументов
		|ГДЕ
		|	РезультатыОбработкиЗаданийРаспознаваниеДокументов.ИдентификаторРезультата = &ИдентификаторРезультата";
	
	Запрос.УстановитьПараметр("ИдентификаторРезультата", ИдентификаторРезультата);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ИдентификаторыФайлов.Добавить(Выборка.ИдентификаторФайла);
		
	КонецЦикла;
	
	Возврат ИдентификаторыФайлов;
	
КонецФункции

Процедура УдалитьЗаписиПоИдРезультата(ИдентификаторРезультата) Экспорт
	
	Набор = РегистрыСведений.РезультатыОбработкиЗаданийРаспознаваниеДокументов.СоздатьНаборЗаписей();
	Набор.Отбор.ИдентификаторРезультата.Установить(ИдентификаторРезультата);
	Набор.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли