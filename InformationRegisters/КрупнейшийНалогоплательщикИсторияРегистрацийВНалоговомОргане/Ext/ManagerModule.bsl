#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует временную таблицу, содержащую данные о регистрациях в ФНС крупнейших налогоплательщиков на дату документа:
// 	* Организация
// 	* РегистрацияВНалоговомОргане.
//
//	Параметры:
//		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц, содержащий таблицу ТаблицаДанныхДокументов с полями
//															Ссылка, Организация, Подразделение, Период.
//
Процедура ПоместитьВременнуюТаблицуДанныеКрупнейшихНалогоплательщиков(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДанныхДокументов.Ссылка КАК Ссылка,
		|	ДанныеРегистрацийОрганизаций.СтруктурнаяЕдиница КАК Организация,
		|	МАКСИМУМ(ДанныеРегистрацийОрганизаций.Период) КАК Период
		|ПОМЕСТИТЬ МаксимумыДанныхОрганизаций
		|ИЗ
		|	РегистрСведений.КрупнейшийНалогоплательщикИсторияРегистрацийВНалоговомОргане КАК ДанныеРегистрацийОрганизаций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
		|		ПО ДанныеРегистрацийОрганизаций.СтруктурнаяЕдиница = ТаблицаДанныхДокументов.Организация
		|		И ДанныеРегистрацийОрганизаций.Период <= ТаблицаДанныхДокументов.Период
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
		|		ПО ДанныеРегистрацийОрганизаций.РегистрацияВНалоговомОргане = РегистрацииВНалоговомОргане.Ссылка
		|		И (РегистрацииВНалоговомОргане.ДатаСнятияСУчета > ДанныеРегистрацийОрганизаций.Период
		|		ИЛИ РегистрацииВНалоговомОргане.ДатаСнятияСУчета = ДАТАВРЕМЯ(1, 1, 1))
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаДанныхДокументов.Ссылка,
		|	ДанныеРегистрацийОрганизаций.СтруктурнаяЕдиница
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	Период
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МаксимумыДанныхОрганизаций.Ссылка КАК Ссылка,
		|	РегистрацииВНалоговомОргане.СтруктурнаяЕдиница КАК Организация,
		|	РегистрацииВНалоговомОргане.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
		|ПОМЕСТИТЬ КрупнейшийНалогоплательщикДанныеОрганизаций
		|ИЗ
		|	РегистрСведений.КрупнейшийНалогоплательщикИсторияРегистрацийВНалоговомОргане КАК РегистрацииВНалоговомОргане
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксимумыДанныхОрганизаций КАК МаксимумыДанныхОрганизаций
		|		ПО РегистрацииВНалоговомОргане.СтруктурнаяЕдиница = МаксимумыДанныхОрганизаций.Организация
		|		И РегистрацииВНалоговомОргане.Период = МаксимумыДанныхОрганизаций.Период
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ МаксимумыДанныхОрганизаций
		|";
	
	РезультатЗапроса = Запрос.Выполнить();
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(СтруктурнаяЕдиница)
	|";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДатаОтсчетаПериодическихСведений() Экспорт
	
	Возврат НачалоМесяца(ОбщегоНазначения.ТекущаяДатаПользователя());
	
КонецФункции

#КонецОбласти

#КонецЕсли