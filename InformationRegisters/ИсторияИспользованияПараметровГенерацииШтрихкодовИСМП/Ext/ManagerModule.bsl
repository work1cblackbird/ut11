#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ПолучениеДанных

Функция ПолучитьПараметрыГенерацииШтрихкодовИзИстории(ХешСуммы) Экспорт
	
	ПараметрыГенерацииШтрихкодов = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ХешСуммы", ХешСуммы);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	История.ХешСуммаУпаковки,
	|	История.ПараметрыГенерацииШтрихкодов
	|ИЗ
	|	РегистрСведений.ИсторияИспользованияПараметровГенерацииШтрихкодовИСМП КАК История
	|ГДЕ
	|	История.ХешСуммаУпаковки В (&ХешСуммы)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ПараметрыГенерацииШтрихкодов.Вставить(Выборка.ХешСуммаУпаковки, Выборка.ПараметрыГенерацииШтрихкодов.Получить());
	КонецЦикла;
	
	Возврат ПараметрыГенерацииШтрихкодов;
	
КонецФункции

#КонецОбласти

#Область ИзменениеДанных

Процедура ЗаписатьПараметрыГенерацииШтрихкодовВИсторию(ПараметрыГенерацииШтрихкодов) Экспорт
	
	Если ПараметрыГенерацииШтрихкодов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ХешСуммаУпаковки", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	Для Каждого КлючИЗначение Из ПараметрыГенерацииШтрихкодов Цикл
		Строка = Таблица.Добавить();
		Строка.ХешСуммаУпаковки = КлючИЗначение.Ключ;
	КонецЦикла;
	
	БлокировкаЗаписей = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаЗаписей.Добавить("РегистрСведений.ИсторияИспользованияПараметровГенерацииШтрихкодовИСМП");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Таблица;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ХешСуммаУпаковки", "ХешСуммаУпаковки");
	
	НачатьТранзакцию();
	
	Попытка
		
		БлокировкаЗаписей.Заблокировать();
		
		Для Каждого КлючИЗначение Из ПараметрыГенерацииШтрихкодов Цикл
			
			Запись = РегистрыСведений.ИсторияИспользованияПараметровГенерацииШтрихкодовИСМП.СоздатьМенеджерЗаписи();
			Запись.ХешСуммаУпаковки             = КлючИЗначение.Ключ;
			Запись.ПараметрыГенерацииШтрихкодов = Новый ХранилищеЗначения(КлючИЗначение.Значение);
			Запись.Записать();
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли