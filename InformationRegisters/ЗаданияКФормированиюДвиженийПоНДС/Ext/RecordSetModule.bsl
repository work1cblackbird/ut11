#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		
		// Перед записью попробуем определить организацию по-умолчанию
		МассивНепроверяемыхРеквизитов = Новый Массив;
		МассивНепроверяемыхРеквизитов.Добавить("Организация");
		
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
		
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если ЭтотОбъект.Количество() = 1 И НЕ ЗначениеЗаполнено(ЭтотОбъект[0].Организация) Тогда
		// При интерактивном добавлении записи регистра в режиме "одна организация" определим организацию по-умолчанию.
		ЭтотОбъект[0].Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
		Если ЭтотОбъект.Отбор.Найти("Организация") <> Неопределено Тогда
			ЭтотОбъект.Отбор.Организация.Значение = ЭтотОбъект[0].Организация;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
