#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьСкидкиТорговыхПредложений(Параметры) Экспорт
	
	Если Параметры.ТорговоеПредложение = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТорговоеПредложение = Параметры.ТорговоеПредложение;
	Опт = Параметры.Опт;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СкидкиТорговыхПредложений.ТорговоеПредложение КАК ТорговоеПредложение,
	|	СкидкиТорговыхПредложений.Количество КАК Количество,
	|	СкидкиТорговыхПредложений.ВидСкидки КАК ВидСкидки
	|ИЗ
	|	РегистрСведений.СкидкиТорговыхПредложений КАК СкидкиТорговыхПредложений
	|ГДЕ
	|	СкидкиТорговыхПредложений.ТорговоеПредложение = &ТорговоеПредложение
	|	И СкидкиТорговыхПредложений.Количество = 1";
	Запрос.УстановитьПараметр("ТорговоеПредложение", ТорговоеПредложение);
	
	Если Опт Тогда
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст, 
			"И СкидкиТорговыхПредложений.Количество = 1", 
			"И СкидкиТорговыхПредложений.Количество <> 1");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;
	
КонецФункции

Процедура ДобавитьСкидкуТорговогоПредложения(ТорговоеПредложение, Скидка, Отказ, Удалить) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ТорговоеПредложение.Установить(ТорговоеПредложение);
		НаборЗаписей.Отбор.ВидСкидки.Установить(Скидка);
		
		ОбщегоНазначенияБЭД.УстановитьУправляемуюБлокировкуПоНаборуЗаписей(НаборЗаписей);
		
		Если Удалить Тогда
			НаборЗаписей.Записать();
			Возврат;
		КонецЕсли;
		
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Количество() Тогда
			
			ТекстСообщения = НСтр("ru = 'Выбранное значение скидки уже используется в скидках за опт'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Скидка", , Отказ);
			ОтменитьТранзакцию();
			Возврат;
			
		КонецЕсли;
		
		СтрокаЗаписи = НаборЗаписей.Добавить();
		СтрокаЗаписи.ТорговоеПредложение = ТорговоеПредложение;
		СтрокаЗаписи.ВидСкидки = Скидка;
		СтрокаЗаписи.Количество = 1;
		
		НаборЗаписей.Записать(Ложь);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Добавление скидки торгового предложения'", ОбщегоНазначения.КодОсновногоЯзыка()), 
			УровеньЖурналаРегистрации.Ошибка,, 
			"СкидкиТорговыхПредложений.ДобавитьСкидкуТорговогоПредложения", 
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли