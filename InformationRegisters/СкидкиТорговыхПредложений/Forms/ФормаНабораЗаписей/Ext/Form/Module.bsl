
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Отбор.Свойство("ТорговоеПредложение", ТорговоеПредложение) 
		Или Не ЗначениеЗаполнено(ТорговоеПредложение) Тогда;
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ТорговыеПредложенияСлужебный.УстановитьУсловноеОформлениеФормыСкидок(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Для Каждого Запись Из НаборЗаписей Цикл
		
		ШаблонПоля = "НаборЗаписей[%1].%2";
		НомерСтроки = НаборЗаписей.Индекс(Запись);
		
		Если Запись.ВидСкидки.Пустая() Тогда
			ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Вид цены""'");
			Поле = СтрШаблон(ШаблонПоля, НомерСтроки, "ВидСкидки");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , Поле, , Отказ);
		КонецЕсли;
		
		Если Запись.Количество = 0 Тогда
			ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Количество""'");
			Поле = СтрШаблон(ШаблонПоля, НомерСтроки, "Количество");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , Поле, , Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	УникальныеЗначенияКоличества = ОбщегоНазначения.ВыгрузитьКолонку(ТекущийОбъект, "Количество", Истина);
	Если УникальныеЗначенияКоличества.Количество() <> ТекущийОбъект.Количество() Тогда
		
		ТекстСообщения = НСтр("ru = 'Колонка ""количество от"" должна содержать уникальные значения.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
		
	КонецЕсли;
	
	Для Каждого Запись Из ТекущийОбъект Цикл
		
		Если Запись.ТорговоеПредложение.Пустая() Тогда
			
			Набор = РегистрыСведений.СкидкиТорговыхПредложений.СоздатьНаборЗаписей();
			Набор.Отбор.ТорговоеПредложение.Установить(ТорговоеПредложение);
			Набор.Отбор.ВидСкидки.Установить(Запись.ВидСкидки);
			Набор.Прочитать();
			
			Если Набор.Количество() Тогда
				
				ШаблонСообщения = НСтр("ru = 'Вид скидки ""%1"" уже используется в скидках торгового предложения'");
				ТекстСообщения = СтрШаблон(ШаблонСообщения, Запись.ВидСкидки);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
				Возврат;
				
			КонецЕсли;
			
			Запись.ТорговоеПредложение = ТорговоеПредложение;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ОповеститьОбИзменении(ТорговоеПредложение);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНаборЗаписей

&НаКлиенте
Процедура НаборЗаписейПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ТекущиеДанные = Элементы.НаборЗаписей.ТекущиеДанные;
	Если ТекущиеДанные.Количество = 1 Тогда
		
		ТекущиеДанные.Количество = 0;
		
		НомерСтроки = НаборЗаписей.Индекс(ТекущиеДанные);
		Поле = СтрШаблон("НаборЗаписей[%1].Количество", НомерСтроки);
		
		ТекстСообщения = НСтр("ru = 'Необходимо указать количество товара 
			|при котором будет предоставлена скидка больше одной единицы'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , Поле, , Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти