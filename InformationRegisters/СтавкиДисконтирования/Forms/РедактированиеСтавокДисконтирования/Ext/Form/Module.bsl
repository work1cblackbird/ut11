
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Параметры.Организация;
	Период = Параметры.Период;
	
	Для Каждого СтрокаСтавки Из Параметры.ЗначенияСтавокДисконтирования Цикл
		НоваяСтрока = ЗначенияСтавокДисконтирования.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСтавки);
	КонецЦикла;

	ТолькоПросмотр = НЕ ПравоДоступа("Изменение", Метаданные.РегистрыСведений.СтавкиДисконтирования);
	
	Элементы.ЗначенияСтавокДисконтирования.ТолькоПросмотр = ТолькоПросмотр;
	ИспользоватьНесколькоВалют = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют");
	Элементы.ЗначенияСтавокДисконтированияВалюта.Видимость = ИспользоватьНесколькоВалют;
	Если Не ИспользоватьНесколькоВалют Тогда
		ВалютаПоУмолчанию = ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию();
	КонецЕсли;
	
	Заголовок = НСтр("ru='Ставки дисконтирования для'") + " " + Организация;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗначенияСтавокДисконтирования

&НаКлиенте
Процедура ЗначенияСтавокДисконтированияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если Не ИспользоватьНесколькоВалют И ЗначенияСтавокДисконтирования.Количество() > 0 Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияСтавокДисконтированияПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Ок(Команда) 
	
	Если Модифицированность Тогда
		
		Если ИспользоватьНесколькоВалют Тогда
			
			ОчиститьСообщения();
			Отказ = Ложь;
			ОшибкиЗаполнения = ПроверкаЗаполнения();
			ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(ОшибкиЗаполнения, Отказ);
			
			Если Отказ Тогда
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
		ВыбранноеЗначение = Новый Массив;
		Для Каждого СтрокаСтавки Из ЗначенияСтавокДисконтирования Цикл
			НоваяСтрока = Новый Структура("Валюта, СтавкаДисконтирования");
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСтавки);
			Если Не ИспользоватьНесколькоВалют Тогда
				НоваяСтрока.Валюта = ВалютаПоУмолчанию;
			КонецЕсли;
			ВыбранноеЗначение.Добавить(НоваяСтрока);
		КонецЦикла;
		
		ОповеститьОВыборе(ВыбранноеЗначение);
		
	Иначе
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПроверкаЗаполнения()
	
	ОшибкиЗаполнения = Неопределено;
	
	МассивВалют = Новый Массив;
	ШаблонОшибкиДублирования = НСтр("ru='Ставка дисконтирования для валюты %1 уже указана'");
	
	Для Каждого СтрокаСтавки Из ЗначенияСтавокДисконтирования Цикл
		Если СтрокаСтавки.Валюта.Пустая() Тогда
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
				ОшибкиЗаполнения,
				"ЗначенияСтавокДисконтирования[%1].Валюта",
				НСтр("ru='Поле Валюта не заполнено'"),
				,
				ЗначенияСтавокДисконтирования.Индекс(СтрокаСтавки));
		ИначеЕсли МассивВалют.Найти(СтрокаСтавки.Валюта) = Неопределено Тогда
			МассивВалют.Добавить(СтрокаСтавки.Валюта);
		Иначе
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
				ОшибкиЗаполнения,
				"ЗначенияСтавокДисконтирования[%1].Валюта",
				СтрШаблон(ШаблонОшибкиДублирования, СтрокаСтавки.Валюта),
				,
				ЗначенияСтавокДисконтирования.Индекс(СтрокаСтавки));
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОшибкиЗаполнения;
	
КонецФункции

#КонецОбласти