#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция получает актуальную ставку дисконтирования.
//
// Параметры:
//  Период - Дата - Период применяемых настроек учетной политики финансового учета .
//  Организация - СправочникСсылка.Организации - Организация договора.
//  Валюта - СправочникСсылка.Валюты - Валюта взаиморасчетов договора.
//
// Возвращаемое значение:
//  Число - Значение ставки дисконтирования по умолчанию.
//
Функция ПолучитьСтавкуДисконтированияПоУмолчанию(Период, Организация, Валюта) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Организация) ИЛИ НЕ ЗначениеЗаполнено(Валюта) Тогда
		Возврат 0;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ГоловнаяОрганизация"));
	Отбор.Вставить("Валюта", Валюта);
	СтруктураРесурсов = РегистрыСведений.СтавкиДисконтирования.Получить(Период, Отбор);
	
	Возврат СтруктураРесурсов.СтавкаДисконтирования;
	
КонецФункции

// Определяет массив организаций использующих дисконтирование или использовавших его ранее
//
// Возвращаемое значение:
//  Массив из СправочникСсылка.Организации - Организации 
//
Функция ПолучитьОрганизацииСВключеннымДисконтированием() Экспорт
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Организации.Ссылка КАК Организация
		|ИЗ
		|	Справочник.Организации КАК Организации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета КАК УчетнаяПолитикаФинансовогоУчета
		|		ПО Организации.ГоловнаяОрганизация = УчетнаяПолитикаФинансовогоУчета.Организация
		|ГДЕ
		|	УчетнаяПолитикаФинансовогоУчета.УчетДисконтированнойКредиторскойЗадолженностиПоставщикам";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Добавить(Выборка.Организация);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти
	
#КонецЕсли
