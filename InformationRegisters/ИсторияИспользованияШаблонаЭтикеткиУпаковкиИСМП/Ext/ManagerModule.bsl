#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ИзменениеДанных

Функция ИнициализироватьТаблицуДляЗаписиДанных() Экспорт
	
	ТаблицаДляЗаписиДанных = Новый ТаблицаЗначений;
	ТаблицаДляЗаписиДанных.Колонки.Добавить("ХешСуммаУпаковки", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ТаблицаДляЗаписиДанных.Колонки.Добавить("СоставУпаковки",   Новый ОписаниеТипов("ДеревоЗначений"));
	
	Возврат ТаблицаДляЗаписиДанных;
	
КонецФункции

Функция ИнициализироватьДеревоСоставаУпаковки() Экспорт
	
	СоставУпаковки = Новый ДеревоЗначений;
	СоставУпаковки.Колонки.Добавить("Номенклатура",          Метаданные.ОпределяемыеТипы.Номенклатура.Тип);
	СоставУпаковки.Колонки.Добавить("Характеристика",        Метаданные.ОпределяемыеТипы.ХарактеристикаНоменклатуры.Тип);
	СоставУпаковки.Колонки.Добавить("GTIN",                  Метаданные.ОпределяемыеТипы.GTIN.Тип);
	СоставУпаковки.Колонки.Добавить("ШаблонЭтикетки",        Метаданные.ОпределяемыеТипы.ШаблонЭтикеткиИС.Тип);
	СоставУпаковки.Колонки.Добавить("КоличествоЭкземпляров", ОбщегоНазначения.ОписаниеТипаЧисло(10));
	
	Возврат СоставУпаковки;
	
КонецФункции

Процедура ЗаписатьДанные(ТаблицаДляЗаписиДанных) Экспорт
	
	Если ТаблицаДляЗаписиДанных.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	БлокировкаЗаписей = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаЗаписей.Добавить("РегистрСведений.ИсторияИспользованияШаблонаЭтикеткиУпаковкиИСМП");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ТаблицаДляЗаписиДанных;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ХешСуммаУпаковки", "ХешСуммаУпаковки");
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	Попытка
		
		БлокировкаЗаписей.Заблокировать();
		
		Для Каждого СтрокаДанных Из ТаблицаДляЗаписиДанных Цикл
			
			Запись = РегистрыСведений.ИсторияИспользованияШаблонаЭтикеткиУпаковкиИСМП.СоздатьМенеджерЗаписи();
			Запись.ХешСуммаУпаковки = СтрокаДанных.ХешСуммаУпаковки;
			Запись.СоставУпаковки   = Новый ХранилищеЗначения(СтрокаДанных.СоставУпаковки);
			Запись.Записать();
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли