
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДопустимыеПотребительскиеСвойства(ОКПД2, Назначение, Страна = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(ОКПД2) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Страна = Неопределено Тогда
		Страна = Справочники.СтраныМира.Россия;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СтраныМира.КодАльфа2 КАК КодАльфа2
	|ПОМЕСТИТЬ КодСтраныНазначения
	|ИЗ
	|	Справочник.СтраныМира КАК СтраныМира
	|ГДЕ
	|	СтраныМира.Ссылка = &Страна
	|;
	|
	|/////////////////////////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.ПотребительскоеСвойство КАК ПотребительскоеСвойство,
	|	ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.ОКПД2                   КАК ОКПД2,
	|	ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.ДиапазонС               КАК ДиапазонС,
	|	ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.ДиапазонПо              КАК ДиапазонПо,
	|	ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.ТипЗначения             КАК ТипЗначения,
	|	ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.ДопустимыеЗначения      КАК ДопустимыеЗначения,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(28))                                         КАК ПредставлениеДиапазона,
	|
	|	ЕСТЬNULL(ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.ПотребительскоеСвойство.Идентификатор, """") КАК ПотребительскоеСвойствоИдентификатор
	|ИЗ
	|	РегистрСведений.ДопустимыеЗначенияПотребительскихСвойствЗЕРНО КАК ДопустимыеЗначенияПотребительскихСвойствЗЕРНО
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ КодСтраныНазначения КАК КодСтраныНазначения
	|		ПО ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.КодСтраны = КодСтраныНазначения.КодАльфа2
	|ГДЕ
	|	ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.ОКПД2 В (&ОКПД2)
	|	И ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.Назначение = &Назначение
	|	И (ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.ДействуетПо >= &Дата
	|		Или ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.ДействуетПо = ДатаВремя(1,1,1))
	|";
	Запрос.УстановитьПараметр("ОКПД2", ОКПД2);
	Запрос.УстановитьПараметр("Назначение", Назначение);
	Запрос.УстановитьПараметр("Дата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("Страна", Страна);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПотребительскиеСвойства = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаРезультата Из ПотребительскиеСвойства Цикл
		СтрокаРезультата.ПредставлениеДиапазона = ИнтеграцияЗЕРНОКлиентСервер.ПредставлениеДопустимыхЗначенийПоДиапазону(
			СтрокаРезультата);
	КонецЦикла;
	
	ПотребительскиеСвойства.Индексы.Добавить("ОКПД2");
	ПотребительскиеСвойства.Индексы.Добавить("ПотребительскоеСвойство");
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ПотребительскиеСвойства;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий


#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс


#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#КонецОбласти

#КонецЕсли
