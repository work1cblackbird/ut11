#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	СуммаИтог = 0; ПроцентыИтог = 0; КомиссияИтог = 0;
	Для каждого СтрокаНабора Из ЭтотОбъект Цикл
		
		СуммаИтог = СуммаИтог + СтрокаНабора.Сумма;
		СтрокаНабора.СуммаИтог = СуммаИтог;
		
		ПроцентыИтог = ПроцентыИтог + СтрокаНабора.Проценты;
		СтрокаНабора.ПроцентыИтог = ПроцентыИтог;
		
		КомиссияИтог = КомиссияИтог + СтрокаНабора.Комиссия;
		СтрокаНабора.КомиссияИтог = КомиссияИтог;
	КонецЦикла;
	
	СФормироватьТаблицуОбъектовОплаты();
	
	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	РегистрыСведений.ГрафикПлатежей.РассчитатьГрафикПлатежейПоФинансовымИнструментам(
		ДополнительныеСвойства.ТаблицаОбъектовОплаты.ВыгрузитьКолонку("ОбъектОплаты"));
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Для Каждого СтрокаНабора Из ЭтотОбъект Цикл
			ЗаполнитьЗначенияСвойств(СтрокаНабора, ДанныеЗаполнения);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует таблицу заказов, которые были раньше в движениях и которые сейчас будут записаны.
//
Процедура СФормироватьТаблицуОбъектовОплаты()
	
	Договор = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Отбор.ВариантГрафика.Значение, "Владелец");
	
	ТаблицаОбъектовОплаты = Новый ТаблицаЗначений;
	ТаблицаОбъектовОплаты.Колонки.Добавить("ОбъектОплаты");
	ТаблицаОбъектовОплаты.Колонки.Добавить("ВариантГрафика");
	НоваяСтрока = ТаблицаОбъектовОплаты.Добавить();
	НоваяСтрока.ОбъектОплаты = Договор;
	НоваяСтрока.ВариантГрафика = Отбор.ВариантГрафика.Значение;
	
	ДополнительныеСвойства.Вставить("ТаблицаОбъектовОплаты", ТаблицаОбъектовОплаты);
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()
	
	Если НЕ ДополнительныеСвойства.Свойство("МенеджерВременныхТаблиц") Тогда
		ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц());
	КонецЕсли;
	
	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ГрафикОплатКредитовИДепозитов.Период КАК Период,
	|	ГрафикОплатКредитовИДепозитов.ВариантГрафика КАК ВариантГрафика,
	|	ГрафикОплатКредитовИДепозитов.Сумма КАК Сумма,
	|	ГрафикОплатКредитовИДепозитов.Проценты КАК Проценты,
	|	ГрафикОплатКредитовИДепозитов.Комиссия КАК Комиссия
	|ПОМЕСТИТЬ ГрафикОплатКредитовИДепозитовПередЗаписью
	|ИЗ
	|	РегистрСведений.ГрафикОплатКредитовИДепозитов КАК ГрафикОплатКредитовИДепозитов
	|ГДЕ
	|	&УсловиеПериод
	|	И &УсловиеВариантГрафика
	|";
	
	Если Отбор.Период.Использование Тогда
		УсловиеПериод = "ГрафикОплатКредитовИДепозитов.Период = &Период";
	Иначе
		УсловиеПериод = "ИСТИНА";
	КонецЕсли;
	
	Если Отбор.ВариантГрафика.Использование Тогда
		УсловиеВариантГрафика = "ГрафикОплатКредитовИДепозитов.ВариантГрафика = &ВариантГрафика";
	Иначе
		УсловиеВариантГрафика = "ИСТИНА";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПериод", УсловиеПериод);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеВариантГрафика", УсловиеВариантГрафика);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	Если Отбор.Период.Использование Тогда
		Запрос.УстановитьПараметр("Период", Отбор.Период.Значение);
	КонецЕсли;
	
	Если Отбор.ВариантГрафика.Использование Тогда
		Запрос.УстановитьПараметр("ВариантГрафика", Отбор.ВариантГрафика.Значение);
	КонецЕсли;
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу для последующей записи в регистрах заданий.
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Таблица.Период КАК Период,
	|	Таблица.ВариантГрафика КАК ВариантГрафика
	|ПОМЕСТИТЬ ТаблицаИзмененийГрафикОплатКредитовИДепозитов
	|ИЗ
	|	(ВЫБРАТЬ
	|		ГрафикОплатКредитовИДепозитов.Период КАК Период,
	|		ГрафикОплатКредитовИДепозитов.ВариантГрафика КАК ВариантГрафика,
	|		ГрафикОплатКредитовИДепозитов.Сумма КАК Сумма,
	|		ГрафикОплатКредитовИДепозитов.Проценты КАК Проценты,
	|		ГрафикОплатКредитовИДепозитов.Комиссия КАК Комиссия
	|	ИЗ
	|		ГрафикОплатКредитовИДепозитовПередЗаписью КАК ГрафикОплатКредитовИДепозитов
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ГрафикОплатКредитовИДепозитов.Период КАК Период,
	|		ГрафикОплатКредитовИДепозитов.ВариантГрафика КАК ВариантГрафика,
	|		-ГрафикОплатКредитовИДепозитов.Сумма КАК Сумма,
	|		-ГрафикОплатКредитовИДепозитов.Проценты КАК Проценты,
	|		-ГрафикОплатКредитовИДепозитов.Комиссия КАК Комиссия
	|	ИЗ
	|		РегистрСведений.ГрафикОплатКредитовИДепозитов КАК ГрафикОплатКредитовИДепозитов
	|	ГДЕ
	|		&УсловиеПериод
	|		И &УсловиеВариантГрафика
	|	) КАК Таблица
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Период,
	|	Таблица.ВариантГрафика
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Сумма) <> 0
	|	ИЛИ СУММА(Таблица.Проценты) <> 0
	|	ИЛИ СУММА(Таблица.Комиссия) <> 0
	|
	|;
	|
	|УНИЧТОЖИТЬ ГрафикОплатКредитовИДепозитовПередЗаписью
	|";
	
	Если Отбор.Период.Использование Тогда
		УсловиеПериод = "ГрафикОплатКредитовИДепозитов.Период = &Период";
	Иначе
		УсловиеПериод = "ИСТИНА";
	КонецЕсли;
	
	Если Отбор.ВариантГрафика.Использование Тогда
		УсловиеВариантГрафика = "ГрафикОплатКредитовИДепозитов.ВариантГрафика = &ВариантГрафика";
	Иначе
		УсловиеВариантГрафика = "ИСТИНА";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПериод", УсловиеПериод);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеВариантГрафика", УсловиеВариантГрафика);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	Если Отбор.Период.Использование Тогда
		Запрос.УстановитьПараметр("Период", Отбор.Период.Значение);
	КонецЕсли;
	
	Если Отбор.ВариантГрафика.Использование Тогда
		Запрос.УстановитьПараметр("ВариантГрафика", Отбор.ВариантГрафика.Значение);
	КонецЕсли;
	
	Запрос.ВыполнитьПакет();
	
	ЗакрытиеМесяцаСервер.ОтразитьЗаданияКЗакрытиюМесяца(
		ПроведениеДокументов.ЭмуляцияДокумента(Неопределено, '00010101'),
		ДополнительныеСвойства.МенеджерВременныхТаблиц);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли