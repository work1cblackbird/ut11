
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	// ОбменДанными.Загрузка
	// Перед записью объекта, необходимо актуализировать служебные реквизиты и зачистить неиспользуемые,
	// но возможно выбранные значения отборов, в процессе настройки узла.
	ПланыОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат.АктуализацияРеквизитовУзлаПланаОбмена(ЭтотОбъект);
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбменДаннымиСобытияУТУП.ОбновитьКэшМеханизмовРегистрации();
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	// ОбменДанными.Загрузка
	// Обмен может быть удален программно. 
	// В этом случае, необходимо актуализировать константу ИспользуетсяОбменСБухгалтериейПредприятия.
	Если ПолучитьФункциональнуюОпцию("УправлениеТорговлей")
		И (ВариантНастройки = ОбменДаннымиЛокализация.ИдентификаторНастройкиОбменаБП30()
		Или ВариантНастройки = ОбменДаннымиЛокализация.ИдентификаторНастройкиОбменаБПКОРП30()
		Или ВариантНастройки = ОбменДаннымиЛокализация.ИдентификаторНастройкиОбменаБазовойУТБП30()
		Или ВариантНастройки = "ОбменУниверсальный") Тогда
		
		ОбменДаннымиУТ.АктуализироватьПризнакИспользованияОбменаСБухгалтерией(ЭтотОбъект, Истина);
		
	КонецЕсли;

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения = Неопределено Тогда
		
		// НастройкаОтборовНаУзле.
		ИспользоватьОтборПоОрганизациям      = Ложь;
		ДатаНачалаВыгрузкиДокументов         = НачалоГода(ТекущаяДатаСеанса());
		ПравилаОтправкиСправочников          = "АвтоматическаяСинхронизация";
		ПравилаОтправкиДокументов            = "АвтоматическаяСинхронизация";
		ПравилаСозданияДоговоровКонтрагентов = "БезРазделения";
		РежимВыгрузкиПриНеобходимости        = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПриНеобходимости;
		УправленческаяОрганизация            = Справочники.Организации.УправленческаяОрганизация;
		ВыгружатьУправленческуюОрганизацию   = Ложь;
		ВыгружатьАналитикуПоСкладам          = Истина;
		ВерсияФорматаОбмена                  = "1.3";
		ВыгружатьЦеныНоменклатуры            = ВариантНастройки = "ОбменКасса";
				
		Если ВариантНастройки = "ОбменУПБП"
			Или ВариантНастройки = ОбменДаннымиЛокализация.ИдентификаторНастройкиОбменаБП30()
			Или ВариантНастройки = ОбменДаннымиЛокализация.ИдентификаторНастройкиОбменаБПКОРП30() 
			Тогда
			ВерсияФорматаОбмена = "1.5";
			НеВыгружатьКартинки = Истина;
		КонецЕсли;
	
		Если ВариантНастройки <> "ОбменУП2ЗУП3" Тогда
			РазделыУчета.Загрузить(ПланыОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат.ПолучитьРазделыУчета(
				Ложь, ВариантНастройки));
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли