
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	УзелОбмена = ПараметрКоманды;
	
	Состояние(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 начат обмен данными с сайтом'"),
			Формат(ОбщегоНазначенияКлиент.ДатаСеанса(), "ДЛФ=DT"))
		,,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'по узлу обмена ""%1""...'"),
			УзелОбмена));
			
			
	ПараметрыОбновленияСписков = Новый Структура;
	ПараметрыОбновленияСписков.Вставить("Обновить", Ложь);
	ПараметрыОбновленияСписков.Вставить("ИмяДокументаЗаказ", "");
	
	ОбменВыполненСервер(УзелОбмена, ПараметрыОбновленияСписков);
	
	Если ПараметрыОбновленияСписков.Обновить Тогда
		
		ОповеститьОбИзменении(Тип("ДокументСсылка." + ПараметрыОбновленияСписков.ИмяДокументаЗаказ));
		
	КонецЕсли;
		
	ПоказатьОповещениеПользователя(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 ""%2""'"),
			Формат(ОбщегоНазначенияКлиент.ДатаСеанса(), "ДЛФ=DT"),
			УзелОбмена) 
		,,
		НСтр("ru = 'Обмен с сайтом завершен'"),
		БиблиотекаКартинок.Информация32);
		
	Оповестить("ЗавершенСеансОбменаССайтом");

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ОбменВыполненСервер(УзелОбмена, ПараметрыОбновления)

	Если ОбменССайтомПовтИсп.ПолучитьЭтотУзелПланаОбмена(УзелОбмена) Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Если УзелОбмена.ПометкаУдаления Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	ОбменССайтомСобытия.ВыполнитьОбмен(УзелОбмена, НСтр("ru = 'Интерактивный обмен'"), ,ПараметрыОбновления);
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти
