
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(АдресСертификата) Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресСертификата);
		ТекущийОбъект.СертификатМобильногоПриложенияIOS = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных());
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ПриЧтенииСозданииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийШапкиФормы

&НаКлиенте
Процедура ИспользоватьAPNSПриИзменении(Элемент)
	
	Если НЕ Объект.ИспользоватьAPNS Тогда
		СертификатЗагружен = Ложь;
	КонецЕсли;
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьGCMПриИзменении(Элемент)
	
	Если НЕ Объект.ИспользоватьGCM Тогда
		Объект.НомерПриложенияGoogleCloud = "";
		Объект.КлючСервераОтправителяGCM = "";
	КонецЕсли;
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьPushУведомленияПриИзменении(Элемент)
	
	ИспользоватьPushУведомленияПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьPushУведомленияРасширеннаяПодсказкаНажатие(Элемент)
	
	ОбщегоНазначенияКлиент.ОткрытьНавигационнуюСсылку("http://its.1c.ru/db/v83doc#bookmark:dev:TI000001541");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьСертификат(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьСертификатЗавершение", ЭтотОбъект);
	НачатьПомещениеФайла(Оповещение, , "", Истина, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьЛогин(Команда)
	
	ОбщегоНазначенияКлиент.ОткрытьНавигационнуюСсылку("https://pushnotifications.1c.com/push/publishers/new");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗагрузитьСертификатЗавершение(Результат, АдресВременногоХранилища, ВыбранноеИмя, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
		АдресСертификата = АдресВременногоХранилища;
		СертификатЗагружен = Не ПустаяСтрока(АдресСертификата);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ИспользоватьPushУведомленияПриИзмененииНаСервере()
	
	Если НЕ Объект.ВариантОтправкиPushУведомлений = Перечисления.ВариантыОтправкиPushУведомлений.ОтправлятьНепосредственно Тогда
		Если Объект.ИспользоватьAPNS Тогда
			Объект.ИспользоватьAPNS = Ложь;
		КонецЕсли;
		Если Объект.ИспользоватьGCM Тогда
			Объект.ИспользоватьGCM = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ВариантОтправкиPushУведомлений = Перечисления.ВариантыОтправкиPushУведомлений.ОтправлятьЧерезВспомогательныйСервис Тогда
		Элементы.ГруппаСторонние.Доступность = Ложь;
		Элементы.ГруппаСервис.Доступность = Истина;
	ИначеЕсли Объект.ВариантОтправкиPushУведомлений = Перечисления.ВариантыОтправкиPushУведомлений.ОтправлятьНепосредственно Тогда
		Элементы.ГруппаСторонние.Доступность = Истина;
		Элементы.ГруппаСервис.Доступность = Ложь;
	Иначе
		Элементы.ГруппаСторонние.Доступность = Ложь;
		Элементы.ГруппаСервис.Доступность = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	Заголовок = НСтр("ru = 'Настройка отправки push-уведомлений пользователям мобильного приложения ""1С:Заказы"".'");
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ГлавныйУзел = ПланыОбмена.МобильноеПриложениеЗаказыКлиентов.ГлавныйУзелОбмена();
		ЗначениеВРеквизитФормы(ГлавныйУзел.ПолучитьОбъект(), "Объект");
	КонецЕсли;
	ХранилищеСертификата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "СертификатМобильногоПриложенияIOS");
	Если ХранилищеСертификата <> Неопределено Тогда
		Сертификат = ХранилищеСертификата.Получить();
		СертификатЗагружен = ЗначениеЗаполнено(Сертификат);
	Иначе
		СертификатЗагружен = Ложь;
	КонецЕсли;
	ИспользоватьPushУведомленияПриИзмененииНаСервере();
	УстановитьДоступность();
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступность()
	
	Элементы.НомерПриложения.Доступность = Объект.ИспользоватьGCM;
	Элементы.КлючОтправителя.Доступность = Объект.ИспользоватьGCM;
	Элементы.ЗагрузитьСертификат.Доступность = Объект.ИспользоватьAPNS;
КонецПроцедуры

#КонецОбласти
