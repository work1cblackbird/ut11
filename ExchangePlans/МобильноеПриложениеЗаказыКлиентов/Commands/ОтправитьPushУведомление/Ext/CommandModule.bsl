
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Получатели = Новый Массив;
	УказаныПолучатели = Ложь;
	Если ТипЗнч(ПараметрыВыполненияКоманды.Источник) = Тип("ФормаКлиентскогоПриложения") Тогда
		Если ПараметрыВыполненияКоманды.Источник.ИмяФормы = "ПланОбмена.МобильноеПриложениеЗаказыКлиентов.Форма.ФормаСписка" Тогда
			Для Каждого Элемент Из ПараметрыВыполненияКоманды.Источник.ПодчиненныеЭлементы.Список.ВыделенныеСтроки Цикл
				Получатели.Добавить(Элемент);
			КонецЦикла;
			УказаныПолучатели = Истина;
		ИначеЕсли ПараметрыВыполненияКоманды.Источник.ИмяФормы = "ПланОбмена.МобильноеПриложениеЗаказыКлиентов.Форма.ФормаУзла" Тогда
			ПланОбменаОбъект = ПараметрыВыполненияКоманды.Источник.Объект;//ПланОбменаОбъект -  
			Получатели.Добавить(ПланОбменаОбъект.Ссылка);
			УказаныПолучатели = Истина;
		КонецЕсли;
	КонецЕсли;
	Если УказаныПолучатели И Получатели.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Укажите получателей уведомления'"),, НСтр("ru = 'Push - уведомление'"));
		Возврат;
	КонецЕсли;
	ОтправитьPushУведомлениеПользователям(ПараметрыВыполненияКоманды.Источник, Получатели, УказаныПолучатели);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция НастроеныPushУведомления()
	
	Возврат ПланыОбмена.МобильноеПриложениеЗаказыКлиентов.ПланОбменаИспользуетОтправкуPushУведомлений();
КонецФункции

&НаКлиенте
Процедура ОтправитьPushУведомлениеПользователям(ФормаВладелец, Получатели, УказаныПолучатели)
	
	Если НастроеныPushУведомления() Тогда
		ПараметрыФормы = Новый Структура("Получатели, УказаныПолучатели", Получатели, УказаныПолучатели);
		ОткрытьФорму("ПланОбмена.МобильноеПриложениеЗаказыКлиентов.Форма.ФормаОтправкиPushУведомления", ПараметрыФормы, ФормаВладелец);
	Иначе
		ДополнительныеПараметры = Новый Структура("ФормаВладелец, ПолучателиУведомления, УказаныПолучатели", ФормаВладелец, Получатели, УказаныПолучатели);
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеНастройкиИспользованияPushУведомлений", ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("ПланОбмена.МобильноеПриложениеЗаказыКлиентов.Форма.ФормаГлавногоУзла",, ФормаВладелец,,,, ОписаниеОповещения);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеНастройкиИспользованияPushУведомлений(Знач Результата, Знач ДополнительныеПараметры) Экспорт
	
	Если НастроеныPushУведомления() Тогда
		ОтправитьPushУведомлениеПользователям(ДополнительныеПараметры.ФормаВладелец,
			ДополнительныеПараметры.ПолучателиУведомления, ДополнительныеПараметры.УказаныПолучатели);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
