#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УзелУжеСуществует = МобильныеПриложения.УзелДляПодключенияСуществует(Пользователь, МобильныйКомпьютер, Ссылка);	
	
	Если УзелУжеСуществует Тогда
		ТекстСообщения = НСтр("ru='Настройка для торгового представителя %Пользователь% и мобильного компьютера %МобильныйКомпьютер% уже существует'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Пользователь%", Пользователь);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%МобильныйКомпьютер%", МобильныйКомпьютер);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Отказ = Истина;
	КонецЕсли;
		
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Код = "";
	Наименование = "";
	Пользователь = Неопределено;
	МобильныйКомпьютер = Неопределено;
	ИмяПользователя = "";
	Пароль = "";
	ХранилищеОтбораСхемыОбмена = Неопределено;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	// Для этого узла не требуется проверка заполнения ряда реквизитов
	Если Ссылка = ПланыОбмена.МобильноеПриложениеТорговыйПредставитель.ЭтотУзел() Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Пользователь");
		МассивНепроверяемыхРеквизитов.Добавить("МобильныйКомпьютер");
		МассивНепроверяемыхРеквизитов.Добавить("ВерсияМобильногоПриложения");
		МассивНепроверяемыхРеквизитов.Добавить("ИспользованиеКонтактнойИнформации");
		МассивНепроверяемыхРеквизитов.Добавить("ИмяПользователя");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);

КонецПроцедуры

#КонецОбласти

#КонецЕсли