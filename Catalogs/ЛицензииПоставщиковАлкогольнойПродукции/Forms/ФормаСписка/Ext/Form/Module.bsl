
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если Параметры.Свойство("Партнер") Тогда
		Партнер = Параметры.Партнер;
		СписокПартнеров = Новый СписокЗначений;
		ПартнерыИКонтрагенты.ЗаполнитьСписокПартнераСРодителями(Партнер,СписокПартнеров);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"Партнер",
			СписокПартнеров,
			ВидСравненияКомпоновкиДанных.ВСписке,
			,
			Истина);
	ИначеЕсли Параметры.Отбор.Свойство("Владелец") Тогда
		Владелец = Параметры.Отбор.Владелец;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, "Контрагент",
			"Видимость", Ложь);
	КонецЕсли;

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если НЕ Копирование И ЗначениеЗаполнено(Партнер) Тогда
		Отказ = Истина;
		ПараметрыФормы = Новый Структура("Партнер", Партнер);
	ИначеЕсли ЗначениеЗаполнено(Владелец) Тогда
		Отказ = Истина;
		ПараметрыФормы = Новый Структура("ПоВладельцу", Истина);
		ПараметрыФормы.Вставить("ЗначенияЗаполнения",
			Новый Структура("Владелец,Наименование,ВидЛицензии,КемВыдана"));
		ПараметрыФормы.ЗначенияЗаполнения.Владелец = Владелец;
		Если Копирование Тогда
			ЗаполнитьЗначенияСвойств(ПараметрыФормы.ЗначенияЗаполнения, Элемент.ТекущиеДанные);
		КонецЕсли;
	КонецЕсли;
	
	Если Отказ Тогда
		ОткрытьФорму(
			"Справочник.ЛицензииПоставщиковАлкогольнойПродукции.Форма.ФормаЭлемента",
			ПараметрыФормы,
			ЭтаФорма,,,, Неопределено, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

