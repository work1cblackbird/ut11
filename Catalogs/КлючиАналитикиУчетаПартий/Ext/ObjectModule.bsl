#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Не ЭтоНовый() И Не ПометкаУдаления Тогда
		СоздатьАналитикуКлюча();
		Наименование = Справочники.КлючиАналитикиУчетаПартий.НаименованиеКлючаАналитики(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьАналитикуКлюча();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьАналитикуКлюча()
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Ключи.Ссылка КАК Ссылка,
	|	Ключи.ГруппаФинансовогоУчета,
	|	Ключи.Поставщик,
	|	Ключи.Контрагент,
	|	Ключи.НалогообложениеНДС,
	|	Ключи.СтавкаНДС,
	|	Ключи.ВидЦенности,
	|	Ключи.КодСтроки
	|ИЗ
	|	Справочник.КлючиАналитикиУчетаПартий КАК Ключи
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК Аналитика
	|		ПО Аналитика.КлючАналитики = Ключи.Ссылка
	|ГДЕ
	|	Ключи.Ссылка = &Ссылка
	|	И Аналитика.КлючАналитики ЕСТЬ NULL
	|");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		МенеджерЗаписи = РегистрыСведений.АналитикаУчетаПартий.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.КлючАналитики = Выборка.Ссылка;
		МенеджерЗаписи.Записать(Истина);
	КонецЕсли;
КонецПроцедуры

Процедура УдалитьАналитикуКлюча()
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	РегистрСведений.АналитикаУчетаПартий КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.КлючАналитики = &Ключ
	|");
	Запрос.УстановитьПараметр("Ключ", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.АналитикаУчетаПартий.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.Удалить();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
