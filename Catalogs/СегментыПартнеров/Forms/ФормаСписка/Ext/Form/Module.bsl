
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьЗапретОтгрузки") Тогда
		Элементы.СтраницыСегменты.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли; 
	
	СозданиеДоступно = ПравоДоступа("Добавление", Метаданные.Справочники.СегментыПартнеров);
	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить("СписокЗапретаОтгрузкиСоздатьСегментЗапретаОтгрузки");
	МассивЭлементов.Добавить("СписокЗапретаОтгрузкиКонтекстноеМенюСоздатьСегментЗапретаОтгрузки");
	МассивЭлементов.Добавить("СписокЗапретаОтгрузкиСкопировать");
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "Видимость", СозданиеДоступно);
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.СегментыПартнеров);
	Элементы.ФормаИзменитьВыделенные.Видимость                  = МожноРедактировать;
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость  = МожноРедактировать;
	Элементы.СписокЗапретаОтгрузкиИзменитьВыделенные.Видимость  = МожноРедактировать;
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные1.Видимость = МожноРедактировать;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	
	ПараметрыРазмещения = Новый Структура;
	ПараметрыРазмещения.Вставить("Источники", Новый ОписаниеТипов("СправочникСсылка.СегментыПартнеров"));
	ПараметрыРазмещения.Вставить("КоманднаяПанель", Элементы.СписокЗапретаОтгрузки.КоманднаяПанель);
	ПараметрыРазмещения.Вставить("ПрефиксГрупп", "ЗапретОтгрузки");
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	Если Элементы.СтраницыСегменты.ТекущаяСтраница = Элементы.СегментыПартнеров Тогда
		ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	ИначеЕсли Элементы.СтраницыСегменты.ТекущаяСтраница = Элементы.СегментыЗапретаОтгрузки Тогда
		ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.СписокЗапретаОтгрузки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСегментЗапретаОтгрузки(Команда)
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("ЗапретОтгрузки",     Истина);
	ДанныеЗаполнения.Вставить("СпособФормирования", ПредопределенноеЗначение("Перечисление.СпособыФормированияСегментов.ПериодическиОбновлять"));
	
	СтруктураПараметры = Новый Структура;
	СтруктураПараметры.Вставить("Основание", ДанныеЗаполнения);
	ОткрытьФорму("Справочник.СегментыПартнеров.ФормаОбъекта", СтруктураПараметры, Элементы.СписокЗапретаОтгрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	Если Элементы.СтраницыСегменты.ТекущаяСтраница = Элементы.СегментыЗапретаОтгрузки Тогда
		ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.СписокЗапретаОтгрузки);
	Иначе
		ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти
