#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Снимает признак Действует для массива подписок. 
//
// Параметры:
//  Подписки  - Массив - массив отменяемых подписок.
//
Процедура ОтменитьПодпискуДляМассива(Подписки) Экспорт
	
	Для Каждого Подписка Из Подписки Цикл
		
		ОтменитьПодписку(Подписка);
		
	КонецЦикла;
	
КонецПроцедуры

// Оформляет подписку для массива подписок. Если подписка существует то для нее устанавливает признак "Действует".
// Если подписка не существует, то создается новая.
//
// Параметры:
//  Подписки  - Массив, - массив существующих подписок или структур, содержащих подписчика и группу.
//
Процедура ОформитьПодпискуДляМассиваСуществующихИНовыхПодписок(Подписки) Экспорт

	СуществующиеПодписки = Новый Массив;
	НовыеПодписки        = Новый Массив;
	
	Для каждого Подписка Из Подписки Цикл
	
		Если ТипЗнч(Подписка) = Тип("СправочникСсылка.ПодпискиНаРассылкиИОповещенияКлиентам") Тогда
			СуществующиеПодписки.Добавить(Подписка);
		ИначеЕсли ТипЗнч(Подписка) = Тип("Структура") Тогда
			НовыеПодписки.Добавить(Подписка);
		КонецЕсли;
	
	КонецЦикла;
	
	Если СуществующиеПодписки.Количество() > 0 Тогда
		ОформитьПодпискуДляМассиваСуществующих(СуществующиеПодписки);
	КонецЕсли;
	
	Если НовыеПодписки.Количество() Тогда
		ОформитьПодпискуДляМассиваНовых(НовыеПодписки);
	КонецЕсли;

КонецПроцедуры

// Оформляет подписку для массива существующих подписок. Устанавливается признак "Действует".
//
// Параметры:
//  МассивПодписок  - Массив, - массив существующих подписок.
//
Процедура ОформитьПодпискуДляМассиваСуществующих(МассивПодписок) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПодпискиНаРассылкиИОповещенияКлиентам.Ссылка,
	|	ПодпискиНаРассылкиИОповещенияКлиентам.ПодпискаДействует,
	|	ГруппыРассылокИОповещений.Принудительная,
	|	ГруппыРассылокИОповещений.ПредназначенаДляЭлектронныхПисем,
	|	ГруппыРассылокИОповещений.ПредназначенаДляSMS,
	|	ГруппыРассылокИОповещений.ВидКонтактнойИнформацииПартнераДляПисем,
	|	ГруппыРассылокИОповещений.ВидКонтактнойИнформацииПартнераДляSMS,
	|	ВЫБОР
	|		КОГДА ПодпискиНаРассылкиИОповещенияКлиентам.ОтправлятьКонтактномуЛицуОбъектаОповещения
	|				ИЛИ ПодпискиНаРассылкиИОповещенияКлиентам.ОтправлятьПартнеру
	|				ИЛИ ПодпискиНаРассылкиИОповещенияКлиентам.КоличествоКонтактныхЛицАдресатов > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК АдресатыНастроены
	|ИЗ
	|	Справочник.ПодпискиНаРассылкиИОповещенияКлиентам КАК ПодпискиНаРассылкиИОповещенияКлиентам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыРассылокИОповещений КАК ГруппыРассылокИОповещений
	|		ПО ПодпискиНаРассылкиИОповещенияКлиентам.ГруппаРассылокИОповещений = ГруппыРассылокИОповещений.Ссылка
	|ГДЕ
	|	ПодпискиНаРассылкиИОповещенияКлиентам.Ссылка В(&МассивПодписок)
	|	И НЕ ПодпискиНаРассылкиИОповещенияКлиентам.ПодпискаДействует";
	
	Запрос.УстановитьПараметр("МассивПодписок", МассивПодписок);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
	
		ПодпискаОбъект = Выборка.Ссылка.ПолучитьОбъект(); // СправочникОбъект.ПодпискиНаРассылкиИОповещенияКлиентам
		ПодпискаОбъект.ПодпискаДействует =  Истина;
		
		ПодпискаОбъект.Записать();
	
	КонецЦикла;
	
КонецПроцедуры

// Оформляет подписку для массива новых подписок.
//
// Параметры:
//  МассивПодписок  - Массив, - массив структур, содержащих подписчика и группу.
//
Процедура ОформитьПодпискуДляМассиваНовых(МассивПодписок) Экспорт

	ТаблицаПодписок = Новый ТаблицаЗначений;
	ТаблицаПодписок.Колонки.Добавить("Подписчик",Новый ОписаниеТипов("СправочникСсылка.Партнеры"));
	ТаблицаПодписок.Колонки.Добавить("ГруппаРассылокИОповещений",Новый ОписаниеТипов("СправочникСсылка.ГруппыРассылокИОповещений"));
	
	Для Каждого ЭлементМассива Из МассивПодписок Цикл
		НоваяСтрока = ТаблицаПодписок.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементМассива);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаПодписок.Подписчик,
	|	ТаблицаПодписок.ГруппаРассылокИОповещений
	|ПОМЕСТИТЬ НовыеПодписки
	|ИЗ
	|	&ТаблицаПодписок КАК ТаблицаПодписок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(ПодпискиНаРассылкиИОповещенияКлиентам.Ссылка, ЗНАЧЕНИЕ(Справочник.ПодпискиНаРассылкиИОповещенияКлиентам.ПустаяСсылка)) КАК ТекущаяПодписка,
	|	НовыеПодписки.ГруппаРассылокИОповещений,
	|	НовыеПодписки.Подписчик,
	|	ГруппыРассылокИОповещений.ПредназначенаДляЭлектронныхПисем,
	|	ГруппыРассылокИОповещений.ПредназначенаДляSMS,
	|	ГруппыРассылокИОповещений.ВидКонтактнойИнформацииПартнераДляПисем,
	|	ГруппыРассылокИОповещений.ВидКонтактнойИнформацииПартнераДляSMS,
	|	ВЫБОР
	|		КОГДА ПодпискиНаРассылкиИОповещенияКлиентам.Ссылка ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЕСТЬNULL(ПодпискиНаРассылкиИОповещенияКлиентам.ОтправлятьПартнеру, ЛОЖЬ)
	|						ИЛИ ЕСТЬNULL(ПодпискиНаРассылкиИОповещенияКлиентам.ОтправлятьКонтактномуЛицуОбъектаОповещения, ЛОЖЬ)
	|						ИЛИ ЕСТЬNULL(ПодпискиНаРассылкиИОповещенияКлиентам.КоличествоКонтактныхЛицАдресатов, 0) > 0
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ
	|	КОНЕЦ КАК АдресатыНастроены
	|ИЗ
	|	НовыеПодписки КАК НовыеПодписки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПодпискиНаРассылкиИОповещенияКлиентам КАК ПодпискиНаРассылкиИОповещенияКлиентам
	|		ПО (ПодпискиНаРассылкиИОповещенияКлиентам.Владелец = НовыеПодписки.Подписчик)
	|			И (ПодпискиНаРассылкиИОповещенияКлиентам.ГруппаРассылокИОповещений = НовыеПодписки.ГруппаРассылокИОповещений)
	|			И (НЕ ПодпискиНаРассылкиИОповещенияКлиентам.ПометкаУдаления)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыРассылокИОповещений КАК ГруппыРассылокИОповещений
	|		ПО НовыеПодписки.ГруппаРассылокИОповещений = ГруппыРассылокИОповещений.Ссылка
	|			И (НЕ ГруппыРассылокИОповещений.ПометкаУдаления)
	|ГДЕ
	|	НЕ ЕСТЬNULL(ПодпискиНаРассылкиИОповещенияКлиентам.ПодпискаДействует, ЛОЖЬ)";
	
	Запрос.УстановитьПараметр("ТаблицаПодписок", ТаблицаПодписок);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
	
		Если Выборка.ТекущаяПодписка.Пустая() Тогда
			ПодпискаОбъект = Справочники.ПодпискиНаРассылкиИОповещенияКлиентам.СоздатьЭлемент();
			ПодпискаОбъект.Владелец                  = Выборка.Подписчик;
			ПодпискаОбъект.ГруппаРассылокИОповещений = Выборка.ГруппаРассылокИОповещений;
		Иначе
			ПодпискаОбъект = Выборка.ТекущаяПодписка.ПолучитьОбъект();
		КонецЕсли;
		
		ПодпискаОбъект.ПодпискаДействует =  Истина;
		НастроитьАдресатаПоУмолчанию(Выборка, ПодпискаОбъект);
		
		ПодпискаОбъект.Записать();
		
	КонецЦикла;

КонецПроцедуры

// Снимает признак Действует для подписки. 
//
// Параметры:
//  Подписка  - СправочникСсылка.ПодпискиНаРассылкиИОповещенияКлиентам - подписка, действие которой отменяется.
//
Процедура ОтменитьПодписку(Подписка) Экспорт
	
	ПодпискаОбъект = Подписка.ПолучитьОбъект();
	Если ПодпискаОбъект.ПодпискаДействует Тогда
		ПодпискаОбъект.ПодпискаДействует = Ложь;
		ПодпискаОбъект.Записать();
	КонецЕсли;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Владелец)";
	
	Ограничение.ТекстДляВнешнихПользователей =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК ЭтотСписок
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВнешниеПользователи КАК ВнешниеПользователиПартнеры
	|	ПО ВнешниеПользователиПартнеры.ОбъектАвторизации = ЭтотСписок.Владелец
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ВнешниеПользователиПартнеры.Ссылка)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НастроитьАдресатаПоУмолчанию(Выборка, ПодпискаОбъект)
	
	Если НЕ Выборка.АдресатыНастроены Тогда
		ПодпискаОбъект.ОтправлятьПартнеру = Истина;
		Если Выборка.ПредназначенаДляЭлектронныхПисем Тогда
			ПодпискаОбъект.ВидКИПартнераДляПисем = Выборка.ВидКонтактнойИнформацииПартнераДляПисем;
		КонецЕсли;
		Если Выборка.ПредназначенаДляSMS Тогда
			ПодпискаОбъект.ВидКИПартнераДляSMS = Выборка.ВидКонтактнойИнформацииПартнераДляSMS;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
