
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоЭкземпляров = 1;
	НазначениеШаблона     = Перечисления.НазначенияШаблоновЭтикетокИЦенников.ЭтикеткаДляШтрихкодовУпаковок;
	ШаблонЭтикетки        = Справочники.ШаблоныЭтикетокИЦенников.ШаблонПоУмолчанию(НазначениеШаблона);
	
	Если ЭтоАдресВременногоХранилища(Параметры.АдресВХранилище) Тогда
		
		ОбъектыПечати = ПолучитьИзВременногоХранилища(Параметры.АдресВХранилище);
		Для Каждого ОбъектПечати Из ОбъектыПечати Цикл
			СтрокаТаблицы = ШтрихкодыУпаковок.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ОбъектПечати);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Печать(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрКоманды = Новый Массив;
	ПараметрКоманды.Добавить(ПредопределенноеЗначение("Справочник.ШтрихкодыУпаковокТоваров.ПустаяСсылка"));
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Обработка.ПечатьЭтикетокИЦенников",
		"ЭтикеткаШтрихкодыУпаковки",
		ПараметрКоманды,
		ПечатьЭтикетокИСМПКлиентСервер.ВладелецФормыВерхнегоУровня(ЭтотОбъект),
		ПолучитьПараметрыДляШтрихкодовУпаковок());
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьПараметрыДляШтрихкодовУпаковок()
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("ОбъектыПечати",
		ОбщегоНазначения.ТаблицаЗначенийВМассив(ШтрихкодыУпаковок.Выгрузить()));
	ПараметрыПечати.Вставить("ШаблонЭтикетки"        , ШаблонЭтикетки);
	ПараметрыПечати.Вставить("КоличествоЭкземпляров" , КоличествоЭкземпляров);
	ПараметрыПечати.Вставить("СтруктураМакетаШаблона", Неопределено);
	
	Возврат ПараметрыПечати;
	
КонецФункции

#КонецОбласти