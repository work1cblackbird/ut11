///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ТекстОшибки = "";
	Если Не ЭтоГруппа Тогда
		Если Не ЗначениеЗаполнено(ИдентификаторМерчанта) Тогда
			ТекстОшибки = ТекстОшибки
				+ НСтр("ru = 'Поле ИдентификаторМерчанта не заполнено.'")
				+ Символы.ПС;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ИдентификаторУчастника) Тогда
			ТекстОшибки = ТекстОшибки
				+ НСтр("ru = 'Поле ИдентификаторУчастника не заполнено.'")
				+ Символы.ПС;
			КонецЕсли;
		Если Не ЗначениеЗаполнено(Родитель) Тогда
			ТекстОшибки = ТекстОшибки
				+ НСтр("ru = 'Поле Родитель не заполнено.'")
				+ Символы.ПС;
		КонецЕсли;
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			Отказ = Истина;
			СистемаБыстрыхПлатежейСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось записать: Настройка подключения к Системе быстрых платежей
						|%1'"),
					ТекстОшибки));
		КонецЕсли;
	КонецЕсли;
	
	// При добавлении первой настройки подключения с Системой быстрых платежей должно быть
	// создано регламентное задание по загрузке идентификаторов платежных.
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.ПереводыСБПc2b") Тогда
		УстановитьПривилегированныйРежим(Истина);
		МодульПереводыСБПc2bСлужебный = ОбщегоНазначения.ОбщийМодуль("ПереводыСБПc2bСлужебный");
		МодульПереводыСБПc2bСлужебный.ДобавитьЗаданиеЗагрузкаСтатусовОперацийСБПc2b();
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
