///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УстановитьУсловноеОформление();
	
	Результат = ОбновлениеИнформационнойБазы.ОбъектОбработан(
		"Справочник.НастройкиПодключенияКСистемеБыстрыхПлатежей");
	
	Если Не Результат.Обработан Тогда
		ЭтотОбъект.ТолькоПросмотр = Истина;
		Элементы.СозданиеНастройки.Доступность = Ложь;
		Элементы.Список.Доступность = Ложь;
		Элементы.ФормаДобавитьИнтеграциюСБП.Доступность = Ложь;
		ОбщегоНазначения.СообщитьПользователю(
			Результат.ТекстИсключения);
	КонецЕсли;
	
	Элементы.СозданиеНастройки.Видимость = СистемаБыстрыхПлатежей.НастройкаПодключенияДоступна();
	Элементы.СписокКонтекстноеМенюСкопировать.Видимость = Элементы.СозданиеНастройки.Видимость;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.ПереводыСБПc2b") Тогда
		
		НастройкиПодключенияПрограммы = СистемаБыстрыхПлатежейСлужебный.НастройкиПодключенияПрограммы();
		
		Элементы.ФормаОткрытьКассовыеСсылки.Видимость =
			НастройкиПодключенияПрограммы.c2b.ИспользоватьНастройкуКассовыхСсылок;
			
		Если НастройкиПодключенияПрограммы.c2b.ШаблоныНазначений.Количество() = 0 Тогда
			Элементы.ФормаОткрытьШаблоныНазначения.Видимость = Ложь;
		КонецЕсли;
		
	Иначе
		Элементы.ФормаОткрытьКассовыеСсылки.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "БИПСменаУчастникаСБП" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(
		Элемент,
		Отказ,
		Копирование,
		Родитель,
		Группа,
		Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(
		Элемент,
		ВыбраннаяСтрока,
		Поле,
		СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Элемент.ТекущиеДанные <> Неопределено
		И ЗначениеЗаполнено(Элемент.ТекущиеДанные.Родитель)
		И Не Элемент.ТекущиеДанные.ЭтоГруппа Тогда
		ОбработатьВыборНастройки(
			ВыбраннаяСтрока,
			Элемент.ТекущиеДанные.ИдентификаторУчастника)
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Создать(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОбновитьДанныеФормы",
		ЭтотОбъект);
	
	ПараметрыПодключения = СистемаБыстрыхПлатежейКлиент.НовыйПараметрыПодключения(
		Неопределено,
		Неопределено);
	
	СистемаБыстрыхПлатежейКлиент.ПодключитьСистемуБыстрыхПлатежей(
		ПараметрыПодключения,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура Скопировать(Команда)
	
	ТекстПредупреждения = НСтр("ru = 'Копирование доступно только для настроек подключения.'");
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(
			Неопределено,
			ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если Элементы.Список.ТекущиеДанные.ЭтоГруппа Тогда
		ПоказатьПредупреждение(
			Неопределено,
			ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОбновитьДанныеФормы",
		ЭтотОбъект);
	
	ПараметрыПодключения = СистемаБыстрыхПлатежейКлиент.НовыйПараметрыПодключения(
		Неопределено,
		Неопределено);
	
	ПараметрыПодключения.Вставить("НастройкаПодключения", Элементы.Список.ТекущиеДанные.Ссылка);
	ПараметрыПодключения.Вставить("ДополнительныеПараметры", Неопределено);
	ПараметрыПодключения.Вставить(
		"РежимРаботы",
		СистемаБыстрыхПлатежейКлиентСервер.ИдентификаторОперацииСозданиеНастройки());
	
	СистемаБыстрыхПлатежейКлиент.СлужебнаяПодключитьСистемуБыстрыхПлатежей(
		ПараметрыПодключения,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьШаблоныНазначения(Команда)
	
	ТекстПредупреждения = НСтр("ru = 'Работа с шаблонами назначения доступна только для настроек подключения.'");
	
	Если Элементы.Список.ТекущиеДанные = Неопределено
		Или Элементы.Список.ТекущиеДанные.ЭтоГруппа Тогда
		ПоказатьПредупреждение(
			Неопределено,
			ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаПодключения", Элементы.Список.ТекущиеДанные.Ссылка);
	
	ОткрытьФорму(
		"Справочник.НастройкиПодключенияКСистемеБыстрыхПлатежей.Форма.ФормаНастройкиНазначенияПлатежа",
		ПараметрыФормы,
		ЭтотОбъект,
		ЭтотОбъект.УникальныйИдентификатор);
	
КонецПроцедуры 

&НаКлиенте
Процедура ОткрытьКассовыеСсылки(Команда)
	
	ТекстПредупреждения = НСтр("ru = 'Работа с кассовыми ссылками доступна только для настроек подключения.'");
	
	Если Элементы.Список.ТекущиеДанные = Неопределено
		Или Элементы.Список.ТекущиеДанные.ЭтоГруппа Тогда
		ПоказатьПредупреждение(
			Неопределено,
			ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	НастройкаПодключения = Элементы.Список.ТекущиеДанные.Ссылка;
	
	Настройки = НастройкиПодключения(НастройкаПодключения);
	
	Если Не Настройки.НастройкиСБПc2b.КассовыеСсылки Тогда
		ПоказатьПредупреждение(
			Неопределено,
			НСтр("ru = 'Работа с кассовыми QR-кодами не поддерживается данным участником СБП.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыНастройки = Новый Структура;
	ПараметрыНастройки.Вставить("НастройкаПодключения", НастройкаПодключения);
	
	ПереводыСБПc2bКлиент.ПриНастройкеКассовыхСсылок(
		ПараметрыНастройки,
		Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьДанныеФормы(
		Результат,
		ДополнительныеПараметры) Экспорт
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементОформления = Список.УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ОформлениеОтбор = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОформлениеОтбор = ОформлениеОтбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОформлениеОтбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	ОформлениеОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОформлениеОтбор.ПравоеЗначение = Истина;
	
	ЭлементОформления = Список.УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра(
		"ЦветТекста",
		Метаданные.ЭлементыСтиля.ЦветНеАктивнойСтроки.Значение);
	
	ОформлениеОтбор = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОформлениеОтбор = ОформлениеОтбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОформлениеОтбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Используется");
	ОформлениеОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОформлениеОтбор.ПравоеЗначение = Ложь;
	
	
	ЭлементОформления = Список.УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра(
		"ЦветТекста",
		Метаданные.ЭлементыСтиля.ЦветИнформацияОшибочна.Значение);
	
	ОформлениеОтбор = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОформлениеОтбор = ОформлениеОтбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОформлениеОтбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИдентификаторУчастника");
	ОформлениеОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОформлениеОтбор.ПравоеЗначение = СистемаБыстрыхПлатежейКлиентСервер.ИдентификаторНеизвестногоУчастника();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборНастройки(
		НастройкаПодключения,
		ИдентификаторУчастника)
	
	Если ИдентификаторУчастника = СистемаБыстрыхПлатежейКлиентСервер.ИдентификаторНеизвестногоУчастника() Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("НастройкаПодключения", НастройкаПодключения);
		ДополнительныеПараметры.Вставить("ИдентификаторУчастника", ИдентификаторУчастника);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПриОтветеНаВопросВыбораУчастника",
			ЭтотОбъект,
			ДополнительныеПараметры);
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(
			"Выбрать",
			НСтр("ru = 'Выбрать'"));
		Кнопки.Добавить(
			"Отмена",
			НСтр("ru = 'Отмена'"));
		
		ПоказатьВопрос(
			ОписаниеОповещения,
			НСтр("ru = 'Идентификатор настройки не определен, для продолжения необходимо выбрать участника СБП.'"),
			Кнопки);
			
	Иначе
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", НастройкаПодключения);
		ОткрытьФорму(
			"Справочник.НастройкиПодключенияКСистемеБыстрыхПлатежей.ФормаОбъекта",
			ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОтветеНаВопросВыбораУчастника(
		Ответ,
		ДополнительныеПараметры) Экспорт
	
	Если Ответ = "Выбрать" Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПослеВыбораУчастникаСБП",
			ЭтотОбъект,
			ДополнительныеПараметры);
		ОткрытьФорму(
			"РегистрСведений.НастройкиУчастниковСБП.Форма.ВыборУчастникаСБП",
			Неопределено,
			ЭтотОбъект,
			ЭтотОбъект.УникальныйИдентификатор,
			,
			,
			ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораУчастникаСБП(
		Результат,
		ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ОбновитьИдентификаторУчастника(
			ДополнительныеПараметры.НастройкаПодключения,
			Результат);
		Элементы.Список.Обновить();
		ОбработатьВыборНастройки(
			ДополнительныеПараметры.НастройкаПодключения,
			Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбновитьИдентификаторУчастника(
		Знач НастройкаПодключения,
		Знач ИдентификаторУчастника)
	
	Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей.ОбновитьИдентификаторУчастника(
		НастройкаПодключения,
		ИдентификаторУчастника);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НастройкиПодключения(Знач НастройкаПодключения)
	
	Возврат СистемаБыстрыхПлатежей.НастройкиПодключения(НастройкаПодключения);
	
КонецФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(Знач ПараметрыВыполнения)
	
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти