#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	НачатьОбновлениеСтатуса();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПовторитьОбновлениеСтатуса(Команда)
	НачатьОбновлениеСтатуса();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НачатьОбновлениеСтатуса()
	
	УправлениеФормойВыполнение();
	
	КоличествоПопытокОбновленияСтатуса = МаксимальноеКоличествоПопытокОбновленияСтатуса();
	
	ОтложенноеОбновлениеСтатусаИзРеестраФНС();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтложенноеОбновлениеСтатусаИзРеестраФНС()
	ПодключитьОбработчикОжидания("ОбновитьСтатусИзРеестраФНС", 1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтатусИзРеестраФНС()
	
	КоличествоПопытокОбновленияСтатуса = КоличествоПопытокОбновленияСтатуса - 1;
	
	ДлительнаяОперация = НачатьОбновлениеСтатусаИзРеестраФНС(УникальныйИдентификатор);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("ПриЗавершенииОбновленияСтатусаИзРеестраФНС", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);

КонецПроцедуры

&НаСервере
Функция НачатьОбновлениеСтатусаИзРеестраФНС(УникальныйИдентификатор)
	
	ДанныеДоверенности = МашиночитаемыеДоверенностиКлиентСервер.ДанныеДляПроверкиВРеестреФНС();
	ДанныеДоверенности.НомерДоверенности = Объект.НомерДоверенности;
	ДанныеДоверенности.СтатусВРеестреФНС = Объект.СтатусВРеестреФНС;
	ДанныеДоверенности.ИдентификаторТранзакции = Объект.ИдентификаторТранзакции;
	ДанныеДоверенности.ДоверительЮЛ_ИНН = Объект.Доверители[0].ИНН;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		"МашиночитаемыеДоверенности.ОбновитьСтатусМЧДИзРеестраФНС", ДанныеДоверенности);
	
КонецФункции

&НаКлиенте
Процедура ПриЗавершенииОбновленияСтатусаИзРеестраФНС(РезультатОбновленияСтатуса, ДополнительныеПараметры) Экспорт
	
	СтатусОбновлен = Ложь;
	ТребуетсяНовыйЗапросСтатуса = Ложь;
	
	Если РезультатОбновленияСтатуса <> Неопределено И РезультатОбновленияСтатуса.Статус <> "Ошибка" Тогда
		ДетальныйРезультат = ПолучитьИзВременногоХранилища(РезультатОбновленияСтатуса.АдресРезультата);
		УдалитьИзВременногоХранилища(РезультатОбновленияСтатуса.АдресРезультата);

		Если ДетальныйРезультат <> Неопределено И НЕ ДетальныйРезультат.СтраницаНеНайдена Тогда
			Если НЕ ЗначениеЗаполнено(ДетальныйРезультат.СтатусВРеестреФНС) Тогда
				ТребуетсяНовыйЗапросСтатуса = КоличествоПопытокОбновленияСтатуса > 0;
			Иначе
				СтатусОбновлен = Истина;
			КонецЕсли
		КонецЕсли;
	КонецЕсли;
	
	Если ТребуетсяНовыйЗапросСтатуса Тогда
		ОтложенноеОбновлениеСтатусаИзРеестраФНС();
		Возврат;
	КонецЕсли;
	
	Если СтатусОбновлен Тогда
		УправлениеФормойВыполнено();
		ЗаписатьСтатусИзРеестраФНС(ДетальныйРезультат);
	Иначе
		УправлениеФормойОшибка();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьСтатусИзРеестраФНС(РезультатОбновленияСтатуса)
	
	Объект.СтатусВРеестреФНС = РезультатОбновленияСтатуса.СтатусВРеестреФНС;
	Объект.ДатаОбновленияСтатуса = ТекущаяДатаСеанса();
	
	Если РезультатОбновленияСтатуса.СтатусВРеестреФНС =
		ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Отозвано") Тогда
			Объект.ДатаПрекращения = РезультатОбновленияСтатуса.ДатаОтзыва;
	КонецЕсли;
	
	Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормойВыполнение()
	
	Элементы.ГруппаВыполнение.Видимость = Истина;
	Элементы.ГруппаВыполнено.Видимость = Ложь;
	Элементы.ГруппаОшибка.Видимость = Ложь;
	
	УправлениеФормойВидимостьКнопок();
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормойВыполнено()
	
	Элементы.ГруппаВыполнение.Видимость = Ложь;
	Элементы.ГруппаВыполнено.Видимость = Истина;
	Элементы.ГруппаОшибка.Видимость = Ложь;
	
	УправлениеФормойВидимостьКнопок();
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормойОшибка()
	
	Элементы.ГруппаВыполнение.Видимость = Ложь;
	Элементы.ГруппаВыполнено.Видимость = Ложь;
	Элементы.ГруппаОшибка.Видимость = Истина;
	
	УправлениеФормойВидимостьКнопок();
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормойВидимостьКнопок()
	
	Элементы.Отмена.Видимость = Элементы.ГруппаВыполнение.Видимость ИЛИ Элементы.ГруппаОшибка.Видимость;
	Элементы.ОК.Видимость = Элементы.ГруппаВыполнено.Видимость;
	Элементы.Повторить.Видимость = Элементы.ГруппаОшибка.Видимость;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция МаксимальноеКоличествоПопытокОбновленияСтатуса()
	Возврат 3;
КонецФункции

#КонецОбласти
