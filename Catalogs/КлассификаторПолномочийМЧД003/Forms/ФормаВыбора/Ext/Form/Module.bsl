#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.КодКлассификатора) Тогда
		
		Элементы.Список.ТекущаяСтрока = Справочники.КлассификаторПолномочийМЧД003.НайтиПолномочиеПоКоду(
			Параметры.КодКлассификатора);
		
	КонецЕсли;
	
	Элементы.Список.МножественныйВыбор = Параметры.МножественныйВыбор;
	
	ОбновитьИспользованиеПараметровСписка();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Массив = Новый Массив;
	
	Если Не Элементы.Список.МножественныйВыбор Тогда
		Значение = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Значение);
	КонецЕсли;
	
	Для Каждого ВыбранныйЭлемент Из Значение Цикл
		
		ТекущиеДанные = Элементы.Список.ДанныеСтроки(ВыбранныйЭлемент);
		
		Структура = Новый Структура;
		Структура.Вставить("КодКлассификатора", ТекущиеДанные.КодКлассификатора);
		Структура.Вставить("Полномочие",        ТекущиеДанные.Полномочие);
		
		Массив.Добавить(Структура);
		
	КонецЦикла;
	
	Закрыть(Массив);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьИспользованиеПараметровСписка()
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ТекущаяДата", ТекущаяДатаСеанса());
	
КонецПроцедуры

#КонецОбласти