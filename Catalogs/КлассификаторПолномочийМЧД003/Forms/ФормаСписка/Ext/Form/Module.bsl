
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьУсловноеОформление();
	ОбновитьИспользованиеПараметровСписка();
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)

	Элементы.ФормаПоказыватьУстаревшиеПолномочия.Пометка = ОтображатьУстаревшиеПолномочия;
	ОбновитьИспользованиеПараметровСписка();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьУстаревшиеПолномочия(Команда)
	
	ОтображатьУстаревшиеПолномочия = Не ОтображатьУстаревшиеПолномочия;
	Элементы.ФормаПоказыватьУстаревшиеПолномочия.Пометка = ОтображатьУстаревшиеПолномочия;
	ОбновитьИспользованиеПараметровСписка();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьИспользованиеПараметровСписка()
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ТекущаяДата", ТекущаяДатаСеанса());
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ОтображатьУстаревшиеПолномочия",
		ОтображатьУстаревшиеПолномочия, Не ОтображатьУстаревшиеПолномочия);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	ОформлениеИстекшихПолномочий = УсловноеОформление.Элементы.Добавить();
	ПоляСписка = ОформлениеИстекшихПолномочий.Поля.Элементы.Добавить();
	ПоляСписка.Поле = Новый ПолеКомпоновкиДанных(Элементы.Список.Имя);
	ПредставлениеОтбора = НСтр("ru = 'Отбор по дате отзыва'");
	ГруппаОтбора = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ОформлениеИстекшихПолномочий.Отбор.Элементы, ПредставлениеОтбора, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
		"Список.ДатаОтзываПолномочия", ВидСравненияКомпоновкиДанных.Меньше, ТекущаяДатаСеанса());
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
		"Список.ДатаОтзываПолномочия", ВидСравненияКомпоновкиДанных.Заполнено);
	ОформлениеИстекшихПолномочий.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаНеважнаяНадписьБЭД);
	
КонецПроцедуры

#КонецОбласти