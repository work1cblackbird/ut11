#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаОбъекта" Тогда
		ЗначенияЗаполнения = Неопределено;
		Ключ = Неопределено;
		ЗначениеКопирования = Неопределено;
		Если Параметры.Свойство("ЗначенияЗаполнения", ЗначенияЗаполнения) Тогда
			Тип = Неопределено;
			ИмяОбъекта = Неопределено;
			Если ЗначенияЗаполнения.Свойство("ИмяОбъекта", ИмяОбъекта) И ЗначенияЗаполнения.Свойство("Тип", Тип) Тогда
				ОписаниеГруппыДанных = ОбменСГИСЭПДКлиентСервер.ОписаниеГруппыДанных(Тип, ИмяОбъекта);
				Для Каждого КиЗ Из Параметры.ЗначенияЗаполнения Цикл
					Параметры.Вставить(КиЗ.Ключ, КиЗ.Значение);	
				КонецЦикла;
				Если ЗначениеЗаполнено(ОписаниеГруппыДанных.ИмяФормы) Тогда
					ВыбраннаяФорма = ОписаниеГруппыДанных.ИмяФормы;  
					СтандартнаяОбработка = Ложь;
				КонецЕсли;	
			КонецЕсли;
		ИначеЕсли Параметры.Свойство("Ключ", Ключ) Тогда
			ГруппаДанныхОткрытия = Неопределено;
			Если Параметры.Свойство("ГруппаДанных", ГруппаДанныхОткрытия) = Ложь Тогда
				ГруппаДанныхОткрытия = Ключ.ГруппаДанных;
			КонецЕсли;
			ИмяОбъектаОткрытия = Неопределено;
			Если Параметры.Свойство("ИмяОбъекта", ИмяОбъектаОткрытия) = Ложь Тогда
				ИмяОбъектаОткрытия = Ключ.ИмяОбъекта;
			КонецЕсли;
			ОписаниеГруппыДанных = ОбменСГИСЭПДКлиентСервер.ОписаниеГруппыДанных(ГруппаДанныхОткрытия, ИмяОбъектаОткрытия);
			Если ЗначениеЗаполнено(ОписаниеГруппыДанных.ИмяФормы) Тогда
				ВыбраннаяФорма = ОписаниеГруппыДанных.ИмяФормы;  
				СтандартнаяОбработка = Ложь;
			КонецЕсли;
		ИначеЕсли Параметры.Свойство("ЗначениеКопирования", ЗначениеКопирования) Тогда
			ИмяОбъектаОткрытия = Неопределено;
			Если Параметры.Свойство("ИмяОбъекта", ИмяОбъектаОткрытия) = Ложь Тогда
				ИмяОбъектаОткрытия = ЗначениеКопирования.ИмяОбъекта;
			КонецЕсли;
			ОписаниеГруппыДанных = ОбменСГИСЭПДКлиентСервер.ОписаниеГруппыДанных(ЗначениеКопирования.Тип, ИмяОбъектаОткрытия);
			Если ЗначениеЗаполнено(ОписаниеГруппыДанных.ИмяФормы) Тогда
				ВыбраннаяФорма = ОписаниеГруппыДанных.ИмяФормы;  
				СтандартнаяОбработка = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Обработка обновления. Выполняет очистку префиксов титулов и частично групп данных 
// для возможности использования одного элемента хранимых данных в разных типах документов ЭПД
Процедура СлияниеЭлементовВРазрезеТиповДокументовЭПД() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ХранимыеДанныеЭПДДанные.Ссылка КАК Ссылка,
	|	ХранимыеДанныеЭПДДанные.НомерСтроки КАК НомерСтроки,
	|	ХранимыеДанныеЭПДДанные.Имя КАК Имя,
	|	ХранимыеДанныеЭПДДанные.Представление КАК Представление,
	|	ХранимыеДанныеЭПДДанные.ИмяТабличнойЧасти КАК ИмяТабличнойЧасти,
	|	ХранимыеДанныеЭПДДанные.НомерСтрокиТЧ КАК НомерСтрокиТЧ,
	|	ХранимыеДанныеЭПДДанные.Значение КАК Значение,
	|	ХранимыеДанныеЭПД.ИмяОбъекта КАК ИмяОбъекта,
	|	ХранимыеДанныеЭПД.Тип КАК Тип
	|ИЗ
	|	Справочник.ХранимыеДанныеЭПД.Данные КАК ХранимыеДанныеЭПДДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХранимыеДанныеЭПД КАК ХранимыеДанныеЭПД
	|		ПО ХранимыеДанныеЭПДДанные.Ссылка = ХранимыеДанныеЭПД.Ссылка
	|ГДЕ
	|	(ПОДСТРОКА(ХранимыеДанныеЭПДДанные.ИмяТабличнойЧасти, 1, 5) = ""Титул""
	|	ИЛИ ПОДСТРОКА(ХранимыеДанныеЭПДДанные.Имя, 1, 5) = ""Титул""
	|	ИЛИ ХранимыеДанныеЭПДДанные.ИмяТабличнойЧасти ПОДОБНО ХранимыеДанныеЭПД.Тип + ""%""
	|	ИЛИ ХранимыеДанныеЭПДДанные.Имя ПОДОБНО ХранимыеДанныеЭПД.Тип + ""%"")
	|	И НЕ ХранимыеДанныеЭПДДанные.ИмяТабличнойЧасти ПОДОБНО ""%ИнформационныеПоля""
	|	И НЕ ХранимыеДанныеЭПДДанные.ИмяТабличнойЧасти ПОДОБНО ""%ДополнительнаяИнформация""
	|	И НЕ ХранимыеДанныеЭПДДанные.ИмяТабличнойЧасти ПОДОБНО ""%НомераТелефонов""
	|	И НЕ ХранимыеДанныеЭПДДанные.ИмяТабличнойЧасти ПОДОБНО ""%КонтактыВодителя""
	|	И НЕ ХранимыеДанныеЭПДДанные.ИмяТабличнойЧасти ПОДОБНО ""%АдресаЭлектроннойПочты""
	|ИТОГИ
	|ПО
	|	Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаСсылка.Следующий() Цикл         
		СправочникОбъект = ВыборкаСсылка.Ссылка.ПолучитьОбъект();   
		ПрефиксыТитулов = ОбменСГИСЭПДКлиентСервер.ПрефиксыТитуловДокумента(СправочникОбъект.ИмяОбъекта);
		Для Каждого Префикс Из ПрефиксыТитулов Цикл
			Если СтрНачинаетсяС(СправочникОбъект.Тип, Префикс) Тогда	
				СправочникОбъект.Тип = СтрЗаменить(СправочникОбъект.Тип, Префикс, "");	
			КонецЕсли;
		КонецЦикла;
		Для Каждого СтрокаТЧ Из СправочникОбъект.Данные Цикл 
			Для Каждого Префикс Из ПрефиксыТитулов Цикл
				Если СтрНачинаетсяС(СтрокаТЧ.ИмяТабличнойЧасти, Префикс) Тогда	
					СтрокаТЧ.ИмяТабличнойЧасти = СтрЗаменить(СтрокаТЧ.ИмяТабличнойЧасти, Префикс, "");	
				КонецЕсли;
				Если СтрНачинаетсяС(СтрокаТЧ.Имя, Префикс) Тогда	
					СтрокаТЧ.Имя = СтрЗаменить(СтрокаТЧ.Имя, Префикс, "");	
				КонецЕсли;
			КонецЦикла;
			Если СтрНачинаетсяС(СтрокаТЧ.ИмяТабличнойЧасти, СправочникОбъект.Тип) Тогда
				СтрокаТЧ.ИмяТабличнойЧасти = СтрЗаменить(СтрокаТЧ.ИмяТабличнойЧасти, СправочникОбъект.Тип, "");	
			КонецЕсли;
			Если СтрНачинаетсяС(СтрокаТЧ.Имя, СправочникОбъект.Тип) Тогда
				СтрокаТЧ.Имя = СтрЗаменить(СтрокаТЧ.Имя, СправочникОбъект.Тип, "");	
			КонецЕсли;
		КонецЦикла;
		СправочникОбъект.Записать();
	КонецЦикла;                                  
	
КонецПроцедуры

#КонецЕсли
