
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, Параметры.СтрокаПараметров);
	
	СписокВыбора = Элементы.ВариантОтображения.СписокВыбора;
	
	СписокВыбора.Очистить();
	
	СписокВыбора.Добавить(ВариантОтображенияКратко);
	СписокВыбора.Добавить(ВариантОтображенияДиаграмма);
	
	Если ТипАнализа = ТипАнализаПокомпонентноеСравнение Тогда
		СписокВыбора.Добавить(ВариантОтображенияТаблица);
	КонецЕсли;
	
	УстановитьВидимость(ЭтаФорма);
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантАнализаПриИзменении(Элемент)
	ВариантАнализаПриИзмененииСервер();
КонецПроцедуры

&НаКлиенте
Процедура ВариантОтображенияПриИзменении(Элемент)
	Детали = Ложь;
	ИтогТаблицы = Ложь;
	УстановитьВидимость(ЭтаФорма);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
 
&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	СтруктураДанныхСтроки = Новый Структура("ВариантАнализа,ВариантОтображения,Детали,ИтогТаблицы,ТипАнализа");
	ЗаполнитьЗначенияСвойств(СтруктураДанныхСтроки, ЭтаФорма);
	Закрыть(СтруктураДанныхСтроки);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура ВариантАнализаПриИзмененииСервер()
	
	ТипАнализа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВариантАнализа, "ТипАнализа");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимость(Форма)
	
	Форма.Элементы.Детали.Видимость = (Форма.ВариантОтображения = Форма.ВариантОтображенияКратко);
	Форма.Элементы.ИтогТаблицы.Видимость = (Форма.ВариантОтображения = Форма.ВариантОтображенияТаблица);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти