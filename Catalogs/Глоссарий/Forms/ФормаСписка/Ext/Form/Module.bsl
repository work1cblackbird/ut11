
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЕстьПравоРедактирования = ПравоДоступа("Изменение", Метаданные.Справочники.Глоссарий);
	
	Элементы.ЗагрузитьИзТаблицы.Видимость             = ЕстьПравоРедактирования;
	Элементы.СписокКонтекстноеМенюЗагрузить.Видимость = ЕстьПравоРедактирования;
	
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект, ,"Список");
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	ЯзыкОписаний = ТекущийЯзык().КодЯзыка; 
	Если Не ЗначениеЗаполнено(ЯзыкОписаний) Тогда
		Возврат;
	КонецЕсли;
	
	СоответствиеСтрокКлючам               = Новый Соответствие;
	ЭлементыГлоссарияДляПолученияОписания = Новый Массив;
	
	Для Каждого СтрокаСписка Из Строки Цикл
		
		СоответствиеСтрокКлючам.Вставить(СтрокаСписка.Ключ, СтрокаСписка.Значение);
		
		ЭлементыГлоссарияДляПолученияОписания.Добавить(СтрокаСписка.Ключ);
		
	КонецЦикла;
	
	Если СоответствиеСтрокКлючам.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ГлоссарийОписанияГлоссария.Ссылка КАК Ссылка,
	|	ГлоссарийОписанияГлоссария.Описание КАК Описание
	|ИЗ
	|	Справочник.Глоссарий.ОписанияГлоссария КАК ГлоссарийОписанияГлоссария
	|ГДЕ
	|	ГлоссарийОписанияГлоссария.Ссылка В(&Глоссарии)
	|	И ГлоссарийОписанияГлоссария.КодЯзыка = &КодЯзыка";
	
	Запрос.УстановитьПараметр("Глоссарии", ЭлементыГлоссарияДляПолученияОписания);
	Запрос.УстановитьПараметр("КодЯзыка",  ТекущийЯзык().КодЯзыка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеРеквизита = Выборка.Описание.Получить();
		
		Если ТипЗнч(ДанныеРеквизита) = Тип("ФорматированныйДокумент") Тогда
			ОписаниеСтрокиСписка = ДанныеРеквизита.ПолучитьТекст();
			МассивНеСтрокОписания = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ОписаниеСтрокиСписка, Символы.ПС);
			МассивНеПустыхСтрок = Новый Массив;
			Для Каждого СтрокаОписания Из МассивНеСтрокОписания Цикл
				Если ПустаяСтрока(СтрокаОписания) Тогда
					Продолжить;
				КонецЕсли;
				МассивНеПустыхСтрок.Добавить(СтрокаОписания);
			КонецЦикла;
			ОписаниеСтрокиСписка = СтрСоединить(МассивНеПустыхСтрок);
			СтрокаСписка =  СоответствиеСтрокКлючам.Получить(Выборка.Ссылка); //СтрокаДинамическогоСписка
			Если СтрокаСписка <> Неопределено Тогда
				СтрокаСписка.Данные.ОписаниеГлоссария = ОписаниеСтрокиСписка; // 
			КонецЕсли;
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	
	ОткрытьФорму("Справочник.Глоссарий.Форма.ФормаЗагрузкиГлоссария",, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеревестиВыделенныеЭлементы(Команда)
	
	МассивВыбранных = Элементы.Список.ВыделенныеСтроки;
	
	Если МассивВыбранных.Количество()>0 Тогда
		Парам  = Новый Структура;
		Парам.Вставить("ВыделенныеЭлементы", МассивВыбранных);
		ОткрытьФорму("Справочник.ДемонстрационныеСценарии.Форма.ПереводНесколькихСценариев",Парам);
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Необходимо выбрать хотя бы один элемент или группу'"), 20);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


