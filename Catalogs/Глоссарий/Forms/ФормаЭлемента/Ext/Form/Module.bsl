

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		
		СкопироватьЗначения(Параметры.ЗначениеКопирования); 
		ПриСозданииЧтенииНаСервере();
		
	КонецЕсли;
	
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОписанияГлоссариев.Очистить();
	
	Для Каждого ТекСтрока Из ТекущийОбъект.ОписанияГлоссария Цикл
		НоваяСтрока = ОписанияГлоссариев.Добавить();
		НоваяСтрока.Описание = ТекСтрока.Описание.Получить();
		НоваяСтрока.КодЯзыка = ТекСтрока.КодЯзыка;
	КонецЦикла;
	
	ПриСозданииЧтенииНаСервере();
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПеренестиРезультатРедактированияВТаблицуОписаний(ЭтотОбъект);
	
	ТекущийОбъект.ОписанияГлоссария.Очистить();
	Для каждого ТекСтрока Из ОписанияГлоссариев Цикл
		НоваяСтрока = ТекущийОбъект.ОписанияГлоссария.Добавить();
		НоваяСтрока.Описание = Новый ХранилищеЗначения(ТекСтрока.Описание);
		НоваяСтрока.КодЯзыка = ТекСтрока.КодЯзыка;
	КонецЦикла; 
	
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьСервер.ПередЗаписьюНаСервере(ТекущийОбъект);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ЗначениеЗаполнено(ВариантОтображенияФормы) Тогда
		
		ПереключитьВариантОтображения( "Просмотр");
		НастроитьОтображениеИнформацииРежимПросмотра();
		
		УправлениеВидимостью(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Подключаемый_Открытие(Элемент, СтандартнаяОбработка)
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьКлиент.ПриОткрытии(ЭтотОбъект, Объект, Элемент, СтандартнаяОбработка);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеревестиСЯзыкаПросмотра(Команда) 
	
	ОчиститьСообщения();
	
	ДанныеHTMLРедактирование = ДемонстрационныеСценарииКлиентСервер.ТекстHTMLИзФорматированногоДокумента(ТекстОписанияРедактирование);
	ДанныеHTMLЧтение = ДемонстрационныеСценарииКлиентСервер.ТекстHTMLИзФорматированногоДокумента(ТекстОписанияЧтение);
	
	ДемонстрационныеСценарииКлиент.ПеревестиСЯзыкаПросмотра(
		ЭтотОбъект, ДанныеHTMLЧтение.HTML, ДанныеHTMLРедактирование.HTML, ЯзыкГлоссарияЧтение, ЯзыкГлоссария, "ТекстСценария", ДанныеHTMLЧтение.Вложения);

КонецПроцедуры

#Область ИзменениеРежимаОтображения

&НаКлиенте
Процедура УстановитьВариантПросмотр(Команда)
	
	ПереключитьВариантОтображения( "Просмотр");
	НастроитьОтображениеИнформацииРежимПросмотра();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВариантРедактирование(Команда)
	
	ПереключитьВариантОтображения( "Редактирование");
	
КонецПроцедуры 

&НаКлиенте
Процедура УстановитьВариантПеревод(Команда)
	
	ПереключитьВариантОтображения( "Перевод");
	НастроитьОтображениеИнформацииРежимПросмотра();
	
КонецПроцедуры 

#КонецОбласти

#Область ИзменениеЯзыка

&НаКлиенте
Процедура Подключаемый_ИзменитьЯзыкРедактирования(Команда)

	ИмяКоманды = Команда.Имя;
	
	КодЯзыка = ДемонстрационныеСценарииКлиентСервер.КодЯзыкаИзИмениКоманды(ИмяКоманды, "ПереключитьНаЯзыкРедактирования_");
	
	ПриИзмененииЯзыкаГлоссария(КодЯзыка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ИзменитьЯзыкПросмотра(Команда)

	ИмяКоманды = Команда.Имя;
	
	КодЯзыка = ДемонстрационныеСценарииКлиентСервер.КодЯзыкаИзИмениКоманды(ИмяКоманды, "ПереключитьНаЯзыкПросмотра_");
	
	ПриИзмененииЯзыкаГлоссарияЧтение(КодЯзыка);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьОписаниеНаЯзыке(КодЯзыка)
	
	СтруктураПоиска = Новый Структура("КодЯзыка", КодЯзыка);
	
	НайденныеСтроки = ОписанияГлоссариев.НайтиСтроки(СтруктураПоиска);
	Если НайденныеСтроки.Количество() > 0 Тогда
		ДанныеХранилища = НайденныеСтроки[0].Описание;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеХранилища) = Тип("ФорматированныйДокумент") Тогда
		ТекстПолученный = ДанныеХранилища;
	Иначе
		ТекстПолученный = Новый ФорматированныйДокумент;
	КонецЕсли;
	
	Возврат ТекстПолученный;
	
КонецФункции

&НаСервере
Процедура ПриСозданииЧтенииНаСервере() 
	
	ДемонстрационныеСценарии.СоздатьЭлементыФормыЯзыкиКонфигурации(ЭтотОбъект);
	ДемонстрационныеСценарии.УстановитьЯзыкиКонфигурацииВФорме(ЭтотОбъект);
	
	ДемонстрационныеСценарии.УстановитьНастройкуПоЯзыкамВФорме(ЭтотОбъект, "ЯзыкГлоссарияЧтение", "ЯзыкГлоссария");
	
	ДемонстрационныеСценарииКлиентСервер.ОтразитьТекущиеЯзыкиНаФорме(ЭтотОбъект, "ЯзыкГлоссарияЧтение", "ЯзыкГлоссария");
	ДемонстрационныеСценарииКлиентСервер.СформироватьЗаголовокКомандыПеревода(ЯзыкГлоссарияЧтение, ЯзыкГлоссария, Элементы.Перевести);
	
	ДемонстрационныеСценарии.ЗагрузитьНастройкуПоВариантуОтображенияФормы(
		ЭтотОбъект,
		ДемонстрационныеСценарии.ДоступныеВариантыОтображения(Истина),
		Метаданные.Справочники.Глоссарий);
	
	УправлениеВидимостью(ЭтотОбъект);
	ТекстОписанияРедактирование = ПолучитьОписаниеНаЯзыке(ЯзыкГлоссария);
	ТекстОписанияЧтение         = ПолучитьОписаниеНаЯзыке(ЯзыкГлоссарияЧтение);
	НастроитьОтображениеИнформацииРежимПросмотра();

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеВидимостью(Форма)
	
	Элементы = Форма.Элементы;
	
	ЭтоРежимРедактирования = (Форма.ВариантОтображенияФормы = "Редактирование");
	ЭтоРежимПеревод        = (Форма.ВариантОтображенияФормы = "Перевод");
	ЭтоРежимПросмотр       = (Форма.ВариантОтображенияФормы = "Просмотр");
	
	Элементы.ГруппаДополнительно.Видимость       = ЭтоРежимРедактирования Или ЭтоРежимПеревод; 
	Элементы.ГруппаРедактирование.Видимость      = ЭтоРежимРедактирования Или ЭтоРежимПеревод;
	
	Элементы.Перевести.Видимость = ЭтоРежимПеревод; 
	
	Элементы.ПодменюЯзыкПросмотра.Видимость = ЭтоРежимПросмотр Или ЭтоРежимПеревод;
	Элементы.ГруппаЧтение.Видимость         = ЭтоРежимПросмотр Или ЭтоРежимПеревод;
	
	ДемонстрационныеСценарииКлиентСервер.УстановитьПометкуКомандПереключенияВариантаОтображенияФормы(Форма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьВариантОтображения( НовыйВариант)
	
	ПараметрыПереключения = ДемонстрационныеСценарииКлиентСервер.НовыйПараметрыПереключенияВариантаОтображения();
	ПараметрыПереключения.Форма                          = ЭтотОбъект;
	ПараметрыПереключения.НовыйВариант                   = НовыйВариант;
	ПараметрыПереключения.ИмяРеквизитаЯзыкЧтение         = "ЯзыкГлоссарияЧтение";
	ПараметрыПереключения.ИмяРеквизитаЯзыкРедактирование = "ЯзыкГлоссария";
	ПараметрыПереключения.ЯзыкиКонфигурации              = ЯзыкиКонфигурации;
	
	ДемонстрационныеСценарииКлиентСервер.ПереключитьВариантОтображения(ПараметрыПереключения);
	
	Если ПараметрыПереключения.ВыполненоПереключениеЯзыка Тогда
		
		Если ПараметрыПереключения.ИмяРеквизитаПереключенЯзык = "ЯзыкГлоссарияЧтение" Тогда
			ПриИзмененииЯзыкаГлоссарияЧтение( ПараметрыПереключения.ПереключеноНаЯзык);
		ИначеЕсли ПараметрыПереключения.ИмяРеквизитаПереключенЯзык = "ЯзыкСценария" Тогда
			ПриИзмененииЯзыкаГлоссария(ПараметрыПереключения.ПереключеноНаЯзык);
		КонецЕсли;
		
	КонецЕсли;
	
	УправлениеВидимостью(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииЯзыкаГлоссария(КодЯзыка)

	Если Модифицированность Тогда
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("КодЯзыка", КодЯзыка);
		ОписаниеОповещения = Новый ОписаниеОповещения("ЯзыкГлоссарияПриИзмененииЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, НСтр("ru = 'Текст глоссария не сохранен. Записать?'"), РежимДиалогаВопрос.ОКОтмена);
	Иначе
		ИзменитьЯзыкГлоссария(КодЯзыка);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ПриИзмененииЯзыкаГлоссарияЧтение(КодЯзыка)

	ЯзыкГлоссарияЧтение = КодЯзыка;
	ТекстОписанияЧтение = ПолучитьОписаниеНаЯзыке(ЯзыкГлоссарияЧтение);
	ДемонстрационныеСценарииКлиентСервер.ОтразитьТекущиеЯзыкиНаФорме(ЭтотОбъект , "ЯзыкГлоссарияЧтение", "ЯзыкГлоссария");
	ДемонстрационныеСценарииКлиентСервер.СформироватьЗаголовокКомандыПеревода(ЯзыкГлоссарияЧтение, ЯзыкГлоссария, Элементы.Перевести);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОтображениеИнформацииРежимПросмотра()

	ПредставлениеОбъектовМетаданных = ПредставлениеОбъектовМетаданных();
	
	ОбъектыКонфигурацииНаименования = ПредставлениеОбъектовМетаданных.ПредставленияМетаданных;
	Элементы.ГруппаПредставлениеОбъектыКонфигурации.Заголовок = ПредставлениеОбъектовМетаданных.Представление;
	
	ПеренестиРезультатРедактированияВТаблицуОписаний(ЭтотОбъект);
	ТекстОписанияЧтение = ПолучитьОписаниеНаЯзыке(ЯзыкГлоссарияЧтение);
	
КонецПроцедуры

&НаСервере
Функция ПредставлениеОбъектовМетаданных()
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Представление",            "");
	СтруктураВозврата.Вставить("Количество",               0);
	СтруктураВозврата.Вставить("ПредставленияМетаданных", Новый СписокЗначений);

	УстановитьПривилегированныйРежим(Истина);
	
	Если Объект.ОбъектыКонфигурации.Количество() = 0 Тогда
		СтруктураВозврата.Представление = НСтр("ru = 'Использование в объектах конфигурации не указано'");
		Возврат СтруктураВозврата;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ИдентификаторыОбъектовМетаданных.Ссылка       КАК Ссылка,
	|	ИдентификаторыОбъектовМетаданных.Наименование КАК Наименование
	|ИЗ
	|	Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
	|ГДЕ
	|	ИдентификаторыОбъектовМетаданных.Ссылка В(&ОбъектыМетаданных)";
	
	Запрос.УстановитьПараметр("ОбъектыМетаданных", Объект.ОбъектыКонфигурации.Выгрузить().ВыгрузитьКолонку("ОбъектКонфигурации"));
	
	Выборка = Запрос.Выполнить().Выбрать(); 
	Пока Выборка.Следующий() Цикл
		 СтруктураВозврата.ПредставленияМетаданных.Добавить(Выборка.Ссылка, Выборка.Наименование);
	КонецЦикла;
	
	СтруктураВозврата.Количество = СтруктураВозврата.ПредставленияМетаданных.Количество();
	СтруктураВозврата.Представление = ДемонстрационныеСценарии.СтрокаПредставлениеСЕще(НСтр("ru = 'Объекты конфигурации:'"),
	                                                                                   СтруктураВозврата.ПредставленияМетаданных, 80);
	
	Возврат СтруктураВозврата;
	
КонецФункции

&НаКлиенте
Процедура ЯзыкГлоссарияПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		
		Успешно = Записать();
		
		Если Успешно Тогда
			
			ИзменитьЯзыкГлоссария(ДополнительныеПараметры.КодЯзыка);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
Процедура ПеренестиРезультатРедактированияВТаблицуОписаний(Форма)
	
	СтруктураПоиска = Новый Структура("КодЯзыка", Форма.ЯзыкГлоссария); 
	
	НайденныеСтроки = Форма.ОписанияГлоссариев.НайтиСтроки(СтруктураПоиска);
	Если НайденныеСтроки.Количество() > 0 Тогда
		СтрокаГлоссарий = НайденныеСтроки[0];
	Иначе
		СтрокаГлоссарий = Форма.ОписанияГлоссариев.Добавить();
		СтрокаГлоссарий.КодЯзыка = Форма.ЯзыкГлоссария;
	КонецЕсли;
	
	СтрокаГлоссарий.Описание = Форма.ТекстОписанияРедактирование;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЯзыкГлоссария(КодЯзыка)

	ЯзыкГлоссария  = КодЯзыка;
	ТекстОписанияРедактирование = ПолучитьОписаниеНаЯзыке(ЯзыкГлоссария);
	ДемонстрационныеСценарииКлиентСервер.ОтразитьТекущиеЯзыкиНаФорме(ЭтотОбъект, "ЯзыкГлоссарияЧтение", "ЯзыкГлоссария");
	ДемонстрационныеСценарииКлиентСервер.СформироватьЗаголовокКомандыПеревода(ЯзыкГлоссарияЧтение, ЯзыкГлоссария, Элементы.Перевести);

КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопросПослеПеревода(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ТекстОписанияРедактирование = ДемонстрационныеСценарииКлиентСервер.ФорматированныйДокументИзДанныхHTML(
		ДополнительныеПараметры.ПереведенныйТекст, ДополнительныеПараметры.Вложения);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура СкопироватьЗначения(ЗначениеКопирования)
	
	Если ЗначениеЗаполнено(ЗначениеКопирования) И ЗначениеЗаполнено(ЗначениеКопирования.ОписанияГлоссария) Тогда
		Объект.ОписанияГлоссария.Очистить();
		ОписанияГлоссариев.Очистить();
		Для каждого СтрокаГлоссарий Из ЗначениеКопирования.ОписанияГлоссария Цикл
			НоваяСтрока = ОписанияГлоссариев.Добавить();
			ТекстГлоссария = СтрокаГлоссарий.Описание.Получить();
			
			НоваяСтрока.Описание = ТекстГлоссария;
			НоваяСтрока.КодЯзыка = СтрокаГлоссарий.КодЯзыка;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти