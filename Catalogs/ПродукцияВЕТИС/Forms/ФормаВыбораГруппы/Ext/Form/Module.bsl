
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	ВыборВидаПродукции = Параметры.ВыборВидаПродукции;
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_ПродукцияВЕТИС" Тогда
		Элементы.Список.Обновить();
		Элементы.Список.ТекущаяСтрока = Параметр;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьКлассификаторИерархииПродукцияВЕТИС(Команда)
	
	ОткрытьФорму("Обработка.КлассификаторыВЕТИС.Форма.КлассификаторИерархииПродукции");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПродукцию(Команда)
	
	ОбработатьВыборПродукции();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборПродукции();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВыборПродукции()
	
	ОчиститьСообщения();
	
	Если Не ИнтеграцияИСКлиент.ВыборСтрокиСпискаКорректен(Элементы.Список, Истина, Ложь) Тогда
		ПоказатьПредупреждение(, ОбщегоНазначенияИСКлиентСервер.ТекстКомандаНеМожетБытьВыполнена());
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если НЕ ВыборВидаПродукции Тогда
		ОповеститьОВыборе(ТекущиеДанные.Ссылка);
	ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.Продукция) Тогда
		Результат = Новый Структура("ТипПродукции, Продукция, ВидПродукции",
			ТекущиеДанные.ТипПродукции,
			ТекущиеДанные.Продукция,
			ТекущиеДанные.Ссылка);
		ОповеститьОВыборе(Результат);
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'В текущей строке не заполнена продукция'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
