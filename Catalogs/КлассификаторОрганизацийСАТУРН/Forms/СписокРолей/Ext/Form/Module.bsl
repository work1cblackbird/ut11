#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокРолейПоОрганизации(Параметры.Организация);
	
	ШаблонЗаголовка = НСтр("ru = 'Роли %1'");
	Заголовок       = СтрШаблон(ШаблонЗаголовка, Строка(Параметры.Организация));
	
	СтандартныеПодсистемыСервер.СброситьРазмерыИПоложениеОкна(ЭтотОбъект);
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокРолейПоОрганизации(Организация)
	
	СписокРолей.Очистить();
	МассивВыбранныхРолей = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КлассификаторОрганизацийСАТУРНРоли.Роль
		|ИЗ
		|	Справочник.КлассификаторОрганизацийСАТУРН.Роли КАК КлассификаторОрганизацийСАТУРНРоли
		|ГДЕ
		|	КлассификаторОрганизацийСАТУРНРоли.Ссылка = &Организация";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		МассивВыбранныхРолей.Добавить(ВыборкаДетальныеЗаписи.Роль);

	КонецЦикла;
	
	Для Каждого ДоступноеЗначение Из Метаданные.Перечисления.РолиКонтрагентовСАТУРН.ЗначенияПеречисления Цикл
		
		ЗначениеРоли = Перечисления.РолиКонтрагентовСАТУРН[ДоступноеЗначение.Имя];
		Выбрана = (МассивВыбранныхРолей.Найти(ЗначениеРоли) <> Неопределено);
		
		СписокРолей.Добавить(ЗначениеРоли, ДоступноеЗначение.Представление(), Выбрана);
		
	КонецЦикла;
	
	СписокРолей.СортироватьПоПредставлению();
	Элементы.СписокРолей.ВысотаВСтрокахТаблицы = СписокРолей.Количество();
	
КонецПроцедуры

#КонецОбласти