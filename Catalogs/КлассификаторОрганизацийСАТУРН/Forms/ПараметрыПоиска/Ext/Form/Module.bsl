#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ПараметрыПоиска) Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ПараметрыПоиска);
		
	Иначе
		
		Тип       = Перечисления.ТипыОрганизацийСАТУРН.ЮридическоеЛицо;
		
	КонецЕсли;
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	ИнициализироватьПоляКонтактнойИнформации();
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	Если Не ЗначениеЗаполнено(ВидАдреса) И Элементы.ВидАдреса.СписокВыбора.Количество() Тогда
		
		ВидАдреса = Элементы.ВидАдреса.СписокВыбора[0].Значение;
		
	КонецЕсли;
	
	НастроитьЭлементыФормы();
	СтандартныеПодсистемыСервер.СброситьРазмерыИПоложениеОкна(ЭтотОбъект);
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеДоступностьюЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипПриИзменении(Элемент)
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ИННПриИзменении(Элемент)
	
	УправлениеДоступностьюЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ИННОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Текст) И ЗначениеЗаполнено(КПП) Тогда
		КПП = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КПППриИзменении(Элемент)
	
	УправлениеДоступностьюЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОГРНПриИзменении(Элемент)
	
	УправлениеДоступностьюЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторПриИзменении(Элемент)
	
	УправлениеДоступностьюЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура GUIDПриИзменении(Элемент)
	
	УправлениеДоступностьюЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолноеНаименованиеПриИзменении(Элемент)
	
	УправлениеДоступностьюЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ФИОПриИзменении(Элемент)
	
	УправлениеДоступностьюЭлементовФормы();
	
КонецПроцедуры

#Область РедактированиеАдреса

&НаКлиенте
Процедура ПредставлениеАдресаПриИзменении(Элемент)
	
	Текст = Элемент.ТекстРедактирования;
	Если ПустаяСтрока(Текст) Тогда
		// Очистка данных, сбрасываем как представления, так и внутренние значения полей.
		Адрес = "";
		Возврат;
	КонецЕсли;
	
	АдресПредставление = Текст;
	// Формируем внутренние значения полей по тексту и параметрам формирования из
	// реквизита ВидКонтактнойИнформацииАдресаДоставки.
	ТекстСообщенияОбОшибке = ПредставлениеАдресаПриИзмененииНаСервере(Текст);
	
	Если ЗначениеЗаполнено(ТекстСообщенияОбОшибке) Тогда
		ОчиститьСообщения();
		ИмяПоля = "ПредставлениеАдреса";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщенияОбОшибке,, ИмяПоля);
	КонецЕсли;
	
	УправлениеДоступностьюЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеАдресаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	// Если представление было изменено в поле и сразу нажата кнопка выбора, то необходимо 
	// привести данные в соответствие и сбросить внутренние поля для повторного разбора.
	Если Элемент.ТекстРедактирования <> АдресПредставление Тогда
		АдресПредставление = Элемент.ТекстРедактирования;
		Адрес              = "";
	КонецЕсли;
	
	// Данные для редактирования
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ВидКонтактнойИнформации", ВидКонтактнойИнформацииАдреса);
	ПараметрыОткрытия.Вставить("ЗначенияПолей",           Адрес);
	ПараметрыОткрытия.Вставить("Представление",           АдресПредставление);
	
	// Переопределямый заголовок формы, по умолчанию отобразятся данные по ВидКонтактнойИнформации.
	ПараметрыОткрытия.Вставить("Заголовок", НСтр("ru='Адрес организации'"));
	
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеАдресаОчистка(Элемент, СтандартнаяОбработка)
	
	// Сбрасываем как представления, так и внутренние значения полей.
	АдресПредставление = "";
	Адрес              = "";
	ДанныеАдреса = ДанныеАдресаКлиент();
	
	УправлениеДоступностьюЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеАдресаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ВыбранноеЗначение)<>Тип("Структура") Тогда
		// Отказ от выбора, данные неизменны.
		Возврат;
	КонецЕсли;
	
	АдресПредставление = ВыбранноеЗначение.Представление;
	Адрес              = ВыбранноеЗначение.КонтактнаяИнформация;
	ДанныеАдреса       = ДанныеАдресаКлиент();
	
	УправлениеДоступностьюЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьПоиск(Команда)
	
	ОчиститьСообщения();
	
	Если Не РеквизитыПоискаКорректны() Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ФизическоеЛицо                = ПредопределенноеЗначение("Перечисление.ТипыОрганизацийСАТУРН.ФизическоеЛицо");
	ИндивидуальныйПредприниматель = ПредопределенноеЗначение("Перечисление.ТипыОрганизацийСАТУРН.ИндивидуальныйПредприниматель");
	ЮридическоеЛицо               = ПредопределенноеЗначение("Перечисление.ТипыОрганизацийСАТУРН.ЮридическоеЛицо");
	
	ПараметрыПоиска = Новый Структура;
	
	Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(GUID) Тогда
		ПараметрыПоиска.Вставить("GUID", GUID);
		Закрыть(ПараметрыПоиска);
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Идентификатор) Тогда
		ПараметрыПоиска.Вставить("Идентификатор", Идентификатор);
		Закрыть(ПараметрыПоиска);
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИНН) Тогда
		ПараметрыПоиска.Вставить("ИНН", ИНН);
	КонецЕсли;
	
	Если Тип = ЮридическоеЛицо Тогда
		
		Если ЗначениеЗаполнено(КПП) Тогда
			ПараметрыПоиска.Вставить("КПП", КПП);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НаименованиеПолное) Тогда
			ПараметрыПоиска.Вставить("Наименование", НаименованиеПолное);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Тип = ЮридическоеЛицо
		Или Тип = ИндивидуальныйПредприниматель Тогда
		
		Если ЗначениеЗаполнено(НаименованиеПолное) Тогда
			ПараметрыПоиска.Вставить("НаименованиеПолное", НаименованиеПолное);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОГРН) И Не ЗначениеЗаполнено(ИНН) Тогда
			ПараметрыПоиска.Вставить("ОГРН", ОГРН);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Тип = ФизическоеЛицо
		Или Тип = ИндивидуальныйПредприниматель Тогда
		
		Если ЗначениеЗаполнено(ФИО) Тогда
			ПараметрыПоиска.Вставить("ФИО", ФИО);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(АдресПредставление) Тогда
		
		ПараметрыПоиска.Вставить("Адрес",                  Адрес);
		ПараметрыПоиска.Вставить("АдресПредставление",     АдресПредставление);
		ПараметрыПоиска.Вставить("ДанныеАдреса",           ДанныеАдреса);
		
		Если ЗначениеЗаполнено(ВидАдреса) Тогда
			ПараметрыПоиска.Вставить("ВидАдреса",              ВидАдреса);
			ПараметрыПоиска.Вставить("ВидАдресаПредставление", Элементы.ВидАдреса.СписокВыбора.НайтиПоЗначению(ВидАдреса).Представление);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыПоиска.Количество() = 0 Тогда
		
		ТекстПредупреждения = НСтр("ru = 'Необходимо указать хотя бы одно условие поиска'");
		ПоказатьПредупреждение( , ТекстПредупреждения);
		Возврат;
		
	Иначе
		
		ПараметрыПоиска.Вставить("Тип", Тип);
		
	КонецЕсли;
	
	Закрыть(ПараметрыПоиска);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	ИндивидуальныйПредприниматель = Перечисления.ТипыОрганизацийСАТУРН.ИндивидуальныйПредприниматель;
	ЮридическоеЛицо               = Перечисления.ТипыОрганизацийСАТУРН.ЮридическоеЛицо;
	ФизическоеЛицо                = Перечисления.ТипыОрганизацийСАТУРН.ФизическоеЛицо;
	
	Если Тип = ЮридическоеЛицо Тогда
		
		Элементы.ИНН.ПодсказкаВвода  = НСтр("ru = 'Введите 10 символов'");
		Элементы.ОГРН.ПодсказкаВвода = НСтр("ru = 'Введите 13 символов'");
		ФИО = "";

	ИначеЕсли Тип = ИндивидуальныйПредприниматель Тогда
		
		Элементы.ИНН.ПодсказкаВвода = НСтр("ru = 'Введите 12 символов'");
		Элементы.ОГРН.ПодсказкаВвода = НСтр("ru = 'Введите 15 символов'");
		НаименованиеПолное = "";
		КПП                = "";
	
	ИначеЕсли Тип = ФизическоеЛицо Тогда
		
		Элементы.ИНН.ПодсказкаВвода = НСтр("ru = 'Введите 12 символов'");
		НаименованиеПолное = "";
		ОГРН               = "";
		КПП                = "";
		
	КонецЕсли;
	
	Элементы.ФИО.Видимость                = (Тип = ФизическоеЛицо Или Тип = ИндивидуальныйПредприниматель);
	Элементы.ПолноеНаименование.Видимость = (Тип = ЮридическоеЛицо);
	Элементы.КПП.Видимость                = (Тип = ЮридическоеЛицо);
	Элементы.ОГРН.Видимость               = (Тип = ИндивидуальныйПредприниматель Или Тип = ЮридическоеЛицо);
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеДоступностьюЭлементовФормы()
	
	УказанИдентификатор = Не ПустаяСтрока(GUID);
	УказанЮридическийРеквизит = Не ПустаяСтрока(ИНН)
	                            Или Не ПустаяСтрока(КПП)
	                            Или Не ПустаяСтрока(ОГРН);
	
	УказанРеквизитНеТочногоСоответствия = Не ПустаяСтрока(НаименованиеПолное)
	                                      Или Не ПустаяСтрока(ФИО)
	                                      Или Не ПустаяСтрока(АдресПредставление);
	
	УказанРеквизитТочногоСоответствия = УказанИдентификатор Или УказанЮридическийРеквизит Или ЗначениеЗаполнено(Идентификатор);
	
	Элементы.ИНН.Доступность  = Не (УказанИдентификатор Или УказанРеквизитНеТочногоСоответствия);
	Элементы.КПП.Доступность  = Не (УказанИдентификатор Или УказанРеквизитНеТочногоСоответствия);
	Элементы.ОГРН.Доступность = Не (УказанИдентификатор Или УказанРеквизитНеТочногоСоответствия);
	
	Элементы.Идентификатор.Доступность = Не (УказанИдентификатор Или УказанЮридическийРеквизит Или УказанРеквизитНеТочногоСоответствия);
	
	Элементы.ПолноеНаименование.Доступность  = Не УказанРеквизитТочногоСоответствия;
	Элементы.ФИО.Доступность                 = Не УказанРеквизитТочногоСоответствия;
	Элементы.ГруппаПоискПоАдресу.Доступность = Не УказанРеквизитТочногоСоответствия;

КонецПроцедуры

&НаКлиенте
Функция РеквизитыПоискаКорректны()
	
	Отказ = Ложь;
	
	ЮридическоеЛицо = ПредопределенноеЗначение("Перечисление.ТипыОрганизацийСАТУРН.ЮридическоеЛицо");
	
	Если Не ПустаяСтрока(ИНН)  Тогда
		
		Если Тип = ЮридическоеЛицо Тогда
			
			Если СтрДлина(ИНН) <> 10 Тогда
			
			ТекстОшибки = НСтр("ru = 'ИНН должен содержать 10 символов'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, ,"ИНН",, Отказ);
			
			КонецЕсли;
			
		Иначе
			
			Если СтрДлина(ИНН) <> 12 Тогда
				
				ТекстОшибки = НСтр("ru = 'ИНН должен содержать 12 символов'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, ,"ИНН",, Отказ);
				
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(КПП)
		И Тип = ЮридическоеЛицо
		И СтрДлина(КПП) <> 9 Тогда
		
		ТекстОшибки = НСтр("ru = 'Необходимо указать 9 символов'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, ,"КПП",, Отказ);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(ОГРН)  Тогда
		
		Если Тип = ЮридическоеЛицо
			 И СтрДлина(ОГРН) <> 13 Тогда 
			
			ТекстОшибки = НСтр("ru = 'ОГРН должен содержать 13 символов'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, ,"ОГРН",, Отказ);
			
		ИначеЕсли Тип <> ЮридическоеЛицо
			И СтрДлина(ОГРН) <> 15 Тогда
			
			ТекстОшибки = НСтр("ru = 'ОГРН должен содержать 15 символов'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, ,"ОГРН",, Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(АдресПредставление)
		И ДанныеАдреса = Неопределено Тогда
		ДанныеАдреса = ДанныеАдресаКлиент(Ложь, Отказ);
	КонецЕсли;
	
	Возврат Не Отказ;
	
КонецФункции

#Область РедактированиеАдреса

&НаКлиенте
Функция ДанныеАдресаКлиент(ОчищатьСообщения = Истина, Отказ = Ложь)
	
	Попытка
		ДанныеАдресаСтруктурой = ИнтеграцияСАТУРНВызовСервера.ДанныеАдресаПоАдресуXML(Адрес, АдресПредставление);
	Исключение
		ТекстСообщения = НСтр("ru = 'Не удалось прочитать данные адреса. Возможно не корректно введен регион. Повторите ввод.'");
		Если ОчищатьСообщения Тогда
			ОчиститьСообщения();
		КонецЕсли;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "ПредставлениеАдреса");
		ДанныеАдресаСтруктурой = Неопределено;
		
		Отказ = Истина;
	КонецПопытки;
	
	Возврат ДанныеАдресаСтруктурой;
	
КонецФункции

&НаСервере
Процедура ИнициализироватьПоляКонтактнойИнформации()
	
	// Реквизит формы, контролирующий работу с адресом доставки.
	// Используемые поля аналогичны полям справочника ВидыКонтактнойИнформации.
	ВидКонтактнойИнформацииАдреса = Новый Структура;
	ВидКонтактнойИнформацииАдреса.Вставить("Тип", Перечисления.ТипыКонтактнойИнформации.Адрес);
	ВидКонтактнойИнформацииАдреса.Вставить("АдресТолькоРоссийский",        Истина);
	ВидКонтактнойИнформацииАдреса.Вставить("ВключатьСтрануВПредставление", Ложь);
	ВидКонтактнойИнформацииАдреса.Вставить("СкрыватьНеактуальныеАдреса",   Ложь);
	
	// Считываем данные из полей адреса в реквизиты для редактирования.
	АдресПредставление = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(Адрес);
	
КонецПроцедуры

&НаСервере
Функция ПредставлениеАдресаПриИзмененииНаСервере(Знач Представление)
	
	ТекстСообщенияОбОшибке = "";
	
	Адрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияXMLПоПредставлению(Представление, ВидКонтактнойИнформацииАдреса);
	
	Попытка
		ДанныеАдресаСтруктурой = ИнтеграцияСАТУРНВызовСервера.ДанныеАдресаПоАдресуXML(Адрес, АдресПредставление);
	Исключение
		ТекстСообщенияОбОшибке = НСтр("ru = 'Не удалось прочитать данные адреса. Возможно не корректно введен регион. Повторите ввод.'");
		ДанныеАдресаСтруктурой = Неопределено;
	КонецПопытки;
	
	ДанныеАдреса = ДанныеАдресаСтруктурой;
	
	Возврат ТекстСообщенияОбОшибке;
	
КонецФункции

#КонецОбласти

#КонецОбласти