#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияИСПереопределяемый.ПередЗаписьюОбъекта(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	КлассификаторОрганизацийСАТУРН.Ссылка        КАК Ссылка,
		|	КлассификаторОрганизацийСАТУРН.Наименование  КАК Наименование,
		|	КлассификаторОрганизацийСАТУРН.Идентификатор КАК Идентификатор
		|ИЗ
		|	Справочник.КлассификаторОрганизацийСАТУРН КАК КлассификаторОрганизацийСАТУРН
		|ГДЕ
		|	КлассификаторОрганизацийСАТУРН.Контрагент = &Контрагент
		|	И КлассификаторОрганизацийСАТУРН.Ссылка <> &ТекущаяОрганизация";
		
		Запрос.УстановитьПараметр("Контрагент",         Контрагент);
		Запрос.УстановитьПараметр("ТекущаяОрганизация", Ссылка);
		
		УстановитьПривилегированныйРежим(Истина);
		Выборка = Запрос.Выполнить().Выбрать();
		УстановитьПривилегированныйРежим(Ложь);
		
		Если Выборка.Следующий() Тогда
			
			ТекстСообщения = СтрШаблон(НСтр("ru='Данный контрагент/организация уже сопоставлена другой организации (%1).'"),
			                           Выборка.Наименование);
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				"Контрагент",,
				Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли