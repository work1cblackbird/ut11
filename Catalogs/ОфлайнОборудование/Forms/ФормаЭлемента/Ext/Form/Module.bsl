
#Область ОбработчикиСобытийФорм

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СозданиеНовогоЭлемента = Объект.Ссылка = Справочники.ОфлайнОборудование.ПустаяСсылка();
	// Защита от изменения уже созданного экземпляра устройства.
	Элементы.ОбработчикОфлайнОборудования.ТолькоПросмотр = НЕ СозданиеНовогоЭлемента;
	Элементы.ОбработчикОфлайнОборудования.КнопкаОткрытия = Не СозданиеНовогоЭлемента;
	Элементы.ТипОборудования.ТолькоПросмотр = НЕ СозданиеНовогоЭлемента;
	Элементы.ТипОборудования.КнопкаОткрытия = Не СозданиеНовогоЭлемента;
	
	Если СозданиеНовогоЭлемента Тогда 
		Если ПустаяСтрока(Объект.РабочееМесто) Тогда
			Объект.РабочееМесто = МенеджерОборудованияВызовСервера.РабочееМестоКлиента();
		КонецЕсли;
		Элементы.ПараметрыПодключения.Видимость = Ложь;
	КонецЕсли;
	
	ПроверкаПараметров = СозданиеНовогоЭлемента;
	
#Если МобильноеПриложениеСервер Тогда
	Элементы.РабочееМесто.Видимость = Ложь;
	Элементы.ПараметрыПодключения.Видимость = Ложь;
	Элементы.ТипПодключения.Видимость = Ложь;
#Иначе
	Если Объект.ТипПодключенияОборудования = Перечисления.ТипыПодключенияОборудования.ЛокальноеПодключение Тогда
		Элементы.РабочееМесто.Видимость = Истина;
	Иначе
		Элементы.РабочееМесто.Видимость = Ложь;
	КонецЕсли;
#КонецЕсли
	
	МенеджерОфлайнОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.Наименование);
	МенеджерОфлайнОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.РабочееМесто);
	МенеджерОфлайнОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ОбработчикОфлайнОборудования);
	
	МенеджерОфлайнОборудованияВызовСервераПереопределяемый.ЭкземплярОфлайнОборудованияПриСозданииНаСервере(Объект, ЭтотОбъект, Отказ, Параметры, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	МенеджерОфлайнОборудованияВызовСервераПереопределяемый.ЭкземплярОфлайнОборудованияПриЧтенииНаСервере(ТекущийОбъект, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	МенеджерОфлайнОборудованияКлиентПереопределяемый.ЭкземплярОфлайнОборудованияПередЗаписью(Объект, ЭтотОбъект, Отказ, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	МенеджерОфлайнОборудованияВызовСервераПереопределяемый.ЭкземплярОфлайнОборудованияПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	МенеджерОфлайнОборудованияВызовСервераПереопределяемый.ЭкземплярОфлайнОборудованияПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыУстройства = МенеджерОфлайнОборудованияВызовСервера.ПараметрыУстройства(Объект.Ссылка);
	МенеджерОфлайнОборудованияВызовСервераПереопределяемый.ЭкземплярОфлайнОборудованияПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МенеджерОфлайнОборудованияВызовСервераПереопределяемый.ЭкземплярОфлайнОборудованияОбработкаПроверкиЗаполненияНаСервере(Объект, ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	СозданиеНовогоЭлемента = Ложь;
	
	Элементы.ПараметрыПодключения.Видимость = НЕ СозданиеНовогоЭлемента;
	
	МенеджерОфлайнОборудованияКлиентПереопределяемый.ЭкземплярОфлайнОборудованияПослеЗаписи(Объект, ЭтотОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыПодключения(Команда)
	
	Если СозданиеНовогоЭлемента Или Модифицированность Тогда
		Если Записать() Тогда
			ПроверкаПараметров = Ложь;
			Закрыть();
		Иначе
			Возврат;
		КонецЕсли;
	Иначе
		ПроверкаПараметров = Ложь;
		Закрыть();
	КонецЕсли;
	
	МенеджерОфлайнОборудованияКлиент.ВыполнитьНастройкуОборудования(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	МенеджерОфлайнОборудованияКлиентПереопределяемый.ЭкземплярОфлайнОборудованияОбработкаНавигационнойСсылки(Объект, ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не Объект.Ссылка.Пустая() Тогда
		Если ПроверкаПараметров Тогда
			Отказ = Истина;
			Текст = НСтр("ru = 'Для использования устройства необходимо настроить параметры подключения.
			|Перейти к настройке параметров подключения?'");
			Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение",  ЭтотОбъект);
			ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ОбработчикОфлайнОборудованияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение <> Объект.ОбработчикОфлайнОборудования Тогда
		Объект.Наименование = НСтр("ru='Оборудование'") + " '" + Строка(ВыбранноеЗначение) + "'";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОборудованияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	Если Объект.ТипОфлайнОборудования <> ВыбранноеЗначение Тогда
		Объект.ТипОфлайнОборудования = ВыбранноеЗначение;
		Модифицированность = Истина;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	НастроитьДрайвераОфлайнОборудования();
	
	МенеджерОфлайнОборудованияКлиентПереопределяемый.ЭкземплярОфлайнОборудованияТипОборудованияВыбор(Объект, ЭтотОбъект, ЭтотОбъект, Элемент, ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Если Модифицированность Или СозданиеНовогоЭлемента Тогда
			Если Записать() Тогда
				ПроверкаПараметров = Ложь;
				Закрыть();
			Иначе
				Возврат;
			КонецЕсли;
		Иначе
			ПроверкаПараметров = Ложь;
			Закрыть();
		КонецЕсли;
		МенеджерОфлайнОборудованияКлиент.ВыполнитьНастройкуОборудования(Объект.Ссылка);
	Иначе
		ПроверкаПараметров = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьДрайвераОфлайнОборудования()
	
	ТипККМ = Перечисления["ТипыОфлайнОборудования"].ККМ;
	ТипПрайсЧекер = Перечисления["ТипыОфлайнОборудования"].ПрайсЧекер;
	ТипОФД = Перечисления["ТипыОфлайнОборудования"].ОФД;
	Элементы.ОбработчикОфлайнОборудования.СписокВыбора.Очистить();
	
	Если Объект.ТипОфлайнОборудования = ТипККМ Тогда
		Элементы.ОбработчикОфлайнОборудования.СписокВыбора.Добавить(Перечисления.ОбработчикиОфлайнОборудования.Обработчик1СККМОфлайн);
		Элементы.ОбработчикОфлайнОборудования.СписокВыбора.Добавить(Перечисления.ОбработчикиОфлайнОборудования.Обработчик1СЭвоторККМОфлайн);
		Элементы.ОбработчикОфлайнОборудования.СписокВыбора.Добавить(Перечисления.ОбработчикиОфлайнОборудования.ОбработчикАтолККМОфлайн);
		Элементы.ОбработчикОфлайнОборудования.СписокВыбора.Добавить(Перечисления.ОбработчикиОфлайнОборудования.ОбработчикШтрихМККМОфлайн);
		Элементы.ОбработчикОфлайнОборудования.СписокВыбора.Добавить(Перечисления.ОбработчикиОфлайнОборудования.Обработчик1СККМED);
	ИначеЕсли Объект.ТипОфлайнОборудования = ТипПрайсЧекер Тогда
		Элементы.ОбработчикОфлайнОборудования.СписокВыбора.Добавить(Перечисления.ОбработчикиОфлайнОборудования.Обработчик1СККМОфлайн);
	ИначеЕсли Объект.ТипОфлайнОборудования = ТипОФД Тогда
		Элементы.ОбработчикОфлайнОборудования.СписокВыбора.Добавить(Перечисления.ОбработчикиОфлайнОборудования.ОбработчикОФД);
	Иначе
		Элементы.ОбработчикОфлайнОборудования.СписокВыбора.Очистить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти