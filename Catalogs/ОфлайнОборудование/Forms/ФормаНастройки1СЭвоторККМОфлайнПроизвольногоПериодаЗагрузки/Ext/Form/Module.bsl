#Область ОбработчикСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаИнформацияНачальнаяЗагрузка.Видимость = Ложь;
	
	РезультатЗагрузки = Ложь;
	Параметры.Свойство("ИдентификаторУстройства", ИдентификаторУстройства);
	
	ТипОфлайнОборудования = ОбщегоНазначенияБПО.ЗначениеРеквизитаОбъекта(ИдентификаторУстройства, "ТипОфлайнОборудования");
	Если ТипОфлайнОборудования = Перечисления.ТипыОфлайнОборудования.ОФД Тогда
		Элементы.ДекорацияИнформация.РасширеннаяПодсказка.Заголовок = НСтр("ru='Укажите желаемый период загрузки данных'");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.ДатаЗагрузкиОтчетовКассЭвотор.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КассаЭвотор.Значение = Параметры.ИдентификаторУстройства;
	НаборЗаписей.Отбор.КассаЭвотор.Использование = Истина;
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() > 0 Тогда
	
		Для Каждого Запись Из НаборЗаписей Цикл
			
			ДатаНачалаЗагрузки    = Запись.ДатаПоследнейЗакрытойКассовойСмены;
			ДатаОкончанияЗагрузки = ТекущаяДатаСеанса();
			
		КонецЦикла;
		
	Иначе
		
		УстановитьМаксимальнуюДатуЗагрузки();
		Элементы.ГруппаИнформацияНачальнаяЗагрузка.Видимость = Истина;
		
	КонецЕсли;
	
	МенеджерОфлайнОборудованияПереопределяемый.ФормаНастройкиПроизвольногоПериодаЭвоторПриСозданииНаСервере(ЭтотОбъект);
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикКомандФормы

&НаКлиенте
Процедура ЗагрузитьДанные(Команда)
	
	Результат = Неопределено;
	
	УстановитьПараметрыУстройства(Результат);
	
	Если Результат Тогда
		ОповещениеПриЗавершении = Новый ОписаниеОповещения("ЗагрузитьДанныеЗавершение", ЭтотОбъект);
		МенеджерОфлайнОборудованияКлиент.НачатьЗагрузкуДанныхИзККМ(ИдентификаторУстройства, УникальныйИдентификатор, ОповещениеПриЗавершении, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отменить(Команда)
	Закрыть();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаНачалаЗагрузкиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ВыбранноеЗначение < ОбщегоНазначенияБПОКлиент.ДатаСеанса() - 6*30*86400 Тогда
		
		ТекстСообщения = НСтр("ru='Выбранный период загрузки превышает 6 месяцев'");
		ОбщегоНазначенияБПОКлиент.СообщитьПользователю(ТекстСообщения);
		
	ИначеЕсли ВыбранноеЗначение > ДатаОкончанияЗагрузки Тогда
		
		ТекстСообщения = НСтр("ru='Начальная дата загрузки не может превышать конечную'");
		ОбщегоНазначенияБПОКлиент.СообщитьПользователю(ТекстСообщения);
		
	Иначе
		
		ДатаНачалаЗагрузки = ВыбранноеЗначение;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияЗагрузкиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ВыбранноеЗначение < ДатаНачалаЗагрузки Тогда
		СообщениеОбОшибке = НСтр("ru='Дата окончания загрузки не может быть меньше даты загрузки.'");
		ОбщегоНазначенияБПОКлиент.СообщитьПользователю(СообщениеОбОшибке);
	Иначе
		ДатаОкончанияЗагрузки = КонецДня(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗагрузитьДанныеЗавершение(Результат, Параметры) Экспорт
	
	УстановитьАвтоматическийПериодЗагрузки();
	РезультатЗагрузки = Результат.Результат;
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыУстройства(Результат)
	
	Результат = Неопределено;
	УстановитьПривилегированныйРежим(Истина);
	ДанныеУстройства = МенеджерОфлайнОборудованияВызовСервера.ДанныеУстройства(ИдентификаторУстройства);
	Если Не ДанныеУстройства.Параметры.Свойство("ДатаНачала") ИЛИ Не ДанныеУстройства.Параметры.Свойство("ДатаОкончания") Тогда
		СообщениеОбОшибке = НСтр("ru='Не записаны параметры устройства. Проведите настройку офлайн кассы.'");
		ОбщегоНазначенияБПО.СообщитьПользователю(СообщениеОбОшибке);
		Результат = Ложь;
		Возврат;
	КонецЕсли;
	
	ПараметрыУстройства = ДанныеУстройства.Параметры; // Структура
	ПараметрыУстройства.ДатаНачала           = ДатаНачалаЗагрузки;
	ПараметрыУстройства.ДатаОкончания        = ДатаОкончанияЗагрузки;
	ПараметрыУстройства.ЭтоПерваяЗагрузка    = Истина;
	
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных();
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ДатаЗагрузкиОтчетовКассЭвотор");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("КассаЭвотор", ИдентификаторУстройства);
		Блокировка.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.ДатаЗагрузкиОтчетовКассЭвотор.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.КассаЭвотор.Значение = ИдентификаторУстройства;
		НаборЗаписей.Отбор.КассаЭвотор.Использование = Истина;
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() = 0 Тогда
			Запись             = НаборЗаписей.Добавить();
			Запись.КассаЭвотор = ИдентификаторУстройства;
			Запись.ДатаПоследнейЗакрытойКассовойСмены = ДатаНачалаЗагрузки;
			НаборЗаписей.Записать();
		КонецЕсли;
		
		ДанныеУстройства.Параметры.ПериодИзмененВручную = Истина;
		РезультатЗавершения = МенеджерОфлайнОборудованияВызовСервера.СохранитьПараметрыУстройства(ИдентификаторУстройства, ДанныеУстройства.Параметры);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗавершения Тогда
		Результат = РезультатЗавершения;
		ОбновитьПовторноИспользуемыеЗначения();
	Иначе
		Результат = Ложь;
		СообщениеОбОшибке = НСтр("ru='Не удалось сохранить параметры устройства.'");
		ОбщегоНазначенияБПО.СообщитьПользователю(СообщениеОбОшибке);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьАвтоматическийПериодЗагрузки()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеУстройства = МенеджерОфлайнОборудованияВызовСервера.ДанныеУстройства(ИдентификаторУстройства);
	ДанныеУстройства.Параметры.ПериодИзмененВручную = Ложь;
	
	РезультатЗавершения = МенеджерОфлайнОборудованияВызовСервера.СохранитьПараметрыУстройства(ИдентификаторУстройства, ДанныеУстройства.Параметры);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗавершения Тогда 
		ОбновитьПовторноИспользуемыеЗначения();
	Иначе
		СообщениеОбОшибке = НСтр("ru='Не удалось сохранить параметры устройства.'");
		ОбщегоНазначенияБПО.СообщитьПользователю(СообщениеОбОшибке);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьМаксимальнуюДатуЗагрузки()
	
	ДатаНачалаЗагрузки    = НачалоДня(ТекущаяДатаСеанса() - 6*30*86400 + 86400);
	ДатаОкончанияЗагрузки = ТекущаяДатаСеанса();
	
КонецПроцедуры

#КонецОбласти