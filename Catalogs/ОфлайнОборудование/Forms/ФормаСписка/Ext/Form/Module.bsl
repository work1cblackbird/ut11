
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
#Если МобильноеПриложениеСервер Тогда
	ОтображатьДопЭлементы = Ложь;
#Иначе
	ОтображатьДопЭлементы = Истина;
#КонецЕсли
	
	ТипПодключенияОборудования = Перечисления.ТипыПодключенияОборудования.ЛокальноеПодключение;
	
	ОбновитьПользовательскийИнтерфейс();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
#Если НЕ ВебКлиент Тогда
	ИмяКомпьютера = ИмяКомпьютера();
#КонецЕсли
	
	МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента();
	
	ОбновитьОтображение()
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ТипПодключенияОборудованияПриИзменении(Элемент)
	
	ОбновитьОтображение();
	
КонецПроцедуры

&НаКлиенте
Процедура РабочееМестоПриИзменении(Элемент)
	
	ОбновитьОтображение();
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяКомпьютераПриИзменении(Элемент)
	
	РабочееМесто = "0";
	ОбновитьОтображение();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьОтображение()
	
	ЛокальноеПодключение = ТипПодключенияОборудования = ПредопределенноеЗначение("Перечисление.ТипыПодключенияОборудования.ЛокальноеПодключение");
	
	СписокУстройств.Отбор.Элементы[0].ПравоеЗначение = ТипПодключенияОборудования;
	
	Элементы.РабочееМесто.Видимость = ЛокальноеПодключение;
	Элементы.ИмяКомпьютера.Видимость = ЛокальноеПодключение;
	
	Если ЛокальноеПодключение Тогда
		
		Если ИмяКомпьютера = "0" Тогда // ВСЕ
			СписокУстройств.Отбор.Элементы[1].Использование = Ложь;  // ИмяКомпьютера
			Элементы.СписокУстройствИмяКомпьютера.Видимость  = Истина;
			Элементы.СписокУстройствРабочееМесто.Доступность = Ложь;
		Иначе 
			СписокУстройств.Отбор.Элементы[1].Использование  = Истина; // ИмяКомпьютера
			СписокУстройств.Отбор.Элементы[1].ПравоеЗначение = ИмяКомпьютера;
			Элементы.СписокУстройствИмяКомпьютера.Видимость  = Ложь;
			Элементы.СписокУстройствРабочееМесто.Доступность = Истина;
		КонецЕсли;
		
		Если РабочееМесто = "0" Тогда // ВСЕ
			СписокУстройств.Отбор.Элементы[2].Использование = Ложь;
			Элементы.СписокУстройствРабочееМесто.Видимость  = Истина;
		Иначе 
			СписокУстройств.Отбор.Элементы[2].ПравоеЗначение = Строка(РабочееМесто);
			СписокУстройств.Отбор.Элементы[2].Использование  = Истина;
			Элементы.СписокУстройствРабочееМесто.Видимость  = Истина;
		КонецЕсли;
		
	Иначе
		СписокУстройств.Отбор.Элементы[1].Использование = Ложь;
		СписокУстройств.Отбор.Элементы[2].Использование = Ложь;
		Элементы.СписокУстройствРабочееМесто.Видимость  = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПользовательскийИнтерфейс()
	
	ОбновитьСписокКомпьютеров();
	ОбновитьСписокРабочихМест();

	РеквизитыТекущегоРабочегоМеста = Неопределено;
	РабочееМестоКлиента = МенеджерОборудованияВызовСервера.РабочееМестоКлиента();
	Если ТекущееРабочееМесто = Справочники.РабочиеМеста.ПустаяСсылка()
		Или ТекущееРабочееМесто <> РабочееМестоКлиента Тогда
		ТекущееРабочееМесто = РабочееМестоКлиента;
		РеквизитыТекущегоРабочегоМеста = ОбщегоНазначенияБПО.ЗначенияРеквизитовОбъекта(ТекущееРабочееМесто, "Код, ИмяКомпьютера");
		РабочееМесто = РеквизитыТекущегоРабочегоМеста.Код;
	КонецЕсли;
	
	Если ОтображатьДопЭлементы Тогда
		// 0 - ТипПодключения
		ЭлементОтбора = СписокУстройств.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТипПодключенияОборудования");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = Перечисления.ТипыПодключенияОборудования.ЛокальноеПодключение;
		ЭлементОтбора.Использование = Истина;
		// 1 - ИмяКомпьютера
		
		Если РеквизитыТекущегоРабочегоМеста = Неопределено Тогда
			ТекущееРабочееМестоИмяКомпьютера = ОбщегоНазначенияБПО.ЗначениеРеквизитаОбъекта(ТекущееРабочееМесто, "ИмяКомпьютера");
		Иначе
			ТекущееРабочееМестоИмяКомпьютера = РеквизитыТекущегоРабочегоМеста.ИмяКомпьютера;
		КонецЕсли;
		
		ЭлементОтбора = СписокУстройств.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("РабочееМесто.ИмяКомпьютера");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = ТекущееРабочееМестоИмяКомпьютера;
		ЭлементОтбора.Использование = Ложь;
		// 2 - РабочееМесто
		ЭлементОтбора = СписокУстройств.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("РабочееМестоКод");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = Строка(РабочееМесто);
		ЭлементОтбора.Использование = Ложь;
		
	КонецЕсли;
	
	ЭлементГруппировки = СписокУстройств.Группировка.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	ЭлементГруппировки.Поле = Новый ПолеКомпоновкиДанных("ТипОфлайнОборудования");
	ЭлементГруппировки.Использование = Истина;
	
	Элементы.СписокУстройств.Шапка = ОтображатьДопЭлементы;
	Элементы.Управление.Видимость = ОтображатьДопЭлементы;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокКомпьютеров();
	
	Элементы.ИмяКомпьютера.СписокВыбора.Добавить("0", НСтр("ru = '<ВСЕ>'"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РабочиеМеста.ИмяКомпьютера КАК ИмяКомпьютера
		|ИЗ
		|	Справочник.РабочиеМеста КАК РабочиеМеста";
	      
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Элементы.ИмяКомпьютера.СписокВыбора.Добавить(ВыборкаДетальныеЗаписи.ИмяКомпьютера, ВыборкаДетальныеЗаписи.ИмяКомпьютера);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокРабочихМест();
	
	Элементы.РабочееМесто.СписокВыбора.Очистить();
	Элементы.РабочееМесто.СписокВыбора.Добавить("0",  НСтр("ru = '<ВСЕ>'"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РабочиеМеста.Ссылка КАК Ссылка,
		|	РабочиеМеста.Код КАК Код,
		|	РабочиеМеста.Наименование КАК Наименование
		|ИЗ
		|	Справочник.РабочиеМеста КАК РабочиеМеста";
	      
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Элементы.РабочееМесто.СписокВыбора.Добавить(ВыборкаДетальныеЗаписи.Код, ВыборкаДетальныеЗаписи.Наименование);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОборудование(Команда)
	
	ОчиститьСообщения();
	
	Если Элементы.СписокУстройств.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.СписокУстройств.ТекущиеДанные.Свойство("Ссылка") Тогда
		МенеджерОфлайнОборудованияКлиент.ВыполнитьНастройкуОборудования(Элементы.СписокУстройств.ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
