#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьПослеДобавленияТНВЭД" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЕстьКлассификатор = Метаданные.Справочники.КлассификаторТНВЭД.Макеты.Найти(
		"КлассификаторТоварнойНоменклатурыВнешнеэкономическойДеятельности") <> Неопределено;
	
	Элементы.ПодборИзТНВЭД.Видимость = ПравоДоступа("Редактирование", Метаданные.Справочники.КлассификаторТНВЭД)
		И ЕстьКлассификатор;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если ЕстьКлассификатор Тогда
		Отказ = Истина;
		
		Текст = НСтр("ru = 'Есть возможность подобрать товарную номенклатуру ВЭД из классификатора.
			|Подобрать?'");
		
		ДополнительныеПараметры = Новый Структура;
		Если Копирование Тогда
			ЗначенияЗаполнения = Новый Структура;
			ЗначенияЗаполнения.Вставить("Код", Элемент.ТекущиеДанные.Код);
			ЗначенияЗаполнения.Вставить("Наименование", Элемент.ТекущиеДанные.Наименование);
			ДополнительныеПараметры.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		КонецЕсли;
		
		Оповещение = Новый ОписаниеОповещения("ВопросПодобратьТНВЭДЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодобратьИзМакета(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Подбор", Истина);
	ПараметрыФормы.Вставить("ИмяСправочника", "КлассификаторТНВЭД");
		
	ОткрытьФорму("ОбщаяФорма.ДобавлениеЭлементовВКлассификатор", ПараметрыФормы, ЭтаФорма, , , , ,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВопросПодобратьТНВЭДЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПодобратьИзМакета(Неопределено);
	Иначе
		ОткрытьФорму("Справочник.КлассификаторТНВЭД.Форма.ФормаЭлемента", ДополнительныеПараметры, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти