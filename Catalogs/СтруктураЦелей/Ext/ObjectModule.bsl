#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	НастройкаПорядкаЭлементов.ЗаполнитьЗначениеРеквизитаУпорядочивания(ЭтотОбъект, Отказ);
	РеквизитИерархическогоУпорядочивания = РеквизитИерархическогоУпорядочивания();
	Если РеквизитДопУпорядочиванияИерархического <> РеквизитИерархическогоУпорядочивания Тогда
		РеквизитДопУпорядочиванияИерархического = РеквизитИерархическогоУпорядочивания;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Очистим кэш рассчитанных значений варианта анализа, 
	// т.к. структура рассчитанных хранимых данных зависит от его настроек.
	ВариантыАнализаКОчисткеИсточников = ВариантыАнализаЦели(Ссылка);
	
	Если НЕ ВариантыАнализаКОчисткеИсточников = Неопределено Тогда
		НаборЗаписей = РегистрыСведений.ИсточникиДанныхВариантовАнализаЦелевыхПоказателей.СоздатьНаборЗаписей();
		Пока ВариантыАнализаКОчисткеИсточников.Следующий() Цикл 
			НаборЗаписей.Отбор.ВариантАнализа.Установить(ВариантыАнализаКОчисткеИсточников.ВариантАнализа);
			НаборЗаписей.Записать();
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОбновитьРеквизитИерархическогоУпорядочивания(Ссылка, РеквизитДопУпорядочиванияИерархического);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если НЕ ЦельИзмеримая Тогда
		 МассивНепроверяемыхРеквизитов.Добавить("ЦелевойПоказатель");
		 МассивНепроверяемыхРеквизитов.Добавить("КраткоеНаименованиеЦелевогоПоказателя");
		 МассивНепроверяемыхРеквизитов.Добавить("ЦелевойТренд");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

Функция РеквизитИерархическогоУпорядочивания()
	
	РеквизитИерархическогоУпорядочивания = Формат(РеквизитДопУпорядочивания, "ЧЦ=5; ЧН=; ЧВН=; ЧГ=");
	ПроверяемыйРодитель = Родитель;
	Если ЗначениеЗаполнено(ПроверяемыйРодитель) Тогда
		ЗначенияРеквизитаРодителя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПроверяемыйРодитель, "РеквизитДопУпорядочиванияИерархического");
		РеквизитИерархическогоУпорядочивания = ЗначенияРеквизитаРодителя + РеквизитИерархическогоУпорядочивания;
	КонецЕсли;
	
	Возврат РеквизитИерархическогоУпорядочивания;
	
КонецФункции

Процедура ОбновитьРеквизитИерархическогоУпорядочивания(Ссылка, РеквизитИерархическогоУпорядочивания)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтруктураЦелей.Ссылка
	|ИЗ
	|	Справочник.СтруктураЦелей КАК СтруктураЦелей
	|ГДЕ
	|	СтруктураЦелей.Родитель = &Ссылка
	|	И НЕ СтруктураЦелей.РеквизитДопУпорядочиванияИерархического ПОДОБНО &РеквизитИерархическогоУпорядочивания";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("РеквизитИерархическогоУпорядочивания", РеквизитИерархическогоУпорядочивания + "%");
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ИзменяемыйОбъект = Выборка.Ссылка.ПолучитьОбъект();
			Попытка 
				ИзменяемыйОбъект.Заблокировать();
			Исключение
				ТекстИсключенияЗаписи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось изменить настройку упорядочивания для элемента структуры целей ""%1"" 
				|Возможно, элемент структуры целей редактируется другим пользователем'"),
				ИзменяемыйОбъект.Наименование);
				ВызватьИсключение ТекстИсключенияЗаписи;
			КонецПопытки;
			ИзменяемыйОбъект.Записать();
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ВариантыАнализаЦели(Цель)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Цель", Цель);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ИсточникиДанныхВариантовАнализаЦелевыхПоказателей.ВариантАнализа
	               |ИЗ
	               |	РегистрСведений.ИсточникиДанныхВариантовАнализаЦелевыхПоказателей КАК ИсточникиДанныхВариантовАнализаЦелевыхПоказателей
	               |ГДЕ
	               |	ИсточникиДанныхВариантовАнализаЦелевыхПоказателей.ВариантАнализа.Владелец = &Цель
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ИсточникиДанныхВариантовАнализаЦелевыхПоказателей.ВариантАнализа";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Возврат РезультатЗапроса.Выбрать();
		
	Иначе
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли