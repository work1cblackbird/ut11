#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Контракт = Параметры.Контракт;
	ДанныеКонтракта = ЭлектронноеАктированиеЕИС.ДанныеГосударственногоКонтракта(Параметры.Контракт);
	ИдентификаторОбъектаЗакупки = Параметры.ИдентификаторОбъектаЗакупки;
	
	СведенияОЛекарственномПрепарате = Неопределено;
	Для каждого Объект Из ДанныеКонтракта.ОбъектыЗакупки Цикл
		Если Объект.Идентификатор = ИдентификаторОбъектаЗакупки Тогда
			СведенияОЛекарственномПрепарате = Объект.СведенияОЛекарственномПрепарате;
			Наименование = Объект.Наименование;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если СведенияОЛекарственномПрепарате = Неопределено Тогда
		ТекстОшибки = НСтр("ru = 'Не удалось найти объект закупки по идентификатору %1.'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, ИдентификаторОбъектаЗакупки);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ВключенВЖНВЛП = СведенияОЛекарственномПрепарате.ВключенВЖНВЛП;
	СрокГодности = СведенияОЛекарственномПрепарате.СрокГодности;
	Для каждого Позиция Из СведенияОЛекарственномПрепарате.СписокМНН Цикл
		Строка = ПозицииМНН.Добавить();
		Строка.Представление = Позиция.НаименованиеМНН;
		Строка.Код = Позиция.КодМНН;
		Строка.ГиперСсылка = "Сведения о позиции МНН";
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПозицииМНН

&НаКлиенте
Процедура ПозицииМННВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ПозицииМННГиперСсылка" Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ИдентификаторОбъектаЗакупки", ИдентификаторОбъектаЗакупки);
		ПараметрыФормы.Вставить("Контракт", Контракт);
		ПараметрыФормы.Вставить("КодМНН", Элемент.ТекущиеДанные.Код);
		
		ОткрытьФорму("Справочник.ГосударственныеКонтракты.Форма.ФормаПозицииМНН",
			ПараметрыФормы, , УникальныйИдентификатор, , , ,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	ЭтотОбъект.Закрыть();
КонецПроцедуры

#КонецОбласти


