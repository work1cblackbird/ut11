#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокЭтапов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьЭтап(Команда)
	
	ЗавершитьВыбор();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокЭтапов()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ГосударственныеКонтрактыЭтапыИсполнения.Идентификатор КАК Идентификатор,
	|	ГосударственныеКонтрактыЭтапыИсполнения.ДатаНачала КАК ДатаНачала,
	|	ГосударственныеКонтрактыЭтапыИсполнения.ДатаОкончания КАК ДатаОкончания,
	|	ГосударственныеКонтрактыЭтапыИсполнения.Цена КАК Цена,
	|	ГосударственныеКонтрактыЭтапыИсполнения.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Справочник.ГосударственныеКонтракты.ЭтапыИсполнения КАК ГосударственныеКонтрактыЭтапыИсполнения
	|ГДЕ
	|	ГосударственныеКонтрактыЭтапыИсполнения.Ссылка = &Госконтракт
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаНачала";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Госконтракт", Параметры.Госконтракт);
	
	Результат = Запрос.Выполнить();
	СписокЭтапов.Загрузить(Результат.Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВыбор()
	
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	
	Если СтрокаТаблицы <> Неопределено Тогда
		РезультатВыбора = Новый Структура();
		РезультатВыбора.Вставить("ЭтапГосконтрактаЕИС", СтрокаТаблицы.Идентификатор);
		РезультатВыбора.Вставить("НомерСтроки", СтрокаТаблицы.НомерСтроки);
		РезультатВыбора.Вставить("ДатаНачала", СтрокаТаблицы.ДатаНачала);
		РезультатВыбора.Вставить("ДатаОкончания", СтрокаТаблицы.ДатаОкончания);
		
		ОповеститьОВыборе(РезультатВыбора);
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти