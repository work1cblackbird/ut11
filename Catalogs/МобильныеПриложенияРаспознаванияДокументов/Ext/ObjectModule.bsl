#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПометкаУдаления Тогда
		Отключено = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоНовый() И Наименование <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Наименование") Тогда
		РаспознаваниеДокументовКоннекторСлужебный.ПереименоватьМобильноеПриложение(Код, Наименование);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПометкаУдаления Тогда
		РаспознаваниеДокументовКоннекторСлужебный.ОтключитьМобильноеПриложения(Код);
	КонецЕсли;
	
	Если Не ЭтоНовый() И Не ПометкаУдаления Тогда
		// Актуализация связанных записей
		УстановитьПривилегированныйРежим(Истина);
		РегистрыСведений.ИсходныеДанныеЗаданийРаспознаваниеДокументов.ПереименоватьМобильныеПриложения(Код, Наименование);
		Документы.РаспознанныйДокумент.ПерезаписатьДокументыСозданныеМобильнымПриложением(Ссылка);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

Процедура Отключить() Экспорт
	
	РаспознаваниеДокументовКоннекторСлужебный.ОтключитьМобильноеПриложения(
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Код"));
	
	Отключено = Истина;
	Записать();
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли