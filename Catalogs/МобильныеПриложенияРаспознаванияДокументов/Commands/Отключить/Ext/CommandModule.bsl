#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Контекст = Новый Структура;
	Контекст.Вставить("МобильныеПриложенияКУдалению", ПараметрКоманды);
	
	Если Контекст.МобильныеПриложенияКУдалению.Количество() = 1 Тогда
		ТекстВопроса = НСтр("ru = 'Отключить мобильное приложение?'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Отключить выбранные мобильные приложения?'");
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ПослеПодтвержденияУдаленияМобильныхПриложений", ЭтотОбъект, Контекст);
	ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодтвержденияУдаленияМобильныхПриложений(Результат, Контекст) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		УдалитьМобильныеПриложения(Контекст.МобильныеПриложенияКУдалению);
		
		Для Каждого Ссылка Из Контекст.МобильныеПриложенияКУдалению Цикл
			ОповеститьОбИзменении(Ссылка);
			Оповестить("МобильныеПриложенияРаспознаванияДокументов_Запись", Ссылка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьМобильныеПриложения(МобильныеПриложенияКУдалению)
	
	Для Каждого Ссылка Из МобильныеПриложенияКУдалению Цикл
		Объект = Ссылка.ПолучитьОбъект();
		Объект.Отключить();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти