#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРаспознаваниеДокументов") Тогда
		ВызватьИсключение НСтр("ru = 'Функциональная опция ИспользоватьРаспознаваниеДокументов отключена.'");
	КонецЕсли;

	Если Объект.Ссылка.Пустая() Тогда
		
		МобильныеПриложенияТ = РаспознаваниеДокументовКоннекторСлужебный.ПодключенныеМобильныеПриложения();
		
		Объект.Наименование = СтрШаблон(НСтр("ru = 'Клиент %1'"), МобильныеПриложенияТ.Количество() + 1);
		Объект.Ответственный = Пользователи.ТекущийПользователь();
		
		Попытка
			
			Объект.Код = РаспознаваниеДокументовКоннекторСлужебный.ЗарегистрироватьМобильноеПриложение(Объект.Наименование);
			Записать();
			
		Исключение
			
			Если Не ПустаяСтрока(Объект.Код) Тогда
				РаспознаваниеДокументовКоннекторСлужебный.ОтключитьМобильноеПриложения(Объект.Код);
			КонецЕсли;
			
			Отказ = Истина;
			Возврат;
			
		КонецПопытки;
		
	КонецЕсли;
	
	QRCodeПодключения = ПоместитьВоВременноеХранилище(ДанныеQRКода(Объект.Код), УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "МобильныеПриложенияРаспознаванияДокументов_Запись" И Параметр = Объект.Ссылка Тогда
		Прочитать();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если Объект.Отключено Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли;
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущийПользователь = ПользователиКлиент.ТекущийПользователь();
	Объект.Наименование = ТекущийПользователь;
	Объект.Ответственный = ТекущийПользователь;
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьМобильноеПриложение(Команда)
	
	ОткрытьФорму("Справочник.МобильныеПриложенияРаспознаванияДокументов.Форма.ФормаЗагрузки");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область БизнесЛогика

&НаСервереБезКонтекста
Функция ДанныеQRКода(Данные)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Печать") Тогда
		МодульУправлениеПечатью = ОбщегоНазначения.ОбщийМодуль("УправлениеПечатью");
		Возврат МодульУправлениеПечатью.ДанныеQRКода(Данные, 0, 350);
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти
