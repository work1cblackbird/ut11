
#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);

	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ОбъектРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Ссылка, "БыстрыеТовары, КомандыФормы");
		ТаблицаБыстрыеТовары = ОбъектРеквизиты.БыстрыеТовары.Получить();
		Если ТаблицаБыстрыеТовары <> Неопределено Тогда
			БыстрыеТовары.Загрузить(ТаблицаБыстрыеТовары);
		КонецЕсли;
		
		ТаблицаКомандыФормы = ОбъектРеквизиты.КомандыФормы.Получить();
		Если ТаблицаКомандыФормы <> Неопределено Тогда
			КомандыФормы.Загрузить(ТаблицаКомандыФормы);
		КонецЕсли;
		
		КоличествоБыстрыхТоваров = БыстрыеТовары.Количество();
		КоличествоКомандФормы = КомандыФормы.Количество();
		
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗаполнитьПоУмолчаниюНаКлиенте();
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Для Каждого СтрокаТЧ Из БыстрыеТовары Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЧ.Клавиша) Тогда
			СтрокаТЧ.Клавиша = Строка(Клавиша.Нет);
		КонецЕсли;
	КонецЦикла;
	Для Каждого СтрокаТЧ Из КомандыФормы Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЧ.Клавиша) Тогда
			СтрокаТЧ.Клавиша = Строка(Клавиша.Нет);
		КонецЕсли;
	КонецЦикла;
	
	ТекущийОбъект.БыстрыеТовары  = Новый ХранилищеЗначения(БыстрыеТовары.Выгрузить());
	ТекущийОбъект.КомандыФормы = Новый ХранилищеЗначения(КомандыФормы.Выгрузить());

	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЧтениеКомандФормы"
		И Параметр.Форма = УникальныйИдентификатор Тогда
		
		ЗаполнитьПоУмолчаниюНаСервере(Параметр.АдресВоВременномХранилище);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ГорячиеКлавиши", , Объект.Ссылка);
	
	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтотОбъект, ПараметрыЗаписи);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыБыстрыетовары

&НаКлиенте
Процедура БыстрыеТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.БыстрыеТовары.ТекущиеДанные;
	ТекущиеДанные.Заголовок = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
				ТекущиеДанные.Номенклатура,
				ТекущиеДанные.Характеристика,
				ТекущиеДанные.Упаковка);
	
	ТекущаяСтрока = Элементы.БыстрыеТовары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.Характеристика);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьУпаковкуПоВладельцу", ТекущаяСтрока.Упаковка);
	СтруктураДействий.Вставить("НоменклатураПриИзмененииПереопределяемый", Новый Структура("ИмяФормы, ИмяТабличнойЧасти",
		ЭтаФорма.ИмяФормы, "БыстрыеТовары"));

	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура БыстрыеТоварыХарактеристикаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.БыстрыеТовары.ТекущиеДанные;
	ТекущиеДанные.Заголовок = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
				ТекущиеДанные.Номенклатура,
				ТекущиеДанные.Характеристика,
				ТекущиеДанные.Упаковка);
	
КонецПроцедуры

&НаКлиенте
Процедура БыстрыеТоварыУпаковкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.БыстрыеТовары.ТекущиеДанные;
	ТекущиеДанные.Заголовок = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
				ТекущиеДанные.Номенклатура,
				ТекущиеДанные.Характеристика,
				ТекущиеДанные.Упаковка);
	
КонецПроцедуры

&НаКлиенте
Процедура БыстрыеТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.БыстрыеТоварыСочетаниеКлавиш = Поле Тогда
		
		СтандартнаяОбработка = Ложь;
		ВозвращаемоеЗначение = Неопределено;
		
		ПараметрыОткрытияФормы = Новый Структура("Адрес", АдресВоВременномХранилище());
		
		ДополнительныеПараметрыОбработкиВыбора = Новый Структура("ВыбраннаяСтрока", ВыбраннаяСтрока);
		ОбработчикВыбора = Новый ОписаниеОповещения("БыстрыеТоварыВыборЗавершение", ЭтотОбъект, ДополнительныеПараметрыОбработкиВыбора);
		
		ОткрытьФорму("Справочник.ГорячиеКлавиши.Форма.ФормаВыборСочетанияКлавиш",
			ПараметрыОткрытияФормы,
			ЭтотОбъект,,,,
			ОбработчикВыбора,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура БыстрыеТоварыПриИзменении(Элемент)
	КоличествоБыстрыхТоваров = БыстрыеТовары.Количество();
КонецПроцедуры

&НаКлиенте
Процедура БыстрыеТоварыПослеУдаления(Элемент)
	КоличествоБыстрыхТоваров = БыстрыеТовары.Количество();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКоманды

&НаКлиенте
Процедура КомандыФормыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.КомандыФормыСочетаниеКлавиш = Поле Тогда
		
		СтандартнаяОбработка = Ложь;
		ВозвращаемоеЗначение = Неопределено;
		
		ПараметрыОткрытияФормы = Новый Структура("Адрес", АдресВоВременномХранилище());
		
		ДополнительныеПараметрыОбработкиВыбора = Новый Структура("ВыбраннаяСтрока", ВыбраннаяСтрока);
		ОбработчикВыбора = Новый ОписаниеОповещения("КомандыФормыВыборЗавершение", ЭтотОбъект, ДополнительныеПараметрыОбработкиВыбора);
		
		ОткрытьФорму("Справочник.ГорячиеКлавиши.Форма.ФормаВыборСочетанияКлавиш",
			ПараметрыОткрытияФормы,
			ЭтотОбъект,,,, 
			ОбработчикВыбора,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПоУмолчанию(Команда)
	
	ЗаполнитьПоУмолчаниюНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьВсеКоманды(Команда)
	
	Для Каждого СтрокаТЧ Из КомандыФормы Цикл
		СтрокаТЧ.Скрывать = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсеКоманды(Команда)
	
	Для Каждого СтрокаТЧ Из КомандыФормы Цикл
		СтрокаТЧ.Скрывать = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	ПараметрыУсловногоОформления = НоменклатураСервер.НовыеПараметрыУсловногоОформленияЕдиницИзмерения();
	ПараметрыУсловногоОформления.ИмяПоляЕдиницаИзмерения = "БыстрыеТоварыНоменклатураЕдиницаИзмерения";
	ПараметрыУсловногоОформления.ПутьКПолюУпаковка = "БыстрыеТовары.Упаковка";
	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтаФорма, ПараметрыУсловногоОформления);

	//

	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма, 
																			 "БыстрыеТоварыХарактеристика",
																		     "БыстрыеТовары.ХарактеристикиИспользуются");

КонецПроцедуры

#Область Прочее

&НаКлиенте
Процедура ЗаполнитьПоУмолчаниюНаКлиенте()
	
	СтруктураОснование = Новый Структура("ЧтениеКомандФормы");
	ПараметрыФормы = Новый Структура("Основание, АвтоТест", СтруктураОснование);
	ФормаДокументаЧекККМ = ПолучитьФорму("Документ.ЧекККМ.Форма.ФормаДокументаРМК", ПараметрыФормы);
	
	ПараметрыОповещения = Новый Структура("Форма, ФормаВладелец",
		ФормаДокументаЧекККМ.УникальныйИдентификатор,
		УникальныйИдентификатор);
		
	Оповестить("ЧтениеКомандФормы", ПараметрыОповещения);
	
КонецПроцедуры

&НаСервере
Функция ПредставлениеСочетанияКлавиш(Сочетание)
	
	Возврат ОбщегоНазначенияУТ.ПредставлениеСочетанияКлавиш(Сочетание, Истина);
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПоУмолчаниюНаСервере(АдресВоВременномХранилище)
	
	КомандыФормы.Очистить();
	
	ТаблицаКомандыФормы = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);
	Для Каждого КомандаФормы Из ТаблицаКомандыФормы Цикл
		НоваяСтрока = Справочники.ГорячиеКлавиши.НоваяКоманда(КомандыФормы, КомандаФормы);
	КонецЦикла;
	
	КоличествоКомандФормы = КомандыФормы.Количество();
	
КонецПроцедуры

&НаСервере
Функция АдресВоВременномХранилище()
	
	ДанныеДляВременногоХранилища = Новый Структура;
	ДанныеДляВременногоХранилища.Вставить("БыстрыеТовары", БыстрыеТовары.Выгрузить());
	ДанныеДляВременногоХранилища.Вставить("КомандыФормы",  КомандыФормы.Выгрузить());
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеДляВременногоХранилища, УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура ОчиститьСочетаниеКлавиш(СочетаниеКлавиш)
	
	ПредставлениеСочетанияКлавиш = ПредставлениеСочетанияКлавиш(СочетаниеКлавиш);
	
	Для Каждого СтрокаТЧ Из БыстрыеТовары Цикл
		Если СтрокаТЧ.СочетаниеКлавиш = ПредставлениеСочетанияКлавиш Тогда
			СтрокаТЧ.Клавиша = Строка(Клавиша.Нет);
			СтрокаТЧ.Ctrl = Ложь;
			СтрокаТЧ.Shift = Ложь;
			СтрокаТЧ.Alt = Ложь;
			СтрокаТЧ.СочетаниеКлавиш = "";
		КонецЕсли;
	КонецЦикла;
	Для Каждого СтрокаТЧ Из КомандыФормы Цикл
		Если СтрокаТЧ.СочетаниеКлавиш = ПредставлениеСочетанияКлавиш Тогда
			СтрокаТЧ.Клавиша = Строка(Клавиша.Нет);
			СтрокаТЧ.Ctrl = Ложь;
			СтрокаТЧ.Shift = Ложь;
			СтрокаТЧ.Alt = Ложь;
			СтрокаТЧ.СочетаниеКлавиш = "";
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Для Каждого СтрокаТЧ Из БыстрыеТовары Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) Тогда
			
			НомерСтроки = БыстрыеТовары.Индекс(СтрокаТЧ);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не заполнено поле ""Номенклатура"" в строке %1'"),
					НомерСтроки + 1
				),,
				"БыстрыеТовары["+НомерСтроки+"].Номенклатура",,Отказ);
			
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СтрокаТЧ.Характеристика) И СтрокаТЧ.ХарактеристикиИспользуются Тогда
			
			НомерСтроки = БыстрыеТовары.Индекс(СтрокаТЧ);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не заполнено поле ""Характеристика"" в строке %1'"),
					НомерСтроки + 1
				),,
				"БыстрыеТовары["+НомерСтроки+"].Характеристика",,Отказ);
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область ОбработчикиОповещений

&НаКлиенте
Процедура КомандыФормыВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ВыбраннаяСтрока = ДополнительныеПараметры.ВыбраннаяСтрока;
	
	ВозвращаемоеЗначение = Результат;
	
	Если ВозвращаемоеЗначение <> Неопределено Тогда
		
		ОчиститьСочетаниеКлавиш(ВозвращаемоеЗначение);
		
		ТекущаяСтрока = КомандыФормы.НайтиПоИдентификатору(ВыбраннаяСтрока);
		ТекущаяСтрока.СочетаниеКлавиш = ПредставлениеСочетанияКлавиш(ВозвращаемоеЗначение);
		
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, ВозвращаемоеЗначение);
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура БыстрыеТоварыВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ВыбраннаяСтрока = ДополнительныеПараметры.ВыбраннаяСтрока;
	
	ВозвращаемоеЗначение = Результат;
	
	Если ВозвращаемоеЗначение <> Неопределено Тогда
		
		ОчиститьСочетаниеКлавиш(ВозвращаемоеЗначение);
		
		ТекущаяСтрока = БыстрыеТовары.НайтиПоИдентификатору(ВыбраннаяСтрока);
		ТекущаяСтрока.СочетаниеКлавиш = ПредставлениеСочетанияКлавиш(ВозвращаемоеЗначение);
		
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, ВозвращаемоеЗначение);
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
