
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	МассивКлавиши = Новый Массив;
	
	МассивКлавиши.Добавить(Клавиша.A);
	МассивКлавиши.Добавить(Клавиша.B);
	МассивКлавиши.Добавить(Клавиша.C);
	МассивКлавиши.Добавить(Клавиша.D);
	МассивКлавиши.Добавить(Клавиша.E);
	МассивКлавиши.Добавить(Клавиша.F);
	МассивКлавиши.Добавить(Клавиша.G);
	МассивКлавиши.Добавить(Клавиша.H);
	МассивКлавиши.Добавить(Клавиша.I);
	МассивКлавиши.Добавить(Клавиша.J);
	МассивКлавиши.Добавить(Клавиша.K);
	МассивКлавиши.Добавить(Клавиша.L);
	МассивКлавиши.Добавить(Клавиша.M);
	МассивКлавиши.Добавить(Клавиша.N);
	МассивКлавиши.Добавить(Клавиша.O);
	МассивКлавиши.Добавить(Клавиша.P);
	МассивКлавиши.Добавить(Клавиша.Q);
	МассивКлавиши.Добавить(Клавиша.R);
	МассивКлавиши.Добавить(Клавиша.S);
	МассивКлавиши.Добавить(Клавиша.T);
	МассивКлавиши.Добавить(Клавиша.U);
	МассивКлавиши.Добавить(Клавиша.V);
	МассивКлавиши.Добавить(Клавиша.W);
	МассивКлавиши.Добавить(Клавиша.X);
	МассивКлавиши.Добавить(Клавиша.Y);
	МассивКлавиши.Добавить(Клавиша.Z);
	
	МассивКлавиши.Добавить(Клавиша.F1);
	МассивКлавиши.Добавить(Клавиша.F2);
	МассивКлавиши.Добавить(Клавиша.F3);
	МассивКлавиши.Добавить(Клавиша.F4);
	МассивКлавиши.Добавить(Клавиша.F5);
	МассивКлавиши.Добавить(Клавиша.F6);
	МассивКлавиши.Добавить(Клавиша.F7);
	МассивКлавиши.Добавить(Клавиша.F8);
	МассивКлавиши.Добавить(Клавиша.F9);
	МассивКлавиши.Добавить(Клавиша.F10);
	МассивКлавиши.Добавить(Клавиша.F11);
	МассивКлавиши.Добавить(Клавиша.F12);
	
	МассивКлавиши.Добавить(Клавиша._1);
	МассивКлавиши.Добавить(Клавиша._2);
	МассивКлавиши.Добавить(Клавиша._3);
	МассивКлавиши.Добавить(Клавиша._4);
	МассивКлавиши.Добавить(Клавиша._5);
	МассивКлавиши.Добавить(Клавиша._6);
	МассивКлавиши.Добавить(Клавиша._7);
	МассивКлавиши.Добавить(Клавиша._8);
	МассивКлавиши.Добавить(Клавиша._9);
	
	МассивКлавиши.Добавить(Клавиша.Num0);
	МассивКлавиши.Добавить(Клавиша.Num1);
	МассивКлавиши.Добавить(Клавиша.Num2);
	МассивКлавиши.Добавить(Клавиша.Num3);
	МассивКлавиши.Добавить(Клавиша.Num4);
	МассивКлавиши.Добавить(Клавиша.Num5);
	МассивКлавиши.Добавить(Клавиша.Num6);
	МассивКлавиши.Добавить(Клавиша.Num7);
	МассивКлавиши.Добавить(Клавиша.Num8);
	МассивКлавиши.Добавить(Клавиша.Num9);
	МассивКлавиши.Добавить(Клавиша.NumAdd);
	МассивКлавиши.Добавить(Клавиша.NumDecimal);
	МассивКлавиши.Добавить(Клавиша.NumDivide);
	МассивКлавиши.Добавить(Клавиша.NumMultiply);
	МассивКлавиши.Добавить(Клавиша.NumSubtract);
	
	МассивКлавиши.Добавить(Клавиша.Space);
	МассивКлавиши.Добавить(Клавиша.BackSpace);
	МассивКлавиши.Добавить(Клавиша.Break);
	
	Данные = Неопределено;
	Если Параметры.Свойство("Адрес") Тогда
		Данные = ПолучитьИзВременногоХранилища(Параметры.Адрес);
	КонецЕсли;
	
	Для Каждого ЭлементКлавиша Из МассивКлавиши Цикл
		
		НоваяСтрока = ТаблицаСочетанийКлавиш.Добавить();
		НоваяСтрока.ИмяКлавиши = Строка(ЭлементКлавиша);
		
		ПредставлениеСочетанияКлавиш(НоваяСтрока, Данные, "Клавиша",        Новый СочетаниеКлавиш(ЭлементКлавиша, Ложь,   Ложь,   Ложь));
		
		ПредставлениеСочетанияКлавиш(НоваяСтрока, Данные, "Ctrl",           Новый СочетаниеКлавиш(ЭлементКлавиша, Ложь,   Истина, Ложь));
		ПредставлениеСочетанияКлавиш(НоваяСтрока, Данные, "Alt",            Новый СочетаниеКлавиш(ЭлементКлавиша, Истина, Ложь,   Ложь));
		ПредставлениеСочетанияКлавиш(НоваяСтрока, Данные, "Shift",          Новый СочетаниеКлавиш(ЭлементКлавиша, Ложь,   Ложь,   Истина));
		
		ПредставлениеСочетанияКлавиш(НоваяСтрока, Данные, "Alt_Shift",      Новый СочетаниеКлавиш(ЭлементКлавиша, Истина, Ложь,   Истина));
		ПредставлениеСочетанияКлавиш(НоваяСтрока, Данные, "Ctrl_Shift",     Новый СочетаниеКлавиш(ЭлементКлавиша, Ложь,   Истина, Истина));
		ПредставлениеСочетанияКлавиш(НоваяСтрока, Данные, "Ctrl_Alt",       Новый СочетаниеКлавиш(ЭлементКлавиша, Истина, Истина, Ложь));
		ПредставлениеСочетанияКлавиш(НоваяСтрока, Данные, "Ctrl_Alt_Shift", Новый СочетаниеКлавиш(ЭлементКлавиша, Истина, Истина, Истина));
		
	КонецЦикла;

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТаблицаСочетанийКлавишПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущийЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СочетаниеКлавиш = ВыбранноеСочетаниеКлавиш(Элемент.ТекущиеДанные, Элемент.ТекущийЭлемент);
	Представление = Представление(СочетаниеКлавиш);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСочетанийКлавишПриАктивизацииПоля(Элемент)
	
	СочетаниеКлавиш = ВыбранноеСочетаниеКлавиш(Элемент.ТекущиеДанные, Элемент.ТекущийЭлемент);
	Представление = Представление(СочетаниеКлавиш);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСочетанийКлавишВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = ТаблицаСочетанийКлавиш.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Закрыть(ВыбранноеСочетаниеКлавиш(ТекущиеДанные, Поле));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Закрыть(СочетаниеКлавиш);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда) Экспорт
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаКлиентеНаСервереБезКонтекста
Функция ЗаполнитьСтруктуруСочетаний(Клавиша, ЭтоAlt, ЭтоCtrl, ЭтоShift)

	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Клавиша", Клавиша);
	СтруктураВозврата.Вставить("Alt",     ЭтоAlt);
	СтруктураВозврата.Вставить("Ctrl",    ЭтоCtrl);
	СтруктураВозврата.Вставить("Shift",   ЭтоShift);
	
	Возврат СтруктураВозврата;

КонецФункции // ЗаполнитьСтруктуруСочетаний()

&НаКлиенте
Функция ВыбранноеСочетаниеКлавиш(ТекущиеДанные, Поле)
	
	Массив = Новый Массив;
	Массив.Добавить(ЗаполнитьСтруктуруСочетаний(Элементы.ТаблицаСочетанийКлавишКлавиша,        Ложь,   Ложь,   Ложь));
	Массив.Добавить(ЗаполнитьСтруктуруСочетаний(Элементы.ТаблицаСочетанийКлавишAlt,            Истина, Ложь,   Ложь));
	Массив.Добавить(ЗаполнитьСтруктуруСочетаний(Элементы.ТаблицаСочетанийКлавишCtrl,           Ложь,   Истина, Ложь));
	Массив.Добавить(ЗаполнитьСтруктуруСочетаний(Элементы.ТаблицаСочетанийКлавишShift,          Ложь,   Ложь,   Истина));
	Массив.Добавить(ЗаполнитьСтруктуруСочетаний(Элементы.ТаблицаСочетанийКлавишAlt_Shift,      Истина, Ложь,   Истина));
	Массив.Добавить(ЗаполнитьСтруктуруСочетаний(Элементы.ТаблицаСочетанийКлавишCtrl_Alt,       Истина, Истина, Ложь));
	Массив.Добавить(ЗаполнитьСтруктуруСочетаний(Элементы.ТаблицаСочетанийКлавишCtrl_Shift,     Ложь,   Истина, Истина));
	Массив.Добавить(ЗаполнитьСтруктуруСочетаний(Элементы.ТаблицаСочетанийКлавишCtrl_Alt_Shift, Истина, Истина, Истина));
	
	Массив.Добавить(ЗаполнитьСтруктуруСочетаний(Элементы.ТаблицаСочетанийКлавишКлавиша_,        Ложь,   Ложь,   Ложь));
	Массив.Добавить(ЗаполнитьСтруктуруСочетаний(Элементы.ТаблицаСочетанийКлавишAlt_,            Истина, Ложь,   Ложь));
	Массив.Добавить(ЗаполнитьСтруктуруСочетаний(Элементы.ТаблицаСочетанийКлавишCtrl_,           Ложь,   Истина, Ложь));
	Массив.Добавить(ЗаполнитьСтруктуруСочетаний(Элементы.ТаблицаСочетанийКлавишShift_,          Ложь,   Ложь,   Истина));
	Массив.Добавить(ЗаполнитьСтруктуруСочетаний(Элементы.ТаблицаСочетанийКлавишAlt_Shift_,      Истина, Ложь,   Истина));
	Массив.Добавить(ЗаполнитьСтруктуруСочетаний(Элементы.ТаблицаСочетанийКлавишCtrl_Alt_,       Истина, Истина, Ложь));
	Массив.Добавить(ЗаполнитьСтруктуруСочетаний(Элементы.ТаблицаСочетанийКлавишCtrl_Shift_,     Ложь,   Истина, Истина));
	Массив.Добавить(ЗаполнитьСтруктуруСочетаний(Элементы.ТаблицаСочетанийКлавишCtrl_Alt_Shift_, Истина, Истина, Истина));
	
	Для Каждого ЭлементМассива Из Массив Цикл
		
		Если ЭлементМассива.Клавиша = Поле Тогда
			
			ИмяКлавиши = ТекущиеДанные.ИмяКлавиши;
			Возврат ЗаполнитьСтруктуруСочетаний(ИмяКлавиши, ЭлементМассива.Alt, ЭлементМассива.Ctrl, ЭлементМассива.Shift);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервереБезКонтекста
Функция Представление(СочетаниеКлавиш)
	
	Возврат ОбщегоНазначенияУТ.ПредставлениеСочетанияКлавиш(СочетаниеКлавиш, Истина);
	
КонецФункции

&НаСервере
Процедура ПредставлениеСочетанияКлавиш(НоваяСтрока, Данные, Имя, Сочетание)
	
	НоваяСтрока[Имя] = ОбщегоНазначенияУТ.ПредставлениеСочетанияКлавиш(Сочетание, Истина);
	
	СтруктураСочетаний = ЗаполнитьСтруктуруСочетаний(Строка(Сочетание.Клавиша), Сочетание.Alt, Сочетание.Ctrl, Сочетание.Shift);
	
	Если ЗначениеЗаполнено(Данные) Тогда
		НайденныеСтроки = Данные.КомандыФормы.НайтиСтроки(СтруктураСочетаний); // Массив из СтрокаТаблицыЗначений - см. Справочник.ГорячиеКлавиши.Форма.ФормаЭлемента.КомандыФормы
		Если НайденныеСтроки.Количество() > 0 Тогда
			НоваяСтрока[Имя+"_"] = НайденныеСтроки[0].Заголовок;
		КонецЕсли;
		
		НайденныеСтроки = Данные.БыстрыеТовары.НайтиСтроки(СтруктураСочетаний); // Массив из СтрокаТаблицыЗначений - см. Справочник.ГорячиеКлавиши.Форма.ФормаЭлемента.БыстрыеТовары
		Если НайденныеСтроки.Количество() > 0 Тогда
			НоваяСтрока[Имя+"_"] = НайденныеСтроки[0].Заголовок;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
