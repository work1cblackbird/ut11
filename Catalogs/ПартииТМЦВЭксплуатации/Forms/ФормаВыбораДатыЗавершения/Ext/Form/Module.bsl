
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	УстановитьСвойстваДинамическогоСписка();
	
	ОтборДата = Параметры.Период;
	
	ЗакрыватьПриВыборе = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборДатаПриИзменении(Элемент)
	
	Список.Параметры.УстановитьЗначениеПараметра("Период", НачалоМесяца(ОтборДата));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ЗавершитьВыбор();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗавершитьВыбор(Команда)
	
	ЗавершитьВыбор();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьСвойстваДинамическогоСписка()
	
	Список.Параметры.УстановитьЗначениеПараметра("Период", НачалоМесяца(Параметры.Период));
	Список.Параметры.УстановитьЗначениеПараметра("Организация", Параметры.Организация);
	Список.Параметры.УстановитьЗначениеПараметра("Номенклатура", Параметры.Номенклатура);
	Список.Параметры.УстановитьЗначениеПараметра("Характеристика", Параметры.Характеристика);
	Список.Параметры.УстановитьЗначениеПараметра("Серия", Параметры.Серия);
	Список.Параметры.УстановитьЗначениеПараметра("НаправлениеДеятельности", Параметры.НаправлениеДеятельности);

КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВыбор()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		ТекстПредупреждения = НСтр("ru = 'Необходимо выбрать дату завершения эксплуатации'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ОповеститьОВыборе(ТекущиеДанные.ДатаЗавершенияЭксплуатации);
	
КонецПроцедуры

#КонецОбласти
