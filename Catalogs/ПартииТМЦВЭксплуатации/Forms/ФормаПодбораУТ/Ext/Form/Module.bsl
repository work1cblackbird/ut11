
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьПараметрыФормы();
	УстановитьСвойстваДинамическогоСписка();
	УстановитьОтборы(ЭтотОбъект);
	
	НастроитьЗависимыеЭлементыФормыНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	УстановитьОтборы(ЭтотОбъект);
	УстановитьПараметрыСписка(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПодразделениеПриИзменении(Элемент)
	
	УстановитьОтборы(ЭтотОбъект);
	УстановитьПараметрыСписка(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборФизическоеЛицоПриИзменении(Элемент)
	
	УстановитьОтборы(ЭтотОбъект);
	УстановитьПараметрыСписка(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборДатаПриИзменении(Элемент)
	
	УстановитьПараметрыСписка(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ЗавершитьВыбор();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	ЗавершитьВыбор();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьЗависимыеЭлементыФормыНаСервере()

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьУчетЗатратПоНаправлениямДеятельности") Тогда
		Элементы.СписокНаправлениеДеятельности.Видимость = Ложь;
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры") Тогда
		Элементы.СписокХарактеристика.Видимость = Ложь;
	КонецЕсли;
	
	Если ПодборПоДаннымСтроки Тогда
		Элементы.ОтборФизическоеЛицо.ПодсказкаВвода = НСтр("ru = 'Не используется'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПараметрыФормы()
	
	Параметры.Свойство("Дата", ОтборДата);
	Параметры.Свойство("Организация", ОтборОрганизация);
	Параметры.Свойство("Подразделение", ОтборПодразделение);
	Параметры.Свойство("Номенклатура", ОтборНоменклатура);
	Параметры.Свойство("Характеристика", ОтборХарактеристика);
	Параметры.Свойство("ФизическоеЛицо", ОтборФизическоеЛицо);

	ПодборПоДаннымСтроки = 
		Параметры.Свойство("Номенклатура")
		ИЛИ Параметры.Свойство("Характеристика");
		
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваДинамическогоСписка()
	
	ОписаниеЗапроса = ТМЦВЭксплуатацииКлиентСервер.ОписаниеЗапросаДляВыбораПартииТМЦ_УТ(ПараметрыПодбора(ЭтотОбъект));
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СвойстваСписка.ТекстЗапроса = ОписаниеЗапроса.ТекстЗапроса;
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список, СвойстваСписка);
	
	УстановитьПараметрыСписка(ЭтотОбъект);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрыСписка(Форма, ОписаниеЗапроса = Неопределено)
	
	Список = Форма.Список; // ДинамическийСписок
	
	Если ОписаниеЗапроса = Неопределено Тогда
		ОписаниеЗапроса = ТМЦВЭксплуатацииКлиентСервер.ОписаниеЗапросаДляВыбораПартииТМЦ_УТ(ПараметрыПодбора(Форма));
	КонецЕсли;

	Для Каждого ОписаниеПараметра Из ОписаниеЗапроса.ПараметрыЗапроса Цикл
		Список.Параметры.УстановитьЗначениеПараметра(ОписаниеПараметра.Ключ, ОписаниеПараметра.Значение);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыПодбора(Форма)
	
	ПараметрыПодбора = ТМЦВЭксплуатацииКлиентСервер.ПараметрыПодбораТМЦВЭксплуатации();
	ПараметрыПодбора.Дата = Форма.ОтборДата;
	ПараметрыПодбора.Организация = Форма.ОтборОрганизация;
	ПараметрыПодбора.Подразделение = Форма.ОтборПодразделение;
	ПараметрыПодбора.ДляДинамическогоСписка = Истина;
	
	Если Форма.ПодборПоДаннымСтроки Тогда
		ПараметрыПодбора.Номенклатура = Форма.ОтборНоменклатура;
		ПараметрыПодбора.Характеристика = Форма.ОтборХарактеристика;
		ПараметрыПодбора.ФизическоеЛицо = Форма.ОтборФизическоеЛицо;
	КонецЕсли;
	
	Возврат ПараметрыПодбора;
		
КонецФункции

&НаКлиенте
Процедура ЗавершитьВыбор()
	
	Если ЗакрыватьПриВыборе Тогда
		
		КоллекцияСтрок = Новый Массив;
		
		ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
		
		Если ТекущаяСтрока <> Неопределено Тогда
			КоллекцияСтрок.Добавить(ТекущаяСтрока);
		КонецЕсли;
		
	Иначе
		КоллекцияСтрок = Элементы.Список.ВыделенныеСтроки;
	КонецЕсли;
	
	МассивРезультат = Новый Массив;
	Для Каждого ИдентификаторСтроки Из КоллекцияСтрок Цикл
		
		ДанныеСтроки = Элементы.Список.ДанныеСтроки(ИдентификаторСтроки);
		
		ВыбранноеЗначение = ТМЦВЭксплуатацииКлиентСервер.НовоеЗначениеВыбора();
		
		ЗаполнитьЗначенияСвойств(ВыбранноеЗначение, ДанныеСтроки);
		ВыбранноеЗначение.КоличествоУпаковок = ДанныеСтроки.Количество;
		
		МассивРезультат.Добавить(ВыбранноеЗначение);
	
	КонецЦикла;
	
	Если МассивРезультат.Количество() = 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'Необходимо выбрать ТМЦ в эксплуатации'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если ЗакрыватьПриВыборе Тогда
		ОповеститьОВыборе(МассивРезультат[0]);
	Иначе
		ОповеститьОВыборе(МассивРезультат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборы(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.Список, "Организация", Форма.ОтборОрганизация,,, ЗначениеЗаполнено(Форма.ОтборОрганизация));
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.Список, "Подразделение", Форма.ОтборПодразделение,,, ЗначениеЗаполнено(Форма.ОтборПодразделение));
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.Список, "ФизическоеЛицо", Форма.ОтборФизическоеЛицо,,, ЗначениеЗаполнено(Форма.ОтборФизическоеЛицо));
	
КонецПроцедуры

#КонецОбласти
