#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗаполнитьТаблицыФормы();
	УстановитьВидимостьДоступность();
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицыФормы()
	
	Если Объект.ВидКлассификатора = Перечисления.ВидыКлассификаторовЗЕРНО.ОКПД2 Тогда
		НаборЗаписей = РегистрыСведений.ВидыСельскохозяйственныхКультурЗЕРНО.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ОКПД2.Установить(Объект.Идентификатор);
		НаборЗаписей.Прочитать();
		ВидыСельскохозяйственныхКультур.Загрузить(НаборЗаписей.Выгрузить());
	ИначеЕсли Объект.ВидКлассификатора = Перечисления.ВидыКлассификаторовЗЕРНО.ТНВЭД Тогда
		НаборЗаписей = РегистрыСведений.ВидыСельскохозяйственныхКультурЗЕРНО.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.КодТНВЭД.Установить(Объект.Идентификатор);
		НаборЗаписей.Прочитать();
		ВидыСельскохозяйственныхКультур.Загрузить(НаборЗаписей.Выгрузить());
	ИначеЕсли Объект.ВидКлассификатора = Перечисления.ВидыКлассификаторовЗЕРНО.ПотребительскоеСвойство Тогда
		НаборЗаписей = РегистрыСведений.ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ПотребительскоеСвойство.Установить(Объект.Ссылка);
		НаборЗаписей.Прочитать();
		ДопустимыеЗначенияПотребительскихСвойств.Загрузить(НаборЗаписей.Выгрузить());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступность()
	
	Элементы.ДопустимыеЗначенияПотребительскихСвойств.Видимость = Ложь;
	Элементы.ВидыСельскохозяйственныхКультур.Видимость          = Ложь;
	
	Если Объект.ВидКлассификатора = Перечисления.ВидыКлассификаторовЗЕРНО.ОКПД2 Тогда
		Элементы.ВидыСельскохозяйственныхКультур.Видимость = Истина;
		Элементы.ВидПродукции.Видимость                    = Истина;
		Элементы.ВидыСельскохозяйственныхКультурОКПД2.Видимость             = Ложь;
		Элементы.ВидыСельскохозяйственныхКультурОКПД2Наименование.Видимость = Ложь;
		Элементы.ВидыСельскохозяйственныхКультурКодТНВЭД.Видимость          = Истина;
		Элементы.ВидыСельскохозяйственныхКультурТНВЭДНаименование.Видимость = Истина;
	ИначеЕсли Объект.ВидКлассификатора = Перечисления.ВидыКлассификаторовЗЕРНО.ТНВЭД Тогда
		Элементы.ВидыСельскохозяйственныхКультур.Видимость = Истина;
		Элементы.ВидПродукции.Видимость                    = Истина;
		Элементы.ВидыСельскохозяйственныхКультурОКПД2.Видимость             = Истина;
		Элементы.ВидыСельскохозяйственныхКультурОКПД2Наименование.Видимость = Истина;
		Элементы.ВидыСельскохозяйственныхКультурКодТНВЭД.Видимость          = Ложь;
		Элементы.ВидыСельскохозяйственныхКультурТНВЭДНаименование.Видимость = Ложь;
	Иначе
		Элементы.ВидПродукции.Видимость = Ложь;
	КонецЕсли;
	
	Если Объект.ВидКлассификатора = Перечисления.ВидыКлассификаторовЗЕРНО.ПотребительскоеСвойство Тогда
		Элементы.ЕдиницаИзмерения.Видимость = Истина;
		Элементы.ДопустимыеЗначенияПотребительскихСвойств.Видимость = Истина;
	Иначе
		Элементы.ЕдиницаИзмерения.Видимость = Ложь;
	КонецЕсли;
	
	Если Объект.ВидКлассификатора = Перечисления.ВидыКлассификаторовЗЕРНО.ЕдиницаИзмерения Тогда
		Элементы.УсловноеОбозначение.Видимость = Истина;
	Иначе
		Элементы.УсловноеОбозначение.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
