#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция ДанныеГруппДоступа(ГруппыДоступа) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ГруппыДоступа", ГруппыДоступа);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГруппыДоступа.Ссылка КАК Ссылка,
		|	ГруппыДоступа.Наименование КАК Наименование
		|ИЗ
		|	Справочник.ГруппыДоступаФизическихЛиц КАК ГруппыДоступа
		|ГДЕ
		|	ГруппыДоступа.Ссылка В (&ГруппыДоступа)";
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ГруппыДоступаПользователя(Пользователь = Неопределено) Экспорт
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.АвторизованныйПользователь();
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГруппыДоступа.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ГруппыДоступаФизическихЛиц КАК ГруппыДоступа";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не УправлениеДоступом.ПроизводительныйВариант() И Пользователь <> Пользователи.АвторизованныйПользователь() Тогда
		Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли;

	ГруппыДоступа = Новый Массив;

	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если УправлениеДоступом.ЧтениеРазрешено(Выборка.Ссылка, Пользователь) Тогда
			ГруппыДоступа.Добавить(Выборка.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ГруппыДоступа;
	
КонецФункции

#КонецОбласти

#КонецЕсли