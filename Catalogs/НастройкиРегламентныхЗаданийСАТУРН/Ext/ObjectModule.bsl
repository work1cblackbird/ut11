#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьХешСуммуПараметровНастройкиОбмена();
	ПроверитьДубли(Отказ);
	
	Если Не Отказ Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		Если ЗначениеЗаполнено(РегламентноеЗадание) Тогда
			Задание = РегламентныеЗаданияСервер.Задание(РегламентноеЗадание);
		Иначе
			Задание = Неопределено;
		КонецЕсли;
		
		Если Задание = Неопределено Тогда
			ПараметрыЗадания = Новый Структура;
			ПараметрыЗадания.Вставить("Метаданные",    Метаданные.РегламентныеЗадания.ОтправкаПолучениеДанныхСАТУРН);
			ПараметрыЗадания.Вставить("Использование", Ложь);
			ПараметрыЗадания.Вставить("Наименование",  Наименование);
			ПараметрыЗадания.Вставить("Ключ",          Строка(Новый УникальныйИдентификатор));
			
			Если ЗначениеЗаполнено(Ссылка) Тогда
				ЭтаСсылка = Ссылка;
			Иначе
				ЭтаСсылка = Справочники.НастройкиРегламентныхЗаданийСАТУРН.ПолучитьСсылку();
				УстановитьСсылкуНового(ЭтаСсылка);
			КонецЕсли;
			
			Параметры = Новый Массив;
			Параметры.Добавить(ЭтаСсылка);
			ПараметрыЗадания.Вставить("Параметры", Параметры);
			
			Задание = РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
			
			РегламентноеЗадание = РегламентныеЗаданияСервер.УникальныйИдентификатор(Задание);
		КонецЕсли;
		
		ДополнительныеСвойства.Вставить("Задание", Задание);
		
		ВидНастройкиОбменДанными = Перечисления.ВидыНастроекОбменаСАТУРН.ОбменДанными;
		Если ВидНастройкиОбмена = ВидНастройкиОбменДанными
			Или (Не Ссылка.Пустая()
				И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ВидНастройкиОбмена") = ВидНастройкиОбменДанными) Тогда
			ДополнительныеСвойства.Вставить("ОбновитьПовторноИспользуемыеЗначения", Истина);
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Отказ Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		Если ДополнительныеСвойства.Свойство("Задание") Тогда
			
			ПараметрыЗадания = Новый Структура;
			
			Если ДополнительныеСвойства.Свойство("Расписание") 
				И ТипЗнч(ДополнительныеСвойства.Расписание) = Тип("РасписаниеРегламентногоЗадания") Тогда
				ПараметрыЗадания.Вставить("Расписание", ДополнительныеСвойства.Расписание);
			КонецЕсли;
			
			Если ПометкаУдаления Тогда
				ПараметрыЗадания.Вставить("Использование", Ложь);
			ИначеЕсли ДополнительныеСвойства.Свойство("Использование") Тогда
				ПараметрыЗадания.Вставить("Использование", ДополнительныеСвойства.Использование);
			КонецЕсли;
			
			ПараметрыЗадания.Вставить("Наименование", Наименование);
			
			Параметры = Новый Массив;
			Параметры.Добавить(Ссылка);
			ПараметрыЗадания.Вставить("Параметры", Параметры);
			
			УстановитьПривилегированныйРежим(Истина);
			РегламентныеЗаданияСервер.ИзменитьЗадание(РегламентноеЗадание, ПараметрыЗадания);
			
		КонецЕсли;
		
		Если ДополнительныеСвойства.Свойство("ОбновитьПовторноИспользуемыеЗначения") Тогда
			ОбновитьПовторноИспользуемыеЗначения();
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Отказ И ЗначениеЗаполнено(РегламентноеЗадание) Тогда
		УстановитьПривилегированныйРежим(Истина);
		РегламентныеЗаданияСервер.УдалитьЗадание(РегламентноеЗадание);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	РегламентноеЗадание = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьХешСуммуПараметровНастройкиОбмена()
	
	ХешСуммаПараметровНастроекОбмена = Справочники.НастройкиРегламентныхЗаданийСАТУРН.ХешСуммаПараметровНастройкиОбмена(
		ВидНастройкиОбмена, ПараметрыНастройкиОбмена.Получить());
	
КонецПроцедуры

Процедура ПроверитьДубли(Отказ)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",                           Ссылка);
	Запрос.УстановитьПараметр("ОрганизацияСАТУРН",                ОрганизацияСАТУРН);
	Запрос.УстановитьПараметр("ВидНастройкиОбмена",               ВидНастройкиОбмена);
	Запрос.УстановитьПараметр("ХешСуммаПараметровНастроекОбмена", ХешСуммаПараметровНастроекОбмена);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПРЕДСТАВЛЕНИЕ(НастройкиОбменаСАТУРН.Ссылка) КАК НастройкаОбменаПредставление,
	|	ПРЕДСТАВЛЕНИЕ(НастройкиОбменаСАТУРН.ОрганизацияСАТУРН) КАК ОрганизацияПредставление,
	|	ПРЕДСТАВЛЕНИЕ(НастройкиОбменаСАТУРН.ВидНастройкиОбмена) КАК ВидНастройкиОбменаПредставление
	|ИЗ
	|	Справочник.НастройкиРегламентныхЗаданийСАТУРН КАК НастройкиОбменаСАТУРН
	|ГДЕ
	|	НастройкиОбменаСАТУРН.Ссылка <> &Ссылка
	|	И НЕ НастройкиОбменаСАТУРН.ПометкаУдаления
	|	И НастройкиОбменаСАТУРН.ОрганизацияСАТУРН = &ОрганизацияСАТУРН
	|	И НастройкиОбменаСАТУРН.ВидНастройкиОбмена = &ВидНастройкиОбмена
	|	И НастройкиОбменаСАТУРН.ХешСуммаПараметровНастроекОбмена = &ХешСуммаПараметровНастроекОбмена";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(НСтр
			("ru = 'По организации %1, вид настройки %2, уже существует настройка обмена %3'"),
			Выборка.ОрганизацияПредставление,
			Выборка.ВидНастройкиОбменаПредставление,
			Выборка.НастройкаОбменаПредставление),,,,
			Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли