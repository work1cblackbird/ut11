#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает текст запроса информация о сопоставлении номенклатуры
// 
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаИнформацияОСопоставлении() Экспорт
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка КАК АлкогольнаяПродукция
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
	|ГДЕ
	|	КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка В(&АлкогольнаяПродукция)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура КАК Номенклатура,
	|	СоответствиеНоменклатурыЕГАИС.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ СопоставленныеПозиции
	|ИЗ
	|	Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО (СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция = ТабличнаяЧасть.АлкогольнаяПродукция)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.АлкогольнаяПродукция,
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура,
	|	СоответствиеНоменклатурыЕГАИС.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(1) КАК Количество
	|ПОМЕСТИТЬ СопоставленоПозиций
	|ИЗ
	|	СопоставленныеПозиции КАК ТабличнаяЧасть
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.АлкогольнаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Ссылка                                     КАК Ссылка,
	|	ЕСТЬNULL(СопоставленоПозиций.Количество, 0)  КАК Количество,
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура   КАК Номенклатура,
	|	СоответствиеНоменклатурыЕГАИС.Характеристика КАК Характеристика,
	|	ПРЕДСТАВЛЕНИЕ(СоответствиеНоменклатурыЕГАИС.Номенклатура) КАК НоменклатураНаименование
	|ИЗ
	|	Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленоПозиций КАК СопоставленоПозиций
	|		ПО (СопоставленоПозиций.АлкогольнаяПродукция = Т.Ссылка)
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО (Т.Ссылка = СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция)
	|		И (СопоставленоПозиций.Количество = 1 ИЛИ СоответствиеНоменклатурыЕГАИС.Порядок = 1)
	|	
	|ГДЕ
	|	Т.Ссылка В(&АлкогольнаяПродукция)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

//@skip-check module-accessibility-at-client
Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	ИнтеграцияЕГАИСВызовСервера.ПриПолученииФормыСправочника(
		"КлассификаторАлкогольнойПродукцииЕГАИС",
		ВидФормы,
		Параметры,
		ВыбраннаяФорма,
		ДополнительнаяИнформация,
		СтандартнаяОбработка);
	
КонецПроцедуры

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ТолькоМаркируемая") ИЛИ Параметры.Свойство("ТолькоНемаркируемая") Тогда
		
		Если ЗначениеЗаполнено(Параметры.СтрокаПоиска) Тогда
			СтандартнаяОбработка = Ложь;
		
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 50
			|	КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка КАК Ссылка,
			|	ПРЕДСТАВЛЕНИЕ(КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка) КАК АлкогольнаяПродукцияСтрока,
			|	КлассификаторАлкогольнойПродукцииЕГАИС.ПометкаУдаления КАК Пометка
			|ИЗ
			|	Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
			|ГДЕ
			|	(КлассификаторАлкогольнойПродукцииЕГАИС.Наименование ПОДОБНО (&Наименование)
			|		ИЛИ КлассификаторАлкогольнойПродукцииЕГАИС.Код ПОДОБНО (&СтрокаПоиска))
			|	И КлассификаторАлкогольнойПродукцииЕГАИС.ВидПродукции.Маркируемый = &Маркируемый
			|УПОРЯДОЧИТЬ ПО Пометка";
			Запрос.УстановитьПараметр("Наименование", "%"+Параметры.СтрокаПоиска+"%");
			Запрос.УстановитьПараметр("СтрокаПоиска", Параметры.СтрокаПоиска+"%");
			Запрос.УстановитьПараметр("Маркируемый",  Параметры.Свойство("ТолькоМаркируемая"));
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать();
		ДанныеВыбора = Новый СписокЗначений;
		Пока Выборка.Следующий() Цикл
			Элемент = ДанныеВыбора.Добавить();
			Элемент.Значение = Выборка.Ссылка;
			Элемент.Представление = Выборка.АлкогольнаяПродукцияСтрока;
			Элемент.Пометка = Выборка.Пометка;
		КонецЦикла;
		
	ИначеЕсли Параметры.Свойство("ИмпортПроизводство") Тогда
		
		Если ЗначениеЗаполнено(Параметры.СтрокаПоиска) Тогда
			СтандартнаяОбработка = Ложь;
		
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 50
			|	КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка КАК Ссылка,
			|	ПРЕДСТАВЛЕНИЕ(КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка) КАК АлкогольнаяПродукцияСтрока,
			|	КлассификаторАлкогольнойПродукцииЕГАИС.ПометкаУдаления КАК Пометка
			|ИЗ
			|	Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
			|ГДЕ
			|	(КлассификаторАлкогольнойПродукцииЕГАИС.Наименование ПОДОБНО (&Наименование)
			|		ИЛИ КлассификаторАлкогольнойПродукцииЕГАИС.Код ПОДОБНО (&СтрокаПоиска))
			|	И ВЫБОР
			|		КОГДА КлассификаторАлкогольнойПродукцииЕГАИС.Производитель.ТипОрганизации В(
			|			ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЕГАИС.ИндивидуальныйПредпринимательРФ),
			|			ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЕГАИС.ЮридическоеЛицоРФ))
			|		ТОГДА ИСТИНА
			|		КОГДА КлассификаторАлкогольнойПродукцииЕГАИС.Производитель.ТипОрганизации В(
			|			ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЕГАИС.КонтрагентТаможенногоСоюза),
			|			ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЕГАИС.ИностранныйКонтрагент))
			|		ТОГДА ЛОЖЬ
			|	ИНАЧЕ Неопределено КОНЕЦ = &ИмпортПроизводство
			|УПОРЯДОЧИТЬ ПО Пометка";
			Запрос.УстановитьПараметр("Наименование", "%"+Параметры.СтрокаПоиска+"%");
			Запрос.УстановитьПараметр("СтрокаПоиска", Параметры.СтрокаПоиска+"%");
			Запрос.УстановитьПараметр("ИмпортПроизводство",  Параметры.ИмпортПроизводство);
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать();
		ДанныеВыбора = Новый СписокЗначений;
		Пока Выборка.Следующий() Цикл
			Элемент = ДанныеВыбора.Добавить();
			Элемент.Значение = Выборка.Ссылка;
			Элемент.Представление = Выборка.АлкогольнаяПродукцияСтрока;
			Элемент.Пометка = Выборка.Пометка;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли

#КонецОбласти

