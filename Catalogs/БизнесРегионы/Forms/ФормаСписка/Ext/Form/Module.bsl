#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Элементы.ФормаСоздатьБизнесРегионы.Видимость = ПравоДоступа("Изменение", Метаданные.Справочники.БизнесРегионы);
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.БизнесРегионы);
	Элементы.ФормаИзменитьВыделенные.Видимость = МожноРедактировать;
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = МожноРедактировать;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьБизнесРегионы(Команда)
	
	Если Не ГеографическаяСхемаДляОтчетовЗаполнена() Тогда
		ТекстПредупреждения = НСтр("ru = 'Не выбрана карта в разделе администрирования ""CRM и продажи"". Географические регионы не могут быть заполнены.'");
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
	Иначе
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбновитьСписокБизнесРегионов", ЭтотОбъект);
		ОткрытьФорму("Справочник.БизнесРегионы.Форма.ФормаГенерацииБизнесРегионов",, ЭтаФорма,,,, ОписаниеОповещения,);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьСписокБизнесРегионов(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Число") Тогда
		Если РезультатЗакрытия Тогда
			Элементы.Список.Обновить();
			ТекстОповещенияПользователя = НСтр("ru = 'Записано новых бизнес-регионов: %1'");
			ТекстОповещенияПользователя = 
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОповещенияПользователя, РезультатЗакрытия);
		Иначе
			ТекстОповещенияПользователя = НСтр("ru = 'Все выбранные регионы найдены.'");
		КонецЕсли;
		ПоказатьОповещениеПользователя(ТекстОповещенияПользователя);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ГеографическаяСхемаДляОтчетовЗаполнена()
	
	Возврат ЗначениеЗаполнено(Константы.ГеографическаяСхемаДляОтчетов.Получить().Получить());
	
КонецФункции

#КонецОбласти

