#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Не ЭтоНовый() И Не ПометкаУдаления Тогда
		СоздатьАналитикуКлюча();
		Наименование = РегистрыСведений.АналитикаУчетаПоПартнерам.НаименованиеКлючаАналитики(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	УдалитьАналитикуКлюча();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

Процедура УдалитьАналитикуКлюча()
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	РегистрСведений.АналитикаУчетаПоПартнерам КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.КлючАналитики = &Ключ
	|");
	Запрос.УстановитьПараметр("Ключ", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.АналитикаУчетаПоПартнерам.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.Удалить();
	КонецЦикла;
	
КонецПроцедуры

Процедура СоздатьАналитикуКлюча()
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Ключи.Ссылка КАК Ссылка,
	|	Ключи.Партнер,
	|	Ключи.Организация,
	|	Ключи.Контрагент,
	|	Ключи.Договор,
	|	Ключи.НаправлениеДеятельности
	|ИЗ
	|	Справочник.КлючиАналитикиУчетаПоПартнерам КАК Ключи
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|		ПО Аналитика.КлючАналитики = Ключи.Ссылка
	|ГДЕ
	|	Ключи.Ссылка = &Ссылка
	|	И Аналитика.КлючАналитики ЕСТЬ NULL
	|");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		МенеджерЗаписи = РегистрыСведений.АналитикаУчетаПоПартнерам.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.КлючАналитики = Выборка.Ссылка;
		МенеджерЗаписи.Записать(Истина);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
