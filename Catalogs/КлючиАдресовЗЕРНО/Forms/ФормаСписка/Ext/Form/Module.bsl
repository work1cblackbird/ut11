#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ЗаголовокФормы") Тогда
		
		Заголовок     = Параметры.ЗаголовокФормы;
		АвтоЗаголовок = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	МассивКлючейАдресов = Строки.ПолучитьКлючи();
	СопоставлениеКлючей = Новый Соответствие();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КлючиАдресовЗЕРНООператорыАдреса.Ссылка          КАК КлючАдреса,
		|	КлючиАдресовЗЕРНООператорыАдреса.СкладКонтрагент КАК СкладКонтрагент
		|ИЗ
		|	Справочник.КлючиАдресовЗЕРНО.ОператорыАдреса КАК КлючиАдресовЗЕРНООператорыАдреса
		|ГДЕ
		|	КлючиАдресовЗЕРНООператорыАдреса.Ссылка В (&МассивСсылок)
		|ИТОГИ
		|ПО
		|	КлючАдреса";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивКлючейАдресов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаКлючАдреса = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаКлючАдреса.Следующий() Цикл
		
		МассивСопоставленийПоКлючу = Новый Массив;
		ВыборкаДетальныеЗаписи = ВыборкаКлючАдреса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл	
			МассивСопоставленийПоКлючу.Добавить(ВыборкаДетальныеЗаписи.СкладКонтрагент);
		КонецЦикла;
		
		СопоставлениеКлючей.Вставить(ВыборкаКлючАдреса.КлючАдреса, МассивСопоставленийПоКлючу);
		
	КонецЦикла;
	
	Для Каждого СтрокаДанных Из Строки Цикл
		
		ДанныеСтроки        = СтрокаДанных.Значение.Данные;
		ДанныеСопоставлений = СопоставлениеКлючей.Получить(СтрокаДанных.Ключ);
		
		Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеСтроки, "Сопоставление") Тогда
			Прервать;
		КонецЕсли;
		
		Если ДанныеСопоставлений = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ДанныеСопоставлений.Количество() = 1 Тогда
			ПредставлениеСопоставления = ДанныеСопоставлений[0];
		Иначе
			
			ШаблонСтроки = НСтр("ru = '%1 ( + еще %2 )'");
			ПредставлениеСопоставления = СтрШаблон(ШаблонСтроки, Строка(ДанныеСопоставлений[0]), ДанныеСопоставлений.Количество() - 1);
			
		КонецЕсли;
		
		ДанныеСтроки.Сопоставление = ПредставлениеСопоставления;
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти