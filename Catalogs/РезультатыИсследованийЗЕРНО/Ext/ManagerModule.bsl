#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Представление =  СтрШаблон("%1, %2, %3",
		Данные.Идентификатор,
		Данные.ОКПД2,
		Данные.ЦельИспользования);
	Если Данные.ТребуетсяЗагрузка Тогда
		Представление = СтрШаблон(НСтр("ru = '(не загружено) %1'"), Представление);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	Поля.Добавить("Идентификатор");
	Поля.Добавить("ОКПД2");
	
	Поля.Добавить("ЦельИспользования");
	Поля.Добавить("ТребуетсяЗагрузка");
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

#Область Отчеты

// Заполняет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//   Параметры - Структура - Вспомогательные параметры. См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры.
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ИнтеграцияИСПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	ИнтеграцияИСПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Сформировать печатные формы объектов.
//
// Параметры:
//   МассивОбъектов        - Массив из Произвольный         - ссылки на объекты, которые нужно распечатать
//   ПараметрыПечати       - Структура                      - дополнительные настройки печати
//   КоллекцияПечатныхФорм - ТаблицаЗначений                - сформированные табличные документы (выходной параметр)
//   ОбъектыПечати         - СписокЗначений из Произвольный - имя области, в которой был выведен объект (выходной параметр)
//   ПараметрыВывода       - Структура                      - дополнительные параметры сформированных табличных документов (выходной параметр)
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	УправлениеДоступомИСПереопределяемый.ПриЗаполненииОграниченияДоступа(
		Метаданные.Справочники.РезультатыИсследованийЗЕРНО, Ограничение);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработкаЗагрузкиПолученныхДанных(ЭлементОчереди, ПараметрыОбмена, ПолученныеДанные, ИзмененныеОбъекты) Экспорт
	
	Если ЭлементОчереди.Операция = Перечисления.ВидыОперацийЗЕРНО.ЗапросРезультатовИсследований Тогда
		
		РеквизитыИсходящегоСообщения = ЭлементОчереди.РеквизитыИсходящегоСообщения;
		ПараметрыЗапроса             = ЭлементОчереди.РеквизитыИсходящегоСообщения.ПараметрыЗапроса;
		ДанныеДляОбработки           = ИнтеграцияЗЕРНОСлужебный.МассивДанныхПоляXDTO(ПолученныеДанные.Record);
		
		Блокировка = Новый БлокировкаДанных();
		
		Для Каждого КлючИЗначение Из ПараметрыОбмена.ПараметрыПреобразования.БлокируемыеОбъекты Цикл
			
			ЭлементБлокировки = Блокировка.Добавить(КлючИЗначение.Ключ);
			БлокируемыеДанные = КлючИЗначение.Значение;
			
			Если БлокируемыеДанные <> Неопределено Тогда
				Для Каждого Колонка Из БлокируемыеДанные.Колонки Цикл
					ЭлементБлокировки.ИспользоватьИзИсточникаДанных(Колонка.Имя, Колонка.Имя);
				КонецЦикла;
				ЭлементБлокировки.ИсточникДанных = БлокируемыеДанные;
			КонецЕсли;
			
		КонецЦикла;
	
		Попытка
			
			Блокировка.Заблокировать();
			УстановитьПривилегированныйРежим(Истина);
			
			ИнтеграцияЗЕРНОСлужебный.СсылкиПоИдентификаторам(ПараметрыОбмена);
			
			Для Каждого СтрокаДанных Из ДанныеДляОбработки Цикл
				СсылкаНаОбъект = ЗагрузитьРезультатИсследований(
					СтрокаДанных,
					ЭлементОчереди.Организация,
					ЭлементОчереди.Подразделение,
					ПараметрыОбмена);
				Если ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
					ИзмененныеОбъекты.Добавить(СсылкаНаОбъект);
				КонецЕсли;
			КонецЦикла;
			
			Если ПолученныеДанные.hasMore Тогда
				
				СообщенияXML = Новый Массив();
				
				НовыеПараметрыФормирования = ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыЗапроса);
				НовыеПараметрыФормирования.НомерСтраницы = НовыеПараметрыФормирования.НомерСтраницы + 1;
				Если Не ЗначениеЗаполнено(НовыеПараметрыФормирования.Организация) Тогда
					НовыеПараметрыФормирования.Организация = ЭлементОчереди.Организация;
				КонецЕсли;
				СообщениеXML = СообщениеЗагрузкиРезультатовИсследований(НовыеПараметрыФормирования, ПараметрыОбмена);
				СообщениеXML.ЗагружатьДо = РеквизитыИсходящегоСообщения.ЗагружатьДо;
				ИнтеграцияЗЕРНОСлужебный.УстановитьСообщениеОснование(СообщениеXML, РеквизитыИсходящегоСообщения);
				
				СообщенияXML.Добавить(СообщениеXML);
			
				ИнтеграцияЗЕРНОСлужебный.ПодготовитьКПередачеИсходныеСообщения(СообщенияXML, ПараметрыОбмена);
				
			КонецЕсли;
			
			ИнтеграцияЗЕРНОСлужебный.УстановитьДатуВыполненияСинхронизации(ПараметрыОбмена, ПолученныеДанные, ЭлементОчереди);
			
		Исключение
			ВызватьИсключение;
		КонецПопытки;
	
	КонецЕсли;
	
КонецПроцедуры

// Сообщение к передаче XML
//
// Параметры:
//  СсылкаНаОбъект          - СправочникСсылка.РезультатыИсследованийЗЕРНО - Ссылка на объект.
//  ДальнейшееДействие      - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЗЕРНО - дальнейшее действие.
//  ДополнительныеПараметры - Структура, Неопределено - Дополнительные параметры.
// Возвращаемое значение:
//  Массив из см. ИнтеграцияЗЕРНОСлужебный.СтруктураСообщенияXML - Сообщения к передаче.
//
Функция СообщениеКПередачеXML(СсылкаНаОбъект, ДальнейшееДействие, ДополнительныеПараметры) Экспорт
		
	Если ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЗЕРНО.ПередайтеДанные Тогда
		Возврат СообщенияЗагрузкиРезультатовИсследований(СсылкаНаОбъект, ДополнительныеПараметры);
	КонецЕсли;
	
КонецФункции

// Сообщения загрузки результата исследований
//
// Параметры:
//  СсылкаНаОбъект          - ОпределяемыйТип.ОбъектыСинхронизацииЗЕРНО - Ссылка на объект.
//  ДополнительныеПараметры - Структура, Неопределено - Дополнительные параметры.
// Возвращаемое значение:
//  Массив из см. ИнтеграцияЗЕРНОСлужебный.СтруктураСообщенияXML
//
Функция СообщенияЗагрузкиРезультатовИсследований(СсылкаНаОбъект, ДополнительныеПараметры)
	
	СообщенияXML = Новый Массив();
	
	ПараметрыФормирования = ПараметрыФормированияСообщенияXML();
	ПараметрыФормирования.Организация      = ДополнительныеПараметры.ПараметрыОбработкиДокумента.Организация;
	ПараметрыФормирования.ПараметрыЗапроса = ДополнительныеПараметры.ПараметрыОбработкиДокумента.ПараметрыЗапроса;
	
	АбстрактноеСообщениеXML = ИнтеграцияЗЕРНОСлужебный.СтруктураСообщенияXML();
	АбстрактноеСообщениеXML.Операция            = Перечисления.ВидыОперацийЗЕРНО.ЗапросРезультатовИсследованийАбстрактнаяОперация;
	АбстрактноеСообщениеXML.ТипСообщения        = Перечисления.ТипыЗапросовИС.Исходящий;
	АбстрактноеСообщениеXML.Версия              = ПараметрыФормирования.НомерВерсии;
	АбстрактноеСообщениеXML.ПараметрыЗапроса    = ПараметрыФормирования;
	АбстрактноеСообщениеXML.Организация         = ПараметрыФормирования.Организация;
	АбстрактноеСообщениеXML.Подразделение       = ПараметрыФормирования.Подразделение;
	АбстрактноеСообщениеXML.СсылкаНаОбъект      = ПустаяСсылка();
	АбстрактноеСообщениеXML.ТребуетсяПодписание = Ложь;
	СообщенияXML.Добавить(АбстрактноеСообщениеXML);
	
	НовыеПараметрыФормирования = ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыФормирования);
	СообщениеXML = СообщениеЗагрузкиРезультатовИсследований(
		НовыеПараметрыФормирования, ДополнительныеПараметры.ПараметрыОбмена);
	СообщениеXML.ЗагружатьДо = АбстрактноеСообщениеXML.Идентификатор;
	ИнтеграцияЗЕРНОСлужебный.УстановитьСообщениеОснование(СообщениеXML, АбстрактноеСообщениеXML);
	АбстрактноеСообщениеXML.ДополнительноеОписание = СообщениеXML.ДополнительноеОписание;
	
	СообщенияXML.Добавить(СообщениеXML);
	
	Возврат СообщенияXML;
	
КонецФункции

Функция ПараметрыФормированияСообщенияXML()
	
	ВозвращаемоеЗначение = ИнтеграцияЗЕРНОСлужебный.ПараметрыФормированияСообщенияXML();
	
	ВозвращаемоеЗначение.КоличествоНаСтранице = ИнтеграцияЗЕРНО.ПараметрыОптимизации().КоличествоЭлементовСтраницыОтвета;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Сообщение загрузки результата исследований
//
// Параметры:
//  ПараметрыФормирования - см. ПараметрыФормированияСообщенияXML
//  ПараметрыОбмена - см. ИнтеграцияЗЕРНО.ПараметрыОбмена
// Возвращаемое значение:
//  см. ИнтеграцияЗЕРНОСлужебный.СтруктураСообщенияXML
//
Функция СообщениеЗагрузкиРезультатовИсследований(ПараметрыФормирования, ПараметрыОбмена)
	
	ИмяСвойстваЗапроса = "RequestGetListResearch";
	ПространствоИмен   = ИнтеграцияЗЕРНОСлужебный.ПространствоИменПоИмениПакетаXDTO("zerno_api_grainmonitor", ПараметрыОбмена.ПараметрыОптимизации);
	Операция           = Перечисления.ВидыОперацийЗЕРНО.ЗапросРезультатовИсследований;
	ПараметрыЗапроса   = ПараметрыФормирования.ПараметрыЗапроса;
	
	ХранилищеВременныхДат = Новый Соответствие;
	
	ТелоXDTO = РаботаСXMLИС.ОбъектXDTOПоИмениСвойства(ПространствоИмен, ИмяСвойстваЗапроса);
	
	СообщениеXML = ИнтеграцияЗЕРНОСлужебный.СтруктураСообщенияXML();
	СообщениеXML.Операция         = Операция;
	СообщениеXML.ТипСообщения     = Перечисления.ТипыЗапросовИС.Исходящий;
	СообщениеXML.Версия           = 1;
	СообщениеXML.Операция         = Операция;
	СообщениеXML.ПараметрыЗапроса = Новый Структура();
	СообщениеXML.СсылкаНаОбъект   = ПустаяСсылка();
	СообщениеXML.Организация      = ПараметрыФормирования.Организация;
	СообщениеXML.Подразделение    = ПараметрыФормирования.Подразделение;
	
	ДанныеОписания = Новый Массив();
	Если ПараметрыЗапроса <> Неопределено Тогда
		
		Если ПараметрыЗапроса.Свойство("Интервал")
			И ПараметрыЗапроса.Интервал <> Неопределено Тогда
			Интервал = ПараметрыЗапроса.Интервал;
			Если ЗначениеЗаполнено(Интервал.НачалоПериода) Тогда
				ИнтеграцияЗЕРНОСлужебный.ЗаполнитьСвойствоXDTO(ТелоXDTO, "dateFrom", Интервал.НачалоПериода, СообщениеXML);
				ДанныеОписания.Добавить(
					СтрШаблон(
						НСтр("ru = 'с %1'"),
						Формат(Интервал.НачалоПериода, "ДФ=dd.MM.yyyy;")));
			КонецЕсли;
			Если ЗначениеЗаполнено(Интервал.КонецПериода) Тогда
				ИнтеграцияЗЕРНОСлужебный.ЗаполнитьСвойствоXDTO(ТелоXDTO, "dateTo", Интервал.КонецПериода, СообщениеXML);
				ДанныеОписания.Добавить(
					СтрШаблон(
						НСтр("ru = 'по %1'"),
						Формат(Интервал.КонецПериода, "ДФ=dd.MM.yyyy;")));
			КонецЕсли;
		КонецЕсли;
		Если ПараметрыЗапроса.Свойство("НачалоПериодаИзменений")
		И ПараметрыЗапроса.НачалоПериодаИзменений <> Неопределено Тогда
			ИнтеграцияИС.УстановитьДатуСЧасовымПоясом(
				ТелоXDTO, "recordsModifiedFrom",
				ПараметрыЗапроса.НачалоПериодаИзменений, ХранилищеВременныхДат);
			ДанныеОписания.Добавить(
				СтрШаблон(
					НСтр("ru = 'измененные с %1'"),
					Формат(ПараметрыЗапроса.НачалоПериодаИзменений, "ДФ=dd.MM.yyyy;")));
		КонецЕсли;
		
		Если ПараметрыЗапроса.Свойство("НомерИсследования") Тогда
			ИнтеграцияЗЕРНОСлужебный.ЗаполнитьСвойствоXDTO(ТелоXDTO, "reserchNumber", ПараметрыЗапроса.НомерИсследования, СообщениеXML);
			ДанныеОписания.Добавить(СтрШаблон(НСтр("ru = '№ %1'"), ПараметрыЗапроса.НомерСДИЗ));
		КонецЕсли;
		Если ПараметрыЗапроса.Свойство("МестоФормированияПартии") Тогда
			ИнтеграцияЗЕРНОСлужебный.ЗаполнитьСвойствоXDTO(ТелоXDTO, "primaryStoragePlaceID", ПараметрыЗапроса.МестоФормированияПартии, СообщениеXML);
		КонецЕсли;
		Если ПараметрыЗапроса.Свойство("Статус") Тогда
			Статус = ИнтеграцияЗЕРНОСлужебный.СтатусРезультатаИсследований(ПараметрыЗапроса.Статус);
			ИнтеграцияЗЕРНОСлужебный.ЗаполнитьСвойствоXDTO(ТелоXDTO, "status", Статус, СообщениеXML);
			ДанныеОписания.Добавить(СтрШаблон(НСтр("ru = 'статус: %1'"), ПараметрыЗапроса.Статус));
		КонецЕсли;
		Если ПараметрыЗапроса.Свойство("ОКПД2") Тогда
			ИнтеграцияЗЕРНОСлужебный.ЗаполнитьСвойствоXDTO(ТелоXDTO, "OKPD2", ПараметрыЗапроса.ОКПД2, СообщениеXML);
		КонецЕсли;
		
	КонецЕсли;
	
	СообщениеXML.ДополнительноеОписание  = СтрСоединить(ДанныеОписания, " ");
	ИнтеграцияЗЕРНОСлужебный.ЗаполнитьДанныеПостраничногоПросмотра(
		ТелоXDTO,
		СообщениеXML,
		ПараметрыФормирования.НомерСтраницы,
		ПараметрыФормирования.КоличествоНаСтранице);
	СообщениеXML.ТекстСообщенияЗапросXML = ИнтеграцияЗЕРНОСлужебный.ОбъектXDTOВXML(
		ТелоXDTO, ПространствоИмен, ИмяСвойстваЗапроса,
		ПараметрыОбмена);
	СообщениеXML.ТекстСообщенияЗапросXML = ИнтеграцияИС.ПреобразоватьВременныеДаты(ХранилищеВременныхДат, СообщениеXML.ТекстСообщенияЗапросXML);
	
	Возврат СообщениеXML;
	
КонецФункции

#Область ПоискСсылок

Функция РезультатИсследований(Идентификатор, Организация, ПараметрыОбмена) Экспорт
	
	ИмяТаблицы = Метаданные.Справочники.РезультатыИсследованийЗЕРНО.ПолноеИмя();
	
	СправочникСсылка = ИнтеграцияЗЕРНОСлужебный.СсылкаПоИдентификатору(ПараметрыОбмена, ИмяТаблицы, Идентификатор);
	
	Если ЗначениеЗаполнено(СправочникСсылка) Тогда
		ИнтеграцияЗЕРНОСлужебный.ОбновитьСсылку(ПараметрыОбмена, ИмяТаблицы, Идентификатор, СправочникСсылка);
	Иначе
		
		Блокировка = Новый БлокировкаДанных();
		ЭлементБлокировки = Блокировка.Добавить(ИмяТаблицы);
		ЭлементБлокировки.УстановитьЗначение("Идентификатор", Идентификатор);
		
		ТранзакцияЗафиксирована = Истина;
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка.Заблокировать();
			
			СправочникСсылка = ИнтеграцияЗЕРНОСлужебный.СсылкаПоИдентификатору(ПараметрыОбмена, ИмяТаблицы, Идентификатор);
			
			Если Не ЗначениеЗаполнено(СправочникСсылка) Тогда
				СправочникСсылка = СоздатьРезультатИсследований(Идентификатор);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТранзакцияЗафиксирована = Ложь;
			
			ТекстОшибки = СтрШаблон(
				НСтр("ru = 'Ошибка при создании справочника %1 с идентификатором %2:
				           |%3'"),
				Метаданные.Справочники.РеестрМестФормированияПартийЗЕРНО.Синоним,
				Идентификатор,
				ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				
			ТекстОшибкиПодробно = СтрШаблон(
				НСтр("ru = 'Ошибка при создании справочника %1 с идентификатором %2:
				           |%3'"),
				Метаданные.Справочники.РезультатыИсследованийЗЕРНО.Синоним,
				Идентификатор,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОбщегоНазначенияИСВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
				ТекстОшибкиПодробно,
				НСтр("ru = 'Работа с результатами исследований'", ОбщегоНазначения.КодОсновногоЯзыка()));
			
			ВызватьИсключение ТекстОшибки;
			
		КонецПопытки;
		
		Если ТранзакцияЗафиксирована Тогда
			ИнтеграцияЗЕРНОСлужебный.ОбновитьСсылку(ПараметрыОбмена, ИмяТаблицы, Идентификатор, СправочникСсылка);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СправочникСсылка;
	
КонецФункции

Функция ЗагрузитьРезультатИсследований(ЭлементДанных, Организация, Подразделение, ПараметрыОбмена, СуществующийОбъект = Неопределено, ТребуетсяПоиск = Истина) Экспорт
	
	ЗаписьНового       = Ложь;
	МетаданныеЭлемента = Метаданные.Справочники.РезультатыИсследованийЗЕРНО;
	Идентификатор      = ЭлементДанных.reserchNumber;
	
	Если СуществующийОбъект = Неопределено Тогда
		
		СуществующийЭлемент = Неопределено;
		Если ТребуетсяПоиск Тогда
			СуществующийЭлемент = ИнтеграцияЗЕРНОСлужебный.СсылкаПоИдентификатору(
				ПараметрыОбмена,
				МетаданныеЭлемента.ПолноеИмя(),
				Идентификатор);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СуществующийЭлемент) Тогда
			СуществующийОбъект = СоздатьЭлемент();
			СуществующийОбъект.Идентификатор = Идентификатор;
			ЗаписьНового = Истина;
		Иначе
			СуществующийОбъект = СуществующийЭлемент.ПолучитьОбъект();
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗаписьНового Тогда
		СуществующийОбъект.Заблокировать();
	КонецЕсли;
	
	СуществующийОбъект.Товаропроизводитель = Справочники.КлючиРеквизитовОрганизацийЗЕРНО.КлючОрганизации(ЭлементДанных.Owner, ПараметрыОбмена);
	СуществующийОбъект.ОКПД2               = ЭлементДанных.Crop;
	СуществующийОбъект.МестоФормированияПартии = Справочники.РеестрМестФормированияПартийЗЕРНО.МестоФормированияПартии(
		Формат(ЭлементДанных.primaryStoragePlaceID, "ЧГ=0;"),
		Организация,
		ПараметрыОбмена);
	СуществующийОбъект.Лаборатория = Справочники.КлючиРеквизитовОрганизацийЗЕРНО.КлючОрганизации(
		ЭлементДанных.Laboratory, ПараметрыОбмена);
	СуществующийОбъект.ЦельИспользования = Справочники.КлассификаторНСИЗЕРНО.КлассификаторНСИ(
		Перечисления.ВидыКлассификаторовЗЕРНО.ЦельИспользованияПартии,
		ЭлементДанных.Target,
		Организация,
		Подразделение,
		ПараметрыОбмена);
	СуществующийОбъект.МестоОтбораПроб = Справочники.КлючиАдресовЗЕРНО.КлючАдреса(ЭлементДанных.PlaceOfChecking, ПараметрыОбмена);
	СуществующийОбъект.ПротоколИсследований = Справочники.КлючиРеквизитовПервичныхДокументовЗЕРНО.КлючПервичногоДокумента(
		ЭлементДанных, "Protocol", Организация, Подразделение, ПараметрыОбмена);
	
	Если ЭлементДанных.Свойство("samplesPickingID") Тогда
		СуществующийОбъект.АктОтбораПроб = Справочники.АктыОтбораПробЗЕРНО.АктОтбораПроб(
			Формат(ЭлементДанных.samplesPickingID, "ЧГ=0;"), Организация, Подразделение, ПараметрыОбмена);
	ИначеЕсли ЭлементДанных.Свойство("Check") Тогда
		СуществующийОбъект.АктОтбораПроб = Справочники.КлючиРеквизитовПервичныхДокументовЗЕРНО.КлючПервичногоДокумента(
			ЭлементДанных, "Check", Организация, Подразделение, ПараметрыОбмена);
	КонецЕсли;
	
	Если ЭлементДанных.Свойство("sampleCode") Тогда
		СуществующийОбъект.НомерПробы = ЭлементДанных.sampleCode;
	КонецЕсли;
	СуществующийОбъект.Количество          = ЭлементДанных.amountAvailable;
	СуществующийОбъект.КоличествоНачальное = ЭлементДанных.amountOriginal;
	СуществующийОбъект.Статус = ИнтеграцияЗЕРНОСлужебный.СтатусРезультатаИсследований(ЭлементДанных.status);
	
	ИнтеграцияЗЕРНОСлужебный.ЗаполнитьПотребительскиеСвойстваСправочникаПриЗагрузке(
		СуществующийОбъект,
		ЭлементДанных.ListValueQualityIndicator,
		Организация,
		ПараметрыОбмена,,
		Перечисления.ВидыОперацийЗЕРНО.ФормированиеПартииПоРезультатамГосмониторинга);
	
	СуществующийОбъект.ТребуетсяЗагрузка = Ложь;
	СуществующийОбъект.Записать();
	
	ИнтеграцияЗЕРНОСлужебный.ОбновитьСсылку(
		ПараметрыОбмена,
		МетаданныеЭлемента.ПолноеИмя(),
		Идентификатор,
		СуществующийОбъект.Ссылка);
	
	Возврат СуществующийОбъект.Ссылка;
	
КонецФункции

Функция СоздатьРезультатИсследований(Идентификатор)
	
	СправочникОбъект = СоздатьЭлемент();
	СправочникОбъект.Идентификатор         = Идентификатор;
	СправочникОбъект.ТребуетсяЗагрузка     = Истина;
	СправочникОбъект.ОбменДанными.Загрузка = Истина;
	СправочникОбъект.Записать();
	
	Возврат СправочникОбъект.Ссылка;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
