
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	// Обработчик механизма "Свойства"
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект", Объект);
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтаФорма, ДополнительныеПараметры);
	УправлениеСвойствамиУТ.УстановитьОтметкуНезаполненныхСвойств(ЭтаФорма, Объект);
	
	Если Не ЗначениеЗаполнено(Объект.ВидНоменклатуры) Тогда
		Элементы.ВидНоменклатуры.ТолькоПросмотр = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.ВидНоменклатуры.ТолькоПросмотр = Истина;
	УстановитьНастройкиПоШаблону();
	
	СтруктураРеквизитовОбъекта = Новый ФиксированнаяСтруктура(ОбщегоНазначенияУТ.СтруктураСоСвойствамиОбъектаМетаданных(
				Метаданные.Справочники.СерииНоменклатуры));
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтаФорма, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Обработчик механизма "Свойства"
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект);
	
	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(Объект.СерияНоменклатурыДляЦенообразования) Тогда
		ОповеститьОбИзменении(Объект.СерияНоменклатурыДляЦенообразования);
	КонецЕсли;

	СтруктураОповещения = Новый Структура(СтруктураРеквизитовОбъекта);
	
	ЗаполнитьЗначенияСвойств(СтруктураОповещения, Объект);
	
	Оповестить("Запись_СерииНоменклатуры", СтруктураОповещения, Объект.Ссылка);
	
	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// Обработчик механизма "Свойства"
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтаФорма, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидНоменклатурыПриИзменении(Элемент)
	
	УстановитьНастройкиПоШаблону();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПроизводстваПриИзменении(Элемент)
	
	НоменклатураКлиентСервер.ПересчитатьДатуСерии(Объект.ДатаПроизводства);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПроизводстваНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НоменклатураКлиент.ДатаПроизводстваНачалоВыбора(Объект, Неопределено, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ГоденДоПриИзменении(Элемент)
	
	НоменклатураКлиентСервер.ПересчитатьДатуСерии(Объект.ГоденДо);
	
КонецПроцедуры

&НаКлиенте
Процедура ГоденДоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИменаПараметров = "ГоденДо, ДатаПроизводства, ФорматДаты, ИспользоватьДатуПроизводства, ЕдиницаИзмеренияСрокаГодности";
	ПараметрыФормы = Новый Структура(ИменаПараметров);
	
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, Объект);
	
	ПараметрыФормы.ФорматДаты                    = Элементы.ГоденДо.ФорматРедактирования;
	ПараметрыФормы.ИспользоватьДатуПроизводства  = Элементы.ДатаПроизводства.Видимость;
	ПараметрыФормы.ЕдиницаИзмеренияСрокаГодности = ПараметрыПолитики.ЕдиницаИзмеренияСрокаГодности;
	
	НоменклатураКлиент.ДатаИстеченияСрокаГодностиНачалоВыбора(Объект, ПараметрыФормы, Неопределено,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура СгенерироватьНомер(Команда)
	СгенерироватьНомерНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#Область Прочее

&НаСервере
Процедура УстановитьНастройкиПоШаблону()
	
	РежимОтладки = ОбщегоНазначенияУТ.РежимОтладки();
	
	ПараметрыПолитики = Новый ФиксированнаяСтруктура(Справочники.ВидыНоменклатуры.НастройкиИспользованияСерий(Объект.ВидНоменклатуры));
		
	Для Каждого Описание Из ПараметрыПолитики.ОписанияИспользованияРеквизитовСерии Цикл
		
		Элементы[Описание.ИмяРеквизита].Видимость = Описание.Использование;
		
		Если Не ПустаяСтрока(Описание.Формат) Тогда
			Элементы[Описание.ИмяРеквизита].Формат               = Описание.Формат;
			Элементы[Описание.ИмяРеквизита].ФорматРедактирования = Описание.Формат;
		КонецЕсли;
		
	КонецЦикла;
		
	
	МожноМенятьНомер = РежимОтладки
						Или Не Элементы.RFIDTID.Видимость
						Или Не ((ЗначениеЗаполнено(Объект.RFIDTID)
									Или ЗначениеЗаполнено(Объект.RFIDEPC))
								И Не Объект.RFIDМеткаНеЧитаемая);
	
	Элементы.Номер.ТолькоПросмотр = Не МожноМенятьНомер;
	
	Элементы.RFIDTID.ТолькоПросмотр = Не РежимОтладки;
	Элементы.RFIDUser.ТолькоПросмотр = Не РежимОтладки;
	Элементы.RFIDEPC.ТолькоПросмотр = Не РежимОтладки;
	Элементы.RFIDМеткаНеЧитаемая.ТолькоПросмотр = Не РежимОтладки;
		
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура СгенерироватьНомерНаСервере()
	
	МаксимальныйНомерИзСправочника = Справочники.СерииНоменклатуры.ВычислитьМаксимальныйНомерСерии(Объект.ВидНоменклатуры);
	МаксимальныйНомерИзСправочника = МаксимальныйНомерИзСправочника + 1;
	Объект.Номер                   = Формат(МаксимальныйНомерИзСправочника, "ЧЦ=8; ЧВН=; ЧГ=");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
