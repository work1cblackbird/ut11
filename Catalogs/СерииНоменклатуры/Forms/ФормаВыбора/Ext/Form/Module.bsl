
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияУТ.НастроитьПодключаемоеОборудование(ЭтаФорма);
	
	ПараметрыТекущаяСтрока = Неопределено;
	Если ЗначениеЗаполнено(Параметры.ВидНоменклатуры) Тогда
		ВидНоменклатуры = Параметры.ВидНоменклатуры;
	ИначеЕсли Параметры.Свойство("ТекущаяСтрока", ПараметрыТекущаяСтрока) И ЗначениеЗаполнено(ПараметрыТекущаяСтрока) Тогда
		РеквизитыСерии = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПараметрыТекущаяСтрока,
			Новый Структура("ВидНоменклатуры", "ВидНоменклатуры"));
		
		ВидНоменклатуры = РеквизитыСерии.ВидНоменклатуры;
	ИначеЕсли ЗначениеЗаполнено(Параметры.Номенклатура) Тогда
		РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Номенклатура,
			Новый Структура("ВидНоменклатуры", "ВидНоменклатуры"));
		
		ВидНоменклатуры = РеквизитыНоменклатуры.ВидНоменклатуры;
	КонецЕсли;
	
	Элементы.ВидНоменклатуры.ТолькоПросмотр = ЗначениеЗаполнено(ВидНоменклатуры);
	
	ВидНоменклатурыПриИзмененииСервер();
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтаФорма, "СканерШтрихкода");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" И МенеджерОборудованияУТКлиент.ЕстьНеобработанноеСобытие() Тогда
			
			Номер = МенеджерОборудованияУТКлиент.ПреобразоватьДанныеСоСканераВСтруктуру(Параметр).Штрихкод;
			
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
																					"Номер",
																					Номер,
																					ВидСравненияКомпоновкиДанных.Содержит,
																					,
																					ЗначениеЗаполнено(Номер));
			
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидНоменклатурыПриИзменении(Элемент)
	
	ВидНоменклатурыПриИзмененииСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Не Копирование Тогда
		Отказ = Истина;
		
		Если Не ЗначениеЗаполнено(ВидНоменклатуры) Тогда
			ТекстПредупреждения = НСтр("ru = 'Перед добавлением серии необходимо указать вид номенклатуры.'");
			
			ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
			
			Возврат;
		ИначеЕсли ЗначениеЗаполнено(ПараметрыШаблона)
			И Не ПараметрыШаблона.ИспользоватьСерии Тогда
			
			ТекстПредупреждения = НСтр("ru = 'Невозможно добавить новую серию, так как в настройках вида номенклатуры выключена возможность использования серий.'");
			
			ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
			
			Возврат;
			
		КонецЕсли;
		
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("ВидНоменклатуры", ВидНоменклатуры);
		
		ОткрытьФорму("Справочник.СерииНоменклатуры.ФормаОбъекта",
			Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения), Элементы.Список);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура ВидНоменклатурыПриИзмененииСервер()
	
	ПараметрыШаблона = Неопределено;
	ВладелецСерий = Справочники.ВидыНоменклатуры.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(ВидНоменклатуры) Тогда
		
		ПараметрыШаблона = Новый ФиксированнаяСтруктура(
			Справочники.ВидыНоменклатуры.НастройкиИспользованияСерий(ВидНоменклатуры)
		);
		
		ВладелецСерий = ПараметрыШаблона.ВладелецСерий;
		
		Для Каждого Описание Из ПараметрыШаблона.ОписанияИспользованияРеквизитовСерии Цикл
			
			Если ЗначениеЗаполнено(Описание.Формат)
				И Элементы.Найти(Описание.ИмяРеквизита) <> Неопределено Тогда
				Элементы[Описание.ИмяРеквизита].Формат = Описание.Формат;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
		"ВидНоменклатуры",
		ВладелецСерий,
		ВидСравненияКомпоновкиДанных.Равно,,
		Истина
	);
	
	НастроитьПоШаблону();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьПоШаблону()
	
	Если ПараметрыШаблона = Неопределено
		Или (ЗначениеЗаполнено(ПараметрыШаблона)
			И Не ПараметрыШаблона.ИспользоватьСерии) Тогда
			
			ОписанияИспользованияРеквизитовСерии = Справочники.ВидыНоменклатуры.ОписанияИспользованияРеквизитовСерии();
			
			Для Каждого Описание Из ОписанияИспользованияРеквизитовСерии Цикл
				
				Элемент = Элементы.Найти(Описание.ИмяРеквизита);
				Если Элемент = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				Если Не ПустаяСтрока(Описание.ОсобенностиУчета) Тогда
					Элемент.Видимость = Ложь;
				КонецЕсли;
			КонецЦикла;	
	Иначе
		
		Для Каждого Описание Из ПараметрыШаблона.ОписанияИспользованияРеквизитовСерии Цикл
			
			Элемент = Элементы.Найти(Описание.ИмяРеквизита);
			
			Если Элемент = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Элемент.Видимость = Описание.Использование;
		КонецЦикла;	
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
