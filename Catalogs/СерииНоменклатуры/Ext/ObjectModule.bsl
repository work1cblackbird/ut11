#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	НастройкиИспользованияСерий = Справочники.ВидыНоменклатуры.НастройкиИспользованияСерий(ВидНоменклатуры);

	УстановитьРеквизитыЦенообразования = Ложь;
	Если Не ДополнительныеСвойства.Свойство("ИспользуетсяЦенообразование25", УстановитьРеквизитыЦенообразования) Тогда
		УстановитьРеквизитыЦенообразования =	ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25();
	КонецЕсли;
	
	Для Каждого Реквизит Из НастройкиИспользованияСерий.ОписанияИспользованияРеквизитовСерии Цикл
		Если Не Реквизит.Использование Тогда
			ЭтотОбъект[Реквизит.ИмяРеквизита] = Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	Номер = СокрЛП(Номер);
	
	Если НастройкиИспользованияСерий.ТочностьУказанияСрокаГодностиСерии = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоЧасов Тогда
		ГоденДо = НачалоЧаса(ГоденДо);
	ИначеЕсли НастройкиИспользованияСерий.ТочностьУказанияСрокаГодностиСерии = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоМесяцев Тогда
		ГоденДо = НачалоМесяца(ГоденДо);
	Иначе
		ГоденДо = НачалоДня(ГоденДо);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НастройкиИспользованияСерий.ШаблонРабочегоНаименованияСерии) Тогда
		Наименование = НоменклатураСервер.НаименованиеПоШаблону(НастройкиИспользованияСерий.ШаблонРабочегоНаименованияСерии, ЭтотОбъект);
	Иначе
		Наименование = НоменклатураКлиентСервер.ПредставлениеСерииБезРасчетаПоШаблонуНаименования(НастройкиИспользованияСерий, ЭтотОбъект);
	КонецЕсли;
	
	НастройкиКлючаЦен = НастройкиИспользованияСерий.НастройкиКлючаЦенПоСерии;
	Если УстановитьРеквизитыЦенообразования 
		И ЗначениеЗаполнено(НастройкиКлючаЦен)
		И НастройкиКлючаЦен <> Перечисления.ВариантОтбораДляКлючаЦен.НеИспользовать Тогда
			
		ОбновлениеНастроекЦенообразованияВидыНоменклатуры = Ложь;
		Если Не ДополнительныеСвойства.Свойство("ОбновлениеНастроекЦенообразованияВидыНоменклатуры", ОбновлениеНастроекЦенообразованияВидыНоменклатуры) Тогда
			ОбновлениеНастроекЦенообразованияВидыНоменклатуры = Ложь;
		КонецЕсли;
		
		ПроизвестиПоискХСО = ОбновлениеНастроекЦенообразованияВидыНоменклатуры;
		Если Не ОбновлениеНастроекЦенообразованияВидыНоменклатуры Тогда
			Если НастройкиКлючаЦен = Перечисления.ВариантОтбораДляКлючаЦен.ИспользоватьПоРеквизитам Тогда
				ПроизвестиПоискХСО = Истина;
			ИначеЕсли НастройкиКлючаЦен = Перечисления.ВариантОтбораДляКлючаЦен.Использовать Тогда
				ПроизвестиПоискХСО = Ложь;
			КонецЕсли;
		КонецЕсли;

		РеквизитыДляПоиска = ПолучитьРеквизитыДляПоиска(ВидНоменклатуры, НастройкиИспользованияСерий, ПроизвестиПоискХСО);
		СерияНоменклатурыДляЦенообразования = Справочники.СерииНоменклатурыДляЦенообразования.ПолучитьСериюНоменклатурыДляЦенообразования(РеквизитыДляПоиска);
		
	КонецЕсли;
	
	ОбщегоНазначенияУТ.ПодготовитьДанныеДляСинхронизацииКлючей(ЭтотОбъект, ПараметрыСинхронизацииКлючей());
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	ОбщегоНазначенияУТ.СинхронизироватьКлючи(ЭтотОбъект);	
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	СерияНоменклатурыДляЦенообразования = Справочники.СерииНоменклатурыДляЦенообразования.ПустаяСсылка();
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	НастройкиИспользованияСерий = Справочники.ВидыНоменклатуры.НастройкиИспользованияСерий(ВидНоменклатуры);

	Для Каждого Реквизит Из НастройкиИспользованияСерий.ОписанияИспользованияРеквизитовСерии Цикл
		Если Не Реквизит.Использование
			Или Не Реквизит.ПроверятьЗаполнение Тогда
			МассивНепроверяемыхРеквизитов.Добавить(Реквизит.ИмяРеквизита);
		КонецЕсли;
	КонецЦикла;
		
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения = "АвтоТест" Тогда
		Возврат;
	КонецЕсли;
	
	ВидНоменклатуры = ДанныеЗаполнения.ВидНоменклатуры;
	НастройкиИспользованияСерий = Новый ФиксированнаяСтруктура(Справочники.ВидыНоменклатуры.НастройкиИспользованияСерий(ВидНоменклатуры));
	
	СтруктураПолей = Новый Структура;
	
	Для Каждого Реквизит Из НастройкиИспользованияСерий.ОписанияИспользованияРеквизитовСерии Цикл
		Если Реквизит.Использование Тогда
			СтруктураПолей.Вставить(Реквизит.ИмяРеквизита);
		КонецЕсли;
	КонецЦикла;
			
	ЗаполнитьЗначенияСвойств(СтруктураПолей, ДанныеЗаполнения);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураПолей);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Получить реквизиты для поиска.
// 
// Параметры:
//  ВидНоменклатуры - СправочникСсылка.ВидыНоменклатуры - Вид номенклатуры
//  НастройкиИспользованияСерий - Структура - Реквизиты объекта
//  ПроизвестиПоискХСО - Булево - Произвести поиск ХСО
// 
// Возвращаемое значение:
//  Структура - Получить реквизиты для поиска:
// * ТекущаяСерия - СправочникСсылка.СерииНоменклатуры -
// * ТекущаяСерияЦО - СправочникСсылка.СерииНоменклатурыДляЦенообразования -
// * ПроизвестиПоискХСО - Булево -
// * ВидНоменклатуры - СправочникСсылка.ВидыНоменклатуры -
// * СсылкаОдинКОдному - Булево - Истина, на основании серии только одна серия ценообразования
// * РеквизитыДляКлючаЦен - ТаблицаЗначений -
Функция ПолучитьРеквизитыДляПоиска(Знач ВидНоменклатуры, Знач НастройкиИспользованияСерий, Знач ПроизвестиПоискХСО) Экспорт

	//	получить список доп реквизитов по которым искать серию
	РеквизитыДляКлючаЦен = Справочники.ВидыНоменклатуры.ПолучитьРеквизитыДляКлючаЦен(ВидНоменклатуры, "Серии");
	
	РеквизитыДляПоиска = Новый Структура;
	РеквизитыДляПоиска.Вставить("ТекущаяСерия",         Ссылка);
	РеквизитыДляПоиска.Вставить("ТекущаяСерияЦО",       СерияНоменклатурыДляЦенообразования);
	РеквизитыДляПоиска.Вставить("ПроизвестиПоискХСО",   ПроизвестиПоискХСО);
	РеквизитыДляПоиска.Вставить("ВидНоменклатуры",      ВидНоменклатуры);
	РеквизитыДляПоиска.Вставить("СсылкаОдинКОдному",    Ложь);
	
	Если НастройкиИспользованияСерий.НастройкиКлючаЦенПоСерии = Перечисления.ВариантОтбораДляКлючаЦен.ИспользоватьПоРеквизитам Тогда
		ДанныеПоДополнительнымРеквизитам = ДополнительныеРеквизиты.Выгрузить(); 
		
		Для Каждого СтрокаРеквизитаДляКлючаЦен Из РеквизитыДляКлючаЦен Цикл
			Если СтрокаРеквизитаДляКлючаЦен.ЭтоДопРеквизит Тогда
				НайденнаяСтрока = ДанныеПоДополнительнымРеквизитам.Найти(СтрокаРеквизитаДляКлючаЦен.Свойство, "Свойство");
				СтрокаРеквизитаДляКлючаЦен.Значение = НайденнаяСтрока.Значение;
			Иначе
				СтрокаРеквизитаДляКлючаЦен.Значение = ЭтотОбъект[СтрокаРеквизитаДляКлючаЦен.ИмяРеквизита];
			КонецЕсли;
		КонецЦикла;
	Иначе // по всем реквизитам для ценообразования
		РеквизитыДляПоиска.Вставить("СсылкаОдинКОдному",          Истина);
		МассивНаименованийРеквизитов = Справочники.СерииНоменклатурыДляЦенообразования.ПолучитьНаборРеквизитовДляКонтроля();
	
		Для Каждого Описание Из НастройкиИспользованияСерий.ОписанияИспользованияРеквизитовСерии Цикл
			Если Описание.Использование И МассивНаименованийРеквизитов.Найти(Описание.ИмяРеквизита) <> Неопределено Тогда

				СтрокаРеквизитаДляКлючаЦен = РеквизитыДляКлючаЦен.Добавить();
				СтрокаРеквизитаДляКлючаЦен.ИмяРеквизита   = Описание.ИмяРеквизита;
				СтрокаРеквизитаДляКлючаЦен.ЭтоДопРеквизит = Ложь;
				СтрокаРеквизитаДляКлючаЦен.Используется   = Истина;
				СтрокаРеквизитаДляКлючаЦен.Значение = ЭтотОбъект[Описание.ИмяРеквизита];
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого Строка Из ДополнительныеРеквизиты Цикл
			СтрокаРеквизитаДляКлючаЦен = РеквизитыДляКлючаЦен.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРеквизитаДляКлючаЦен, Строка);
			СтрокаРеквизитаДляКлючаЦен.ИмяРеквизита = Строка.Свойство;
			СтрокаРеквизитаДляКлючаЦен.ЭтоДопРеквизит = Истина;
			СтрокаРеквизитаДляКлючаЦен.Используется = Истина;
		КонецЦикла;
		
		СтрокаРеквизитаДляКлючаЦен = РеквизитыДляКлючаЦен.Добавить();
		СтрокаРеквизитаДляКлючаЦен.ИмяРеквизита   = "Наименование";
		СтрокаРеквизитаДляКлючаЦен.Значение       = Наименование;
		СтрокаРеквизитаДляКлючаЦен.ЭтоДопРеквизит = Ложь;
		СтрокаРеквизитаДляКлючаЦен.Используется   = Истина;
		
	КонецЕсли;
	
	РеквизитыДляПоиска.Вставить("РеквизитыДляКлючаЦен", РеквизитыДляКлючаЦен);
	
	Возврат РеквизитыДляПоиска
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПараметрыСинхронизацииКлючей()
	
	Результат = Новый Соответствие;
	
	Результат.Вставить("Справочник.КлючиАналитикиУчетаНоменклатуры", "ПометкаУдаления");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
#КонецЕсли