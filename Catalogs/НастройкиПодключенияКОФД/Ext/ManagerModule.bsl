///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Определяет наличие настроек подключения к ОФД.
//
// Возвращаемое значение:
//  Булево - если Истина, есть настройки подключения.
//
Функция ЕстьНастройкиПодключения() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК Признак
		|ИЗ
		|	Справочник.НастройкиПодключенияКОФД КАК НастройкиПодключенияКОФД";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Сохраняет настройки подключения к ОФД в информационную базу.
//
// Параметры:
//  ПараметрыЗаполнения - Структура - данные заполнения настройки подключения к ОФД;
//  ПараметрыАутентификации - Структура - данные аутентификации;
//
// Возвращаемое значение:
//  Структура - результат создания торговой точки:
//    * Ссылка - ОпределяемыйТип.КассаОФД - новая настройка подключения;
//    * Ошибка - Булево - признак ошибки сохранения;
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя.
//
Функция НоваяНастройкаПодключения(
		ПараметрыЗаполнения,
		ПараметрыАутентификации) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Ошибка", Ложь);
	Результат.Вставить("СообщениеОбОшибке", "");
	Результат.Вставить("Ссылка", Неопределено);
	
	НачатьТранзакцию();
	
	Попытка
		
		НастройкаПодключения = Справочники.НастройкиПодключенияКОФД.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(
			НастройкаПодключения,
			ПараметрыЗаполнения,
			"ИдентификаторУчастника, Наименование");
		
		НастройкаПодключения.Записать();
		
		ОФДСлужебный.СохранитьНастройкиАутентификации(
			НастройкаПодключения.Ссылка,
			ПараметрыАутентификации);
		
		Результат.Ссылка = НастройкаПодключения.Ссылка;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		Результат.Ошибка = Истина;
		Результат.СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось записать настройку подключения по причине:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли