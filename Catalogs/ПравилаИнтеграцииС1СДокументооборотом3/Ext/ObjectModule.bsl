#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	ПроверитьВозможностьЗаписиПравила();
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьПредставление();
	
	Для Каждого Строка Из ПравилаЗаполненияРеквизитовИС Цикл
		Если Не ЗначениеЗаполнено(Строка.id) Тогда
			Строка.id = Строка(Новый УникальныйИдентификатор);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обновляет представление правила согласно представлениям объектов и ключевых реквизитов.
//
Процедура ОбновитьПредставление()
	
	// Соберем представление объекта ИС.
	ПредставлениеОбъектаИС = "";
	ПредставлениеОбъектаИССКлючевымиПолями = "";
	
	МассивПолноеПредставлениеОбъектаИС = Новый Массив;
	
	ОбъектПотребителя = Метаданные.НайтиПоПолномуИмени(ТипОбъектаИС);
	Если ОбъектПотребителя <> Неопределено Тогда
		ПредставлениеОбъектаИС = ?(ОбъектПотребителя.Синоним = "",
			ОбъектПотребителя.Имя,
			ОбъектПотребителя.Синоним);
		
		МассивПолноеПредставлениеОбъектаИС.Добавить(ПредставлениеОбъектаИС);
		
		// Дополним представление ключевыми реквизитами.
		Для Каждого КлючевойРеквизит Из КлючевыеРеквизитыИС Цикл
			ПредставлениеКлючевогоРеквизита = ПредставлениеКлючевогоРеквизита(КлючевойРеквизит);
			Если ПредставлениеКлючевогоРеквизита <> "" Тогда
				МассивПолноеПредставлениеОбъектаИС.Добавить(ПредставлениеКлючевогоРеквизита);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ПредставлениеОбъектаИССКлючевымиПолями = СтрСоединить(МассивПолноеПредставлениеОбъектаИС, ", ");
	
	Если СокрЛП(Комментарий) <> "" Тогда
		// Комментарий не должен входить в ПредставлениеОбъектаИС, но должен входить в наименование
		МассивПолноеПредставлениеОбъектаИС.Добавить(СокрЛП(Комментарий));
	КонецЕсли;
	
	// Соберем представление объекта ДО
	МассивПредставлениеОбъектаДО = Новый Массив;
	МассивПредставлениеОбъектаДО.Добавить(ПредставлениеОбъектаДО);
	
	Для Каждого КлючевойРеквизит Из КлючевыеРеквизитыДО Цикл
		Если КлючевойРеквизит.Имя = "documentType" Тогда
			Продолжить;
		КонецЕсли;
		
		ПредставлениеКлючевогоРеквизита = ПредставлениеКлючевогоРеквизита(КлючевойРеквизит);
		Если ПредставлениеКлючевогоРеквизита <> "" Тогда
			МассивПредставлениеОбъектаДО.Добавить(ПредставлениеКлючевогоРеквизита);
		КонецЕсли;
	КонецЦикла;
	
	ПредставлениеОбъектаДОСКлючевымиПолями = СтрСоединить(МассивПредставлениеОбъектаДО, ", ");
	
	// Соберем наименование.
	Наименование = СтрШаблон("%1 - %2",
		СтрСоединить(МассивПолноеПредставлениеОбъектаИС, ", "),
		ПредставлениеОбъектаДОСКлючевымиПолями);
	
КонецПроцедуры

Функция ПредставлениеКлючевогоРеквизита(КлючевойРеквизит)
	
	Если ТипЗнч(КлючевойРеквизит.ЗначениеРеквизита) = Тип("Булево")
			Или ТипЗнч(КлючевойРеквизит.ЗначениеРеквизита) = Тип("Число")
			Или ТипЗнч(КлючевойРеквизит.ЗначениеРеквизита) = Тип("Дата") Тогда
		Возврат СтрШаблон("%1: %2", КлючевойРеквизит.Представление, СокрЛП(КлючевойРеквизит.ЗначениеРеквизита));
	Иначе
		Представление = СокрЛП(КлючевойРеквизит.ЗначениеРеквизита);
		Если Представление = "" Тогда
			Представление = СтрШаблон(НСтр("ru = '%1: пустое значение'"), КлючевойРеквизит.Представление);
		КонецЕсли;
		Возврат Представление;
	КонецЕсли;
	
КонецФункции

Процедура ПроверитьВозможностьЗаписиПравила()
	
	Если Пользователи.ЭтоПолноправныйПользователь() Тогда
		Возврат;
	КонецЕсли;
	
	ШаблоныВыражений =
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ШаблоныВыраженийНаВстроенномЯзыке();
	
	СписокВыраженийДляЗаполненияРеквизитовИС = Новый Соответствие;
	Для Каждого Строка Из ПравилаЗаполненияРеквизитовИС Цикл
		Если ЗначениеЗаполнено(Строка.ВычисляемоеВыражение) Тогда
			ЭтоШаблон = Ложь;
			Для Каждого ШаблонВыражения Из ШаблоныВыражений Цикл
				Если ШаблонВыражения.Значение = Строка.ВычисляемоеВыражение Тогда
					ЭтоШаблон = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если ЭтоШаблон Тогда
				Продолжить;
			КонецЕсли;
			Ключ = Новый Структура("ИмяРеквизитаОбъектаИС, ЭтоТаблица, Таблица");
			ЗаполнитьЗначенияСвойств(Ключ, Строка);
			СписокВыраженийДляЗаполненияРеквизитовИС.Вставить(Ключ, Строка.ВычисляемоеВыражение);
		КонецЕсли;
	КонецЦикла;
	
	Если СписокВыраженийДляЗаполненияРеквизитовИС.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		ВызватьИсключение НСтр("ru = 'В правилах интеграции с 1С:Документооборотом запрещено изменять выражения на встроенном языке.
			|Обратитесь к администратору.'");
	КонецЕсли;
	
	СтарыеПравилаЗаполненияРеквизитовИС =
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПравилаЗаполненияРеквизитовИС").Выгрузить();
	
	Для Каждого ТекущееВыражение Из СписокВыраженийДляЗаполненияРеквизитовИС Цикл
		Строки = СтарыеПравилаЗаполненияРеквизитовИС.НайтиСтроки(ТекущееВыражение.Ключ);
		Если Строки.Количество() = 1 И Строки[0].ВычисляемоеВыражение = ТекущееВыражение.Значение Тогда
			Продолжить;
		КонецЕсли;
		ВызватьИсключение НСтр("ru = 'В правилах интеграции с 1С:Документооборотом запрещено изменять выражения на встроенном языке.
			|Обратитесь к администратору.'");
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли