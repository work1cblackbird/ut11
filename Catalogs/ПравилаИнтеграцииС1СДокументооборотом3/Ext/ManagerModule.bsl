#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает список выражений на встроенном языке, которые требуется предварительно выполнить на стороне ДО,
// для последующего заполнения объектов на стороне ИС.
//
// Параметры:
//   ПравилаДляАнализа - Массив из СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом3 - правила интеграции.
//
// Возвращаемое значение:
//   ТаблицаЗначений:
//     * Ссылка - СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом3
//     * id - Строка
//     * ВычисляемоеВыражение - Строка
//     * Таблица - Строка
//     * ЭтоТаблица - Булево
//     * ИмяТаблицыИсточника - Строка
//
Функция ВыраженияДляВыполненияНаСторонеДО(ПравилаДляАнализа) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПравилаЗаполненияРеквизитовИС.Ссылка КАК Ссылка,
		|	ПравилаЗаполненияРеквизитовИС.id КАК id,
		|	ПравилаЗаполненияРеквизитовИС.ВычисляемоеВыражение КАК ВычисляемоеВыражение,
		|	ПравилаЗаполненияРеквизитовИС.Таблица КАК Таблица,
		|	ПравилаЗаполненияРеквизитовИС.ЭтоТаблица КАК ЭтоТаблица,
		|	ЕСТЬNULL(ПравилаЗаполненияРеквизитовИСРодитель.ИмяРеквизитаОбъектаДО, """") КАК ИмяТаблицыИсточника
		|ИЗ
		|	Справочник.ПравилаИнтеграцииС1СДокументооборотом3.ПравилаЗаполненияРеквизитовИС КАК ПравилаЗаполненияРеквизитовИС
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаИнтеграцииС1СДокументооборотом3.ПравилаЗаполненияРеквизитовИС КАК ПравилаЗаполненияРеквизитовИСРодитель
		|		ПО ПравилаЗаполненияРеквизитовИС.Ссылка = ПравилаЗаполненияРеквизитовИСРодитель.Ссылка
		|			И ПравилаЗаполненияРеквизитовИС.Таблица = ПравилаЗаполненияРеквизитовИСРодитель.ИмяРеквизитаОбъектаИС
		|			И (ПравилаЗаполненияРеквизитовИСРодитель.ЭтоТаблица)
		|ГДЕ
		|	ПравилаЗаполненияРеквизитовИС.Вариант = ЗНАЧЕНИЕ(Перечисление.ВариантыПравилЗаполненияРеквизитов.ВыражениеНаВстроенномЯзыке)
		|	И ПравилаЗаполненияРеквизитовИС.МестоВыполненияВыражения = ЗНАЧЕНИЕ(Перечисление.МестаВыполненияВыраженийНаВстроенномЯзыке.НаСторонеДО)
		|	И ПравилаЗаполненияРеквизитовИС.Ссылка В(&ПравилаДляАнализа)");
	Запрос.УстановитьПараметр("ПравилаДляАнализа", ПравилаДляАнализа);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Заполняет объект ИС по объекту XDTO.
//
// Параметры:
//   Прокси - WSПрокси - объект для подключения к web-сервисам Документооборота.
//   ЗаполняемыйОбъектИС - Произвольный - заполняемый объект ИС.
//   ОбъектXDTO - ОбъектXDTO - объект ДО, источник данных заполнения.
//   ПравилоИнтеграции - СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом3 - правило интеграции для
//     заполнения объекта ИС.
//   Обновление - Булево - заполнять только реквизиты с Обновлять = Истина.
//   ТребуетсяПерепроведение - Булево - Истина, если изменение требует перепроведения.
//
// Возвращаемое значение:
//   Булево - Истина, если объект был изменен, и Ложь в противном случае.
//
Функция ЗаполнитьОбъектИСПоОбъектуXDTO(Прокси, ЗаполняемыйОбъектИС, ОбъектXDTO, ПравилоИнтеграции, Обновление = Ложь,
		ТребуетсяПерепроведение = Ложь) Экспорт
	
	ОбъектИзменен = Ложь;
	ОбновленныеТабличныеЧасти = Новый Соответствие;
	
	МетаданныеОбъекта = ЗаполняемыйОбъектИС.Метаданные();
	ПолноеИмяОбъектаИС = МетаданныеОбъекта.ПолноеИмя();
	
	ПроверятьПерепроведение = Обновление
		И ОбщегоНазначения.ЭтоДокумент(МетаданныеОбъекта)
		И МетаданныеОбъекта.Проведение = Метаданные.СвойстваОбъектов.Проведение.Разрешить
		И ЗаполняемыйОбъектИС.Проведен;
	
	РазрешеноБезПерепроведения =
		Перечисления.РежимИзмененияПроведенногоДокументаДанными1СДокументооборота.РазрешеноБезПерепроведения;
	РазрешеноСПерепроведением =
		Перечисления.РежимИзмененияПроведенногоДокументаДанными1СДокументооборота.РазрешеноСПерепроведением;
	
	// Переопределяемый модуль может перекрыть настройки правил, разрешив изменение без перепроведения.
	ОбновляемыеРеквизиты = Новый Массив;
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьПереопределяемый.ПриОпределенииОбновляемыхРеквизитовПроведенногоДокумента(
		ПолноеИмяОбъектаИС,
		ОбновляемыеРеквизиты);
	
	КонтекстЗаполненияПоПравилу = КонтекстЗаполненияПоПравилу(
		ЗаполняемыйОбъектИС,
		ОбъектXDTO,
		ПравилоИнтеграции,
		ПолноеИмяОбъектаИС);
	
	Для Каждого ДанныеРеквизита Из КонтекстЗаполненияПоПравилу.РеквизитыОбъектаИС Цикл
		// Метод ИнтеграцияС1СДокументооборотБазоваяФункциональность.РеквизитыОбъектаИС сортирует реквизиты
		// объекта ИС в порядке, необходимом для загрузки данных. Объекты, имеющие владельца, должны грузиться
		// после того, как загружен их владелец. Например, к загрузке контактного лица и договора можно
		// приступать только после того, как загружен контрагент.
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ИмяРеквизитаОбъектаИС", ДанныеРеквизита.Имя);
		ПараметрыОтбора.Вставить("ЭтоТаблица", ДанныеРеквизита.ЭтоТаблица);
		ПараметрыОтбора.Вставить("Таблица", ""); // Реквизиты табличной части грузим в ЗаполнитьТаблицуПоПравилу.
		Если Обновление Тогда
			ПараметрыОтбора.Вставить("Обновлять", Истина);
		КонецЕсли;
		
		НайденныеПравилаРеквизитов = КонтекстЗаполненияПоПравилу.ПравилаЗаполненияРеквизитовИС.НайтиСтроки(
			ПараметрыОтбора); // см. Справочник.ПравилаИнтеграцииС1СДокументооборотом3.ПравилаЗаполненияРеквизитовИС
		Если НайденныеПравилаРеквизитов.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		ПравилоРеквизита = НайденныеПравилаРеквизитов[0];
		
		Если ПроверятьПерепроведение Тогда
			Если ПравилоРеквизита.РежимИзмененияДанныхПроведенногоДокумента = РазрешеноСПерепроведением Тогда
				РежимИзменения = РазрешеноСПерепроведением;
				
			ИначеЕсли ПравилоРеквизита.РежимИзмененияДанныхПроведенногоДокумента = РазрешеноБезПерепроведения
					Или ОбновляемыеРеквизиты.Найти(ПравилоРеквизита.ИмяРеквизитаОбъектаИС) <> Неопределено Тогда
				РежимИзменения = РазрешеноБезПерепроведения;
				
			Иначе
				// Изменение запрещено.
				Продолжить;
				
			КонецЕсли;
		Иначе
			РежимИзменения = РазрешеноБезПерепроведения;
		КонецЕсли;
		
		Если ПравилоРеквизита.ЭтоТаблица Тогда
			// Заполняем табличную часть объекта ИС.
			ТаблицаОбъектаИС = ЗаполняемыйОбъектИС[ПравилоРеквизита.ИмяРеквизитаОбъектаИС];
			ОбновленныеТабличныеЧасти.Вставить(ПравилоРеквизита.ИмяРеквизитаОбъектаИС, ТаблицаОбъектаИС.Выгрузить());
			ЗаполнитьТаблицуПоПравилу(Прокси, ТаблицаОбъектаИС, ПравилоРеквизита, КонтекстЗаполненияПоПравилу);
			
		ИначеЕсли ПравилоРеквизита.ЭтоДополнительныйРеквизитИС Тогда
			// Заполняем дополнительный реквизит объекта ИС.
			СтруктураПоиска = Новый Структура("Свойство", ПравилоРеквизита.ДополнительныйРеквизитИС);
			СтрокиРеквизита = ЗаполняемыйОбъектИС.ДополнительныеРеквизиты.НайтиСтроки(СтруктураПоиска);
			Если СтрокиРеквизита.Количество() = 0 Тогда
				ОбъектИзменен = Истина;
				СтрокаРеквизита = ЗаполняемыйОбъектИС.ДополнительныеРеквизиты.Добавить();
				СтрокаРеквизита.Свойство = ПравилоРеквизита.ДополнительныйРеквизитИС;
			Иначе
				СтрокаРеквизита = СтрокиРеквизита[0];
			КонецЕсли;
			
			ПредыдущееЗначение = СтрокаРеквизита.Значение;
			
			ЗаполнитьРеквизитПоПравилу(
				Прокси,
				СтрокаРеквизита.Значение,
				ПравилоРеквизита,
				ДанныеРеквизита,
				КонтекстЗаполненияПоПравилу);
			
			Если СтрокаРеквизита.Значение <> ПредыдущееЗначение Тогда
				Если РежимИзменения = РазрешеноБезПерепроведения Тогда
					ОбъектИзменен = Истина;
				ИначеЕсли РежимИзменения = РазрешеноСПерепроведением Тогда
					ОбъектИзменен = Истина;
					ТребуетсяПерепроведение = Истина;
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли ПравилоРеквизита.Таблица = "" Тогда
			// Заполняем реквизит объекта ИС.
			// Реквизиты табличной части пропускаем, их заполняем в методе ЗаполнитьТаблицуПоПравилу.
			ПредыдущееЗначение = ЗаполняемыйОбъектИС[ПравилоРеквизита.ИмяРеквизитаОбъектаИС];
			
			ЗаполнитьРеквизитПоПравилу(
				Прокси,
				ЗаполняемыйОбъектИС[ПравилоРеквизита.ИмяРеквизитаОбъектаИС],
				ПравилоРеквизита,
				ДанныеРеквизита,
				КонтекстЗаполненияПоПравилу);
			
			Если ЗаполняемыйОбъектИС[ПравилоРеквизита.ИмяРеквизитаОбъектаИС] <> ПредыдущееЗначение Тогда
				Если РежимИзменения = РазрешеноБезПерепроведения Тогда
					ОбъектИзменен = Истина;
				ИначеЕсли РежимИзменения = РазрешеноСПерепроведением Тогда
					ОбъектИзменен = Истина;
					ТребуетсяПерепроведение = Истина;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ТабличнаяЧасть Из ОбновленныеТабличныеЧасти Цикл
		ОбъектИзменен = (ОбъектИзменен
			Или (ЗначениеВСтрокуВнутр(ТабличнаяЧасть.Значение) <>
				ЗначениеВСтрокуВнутр(ЗаполняемыйОбъектИС[ТабличнаяЧасть.Ключ].Выгрузить())));
	КонецЦикла;
	
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ОбъектXDTO, "deletionMark") Тогда
		ОбъектИзменен = (ОбъектИзменен Или (ОбъектXDTO.deletionMark <> ЗаполняемыйОбъектИС.ПометкаУдаления));
		ЗаполняемыйОбъектИС.ПометкаУдаления = ОбъектXDTO.deletionMark;
	КонецЕсли;
	
	Возврат ОбъектИзменен;
	
КонецФункции

// Конструктор структуры, описывающей контекст для выполнения выражения на встроенном языке.
//
// Возвращаемое значение:
//   Структура:
//     * ОбъектXDTO - ОбъектXDTO - объект ДО, источник данных заполнения.
//                  - Неопределено - конкретный источник отсутствует.
//     * ОбъектИС - Произвольный - приемник данных.
//                - Неопределено - конкретный приемник отсутствует.
//     * ТаблицаИсточник - Строка - имя таблицы, из которой будет выполняться загрузка данных.
//     * ВидДокументаДОID - Строка - идентификатор вида документа, который является источником.
//
Функция КонтекстВыраженияНаВстроенномЯзыке() Экспорт
	
	КонтекстВыражения = Новый Структура;
	КонтекстВыражения.Вставить("ОбъектXDTO", Неопределено);
	КонтекстВыражения.Вставить("ОбъектИС", Неопределено);
	КонтекстВыражения.Вставить("ТаблицаИсточник", "");
	КонтекстВыражения.Вставить("ВидДокументаДОID", "");
	
	Возврат КонтекстВыражения;
	
КонецФункции

// Возвращает реквизиты объекта ДО указанного типа, возможно, с уточнением до вида документа.
//
// Параметры:
//   ТипОбъектаДО - Строка - имя типа объекта XDTO Документооборота.
//   ВидДокументаДОID - Строка - идентификатор вида документа. Заполняется, если тип объекта ДО равен DMDocument.
//   ПолучитьВсе - Булево - позволяет вернуть все возможные реквизиты, вне зависимости от
//     вида объекта Документооборота.
//   НастройкиВида - ОбъектXDTO - неявно возвращаемое значение, содержащее настройки вида документа.
//   ДопЗапрос - ОбъектXDTO - дополнительный запрос, который в целях оптимизации требуется выполнить в пакете
//     с прочими запросами к 1С:Документообороту.
//   ОтветНаДопЗапрос - ОбъектXDTO - неявно возвращаемое значение, ответ на дополнительный запрос.
//
// Возвращаемое значение:
//   ТаблицаЗначений:
//     * Имя - Строка
//     * Представление - Строка
//     * Тип - СписокЗначений из Строка
//     * ДопРеквизит - Булево
//     * ДопРеквизитID - Строка
//     * ДопРеквизитТип - Строка
//     * ОбязательноеЗаполнение - Булево
//     * ЭтоТаблица - Булево
//     * Таблица - Строка
//
Функция РеквизитыОбъектаДО(ТипОбъектаДО, ВидДокументаДОID, ПолучитьВсе, НастройкиВида = Неопределено,
		ДопЗапрос = Неопределено, ОтветНаДопЗапрос = Неопределено) Экспорт
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	ЗапросыПакета = Новый Массив;
	
	НачальныйНомерЗапроса = 0;
	Если ДопЗапрос <> Неопределено Тогда
		ЗапросыПакета.Добавить(ДопЗапрос);
		НачальныйНомерЗапроса = НачальныйНомерЗапроса + 1;
	КонецЕсли;
	
	// Общие настройки.
	ПолучаемыеПоля = Новый Массив;
	ПолучаемыеПоля.Добавить("additionalProperties");
	ПолучаемыеПоля.Добавить("accessLevelEnabled");
	ПолучаемыеПоля.Добавить("activityMatterEnabled");
	ПолучаемыеПоля.Добавить("foldersEnabled");
	ПолучаемыеПоля.Добавить("organizationEnabled");
	ПолучаемыеПоля.Добавить("statusEnabled");
	ПолучаемыеПоля.Добавить("projectsEnabled");
	ЗапросыПакета.Добавить(
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьНовыйОбъектЗапрос(
			Прокси,
			ТипОбъектаДО,
			ПолучаемыеПоля,
			ВидДокументаДОID));
	
	Если ИнтеграцияС1СДокументооборот3.ЭтоДокументДО3(ТипОбъектаДО) Тогда
		Если Не ЗначениеЗаполнено(ВидДокументаДОID) Тогда
			ЗапросыПакета.Добавить(
				ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьНовыйОбъектЗапрос(
					Прокси,
					"DMDocumentType"));
		Иначе
			// Вместе с настройками типа получим настройки по виду документа.
			ВидДокументаДО = Новый Структура("ID, Тип", ВидДокументаДОID, "DMDocumentType");
			Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьОбъектыЗапрос(
				Прокси,
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВидДокументаДО));
			ЗапросыПакета.Добавить(Запрос);
		КонецЕсли;
		
		ОтветНаЗапросыПакета = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьПакетныйЗапрос(
			Прокси,
			ЗапросыПакета,
			Истина);
		
		НастройкиТипа = ОтветНаЗапросыПакета.responses[НачальныйНомерЗапроса];
		
		РезультатНастройкиВида = ОтветНаЗапросыПакета.responses[НачальныйНомерЗапроса + 1];
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьТип(
				Прокси, РезультатНастройкиВида, "DMDocumentType") Тогда
			НастройкиВида = РезультатНастройкиВида;
		Иначе
			НастройкиВида = РезультатНастройкиВида.objects[0];
		КонецЕсли;
	Иначе
		// Будем получать только настройки типа.
		ОтветНаЗапросыПакета = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьПакетныйЗапрос(
			Прокси,
			ЗапросыПакета,
			Истина);
		
		НастройкиТипа = ОтветНаЗапросыПакета.responses[НачальныйНомерЗапроса];
	КонецЕсли;
	
	Если ДопЗапрос <> Неопределено Тогда
		ОтветНаДопЗапрос = ОтветНаЗапросыПакета.responses[0];
	КонецЕсли;
	
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(
			НастройкиТипа, "additionalProperties") Тогда
		ДопРеквизиты = НастройкиТипа.additionalProperties;
	Иначе
		ДопРеквизиты = Новый Массив;
	КонецЕсли;
	
	Реквизиты = СтруктураТаблицыРеквизитовДО();
	
	// Документ
	Если ИнтеграцияС1СДокументооборот3.ЭтоДокументДО3(ТипОбъектаДО) Тогда
		
		ИспользуютсяКонтрагентыИлиСтороны = (НастройкиВида.correspondentEnabled = Истина
			Или (НастройкиВида.partiesEnabled = Истина И НастройкиВида.isAPaymentRequest <> Истина));
		ЭтоНеКорреспонденция = (НастройкиВида.isIncoming <> Истина И НастройкиВида.isOutgoing <> Истина);
		
		ТипСторона = Новый СписокЗначений;
		ТипСторона.Добавить("DMEmployee", НСтр("ru = 'Сотрудники'"));
		ТипСторона.Добавить("DMCorrespondent", НСтр("ru = 'Контрагенты'"));
		ТипСторона.Добавить("DMOrganization", НСтр("ru = 'Организации'"));
		
		ТипКонтактноеЛицоСтороны = Новый СписокЗначений;
		ТипКонтактноеЛицоСтороны.Добавить("DMEmployee", НСтр("ru = 'Сотрудники'"));
		ТипКонтактноеЛицоСтороны.Добавить("DMContactPerson", НСтр("ru = 'Контактные лица'"));
		ТипКонтактноеЛицоСтороны.Добавить("Строка", НСтр("ru = 'Строка'"));
		
		// Вид документа.
		ДобавитьРеквизит(Реквизиты, "documentType", НСтр("ru = 'Вид документа'"), "DMDocumentType", Истина, "ВидДокумента");
		
		// Общие реквизиты.
		ДобавитьРеквизит(Реквизиты, "title", НСтр("ru = 'Заголовок'"), "Строка", Истина, "Заголовок");
		ДобавитьРеквизит(Реквизиты, "summary", НСтр("ru = 'Содержание'"), "Строка",, "Содержание");
		ДобавитьРеквизит(Реквизиты, "regNumber", НСтр("ru = 'Регистрационный номер'"), "Строка",, "РегистрационныйНомер");
		ДобавитьРеквизит(Реквизиты, "regDate", НСтр("ru = 'Дата регистрации'"), "Дата",, "ДатаРегистрации");
		ДобавитьРеквизит(Реквизиты, "author", НСтр("ru = 'Подготовил'"), "DMEmployee",, "Подготовил");
		ДобавитьРеквизит(Реквизиты, "subdivision", НСтр("ru = 'Подразделение'"), "DMSubdivision",, "Подразделение");
		ДобавитьРеквизит(Реквизиты, "comment", НСтр("ru = 'Комментарий'"), "Строка",, "Комментарий");
		ДобавитьРеквизит(Реквизиты, "responsible", НСтр("ru = 'Ответственный'"), "DMEmployee",, "Ответственный");
		
		// Прочие реквизиты.
		Если ПолучитьВсе Или НастройкиВида.performanceDateEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "performanceDate", НСтр("ru = 'Срок исполнения'"), "Дата",, "СрокИсполнения");
		КонецЕсли;
		Если ПолучитьВсе Или НастройкиТипа.accessLevelEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "accessLevel", НСтр("ru = 'Гриф доступа'"), "DMAccessLevel", Истина, "ГрифДоступа");
		КонецЕсли;
		Если ПолучитьВсе Или НастройкиТипа.activityMatterEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "activityMatter", НСтр("ru = 'Вопрос деятельности'"), "DMActivityMatter", Истина, "ВопросДеятельности");
		КонецЕсли;
		Если ПолучитьВсе Или НастройкиТипа.projectsEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "project", НСтр("ru = 'Проект'"), "DMProject",, "Проект");
		КонецЕсли;
		Если ПолучитьВсе Или НастройкиТипа.foldersEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "folder", НСтр("ru = 'Папка'"), "DMDocumentFolder", Истина, "Папка");
		КонецЕсли;
		
		// Состояния.
		Если ПолучитьВсе Или НастройкиТипа.statusEnabled = Истина Тогда
			Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(НастройкиВида, "actionTypes") Тогда
				Для Каждого ВидДействияXDTO Из НастройкиВида.actionTypes Цикл
					ДобавитьРеквизит(
						Реквизиты,
						СтрШаблон("status_%1", СтрЗаменить(ВидДействияXDTO.objectID.id, "-", "_")),
						СтрШаблон(НСтр("ru = 'Состояние действия ""%1""'"), ВидДействияXDTO.name),
						"DMDocumentStatus");
				КонецЦикла;
			Иначе
				ДобавитьРеквизит(Реквизиты, "statusApproval", НСтр("ru = 'Состояние согласования'"), "DMDocumentStatus");
				ДобавитьРеквизит(Реквизиты, "statusConfirmation", НСтр("ru = 'Состояние утверждения'"), "DMDocumentStatus");
				ДобавитьРеквизит(Реквизиты, "statusPerformance", НСтр("ru = 'Состояние исполнения'"), "DMDocumentStatus");
				ДобавитьРеквизит(Реквизиты, "statusRegistration", НСтр("ru = 'Состояние регистрации'"), "DMDocumentStatus");
				ДобавитьРеквизит(Реквизиты, "statusSigning", НСтр("ru = 'Состояние подписания'"), "DMDocumentStatus");
				Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоСуществует(
						НастройкиТипа, "statusAcquaintance") Тогда
					ДобавитьРеквизит(Реквизиты, "statusAcquaintance", НСтр("ru = 'Состояние ознакомления'"), "DMDocumentStatus");
				ИначеЕсли ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоСуществует(
						НастройкиТипа, "statusExamination") Тогда
					ДобавитьРеквизит(Реквизиты, "statusExamination", НСтр("ru = 'Состояние ознакомления'"), "DMDocumentStatus");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// Контрагент.
		Если ПолучитьВсе Или ИспользуютсяКонтрагентыИлиСтороны Тогда
			Заголовок = НСтр("ru = 'Контрагент'");
			Обязателен = Ложь; // Обязательным будет заполнение контрагента в табличной части
			
			Если НастройкиВида.isIncoming = Истина Тогда
				Заголовок = НСтр("ru = 'Отправитель'");
				Обязателен = Истина;
			ИначеЕсли НастройкиВида.isOutgoing = Истина Тогда
				Заголовок = НСтр("ru = 'Получатель'");
				Обязателен = Истина;
			КонецЕсли;
			
			ДобавитьРеквизит(Реквизиты, "correspondent", Заголовок, "DMCorrespondent", Обязателен, "Контрагент");
		КонецЕсли;
		
		// Контактное лицо
		Если ПолучитьВсе Или (ИспользуютсяКонтрагентыИлиСтороны И ЭтоНеКорреспонденция) Тогда
			ДобавитьРеквизит(Реквизиты, "contactPerson", НСтр("ru = 'Контактное лицо'"), "DMContactPerson",, "КонтактноеЛицо");
		КонецЕсли;
		
		// Получатель.
		Если ПолучитьВсе Или НастройкиВида.isAPaymentRequest = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "recipient", НСтр("ru = 'Получатель'"), ТипСторона, Истина);
		КонецЕсли;
		
		// Табличная часть Контрагенты.
		Если ПолучитьВсе
				Или (НастройкиВида.correspondentEnabled = Истина И ЭтоНеКорреспонденция) Тогда
			ДобавитьТаблицу(Реквизиты, "correspondentRows.rows", НСтр("ru = 'Контрагенты'"));
			ДобавитьРеквизитТаблицы(Реквизиты,
				"correspondentRows.rows", "correspondent", НСтр("ru = 'Контрагент'"), "DMCorrespondent", Истина);
			ДобавитьРеквизитТаблицы(Реквизиты,
				"correspondentRows.rows", "contact", НСтр("ru = 'Контактное лицо'"), "DMContactPerson");
			ДобавитьРеквизитТаблицы(Реквизиты,
				"correspondentRows.rows",
				"signedFromTheCorrespondent", НСтр("ru = 'Подписал от контрагента'"), "DMContactPerson");
		КонецЕсли;
		
		// Организация.
		Если ПолучитьВсе Или НастройкиВида.organizationEnabled = Истина Тогда
			Если НастройкиВида.isAPaymentRequest = Истина Тогда
				Заголовок = НСтр("ru = 'Плательщик'");
			Иначе
				Заголовок = НСтр("ru = 'Организация'");
			КонецЕсли;
			
			ДобавитьРеквизит(Реквизиты, "organization", Заголовок, "DMOrganization", Истина, "Организация");
		КонецЕсли;
		
		// Внешний номер и дата.
		Если ПолучитьВсе Или НастройкиВида.externalNumberEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "externalNumber", НСтр("ru = '№ получателя'"), "Строка");
			ДобавитьРеквизит(Реквизиты, "externalDate", НСтр("ru = 'Дата получателя'"), "Дата");
		КонецЕсли;
		
		// Сумма.
		Если ПолучитьВсе Или НастройкиВида.sumEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "sum", НСтр("ru = 'Сумма'"), "Число",, "Сумма");
			ДобавитьРеквизит(Реквизиты, "VAT", НСтр("ru = 'В т.ч. НДС'"), "Число",, "СуммаНДС");
			ДобавитьРеквизит(Реквизиты, "currency", НСтр("ru = 'Валюта'"), "DMCurrency",, "Валюта");
		КонецЕсли;
		
		// Подписал от контрагента.
		Если ПолучитьВсе Или НастройкиВида.isIncoming = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "signerFromTheCorrespondent", НСтр("ru = 'Подписал'"), "DMContactPerson",, "ПодписалОтКонтрагента");
		КонецЕсли;
		
		// Адресат.
		Показывать = ПолучитьВсе;
		ТипЗначения = "DMEmployee";
		Если НастройкиВида.isIncoming = Истина Тогда
			Показывать = Истина;
			ТипЗначения = "DMEmployee";
			Обязательный = Ложь;
		ИначеЕсли НастройкиВида.isOutgoing = Истина Тогда
			Показывать = Истина;
			ТипЗначения = "DMContactPerson";
			Обязательный = Ложь;
		ИначеЕсли НастройкиВида.addresseeEnabled = Истина Тогда
			Показывать = Истина;
			ТипЗначения = "DMEmployee";
			Обязательный = Истина;
		КонецЕсли;
		Если Показывать Тогда
			ДобавитьРеквизит(Реквизиты, "addressee", НСтр("ru = 'Адресат'"), ТипЗначения, Обязательный, "Адресат");
		КонецЕсли;
		
		// Доставка.
		Показывать = ПолучитьВсе;
		Если НастройкиВида.isIncoming = Истина Тогда
			Показывать = Истина;
			Заголовок = НСтр("ru = 'Способ получения'");
		ИначеЕсли НастройкиВида.isOutgoing = Истина Тогда
			Показывать = Истина;
			Заголовок = НСтр("ru = 'Способ отправки'");
		КонецЕсли;
		Если Показывать Тогда
			ДобавитьРеквизит(Реквизиты, "deliveryMethod", Заголовок, "DMDeliveryMethod");
		КонецЕсли;
		
		// Отправка.
		Если ПолучитьВсе Или НастройкиВида.isOutgoing = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "sent", НСтр("ru = 'Отправлен'"), "Булево");
			ДобавитьРеквизит(Реквизиты, "sendDate", НСтр("ru = 'Дата отправки'"), "Дата");
		КонецЕсли;
		
		// Срок действия.
		Если ПолучитьВсе Или НастройкиВида.durationEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты,
				"beginDate", НСтр("ru = 'Дата начала действия'"), "Дата",, "ДатаНачалаДействия");
			ДобавитьРеквизит(Реквизиты,
				"endDate", НСтр("ru = 'Дата окончания действия'"), "Дата",, "ДатаОкончанияДействия");
			ДобавитьРеквизит(Реквизиты,
				"openEnded", НСтр("ru = 'Бессрочный'"), "Булево",, "Бессрочный");
			ДобавитьРеквизит(Реквизиты,
				"prolongationProcedure", НСтр("ru = 'Порядок продления'"), "DMProlongationProcedure",, "ПорядокПродления");
		КонецЕсли;
		
		// Суммы по статьям ДДС.
		Если ПолучитьВсе Или НастройкиВида.cashFlowDetailsEnabled = Истина Тогда
			ДобавитьТаблицу(Реквизиты, "cashFlowRows.rows", НСтр("ru = 'Статьи движения денежных средств'"));
			ДобавитьРеквизитТаблицы(Реквизиты, "cashFlowRows.rows", "item", НСтр("ru = 'Статья'"), "DMCashFlowItem", Истина);
			ДобавитьРеквизитТаблицы(Реквизиты, "cashFlowRows.rows", "total", НСтр("ru = 'Сумма'"), "Число");
			ДобавитьРеквизитТаблицы(Реквизиты, "cashFlowRows.rows", "VAT", НСтр("ru = 'Сумма НДС'"), "Число");
		КонецЕсли;
		
		// Табличная часть Товары.
		Если ПолучитьВсе Или НастройкиВида.productRowsEnabled = Истина Тогда
			ДобавитьТаблицу(Реквизиты, "productRows.rows", НСтр("ru = 'Товары и услуги'"));
			ДобавитьРеквизитТаблицы(Реквизиты,
				"productRows.rows", "product", НСтр("ru = 'Товар, услуга'"), "DMProduct");
			ДобавитьРеквизитТаблицы(Реквизиты,
				"productRows.rows", "price", НСтр("ru = 'Цена'"), "Число");
			ДобавитьРеквизитТаблицы(Реквизиты,
				"productRows.rows", "quantity", НСтр("ru = 'Количество'"), "Число");
			ДобавитьРеквизитТаблицы(Реквизиты,
				"productRows.rows", "measurementUnit", НСтр("ru = 'Единица измерения'"), "DMMeasurementUnit");
			ДобавитьРеквизитТаблицы(Реквизиты,
				"productRows.rows", "VATRate", НСтр("ru = 'Ставка НДС'"), "DMVATRate");
			ДобавитьРеквизитТаблицы(Реквизиты,
				"productRows.rows", "VAT", НСтр("ru = 'Сумма НДС'"), "Число");
			ДобавитьРеквизитТаблицы(Реквизиты,
				"productRows.rows", "total", НСтр("ru = 'Сумма'"), "Число");
		КонецЕсли;
		
		// Табличная часть Стороны.
		Если ПолучитьВсе Или (НастройкиВида.partiesEnabled = Истина И НастройкиВида.isAPaymentRequest <> Истина) Тогда
			ДобавитьТаблицу(Реквизиты, "partyRows.rows", НСтр("ru = 'Стороны'"));
			ДобавитьРеквизитТаблицы(Реквизиты,
				"partyRows.rows", "comment", НСтр("ru = 'Комментарий'"), "Строка");
			ДобавитьРеквизитТаблицы(Реквизиты,
				"partyRows.rows", "contact", НСтр("ru = 'Контактное лицо'"), ТипКонтактноеЛицоСтороны);
			ДобавитьРеквизитТаблицы(Реквизиты,
				"partyRows.rows", "description", НСтр("ru = 'Наименование стороны'"), "DMPartyName");
			ДобавитьРеквизитТаблицы(Реквизиты,
				"partyRows.rows", "party", НСтр("ru = 'Сторона'"), ТипСторона, Истина);
			ДобавитьРеквизитТаблицы(Реквизиты,
				"partyRows.rows", "setBy", НСтр("ru = 'Установил'"), "DMEmployee");
			ДобавитьРеквизитТаблицы(Реквизиты,
				"partyRows.rows", "signatureDate", НСтр("ru = 'Дата подписи'"), "Дата");
			ДобавитьРеквизитТаблицы(Реквизиты,
				"partyRows.rows", "signed", НСтр("ru = 'Подписан'"), "Булево");
			ДобавитьРеквизитТаблицы(Реквизиты,
				"partyRows.rows", "signedBy", НСтр("ru = 'Подписал'"), ТипКонтактноеЛицоСтороны);
		КонецЕсли;
		
	// Контрагент.
	ИначеЕсли ТипОбъектаДО = "DMCorrespondent" Тогда
		
		ДобавитьРеквизит(Реквизиты, "name", НСтр("ru = 'Наименование'"), "Строка", Истина, "Наименование");
		ДобавитьРеквизит(Реквизиты, "legalPrivatePerson", НСтр("ru = 'Вид контрагента'"), "DMLegalPrivatePerson", Истина, "ЮрФизЛицо");
		ДобавитьРеквизит(Реквизиты, "inn", НСтр("ru = 'ИНН'"), "Строка",, "ИНН");
		ДобавитьРеквизит(Реквизиты, "kpp", НСтр("ru = 'КПП'"), "Строка",, "КПП");
		ДобавитьРеквизит(Реквизиты, "okpo", НСтр("ru = 'Код по ОКПО'"), "Строка",, "КодПоОКПО");
		ДобавитьРеквизит(Реквизиты, "comment", НСтр("ru = 'Комментарий'"), "Строка",, "Комментарий");
		ДобавитьРеквизит(Реквизиты, "fullName", НСтр("ru = 'Полное наименование'"), "Строка",, "НаименованиеПолное");
		ДобавитьРеквизит(Реквизиты, "privatePerson", НСтр("ru = 'Физическое лицо'"), "DMPrivatePerson",, "ФизЛицо");
		ДобавитьРеквизит(Реквизиты, "responsible", НСтр("ru = 'Ответственный'"), "DMEmployee",, "Ответственный");
		
	// Мероприятие.
	ИначеЕсли ТипОбъектаДО = "DMMeeting" Тогда
		
		ДобавитьРеквизит(Реквизиты, "name", НСтр("ru = 'Наименование'"), "Строка", Истина, "Наименование");
		ДобавитьРеквизит(Реквизиты, "type", НСтр("ru = 'Вид мероприятия'"), "DMMeetingType", Истина, "ВидМероприятия");
		
		Если ПолучитьВсе Или НастройкиТипа.accessLevelEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "accessLevel", НСтр("ru = 'Гриф доступа'"), "DMAccessLevel", Истина, "ГрифДоступа");
		КонецЕсли;
		
		Если ПолучитьВсе Или НастройкиТипа.foldersEnabled = Истина Тогда
			ДобавитьРеквизит(Реквизиты, "folder", НСтр("ru = 'Папка'"), "DMMeetingFolder", Истина, "Папка");
		КонецЕсли;
		
		ТипОрганизатор = Новый СписокЗначений;
		ТипОрганизатор.Добавить("DMEmployee", НСтр("ru = 'Сотрудники'"));
		ТипОрганизатор.Добавить("DMCorrespondent", НСтр("ru = 'Контрагенты'"));
		ДобавитьРеквизит(Реквизиты, "calledBy", НСтр("ru = 'Организатор'"), ТипОрганизатор, Истина, "Организатор");
		
		ТипПредседатель = Новый СписокЗначений;
		ТипПредседатель.Добавить("DMEmployee", НСтр("ru = 'Сотрудники'"));
		ТипПредседатель.Добавить("DMCorrespondent", НСтр("ru = 'Контрагенты'"));
		ТипПредседатель.Добавить("DMContactPerson", НСтр("ru = 'Контактные лица'"));
		ДобавитьРеквизит(Реквизиты, "chairPerson", НСтр("ru = 'Председатель'"), ТипПредседатель,, "Председатель");
		
		ТипСекретарь = Новый СписокЗначений;
		ТипСекретарь.Добавить("DMEmployee", НСтр("ru = 'Сотрудники'"));
		ТипСекретарь.Добавить("DMCorrespondent", НСтр("ru = 'Контрагенты'"));
		ТипСекретарь.Добавить("DMContactPerson", НСтр("ru = 'Контактные лица'"));
		ДобавитьРеквизит(Реквизиты, "secretary", НСтр("ru = 'Секретарь'"), ТипСекретарь,, "Секретарь");
		
		ДобавитьРеквизит(Реквизиты, "comment", НСтр("ru = 'Комментарий'"), "Строка",, "Комментарий");
		ДобавитьРеквизит(Реквизиты, "description", НСтр("ru = 'Описание мероприятия'"), "Строка",, "Описание");
		
		ДобавитьРеквизит(Реквизиты, "startDate", НСтр("ru = 'Дата и время начала'"), "Дата",, "ДатаНачала");
		ДобавитьРеквизит(Реквизиты, "endDate", НСтр("ru = 'Дата и время окончания'"), "Дата",, "ДатаОкончания");
		
		ДобавитьРеквизит(Реквизиты, "organization", НСтр("ru = 'Организация'"), "DMOrganization",, "Организация");
		ДобавитьРеквизит(Реквизиты, "place", НСтр("ru = 'Место проведения'"), "Строка",, "МестоПроведения");
		ДобавитьРеквизит(Реквизиты, "project", НСтр("ru = 'Проект'"), "DMProject",, "Проект");
		
		// Табличная часть Программа
		ДобавитьТаблицу(Реквизиты, "agenda", НСтр("ru = 'Программа'"));
		ДобавитьРеквизитТаблицы(Реквизиты, "agenda", "employee", НСтр("ru = 'Исполнитель'"), "DMEmployee");
		ДобавитьРеквизитТаблицы(Реквизиты,
			"agenda", "role", НСтр("ru = 'Роль исполнителя'"), "DMBusinessProcessExecutorRole");
		ДобавитьРеквизитТаблицы(Реквизиты,
			"agenda", "mainAddressingItem", НСтр("ru = 'Основной объект адресации исполнителя'"), "DMMainAddressingObject");
		ДобавитьРеквизитТаблицы(Реквизиты,
			"agenda", "secondaryAddressingItem", НСтр("ru = 'Дополнительный объект адресации исполнителя'"), "DMSecondaryAddressingObject");
		ДобавитьРеквизитТаблицы(Реквизиты,
			"agenda", "number", НСтр("ru = 'Номер пункта'"), "Число");
		ДобавитьРеквизитТаблицы(Реквизиты,
			"agenda", "description", НСтр("ru = 'Содержание'"), "Строка");
		ДобавитьРеквизитТаблицы(Реквизиты,
			"agenda", "comment", НСтр("ru = 'Комментарий'"), "Строка");
		ДобавитьРеквизитТаблицы(Реквизиты,
			"agenda", "decisionIsNecessary", НСтр("ru = 'Требует принятия решения'"), "Булево");
		ДобавитьРеквизитТаблицы(Реквизиты,
			"agenda", "estimatedDuration", НСтр("ru = 'Время план (секунд)'"), "Число");
		ДобавитьРеквизитТаблицы(Реквизиты,
			"agenda", "duration", НСтр("ru = 'Время факт (секунд)'"), "Число");
		ДобавитьРеквизитТаблицы(Реквизиты,
			"agenda", "startDate", НСтр("ru = 'Дата и время начала'"), "Дата");
		ДобавитьРеквизитТаблицы(Реквизиты,
			"agenda", "endDate", НСтр("ru = 'Дата и время окончания'"), "Дата");
		
		// Табличная часть Участники
		ДобавитьТаблицу(Реквизиты, "attendees", НСтр("ru = 'Участники'"));
		ДобавитьРеквизитТаблицы(Реквизиты,
			"attendees", "absent", НСтр("ru = 'Отсутствовал'"), "Булево");
		ДобавитьРеквизитТаблицы(Реквизиты,
			"attendees", "invitationStatus", НСтр("ru = 'Состояние приглашения'"), "DMInvitationStatus");
		ДобавитьРеквизитТаблицы(Реквизиты,
			"attendees", "mainAddressingObject", НСтр("ru = 'Основной объект адресации участника'"), "DMMainAddressingObject");
		ДобавитьРеквизитТаблицы(Реквизиты,
			"attendees", "obligatory", НСтр("ru = 'Явка обязательна'"), "Булево");
		ДобавитьРеквизитТаблицы(Реквизиты,
			"attendees", "role", НСтр("ru = 'Роль исполнителя'"), "DMBusinessProcessExecutorRole");
		ДобавитьРеквизитТаблицы(Реквизиты,
			"attendees", "secondaryAddressingObject", НСтр("ru = 'Дополнительный объект адресации участника'"), "DMSecondaryAddressingObject");
		ДобавитьРеквизитТаблицы(Реквизиты,
			"attendees", "employee", НСтр("ru = 'Пользователь'"), "DMEmployee");
		
		// Табличная часть Состояния
		ДобавитьТаблицу(Реквизиты, "statuses", НСтр("ru = 'Состояния'"));
		ДобавитьРеквизитТаблицы(Реквизиты,
			"statuses", "objectID.ID", НСтр("ru = 'Состояние'"), "Строка");
		
	КонецЕсли;
	
	// Дополнительные свойства.
	Для Каждого ДопСвойство Из ДопРеквизиты Цикл
		
		Если Не ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ДопСвойство, "propertyValueTypes") Тогда
			Продолжить;
		КонецЕсли;
		
		Типы = Новый СписокЗначений;
		Для Каждого ОписаниеТипа Из ДопСвойство.propertyValueTypes Цикл
			Если ОписаниеТипа.xdtoClassName = "integer" Тогда
				ТипЗначения = "Число";
			ИначеЕсли ОписаниеТипа.xdtoClassName = "boolean" Тогда
				ТипЗначения = "Булево";
			ИначеЕсли ОписаниеТипа.xdtoClassName = "string" Тогда
				ТипЗначения = "Строка";
			ИначеЕсли ОписаниеТипа.xdtoClassName = "date" Тогда
				ТипЗначения = "Дата";
			Иначе
				ТипЗначения = ОписаниеТипа.xdtoClassName;
			КонецЕсли;
			Типы.Добавить(ТипЗначения, ОписаниеТипа.presentation);
		КонецЦикла;
		
		ДобавитьДополнительныйРеквизит(Реквизиты,
			ДопСвойство.name,
			Типы,
			ДопСвойство.objectID.ID,
			ДопСвойство.objectID.type);
		
	КонецЦикла;
	
	Возврат Реквизиты;
	
КонецФункции

// Проверяет выражение на встроенном языке, используемое для заполнения реквизита при получении данных из ДО.
//
// Параметры:
//   ВычисляемоеВыражение - Строка - выражение на встроенном языке.
//   МестоВыполненияВыражения - ПеречислениеСсылка.МестаВыполненияВыраженийНаВстроенномЯзыке - указывает где
//     именно нужно выполнять выражение на встроенном языке, на стороне ДО или на стороне ИС.
//   ТипОбъектаДО - Строка - тип объекта 1С:Документооборот.
//   ТипОбъектаИС - Строка - тип объекта интегрированной системы.
//   КонтекстВыражения - см. Справочники.ПравилаИнтеграцииС1СДокументооборотом3.КонтекстВыраженияНаВстроенномЯзыке
//   ЭтоТаблица - Булево - это выражение для заполнения таблицы.
//
Процедура ПроверитьВыражениеПравилаЗагрузки(ВычисляемоеВыражение, МестоВыполненияВыражения, ТипОбъектаДО,
		ТипОбъектаИС, КонтекстВыражения, ЭтоТаблица = Ложь) Экспорт
	
	Если СокрЛП(ВычисляемоеВыражение) = "" Или Не ЗначениеЗаполнено(МестоВыполненияВыражения) Тогда
		Возврат;
	КонецЕсли;
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	Если МестоВыполненияВыражения = Перечисления.МестаВыполненияВыраженийНаВстроенномЯзыке.НаСторонеИС Тогда
		
		Если КонтекстВыражения.ОбъектXDTO = Неопределено Тогда
			КонтекстВыражения.ОбъектXDTO = ИнтеграцияС1СДокументооборотБазоваяФункциональность.НовыйОбъектДО(
				Прокси,
				ТипОбъектаДО,,,
				КонтекстВыражения.ВидДокументаДОID);
		КонецЕсли;
		
		Если КонтекстВыражения.ОбъектИС = Неопределено Тогда
			КонтекстВыражения.ОбъектИС = ИнтеграцияС1СДокументооборотБазоваяФункциональность.НовыйОбъектИС(ТипОбъектаИС);
		КонецЕсли;
		
		ТекущаяСтрока = Неопределено;
		Если КонтекстВыражения.ТаблицаИсточник <> "" Тогда
			ЧастиСтроки = СтрРазделить(КонтекстВыражения.ТаблицаИсточник, ".");
			URIПространстваИменВебСервисаДокументооборота =
				ИнтеграцияС1СДокументооборотБазоваяФункциональность.URIПространстваИменВебСервисаДокументооборота();
			ТипОбъектаXDTO = Прокси.ФабрикаXDTO.Пакеты.Получить(
				URIПространстваИменВебСервисаДокументооборота).Получить(ТипОбъектаДО);
			
			Если ЧастиСтроки.Количество() = 1 Тогда
				ТипСтроки = ТипОбъектаXDTO.Свойства.Получить(ЧастиСтроки[0]).Тип.Имя;
				
			ИначеЕсли ЧастиСтроки.Количество() = 2 Тогда
				ТипСтроки = ТипОбъектаXDTO.Свойства.Получить(ЧастиСтроки[0]).Тип.Свойства.Получить(ЧастиСтроки[1]).Тип.Имя;
				
			Иначе
				ТипСтроки = "";
				
			КонецЕсли;
			
			Если ТипСтроки <> "" Тогда
				ТекущаяСтрока = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, ТипСтроки);
			КонецЕсли;
		КонецЕсли;
		
		Результат = ИнтеграцияС1СДокументооборот3.РезультатВыраженияНаВстроенномЯзыке(
			ВычисляемоеВыражение,
			КонтекстВыражения.ОбъектXDTO,
			КонтекстВыражения.ОбъектИС,
			ТекущаяСтрока);
		Если ЭтоТаблица И ТипЗнч(Результат) <> Тип("ТаблицаЗначений") Тогда
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Для заполнения таблицы результат должен иметь тип %1'"),
				"ТаблицаЗначений");
		КонецЕсли;
		
	ИначеЕсли МестоВыполненияВыражения = Перечисления.МестаВыполненияВыраженийНаВстроенномЯзыке.НаСторонеДО Тогда
		
		Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(
			Прокси, "DMCheckExpressionOnBuiltInLanguageRequest");
		Запрос.tabularSectionName = КонтекстВыражения.ТаблицаИсточник;
		Запрос.expressionOnBuiltInLanguage = ВычисляемоеВыражение;
		
		Запрос.objectType = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMType");
		Запрос.objectType.xdtoClassName = ТипОбъектаДО;
		Запрос.objectType.presentation = ТипОбъектаДО;
		
		РезультатПроверкиВыражения = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, РезультатПроверкиВыражения);
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет проверку правила интеграции.
//
// Параметры:
//   ПравилоИнтеграции - СправочникОбъект.ПравилаИнтеграцииС1СДокументооборотом3 - правило интеграции для
//     заполнения объекта ИС.
//
Процедура ПроверитьПравило(ПравилоИнтеграции) Экспорт
	
	Если Не ЗначениеЗаполнено(ПравилоИнтеграции.ТипОбъектаДО) Тогда
		ВызватьИсключение НСтр("ru = 'Не задан тип объекта 1С:Документооборот'");
	КонецЕсли;
	
	РеквизитыТекущегоВидаДокументаДО = РеквизитыОбъектаДО(
		ПравилоИнтеграции.ТипОбъектаДО,
		ПравилоИнтеграции.ВидДокументаДОID,
		Ложь);
	
	ВсеРеквизитыОбъектаДО = РеквизитыОбъектаДО(
		ПравилоИнтеграции.ТипОбъектаДО,
		ПравилоИнтеграции.ВидДокументаДОID,
		Истина);
	
	РеквизитыОбъектаИС = ИнтеграцияС1СДокументооборотБазоваяФункциональность.РеквизитыОбъектаИС(
		ПравилоИнтеграции.ТипОбъектаИС);
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	ОбъектXDTO = ИнтеграцияС1СДокументооборотБазоваяФункциональность.НовыйОбъектДО(
		Прокси,
		ПравилоИнтеграции.ТипОбъектаДО,,,
		ПравилоИнтеграции.ВидДокументаДОID);
	ОбъектИС = ИнтеграцияС1СДокументооборотБазоваяФункциональность.НовыйОбъектИС(ПравилоИнтеграции.ТипОбъектаИС);
	
	СписокОшибок = Новый Массив;
	
	ПроверитьПравилаЗаполненияРеквизитовИС(
		ПравилоИнтеграции,
		ОбъектИС,
		ОбъектXDTO,
		РеквизитыОбъектаИС,
		РеквизитыТекущегоВидаДокументаДО,
		ВсеРеквизитыОбъектаДО,
		СписокОшибок);
	
	Если СписокОшибок.Количество() > 0 Тогда
		ВызватьИсключение СтрСоединить(СписокОшибок, Символы.ПС + Символы.ПС);
	КонецЕсли;
	
КонецПроцедуры

// По ссылке на правило загрузки данных в ДО ищет в ИС связанные правила интеграции. Возвращает Истина в случае
// наличия, и Ложь, если связанных правил интеграции нет.
//
// Параметры:
//   Сообщение - ОбъектXDTO - объект XDTO типа DMILRelatedIntegrationRulesExistenceRequest.
//
// Возвращаемое значение:
//   ОбъектXDTO - объект XDTO типа DMILRelatedIntegrationRulesExistenceResponse или DMError.
//
Функция СвязанныеПравилаИнтеграцииСуществуют(Сообщение) Экспорт
	
	Попытка
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ПравилаИнтеграцииС1СДокументооборотом3.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.ПравилаИнтеграцииС1СДокументооборотом3 КАК ПравилаИнтеграцииС1СДокументооборотом3
			|ГДЕ
			|	ПравилаИнтеграцииС1СДокументооборотом3.ПравилоЗагрузкиДанныхВДО = &ПравилоЗагрузкиДанныхВДО
			|	И НЕ ПравилаИнтеграцииС1СДокументооборотом3.ПометкаУдаления");
		Запрос.УстановитьПараметр("ПравилоЗагрузкиДанныхВДО", Сообщение.relatedDataLoadingRule);
		
		Ответ = ИнтеграцияС1СДокументооборот3.СоздатьОбъектБИД("DMILRelatedIntegrationRulesExistenceResponse");
		Ответ.exist = Не Запрос.Выполнить().Пустой();
		
	Исключение
		
		Ответ = ИнтеграцияС1СДокументооборот3.ОписаниеОшибкиXDTO(
			НСтр("ru = 'Ошибка при поиске связанных правил интеграции'"),
			ИнтеграцияС1СДокументооборот3.ПолучитьОписаниеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

// Создает для правила загрузки данных в ДО связанное с ним правило интеграции.
//
// Параметры:
//   Сообщение - ОбъектXDTO - объект XDTO типа DMILCreateRelatedIntegrationRuleExistenceRequest.
//
// Возвращаемое значение:
//   ОбъектXDTO - объект XDTO типа DMOK или DMError.
//
Функция СоздатьСвязанноеПравилоИнтеграции(Сообщение) Экспорт
	
	Попытка
		
		Ответ = ИнтеграцияС1СДокументооборот3.СоздатьОбъектБИД("DMILOK");
		
		ПравилоОбъект = СоздатьЭлемент();
		ПравилоОбъект.Заполнить(Неопределено);
		
		ПравилоОбъект.ПравилоЗагрузкиДанныхВДО = Сообщение.relatedDataLoadingRule;
		ПравилоОбъект.ВидДокументаДО = Сообщение.documentTypeDM;
		ПравилоОбъект.ВидДокументаДОID = Сообщение.documentTypeDMID;
		ПравилоОбъект.ТипОбъектаДО = Сообщение.objectTypeDM;
		ПравилоОбъект.ПредставлениеОбъектаДО = Сообщение.objectNameDM;
		
		ПравилоОбъект.ТипОбъектаИС = Сообщение.objectTypeIS;
		ПравилоОбъект.ПредставлениеОбъектаИС = Метаданные.НайтиПоПолномуИмени(ПравилоОбъект.ТипОбъектаИС).Представление();
		
		Если ИнтеграцияС1СДокументооборот3.ЭтоДокументДО3(ПравилоОбъект.ТипОбъектаДО)
				И ЗначениеЗаполнено(ПравилоОбъект.ВидДокументаДОID) Тогда
			НоваяСтрока = ПравилоОбъект.КлючевыеРеквизитыДО.Добавить();
			НоваяСтрока.Имя = "documentType";
			НоваяСтрока.ЗначениеРеквизита = ПравилоОбъект.ВидДокументаДО;
			НоваяСтрока.ЗначениеРеквизитаID = ПравилоОбъект.ВидДокументаДОID;
			НоваяСтрока.ЗначениеРеквизитаТип = "DMDocumentType";
		КонецЕсли;
		
		ПравилоОбъект.Записать();
		
	Исключение
		
		Ответ = ИнтеграцияС1СДокументооборот3.ОписаниеОшибкиXDTO(
			НСтр("ru = 'Ошибка при создании правила интеграции'"),
			ИнтеграцияС1СДокументооборот3.ПолучитьОписаниеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

// Возвращает структуру таблицы реквизитов объекта ДО.
//
// Возвращаемое значение:
//   ТаблицаЗначений:
//     * Имя - Строка
//     * Представление - Строка
//     * Тип - СписокЗначений из Строка
//     * ДопРеквизит - Булево
//     * ДопРеквизитID - Строка
//     * ДопРеквизитТип - Строка
//     * ОбязательноеЗаполнение - Булево
//     * ЭтоТаблица - Булево
//     * Таблица - Строка
//
Функция СтруктураТаблицыРеквизитовДО() Экспорт
	
	Реквизиты = Новый ТаблицаЗначений;
	Реквизиты.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("Тип", Новый ОписаниеТипов("СписокЗначений"));
	Реквизиты.Колонки.Добавить("ДопРеквизит", Новый ОписаниеТипов("Булево"));
	Реквизиты.Колонки.Добавить("ДопРеквизитID", Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("ДопРеквизитТип", Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("ОбязательноеЗаполнение", Новый ОписаниеТипов("Булево"));
	Реквизиты.Колонки.Добавить("ЭтоТаблица", Новый ОписаниеТипов("Булево"));
	Реквизиты.Колонки.Добавить("Таблица", Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("ТаблицаДляСортировки", Новый ОписаниеТипов("Строка"));
	Реквизиты.Колонки.Добавить("ИмяРеквизитаВДО", Новый ОписаниеТипов("Строка"));
	
	Возврат Реквизиты;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеОбъектовИС

// Возвращает данные реквизита объекта ИС.
//
// Параметры:
//   РеквизитыОбъектаИС - см. ИнтеграцияС1СДокументооборотБазоваяФункциональность.РеквизитыОбъектаИС
//   ИмяРеквизитаОбъектаИС - Строка - имя заполняемого реквизита объекта ИС.
//   ИмяТаблицыОбъектаИС - Строка - имя заполняемой таблицы объекта ИС.
//   ЗаполняемыйОбъектИС - Произвольный - заполняемый объект ИС. Требуется для формирования сообщения в случае
//     возникновения ошибки.
//
// Возвращаемое значение:
//   СтрокаТаблицыЗначений - строка таблицы из метода
//     ИнтеграцияС1СДокументооборотБазоваяФункциональность.РеквизитыОбъектаИС.
//
Функция ДанныеРеквизита(РеквизитыОбъектаИС, ИмяРеквизитаОбъектаИС, ИмяТаблицыОбъектаИС, ЗаполняемыйОбъектИС)
	
	ДанныеРеквизитаСтроки = РеквизитыОбъектаИС.НайтиСтроки(
		Новый Структура("Имя, Таблица", ИмяРеквизитаОбъектаИС, ИмяТаблицыОбъектаИС));
	Если ДанныеРеквизитаСтроки.Количество() = 1 Тогда
		ДанныеРеквизита = ДанныеРеквизитаСтроки[0];
	Иначе
		Если ИмяТаблицыОбъектаИС = "" Тогда
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'В метаданных объекта ""%1"" не удалось найти реквизит с именем ""%2""'"),
				ЗаполняемыйОбъектИС,
				ИмяРеквизитаОбъектаИС);
		Иначе
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'В метаданных объекта ""%1"" не удалось найти реквизит таблицы ""%2"" с именем ""%3""'"),
				ЗаполняемыйОбъектИС,
				ИмяТаблицыОбъектаИС,
				ИмяРеквизитаОбъектаИС);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеРеквизита;
	
КонецФункции

// Считывает значение свойства из объекта XDTO. По умолчанию значение получается по имени свойства.
// Исключение составляют получение статуса документа и стороны заявки на оплату.
//
// Параметры:
//   ОбъектXDTO - ОбъектXDTO - объект ДО, источник данных заполнения.
//   ЗначениеДляЗаполнения - см. ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗначениеДляЗаполненияРеквизитаОбъектаИС
//   ЯвляетсяЗаявкойНаОплату - Булево - документ ДО является заявкой на оплату.
//
Процедура ЗаполнитьЗначениеСвойстваИзРеквизитаОбъектаXDTO(ОбъектXDTO, ЗначениеДляЗаполнения, ЯвляетсяЗаявкойНаОплату)
	
	Если ЯвляетсяЗаявкойНаОплату И ЗначениеДляЗаполнения.ИмяСвойства = "recipient" Тогда
		ЗначениеДляЗаполнения.ЗначениеСвойства = СторонаЗаявкиНаОплатуXDTO(
			ОбъектXDTO,
			"recipient",
			ЗначениеДляЗаполнения.ТипОбъектаДО);
		
	ИначеЕсли СтрНайти(ЗначениеДляЗаполнения.ИмяСвойства, "status_") = 1 Тогда
		ВидДействияID = СтрЗаменить(Прав(ЗначениеДляЗаполнения.ИмяСвойства, 36), "_", "-");
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(
				ОбъектXDTO, "statusByActionTypeRows") Тогда
			Для Каждого СтрокаСтатус Из ОбъектXDTO.statusByActionTypeRows.rows Цикл
				Если СтрокаСтатус.actionType.objectID.ID = ВидДействияID Тогда
					ЗначениеДляЗаполнения.ЗначениеСвойства = СтрокаСтатус.status;
					ЗначениеДляЗаполнения.ТипОбъектаДО = "DMDocumentStatus";
					Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(
							СтрокаСтатус, "statusPresentation") Тогда
						ЗначениеДляЗаполнения.ПредставлениеДляПоиска = СтрокаСтатус.statusPresentation;
					КонецЕсли;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	Иначе
		ЗначениеДляЗаполнения.ЗначениеСвойства = ОбъектXDTO[ЗначениеДляЗаполнения.ИмяСвойства];
		ЗначениеДляЗаполнения.ТипОбъектаДО = ОбъектXDTO.Тип().Свойства.Получить(ЗначениеДляЗаполнения.ИмяСвойства).Тип.Имя;
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет реквизит объекта ИС из значения свойства объекта XDTO.
//
// Параметры:
//   Прокси - WSПрокси - объект для подключения к web-сервисам Документооборота.
//   Реквизит - Произвольный - реквизит объекта ИС, который требуется заполнить.
//   ЗначениеДляЗаполнения - см. ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗначениеДляЗаполненияРеквизитаОбъектаИС
//   ПравилоРеквизита - СтрокаТаблицыЗначений - правило заполнения реквизита. Строка ТЧ ПравилаЗаполненияРеквизитовИС.
//   ДанныеРеквизита - СтрокаТаблицыЗначений - строка таблицы из метода
//     ИнтеграцияС1СДокументооборотБазоваяФункциональность.РеквизитыОбъектаИС.
//   ЗаполняемыйОбъектИС - Произвольный - заполняемый объект ИС.
//   НомерСтроки - Число - передается при заполнении определенной строки таблицы объекта.
//
Процедура ЗаполнитьРеквизитИзЗначенияСвойства(Прокси, Реквизит, ЗначениеДляЗаполнения, ПравилоРеквизита,
		ДанныеРеквизита, ЗаполняемыйОбъектИС, НомерСтроки = Неопределено)
	
	Контекст = ИнтеграцияС1СДокументооборотБазоваяФункциональность.КонтекстДляЗаполненияРеквизитаОбъектаИС();
	Контекст.ЗаполняемыйОбъектИС = ЗаполняемыйОбъектИС;
	Контекст.ИмяРеквизитаОбъектаИС = ПравилоРеквизита.ИмяРеквизитаОбъектаИС;
	Контекст.ЭтоДополнительныйРеквизитИС = ПравилоРеквизита.ЭтоДополнительныйРеквизитИС;
	Контекст.ДополнительныйРеквизитИС = ПравилоРеквизита.ДополнительныйРеквизитИС;
	
	Если ПравилоРеквизита.ЭтоДополнительныйРеквизитИС Тогда
		ОписаниеТиповРеквизитаИС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ПравилоРеквизита.ДополнительныйРеквизитИС,
			"ТипЗначения");
	Иначе
		ОписаниеТиповРеквизитаИС = ДанныеРеквизита.Тип;
	КонецЕсли;
	ТипРеквизитаОбъектаИС = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПодходящийТип(
		ОписаниеТиповРеквизитаИС.Типы(),
		ЗначениеДляЗаполнения.ЗначениеСвойства,
		ТипЗнч(Реквизит));
	
	Для Каждого СвязьПараметровВыбора Из ДанныеРеквизита.СвязиПараметровВыбора Цикл
		РазделенныйПуть = СтрРазделить(СвязьПараметровВыбора.ПутьКДанным, ".");
		Если РазделенныйПуть.Количество() = 2 И РазделенныйПуть[0] = ДанныеРеквизита.Таблица Тогда
			Пока ЗаполняемыйОбъектИС[ДанныеРеквизита.Таблица].Количество() - 1 < НомерСтроки Цикл
				ЗаполняемыйОбъектИС[ДанныеРеквизита.Таблица].Добавить();
			КонецЦикла;
			Строка = ЗаполняемыйОбъектИС[ДанныеРеквизита.Таблица][НомерСтроки];
			Контекст.ПараметрыВыбора[СвязьПараметровВыбора.Имя] = Строка[РазделенныйПуть[1]];
		ИначеЕсли РазделенныйПуть.Количество() = 1 Тогда
			Контекст.ПараметрыВыбора[СвязьПараметровВыбора.Имя] = ЗаполняемыйОбъектИС[РазделенныйПуть[0]];
		Иначе
			// Прочие варианты пути к данным не поддерживаются.
			Продолжить;
		КонецЕсли;
	КонецЦикла;
	ЗначениеДляЗаполнения.Владелец = Контекст.ПараметрыВыбора["Отбор.Владелец"];
	
	Если ПравилоРеквизита.ЭтоДополнительныйРеквизитИС
			И (ТипРеквизитаОбъектаИС = Тип("СправочникСсылка.ЗначенияСвойствОбъектов")
				Или ТипРеквизитаОбъектаИС = Тип("СправочникСсылка.ЗначенияСвойствОбъектовИерархия")) Тогда
		ЗначениеДляЗаполнения.Владелец = ПравилоРеквизита.ДополнительныйРеквизитИС;
	КонецЕсли;
	
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЭтоОбъектноеЗначение(
			Прокси, ЗначениеДляЗаполнения.ЗначениеСвойства) Тогда
		
		ЗначениеДляЗаполнения.ИмяДляПоиска = ЗначениеДляЗаполнения.ЗначениеСвойства.name;
		ЗначениеДляЗаполнения.ИдентификаторОбъектаДО = ЗначениеДляЗаполнения.ЗначениеСвойства.objectID.ID;
		ЗначениеДляЗаполнения.ТипОбъектаДО = ЗначениеДляЗаполнения.ЗначениеСвойства.objectID.type;
		ЗначениеДляЗаполнения.ПредставлениеДляПоиска = ЗначениеДляЗаполнения.ЗначениеСвойства.objectID.presentation;
		ЗначениеДляЗаполнения.ExternalObject = ЗначениеДляЗаполнения.ЗначениеСвойства.externalObject;
		
	ИначеЕсли ТипЗнч(ЗначениеДляЗаполнения.ЗначениеСвойства) = Тип("Строка") Тогда
		
		Если ПустаяСтрока(ЗначениеДляЗаполнения.ИмяДляПоиска) Тогда
			ЗначениеДляЗаполнения.ИмяДляПоиска = ЗначениеДляЗаполнения.ЗначениеСвойства;
		КонецЕсли;
		Если ПустаяСтрока(ЗначениеДляЗаполнения.ПредставлениеДляПоиска) Тогда
			ЗначениеДляЗаполнения.ПредставлениеДляПоиска = ЗначениеДляЗаполнения.ЗначениеСвойства;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ЗначениеДляЗаполнения.ЗначениеСвойства) = Тип("Число")
			Или ТипЗнч(ЗначениеДляЗаполнения.ЗначениеСвойства) = Тип("Дата") Тогда
		
		Если ПустаяСтрока(ЗначениеДляЗаполнения.ИмяДляПоиска) Тогда
			ЗначениеДляЗаполнения.ИмяДляПоиска = Строка(ЗначениеДляЗаполнения.ЗначениеСвойства);
		КонецЕсли;
		
	КонецЕсли;
	
	Реквизит = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗначениеРеквизитаОбъектаИСПоЗначениюДляЗаполнения(
		ТипРеквизитаОбъектаИС,
		ЗначениеДляЗаполнения,
		Контекст);
	
КонецПроцедуры

// Заполняет реквизит по правилу заполнения, указанному в правилах интеграции.
//
// Параметры:
//   Прокси - WSПрокси - объект для подключения к web-сервисам Документооборота.
//   Реквизит - Произвольный - реквизит объекта ИС, который требуется заполнить.
//   ПравилоРеквизита - СтрокаТаблицыЗначений - правило заполнения реквизита. Строка ТЧ ПравилаЗаполненияРеквизитовИС.
//   ДанныеРеквизита - СтрокаТаблицыЗначений - строка таблицы из метода
//     ИнтеграцияС1СДокументооборотБазоваяФункциональность.РеквизитыОбъектаИС.
//   КонтекстЗаполненияПоПравилу - см. КонтекстЗаполненияПоПравилу
//   НомерСтроки - Число - передается при заполнении определенной строки таблицы объекта.
//   ТекущаяСтрока - Произвольный - передается в качестве контекста для выражения на встроенном языке, для заполнения
//     реквизита строки табличной части.
//
Процедура ЗаполнитьРеквизитПоПравилу(Прокси, Реквизит, ПравилоРеквизита, ДанныеРеквизита, КонтекстЗаполненияПоПравилу,
		НомерСтроки = Неопределено, ТекущаяСтрока = Неопределено)
	
	ОбъектXDTO = КонтекстЗаполненияПоПравилу.ОбъектXDTO; // ОбъектXDTO
	ЗначениеДляЗаполнения = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗначениеДляЗаполненияРеквизитаОбъектаИС();
	ЗначениеДляЗаполнения.ТипXDTOОбъекта = ОбъектXDTO.Тип().Имя;
	ЗначениеДляЗаполнения.ИмяСвойства = ПравилоРеквизита.ИмяРеквизитаОбъектаДО;
	
	Если ПравилоРеквизита.Вариант = ВариантЗначение() Тогда
		// Из указанного значения.
		Реквизит = ПравилоРеквизита.ЗначениеРеквизитаИС;
		
	ИначеЕсли ПравилоРеквизита.Вариант = ВариантРеквизит() Тогда
		// Из реквизита объекта ДО.
		
		Если Не ПравилоРеквизита.ЭтоДополнительныйРеквизитДО Тогда
			
			Если НомерСтроки = Неопределено Тогда
				// Заполняем реквизит объекта ДО.
				ЗаполнитьЗначениеСвойстваИзРеквизитаОбъектаXDTO(
					ОбъектXDTO,
					ЗначениеДляЗаполнения,
					КонтекстЗаполненияПоПравилу.ЯвляетсяЗаявкойНаОплату);
				
			Иначе
				// Заполняем реквизит табличной части объекта ДО.
				РазделеннаяСтрока = СтрРазделить(ПравилоРеквизита.ИмяРеквизитаОбъектаДО, ".", Ложь);
				
				Если РазделеннаяСтрока.Количество() = 1 Тогда // "свойство"
					ЗаполнитьЗначениеСвойстваИзРеквизитаОбъектаXDTO(
						ОбъектXDTO,
						ЗначениеДляЗаполнения,
						КонтекстЗаполненияПоПравилу.ЯвляетсяЗаявкойНаОплату);
					
				ИначеЕсли РазделеннаяСтрока.Количество() = 2 Тогда // "список.свойство"
					СписокXDTO = ОбъектXDTO[РазделеннаяСтрока[0]];
					ЗначениеДляЗаполнения.ЗначениеСвойства = СписокXDTO[НомерСтроки][РазделеннаяСтрока[1]];
					
				ИначеЕсли РазделеннаяСтрока.Количество() = 3 Тогда // "список.список.свойство"
					СписокXDTO = ОбъектXDTO[РазделеннаяСтрока[0]][РазделеннаяСтрока[1]];
					ЗначениеДляЗаполнения.ЗначениеСвойства = СписокXDTO[НомерСтроки][РазделеннаяСтрока[2]];
					
				Иначе
					ВызватьИсключение СтрШаблон(
						НСтр("ru = 'В правилах интеграции указано недопустимое имя реквизита-источника данных заполнения: %1'"),
						ПравилоРеквизита.ИмяРеквизитаОбъектаДО);
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			// Это дополнительный реквизит ДО.
			
			Для Каждого Строка Из ОбъектXDTO.additionalProperties Цикл
				Если Строка.objectID.ID = ПравилоРеквизита.ДополнительныйРеквизитДОID
						И Строка.objectID.type = ПравилоРеквизита.ДополнительныйРеквизитДОТип Тогда
					Если Строка.propertySimpleValue <> Неопределено Тогда
						ЗначениеДляЗаполнения.ЗначениеСвойства = Строка.propertySimpleValue;
					ИначеЕсли Строка.propertyObjectValue <> Неопределено Тогда
						ЗначениеДляЗаполнения.ЗначениеСвойства = Строка.propertyObjectValue;
					КонецЕсли;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		ЗаполнитьРеквизитИзЗначенияСвойства(
			Прокси,
			Реквизит,
			ЗначениеДляЗаполнения,
			ПравилоРеквизита,
			ДанныеРеквизита,
			КонтекстЗаполненияПоПравилу.ЗаполняемыйОбъектИС,
			НомерСтроки);
		
	// Из выражения на встроенном языке.
	ИначеЕсли ПравилоРеквизита.Вариант = ВариантВыражение() Тогда
		
		Если ПравилоРеквизита.МестоВыполненияВыражения =
				Перечисления.МестаВыполненияВыраженийНаВстроенномЯзыке.НаСторонеДО Тогда
			
			// Выражение уже было выполнено на стороне ДО, его результат находится во входящих данных.
			Если НомерСтроки = Неопределено Тогда
				// Заполняем реквизит объекта ДО.
				ЗначениеXDTO = ЗначениеИзВходящихДанных(
					ОбъектXDTO.expressionResultsForAttributes, ПравилоРеквизита.id);
			Иначе
				// Заполняем реквизит табличной части объекта ДО.
				ТаблицаXDTO = ЗначениеИзВходящихДанных(
					ОбъектXDTO.expressionResultsForTabularSections, ПравилоРеквизита.id);
				Если ТаблицаXDTO = Неопределено Тогда
					ЗначениеXDTO = Неопределено;
				Иначе
					ЗначениеXDTO = ТаблицаXDTO.rows[НомерСтроки].attributes[0];
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеXDTO = Неопределено Тогда
				Возврат;
				
			ИначеЕсли ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ЗначениеXDTO, "error") Тогда
				ВызватьИсключение ЗначениеXDTO.error;
				
			ИначеЕсли ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ЗначениеXDTO, "value") Тогда
				ЗначениеДляЗаполнения.ЗначениеСвойства = ЗначениеXDTO.value;
				ЗаполнитьРеквизитИзЗначенияСвойства(
					Прокси,
					Реквизит,
					ЗначениеДляЗаполнения,
					ПравилоРеквизита,
					ДанныеРеквизита,
					КонтекстЗаполненияПоПравилу.ЗаполняемыйОбъектИС,
					НомерСтроки);
				
			КонецЕсли;
			
		ИначеЕсли ПравилоРеквизита.МестоВыполненияВыражения =
				Перечисления.МестаВыполненияВыраженийНаВстроенномЯзыке.НаСторонеИС Тогда
			
			// Требуется выполнить выражение на встроенном языке.
			РезультатВыражения = ИнтеграцияС1СДокументооборот3.РезультатВыраженияНаВстроенномЯзыке(
				ПравилоРеквизита.ВычисляемоеВыражение,
				ОбъектXDTO,
				КонтекстЗаполненияПоПравилу.ЗаполняемыйОбъектИС,
				ТекущаяСтрока);
			Если РезультатВыражения <> Неопределено Тогда
				Если ПравилоРеквизита.ЭтоДополнительныйРеквизитИС Тогда
					ОписаниеТиповРеквизитаИС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
						ПравилоРеквизита.ДополнительныйРеквизитИС,
						"ТипЗначения");
				Иначе
					ОписаниеТиповРеквизитаИС = ДанныеРеквизита.Тип;
				КонецЕсли;
				ЗаполнитьРеквизитПоРезультатуВыраженияНаВстроенномЯзыке(
					Реквизит,
					РезультатВыражения,
					ОписаниеТиповРеквизитаИС,
					ПравилоРеквизита,
					КонтекстЗаполненияПоПравилу.ЗаполняемыйОбъектИС);
			КонецЕсли;
			
		Иначе
			ВызватьИсключение НСтр("ru = 'Не задано место выполнения выражения на встроенном языке.'");
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// В случае, когда выражение на встроенном языке возвращает имя или синоним значения
// перечисления - процедура заполняет реквизит объекта соответствующим значением.
// Если тип реквизита не является перечислением - реквизит будет заполнен переданным результатом.
//
// Параметры:
//   Реквизит - Произвольный - реквизит объекта ИС, который требуется заполнить.
//   РезультатВыражения - Произвольный - результат выполнения выражения на встроенном языке.
//   ОписаниеТиповРеквизитаИС - ОписаниеТипов - описание типов реквизита, который требуется заполнить.
//   ПравилоРеквизита - СтрокаТаблицыЗначений - правило заполнения реквизита. Строка ТЧ ПравилаЗаполненияРеквизитовИС.
//   ЗаполняемыйОбъектИС - Произвольный - заполняемый объект ИС.
//
Процедура ЗаполнитьРеквизитПоРезультатуВыраженияНаВстроенномЯзыке(Реквизит, РезультатВыражения,
		ОписаниеТиповРеквизитаИС, ПравилоРеквизита, ЗаполняемыйОбъектИС)
	
	ТипРезультата = ТипЗнч(РезультатВыражения);
	ТипПеречисления = Неопределено;
	ТипРезультатаСоответствуетТипуРеквизитаИС = Ложь;
	Для Каждого ТипРеквизитаОбъектаИС Из ОписаниеТиповРеквизитаИС.Типы() Цикл
		Если ТипРеквизитаОбъектаИС = ТипРезультата Тогда
			ТипРезультатаСоответствуетТипуРеквизитаИС = Истина;
		КонецЕсли;
		Если ОбщегоНазначения.ЭтоСсылка(ТипРеквизитаОбъектаИС)
				И ОбщегоНазначения.ЭтоПеречисление(Метаданные.НайтиПоТипу(ТипРеквизитаОбъектаИС)) Тогда
			ТипПеречисления = ТипРеквизитаОбъектаИС;
		КонецЕсли;
	КонецЦикла;
	
	Если ТипРезультата = Тип("Строка") И Не ПустаяСтрока(РезультатВыражения) И ТипПеречисления <> Неопределено Тогда
		ЗначениеДляЗаполнения = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗначениеДляЗаполненияРеквизитаОбъектаИС();
		ЗначениеДляЗаполнения.ЗначениеСвойства = РезультатВыражения;
		ЗначениеДляЗаполнения.ИмяДляПоиска = РезультатВыражения;
		Контекст = ИнтеграцияС1СДокументооборотБазоваяФункциональность.КонтекстДляЗаполненияРеквизитаОбъектаИС();
		Контекст.ЗаполняемыйОбъектИС = ЗаполняемыйОбъектИС;
		Контекст.ИмяРеквизитаОбъектаИС = ПравилоРеквизита.ИмяРеквизитаОбъектаИС;
		Контекст.ЭтоДополнительныйРеквизитИС = ПравилоРеквизита.ЭтоДополнительныйРеквизитИС;
		Контекст.ДополнительныйРеквизитИС = ПравилоРеквизита.ДополнительныйРеквизитИС;
		Реквизит = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗначениеРеквизитаОбъектаИСПоЗначениюДляЗаполнения(
			ТипПеречисления,
			ЗначениеДляЗаполнения,
			Контекст);
	Иначе
		Если ТипРезультатаСоответствуетТипуРеквизитаИС Тогда
			Реквизит = РезультатВыражения;
		Иначе
			Реквизит = Неопределено;
			ТекстОшибки = СтрШаблон(
				НСтр("ru = 'Тип результата выражения на встроенном языке не соответствует типу реквизита.
					|Тип реквизита: %1.
					|Тип результата выражения: %2.
					|Результат выражения на встроенном языке:
					|%3'"),
				ОбщегоНазначения.СтроковоеПредставлениеТипа(ТипРеквизитаОбъектаИС),
				ОбщегоНазначения.СтроковоеПредставлениеТипа(ТипРезультата),
				Строка(РезультатВыражения));
			ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаписатьОшибку(ТекстОшибки,, ПравилоРеквизита.Ссылка);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет строки таблицы по правилу интеграции.
//
// Параметры:
//   Прокси - WSПрокси - объект для подключения к web-сервисам Документооборота.
//   Таблица - ТабличнаяЧасть - табличная часть объекта ИС, которую требуется заполнить.
//   ПравилаЗаполненияСтрок - Массив из СтрокаТаблицыЗначений - правило заполнения строк таблицы.
//   КоличествоСтрок - Число - итоговое количество строк в таблице.
//   КонтекстЗаполненияПоПравилу - см. КонтекстЗаполненияПоПравилу
//   ТаблицаИсточника - СписокXDTO - таблица из объекта ДО, являющаяся источником данных заполнения.
//                    - Неопределено - если строки заполняются не из таблицы из объекта ДО.
//
Процедура ЗаполнитьСтрокиТаблицыПоПравилу(Прокси, Таблица, ПравилаЗаполненияСтрок, КоличествоСтрок,
		КонтекстЗаполненияПоПравилу, ТаблицаИсточника)
	
	Для НомерСтроки = 0 По КоличествоСтрок - 1 Цикл
		НоваяСтрока = Таблица.Добавить();
		Для Каждого ПравилоРеквизита Из ПравилаЗаполненияСтрок Цикл
			ДанныеРеквизита = ДанныеРеквизита(
				КонтекстЗаполненияПоПравилу.РеквизитыОбъектаИС,
				ПравилоРеквизита.ИмяРеквизитаОбъектаИС,
				ПравилоРеквизита.Таблица,
				КонтекстЗаполненияПоПравилу.ЗаполняемыйОбъектИС);
			ЗаполнитьРеквизитПоПравилу(
				Прокси,
				НоваяСтрока[ПравилоРеквизита.ИмяРеквизитаОбъектаИС],
				ПравилоРеквизита,
				ДанныеРеквизита,
				КонтекстЗаполненияПоПравилу,
				НомерСтроки,
				?(ТаблицаИсточника = Неопределено, Неопределено, ТаблицаИсточника[НомерСтроки]));
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Заполняет таблицу по правилу заполнения, указанному в правилах интеграции.
//
// Параметры:
//   Прокси - WSПрокси - объект для подключения к web-сервисам Документооборота.
//   Таблица - ТабличнаяЧасть - табличная часть объекта ИС, которую требуется заполнить.
//   ПравилоТаблицы - СтрокаТаблицыЗначений - правило заполнения таблицы. Строка ТЧ ПравилаЗаполненияРеквизитовИС.
//   КонтекстЗаполненияПоПравилу - см. КонтекстЗаполненияПоПравилу
//
Процедура ЗаполнитьТаблицуПоПравилу(Прокси, Таблица, ПравилоТаблицы, КонтекстЗаполненияПоПравилу)
	
	Таблица.Очистить();
	
	ОбъектXDTO = КонтекстЗаполненияПоПравилу.ОбъектXDTO;
	ЗаполняемыйОбъектИС = КонтекстЗаполненияПоПравилу.ЗаполняемыйОбъектИС;
	РеквизитыОбъектаИС = КонтекстЗаполненияПоПравилу.РеквизитыОбъектаИС; // см. ИнтеграцияС1СДокументооборотБазоваяФункциональность.РеквизитыОбъектаИС
	ПравилаЗаполненияРеквизитовИС = КонтекстЗаполненияПоПравилу.ПравилаЗаполненияРеквизитовИС; // ТаблицаЗначений
	
	Если ПравилоТаблицы.Вариант = ВариантВыражение() Тогда
		
		Если ПравилоТаблицы.МестоВыполненияВыражения =
				Перечисления.МестаВыполненияВыраженийНаВстроенномЯзыке.НаСторонеДО Тогда
			// Выражение уже было выполнено на стороне ДО, его результат находится во входящих данных.
			ТаблицаXDTO = ЗначениеИзВходящихДанных(
				ОбъектXDTO.expressionResultsForTabularSections,
				ПравилоТаблицы.id);
			Если ТаблицаXDTO <> Неопределено Тогда
				Для НомерСтроки = 0 По ТаблицаXDTO.rows.Количество() - 1 Цикл
					СтрокаXDTO = ТаблицаXDTO.rows[НомерСтроки];
					НоваяСтрока = Таблица.Добавить();
					
					ОтборРеквизитыТаблицы = Новый Структура("Таблица", ПравилоТаблицы.ИмяРеквизитаОбъектаИС);
					Для Каждого Реквизит Из РеквизитыОбъектаИС.НайтиСтроки(ОтборРеквизитыТаблицы) Цикл
						ЗначениеXDTO = ЗначениеИзВходящихДанных(СтрокаXDTO.attributes, Реквизит.Имя);
						Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ЗначениеXDTO, "error") Тогда
							ВызватьИсключение ЗначениеXDTO.error;
							
						ИначеЕсли ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ЗначениеXDTO, "value") Тогда
							ЗначениеДляЗаполнения =
								ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗначениеДляЗаполненияРеквизитаОбъектаИС();
							ЗначениеДляЗаполнения.ТипXDTOОбъекта = ОбъектXDTO.Тип().Имя;
							ЗначениеДляЗаполнения.ИмяСвойства = ПравилоТаблицы.ИмяРеквизитаОбъектаДО;
							ЗначениеДляЗаполнения.ЗначениеСвойства = ЗначениеXDTO.value;
							ЗаполнитьРеквизитИзЗначенияСвойства(
								Прокси,
								НоваяСтрока[Реквизит.Имя],
								ЗначениеДляЗаполнения,
								ПравилоТаблицы,
								ДанныеРеквизита(
									РеквизитыОбъектаИС,
									ЗначениеXDTO.name,
									ПравилоТаблицы.ИмяРеквизитаОбъектаИС,
									ЗаполняемыйОбъектИС),
								ЗаполняемыйОбъектИС,
								НомерСтроки);
							
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
			
		ИначеЕсли ПравилоТаблицы.МестоВыполненияВыражения =
				Перечисления.МестаВыполненияВыраженийНаВстроенномЯзыке.НаСторонеИС Тогда
			// Требуется выполнить выражение на встроенном языке.
			РезультатВыражения = ИнтеграцияС1СДокументооборот3.РезультатВыраженияНаВстроенномЯзыке(
				ПравилоТаблицы.ВычисляемоеВыражение,
				ОбъектXDTO,
				ЗаполняемыйОбъектИС); // ТаблицаЗначений
			КолонкиТаблицы = Таблица.Выгрузить().Колонки; // КоллекцияКолонокТаблицыЗначений
			Для Каждого Строка Из РезультатВыражения Цикл
				НоваяСтрока = Таблица.Добавить();
				Для Каждого Колонка Из РезультатВыражения.Колонки Цикл
					Если ЗначениеЗаполнено(Строка[Колонка.Имя]) И Колонка.Имя <> "НомерСтроки" Тогда
						ЗаполнитьРеквизитПоРезультатуВыраженияНаВстроенномЯзыке(
							НоваяСтрока[Колонка.Имя],
							Строка[Колонка.Имя],
							КолонкиТаблицы.Найти(Колонка.Имя).ТипЗначения,
							ПравилоТаблицы,
							ЗаполняемыйОбъектИС);
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			
		Иначе
			ВызватьИсключение НСтр("ru = 'Не задано место выполнения выражения на встроенном языке.'");
			
		КонецЕсли;
		
	Иначе
		
		ПравилаЗаполненияСтрок = Новый Массив;
		
		ОтборРеквизитыТаблицы = Новый Структура("Таблица", ПравилоТаблицы.ИмяРеквизитаОбъектаИС);
		Для Каждого Реквизит Из РеквизитыОбъектаИС.НайтиСтроки(ОтборРеквизитыТаблицы) Цикл
			// ПравилаЗаполненияСтрок должны быть отсортированы таким образом, чтобы
			// объекты, имеющие владельца, грузились после того, как загружен их владелец.
			// Например, единица измерения с владельцем номенклатура должна грузиться после
			// загрузки самой номенклатуры.
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("ИмяРеквизитаОбъектаИС", Реквизит.Имя);
			ПараметрыОтбора.Вставить("ЭтоТаблица", Ложь);
			ПараметрыОтбора.Вставить("Таблица", ПравилоТаблицы.ИмяРеквизитаОбъектаИС);
			
			НайденныеПравилаРеквизитов = ПравилаЗаполненияРеквизитовИС.НайтиСтроки(ПараметрыОтбора);
			Если НайденныеПравилаРеквизитов.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ПравилаЗаполненияСтрок.Добавить(НайденныеПравилаРеквизитов[0]);
		КонецЦикла;
		
		СписокXDTO = Неопределено;
		Если ПравилоТаблицы.Вариант = ВариантРеквизит() Тогда
			РазделеннаяСтрока = СтрРазделить(ПравилоТаблицы.ИмяРеквизитаОбъектаДО, ".", Ложь);
			Если РазделеннаяСтрока.Количество() = 1 Тогда
				СписокXDTO = ОбъектXDTO[РазделеннаяСтрока[0]];
			Иначе
				СписокXDTO = ОбъектXDTO[РазделеннаяСтрока[0]][РазделеннаяСтрока[1]];
			КонецЕсли;
			КоличествоСтрок = СписокXDTO.Количество();
			
		ИначеЕсли ПравилоТаблицы.Вариант = ВариантРеквизитТаблицы() Тогда
			КоличествоСтрок = 1;
			
		Иначе
			Возврат;
			
		КонецЕсли;
		
		ЗаполнитьСтрокиТаблицыПоПравилу(
			Прокси,
			Таблица,
			ПравилаЗаполненияСтрок,
			КоличествоСтрок,
			КонтекстЗаполненияПоПравилу,
			СписокXDTO);
		
	КонецЕсли;
	
КонецПроцедуры

// Получает значение из входящих данных.
//
// Параметры:
//   Список - СписокXDTO
//   ИдентификаторЗначения - Строка
//
// Возвращаемое значение:
//   ОбъектXDTO
//   Неопределено
//
Функция ЗначениеИзВходящихДанных(Список, ИдентификаторЗначения)
	
	Для Каждого Элемент Из Список Цикл
		Если Элемент.name = ИдентификаторЗначения Тогда
			Возврат Элемент;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

// Конструктор структуры, описывающей контекст для заполнения объекта ИС по правилу интеграции.
//
// Параметры:
//   ЗаполняемыйОбъектИС - Произвольный - заполняемый объект ИС.
//   ОбъектXDTO - ОбъектXDTO - объект ДО, источник данных заполнения.
//   ПравилоИнтеграции - СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом3 - правило интеграции для
//     заполнения объекта ИС.
//   ПолноеИмяОбъектаИС - Строка - полное имя заполняемого объекта ИС.
//
// Возвращаемое значение:
//   Структура:
//     * ПравилоИнтеграции - СправочникСсылка.ПравилаИнтеграцииС1СДокументооборотом3 - правило интеграции для
//         заполнения объекта ИС.
//     * ЗаполняемыйОбъектИС - Произвольный - заполняемый объект ИС.
//     * РеквизитыОбъектаИС - см. ИнтеграцияС1СДокументооборотБазоваяФункциональность.РеквизитыОбъектаИС
//     * ОбъектXDTO - ОбъектXDTO - объект ДО, источник данных заполнения.
//     * ЯвляетсяЗаявкойНаОплату - Булево - документ ДО является заявкой на оплату.
//     * ПравилаЗаполненияРеквизитовИС - ТаблицаЗначений - таблица правила ПравилаЗаполненияРеквизитовИС.
//
Функция КонтекстЗаполненияПоПравилу(ЗаполняемыйОбъектИС, ОбъектXDTO, ПравилоИнтеграции, ПолноеИмяОбъектаИС)
	
	Контекст = Новый Структура;
	Контекст.Вставить("ЗаполняемыйОбъектИС", ЗаполняемыйОбъектИС);
	Контекст.Вставить("ОбъектXDTO", ОбъектXDTO);
	Контекст.Вставить("ПравилоИнтеграции", ПравилоИнтеграции);
	
	Контекст.Вставить("ЯвляетсяЗаявкойНаОплату", Ложь);
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ОбъектXDTO, "documentType")
			И ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(
				ОбъектXDTO.documentType, "isAPaymentRequest") Тогда
		Контекст.ЯвляетсяЗаявкойНаОплату = ОбъектXDTO.documentType.isAPaymentRequest;
	КонецЕсли;
	
	РеквизитыОбъектаИС = ИнтеграцияС1СДокументооборотБазоваяФункциональность.РеквизитыОбъектаИС(ПолноеИмяОбъектаИС);
	Контекст.Вставить("РеквизитыОбъектаИС", РеквизитыОбъектаИС);
	
	ПравилаЗаполненияРеквизитовИС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		ПравилоИнтеграции,
		"ПравилаЗаполненияРеквизитовИС").Выгрузить();
	Контекст.Вставить("ПравилаЗаполненияРеквизитовИС", ПравилаЗаполненияРеквизитовИС);
	
	Возврат Контекст;
	
КонецФункции

// Получает сторону заявки на оплату по предопределенному имени стороны.
//
// Параметры:
//   ОбъектXDTO - ОбъектXDTO - объект ДО, источник данных заполнения.
//   ПредопределенноеИмяСтороны - Строка - принимает значения: "Payer" - плательщик, "Recipient" - получатель.
//   ТипОбъектаДО - Строка - неявно возвращаемое значение, имя типа объекта ДО, являющегося стороной.
//
Функция СторонаЗаявкиНаОплатуXDTO(ОбъектXDTO, ПредопределенноеИмяСтороны, ТипОбъектаДО)
	
	Результат = Неопределено;
	
	УстановленаТЧСтороны = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ОбъектXDTO, "partyRows");
	
	Если УстановленаТЧСтороны Тогда
		Для Каждого СторонаXDTO Из ОбъектXDTO.partyRows.rows Цикл
			Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(СторонаXDTO, "paymentRequestParty")
					И НРег(СторонаXDTO.paymentRequestParty) = НРег(ПредопределенноеИмяСтороны) Тогда
				Результат = СторонаXDTO.party;
				ТипОбъектаДО = СторонаXDTO.party.objectID.type;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

// Возвращает значение Перечисления.ВариантыПравилЗаполненияРеквизитов.ВыражениеНаВстроенномЯзыке.
//
Функция ВариантВыражение()
	
	Возврат Перечисления.ВариантыПравилЗаполненияРеквизитов.ВыражениеНаВстроенномЯзыке;
	
КонецФункции

// Возвращает значение Перечисления.ВариантыПравилЗаполненияРеквизитов.УказанноеЗначение.
//
Функция ВариантЗначение()
	
	Возврат Перечисления.ВариантыПравилЗаполненияРеквизитов.УказанноеЗначение;
	
КонецФункции

// Возвращает значение Перечисления.ВариантыПравилЗаполненияРеквизитов.РеквизитОбъекта.
//
Функция ВариантРеквизит()
	
	Возврат Перечисления.ВариантыПравилЗаполненияРеквизитов.РеквизитОбъекта;
	
КонецФункции

// Возвращает значение Перечисления.ВариантыПравилЗаполненияРеквизитов.РеквизитОбъекта.
//
Функция ВариантРеквизитТаблицы()
	
	Возврат Перечисления.ВариантыПравилЗаполненияРеквизитов.РеквизитТаблицы;
	
КонецФункции

// Добавляет дополнительный реквизит в строку таблицы с заполнением колонок, указанных в качестве параметров.
//
Процедура ДобавитьДополнительныйРеквизит(Реквизиты, Представление, Тип, ДопРеквизитID, ДопРеквизитТип)
	
	НоваяСтрока = Реквизиты.Добавить();
	НоваяСтрока.Представление = Представление;
	
	Если ТипЗнч(Тип) <> Тип("СписокЗначений") Тогда
		Типы = Новый СписокЗначений;
		Типы.Добавить(Тип);
		НоваяСтрока.Тип = Типы;
	Иначе
		НоваяСтрока.Тип = Тип;
	КонецЕсли;
	
	НоваяСтрока.ДопРеквизит = Истина;
	НоваяСтрока.ДопРеквизитID = ДопРеквизитID;
	НоваяСтрока.ДопРеквизитТип = ДопРеквизитТип;
	
КонецПроцедуры

// Добавляет реквизит в строку таблицы с заполнением колонок, указанных в качестве параметров.
//
Процедура ДобавитьРеквизит(Реквизиты, Имя, Представление, Тип, ОбязательноеЗаполнение = Ложь, ИмяРеквизитаВДО = "")
	
	НоваяСтрока = Реквизиты.Добавить();
	НоваяСтрока.Имя = Имя;
	НоваяСтрока.Представление = Представление;
	НоваяСтрока.ИмяРеквизитаВДО = ИмяРеквизитаВДО;
	
	Если ТипЗнч(Тип) <> Тип("СписокЗначений") Тогда
		Типы = Новый СписокЗначений;
		Типы.Добавить(Тип);
		НоваяСтрока.Тип = Типы;
	Иначе
		НоваяСтрока.Тип = Тип;
	КонецЕсли;
	
	НоваяСтрока.ОбязательноеЗаполнение = ОбязательноеЗаполнение;
	
КонецПроцедуры

// Добавляет реквизит в подчиненную строку таблицы, с заполнением колонок, указанных в качестве параметров.
//
Процедура ДобавитьРеквизитТаблицы(Реквизиты, ИмяТаблицы, Имя, Представление, Тип, ОбязательноеЗаполнение = Ложь)
	
	НоваяСтрока = Реквизиты.Добавить();
	НоваяСтрока.Таблица = ИмяТаблицы;
	НоваяСтрока.Имя = Имя;
	НоваяСтрока.Представление = Представление;
	
	Если ТипЗнч(Тип) <> Тип("СписокЗначений") Тогда
		Типы = Новый СписокЗначений;
		Типы.Добавить(Тип);
		НоваяСтрока.Тип = Типы;
	Иначе
		НоваяСтрока.Тип = Тип;
	КонецЕсли;
	
	НоваяСтрока.ОбязательноеЗаполнение = ОбязательноеЗаполнение;
	
КонецПроцедуры

// Добавляет таблицу в строку таблицы с заполнением колонок, указанных в качестве параметров.
//
Процедура ДобавитьТаблицу(Реквизиты, Имя, Представление)
	
	НоваяСтрока = Реквизиты.Добавить();
	НоваяСтрока.ЭтоТаблица = Истина;
	НоваяСтрока.Имя = Имя;
	НоваяСтрока.Представление = Представление;
	
КонецПроцедуры

Процедура ПроверитьПравилаЗаполненияРеквизитовИС(ПравилоИнтеграции, ОбъектИС, ОбъектXDTO, РеквизитыОбъектаИС,
		РеквизитыТекущегоВидаДокументаДО, ВсеРеквизитыОбъектаДО, СписокОшибок)
	
	Для Каждого Правило Из ПравилоИнтеграции.ПравилаЗаполненияРеквизитовИС Цикл
		
		РеквизитИС = РеквизитыОбъектаИС.Найти(Правило.ИмяРеквизитаОбъектаИС, "Имя");
		ТаблицаИС = РеквизитыОбъектаИС.Найти(Правило.Таблица, "Имя");
		ПредставлениеРеквизитаИС = "";
		ПредставлениеТипаРеквизитаИС = "";
		
		Если Правило.ЭтоТаблица Тогда
			ПредставлениеТипаРеквизитаИС = НСтр("ru = 'таблицы ИС'");
			
		ИначеЕсли Правило.ЭтоДополнительныйРеквизитИС Тогда
			ПредставлениеТипаРеквизитаИС = НСтр("ru = 'доп. реквизита ИС'");
			
		ИначеЕсли Не ЗначениеЗаполнено(Правило.Таблица) Тогда
			ПредставлениеТипаРеквизитаИС = НСтр("ru = 'реквизита ИС'");
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Правило.Таблица) Тогда
			ПредставлениеРеквизитаИС = СтрШаблон(НСтр("ru = 'реквизита ""%1"" таблицы ИС ""%2""'"),
				?(РеквизитИС = Неопределено, Правило.ИмяРеквизитаОбъектаИС, РеквизитИС.Представление),
				?(ТаблицаИС = Неопределено, Правило.Таблица, ТаблицаИС.Представление));
			
		Иначе
			ПредставлениеРеквизитаИС = СтрШаблон("%1 ""%2""",
				ПредставлениеТипаРеквизитаИС,
				?(РеквизитИС = Неопределено, Правило.ИмяРеквизитаОбъектаИС, РеквизитИС.Представление));
			
		КонецЕсли;
		
		Если Правило.Вариант = Перечисления.ВариантыПравилЗаполненияРеквизитов.РеквизитОбъекта Тогда
			
			Если Не Правило.ЭтоДополнительныйРеквизитДО Тогда
				
				РазделеннаяСтрока = СтрРазделить(Правило.ИмяРеквизитаОбъектаДО, ".", Ложь);
				Если РазделеннаяСтрока.Количество() = 1 Тогда // "свойство"
					Таблица = "";
					ИмяРеквизита = Правило.ИмяРеквизитаОбъектаДО;
					
				ИначеЕсли РазделеннаяСтрока.Количество() = 2 Тогда // "список.свойство"
					Таблица = "";
					ИмяРеквизита = СтрШаблон("%1.%2", РазделеннаяСтрока[0], РазделеннаяСтрока[1]);
					
				ИначеЕсли РазделеннаяСтрока.Количество() = 3 Тогда // "список.список.свойство"
					Таблица = СтрШаблон("%1.%2", РазделеннаяСтрока[0], РазделеннаяСтрока[1]);
					ИмяРеквизита = РазделеннаяСтрока[2];
					
				Иначе
					СписокОшибок.Добавить(
						СтрШаблон(
							НСтр("ru = 'Ошибка в правиле заполнения %1 (Закладка ""Получение из 1С:Документооборота""):
								|Указано недопустимое имя реквизита-источника данных заполнения: %2'"),
									ПредставлениеРеквизитаИС,
									Правило.ИмяРеквизитаОбъектаДО));
					Продолжить;
					
				КонецЕсли;
				
				Отбор = Новый Структура("Имя, Таблица", ИмяРеквизита, Таблица);
				
				НайденныеСтроки = РеквизитыТекущегоВидаДокументаДО.НайтиСтроки(Отбор);
				
				Если НайденныеСтроки.Количество() = 0 Тогда
					ВсеРеквизитыДОРеквизит = ВсеРеквизитыОбъектаДО.НайтиСтроки(Отбор);
					ВсеРеквизитыДОТаблица = ВсеРеквизитыОбъектаДО.НайтиСтроки(Новый Структура("Имя", Таблица));
					
					Если Правило.ЭтоТаблица Тогда
						СписокОшибок.Добавить(
							СтрШаблон(
								НСтр("ru = 'Ошибка в правиле заполнения %1 (Закладка ""Получение из 1С:Документооборота""):
									|выбран вариант заполнения из несуществующей таблицы ""%2""'"),
										ПредставлениеРеквизитаИС,
										?(ВсеРеквизитыДОРеквизит.Количество() = 0,
											Правило.ИмяРеквизитаОбъектаДО,
											ВсеРеквизитыДОРеквизит[0].Представление)));
					Иначе
						Если ЗначениеЗаполнено(Таблица) Тогда
							СписокОшибок.Добавить(
								СтрШаблон(
									НСтр("ru = 'Ошибка в правиле заполнения %1 (Закладка ""Получение из 1С:Документооборота""):
										|выбран вариант заполнения из реквизита ""%2"" несуществующей таблицы ""%3""'"),
											ПредставлениеРеквизитаИС,
											?(ВсеРеквизитыДОРеквизит.Количество() = 0,
												Правило.ИмяРеквизитаОбъектаДО,
												ВсеРеквизитыДОРеквизит[0].Представление),
											?(ВсеРеквизитыДОТаблица.Количество() = 0,
												Таблица,
												ВсеРеквизитыДОТаблица[0].Представление)));
						Иначе
							СписокОшибок.Добавить(
								СтрШаблон(
									НСтр("ru = 'Ошибка в правиле заполнения %1 (Закладка ""Получение из 1С:Документооборота""):
										|выбран вариант заполнения из несуществующего реквизита ""%2""'"),
											ПредставлениеРеквизитаИС,
											?(ВсеРеквизитыДОРеквизит.Количество() = 0,
												Правило.ИмяРеквизитаОбъектаДО,
												ВсеРеквизитыДОРеквизит[0].Представление)));
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
			Иначе // Дополнительный реквизит ДО.
				
				Отбор = Новый Структура("ДопРеквизитID, ДопРеквизитТип",
					Правило.ДополнительныйРеквизитДОID,
					Правило.ДополнительныйРеквизитДОТип);
				НайденныеСтроки = РеквизитыТекущегоВидаДокументаДО.НайтиСтроки(Отбор);
				
				Если НайденныеСтроки.Количество() = 0 Тогда
					СписокОшибок.Добавить(
						СтрШаблон(
							НСтр("ru = 'Ошибка в правиле заполнения %1 (Закладка ""Получение из 1С:Документооборота""):
								|выбран вариант заполнения из несуществующего доп. реквизита ""%2""'"),
									ПредставлениеРеквизитаИС,
									Правило.ДополнительныйРеквизитДОID));
				КонецЕсли;
				
			КонецЕсли;
			
		ИначеЕсли Правило.Вариант = Перечисления.ВариантыПравилЗаполненияРеквизитов.ВыражениеНаВстроенномЯзыке Тогда
			
			ТаблицаИсточник = "";
			Если ЗначениеЗаполнено(Правило.Таблица) Тогда
				ВышестоящееПравилоСтроки = ПравилоИнтеграции.ПравилаЗаполненияРеквизитовИС.НайтиСтроки(
					Новый Структура("ИмяРеквизитаОбъектаИС, ЭтоТаблица", Правило.Таблица, Истина));
				Если ВышестоящееПравилоСтроки.Количество() = 1 Тогда
					ТаблицаИсточник = ВышестоящееПравилоСтроки[0].ИмяРеквизитаОбъектаДО;
				КонецЕсли;
			КонецЕсли;
			
			КонтекстВыражения = КонтекстВыраженияНаВстроенномЯзыке();
			КонтекстВыражения.ОбъектXDTO = ОбъектXDTO;
			КонтекстВыражения.ОбъектИС = ОбъектИС;
			КонтекстВыражения.ТаблицаИсточник = ТаблицаИсточник;
			
			Попытка
				ПроверитьВыражениеПравилаЗагрузки(
					Правило.ВычисляемоеВыражение,
					Правило.МестоВыполненияВыражения,
					ПравилоИнтеграции.ТипОбъектаДО,
					ПравилоИнтеграции.ТипОбъектаИС,
					КонтекстВыражения,
					Правило.ЭтоТаблица);
			Исключение
				ТекстИсключения = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
				СписокОшибок.Добавить(
					СтрШаблон(
						НСтр("ru = 'Ошибка в правиле заполнения %1 (Закладка ""Получение из 1С:Документооборота""):
							|Не удалось выполнить выражение на встроенном языке по причине:
							|""%2""'"),
								ПредставлениеРеквизитаИС,
								ТекстИсключения));
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли