
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДемонстрационныйСценарий = Параметры.ДемонстрационныйСценарий;
	ПолеСОшибкойФон          = ЦветаСтиля.ПолеСОшибкойФон;
	ФонПоля                  = ЦветаСтиля.ЦветФонаПоля;
	
	СпискиВыбораКлиентСервер.Загрузить("ИсторияЧтоМеняемДемонстрационныеСценарии", Элементы.ЧтоМеняем.СписокВыбора);
	СпискиВыбораКлиентСервер.Загрузить("ИсторияНаЧтоМеняемДемонстрационныеСценарии", Элементы.НаЧтоМеняем.СписокВыбора);
	
	Если Параметры.Свойство("ПараметрыПоискаПриОткрытии") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ПараметрыПоискаПриОткрытии);
		ВыполнитьПоискПриОткрытии = Истина;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВыполнитьПоискПриОткрытии Тогда
		
		ВыполнитьПоискВсеВхождения();
		
	Иначе
		
		УправлениеДоступностью();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	Если ВыполнитьПоискПриОткрытии Тогда
		
		Настройки.Удалить("НаЧтоМеняем");
		Настройки.Удалить("ЧтоМеняем");
		Настройки.Удалить("УчитыватьРегистр");
		Настройки.Удалить("ИскатьСловоЦеликом");
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ДемонстрационныеСценарии_ИзменениеПараметровФормыПоискаИЗамены" Тогда 
		
		Если Параметр.ДемонстрационныйСценарий <> ДемонстрационныйСценарий Тогда
			Возврат;
		КонецЕсли;
		
		УправлениеДоступностью(Параметр);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы 

&НаКлиенте
Процедура ЧтоМеняемПриИзменении(Элемент)
	
	УправлениеДоступностью();
	
КонецПроцедуры

&НаКлиенте
Процедура НаЧтоМеняемПриИзменении(Элемент)
	
	УправлениеДоступностью();
	
КонецПроцедуры

&НаКлиенте
Процедура ЧтоМеняемАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СпискиВыбораКлиентСервер.АвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НаЧтоМеняемАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СпискиВыбораКлиентСервер.АвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НайтиСледующий(Команда)
	
	ПараметрыОповещенияФормы = ПараметрыОповещенияФормы();
	ПараметрыОповещенияФормы.Операция = "НайтиСледующий";
	
	Оповестить("ДемонстрационныеСценарии_ПоискИЗамена", ПараметрыОповещенияФормы, ВладелецФормы);
	
КонецПроцедуры 

&НаКлиенте
Процедура Заменить(Команда)
	
	ПараметрыОповещенияФормы = ПараметрыОповещенияФормы();
	ПараметрыОповещенияФормы.Операция = "ЗаменитьТекущее";
	
	Оповестить("ДемонстрационныеСценарии_ПоискИЗамена", ПараметрыОповещенияФормы, ВладелецФормы);

КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьВсе(Команда)
	
	ПараметрыОповещенияФормы = ПараметрыОповещенияФормы();
	ПараметрыОповещенияФормы.Операция = "ЗаменитьВсеВхождения";
	
	Оповестить("ДемонстрационныеСценарии_ПоискИЗамена", ПараметрыОповещенияФормы, ВладелецФормы);

КонецПроцедуры

&НаКлиенте
Процедура НайтиПредыдущий(Команда)
	
	ПараметрыОповещенияФормы = ПараметрыОповещенияФормы();
	ПараметрыОповещенияФормы.Операция = "НайтиПредыдущий";
	
	Оповестить("ДемонстрационныеСценарии_ПоискИЗамена", ПараметрыОповещенияФормы, ВладелецФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиВсеВхождения(Команда)
	
	Элементы.ЧтоМеняем.ЦветФона = ФонПоля;
	
	ВыполнитьПоискВсеВхождения();
	
	СпискиВыбораКлиентСервер.ОбновитьСписокВыбора(Элементы.ЧтоМеняем.СписокВыбора, ЧтоМеняем, 1000);
	СпискиВыбораКлиентСервер.ОбновитьСписокВыбора(Элементы.НаЧтоМеняем.СписокВыбора, НаЧтоМеняем, 1000);
	
	СпискиВыбораКлиентСервер.Сохранить("ИсторияЧтоМеняемДемонстрационныеСценарии", Элементы.ЧтоМеняем.СписокВыбора);
	СпискиВыбораКлиентСервер.Сохранить("ИсторияНаЧтоМеняемДемонстрационныеСценарии", Элементы.НаЧтоМеняем.СписокВыбора);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыполнитьПоискВсеВхождения()
	
	ПараметрыОповещенияФормы = ПараметрыОповещенияФормы();
	ПараметрыОповещенияФормы.Операция = "НайтиВсеВхождения";
	
	Оповестить("ДемонстрационныеСценарии_ПоискИЗамена", ПараметрыОповещенияФормы, ВладелецФормы);
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыОповещенияФормы()

	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ЧтоМеняем",                ЧтоМеняем);
	ПараметрыОповещения.Вставить("НаЧтоМеняем",              НаЧтоМеняем);
	ПараметрыОповещения.Вставить("УчитыватьРегистр",         УчитыватьРегистр);
	ПараметрыОповещения.Вставить("ИскатьСловоЦеликом",       ИскатьСловоЦеликом);
	ПараметрыОповещения.Вставить("ДемонстрационныйСценарий", ДемонстрационныйСценарий);
	ПараметрыОповещения.Вставить("КакИскатьВГиперссылках",   "Игнорировать");
	ПараметрыОповещения.Вставить("Операция",                 "");
	
	Возврат ПараметрыОповещения;
	
КонецФункции

&НаКлиенте
Процедура УправлениеДоступностью(ПараметрыОповещения = Неопределено)
	
	ЕстьЧтоИскать = СтрДлина(ЧтоМеняем) > 1;
	
	Элементы.ФормаНайти.Доступность = ЕстьЧтоИскать;
	
	Если ПараметрыОповещения = Неопределено Тогда
		
		Элементы.ФормаНайтиСледующий.Доступность  = Ложь;
		Элементы.ФормаНайтиПредыдущий.Доступность = Ложь;
		Элементы.ФормаЗаменить.Доступность        = Ложь;
		Элементы.ФормаЗаменитьВсе.Доступность     = Ложь;
		Элементы.ЧтоМеняем.ЦветФона               = ФонПоля;
		
	Иначе
		
		Элементы.ФормаНайтиСледующий.Доступность  = ЕстьЧтоИскать И ПараметрыОповещения.ЕстьСледующийРезультат;
		Элементы.ФормаНайтиПредыдущий.Доступность = ЕстьЧтоИскать И ПараметрыОповещения.ЕстьПредыдущийРезультат;
		Элементы.ФормаЗаменить.Доступность        = ЕстьЧтоИскать И ПараметрыОповещения.ЕстьТекущийРезультатПоиска;
		Элементы.ФормаЗаменитьВсе.Доступность     = ЕстьЧтоИскать И ПараметрыОповещения.ЕстьРезультатыПоиска; 
		Элементы.ЧтоМеняем.ЦветФона               = ?(ПараметрыОповещения.ЕстьРезультатыПоиска, ФонПоля, ПолеСОшибкойФон);
	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
