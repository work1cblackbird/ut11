#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("Ссылка");
	Поля.Добавить("Наименование");
	Поля.Добавить("ТребуетсяЗагрузка");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Если Данные.ТребуетсяЗагрузка = Истина Тогда
		СтандартнаяОбработка = Ложь;
		Представление = НСтр("ru = '<не загружено>'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//Возвращает коэффициент пересчета единицы измерения ВЕТИС относительно базовой единицы измерения.
//
//Параметры:
//   БазоваяЕдиницаИзмерения - СправочникСсылка.ЕдиницыИзмеренияВЕТИС - Базовая единица измерения ВЕТИС.
//   ЕдиницаИзмерения        - СправочникСсылка.ЕдиницыИзмеренияВЕТИС - Единица измерения ВЕТИС, для которой 
//                                                                      рассчитывается коэффициент пересчета.
//
//Возвращаемое значение:
//   Число - коэффициент пересчета единицы измерения ВЕТИС относительно базовой единицы измерения.
//
Функция КоэффициентПересчета(БазоваяЕдиницаИзмерения, ЕдиницаИзмерения) Экспорт
	
	Если Не ЗначениеЗаполнено(БазоваяЕдиницаИзмерения)
		Или Не ЗначениеЗаполнено(ЕдиницаИзмерения)
		Или БазоваяЕдиницаИзмерения = ЕдиницаИзмерения Тогда
		
		Коэффициент = 1;
		
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(ВЫРАЗИТЬ(&ЕдиницаИзмерения КАК Справочник.ЕдиницыИзмеренияВЕТИС).Коэффициент, 1)
		|		/ ЕСТЬNULL(ВЫРАЗИТЬ(&БазоваяЕдиницаИзмерения КАК Справочник.ЕдиницыИзмеренияВЕТИС).Коэффициент, 1) КАК Коэффициент";
		
		Запрос.УстановитьПараметр("БазоваяЕдиницаИзмерения", БазоваяЕдиницаИзмерения);
		Запрос.УстановитьПараметр("ЕдиницаИзмерения",        ЕдиницаИзмерения);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		Коэффициент = Выборка.Коэффициент;
		
	КонецЕсли;
	
	Возврат Коэффициент;
	
КонецФункции

#КонецОбласти

#КонецЕсли
