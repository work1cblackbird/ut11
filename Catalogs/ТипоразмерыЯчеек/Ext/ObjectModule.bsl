#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если НеОграниченаПоГрузоподъемности Тогда
		Грузоподъемность = 0;
	КонецЕсли;
	
	Если НеОграниченаПоРазмерам Тогда
		Высота	= 0;
		Глубина	= 0;
		Объем	= 0;
		Ширина	= 0;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
 	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если НеОграниченаПоГрузоподъемности Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Грузоподъемность");
	КонецЕсли;
	
	Если НеОграниченаПоРазмерам Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Высота");
		МассивНепроверяемыхРеквизитов.Добавить("Глубина");
		МассивНепроверяемыхРеквизитов.Добавить("Объем");
		МассивНепроверяемыхРеквизитов.Добавить("Ширина");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ЕдиницаИзмеренияВеса 	= Константы.ЕдиницаИзмеренияВеса.Получить();
	ЕдиницаИзмеренияОбъема 	= Константы.ЕдиницаИзмеренияОбъема.Получить();
	ЕдиницаИзмеренияДлины 	= Константы.ЕдиницаИзмеренияДлины.Получить();
	
	ВысотаЕдиницаИзмерения	 = ЕдиницаИзмеренияДлины;
	ГлубинаЕдиницаИзмерения	 = ЕдиницаИзмеренияДлины;
	ШиринаЕдиницаИзмерения	 = ЕдиницаИзмеренияДлины;
	ВесЕдиницаИзмерения		 = ЕдиницаИзмеренияВеса;
	ОбъемЕдиницаИзмерения	 = ЕдиницаИзмеренияОбъема;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли