#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения <> Неопределено И ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("Закрыта") Тогда
			ДанныеЗаполнения.Закрыта = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Не ЗначениеЗаполнено(Автор) И Не ЗначениеЗаполнено(Подразделение) Тогда
		ТекстСообщения = НСтр("ru='Не заполнены поля ""Автор"" и ""Подразделение"". Заполните одно из полей.'");
		ОбщегоНазначения.СообщитьПользователю(
		ТекстСообщения,
		ЭтотОбъект,
		"Автор",, Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Исполнитель) И Не ЗначениеЗаполнено(ПодразделениеИсполнитель) Тогда
		ТекстСообщения = НСтр("ru='Не заполнены поля ""Исполнитель"" и ""Подразделение исполнитель"". Заполните одно из полей.'");
		ОбщегоНазначения.СообщитьПользователю(
		ТекстСообщения,
		ЭтотОбъект,
		"Исполнитель",, Отказ);
	КонецЕсли;
	
	Если Статус = Перечисления.СтатусыИнцидентов.Зарегистрирован Тогда
		
		// Желаемая дата закрытия не может быть ранее даты создания инцидента
		Если ЗначениеЗаполнено(ЖелаемаяДатаЗакрытия) И ЖелаемаяДатаЗакрытия < ?(ЗначениеЗаполнено(ДатаРегистрации), ДатаРегистрации, ТекущаяДатаСеанса()) Тогда
			ТекстСообщения = НСтр("ru='""Желаемая дата закрытия"" должна быть не ранее даты создания инцидента'");
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				"ЖелаемаяДатаЗакрытия",, Отказ);
		КонецЕсли;
		
	Иначе
		
		МассивНепроверяемыхРеквизитов.Добавить("ЖелаемаяДатаЗакрытия");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Дубль) Тогда
		
		Если Дубль = Ссылка Тогда
			ТекстСообщения = НСтр("ru='Значение поля ""Это дубль инцидента"" указано неверно - инцидент ссылается сам на себя.'");
			ОбщегоНазначения.СообщитьПользователю(
			ТекстСообщения,
			ЭтотОбъект, "Дубль",, Отказ);
		КонецЕсли;
	
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Статус = Перечисления.СтатусыИнцидентов.Зарегистрирован Тогда
		Если Не ЗначениеЗаполнено(ДатаРегистрации) Тогда 
			ДатаРегистрации = ТекущаяДатаСеанса();
		КонецЕсли;
		Если ЗначениеЗаполнено(ДатаЗакрытия) Тогда
			ДатаЗакрытия = Дата(1,1,1);
		КонецЕсли;
	КонецЕсли;
	
	Если Статус = Перечисления.СтатусыИнцидентов.Решен И НЕ ЗначениеЗаполнено(ДатаЗакрытия) Тогда
		ДатаЗакрытия = ТекущаяДатаСеанса();
	КонецЕсли;

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Автор 			= Пользователи.ТекущийПользователь();
	Статус			= Перечисления.СтатусыИнцидентов.Зарегистрирован;
	ДатаРегистрации	= Дата(1,1,1);
	ДатаЗакрытия	= Дата(1,1,1); 
	ЖелаемаяДатаЗакрытия = Дата(1,1,1);
	Решение			= "";
	Дубль			= Справочники.Инциденты.ПустаяСсылка();

КонецПроцедуры

#КонецОбласти

#КонецЕсли