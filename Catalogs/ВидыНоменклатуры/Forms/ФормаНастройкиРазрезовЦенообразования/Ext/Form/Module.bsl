#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВариантХарактеристики 	= Параметры.ВариантХарактеристики;
	ВариантСерии 			= Параметры.ВариантСерии;
	ВариантУпаковки 		= Параметры.ВариантУпаковки;
	
	ИспользоватьХарактеристики 	= Параметры.ИспользоватьХарактеристики;
	ИспользоватьСерии 			= Параметры.ИспользоватьСерии;
	ИспользоватьУпаковки 		= Параметры.ИспользоватьУпаковки;
	
	ОбновитьВидимостьИДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОбновитьНадпись(ВариантХарактеристики, Элементы.ДекорацияХарактеристики);
	ОбновитьНадпись(ВариантСерии, Элементы.ДекорацияСерии);
	ОбновитьНадпись(ВариантУпаковки, Элементы.ДекорацияУпаковки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("ВариантХарактеристики", 	ВариантХарактеристики);
	РезультатВыбора.Вставить("ВариантСерии", 			ВариантСерии);
	РезультатВыбора.Вставить("ВариантУпаковки", 		ВариантУпаковки);

	Закрыть(РезультатВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантХарактеристикиПриИзменении(Элемент)
	ОбновитьНадпись(ВариантХарактеристики, Элементы.ДекорацияХарактеристики);
КонецПроцедуры

&НаКлиенте
Процедура ВариантСерииПриИзменении(Элемент)
	ОбновитьНадпись(ВариантСерии, Элементы.ДекорацияСерии);
КонецПроцедуры

&НаКлиенте
Процедура ВариантУпаковкиПриИзменении(Элемент)
	ОбновитьНадпись(ВариантУпаковки, Элементы.ДекорацияУпаковки);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьВидимостьИДоступностьЭлементов()

	Элементы.ГруппаХарактеристики.Видимость 	= ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	Элементы.ГруппаСерии.Видимость 				= ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	Элементы.ГруппаУпаковки.Видимость 			= ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры");
	
	Элементы.ГруппаХарактеристики.Доступность 	= ИспользоватьХарактеристики;
	Элементы.ГруппаСерии.Доступность 			= ИспользоватьСерии;
	Элементы.ГруппаУпаковки.Доступность 		= ИспользоватьУпаковки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНадпись(ЗначениеРеквизита, Декорация)
	
	Если ЗначениеРеквизита = 0 Тогда
		Декорация.Заголовок = НСтр("ru = '(снять)'");
	ИначеЕсли ЗначениеРеквизита = 1 Тогда
		Декорация.Заголовок = НСтр("ru = '(установить)'");
	Иначе	
		Декорация.Заголовок = НСтр("ru = '(не изменять)'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
