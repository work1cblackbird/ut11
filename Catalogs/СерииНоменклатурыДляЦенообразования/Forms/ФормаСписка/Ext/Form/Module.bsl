
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Ключи = Строки.ПолучитьКлючи();
	СтрокаСписка = Строки.Получить(Ключи[0]);
	Если Не СтрокаСписка.Данные.Свойство("НетСсылкиНаЭлемент") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СерииНоменклатуры.СерияНоменклатурыДляЦенообразования Как Ссылка,
		|	МАКСИМУМ(ВЫБОР
		|		КОГДА СерииНоменклатуры.Ссылка ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ) КАК НетСсылкиНаЭлемент
		|ИЗ
		|	Справочник.СерииНоменклатуры КАК СерииНоменклатуры
		|ГДЕ
		|	СерииНоменклатуры.СерияНоменклатурыДляЦенообразования В (&Ссылки)
		|СГРУППИРОВАТЬ ПО
		|	СерииНоменклатуры.СерияНоменклатурыДляЦенообразования";
		
	Ссылки = Новый Массив;
	Для Каждого Ключ Из Ключи Цикл // СправочникСсылка.СерииНоменклатурыДляЦенообразования
		Ссылки.Добавить(Ключ);
	КонецЦикла;
		
	Запрос.УстановитьПараметр("Ссылки", Ссылки);
	
	ДанныеПоСериям = Запрос.Выполнить().Выгрузить();
	Для Каждого Строка Из Строки Цикл
		Строка = Строки[Строка.Ключ];
		НайденнаяСтрока = ДанныеПоСериям.Найти(Строка.Данные["Ссылка"], "Ссылка");
		Если НайденнаяСтрока <> Неопределено Тогда
			Строка.Данные["НетСсылкиНаЭлемент"] = НайденнаяСтрока.НетСсылкиНаЭлемент;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти
