	
#Область ОбработчикиСобытийФормы
	
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	МассивСсылокНаПодтверждениеПлатежей	= Параметры.МассивСсылкокНаПодтверждениеПлатежей;  
	СсылкаНаПодтверждениеПлатежа = МассивСсылокНаПодтверждениеПлатежей[0].СсылкаНаПодтверждениеПлатежа; 
	АдресБанковскиеСчета = Параметры.АдресБанковскиеСчета;
	АдресДокументыКВыгрузке = Параметры.АдресДокументыКВыгрузке;
	ВыгрузкаВыполняетсяПриСинхронизации = Параметры.ВыгрузкаВыполняетсяПриСинхронизации;  
	БанковскийСчет = Параметры.БанковскийСчет;
	
	Для Каждого ТекущаяСтрокаМассивСсылкокНаПодтверждениеПлатежей Из МассивСсылокНаПодтверждениеПлатежей Цикл
		
		ДанныеПлатежногоДокумента = ТекущаяСтрокаМассивСсылкокНаПодтверждениеПлатежей.ДанныеПлатежногоДокумента;
		
		ТекстПредставления = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1, на сумму %2 (%3)'"),
				ДанныеПлатежногоДокумента.ВидДокумента,
				ДанныеПлатежногоДокумента.Сумма,
				ДанныеПлатежногоДокумента.Валюта);
					
		НоваяСтрока = ТаблицаПлатежейДляПодтвержденияПлатежа.Добавить();			
		НоваяСтрока.ТекстПодтвердить = ТекстПредставления;
		НоваяСтрока.ПлатежныйДокумент = ДанныеПлатежногоДокумента.Ссылка;
		НоваяСтрока.ВидДокумента = ДанныеПлатежногоДокумента.ВидДокумента;
		НоваяСтрока.Сумма = ДанныеПлатежногоДокумента.Сумма;
		НоваяСтрока.Валюта = ДанныеПлатежногоДокумента.Валюта;
		НоваяСтрока.Номер = ДанныеПлатежногоДокумента.Номер;
		НоваяСтрока.Дата = ДанныеПлатежногоДокумента.Дата;
		НоваяСтрока.СсылкаНаПодтверждениеПлатежа = 
			ТекущаяСтрокаМассивСсылкокНаПодтверждениеПлатежей.СсылкаНаПодтверждениеПлатежа; 
		НоваяСтрока.Сервис = ТекущаяСтрокаМассивСсылкокНаПодтверждениеПлатежей.ПараметрыЗаполнения.Сервис;
		
	КонецЦикла;	

	Если ТаблицаПлатежейДляПодтвержденияПлатежа.Количество() > 0 Тогда
		ТекущиеДанные = ТаблицаПлатежейДляПодтвержденияПлатежа[0];
		
		СформироватьОтображениеТекстаПодтвержденияПлатежаНаСервер(
						ТекущиеДанные.ПлатежныйДокумент,
						ТекущиеДанные.ВидДокумента, 
						ТекущиеДанные.Номер,
						ТекущиеДанные.Дата,
						ТекущиеДанные.Сумма,
						ТекущиеДанные.Валюта,
						ТекущиеДанные.Сервис,
						ТекущиеДанные.СсылкаНаПодтверждениеПлатежа);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьСтатусыПлатежей(Команда)

	ОбменССервисомBankFeedsКлиент.ПроверитьСтатусПлатежейПослеОтправки(ЭтотОбъект);	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаПлатежейДляПодтвержденияПлатежа

&НаКлиенте
Процедура ТаблицаПлатежейДляПодтвержденияПлатежаПриАктивизацииПоля(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаПлатежейДляПодтвержденияПлатежа.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		СформироватьОтображениеТекстаПодтвержденияПлатежаНаСервер(
						ТекущиеДанные.ПлатежныйДокумент,
						ТекущиеДанные.ВидДокумента, 
						ТекущиеДанные.Номер,
						ТекущиеДанные.Дата,
						ТекущиеДанные.Сумма,
						ТекущиеДанные.Валюта,
						ТекущиеДанные.Сервис,
						ТекущиеДанные.СсылкаНаПодтверждениеПлатежа);
						
	Иначе
		
		Элементы.ДекорацияПодтвержденияПлатежа.Заголовок = Новый ФорматированнаяСтрока(
		    					НСтр("ru = 'Все платежные документы отправлены'"));
			
	КонецЕсли;					
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УправлениеФормой()
	
	Если ТаблицаПлатежейДляПодтвержденияПлатежа.Количество() = 1 Тогда
		Заголовок = НСтр("ru = 'Подтверждение платежа в сервисе'");
		
		Элементы.ТаблицаПлатежейДляПодтвержденияПлатежа.Видимость = False;
		
		Элементы.ФормаПодтверждения.Подсказка = НСтр("ru = 'После подтверждения платежей, закройте это окно'");
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ОбновитьСписокПлатежей() Экспорт
	
	ОбновитьСписокПлатежейНаСервере();
	
	Если ТаблицаПлатежейДляПодтвержденияПлатежа.Количество() > 0 Тогда
		ТекущиеДанные = ТаблицаПлатежейДляПодтвержденияПлатежа[0];
		
		СформироватьОтображениеТекстаПодтвержденияПлатежаНаСервер(
						ТекущиеДанные.ПлатежныйДокумент,
						ТекущиеДанные.ВидДокумента, 
						ТекущиеДанные.Номер,
						ТекущиеДанные.Дата,
						ТекущиеДанные.Сумма,
						ТекущиеДанные.Валюта,
						ТекущиеДанные.Сервис,
						ТекущиеДанные.СсылкаНаПодтверждениеПлатежа);
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Процедура ОбновитьСписокПлатежейНаСервере()
	
	МассивПлатежей = Новый Массив();
	Для Каждого ТекущаяСтрокаТаблицаПлатежейДляПодтвержденияПлатежа Из ТаблицаПлатежейДляПодтвержденияПлатежа Цикл
		
		МассивПлатежей.Добавить(ТекущаяСтрокаТаблицаПлатежейДляПодтвержденияПлатежа.ПлатежныйДокумент);
		
	КонецЦикла;	
	
	Запрос = Новый Запрос(); 
	Запрос.УстановитьПараметр("МассивПлатежей", МассивПлатежей);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатусыТранзакцийBankFeeds.Объект КАК ПлатежныйДокумент,
	|	СтатусыТранзакцийBankFeeds.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.СтатусыТранзакцийBankFeeds КАК СтатусыТранзакцийBankFeeds
	|ГДЕ
	|	СтатусыТранзакцийBankFeeds.Объект В(&МассивПлатежей)";
	
	Результат = Запрос.Выполнить().Выбрать();
	ФильтрПоПлатежам = Новый Структура("ПлатежныйДокумент");
	
	Пока Результат.Следующий() Цикл
		
		Если Результат.Статус = Перечисления.СтатусыПлатежныхДокументовBankFeeds.НеОтправлен Тогда
			Продолжить;
		КонецЕсли;	
		
	    ФильтрПоПлатежам.ПлатежныйДокумент = Результат.ПлатежныйДокумент;
		
		НайденныеСтроки = ТаблицаПлатежейДляПодтвержденияПлатежа.НайтиСтроки(ФильтрПоПлатежам);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			
			ТаблицаПлатежейДляПодтвержденияПлатежа.Удалить(НайденныеСтроки[0]);				
	
		КонецЕсли;	
		
	КонецЦикла;
	
КонецПроцедуры	
	
&НаСервере
Процедура СформироватьОтображениеТекстаПодтвержденияПлатежаНаСервер(ПлатежныйДокумент,ВидДокумента,Номер,Дата,Сумма,Валюта,Сервис,СсылкаНаПодтверждениеПлатежа)
	
	Элементы.ДекорацияПодтвержденияПлатежа.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
		НСтр("ru = 'Согласно требованиям провайдера, <a href = ""%1"">%2 </a> %3 необходимо подтвердить на сайте сервиса %4
				|Перейдите по <a href = ""%5"">Ссылке</a> и подтвердите платеж'"),
		ПолучитьНавигационнуюСсылку(ПлатежныйДокумент), ВидДокумента + НСтр("ru = ' №'") + ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Номер) 
			+" от: "+ Формат(Дата, "ДФ=dd.MM.yyyy; ДЛФ=D"), 
			НСтр("ru = 'на сумму'") + " " + Сумма + " "+ Валюта, 
		Сервис,	СсылкаНаПодтверждениеПлатежа);
	
КонецПроцедуры

#КонецОбласти
