#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПользовательскиеНастройки = ХранилищеПользовательскихНастроекКомпоновкиДанных.Получить();
	Если Ссылка.Пустая() Тогда
		Если ПользовательскиеНастройки = Неопределено Тогда
			ПользовательскиеНастройки = Справочники.СтруктураЦелей.ПользовательскиеНастройкиПоУмолчанию(Владелец);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Заполним обязательный параметр ВалютаРасчета для СКД где он есть и не заполнен
	Если НЕ ПользовательскиеНастройки = Неопределено Тогда
		ПараметрВалютаРасчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(ПользовательскиеНастройки,
			"ВалютаРасчета");
		Если НЕ ПараметрВалютаРасчета = Неопределено И ПараметрВалютаРасчета.Значение = Справочники.Валюты.ПустаяСсылка() Тогда
			КомпоновкаДанныхКлиентСервер.УстановитьПараметр(ПользовательскиеНастройки,
				"ВалютаРасчета",
				МониторингЦелевыхПоказателей.ПолучитьВалютуПоУмолчанию(),
				Истина);
			
			ХранилищеПользовательскихНастроекКомпоновкиДанных = Новый ХранилищеЗначения(ПользовательскиеНастройки);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Очистим кэш рассчитанных значений варианта анализа, 
	// т.к. структура рассчитанных хранимых данных зависит от его настроек.
	НаборЗаписей = РегистрыСведений.ИсточникиДанныхВариантовАнализаЦелевыхПоказателей.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ВариантАнализа.Установить(Ссылка);
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли