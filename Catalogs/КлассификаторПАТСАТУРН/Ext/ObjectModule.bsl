#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияИСПереопределяемый.ПередЗаписьюОбъекта(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	МассивНепроверяемыхРеквизитов = Новый Массив;

	Если ВидПродукции = Перечисления.ВидыПродукцииСАТУРН.Тукосмесь Тогда
		
		КоличествоСоставляющихТукосмеси = СоставТукосмеси.Количество();
		Если КоличествоСоставляющихТукосмеси < 2 Тогда
			
			Шаблон = НСтр("ru = 'Количество ПА в составе тукосмеси - %1. 
						  |Должно быть не менее 2.'");
			
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(Шаблон, Формат(КоличествоСоставляющихТукосмеси, "ЧДЦ=0; ЧГ=3,0;")),,
				"СоставТукосмеси",,
				Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВидПродукции <> Перечисления.ВидыПродукцииСАТУРН.Тукосмесь Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СоставТукосмеси");
	КонецЕсли;
	
	Если ВидПродукции <> Перечисления.ВидыПродукцииСАТУРН.Комбипак Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СоставКомбипака");
	КонецЕсли;
	
	ИнтеграцияСАТУРНПереопределяемый.ПриОпределенииОбработкиПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли