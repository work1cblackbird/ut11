#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает список реквизитов, которые разрешается редактировать
// с помощью обработки группового изменения объектов.
//
// Возвращаемое значение:
//  Массив - список имен реквизитов объекта.
//
Функция РеквизитыРедактируемыеВГрупповойОбработке() Экспорт
	
	Возврат РаботаСФайлами.РеквизитыРедактируемыеВГрупповойОбработке();
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает краткое описание последнего сообщения обмена указанного типа (Передано, ВОчереди, СОшибкой, Любое)
//   по документу. Если такого сообщения нет, возвращает Неопределено.
//
// Параметры:
//   ДокументСсылка         - ОпределяемыйТип.ДокументыСАТУРН - документ для получения описания
//   ТипПоследнегоСообщения - Строка                        - тип сообщения из фиксированного списка
// Возвращаемое значение:
//   Структура - реквизиты сообщения:
//   * ВладелецФайла - ОпределяемыйТип.ДокументыСАТУРН            - владелец файла
//   * Операция      - ПеречислениеСсылка.ВидыОперацийСАТУРН      - операция обмена
//   * ТипСообщения  - ПеречислениеСсылка.ТипыЗапросовИС        - тип сообщения (исходящий, входящий)
//   * Сообщение     - СправочникСсылка.САТУРНПрисоединенныеФайлы - ссылка на сообщение
//                   - Неопределено - сообщение не найдено
//
Функция ПоследнееСообщение(ДокументСсылка, ТипПоследнегоСообщения = "Передано") Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	САТУРНПрисоединенныеФайлы.Ссылка                     КАК Сообщение,
	|	САТУРНПрисоединенныеФайлы.ТипСообщения               КАК ТипСообщения,
	|	САТУРНПрисоединенныеФайлы.Документ.ОрганизацияСАТУРН КАК ОрганизацияСАТУРН,
	|	САТУРНПрисоединенныеФайлы.СтатусОбработки            КАК СтатусОбработки,
	|	САТУРНПрисоединенныеФайлы.ВладелецФайла              КАК ВладелецФайла,
	|	САТУРНПрисоединенныеФайлы.Операция                   КАК Операция
	|ПОМЕСТИТЬ САТУРНПрисоединенныеФайлы
	|ИЗ
	|	Справочник.САТУРНПрисоединенныеФайлы КАК САТУРНПрисоединенныеФайлы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОчередьСообщенийСАТУРН КАК ОчередьСообщенийСАТУРН
	|		ПО ОчередьСообщенийСАТУРН.Сообщение = САТУРНПрисоединенныеФайлы.Ссылка
	|ГДЕ
	|	САТУРНПрисоединенныеФайлы.Документ = &Документ
	|	И &Условие1
	|УПОРЯДОЧИТЬ ПО
	|	САТУРНПрисоединенныеФайлы.ДатаСоздания УБЫВ,
	|	ВЫБОР
	|		КОГДА САТУРНПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий) ТОГДА
	|			0
	|		ИНАЧЕ
	|			1
	|	КОНЕЦ УБЫВ
	|;
	|
	|//////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОчередьСообщенийСАТУРН.Сообщение                  КАК Сообщение,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)   КАК ТипСообщения,
	|	ОчередьСообщенийСАТУРН.ОрганизацияСАТУРН          КАК ОрганизацияСАТУРН,
	|	НЕОПРЕДЕЛЕНО                                      КАК СтатусОбработки,
	|	ОчередьСообщенийСАТУРН.Документ                   КАК ВладелецФайла,
	|	ОчередьСообщенийСАТУРН.Операция                   КАК Операция
	|ПОМЕСТИТЬ ОчередьСообщенийСАТУРН
	|ИЗ
	|	РегистрСведений.ОчередьСообщенийСАТУРН КАК ОчередьСообщенийСАТУРН
	|ГДЕ
	|	ОчередьСообщенийСАТУРН.Документ = &Документ
	|	И ОчередьСообщенийСАТУРН.СообщениеОснование = """"
	|	И &Условие2
	|УПОРЯДОЧИТЬ ПО
	|	ОчередьСообщенийСАТУРН.ДатаСоздания УБЫВ
	|;
	|
	|//////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Сообщение         КАК Сообщение,
	|	Т.ТипСообщения      КАК ТипСообщения,
	|	Т.ОрганизацияСАТУРН КАК ОрганизацияСАТУРН,
	|	Т.СтатусОбработки   КАК СтатусОбработки,
	|	Т.ВладелецФайла     КАК ВладелецФайла,
	|	Т.Операция          КАК Операция,
	|	1                   КАК Приоритет
	|ПОМЕСТИТЬ Итог
	|ИЗ
	|	САТУРНПрисоединенныеФайлы КАК Т
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Сообщение         КАК Сообщение,
	|	Т.ТипСообщения      КАК ТипСообщения,
	|	Т.ОрганизацияСАТУРН КАК ОрганизацияСАТУРН,
	|	Т.СтатусОбработки   КАК СтатусОбработки,
	|	Т.ВладелецФайла     КАК ВладелецФайла,
	|	Т.Операция          КАК Операция,
	|	2                   КАК Приоритет
	|ИЗ
	|	ОчередьСообщенийСАТУРН КАК Т
	|;
	|
	|//////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Т.Сообщение         КАК Сообщение,
	|	Т.ТипСообщения      КАК ТипСообщения,
	|	Т.ОрганизацияСАТУРН КАК ОрганизацияСАТУРН,
	|	Т.СтатусОбработки   КАК СтатусОбработки,
	|	Т.ВладелецФайла     КАК ВладелецФайла,
	|	Т.Операция          КАК Операция,
	|	Т.Приоритет         КАК Приоритет
	|ИЗ
	|	Итог КАК Т
	|УПОРЯДОЧИТЬ ПО
	|	Т.Приоритет УБЫВ
	|");
	
	Запрос.УстановитьПараметр("Документ", ДокументСсылка);
	
	Если ТипПоследнегоСообщения = "Передано" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие1", "ИСТИНА");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие2", "ЛОЖЬ");
	ИначеЕсли ТипПоследнегоСообщения = "ВОчереди" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие1", "ЛОЖЬ");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие2", "ИСТИНА");
	ИначеЕсли ТипПоследнегоСообщения = "СОшибкой" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие1",
			"
			|ВЫБОР
			|	КОГДА ВЫРАЗИТЬ(САТУРНПрисоединенныеФайлы.Описание КАК Строка(255)) = """"
			|		ТОГДА ЛОЖЬ
			|	ИНАЧЕ ИСТИНА
			|КОНЕЦ
			|");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие2", "ЛОЖЬ");
	ИначеЕсли ТипПоследнегоСообщения = "Любое" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие1", "ИСТИНА");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие2", "ИСТИНА");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Не Выборка.Следующий() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ОрганизацияСАТУРН");
	ВозвращаемоеЗначение.Вставить("Сообщение");
	ВозвращаемоеЗначение.Вставить("ТипСообщения");
	ВозвращаемоеЗначение.Вставить("ВладелецФайла");
	ВозвращаемоеЗначение.Вставить("Операция");
	ВозвращаемоеЗначение.Вставить("СтатусОбработки");
	
	ЗаполнитьЗначенияСвойств(ВозвращаемоеЗначение, Выборка);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Возвращает выборку последних сообщений протокола обмена, обработанных с ошибкой,
//   по документу. Если такого сообщения нет, возвращает Неопределено.
//
// Параметры:
//   ДокументСсылка         - ОпределяемыйТип.ДокументыСАТУРН - документ для получения описания
//   ТипПоследнегоСообщения - Строка                        - тип сообщения из фиксированного списка
//   ИдентификаторСтроки    - ОпределяемыйТип.УникальныйИдентификаторИС - идентификатор строки
// Возвращаемое значение:
//   ВыборкаИзРезультатаЗапроса - элементы коллекции:
//   * ВладелецФайла - ОпределяемыйТип.ДокументыСАТУРН            - владелец файла
//   * Операция      - ПеречислениеСсылка.ВидыОперацийСАТУРН      - операция обмена
//   * ТипСообщения  - ПеречислениеСсылка.ТипыЗапросовИС        - тип сообщения (исходящий, входящий)
//   * Сообщение     - СправочникСсылка.САТУРНПрисоединенныеФайлы - ссылка на сообщение
//                   - Неопределено - сообщение не найдено
//
Функция ПоследниеСообщенияСОшибкой(ДокументСсылка, ТипПоследнегоСообщения = "Передано", ИдентификаторСтроки = "") Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	САТУРНПрисоединенныеФайлы.Версия   КАК Версия,
	|	САТУРНПрисоединенныеФайлы.Документ КАК Документ,
	|	САТУРНПрисоединенныеФайлы.Операция КАК Операция,
	|	САТУРНПрисоединенныеФайлы.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ САТУРНПрисоединенныеФайлыВерсии
	|ИЗ
	|	Справочник.САТУРНПрисоединенныеФайлы КАК САТУРНПрисоединенныеФайлы
	|ГДЕ
	|	САТУРНПрисоединенныеФайлы.Документ = &Документ
	|	И ВЫБОР
	|	КОГДА ВЫРАЗИТЬ(САТУРНПрисоединенныеФайлы.Описание КАК Строка(255)) = """"
	|		ТОГДА ЛОЖЬ
	|	ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|	И ВЫБОР
	|	КОГДА &ИдентификаторСтроки = """"
	|		ТОГДА ИСТИНА
	|	ИНАЧЕ
	|		САТУРНПрисоединенныеФайлы.ИдентификаторСтроки = &ИдентификаторСтроки
	|	КОНЕЦ
	|УПОРЯДОЧИТЬ ПО
	|	САТУРНПрисоединенныеФайлы.ДатаСоздания УБЫВ,
	|	ВЫБОР
	|		КОГДА САТУРНПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий) ТОГДА
	|			0
	|		ИНАЧЕ
	|			1
	|	КОНЕЦ УБЫВ
	|;
	|
	|//////////////////////////////////
	|ВЫБРАТЬ
	|	САТУРНПрисоединенныеФайлы.Ссылка               КАК Сообщение,
	|	САТУРНПрисоединенныеФайлы.ТипСообщения         КАК ТипСообщения,
	|	САТУРНПрисоединенныеФайлы.СтатусОбработки      КАК СтатусОбработки,
	|	САТУРНПрисоединенныеФайлы.ВладелецФайла        КАК ВладелецФайла,
	|	САТУРНПрисоединенныеФайлы.Операция             КАК Операция
	|ИЗ
	|	САТУРНПрисоединенныеФайлыВерсии КАК САТУРНПрисоединенныеФайлыВерсии
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.САТУРНПрисоединенныеФайлы КАК САТУРНПрисоединенныеФайлы
	|		ПО САТУРНПрисоединенныеФайлыВерсии.Документ = САТУРНПрисоединенныеФайлы.Документ
	|		И САТУРНПрисоединенныеФайлыВерсии.Операция = САТУРНПрисоединенныеФайлы.Операция
	|		И САТУРНПрисоединенныеФайлыВерсии.Версия = САТУРНПрисоединенныеФайлы.Версия
	|		И САТУРНПрисоединенныеФайлыВерсии.ИдентификаторСтроки = САТУРНПрисоединенныеФайлы.ИдентификаторСтроки
	|ГДЕ
	|	ВЫБОР
	|	КОГДА ВЫРАЗИТЬ(САТУРНПрисоединенныеФайлы.Описание КАК Строка(255)) = """"
	|		ТОГДА ЛОЖЬ
	|	ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|УПОРЯДОЧИТЬ ПО
	|	САТУРНПрисоединенныеФайлы.ДатаСоздания УБЫВ,
	|	ВЫБОР
	|		КОГДА САТУРНПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий) ТОГДА
	|			0
	|		ИНАЧЕ
	|			1
	|	КОНЕЦ УБЫВ
	|");
	
	Запрос.УстановитьПараметр("Документ",            ДокументСсылка);
	Запрос.УстановитьПараметр("ИдентификаторСтроки", ИдентификаторСтроки);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Результат.Выбрать();
	
КонецФункции

// Возвращает текст последней ошибки из протокола обмена
//
// Параметры:
//  ДокументСсылка      - ДокументСсылка - документ, по которому требуется показать ошибку обмена
//  ИдентификаторСтроки - ОпределяемыйТип.УникальныйИдентификаторИС - идентификатор строки
//
// Возвращаемое значение:
//  Строка - текст ошибки обмена.
//
Функция ТекстОшибкиИзПротокола(ДокументСсылка, ИдентификаторСтроки = "") Экспорт
	
	ДанныеПоследнегоСообщения = ПоследниеСообщенияСОшибкой(ДокументСсылка, ИдентификаторСтроки);
	Если ДанныеПоследнегоСообщения = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	МассивОшибок = Новый Массив;
	Пока ДанныеПоследнегоСообщения.Следующий() Цикл
		МассивОшибок.Добавить(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеПоследнегоСообщения.Сообщение, "Описание"));
	КонецЦикла;
	
	Возврат СтрСоединить(МассивОшибок, Символы.ПС);
	
КонецФункции

#КонецОбласти

#КонецЕсли
