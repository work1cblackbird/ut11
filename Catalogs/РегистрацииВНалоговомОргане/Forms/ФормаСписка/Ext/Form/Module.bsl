#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоФизическоеЛицо = Ложь;
	
	Если Параметры.Отбор.Свойство("Владелец") Тогда
		
		Организация = Параметры.Отбор.Владелец;
		
		УстановитьОтбор();
		
		Если Параметры.Отбор.Владелец.Метаданные().Реквизиты.Найти("ЮридическоеФизическоеЛицо") <> Неопределено Тогда
						
			РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Отбор.Владелец, "ЮридическоеФизическоеЛицо");
						
			ЭтоФизическоеЛицо = РеквизитыОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
			
		КонецЕсли;
		
		Параметры.Отбор.Удалить("Владелец");
		
	КонецЕсли;
	
	Элементы.КПП.Видимость = НЕ ЭтоФизическоеЛицо;
	
	Элементы.Владелец.Видимость = НЕ ЗначениеЗаполнено(Организация);
	Элементы.Организация.Видимость = ЗначениеЗаполнено(Организация);
	ТолькоПросмотр = НЕ ЗначениеЗаполнено(Организация);
	
	Если Не Справочники.Организации.ИспользуетсяНесколькоОрганизаций() Тогда
		
		Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
		
		Элементы.Владелец.Видимость    = Ложь;
		Элементы.Организация.Видимость = Ложь;
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура УстановитьОтбор()
			
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор,
		"Владелец",
		Организация,
		,
		,
		Истина);
			
КонецПроцедуры

#КонецОбласти
