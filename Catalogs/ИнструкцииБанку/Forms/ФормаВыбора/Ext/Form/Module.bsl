
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("СписокИнструкций") Тогда
		ТекстСообщения = НСтр("ru='Непосредственное открытие этой формы не предусмотрено.'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Ссылка КАК Код,
	|	Описание,
	|	НеДопускаетсяСовместноеИспользование,
	|	Ссылка В (&ВыбранныеИнструкции) КАК Пометка
	|ИЗ
	|	Справочник.ИнструкцииБанку
	|";
	Запрос.УстановитьПараметр("ВыбранныеИнструкции", Параметры.СписокИнструкций);
	СписокИнструкций.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура СписокИнструкцийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПоказатьЗначение(,СписокИнструкций[ВыбраннаяСтрока].Код);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокИнструкцийПометкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СписокИнструкций.ТекущиеДанные;
	Если ТекущиеДанные.Пометка Тогда
		НеДопускаетсяСовместноеИспользование = ТекущиеДанные.НеДопускаетсяСовместноеИспользование;
		ОтмеченныеСтроки = СписокИнструкций.НайтиСтроки(Новый Структура("Пометка", Истина));
		Для каждого СтрокаИнструкции Из ОтмеченныеСтроки Цикл
			Если СтрНайти(НеДопускаетсяСовместноеИспользование, Строка(СтрокаИнструкции.Код)) Тогда
				СтрокаИнструкции.Пометка = Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ОтмеченныеИнструкции = Новый Массив;
	Для каждого ОтмеченнаяИнструкция Из СписокИнструкций.НайтиСтроки(Новый Структура("Пометка", Истина)) Цикл
		ОтмеченныеИнструкции.Добавить(ОтмеченнаяИнструкция.Код);
	КонецЦикла;
	
	Закрыть(ОтмеченныеИнструкции);
	
КонецПроцедуры

#КонецОбласти