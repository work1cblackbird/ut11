
#Область ОписаниеПеременных

&НаКлиенте
Перем ХронологияПереключенияСтраниц;

&НаКлиенте
Перем ПодключаемыйМодуль;

&НаКлиенте
Перем СертификатXML; //данные сертификата дополнительной обработки в виде строки

&НаКлиенте
Перем МенеджерКриптографииНаКлиенте;

&НаКлиенте
Перем ОтпечаткиСертификатов;

&НаКлиенте
Перем ИдентификаторЗадания; // идентификатор фоновых операций

&НаКлиенте
Перем Отпечаток; // отпечаток текущего сертификата

&НаКлиенте
Перем ИдентификаторХранилища; // Идентификатор банковского ключа

&НаКлиенте
Перем URLВК; // ссылка на скачивание внешней компоненты

&НаКлиенте
Перем КомуВыданСертификатВК;

&НаКлиенте
Перем СертификатВК; //двоичные данные сертификата внешней компоненты

&НаКлиенте
Перем ПараметрыОбработчикаОжидания; // содержит параметры подключаемых процедур

&НаКлиенте
Перем ТикетОбменаСбербанк; //тикет, который используется при получении настроек обмена из Сбербанка

&НаКлиенте
Перем ЗаблокированныеКриптопрофилиСбербанк; // содержит массив заблокированных криптопрофилей, полученных из персональной информации организации

&НаКлиенте
Перем НаименованиеБанка; // наименование банка

&НаКлиенте
Перем НомерПодразделенияБанка; // номер подразделения банка

&НаКлиенте
Перем ТипыПисемСсылкаСбербанк; // таблица типов писем Сбербанка

//@skip-warning
&НаКлиенте
Перем СертификатыСбербанка; // массив сертификатов Сбербанка

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьРеквизитыФормы();
	
	Если Не РазделениеВключено Тогда
		НаименованиеЗадания = НСтр("ru = '1С:ДиректБанк: Обновление списка банков, поддерживающих обмен.'");
		ФоновыеЗадания.Выполнить("ОбменСБанкамиСлужебный.ЗагрузитьСписокБанковССайта", , , НаименованиеЗадания);
	КонецЕсли;
	
	ЕстьПравоДобавленияНастройкиЭДО = ПравоДоступа("Добавление", Метаданные.Справочники.НастройкиОбменСБанками);
	
	Если Не ЕстьПравоДобавленияНастройкиЭДО Тогда
		ТекстСообщения = НСтр("ru = 'Нет прав настройки прямого обмена с банком.
									|Обратитесь к администратору'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
	КонецЕсли;
	
	ИспользуетсяНесколькоОрганизацийЭД = ОбщегоНазначенияБЭД.ИспользуетсяНесколькоОрганизаций();
	
	Если Не ИспользуетсяНесколькоОрганизацийЭД Тогда
		Организация = ОбщегоНазначенияБЭД.ОрганизацияПоУмолчанию();
		Если ЗначениеЗаполнено(Организация) Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Организация", "Видимость", Ложь);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		Организация = Параметры.Организация;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Банк) Тогда
		Банк = Параметры.Банк;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ИдентификаторОрганизации) Тогда
		ИдентификаторОрганизации = Параметры.ИдентификаторОрганизации;
	КонецЕсли;
	
	Если Параметры.Свойство("ЗначенияЗаполнения") И Параметры.ЗначенияЗаполнения.Свойство("Организация")
		И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Организация) Тогда
		Организация = Параметры.ЗначенияЗаполнения.Организация;
	КонецЕсли;
	
	Если  Параметры.Свойство("ЗначенияЗаполнения") И Параметры.ЗначенияЗаполнения.Свойство("Банк")
		И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Банк) Тогда
		Банк = Параметры.ЗначенияЗаполнения.Банк;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) И Не ЗначениеЗаполнено(Банк) Тогда
		БанкиОрганизации = БанкиОрганизации(Организация);
		Если БанкиОрганизации.Количество() = 1 Тогда
			Банк = БанкиОрганизации.Получить(0);
		КонецЕсли;
	КонецЕсли;

	Параметры.Свойство("УпрощенныйРежимНастройки", УпрощенныйРежимНастройки);
	
	Если НЕ ЗначениеЗаполнено(Банк) Тогда
		ПараметрыОтбора = Новый Структура;
		Если ЗначениеЗаполнено(Организация) Тогда
			ПараметрыОтбора.Вставить("Организация", Организация);
		КонецЕсли;
		УчастникиОбмена = УчастникиОбменаПоСчетам(ПараметрыОтбора);
		
		// Упрощенный режим вызывается из баннера рекламы ДиректБанк.
		// Необходимо исключить из списка неизвестные банки.
		Если УпрощенныйРежимНастройки Тогда
			МассивБИКИзвестныхБанков = Новый Массив;
			ТаблицаИзвестныхБанков = ОбменСБанкамиСлужебныйПовтИсп.СписокБанков();
			КоличествоЗаписей = ТаблицаИзвестныхБанков.ВысотаТаблицы;
			Для Индекс = 2 По КоличествоЗаписей Цикл
				БИК = ТаблицаИзвестныхБанков.Область(Индекс, 2).Текст;
				МассивБИКИзвестныхБанков.Добавить(БИК);
			КонецЦикла;
			КоличествоУчастников = УчастникиОбмена.Количество();
			Для Индекс = 1 По КоличествоУчастников Цикл
				Если МассивБИКИзвестныхБанков.Найти(УчастникиОбмена[КоличествоУчастников - Индекс].БИК) = Неопределено Тогда
					УчастникиОбмена.Удалить(УчастникиОбмена[КоличествоУчастников - Индекс]);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если УчастникиОбмена.Количество() Тогда
			
			ЕстьОрганизация = Элементы.Найти("Организация") <> Неопределено;
			ЕстьБанк = Элементы.Найти("Банк") <> Неопределено;
			МассивОрганизаций = Новый Массив;
			МассивБанков = Новый Массив;
		
			Для Каждого СтрокаТаблицы Из УчастникиОбмена Цикл
				Если ЕстьОрганизация Тогда
					Если МассивОрганизаций.Найти(СтрокаТаблицы.Организация) = Неопределено Тогда
						МассивОрганизаций.Добавить(СтрокаТаблицы.Организация);
					КонецЕсли;
				Иначе
					Организация = СтрокаТаблицы.Организация;
				КонецЕсли;
				
				Если ЕстьБанк Тогда
					Если МассивБанков.Найти(СтрокаТаблицы.Банк) = Неопределено Тогда
						МассивБанков.Добавить(СтрокаТаблицы.Банк);
					КонецЕсли;
				Иначе
					Банк = СтрокаТаблицы.Банк;
				КонецЕсли;
				
			КонецЦикла;
			
			Если ЕстьОрганизация И МассивОрганизаций.Количество() = 1 Тогда
				Организация = МассивОрганизаций.Получить(0);
			КонецЕсли;
			
			Если ЕстьБанк И МассивБанков.Количество() = 1 Тогда
				Банк = МассивБанков.Получить(0);
			КонецЕсли;
			
			Если УпрощенныйРежимНастройки Тогда
				Если ЕстьОрганизация Тогда
					Элементы.Организация.РежимВыбораИзСписка = Истина;
					Элементы.Организация.КнопкаВыбора = Ложь;
					Элементы.Организация.КнопкаВыпадающегоСписка = Истина;
					Элементы.Организация.СписокВыбора.ЗагрузитьЗначения(МассивОрганизаций);
					
				КонецЕсли;
				Если ЕстьБанк Тогда
					Элементы.Банк.РежимВыбораИзСписка = Истина;
					Элементы.Банк.КнопкаВыбора = Ложь;
					Элементы.Банк.КнопкаВыпадающегоСписка = Истина;
					Элементы.Банк.СписокВыбора.ЗагрузитьЗначения(МассивБанков);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	НеПоказыватьСтраницуВыборОрганизацииИБанка = ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Банк);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбменСБанкамиПереопределяемый.ПроверитьИспользованиеТестовогоРежима(ТестовыйРежим);
	
	Если Не ТестовыйРежим Тогда
		Элементы.ПолучениеНастроекССервераБанка.Видимость = Ложь;
		СпособСозданияНастройки = 1;
	КонецЕсли;
	
	// Загрузка настроек из файла в форме списка настроек обмена
	Если ЗначениеЗаполнено(Параметры.АдресДанныхФайлаНастроек) Тогда
		СпособСозданияНастройки = 1;
		АдресФайлаНастроекОбмена = Параметры.АдресДанныхФайлаНастроек;
		НастройкиЗагруженыИзФайла = Истина;
		УпрощенныйРежимНастройки = Ложь;
	Иначе
		Если ЗначениеЗаполнено(Банк) Тогда
			НастройкиОбменаСБанком = ОбменСБанкамиСлужебныйВызовСервера.ПоставляемыеНастройкиОбмена(Банк, Истина);
			Если ЗначениеЗаполнено(НастройкиОбменаСБанком) Тогда
				ПрограммаБанка = НастройкиОбменаСБанком.ПрограммаБанка;
				Если ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезВК Тогда
					Элементы.ПолучениеНастроекССервераБанка.Видимость = Ложь;
					СпособСозданияНастройки = 0; // Автоматическое получение настроек
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Если ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Банк) Тогда
			НастройкаОбмена = ОбменСБанкамиСлужебныйВызовСервера.НастройкаОбмена(Организация, Банк, Ложь, Ложь);
			Если ЗначениеЗаполнено(НастройкаОбмена) Тогда
				ВерсияФормата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаОбмена, "ВерсияФормата");
			КонецЕсли;
			Если ЗначениеЗаполнено(НастройкиОбменаСБанком) И (ПрограммаБанка = Перечисления.ПрограммыБанка.АсинхронныйОбмен
																ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн
																ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезВК) Тогда
				УпрощенныйРежимНастройки = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если УпрощенныйРежимНастройки Тогда
		КлючСохраненияПоложенияОкна = "УпрощенныйРежимНастройки";
		НеПоказыватьСтраницуИнформации = Истина;
		НастройкаФормыУпрощенногоРежима();
	Иначе
		КлючСохраненияПоложенияОкна = "ОбычныйРежимНастройки";
		Элементы.СтраницаУпрощенныйРежимНастройки.Видимость = Ложь;
	КонецЕсли;
	
	НастроитьЭлементыФормыПриОткрытии();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ХронологияПереключенияСтраниц = Новый Массив;
	
	#Если ВебКлиент Тогда
		Элементы.ПутьКФайлуНастроек.РедактированиеТекста = Ложь;
	#КонецЕсли
	
	ИзменитьЗаголовок();
	
	СпособАутентификацииПриИзменении(Неопределено);
	
	Если НеПоказыватьСтраницуИнформации Тогда
		Если НеПоказыватьСтраницуВыборОрганизацииИБанка Тогда
			ПереключитьШагПослеВыбораОрганизацииИБанка();
		Иначе
			Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаВыборОрганизацииИБанка;
		КонецЕсли;
	КонецЕсли;
	
	Если УпрощенныйРежимНастройки Тогда
		Возврат;
	КонецЕсли;
	
	// Загрузка из файла формы списка настроек обмена.
	Если СпособСозданияНастройки = 1 И ЗначениеЗаполнено(АдресФайлаНастроекОбмена) Тогда
		
		УчастникиОбменаИзФайла = УчастникиОбменаИзФайла(АдресФайлаНастроекОбмена, Организация, ЛокальныйФайл, Банк);
		
		Если УчастникиОбменаИзФайла = Неопределено Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Банк = УчастникиОбменаИзФайла.Банк;
		Организация = УчастникиОбменаИзФайла.Организация;
		
		ИзменитьЗаголовок();
		
		Если УчастникиОбменаИзФайла.ЭтоНастройкиСбербанка Тогда
			ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн");
			СменитьСтраницуШаги(Элементы.СтраницаФинал);
			URLВК = УчастникиОбменаИзФайла.URLВК;
			ПодключитьОбработчикОжидания("ОпределитьИдентификаторВКСбербанк", 0.1, Истина);
		Иначе
			Элементы.СозданиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
			СменитьСтраницуШаги(Элементы.СтраницаФинал);
			#Если ВебКлиент Тогда
				ОбновитьОтображениеДанных();
			#КонецЕсли
			ПодключитьОбработчикОжидания("СоздатьНастройкуОбмена", 0.1, Истина);
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НаборКонстант" И Источник = "ИспользоватьДополнительныеОтчетыИОбработки" Тогда
		
		ОбновитьСтраницуВнешнийМодуль();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если НЕ РаботаПомощникаЗавершена Тогда
	
		СтандартнаяОбработка = Ложь;
		Если ЗавершениеРаботы Тогда
			ТекстПредупреждения = НСтр("ru = 'Подключение к сервису 1С:ДиректБанк не завершено.
											|Продолжить закрытие программы?'");
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Подключение к сервису 1С:ДиректБанк не завершено.
											|Закрыть помощник подключения?'");
		КонецЕсли;
	
		Оповещение = Новый ОписаниеОповещения("ПриЗакрытииПомощника", ЭтотОбъект);
		
		ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияПроизвольнойФормы(
			ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, "ЗакрытьФорму", Оповещение);

	КонецЕсли;
	
	Если НЕ ЗавершениеРаботы И ЗначениеЗаполнено(СертификатАутентификацииСсылка) Тогда
		ЭлектроннаяПодписьКлиент.УстановитьПарольСертификата(СертификатАутентификацииСсылка, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы Тогда
		ПрерватьДлительнуюОперацию(ИдентификаторЗадания);
		ОбновитьИнтерфейс(); // Возможно была включена функциональная опция
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВнешняяОбработкаПриИзменении(Элемент)
	
	КомпонентаОбработка = 1;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСинхронизацииНажатие(Элемент)
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(
		"https://v8.1c.ru/tekhnologii/obmen-dannymi-i-integratsiya/realizovannye-resheniya/directbank-pryamoy-obmen-s-bankom/?utm_source=led&utm_campaign=2017&utm_medium=wiz");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияЗагрузитьВКИзФайлаНажатие(Элемент)
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗагрузкиВКИзФайла", ЭтотОбъект);
	ВнешниеКомпонентыКлиент.ЗагрузитьКомпонентуИзФайла(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользуетсяКриптографияПриИзменении(Элемент)
	
	Элементы.ГруппаСертификаты.Доступность = ИспользуетсяКриптография;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользуетсяВнешнийМодульПриИзменении(Элемент)
	
	Если ИспользуетсяВнешнийМодуль Тогда
		Элементы.СтраницыВнешнегоМодуля.ТекущаяСтраница = Элементы.ГруппаВнешнийМодуль;
	Иначе
		Элементы.СтраницыВнешнегоМодуля.ТекущаяСтраница = Элементы.ПустаяСтраница;
	КонецЕсли;
	
	Если ИспользуетсяВнешнийМодуль Тогда
		СпособАутентификации = 1;
		ИспользуетсяКриптография = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнструкцияНажатие(Элемент)
	
	ЭлектроннаяПодписьКлиент.ОткрытьИнструкциюПоРаботеСПрограммами();
	
КонецПроцедуры

&НаКлиенте
Процедура БанкПриИзменении(Элемент)
	
	ПриИзмененииБанка();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ПриИзмененииОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКФайлуНастроекНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбранФайлНастроекОбмена = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораФайлаНастроекОбмена", ЭтотОбъект);
	Фильтр = НСтр("ru = 'Файл настроек обмена'") + "(*.xml)|*.xml";
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.Диалог.Фильтр = Фильтр;
	ПараметрыЗагрузки.Диалог.МножественныйВыбор = Ложь;
	ПараметрыЗагрузки.Диалог.Заголовок = НСтр("ru = 'Выберите файл настроек обмена'");
	ФайловаяСистемаКлиент.ЗагрузитьФайл(ОписаниеОповещения, ПараметрыЗагрузки);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкуОбменаНажатие(Элемент)
	
	ПоказатьЗначение( , НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособАутентификацииПриИзменении(Элемент)
	
	Если СпособАутентификации Тогда
		Элементы.СтраницаПользовательСмешанный.Видимость = Истина;
		Элементы.СтраницаСертификатСмешанный.Видимость = Ложь;
	Иначе
		Элементы.СтраницаПользовательСмешанный.Видимость = Ложь;
		Элементы.СтраницаСертификатСмешанный.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлНастроекНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПутьКФайлуНастроекНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ВключитьАвтоматическоеПолучениеВыпискиНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура("НастройкаОбмена", НастройкаОбмена);
	Оповещение = Новый ОписаниеОповещения("ПослеВключенияАвтоматическогоПолученияВыписки", ЭтотОбъект);
	ОткрытьФорму("Справочник.НастройкиОбменСБанками.Форма.АвтоматическоеПолучениеВыписки", ПараметрыФормы, , , , ,
		Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешняяКомпонентаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ВнешняяКомпонента) Тогда
		ИмяВнешнегоМодуля = ИдентификаторВнешнейКомпоненты(ВнешняяКомпонента);
	Иначе
		ИмяВнешнегоМодуля = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатСмешанныйНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПодключитьОбработчикОжидания("ЗаполнитьСписокВыбораСертификатовАутентификацииСВыводомОшибки", 0.1, Истина);

КонецПроцедуры

&НаКлиенте
Процедура СервисСбербанкПриИзменении(Элемент)
	
	Если СервисСбербанк = "УПШ" Тогда
		Элементы.ПреимуществаСервисовСбербанк.ТекущаяСтраница = Элементы.УПШ;
	Иначе
		Элементы.ПреимуществаСервисовСбербанк.ТекущаяСтраница = Элементы.Fintech;
	КонецЕсли;

КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСертификаты

&НаКлиенте
Процедура СертификатыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ЗаполнитьОрганизациюВСертификате(ВыбранноеЗначение, Организация);
	
	НовСтрока = Сертификаты.Добавить();
	НовСтрока.Сертификат = ВыбранноеЗначение;
	
	ПеренумероватьСертификаты();
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатыПриИзменении(Элемент)
	
	ПеренумероватьСертификаты();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДекорацияВнешниеОбработкиНеИспользуютсяНажатие(Элемент)
	
	ФормаНастроекБСП = "Обработка.ПанельАдминистрированияБСП.Форма.ПечатныеФормыОтчетыИОбработки";
	ОткрытьФорму(ФормаНастроекБСП);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСертификат(Команда)
	
	ОчиститьСообщения();
	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен")
		ИЛИ ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АльфаБанкОнлайн") Тогда
		
		ЭлектроннаяПодписьКлиент.СертификатНачалоВыбораСПодтверждением(Элементы.Сертификаты, Неопределено, Ложь, Ложь);
	
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
		
		Устройства = ОбменСБанкамиСлужебныйКлиент.ПодключенныеХранилищаЧерезДополнительнуюОбработку(ПодключаемыйМодуль);
		Если Устройства = Неопределено ИЛИ Устройства.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;

		СписокВыбора = Новый СписокЗначений;
		Для Каждого Элемент Из Устройства Цикл
			ТекЗапись = СписокВыбора.Добавить(Элемент);
		КонецЦикла;
		
		Если Устройства.Количество() > 1 Тогда
			ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПослеВыбораКлючаЧерезДополнительнуюОбработку", ЭтотОбъект);
			ЗаголовокВыбора = НСтр("ru = 'Выберите электронный ключ:'");
			СписокВыбора.ПоказатьВыборЭлемента(ОповещениеОЗакрытии, ЗаголовокВыбора);
		Иначе
			ПослеВыбораКлючаЧерезДополнительнуюОбработку(ТекЗапись)
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	Закрыть(НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)
	
	ОчиститьСообщения();
	Если Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаОписание Тогда
		
		ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаОписание);
		
		Если НеПоказыватьСтраницуВыборОрганизацииИБанка Тогда
			ПереключитьШагПослеВыбораОрганизацииИБанка();
		Иначе
			Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаВыборОрганизацииИБанка;
		КонецЕсли;
		
		Элементы.СтраницаОписание.Видимость = Ложь;
		
	ИначеЕсли Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаВыборОрганизацииИБанка Тогда
		
		Отказ = Ложь;
		Если Не ЗначениеЗаполнено(Организация) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выберите организацию'"), , "Организация", , Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Банк) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выберите банк'"), , "Банк", , Отказ);
		КонецЕсли;
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаВыборОрганизацииИБанка);
		
		НастройкаОбмена = ОбменСБанкамиСлужебныйВызовСервера.НастройкаОбмена(Организация, Банк, Ложь, Ложь);
		
		ПереключитьШагПослеВыбораОрганизацииИБанка();
		
	ИначеЕсли Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаВыборСпособаСозданияНастройки Тогда
		ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаВыборСпособаСозданияНастройки);
		Если СпособСозданияНастройки = 0 Тогда // получение настроек с сервера банка
			ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен");
			Оповещение = Новый ОписаниеОповещения(
				"СменитьСтраницуПослеПолучениеССервераПослеЗаполненияПрограммКриптографии", ЭтотОбъект);
			ЗаполнитьПрограммыКриптографии(Оповещение);
		ИначеЕсли СпособСозданияНастройки = 1 Тогда // загрузка настроек из файла
			СменитьСтраницуШаги(Элементы.СтраницаВыборФайлаНастроек);
		ИначеЕсли СпособСозданияНастройки = 2 Тогда // ручной режим
			СменитьСтраницуШаги(Элементы.СтраницаЗапросИспользованияВнешнегоМодуля);
		КонецЕсли;
		
	ИначеЕсли Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаЗапросИспользованияВнешнегоМодуля Тогда
		
		Если ИспользуетсяВнешнийМодуль Тогда
			Если КомпонентаОбработка = 0 Тогда // Компонента
				Если НЕ ЗначениеЗаполнено(ИмяВнешнегоМодуля) Тогда
					ТекстСообщения = НСтр("ru = 'Выберите или загрузите внешнюю компоненту из файла.'");
					ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ВнешняяКомпонента");
					Возврат;
				КонецЕсли;
				ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК");
				ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаЗапросИспользованияВнешнегоМодуля);
				СменитьСтраницуШаги(Элементы.СтраницаФинал);
				ПолучитьНастройкиОбменаССервераБанкаЧерезВК();
			Иначе //внешняя обработка
				Если НЕ ЗначениеЗаполнено(ДополнительнаяОбработка) Тогда
					ТекстСообщения = НСтр("ru = 'Выберите обработку.'");
					ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ВнешняяОбработка");
					Возврат;
				КонецЕсли;
				Оповещение = Новый ОписаниеОповещения("ПослеИнициализацииДополнительнойОбработки", ЭтотОбъект);
				ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку");
				НачатьИнициализациюДополнительнойОбработки(Оповещение);
			КонецЕсли;
		Иначе
			ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаЗапросИспользованияВнешнегоМодуля);
			ПодключитьОбработчикОжидания("ЗаполнитьСписокВыбораСертификатовАутентификации", 0.1, Истина);
			СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
		КонецЕсли;
		
	ИначеЕсли Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаАсинхронныйОбмен Тогда
		
		НастройкиЗагруженыИзФайла = Ложь;
		
		Отказ = Ложь;
		
		АдресСервера = АдресСервераНастроек;
		
		Если Не ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
			ТекстСообщения = НСтр("ru = 'Не указан идентификатор организации'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ИдентификаторОрганизации", , Отказ);
		КонецЕсли;
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен");
		
		ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаАсинхронныйОбмен);
		
		СменитьСтраницуШаги(Элементы.СтраницаСертификаты);
	
	ИначеЕсли Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаСертификаты Тогда
		Если ИспользуетсяКриптография И НЕ Сертификаты.Количество() Тогда
			ТекстСообщения = НСтр("ru = 'Добавьте сертификаты электронной подписи'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Сертификаты");
			Возврат;
		КонецЕсли;
		
		ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаСертификаты);
		СменитьСтраницуШаги(Элементы.СтраницаФинал);
		
		Элементы.СозданиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
		#Если ВебКлиент Тогда
			ОбновитьОтображениеДанных();
		#КонецЕсли
		ПодключитьОбработчикОжидания("СоздатьНастройкуОбмена", 0.1, Истина);
		
	ИначеЕсли Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаПолучениеССервера Тогда
		
		НастройкиЗагруженыИзФайла = Ложь;
		
		Отказ = Ложь;
		
		Если НЕ ЗначениеЗаполнено(АдресСервераНастроек) Тогда
			ТекстСообщения = НСтр("ru = 'Укажите адрес сервера банка'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "АдресСервераНастроек", , Отказ);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(АдресСервера)
			И НЕ ОбменСБанкамиКлиентСервер.ПравильныйФорматАдреса(АдресСервераНастроек) Тогда
			ТекстСообщения = НСтр("ru = 'Адрес сервера банка должен начинаться с ""https://"" или ""http://""'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "АдресСервераНастроек", , Отказ);
		КонецЕсли;
		
		Если СпособАутентификации = 0 И Не ЗначениеЗаполнено(СертификатАутентификации) Тогда  // по сертификату
			ТекстСообщения = НСтр("ru = 'Выберите сертификат для аутентификации на сервере банка'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "СертификатАутентификации", , Отказ);
		КонецЕсли;
		
		Если СпособАутентификации = 1 И НЕ ВведенЛогинИПароль() Тогда
			Отказ = Истина;
		КонецЕсли;
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаПолучениеССервера);
		Если СпособСозданияНастройки = 2 Тогда // ручное создание настройки
			СменитьСтраницуШаги(Элементы.СтраницаПодождите);
		Иначе
			СменитьСтраницуШаги(Элементы.СтраницаФинал);
		КонецЕсли;
	
		Если СпособАутентификации = 0 Тогда // по сертификату
			ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьДанныеСертификатаПослеПолучения", ЭтотОбъект);
			ЭлектроннаяПодписьКлиент.ПолучитьСертификатПоОтпечатку(ОписаниеОповещения, СертификатАутентификации, Истина);
		Иначе
			ПодключитьОбработчикОжидания("ПолучитьНастройкиОбменаССервераБанкаБазоваяАутентификация", 0.1, Истина);
		КонецЕсли;
	ИначеЕсли Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаВыборФайлаНастроек Тогда
		
		НастройкиЗагруженыИзФайла = Истина;
		
		#Если ВебКлиент Тогда
			Если Не ВыбранФайлНастроекОбмена Тогда
				ТекстСообщения = НСтр("ru = 'Не выбран файл настроек обмена'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ПутьКФайлуНастроекОбмена");
				Возврат;
			КонецЕсли;
		#Иначе
			Если ЗначениеЗаполнено(ПутьКФайлуНастроекОбмена) Тогда
				Попытка
					ДвоичныеДанныеФайлаНастроек = Новый ДвоичныеДанные(ПутьКФайлуНастроекОбмена);
				Исключение
					ТекстСообщения = НСтр("ru = 'Неверно указан путь к файлу настроек обмена'");
					ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ПутьКФайлуНастроекОбмена");
					Возврат;
				КонецПопытки;
				АдресФайлаНастроекОбмена = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайлаНастроек, УникальныйИдентификатор);
			Иначе
				ТекстСообщения = НСтр("ru = 'Не выбран файл настроек обмена'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ПутьКФайлуНастроекОбмена");
				Возврат;
			КонецЕсли;
		#КонецЕсли
		
		Файл = Новый Файл(ПутьКФайлуНастроекОбмена);
		Если Не Файл.Расширение = ".xml" Тогда
			ТекстСообщения = НСтр("ru = 'Указан неверный файл.
										|Файл настроек должен иметь расширение ""xml""'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ПутьКФайлуНастроекОбмена");
			Возврат;
		КонецЕсли;
		
		УчастникиОбменаИзФайла = УчастникиОбменаИзФайла(АдресФайлаНастроекОбмена, Организация, , Банк);
		
		Если УчастникиОбменаИзФайла = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если УчастникиОбменаИзФайла.Организация <> Организация Тогда
			ТекстСообщения = НСтр("ru = 'Указан неверный файл.
										|Требуются настройки для организации: %1
										|Файл настроек содержит настройки для организации: %2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Организация, УчастникиОбменаИзФайла.Организация);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
	
		Если УчастникиОбменаИзФайла.Банк <> Банк Тогда
			ТекстСообщения = НСтр("ru = 'Указан неверный файл.
										|Требуются настройки для банка: %1
										|Файл настроек содержит настройки для банка: %2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Банк, УчастникиОбменаИзФайла.Банк);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
	
		СпособСозданияНастройки = 1;
		
		Если УчастникиОбменаИзФайла.ЭтоНастройкиСбербанка Тогда
			ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн");
			СменитьСтраницуШаги(Элементы.СтраницаФинал);
			URLВК = УчастникиОбменаИзФайла.URLВК;
			ПодключитьОбработчикОжидания("ОпределитьИдентификаторВКСбербанк", 0.1, Истина);
		Иначе
			ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаВыборФайлаНастроек);
			СменитьСтраницуШаги(Элементы.СтраницаФинал);
			Элементы.СозданиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
			#Если ВебКлиент Тогда
				ОбновитьОтображениеДанных();
			#КонецЕсли
			ПодключитьОбработчикОжидания("СоздатьНастройкуОбмена", 0.1, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)

	Если Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаФинал Тогда
		Элементы.ПолучениеНастроекОбменаКартинка.Картинка = Новый Картинка;
		Элементы.СозданиеНастройкиОбменаКартинка.Картинка = Новый Картинка;
		Элементы.ТестированиеНастройкиОбменаКартинка.Картинка = Новый Картинка;
	КонецЕсли;
	
	ОбменСБанкамиСлужебныйКлиент.ПрерватьПроцессыНаКлиенте(НастройкаОбмена);
	ВернутьсяНаПредыдущуюСтраницу();

КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ОбменСБанкамиСлужебныйКлиент.ПрерватьПроцессыНаКлиенте(НастройкаОбмена);
	Закрыть(НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьНастройки(Команда)
	
	ОчиститьСообщения();
	
	Элементы.ПолучитьНастройки.Видимость = Ложь;
	Элементы.ПолучениеНастроекОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
	Элементы.СтраницыРезультатПодключения.ТекущаяСтраница = Элементы.СтраницаНеЗавершено;
	
	ОбменЧерезВК = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК");
	
	Если ПрограммаБанка = ОбменЧерезВК Тогда
		Если СпособСозданияНастройки = 2 Тогда // ручная настройка
			ПолучитьНастройкиОбменаССервераБанкаЧерезВК();
		Иначе
			ПолучитьНастройкиОбменаССервераБанкаЧерезВК();
		КонецЕсли;
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
		ВыполнитьАутентификациюПоЛогинуСбербанк();
	Иначе // асинхронный обмен
		Если СпособАутентификации = 0 Тогда // по сертификату
			ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьДанныеСертификатаПослеПолучения", ЭтотОбъект);
			ЭлектроннаяПодписьКлиент.ПолучитьСертификатПоОтпечатку(ОписаниеОповещения, СертификатАутентификации, Истина);
		Иначе
			#Если ВебКлиент Тогда
				ОбновитьОтображениеДанных();
			#КонецЕсли
			ПодключитьОбработчикОжидания("ПолучитьНастройкиОбменаССервераБанкаБазоваяАутентификация", 0.1, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьСервис(Команда)
	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен") Тогда
		
		ПодключитьАсинхронныйОбмен();
		
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн")
		Или ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.FintechIntegration") Тогда
		
		Если СервисСбербанк = "УПШ" Тогда
			
			ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн");
			Если Не ВведенЛогинИПароль() Тогда
				Возврат;
			КонецЕсли;
			
			ПередНачаломПодключенияСервиса();
			
			ИмяВнешнегоМодуля = НастройкиОбменаСБанком.ИдентификаторВК;
			Если Не ЗначениеЗаполнено(ИмяВнешнегоМодуля) Тогда
				ИмяВнешнегоМодуля = ОбменСБанкамиКлиентСервер.ИдентификаторВнешнейКомпонентыСбербанк();
			КонецЕсли;
			
			ВыполнитьАутентификациюПоЛогинуСбербанк();
		
		Иначе
			
			ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.FintechIntegration");
			Если Не ЗначениеЗаполнено(НастройкаОбмена) Тогда
				НастройкаОбмена = СсылкаНаНовуюНастройкуОбмена()
			КонецЕсли;
	
			Оповещение = Новый ОписаниеОповещения("ПослеАутентификацииFintechСбербанк", ЭтотОбъект);
			ОбменСБанкамиСлужебныйКлиент.ВыполнитьАутентификациюFintech(
				Оповещение, НастройкаОбмена, Организация, Банк, , "Подключение");
		
		КонецЕсли;
		
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК") Тогда
		
		ПередНачаломПодключенияСервиса();

		ИмяВнешнегоМодуля = НастройкиОбменаСБанком.ИдентификаторВК;
		
		// Если макет старый, то название ВК определяется из информационного файла с сайта банка.
		Если НЕ ЗначениеЗаполнено(ИмяВнешнегоМодуля) И ЗначениеЗаполнено(URLВК) Тогда
			Оповещение = Новый ОписаниеОповещения("ПодключитьВКПослеОпределенияИдентификатора", ЭтотОбъект);
			ПолучитьИдентификаторВнешнейКомпоненты(Оповещение);
		Иначе
			ПолучитьНастройкиОбменаССервераБанкаЧерезВК();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОстановитьАвтоматическоеПолучениеВыпискиНажатие(Элемент)
	
	Элементы.ГруппаДоступноАвтоматическоеПолучениеВыписки.Видимость = Истина;
	Элементы.ГруппаЗапущеноАвтоматическоеПолучениеВыписки.Видимость = Ложь;
	ОбменСБанкамиСлужебныйВызовСервера.ОстановитьАвтоматическоеПолучениеВыписки(НастройкаОбмена);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиСобытий

&НаКлиенте
Процедура ПриИзмененииОрганизации()
	
	Сертификаты.Очистить();
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Элементы.Банк.СписокВыбора.Очистить();
	КонецЕсли;
	
	БанкиОрганизации = БанкиОрганизации(Организация);
	Элементы.Банк.СписокВыбора.ЗагрузитьЗначения(БанкиОрганизации);
	
	Если БанкиОрганизации.Найти(Банк) = Неопределено Тогда
		Если БанкиОрганизации.Количество() = 1 Тогда 
			Банк = БанкиОрганизации[0];
		Иначе
			Банк = Неопределено;
		КонецЕсли;
		ПриИзмененииБанка();
	Иначе
		ИзменитьЗаголовок();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииБанка()
	
	ВнешняяКомпонентаЗагружена = Ложь;
	ИзменитьЗаголовок();
	Элементы.ИнформацияНаСайтеБанка.Видимость = Ложь;
	
	Если ЗначениеЗаполнено(Банк) Тогда
		НастройкиОбменаСБанком = ОбменСБанкамиСлужебныйВызовСервера.ПоставляемыеНастройкиОбмена(Банк, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция БанкиОрганизации(Организация)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПустаяСсылкаБанка", ОбменСБанкамиСлужебный.ПустаяСсылкаНаСправочникБанки());
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	БанковскиеСчетаОрганизаций.Банк КАК Банк
	               |ИЗ
	               |	&БанковскиеСчетаОрганизаций КАК БанковскиеСчетаОрганизаций
	               |ГДЕ
	               |	БанковскиеСчетаОрганизаций.Владелец = &Организация
	               |	И НЕ БанковскиеСчетаОрганизаций.ПометкаУдаления
	               |	И БанковскиеСчетаОрганизаций.Банк <> &ПустаяСсылкаБанка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	БанковскиеСчетаОрганизаций.Банк";
	
	ИмяСправочникаБанковскиеСчета = ОбщегоНазначенияБЭД.ИмяПрикладногоСправочника("БанковскиеСчетаОрганизаций");
	
	Если ИмяСправочникаБанковскиеСчета = Неопределено Тогда 
		Возврат Новый Массив;
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&БанковскиеСчетаОрганизаций", "Справочник." + ИмяСправочникаБанковскиеСчета);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Банк");
	
КонецФункции

#КонецОбласти

#Область ИзменениеИнтерфейса

&НаСервере
Процедура УстановитьРеквизитыФормы()
	
	РазделениеВключено = ОбщегоНазначения.РазделениеВключено();
	
	СоздаватьЭлектронныеПодписиНаСервере = ЭлектроннаяПодпись.СоздаватьЭлектронныеПодписиНаСервере();
	
	СервисСбербанк = "Fintech";
	
	ПрограммаБанка = Перечисления.ПрограммыБанка.АсинхронныйОбмен;
	
	АдресСервера = "https://";
	
	АдресСервераНастроек = "https://";
	
	РасширенныйРежим = Истина;

	СохранитьЛогин = Истина;
	
	НеПоказыватьСтраницуИнформации = Параметры.НеПоказыватьСтраницуИнформации;
	
	НомерБанковскогоСчета = Параметры.НомерБанковскогоСчета;
	
	ЛокальныйФайл = Параметры.ЛокальныйФайл;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормыПриОткрытии()
	
	Элементы.Шаги.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.КомандыНавигации.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.СтраницыВнешнегоМодуля.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.ПреимуществаСервисовСбербанк.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		
	Элементы.ИнформацияНаСайтеБанка.Видимость = Ложь;
		
	Элементы.ПолучитьНастройки.Видимость = Ложь;	
	Элементы.ОткрытьНастройкуОбмена.Видимость = Ложь;
	
	Элементы.ГруппаДоступноАвтоматическоеПолучениеВыписки.Видимость = Ложь;
	Элементы.ГруппаЗапущеноАвтоматическоеПолучениеВыписки.Видимость = Ложь;
	
	ПанельАдминистрированияБСП = Метаданные.НайтиПоПолномуИмени("Обработка.ПанельАдминистрированияБСП");
	Элементы.ДекорацияВнешниеОбработкиНеИспользуются.Гиперссылка = ПанельАдминистрированияБСП <> Неопределено;
	
	ОформитьВерсиюФорматаОбмена();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтраницуВнешнийМодуль()
	
	ИспользуютсяВнешниеОбработки = ОбменСБанкамиСлужебныйВызовСервера.ИспользуютсяДополнительныеОтчетыИОбработки();
	
	Элементы.ДекорацияВнешниеОбработкиНеИспользуются.Видимость = Не ИспользуютсяВнешниеОбработки;
	Элементы.Обработка.Доступность = ИспользуютсяВнешниеОбработки;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьМенеджерКриптографииПослеЗаполненияПрограммКриптографии(Результат, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ПереключитьСтраницыПослеСозданияМенеджераКриптографии", ЭтотОбъект);
	ЭлектроннаяПодписьКлиент.СоздатьМенеджерКриптографии(Оповещение, "");
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренумероватьСертификаты()
	
	Счетчик = 0;
	Для Каждого Строка Из Сертификаты Цикл
		Счетчик = Счетчик + 1;
		Строка.Номер = Счетчик;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СменитьСтраницуКомандНавигации(НоваяСтраница)
	
	Если НоваяСтраница = Элементы.ДалееОтмена Тогда
		Элементы.ДалееПервая.КнопкаПоУмолчанию = Истина;
		Элементы.ДалееПервая.АктивизироватьПоУмолчанию = Истина;
		ТекущийЭлемент = Элементы.ДалееПервая;
	ИначеЕсли НоваяСтраница = Элементы.НазадДалееОтмена Тогда
		Элементы.ДалееВторая.КнопкаПоУмолчанию = Истина;
		Элементы.ДалееВторая.АктивизироватьПоУмолчанию = Истина;
		ТекущийЭлемент = Элементы.ДалееВторая;
	ИначеЕсли НоваяСтраница = Элементы.НазадГотово Тогда
		Элементы.ГотовоПервая.КнопкаПоУмолчанию = Истина;
	ИначеЕсли НоваяСтраница = Элементы.Готово Тогда
		Элементы.ГотовоВторая.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	Элементы.КомандыНавигации.ТекущаяСтраница = НоваяСтраница;
	
КонецПроцедуры

&НаКлиенте
Процедура СменитьСтраницуШаги(НоваяСтраница)
	
	Если СменитьСтраницуНаУпрощеннуюНастройку(НоваяСтраница) Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ИнформацияНаСайтеБанка.Видимость = Ложь;
	Элементы.ПолучитьНастройки.Видимость = Ложь;
	Элементы.КомандыНавигации.Доступность = Истина;
	
	КомандыСВозвратом = Элементы.НазадДалееОтмена;
	КомандыБезВозврата = Элементы.ДалееОтмена;

	Если НоваяСтраница = Элементы.СтраницаФинал Тогда
		
		Элементы.СтраницыРезультатПодключения.ТекущаяСтраница = Элементы.СтраницаНеЗавершено;
		
		Элементы.ОткрытьНастройкуОбмена.Видимость = Ложь;
		
		Если ХронологияПереключенияСтраниц.Количество() Тогда
			КомандыСВозвратом = Элементы.НазадОтмена;
		Иначе
			КомандыБезВозврата = Элементы.Отмена;
		КонецЕсли;
		
		Элементы.ТестированиеНастройкиОбменаКартинка.Картинка = Новый Картинка;
		СинхронныйОбмен = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АльфаБанкОнлайн");
		Элементы.ГруппаТестированиеНастройкиОбмена.Видимость = ПрограммаБанка <> СинхронныйОбмен;
		Если СпособСозданияНастройки <> 0
			И НЕ (ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК")
					И СпособСозданияНастройки = 2)
			И НЕ ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
			Элементы.ГруппаПолучениеНастроекССервераБанка.Видимость = Ложь;
		Иначе // Получение настроек с сервера банка
			Элементы.ГруппаПолучениеНастроекССервераБанка.Видимость = Истина;
			Элементы.ПолучениеНастроекОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
			Элементы.СозданиеНастройкиОбменаКартинка.Картинка = Новый Картинка;
		КонецЕсли;
	ИначеЕсли НоваяСтраница = Элементы.СтраницаАсинхронныйОбмен Тогда
		ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен");
		Если Не ЗначениеЗаполнено(ВерсияФормата) Тогда
			ВерсияФормата = ОбменСБанкамиКлиентСервер.АктуальнаяВерсияФорматаАсинхронногоОбмена();
		КонецЕсли;
	ИначеЕсли НоваяСтраница = Элементы.СтраницаВыборСпособаСозданияНастройки Тогда
		Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
			Элементы.ПолучениеНастроекССервераБанка.Видимость = Ложь;
			Если СпособСозданияНастройки = 0 Тогда
				СпособСозданияНастройки = 1;
			КонецЕсли
		Иначе
			Элементы.ПолучениеНастроекССервераБанка.Видимость = ТестовыйРежим;
		КонецЕсли;
	ИначеЕсли НоваяСтраница = Элементы.СтраницаПолучениеССервера Тогда
		СпособАутентификацииПриИзменении(Неопределено);
		Если ЗначениеЗаполнено(НастройкиОбменаСБанком) И ЗначениеЗаполнено(НастройкиОбменаСБанком.АдресСтраницыБанка) Тогда
			ВывестиСсылкуНаСайтБанка(Элементы.ИнформацияНаСайтеБанка, НастройкиОбменаСБанком.АдресСтраницыБанка);
		КонецЕсли;
	ИначеЕсли НоваяСтраница = Элементы.СтраницаВыборОрганизацииИБанка Тогда
		НастройкиЗагруженыИзФайла = Ложь;
		Элементы.СтраницаОписание.Видимость = Ложь;
	ИначеЕсли НоваяСтраница = Элементы.СтраницаСертификаты Тогда
		Элементы.ГруппаСертификаты.Доступность = ИспользуетсяКриптография;
		АсинхронныйОбмен = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен");
		АльфаБанк = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АльфаБанкОнлайн");
		Элементы.ГруппаНастроекКриптографии.Видимость = ПрограммаБанка = АсинхронныйОбмен;
		Элементы.ГруппаАльфаБанк.Видимость = ПрограммаБанка = АльфаБанк;
	ИначеЕсли НоваяСтраница = Элементы.СтраницаВыборФайлаНастроек Тогда
		Если ЗначениеЗаполнено(НастройкиОбменаСБанком) И ЗначениеЗаполнено(НастройкиОбменаСБанком.АдресСтраницыБанка) Тогда
			ВывестиСсылкуНаСайтБанка(Элементы.ИнформацияНаСайтеБанка, НастройкиОбменаСБанком.АдресСтраницыБанка);
		КонецЕсли;
	ИначеЕсли НоваяСтраница = Элементы.СтраницаПодождите Тогда
		КомандыСВозвратом = Элементы.Отмена;
		КомандыБезВозврата = Элементы.Отмена;
	ИначеЕсли НоваяСтраница = Элементы.СтраницаОписание Тогда
		Элементы.СтраницаОписание.Видимость = Истина;
	ИначеЕсли НоваяСтраница = Элементы.СтраницаЗапросИспользованияВнешнегоМодуля Тогда
		ОбновитьСтраницуВнешнийМодуль();
	КонецЕсли;
	
	Если ХронологияПереключенияСтраниц.Количество() Тогда
		СменитьСтраницуКомандНавигации(КомандыСВозвратом)
	Иначе
		СменитьСтраницуКомандНавигации(КомандыБезВозврата)
	КонецЕсли;
	
	#Если ВебКлиент Тогда
		ОбновитьОтображениеДанных();
	#КонецЕсли
	
	Элементы.Шаги.ТекущаяСтраница = НоваяСтраница;

КонецПроцедуры

&НаКлиенте
Процедура ПереключитьСтраницыПослеСозданияМенеджераКриптографии(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда  // ошибка создания менеджера криптографии
		ВернутьсяНаПредыдущуюСтраницу();
		Возврат;
	КонецЕсли;
	
	СменитьСтраницуШаги(Элементы.СтраницаСертификаты);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьШагПослеВыбораОрганизацииИБанка()
	
	Элементы.АдресСервераНастроек.Видимость = Истина;
	Элементы.СпособАутентификации.Видимость = Истина;
	ОбновитьОтображениеДанных();
	
	ВидЭДЗапросВыписки = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ЗапросВыписки");
	
	Если НастройкиОбменаСБанком = Неопределено Тогда // Неизвестный банк
		УпрощенныйРежимНастройки = Ложь;
		ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен");
		СменитьСтраницуШаги(Элементы.СтраницаВыборСпособаСозданияНастройки);
		
	ИначеЕсли НастройкиОбменаСБанком.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
		УпрощенныйРежимНастройки = Истина;
		Если Не ОбменСБанкамиСлужебныйВызовСервера.ЕстьПоддержкаВидаЭДВКонфигурации(ВидЭДЗапросВыписки) Тогда // ЗУП
			ТекстСообщения = НСтр("ru = 'Обмен документами по заработной плате через сервис 1С:ДиректБанк с выбранным банком не поддерживается.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		ПрограммаБанка = НастройкиОбменаСБанком.ПрограммаБанка;
		URLВК = НастройкиОбменаСБанком.АдресСервера;
		СменитьСтраницуШаги(Элементы.СтраницаУпрощенныйРежимНастройки);
		
	ИначеЕсли НастройкиОбменаСБанком.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК") Тогда
		УпрощенныйРежимНастройки = Истина;
		Если Не ОбменСБанкамиСлужебныйВызовСервера.ЕстьПоддержкаВидаЭДВКонфигурации(ВидЭДЗапросВыписки) Тогда // ЗУП
			ТекстСообщения = НСтр("ru = 'Обмен документами по заработной плате через сервис 1С:ДиректБанк с выбранным банком не поддерживается.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		ПрограммаБанка = НастройкиОбменаСБанком.ПрограммаБанка;
		URLВК = НастройкиОбменаСБанком.АдресСервера;
		СменитьСтраницуШаги(Элементы.СтраницаФинал);
		
	ИначеЕсли НастройкиОбменаСБанком.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АльфаБанкОнлайн") Тогда
		Если Не ОбменСБанкамиСлужебныйВызовСервера.ЕстьПоддержкаВидаЭДВКонфигурации(ВидЭДЗапросВыписки) Тогда // ЗУП
			ТекстСообщения = НСтр("ru = 'Обмен документами по заработной плате через сервис 1С:ДиректБанк с выбранным банком не поддерживается.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		РесурсПриемник = "/wsi2/order";
		РесурсИсточник = "/wsi2/statement";
		АдресСервера = "https://ic.alfabank.ru/";
		СпособСозданияНастройки = 2;
		ПрограммаБанка = НастройкиОбменаСБанком.ПрограммаБанка;
		ИспользуетсяКриптография = Истина;
		Оповещение = Новый ОписаниеОповещения("СоздатьМенеджерКриптографииПослеЗаполненияПрограммКриптографии", ЭтотОбъект);
		ЗаполнитьПрограммыКриптографии(Оповещение);
		
	ИначеЕсли НастройкиОбменаСБанком.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен") Тогда
		УпрощенныйРежимНастройки = Истина;
		ПрограммаБанка = НастройкиОбменаСБанком.ПрограммаБанка;
		Если Не ОбменСБанкамиСлужебныйВызовСервера.ЕстьПоддержкаВидаЭДВКонфигурации(ВидЭДЗапросВыписки)
			И СтрНайти(НастройкиОбменаСБанком.Проект, "2") = 0 Тогда
			ТекстСообщения = НСтр("ru = 'Обмен документами по заработной плате через сервис 1С:ДиректБанк с выбранным банком не поддерживается.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		Если ЗначениеЗаполнено(НастройкиОбменаСБанком.АдресСервера) Тогда
			АдресСервераНастроек = НастройкиОбменаСБанком.АдресСервера;
			СпособСозданияНастройки = 0;
			Если Не ТестовыйРежим Тогда
				Элементы.АдресСервераНастроек.Видимость = Ложь;
			КонецЕсли;
		КонецЕсли;
		Если ЗначениеЗаполнено(НастройкиОбменаСБанком.СпособАутентификации) Тогда
			Если НастройкиОбменаСБанком.СпособАутентификации = "ИзФайла" Тогда
				СменитьСтраницуШаги(Элементы.СтраницаВыборФайлаНастроек);
			ИначеЕсли НастройкиОбменаСБанком.СпособАутентификации = "ПоЛогинуИлиСертификату" Тогда
				Оповещение = Новый ОписаниеОповещения(
					"СменитьСтраницуПослеПолучениеССервераПослеЗаполненияПрограммКриптографии", ЭтотОбъект);
				ЗаполнитьПрограммыКриптографии(Оповещение);
			ИначеЕсли НастройкиОбменаСБанком.СпособАутентификации = "ПоСертификату" Тогда
				СпособАутентификации = 0;
				Элементы.СпособАутентификации.Видимость = Ложь;
				Оповещение = Новый ОписаниеОповещения(
					"СменитьСтраницуСертификатДляПолученияНастроекПослеЗаполненияПрограммКриптографии", ЭтотОбъект);
				ЗаполнитьПрограммыКриптографии(Оповещение);
			ИначеЕсли НастройкиОбменаСБанком.СпособАутентификации = "ПоЛогину" Тогда
				Элементы.СпособАутентификации.Видимость = Ложь;
				СпособАутентификации = 1;
				СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
			Иначе
				СменитьСтраницуШаги(Элементы.СтраницаВыборСпособаСозданияНастройки);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СменитьСтраницуПослеПолучениеССервераПослеЗаполненияПрограммКриптографии(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаполнитьСписокВыбораСертификатовАутентификации();
	Если УпрощенныйРежимНастройки Тогда
		ПодключитьОбработчикОжидания("ПослеЗаполненияСертификатовАутентификации", 0.1, Истина);
	Иначе
		СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СменитьСтраницуСертификатДляПолученияНастроекПослеЗаполненияПрограммКриптографии(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаполнитьСписокВыбораСертификатовАутентификации();
	СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЗаголовок()
	
	Если ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Банк) Тогда
		Шаблон = "%1 - %2";
		Элементы.Заголовок.Заголовок = СтрШаблон(Шаблон, Организация, Банк);
	Иначе
		Элементы.Заголовок.Заголовок = "";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область АсинхронныйОбмен

&НаКлиенте
Процедура ПодключитьАсинхронныйОбмен()
	
	Если (НастройкиОбменаСБанком.СпособАутентификации = "ПоЛогину"
			ИЛИ (НастройкиОбменаСБанком.СпособАутентификации = "ПоЛогинуИлиСертификату"
					И НЕ ЕстьСертификаты)) Тогда
		
		НастройкиЗагруженыИзФайла = Ложь;
		
		Если НЕ ВведенЛогинИПароль() Тогда
			Элементы.ГруппаЗагрузкаПараметровОбменаСБанкомУРН.Видимость = Ложь;
			Возврат;
		КонецЕсли;
		
		ПередНачаломПодключенияСервиса();
		
		ПолучитьНастройкиОбменаССервераБанкаБазоваяАутентификация();
		
	Иначе
		
		ПередНачаломПодключенияСервиса();
		
		Если ЗначениеЗаполнено(СертификатАутентификации) Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьДанныеСертификатаПослеПолучения", ЭтотОбъект);
			ЭлектроннаяПодписьКлиент.ПолучитьСертификатПоОтпечатку(
				ОписаниеОповещения, СертификатАутентификации, Истина, СоздаватьЭлектронныеПодписиНаСервере = Ложь);
		Иначе
			ТекстСообщения = НСтр("ru = 'Выберите сертификат для аутентификации на сервере банка'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "СертификатАутентификации");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОформитьВерсиюФорматаОбмена()
	
	ИзвестныеВерсииФормата = Новый Массив;
	АктуальныеВерсииФормата = ОбменСБанкамиСлужебный.АктуальныеВерсииФормата();
	СписокЗаполнен = Элементы.ВерсияФормата.СписокВыбора.Количество() > 0;
	ВерсииСхемАсинхронногоОбмена = ОбменСБанкамиСлужебный.ВерсииСхемАсинхронногоОбмена();
	Для каждого ЭлементКоллекции Из ВерсииСхемАсинхронногоОбмена Цикл
		ИзвестныеВерсииФормата.Добавить(ЭлементКоллекции.Ключ);
		Если НЕ СписокЗаполнен Тогда
			Если АктуальныеВерсииФормата.Найти(ЭлементКоллекции.Ключ) = Неопределено Тогда
				Элементы.ВерсияФормата.СписокВыбора.Добавить(
					ЭлементКоллекции.Ключ, ЭлементКоллекции.Ключ + " " + НСтр("ru = '(устарела)'"));
			Иначе
				Элементы.ВерсияФормата.СписокВыбора.Добавить(ЭлементКоллекции.Ключ, ЭлементКоллекции.Ключ);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Элементы.ВерсияФормата.СписокВыбора.СортироватьПоЗначению();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеУстановкиРасширенияДляРаботыСКриптографией(ДополнительныеПараметры) Экспорт
	
	ОповещениеПослеПодключенияРасширенияДляРаботыСКриптографией = Новый ОписаниеОповещения(
		"ПослеПовторногоПодключенияРасширенияДляРаботыСКриптографией", ЭтотОбъект, ДополнительныеПараметры);
	
	НачатьПодключениеРасширенияРаботыСКриптографией(ОповещениеПослеПодключенияРасширенияДляРаботыСКриптографией);

КонецПроцедуры

&НаКлиенте
Процедура ПриОшибкеПолученияИнформацииМодуляКриптографии(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПроверитьОчереднуюПрограммуКриптографии(ДополнительныеПараметры.МассивПрограмм,
		ДополнительныеПараметры.ПрограммыКриптографии, ДополнительныеПараметры.Оповещение,
		ДополнительныеПараметры.МассивПрограммДляСоздания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСертификатЦиклПослеСозданияМенеджераКриптографии(Результат, Контекст) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("МенеджерКриптографии") Тогда
		Контекст.ОшибкаНаКлиенте.Ошибки.Добавить(Результат);
		ПроверитьСертификатЦиклНачало(Контекст);
		Возврат;
	КонецЕсли;
	Контекст.Вставить("МенеджерКриптографии", Результат);
	
	Контекст.МенеджерКриптографии.ПарольДоступаКЗакрытомуКлючу = ПарольСертификата;
	
	Оповещение = Новый ОписаниеОповещения("ПроверитьСертификатЦиклПослеПодписания", ЭтотОбъект, Контекст,
		"ПроверитьСертификатЦиклПослеОшибкиПодписания", ЭтотОбъект);
	
	Контекст.МенеджерКриптографии.НачатьПодписывание(
		Оповещение, Контекст.ДвоичныеДанныеСертификата, Контекст.СертификатКриптографии);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСертификатЦиклПослеПодписания(ДанныеПодписи, Контекст) Экспорт
	
	ПредставлениеОшибки = "";
	Попытка
		ПустыеДанныеПодписи(ДанныеПодписи, ПредставлениеОшибки);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	КонецПопытки;
	
	Если ЗначениеЗаполнено(ПредставлениеОшибки) Тогда
		ЗаполнитьОшибкуПодписания(Контекст.ОшибкаНаКлиенте, Контекст.ОписаниеОшибки, Контекст.ОписаниеПрограммы,
			ПредставлениеОшибки, ИнформацияОбОшибке = Неопределено);
		ПроверитьСертификатЦиклНачало(Контекст);
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура("Программа", Контекст.ОписаниеПрограммы.Ссылка);
	ВыполнитьОбработкуОповещения(Контекст.Оповещение, Результат);
	
КонецПроцедуры

&НаКлиенте
Функция ПустыеДанныеПодписи(ДанныеПодписи, ОписаниеОшибки)
	
	Если Не ЗначениеЗаполнено(ДанныеПодписи) Тогда
		ОписаниеОшибки = НСтр("ru = 'Сформирована пустая подпись.'");
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьСертификатЦиклПослеОшибкиПодписания(ИнформацияОбОшибке, СтандартнаяОбработка, Контекст) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ЗаполнитьОшибкуПодписания(Контекст.ОшибкаНаКлиенте, Контекст.ОписаниеОшибки, Контекст.ОписаниеПрограммы,
		КраткоеПредставлениеОшибки(ИнформацияОбОшибке), Ложь);
	
	ПроверитьСертификатЦиклНачало(Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСертификатЦиклНачало(Контекст)
	
	Если Контекст.ОписанияПрограмм.Количество() <= Контекст.Индекс + 1 Тогда
		ВыполнитьОбработкуОповещения(Контекст.Оповещение);
		Возврат;
	КонецЕсли;
	Контекст.Индекс = Контекст.Индекс + 1;
	Контекст.Вставить("ОписаниеПрограммы", Контекст.ОписанияПрограмм[Контекст.Индекс]);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПроверитьСертификатЦиклПослеСозданияМенеджераКриптографии", ЭтотОбъект, Контекст);
	
	ЭлектроннаяПодписьКлиент.СоздатьМенеджерКриптографии(Оповещение, "", Ложь, Контекст.ОписаниеПрограммы.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСертификатПослеИнициализацииСертификата(СертификатКриптографии, Контекст) Экспорт
	
	Контекст.Вставить("СертификатКриптографии", СертификатКриптографии);
	
	Контекст.Вставить("ОписаниеОшибки", "");
	Контекст.Вставить("ОшибкаНаКлиенте", Новый Структура);
	
	Контекст.ОшибкаНаКлиенте.Вставить("ОписаниеОшибки", "");
	Контекст.ОшибкаНаКлиенте.Вставить("Ошибки", Новый Массив);
	
	Контекст.Вставить("ОписанияПрограмм",
		СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиента().ЭлектроннаяПодпись.ОбщиеНастройки.ОписанияПрограмм);
	Контекст.Вставить("Индекс", -1);
	
	ПроверитьСертификатЦиклНачало(Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьОчереднуюПрограммуКриптографии(МассивПрограмм, ПрограммыКриптографии, Оповещение, МассивПрограммДляСоздания)
	
	Если МассивПрограмм.Количество() = 0 Тогда
		
		Если МассивПрограммДляСоздания.Количество() Тогда
			ЗаписатьПрограммыКриптографии(ПрограммыКриптографии, МассивПрограммДляСоздания);
		КонецЕсли;
		ВыполнитьОбработкуОповещения(Оповещение);
		Возврат;
	КонецЕсли;
	
	ТекПрограмма = МассивПрограмм.Получить(0);
	МассивПрограмм.Удалить(0);
	
	ПараметрыПрограммы = Неопределено;
	Если НЕ ПрограммыКриптографии.Свойство(ТекПрограмма, ПараметрыПрограммы) Тогда
		ПроверитьОчереднуюПрограммуКриптографии(
			МассивПрограмм, ПрограммыКриптографии, Оповещение, МассивПрограммДляСоздания);
		Возврат;
	КонецЕсли;
		
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
	ДополнительныеПараметры.Вставить("МассивПрограмм", МассивПрограмм);
	ДополнительныеПараметры.Вставить("ПрограммыКриптографии", ПрограммыКриптографии);
	ДополнительныеПараметры.Вставить("ТекущаяПрограмма", ТекПрограмма);
	ДополнительныеПараметры.Вставить("МассивПрограммДляСоздания", МассивПрограммДляСоздания);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПолученияИнформацииМодуляКриптографии", ЭтотОбъект,
		ДополнительныеПараметры, "ПриОшибкеПолученияИнформацииМодуляКриптографии", ЭтотОбъект);
	
	СредстваКриптографии.НачатьПолучениеИнформацииМодуляКриптографии(
		ОписаниеОповещения, ПараметрыПрограммы.ИмяПрограммы, "", ПараметрыПрограммы.ТипПрограммы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияИнформацииМодуляКриптографии(ИнформацияМодуля, ДополнительныеПараметры) Экспорт
	
	Если ИнформацияМодуля <> Неопределено Тогда
		ДополнительныеПараметры.МассивПрограммДляСоздания.Добавить(ДополнительныеПараметры.ТекущаяПрограмма);
	КонецЕсли;
	ПроверитьОчереднуюПрограммуКриптографии(ДополнительныеПараметры.МассивПрограмм,
		ДополнительныеПараметры.ПрограммыКриптографии, ДополнительныеПараметры.Оповещение,
		ДополнительныеПараметры.МассивПрограммДляСоздания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодключенияРасширенияДляРаботыСКриптографией(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Подключено Тогда
		НачатьЗаполнениеПрограммКриптографии(ДополнительныеПараметры.Оповещение);
	Иначе
		ОповещениеПослеУстановкиРасширенияДляРаботыСКриптографией = Новый ОписаниеОповещения(
			"ПослеУстановкиРасширенияДляРаботыСКриптографией", ЭтотОбъект, ДополнительныеПараметры);
		НачатьУстановкуРасширенияРаботыСКриптографией(ОповещениеПослеУстановкиРасширенияДляРаботыСКриптографией);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПовторногоПодключенияРасширенияДляРаботыСКриптографией(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Подключено Тогда
		НачатьЗаполнениеПрограммКриптографии(ДополнительныеПараметры.Оповещение);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение);
	КонецЕсли;
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура ПослеЗаполненияСертификатовАутентификации()
	
	СменитьСтраницуШаги(Элементы.СтраницаУпрощенныйРежимНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьПрограммуКриптографииДляСертификата(Оповещение, ДвоичныеДанныеСертификата)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДвоичныеДанныеСертификата", ДвоичныеДанныеСертификата);
	ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
	
	СертификатКриптографии = Новый СертификатКриптографии;
	Оповещение = Новый ОписаниеОповещения(
		"ПроверитьСертификатПослеИнициализацииСертификата", ЭтотОбъект, ДополнительныеПараметры);
	СертификатКриптографии.НачатьИнициализацию(Оповещение, ДвоичныеДанныеСертификата);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьОшибкуПодписания(Ошибка, ОписаниеОшибки, ОписаниеПрограммы, ПредставлениеОшибки, ПустыеДанные)
	
	ТекущаяОшибка = Новый Структура;
	ТекущаяОшибка.Вставить("Описание", ПредставлениеОшибки);
	
	Если Не ПустыеДанные Тогда
		ТекущаяОшибка.Вставить("НастройкаПрограмм", Истина);
		ТекущаяОшибка.Вставить("Инструкция", Истина);
	КонецЕсли;
	
	Ошибка.Ошибки.Добавить(ТекущаяОшибка);
	
	ОписаниеОшибки = ОписаниеОшибки + Символы.ПС + Символы.ПС + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Не удалось пройти проверку подписания с помощью программы %1 по причине:
					|%2'"),
		ОписаниеПрограммы.Представление,
		ПредставлениеОшибки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПрограммыКриптографии(Оповещение)
	
	СменитьСтраницуШаги(Элементы.СтраницаПодождите);
	ДополнительныеПараметры = Новый Структура("Оповещение", Оповещение);
	
	ОповещениеПослеПодключенияРасширенияДляРаботыСКриптографией = Новый ОписаниеОповещения(
		"ПослеПодключенияРасширенияДляРаботыСКриптографией", ЭтотОбъект, ДополнительныеПараметры);
	
	НачатьПодключениеРасширенияРаботыСКриптографией(ОповещениеПослеПодключенияРасширенияДляРаботыСКриптографией);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьПрограммыКриптографии(ПрограммыКриптографии, МассивПрограммДляСоздания)
	
	ОписаниеПрограмм = Новый Массив;
	
	Для Каждого ТекПрограмма Из МассивПрограммДляСоздания Цикл
		ПараметрыПрограммы = ПрограммыКриптографии[ТекПрограмма];
		ОписаниеПрограммы = ЭлектроннаяПодпись.НовоеОписаниеПрограммы();
		ЗаполнитьЗначенияСвойств(ОписаниеПрограммы, ПараметрыПрограммы);
		ОписаниеПрограмм.Добавить(ОписаниеПрограммы);
	КонецЦикла;
	
	ЭлектроннаяПодпись.ЗаполнитьСписокПрограмм(ОписаниеПрограмм);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайлаНастроекОбмена(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено Тогда
		ПутьКФайлуНастроекОбмена = ВыбранныеФайлы.Имя;
		ВыбранФайлНастроекОбмена = Истина;
		АдресФайлаНастроекОбмена = ВыбранныеФайлы.Хранение;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТестНастройкиАсинхронныйОбмен()
	
	ВидЭДЗапросЗонд = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ЗапросЗонд");
	
	РеквизитыНастройкиОбмена = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыОбменаПоВидуЭД(
		НастройкаОбмена, ВидЭДЗапросЗонд);
	
	Если РеквизитыНастройкиОбмена.ТребуетсяПодпись ИЛИ РеквизитыНастройкиОбмена.АутентификацияПоСертификату Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("РеквизитыНастройкиОбмена", РеквизитыНастройкиОбмена);
		Оповещение = Новый ОписаниеОповещения(
			"ПродолжитьТестНастроекАсинхронныйОбменПослеПолученияОтпечатков", ЭтотОбъект, ДополнительныеПараметры);
		ОбменСБанкамиСлужебныйКлиент.ПолучитьОтпечаткиСертификатов(Оповещение);
		
	Иначе
	
		НастройкиОбмена = Неопределено;
		СообщениеЗапросЗонд = Неопределено;
		ОбменСБанкамиСлужебныйВызовСервера.СформироватьЗапросЗонд(
			НастройкаОбмена, Новый Массив, СообщениеЗапросЗонд, НастройкиОбмена);
	
		Если НЕ ЗначениеЗаполнено(СообщениеЗапросЗонд) ИЛИ НастройкиОбмена = Неопределено Тогда
			ОбработатьРезультатТестаНастройки(Ложь);
			Возврат;
		КонецЕсли;
	
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ЗапросЗонд", СообщениеЗапросЗонд);
		ДополнительныеПараметры.Вставить("РеквизитыНастройкиОбмена", РеквизитыНастройкиОбмена);
		
		Если НастройкиЗагруженыИзФайла Тогда
			Оповещение = Новый ОписаниеОповещения(
				"ПолучитьИдентификаторСессииПослеЗапросаПароляБазоваяАутентификация", ЭтотОбъект, ДополнительныеПараметры);
			ОбменСБанкамиСлужебныйКлиент.ПолучитьДанныеАутентификации(Оповещение, НастройкаОбмена);
		Иначе
			ПолучитьРеквизитыНастройкиОбмена(НастройкаОбмена, АдресСервера, ИдентификаторОрганизации, ВерсияФормата);
			Оповещение = Новый ОписаниеОповещения(
			"ВыполнитьТестНастроекПослеПолученияИдентификатораСессии", ЭтотОбъект, ДополнительныеПараметры);
			ПолучитьИдентификаторСессииБазоваяАутентификация(
				Оповещение, АдресСервера, ИдентификаторОрганизации, Логин, Пароль, ВерсияФормата);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИдентификаторСессииПослеЗапросаПароляБазоваяАутентификация(ДанныеАутентификации, ДополнительныеПараметры) Экспорт
	
	Если ДанныеАутентификации = Неопределено Тогда
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;

	Оповещение = Новый ОписаниеОповещения(
		"ВыполнитьТестНастроекПослеПолученияИдентификатораСессии", ЭтотОбъект, ДополнительныеПараметры);
	ПолучитьИдентификаторСессииБазоваяАутентификация(Оповещение,
		ДополнительныеПараметры.РеквизитыНастройкиОбмена.АдресСервера,
		ДополнительныеПараметры.РеквизитыНастройкиОбмена.ИдентификаторОрганизации,
		ДанныеАутентификации.Логин, ДанныеАутентификации.Пароль,
		ДополнительныеПараметры.РеквизитыНастройкиОбмена.ВерсияФормата);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПолучитьРеквизитыНастройкиОбмена(Знач НастройкаОбмена, АдресСервера, ИдентификаторОрганизации, ВерсияФормата)
	
	РеквизитыНастройкиОбмена = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		НастройкаОбмена, "АдресСервера, ИдентификаторОрганизации, ВерсияФормата");
	АдресСервера = РеквизитыНастройкиОбмена.АдресСервера;
	ИдентификаторОрганизации = РеквизитыНастройкиОбмена.ИдентификаторОрганизации;
	ВерсияФормата = РеквизитыНастройкиОбмена.ВерсияФормата;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьТестНастроекАсинхронныйОбменПослеПолученияОтпечатков(ДанныеОтпечатков, ДополнительныеПараметры) Экспорт
	
	ОтпечаткиСертификатов = Новый Массив;
	Если ТипЗнч(ДанныеОтпечатков) = Тип("Соответствие") Тогда
		Для Каждого КлючЗначение Из ДанныеОтпечатков Цикл
			ОтпечаткиСертификатов.Добавить(КлючЗначение.Ключ);
		КонецЦикла;
	ИначеЕсли Не ЭлектроннаяПодписьКлиент.СоздаватьЭлектронныеПодписиНаСервере() Тогда
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
	
	НастройкиОбмена = Неопределено;
	СообщениеЗапросЗонд = Неопределено;
	ОбменСБанкамиСлужебныйВызовСервера.СформироватьЗапросЗонд(
		НастройкаОбмена, ОтпечаткиСертификатов, СообщениеЗапросЗонд, НастройкиОбмена);
	
	Если НЕ ЗначениеЗаполнено(СообщениеЗапросЗонд) ИЛИ НастройкиОбмена = Неопределено Тогда
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ЗапросЗонд", СообщениеЗапросЗонд);

	Если НастройкиОбмена.Подписывать Тогда
		
		Если НастройкиОбмена.ДоступныеСертификаты.Количество() = 0 Тогда
			ТекстСообщения = НСтр("ru = 'Не найден подходящий сертификат подписи.
										|Убедитесь, что сертификаты, указанные в настройке обмена, установлены в личное хранилище сертификатов операционной системы'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			ОбработатьРезультатТестаНастройки(Ложь);
			Возврат;
		КонецЕсли;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодписанияЗапросаЗонда", ЭтотОбъект, ДополнительныеПараметры);
		МассивСообщенийОбмена = Новый Массив;
		МассивСообщенийОбмена.Добавить(СообщениеЗапросЗонд);
		ОбменСБанкамиСлужебныйКлиент.Подписать(ОписаниеОповещения, МассивСообщенийОбмена, НастройкиОбмена.ДоступныеСертификаты);
	Иначе
		ПродолжитьТестНастроекПослеПроверкиЭПЗапросаЗонда(Истина, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодписанияЗапросаЗонда(Результат, ПараметрыОбработки) Экспорт
	
	Если Результат.Успех Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПродолжитьТестНастроекПослеПроверкиЭПЗапросаЗонда", ЭтотОбъект, ПараметрыОбработки);
		ОбменСБанкамиСлужебныйКлиент.ПроверитьПодписиСообщенияОбмена(ОписаниеОповещения, ПараметрыОбработки.ЗапросЗонд);
	Иначе
		ОбработатьРезультатТестаНастройки(Ложь);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьТестНастроекПослеПроверкиЭПЗапросаЗонда(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат Тогда // Подпись неверна
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СертификатАутентификацииСсылка) Тогда
		МассивСертификатов = Новый Массив;
		МассивСертификатов.Добавить(СертификатАутентификацииСсылка);
		ПарольСертификатаБСП = ПарольСертификата; // предотвращение очистки
		ЭлектроннаяПодписьКлиент.УстановитьПарольСертификата(СертификатАутентификации, ПарольСертификатаБСП);
	Иначе
		МассивСертификатов = СертификатыАутентификации(НастройкаОбмена, ОтпечаткиСертификатов);
	КонецЕсли;
	
	Если ДополнительныеПараметры.РеквизитыНастройкиОбмена.АутентификацияПоСертификату Тогда
	
		ОписаниеДанных = Новый Структура;
		ОписаниеДанных.Вставить("ОтборСертификатов", МассивСертификатов);
		ОписаниеДанных.Вставить("БезПодтверждения",  Истина);
		ОписаниеДанных.Вставить("ЭтоАутентификация", Истина);
		ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Аутентификация на сервере банка'"));
		ОписаниеДанных.Вставить("РазрешитьЗапоминатьПароль", Ложь);
		ОписаниеДанных.Вставить("ЗаголовокДанных", "");
		
		ПараметрыПолученияМаркера = Новый Структура("НастройкаОбмена", НастройкаОбмена);

		ОписаниеПолученияДанных = Новый ОписаниеОповещения(
			"ПолучитьЗашифрованныйИдентификаторСессии", ОбменСБанкамиСлужебныйКлиент, ПараметрыПолученияМаркера);
				
		ОписаниеДанных.Вставить("Данные", ОписаниеПолученияДанных);
					
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПродолжитьОтправкуЗапросаЗондаПослеРасшифровкиМаркера", ЭтотОбъект, ДополнительныеПараметры);
		ЭлектроннаяПодписьКлиент.Расшифровать(ОписаниеДанных, ЭтотОбъект, ОписаниеОповещения);
	Иначе
		Оповещение = Новый ОписаниеОповещения(
			"ПолучитьИдентификаторСессииПослеЗапросаПароляБазоваяАутентификация", ЭтотОбъект, ДополнительныеПараметры);
		ОбменСБанкамиСлужебныйКлиент.ПолучитьДанныеАутентификации(Оповещение, НастройкаОбмена);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьОтправкуЗапросаЗондаПослеРасшифровкиМаркера(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Успех Тогда
		ВыполнитьТестНастроекПослеПолученияИдентификатораСессии(
			Результат.РасшифрованныеДанные, ДополнительныеПараметры);
	Иначе
		ОбработатьРезультатТестаНастройки(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИдентификаторСессииБазоваяАутентификация(Оповещение, АдресСервера, ИдентификаторОрганизации, Логин, Пароль, ВерсияФормата)
	
	ДанныеАутентификации = Новый Структура("Логин, Пароль", Логин, Пароль);
	Если Не ЗначениеЗаполнено(ВерсияФормата) Тогда
		ВерсияФормата = ОбменСБанкамиКлиентСервер.БазоваяВерсияФорматаАсинхронногоОбмена();
	КонецЕсли;
	
	Результат = ОбменСБанкамиСлужебныйВызовСервера.БазоваяАутентификация(
		АдресСервера, ИдентификаторОрганизации, ДанныеАутентификации, ВерсияФормата);
		
	Если ЗначениеЗаполнено(Результат.ТекстОшибки) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ТекстОшибки, НастройкаОбмена());
	КонецЕсли;

	Если Результат.ТребуетсяSMSАвторизация Тогда
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ИдентификаторСессии", Результат.ИдентификаторСессии);
		ПараметрыФормы.Вставить("Телефон", Результат.МаскаТелефона);
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияИдентификатораСессии", Оповещение);
		ДополнительныеПараметры.Вставить("НеподтвержденныйИдентификаторСессии", Результат.ИдентификаторСессии);
		ДополнительныеПараметры.Вставить("АдресСервера", АдресСервера);
		ДополнительныеПараметры.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизации);
		ДополнительныеПараметры.Вставить("ВерсияAPI", ВерсияФормата);
		ОО = Новый ОписаниеОповещения("ОтправитьОдноразовыйПарольПослеВводаИзSMS", ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросОдноразовогоПароля", ПараметрыФормы, , , , , ОО);
	Иначе
		ВыполнитьОбработкуОповещения(Оповещение, Результат.ИдентификаторСессии);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция НастройкаОбмена()
	
	Если ЗначениеЗаполнено(НастройкаОбмена) Тогда
		Возврат НастройкаОбмена;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Функция РеквизитыНовойНастройкиОбмена()
	
	Результат = Новый Структура("Банк,ПрограммаБанка,АдресСервера");
	ЗаполнитьЗначенияСвойств(Результат, ЭтотОбъект);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОтправитьОдноразовыйПарольПослеВводаИзSMS(ОдноразовыйПароль, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ОдноразовыйПароль) Тогда
		ИдентификаторСессии = ОбменСБанкамиСлужебныйВызовСервера.МаркерБанкаПоSMS(ДополнительныеПараметры.АдресСервера,
			ДополнительныеПараметры.ИдентификаторОрганизации, ДополнительныеПараметры.НеподтвержденныйИдентификаторСессии,
			ОдноразовыйПароль, ДополнительныеПараметры.ВерсияAPI, НастройкаОбмена());
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(
		ДополнительныеПараметры.ОповещениеПослеПолученияИдентификатораСессии, ИдентификаторСессии);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьТестНастроекПослеПолученияИдентификатораСессии(ИдентификаторСессии, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторСессии) Тогда
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
		
	ПараметрыЗапроса = Новый Структура;

	ПараметрыЗапроса.Вставить("ИдентификаторСессии", ИдентификаторСессии);
	ПараметрыЗапроса.Вставить("СообщениеОбмена", ДополнительныеПараметры.ЗапросЗонд);
	ПараметрыЗапроса.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	Результат = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияОтправкиЗапросЗондаНаСервере(
			ПараметрыЗапроса, УникальныйИдентификатор);
			
	ИдентификаторЗадания = Результат.ИдентификаторЗадания;
			
	Если Результат.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПослеОтправкиЗапросаЗонда", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	Иначе
		ПослеОтправкиЗапросаЗонда(Результат, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиЗапросаЗонда(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Отправка тестового запроса в банк'");
		ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(ВидОперации, 
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками,
			Результат.ПодробноеПредставлениеОшибки, Результат.КраткоеПредставлениеОшибки, НастройкаОбмена);
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
	
	Результат = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияПолученияИзвещенияОСостоянииЭД(
		ПараметрыЗапроса, УникальныйИдентификатор);
		
	ИдентификаторЗадания = Результат.ИдентификаторЗадания;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияИзвещенияДляЗапросаЗонда", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияИзвещенияДляЗапросаЗонда(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Получение извещения на тестовый запрос из банка'");
		ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(ВидОперации, 
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками,
			Результат.ПодробноеПредставлениеОшибки, Результат.КраткоеПредставлениеОшибки, НастройкаОбмена);
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
	
	СтруктураВозврата = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Оповестить("ОбновитьСостояниеОбменСБанками", СтруктураВозврата.ПараметрОповещения);
	
	Если СтруктураВозврата <> Неопределено И СтруктураВозврата.Свойство("ДанныеЭП")
		И СтруктураВозврата.ДанныеЭП.Количество() Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ДанныеЭП", СтруктураВозврата.ДанныеЭП);
		ДополнительныеПараметры.Вставить("ЕстьОшибка", Ложь);
		СохранитьОчереднуюПодпись(ДополнительныеПараметры);
		
		Возврат;
		
	КонецЕсли;
	
	ПослеОбработкиПолученныхПодписей(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОбработкиПолученныхПодписей(ЕстьОшибка)
	
	Если ЕстьОшибка Тогда
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;

	ОбработатьРезультатТестаНастройки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьОчереднуюПодпись(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.ДанныеЭП.Количество() = 0 Тогда
		ПослеОбработкиПолученныхПодписей(ДополнительныеПараметры.ЕстьОшибка);
		Возврат;
	КонецЕсли;
	
	Для Каждого КлючЗначение Из ДополнительныеПараметры.ДанныеЭП Цикл
		Прервать;
	КонецЦикла;
	ДополнительныеПараметры.ДанныеЭП.Удалить(КлючЗначение.Ключ);
	
	Оповещение = Новый ОписаниеОповещения("ПослеСохраненияПодписейСообщенияОбмена", ЭтотОбъект, ДополнительныеПараметры);
	ОбменСБанкамиСлужебныйКлиент.ДобавитьПодписиИОпределитьСтатусы(Оповещение, КлючЗначение.Ключ, КлючЗначение.Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияПодписейСообщенияОбмена(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		ДополнительныеПараметры.Вставить("ЕстьОшибка", Истина);
	КонецЕсли;
	
	СохранитьОчереднуюПодпись(ДополнительныеПараметры)
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНастройкуОбменаИзФайла()
	
	Если СпособСозданияНастройки = 0 ИЛИ СпособСозданияНастройки = 2 Тогда // автоматическое получение и ручной режим
		
		УчастникиОбменаИзФайла = УчастникиОбменаИзФайла(АдресФайлаНастроекОбмена, Организация, , Банк);
		
		Если УчастникиОбменаИзФайла = Неопределено Тогда
			ОбработатьРезультатСозданияНастройкиОбмена(Ложь);
			Возврат;
		КонецЕсли;
		
		Если УчастникиОбменаИзФайла.Организация <> Организация Тогда
			ТекстСообщения = НСтр("ru = 'Получены некорректные настройки обмена из банка.
										|Требуются настройки для организации: %1
										|Получены настройки для организации: %2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Организация, УчастникиОбменаИзФайла.Организация);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			ОбработатьРезультатСозданияНастройкиОбмена(Ложь);
			Возврат;
		КонецЕсли;
	
		Если УчастникиОбменаИзФайла.Банк <> Банк Тогда
			ТекстСообщения = НСтр("ru = 'Получены некорректные настройки обмена из банка.
										|Требуются настройки для банка: %1
										|Получены настройки для банка: %2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Банк, УчастникиОбменаИзФайла.Банк);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			ОбработатьРезультатСозданияНастройкиОбмена(Ложь);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Результат = ЗагрузитьНастройкуОбменаИзФайлаНаСервере(
		АдресФайлаНастроекОбмена, УникальныйИдентификатор, ИмяВнешнегоМодуля, Организация, ЛокальныйФайл);
		
	Если Результат.Статус = "Выполняется" Тогда
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПослеСозданияНастройкиОбмена", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	Иначе
		ПослеСозданияНастройкиОбмена(Результат, Неопределено);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗагрузитьНастройкуОбменаИзФайлаНаСервере(
	Знач АдресФайлаНастроекОбмена,
	Знач ИдентификаторФормы,
	Знач ИмяВнешнегоМодуля,
	Знач Организация,
	Знач ЛокальныйФайл = Ложь)
	
	Задание = Новый Структура();
	Задание.Вставить("Завершено", Ложь);
	Задание.Вставить("Успешно", Ложь);
	Задание.Вставить("Идентификатор");
	Задание.Вставить("АдресХранилища");
	
	ДвоичныеДанныеФайлаНастроек = ПолучитьИзВременногоХранилища(АдресФайлаНастроекОбмена);
	Параметры = Новый Структура;
	Параметры.Вставить("ДвоичныеДанныеФайлаНастроек", ДвоичныеДанныеФайлаНастроек);
	Параметры.Вставить("ИмяВнешнегоМодуля", ИмяВнешнегоМодуля);
	Параметры.Вставить("Организация", Организация);
	Параметры.Вставить("ЛокальныйФайл", ЛокальныйФайл);
	НазваниеЗадания = НСтр("ru = 'Прямой обмен с банком: Загрузка настроек обмена из файла'");
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НазваниеЗадания;
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"Справочники.НастройкиОбменСБанками.ЗагрузитьНастройкиОбменаИзФайла", Параметры, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ПодобратьПрограммуКриптографииПослеПолученияДвоичныхДанныхСертификата(
	ВыгруженныеДанные,
	ДополнительныеПараметры) Экспорт
	
	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("ДанныеСертификата", ВыгруженныеДанные);
	Оповещение = Новый ОписаниеОповещения(
		"СохранитьСертификатВИнформационнойБазеПослеОпределенияПрограммыКриптографии", ЭтотОбъект, ПараметрыОбработки);
	
	ПодобратьПрограммуКриптографииДляСертификата(Оповещение, ВыгруженныеДанные);

КонецПроцедуры

&НаКлиенте
Процедура СохранитьСертификатВИнформационнойБазеПослеОпределенияПрограммыКриптографии(
	Результат,
	ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда // не удалось подобрать программу криптографии
		Если СоздаватьЭлектронныеПодписиНаСервере Тогда
			ПопыткаСозданияСертификатаНаСервере()
		Иначе
			ТекстСообщения = НСтр("ru = 'Не удалось найти подходящую программу криптографии для указанного сертификата.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Если СпособСозданияНастройки = 0 Тогда // автоматическое получение с сервера банка
				ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
			Иначе // ручное создание настройки обмена, переключаем на предыдущий шаг
				СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
			КонецЕсли;
		КонецЕсли;
	Иначе
		СертификатАутентификацииСсылка = НайтиСоздатьСертификатЭП(
			ДополнительныеПараметры.ДанныеСертификата, Организация, Результат.Программа);
		Если Не ЗначениеЗаполнено(СертификатАутентификацииСсылка) Тогда
			Если СпособСозданияНастройки = 0 Тогда // автоматическое получение с сервера банка
				ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
			Иначе // ручное создание настройки обмена, переключаем на предыдущий шаг
				СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
			КонецЕсли;
			Возврат;
		КонецЕсли;
		ПолучитьИдентификаторСессииПоСертификату(
			СертификатАутентификацииСсылка, ДополнительныеПараметры.ДанныеСертификата)
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиСоздатьСертификатЭП(ДанныеСертификата, Организация, ИнформацияОПрограммеКриптографии)
	
	Возврат КриптографияБЭД.НайтиСоздатьСертификатЭП(ДанныеСертификата, Организация, ИнформацияОПрограммеКриптографии);
	
КонецФункции

&НаКлиенте
Процедура ВыгрузитьДанныеСертификатаПослеПолучения(СертификатКриптографии, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(СертификатКриптографии) <> Тип("СертификатКриптографии") Тогда
		Если СоздаватьЭлектронныеПодписиНаСервере Тогда
			ПопыткаСозданияСертификатаНаСервере()
		Иначе
			Если СпособСозданияНастройки = 0 Тогда // автоматическое получение с сервера банка
				ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
			Иначе // ручное создание настройки обмена, переключаем на предыдущий шаг
				СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
			КонецЕсли;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ПодобратьПрограммуКриптографииПослеПолученияДвоичныхДанныхСертификата", ЭтотОбъект);
	
	СертификатКриптографии.НачатьВыгрузку(Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура ПопыткаСозданияСертификатаНаСервере()

	ДанныеСертификата = Неопределено;
	СертификатАутентификацииСсылка = СоздатьСертификатНаСервере(
		СертификатАутентификации, ПарольСертификата, ДанныеСертификата);
	Если ЗначениеЗаполнено(СертификатАутентификацииСсылка) Тогда
		ПолучитьИдентификаторСессииПоСертификату(СертификатАутентификацииСсылка, ДанныеСертификата)
	Иначе
		Если СпособСозданияНастройки = 0 Тогда // автоматическое получение с сервера банка
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Иначе // ручное создание настройки обмена, переключаем на предыдущий шаг
			СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СоздатьСертификатНаСервере(
	Знач СертификатАутентификации,
	Знач ПарольДоступаКЗакрытомуКлючу,
	ДвоичныеДанныеСертификата)
	
	СертификатКриптографии = ЭлектроннаяПодпись.ПолучитьСертификатПоОтпечатку(СертификатАутентификации, Истина);
	ДвоичныеДанныеСертификата = СертификатКриптографии.Выгрузить();
	
	ОписаниеОшибки = "";
	ВыборкаПрограмм = Справочники.ПрограммыЭлектроннойПодписиИШифрования.Выбрать();
	Пока ВыборкаПрограмм.Следующий() Цикл
		
		МенеджерКриптографии = ЭлектроннаяПодпись.МенеджерКриптографии(
			"", Ложь, ОписаниеОшибки, ВыборкаПрограмм.Ссылка);
		Если МенеджерКриптографии <> Неопределено Тогда
			Если ЭлектроннаяПодпись.ПроверитьСертификат(МенеджерКриптографии, СертификатКриптографии) Тогда
				Попытка
					МенеджерКриптографии.ПарольДоступаКЗакрытомуКлючу = ПарольДоступаКЗакрытомуКлючу;
					РезультатПодписи = МенеджерКриптографии.Подписать(
						ДвоичныеДанныеСертификата, СертификатКриптографии);
					Если РезультатПодписи <> Неопределено Тогда
						ДополнительныеПараметры = Новый Структура("Программа", ВыборкаПрограмм.Ссылка);
						Возврат ЭлектроннаяПодпись.ЗаписатьСертификатВСправочник(
							СертификатКриптографии, ДополнительныеПараметры);
					КонецЕсли;
				Исключение
					Продолжить;
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;

	КонецЦикла;
	
	ТекстСообщения = НСтр("ru = 'Не удалось найти подходящую программу криптографии для указанного сертификата.'");
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	
	Возврат Неопределено

КонецФункции

&НаКлиенте
Процедура ПолучитьИдентификаторСессииПоСертификату(СертификатАутентификацииСсылка, ДанныеСертификата)
	
	Если Не ЗначениеЗаполнено(СертификатАутентификацииСсылка) Тогда
		Если СпособСозданияНастройки = 0 Тогда // автоматическое получение с сервера банка
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Иначе // ручное создание настройки обмена, переключаем на предыдущий шаг
			СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ПарольСертификатаБСП = ПарольСертификата; // предотвращение очистки
	ЭлектроннаяПодписьКлиент.УстановитьПарольСертификата(СертификатАутентификацииСсылка, ПарольСертификатаБСП);

	ПробнаяОперация = СпособСозданияНастройки = 2;
	// Первая попытка аутентификации по актуальной версии формата обмена
	Попытка
		ИдентификаторСессии = ОбменСБанкамиСлужебныйВызовСервера.МаркерБанкаПоСертификату(
			АдресСервераНастроек, "0", ДанныеСертификата, , ПробнаяОперация);
	Исключение
		ТекстПервойОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	// Вторая попытка аутентификации по версии формата обмена 2.03
	Если ТекстПервойОшибки <> Неопределено Тогда
		Попытка
			ВерсияФормата = ОбменСБанкамиКлиентСервер.УстаревшаяВерсияФорматаАсинхронногоОбмена();
			ИдентификаторСессии = ОбменСБанкамиСлужебныйВызовСервера.МаркерБанкаПоСертификату(
				АдресСервераНастроек, "0", ДанныеСертификата, ВерсияФормата, ПробнаяОперация);
		Исключение
			Если НЕ ПробнаяОперация Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстПервойОшибки);
			КонецЕсли;
		КонецПопытки;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИдентификаторСессии) Тогда
		Если СпособСозданияНастройки = 0 Тогда // автоматическое получение с сервера банка
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Иначе // автоматическое получение настроек не поддерживается банком, переключаем на следующий шаг.
			СменитьСтраницуШаги(Элементы.СтраницаАсинхронныйОбмен);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ОписаниеДанных = Новый Структура;
	ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Аутентификация на сервере банка'"));
	ОписаниеДанных.Вставить("ЗаголовокДанных", НСтр("ru = 'Аутентификация на сервере банка'"));
	ОписаниеДанных.Вставить("СообщитьОЗавершении", Ложь);
	МассивСертификатов = Новый Массив;
	МассивСертификатов.Добавить(СертификатАутентификацииСсылка);
	ОписаниеДанных.Вставить("ОтборСертификатов", МассивСертификатов);
	ОписаниеДанных.Вставить("БезПодтверждения", Истина);
	ОписаниеДанных.Вставить("ЭтоАутентификация", Истина);
	ОписаниеДанных.Вставить("Данные", ИдентификаторСессии);
	
	ОбработкаРезультата = Новый ОписаниеОповещения("ПослеРасшифровкиИдентификатораСессии", ЭтотОбъект);
	
	ЭлектроннаяПодписьКлиент.Расшифровать(ОписаниеДанных, ЭтотОбъект, ОбработкаРезультата);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеРасшифровкиИдентификатораСессии(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		Если СпособСозданияНастройки = 0 Тогда // автоматическое получение с сервера банка
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Иначе // ручное создание настройки обмена, переключаем на предыдущий шаг, т.к. маркер не расшифровывается
			СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
		КонецЕсли;
		Возврат;
	КонецЕсли;

	ПолучитьНастройкиОбменаПослеПолученияИдентификатораСессии(Результат.РасшифрованныеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьНастройкиОбменаССервераБанкаБазоваяАутентификация()
	
	НастройкаОбмена = НастройкаОбмена();
	
	Если НЕ ЗначениеЗаполнено(НастройкаОбмена) Тогда
		ДополнительныеПараметры = РеквизитыНовойНастройкиОбмена(); 
	КонецЕсли;
		
	Обработчик = Новый ОписаниеОповещения("ПолучитьНастройкиОбменаПослеПолученияИдентификатораСессии", 
											ЭтотОбъект, ДополнительныеПараметры);
	
	ДанныеАутентификации = Новый Структура("Логин, Пароль", Логин, Пароль);
	БазоваяВерсияФорматаАсинхронногоОбмена = ОбменСБанкамиКлиентСервер.БазоваяВерсияФорматаАсинхронногоОбмена();
	ОбменСБанкамиСлужебныйКлиент.ПолучитьМаркерБанкаПоЛогинуИПаролю(Обработчик, АдресСервераНастроек, "0",
		ДанныеАутентификации, БазоваяВерсияФорматаАсинхронногоОбмена, НастройкаОбмена, СпособСозданияНастройки = 2);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьНастройкиОбменаПослеПолученияИдентификатораСессии(
	ИдентификаторСессии,
	ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(ИдентификаторСессии) Тогда
		Если СпособСозданияНастройки = 0 Тогда // автоматическое получение с сервера банка
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Иначе // ручное создание настройки обмена, переключаем на предыдущий шаг, т.к. пустой идентификатор сессии.
			СменитьСтраницуШаги(Элементы.СтраницаПолучениеССервера);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	// При ручном создании настройки обмена пробуем получить настройки с сервера банка.
	ПробнаяОперация = СпособСозданияНастройки = 2;
	
	ПараметрыПолученияНастроек = Новый Структура;
	ПараметрыПолученияНастроек.Вставить("АдресСервера", АдресСервераНастроек);
	ПараметрыПолученияНастроек.Вставить("Организация", Организация);
	ПараметрыПолученияНастроек.Вставить("Банк", Банк);
	ПараметрыПолученияНастроек.Вставить("ИдентификаторСессии", ИдентификаторСессии);
	ПараметрыПолученияНастроек.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	ПараметрыПолученияНастроек.Вставить("ПробнаяОперация", ПробнаяОперация);
	ПараметрыПолученияНастроек.Вставить("НомерБанковскогоСчета", НомерБанковскогоСчета);
	ПараметрыПолученияНастроек.Вставить("ИдентификаторОрганизации", "0");
	ПараметрыПолученияНастроек.Вставить("ВерсияФормата", ВерсияФормата);
	ПараметрыПолученияНастроек.Вставить("НастройкаОбмена", НастройкаОбмена());
	
	Результат = ЗапускЗаданияПоПолучениюНастроекОбменаНаСервере(УникальныйИдентификатор, ПараметрыПолученияНастроек);
	
	ИдентификаторЗадания = Результат.ИдентификаторЗадания;
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияНастроекОбмена", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияНастроекОбмена(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		Если СпособСозданияНастройки = 2 Тогда // пробная операция
			СменитьСтраницуШаги(Элементы.СтраницаФинал);
			НастройкиЗагруженыИзФайла = Истина;
		КонецЕсли;
		АдресФайлаНастроекОбмена = Результат.АдресРезультата;
		ОбработатьРезультатПолученияНастроекССервераБанка(Истина);
		Элементы.СозданиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
		ПодключитьОбработчикОжидания("СоздатьНастройкуОбмена", 0.1, Истина);
	Иначе // Ошибка
		ПробнаяОперация = СпособСозданияНастройки = 2;
		Если ПробнаяОперация Тогда
			СменитьСтраницуШаги(Элементы.СтраницаАсинхронныйОбмен);
		Иначе
			ВидОперации = НСтр("ru = 'Получение настроек обмена из банка.'");
			ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(ВидОперации, 
				ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками, Результат.ПодробноеПредставлениеОшибки,
				Результат.КраткоеПредставлениеОшибки);
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапускЗаданияПоПолучениюНастроекОбменаНаСервере(Знач УникальныйИдентификатор, Знач ПараметрыПолученияНастроек)

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр(
		"ru = '1С:ДиректБанк: Получение настроек обмена с сервера банка.'");
	
	ПараметрыПолученияНастроек.ИдентификаторСессии = ОбменСБанкамиСлужебныйВызовСервера.СтрокаИзДвоичныхДанных(
		ПараметрыПолученияНастроек.ИдентификаторСессии);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"Справочники.НастройкиОбменСБанками.ПолучитьНастройкиОбменаССервераБанка", ПараметрыПолученияНастроек,
		ПараметрыВыполнения);
	
КонецФункции

&НаСервереБезКонтекста
Функция СертификатыНаСервере(ОписаниеОшибки)
	
	Если Не ЭлектроннаяПодпись.СоздаватьЭлектронныеПодписиНаСервере() Тогда
		Возврат Неопределено;
	КонецЕсли;

	МенеджерКриптографии = ЭлектроннаяПодпись.МенеджерКриптографии("", Ложь, ОписаниеОшибки);
	
	Если МенеджерКриптографии = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ХранилищеСертификатовКриптографии = МенеджерКриптографии.ПолучитьХранилищеСертификатов(
		ТипХранилищаСертификатовКриптографии.ПерсональныеСертификаты);
	
	МассивСертификатов = ХранилищеСертификатовКриптографии.ПолучитьВсе();
	
	СписокВозврата = Новый СписокЗначений();
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	
	Для Каждого Сертификат Из МассивСертификатов Цикл
		Если Сертификат.ДатаОкончания < ТекущаяДатаСеанса Тогда
			Продолжить;
		КонецЕсли;
		
		ПредставлениеСертификата = ЭлектроннаяПодпись.ПредставлениеСертификата(Сертификат);
		Если Сертификат.Субъект.Свойство("O") И ЗначениеЗаполнено(Сертификат.Субъект.O) Тогда
			ПредставлениеСертификата = ПредставлениеСертификата + " (" + Сертификат.Субъект.O + ")";
		КонецЕсли;
		ОтпечатокСертификата = Base64Строка(Сертификат.Отпечаток);
		СписокВозврата.Добавить(ОтпечатокСертификата, ПредставлениеСертификата);
	КонецЦикла;
	
	Возврат СписокВозврата;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьСписокВыбораСертификатовАутентификации()
	
	Оповещение = Новый ОписаниеОповещения("СоздатьПрограммыКриптографииПослеПоискаУстановленных", ЭтотОбъект);
	ЭлектроннаяПодписьКлиент.НайтиУстановленныеПрограммы(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПрограммыКриптографииПослеПоискаУстановленных(Результат, ДополнительныеПараметры) Экспорт
	
	СоздатьПрограммыКриптографииНаСервере(Результат);
	
	Если СоздаватьЭлектронныеПодписиНаСервере Тогда
		ОписаниеОшибки = "";
		СертификатыСервера = СертификатыНаСервере(ОписаниеОшибки);
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОшибкаНаСервере", ОписаниеОшибки);
	ДополнительныеПараметры.Вставить("СертификатыСервера", СертификатыСервера);
	ДополнительныеПараметры.Вставить("ВыводитьОшибку", Ложь);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПолучитьЛичныеСертификатыПослеСозданияМенеджераКриптографии", ЭтотОбъект, ДополнительныеПараметры);
	ЭлектроннаяПодписьКлиент.СоздатьМенеджерКриптографии(Оповещение, "ПолучениеСертификатов", Ложь);
	
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СоздатьПрограммыКриптографииНаСервере(Знач ОписанияПрограмм)
	
	ОписанияУстановленныхПрограмм = Новый Массив;
	Для Каждого ПрограммаКриптографии Из ОписанияПрограмм Цикл
		Если ПрограммаКриптографии.ИмяПрограммы = "Microsoft Enhanced Cryptographic Provider v1.0"
			ИЛИ Не ПрограммаКриптографии.Установлена Тогда
			Продолжить;
		КонецЕсли;

		ОписаниеПрограммы = ЭлектроннаяПодпись.НовоеОписаниеПрограммы();
		ЗаполнитьЗначенияСвойств(ОписаниеПрограммы, ПрограммаКриптографии);
		ОписанияУстановленныхПрограмм.Добавить(ОписаниеПрограммы);
	КонецЦикла;
	УстановитьПривилегированныйРежим(Истина);
	ЭлектроннаяПодпись.ЗаполнитьСписокПрограмм(ОписанияУстановленныхПрограмм);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокВыбораСертификатовАутентификацииСВыводомОшибки()
	
	Если СоздаватьЭлектронныеПодписиНаСервере Тогда
		ОписаниеОшибки = "";
		СертификатыСервера = СертификатыНаСервере(ОписаниеОшибки);
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОшибкаНаСервере", ОписаниеОшибки);
	ДополнительныеПараметры.Вставить("СертификатыСервера", СертификатыСервера);
	ДополнительныеПараметры.Вставить("ВыводитьОшибку", Истина);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПолучитьЛичныеСертификатыПослеСозданияМенеджераКриптографии", ЭтотОбъект, ДополнительныеПараметры);
	ЭлектроннаяПодписьКлиент.СоздатьМенеджерКриптографии(
		Оповещение, "ПолучениеСертификатов", НЕ СоздаватьЭлектронныеПодписиНаСервере);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьЛичныеСертификатыПослеСозданияМенеджераКриптографии(
	МенеджерКриптографии,
	ДополнительныеПараметры) Экспорт

	Если НЕ ТипЗнч(МенеджерКриптографии) = Тип("МенеджерКриптографии") Тогда
		Если ЗначениеЗаполнено(ДополнительныеПараметры.СертификатыСервера) Тогда
			Если УпрощенныйРежимНастройки Тогда
				СертификатСписокВыбора = Элементы.СертификатСмешанныйУН.СписокВыбора;
			Иначе
				СертификатСписокВыбора = Элементы.СертификатСмешанный.СписокВыбора;
			КонецЕсли;
			СертификатСписокВыбора.Очистить();
			Для Каждого Элемент Из ДополнительныеПараметры.СертификатыСервера Цикл
				НовЗапись = СертификатСписокВыбора.Добавить();
				ЗаполнитьЗначенияСвойств(НовЗапись, Элемент);
			КонецЦикла;
		ИначеЕсли ЗначениеЗаполнено(ДополнительныеПараметры.ОшибкаНаСервере)
			И ДополнительныеПараметры.ВыводитьОшибку Тогда
				ШаблонСообщения = НСтр("ru ='При подключении программы криптографии произошла ошибка.
				|НА КЛИЕНТЕ:
				|%1
				|НА СЕРВЕРЕ:
				|%2'");
				ТекстСообщения = СтрШаблон(
					ШаблонСообщения, МенеджерКриптографии, ДополнительныеПараметры.ОшибкаНаСервере);
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		Возврат;
	Иначе
		МенеджерКриптографииНаКлиенте = МенеджерКриптографии;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПолучитьСертификатыПослеПолученияХранилища", ЭтотОбъект, ДополнительныеПараметры);
		
	МенеджерКриптографии.НачатьПолучениеХранилищаСертификатов(
		ОписаниеОповещения, ТипХранилищаСертификатовКриптографии.ПерсональныеСертификаты);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСертификатыПослеПолученияХранилища(ХранилищеСертификатовКриптографии, ДополнительныеПараметры) Экспорт
	
	ОповещениеПослеПолученияСертификатов = Новый ОписаниеОповещения(
		"ЗаполнитьСписокВыбораСертификатовПослеПолученияИзХранилища", ЭтотОбъект, ДополнительныеПараметры);
		
	ХранилищеСертификатовКриптографии.НачатьПолучениеВсех(ОповещениеПослеПолученияСертификатов);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокВыбораСертификатовПослеПолученияИзХранилища(Сертификаты, ДополнительныеПараметры) Экспорт
	
	Если НЕ Сертификаты.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	Если УпрощенныйРежимНастройки Тогда
		СертификатСписокВыбора = Элементы.СертификатСмешанныйУН.СписокВыбора;
	Иначе
		СертификатСписокВыбора = Элементы.СертификатСмешанный.СписокВыбора;
	КонецЕсли;
	
	СертификатСписокВыбора.Очистить();
	ТекущаяДатаСеанса = ОбщегоНазначенияКлиент.ДатаСеанса();
	
	Для Каждого Сертификат Из Сертификаты Цикл
		Если Сертификат.ДатаОкончания < ТекущаяДатаСеанса Тогда
			Продолжить;
		КонецЕсли;
		
		ПредставлениеСертификата = ЭлектроннаяПодписьКлиент.ПредставлениеСертификата(Сертификат);
		Если Сертификат.Субъект.Свойство("O") И ЗначениеЗаполнено(Сертификат.Субъект.O) Тогда
			ПредставлениеСертификата = ПредставлениеСертификата + " (" + Сертификат.Субъект.O + ")";
		КонецЕсли;
		ОтпечатокСертификата = Base64Строка(Сертификат.Отпечаток);
		СертификатСписокВыбора.Добавить(ОтпечатокСертификата, ПредставлениеСертификата);
	КонецЦикла;
	
	СертификатСписокВыбора.СортироватьПоПредставлению();
	
	Если УпрощенныйРежимНастройки Тогда
		ЕстьСертификаты = СертификатСписокВыбора.Количество() > 0;
		СменитьСтраницуШаги(Элементы.СтраницаУпрощенныйРежимНастройки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СертификатыАутентификации(НастройкаОбмена, ОтпечаткиСертификатов)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиОбменСБанкамиСертификатыПодписейОрганизации.СертификатЭП
	|ИЗ
	|	Справочник.НастройкиОбменСБанками.СертификатыПодписейОрганизации КАК НастройкиОбменСБанкамиСертификатыПодписейОрганизации
	|ГДЕ
	|	НастройкиОбменСБанкамиСертификатыПодписейОрганизации.Ссылка = &НастройкаОбмена
	|	И &ПроверкаПользователя
	|	И НЕ НастройкиОбменСБанкамиСертификатыПодписейОрганизации.СертификатЭП.ПометкаУдаления
	|	И НастройкиОбменСБанкамиСертификатыПодписейОрганизации.СертификатЭП.Отпечаток В(&ОтпечаткиСертификатов)";
	Запрос.УстановитьПараметр("НастройкаОбмена", НастройкаОбмена);
	Запрос.УстановитьПараметр("ОтпечаткиСертификатов", ОтпечаткиСертификатов);
	
	Если Пользователи.ЭтоПолноправныйПользователь( , , Ложь) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПроверкаПользователя", "ИСТИНА");
	Иначе
		Запрос.УстановитьПараметр("ПользовательНеУказан", Пользователи.СсылкаНеуказанногоПользователя());
		Запрос.УстановитьПараметр("Пользователь",  Пользователи.АвторизованныйПользователь());
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПроверкаПользователя",
			"НастройкиОбменСБанкамиСертификатыПодписейОрганизации.СертификатЭП.Пользователь В (&Пользователь, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка), &ПользовательНеУказан)");
	КонецЕсли;
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат.ВыгрузитьКолонку("СертификатЭП");
	
КонецФункции

&НаСервереБезКонтекста
Функция БИКБанка(Банк)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Банк, "Код");
	
КонецФункции

&НаКлиенте
Процедура ПослеВключенияАвтоматическогоПолученияВыписки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		Элементы.ГруппаДоступноАвтоматическоеПолучениеВыписки.Видимость = Ложь;
		Элементы.ГруппаЗапущеноАвтоматическоеПолучениеВыписки.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Сбербанк

#Область FintechAPI

&НаКлиенте
Процедура ПослеАутентификацииFintechСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
		Элементы.ГруппаЗагрузкаПараметровОбменаСБанкомУРН.Видимость = Истина;
		Элементы.ПолучениеНастроекОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.НезавершившаясяПроверка;
		Элементы.СтраницыРезультатПодключенияУРН.ТекущаяСтраница = Элементы.СтраницаНеЗавершеноУРН;
		
		ОбработатьРезультатПолученияНастроекССервераБанка(Истина);
		Элементы.СозданиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
		ПодключитьОбработчикОжидания("СоздатьНастройкуОбмена", 0.1, Истина);
	Иначе
		ОбработатьРезультатПолученияНастроекССервераБанка(Результат)
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СсылкаНаНовуюНастройкуОбмена()
	
	НастройкаОбменаОбъект = Справочники.НастройкиОбменСБанками.СоздатьЭлемент();
	Возврат НастройкаОбменаОбъект.ПолучитьСсылкуНового();
	
КонецФункции

&НаКлиенте
Процедура ТестНастройкиОбменаFintech()
	
	Обработчик = Новый ОписаниеОповещения("ПослеТестаНастройкиОбменаFintech", ЭтотОбъект);
	ОбменСБанкамиСлужебныйКлиент.ПровестиТестНастройки(Обработчик, НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеТестаНастройкиОбменаFintech(Результат, ДополнительныеПараметры) Экспорт
	
	ОбработатьРезультатТестаНастройки(Результат);
	
КонецПроцедуры

#КонецОбласти

&НаСервереБезКонтекста
Функция ЗаданиеПоПолучениюИдентификатораВКСбербанк(Знач URLВК, Знач УникальныйИдентификатор)

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Скачивание внешней компоненты с интернет.'");
	
	ПараметрыПроцедуры = Новый Структура("URLВК", URLВК);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиСлужебный.ОпределитьИдентификаторВКСбербанк", ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура АутентификацияНаСервереПослеУстановкиКаналаСбербанк(Успех, ДополнительныеПараметры) Экспорт
	
	Если Не Успех Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПолучитьСертификатыСТокенаПослеПодключенияВКСбербанк", ЭтотОбъект);
	ОбменСБанкамиСлужебныйКлиент.ПодключитьКомпоненту(Оповещение, ИмяВнешнегоМодуля);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСертификатыСТокенаПослеПодключенияВКСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Подключено Тогда
		ДополнительныеПараметры = Новый Структура("ПодключаемыйМодуль", Результат.ПодключаемыйМодуль);
		Оповещение = Новый ОписаниеОповещения(
			"ВыбратьСертификатПослеПолученияСТокенаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		ОбменСБанкамиСлужебныйКлиент.ПолучитьДанныеСертификатовСТокенаСбербанк(Оповещение, Результат.ПодключаемыйМодуль);
	Иначе
		Если НЕ ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ОписаниеОшибки);
		КонецЕсли;
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСертификатПослеПолученияСТокенаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ТекстОшибки);
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	СоответствиеСертификатов = Результат.СоответствиеСертификатов;
	ДополнительныеПараметры.Вставить("СоответствиеСертификатов", СоответствиеСертификатов);
	
	СписокСертификатов = Новый СписокЗначений;
	Если СоответствиеСертификатов.Количество() = 1 Тогда
		Для Каждого КлючЗначение Из СоответствиеСертификатов Цикл
			ДополнительныеПараметры.Вставить("ИдентификаторСертификата", КлючЗначение.Ключ);
			Оповещение = Новый ОписаниеОповещения(
				"ПослеПолученияДвоичныхДанныхСертификатаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ОбменСБанкамиСлужебныйКлиент.ПолучитьДвоичныеДанныеСертификатаСбербанк(
				Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, КлючЗначение.Ключ);
			Возврат;
		КонецЦикла
	ИначеЕсли НЕ СоответствиеСертификатов.Количество() Тогда
		ТекстСообщения = НСтр("ru = 'Отсутствуют сертификаты на банковском ключе.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	ВыборкаСертификатов = Новый Соответствие;
	
	Для Каждого Элемент Из СоответствиеСертификатов Цикл
		
		ИдентификаторСертификата = Элемент.Ключ;
		ДвоичныеДанныеСертификата = Элемент.Значение;
		
		Если ДвоичныеДанныеСертификата = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		СтруктураСертификата = ОбменСБанкамиСлужебныйВызовСервера.СтруктураСертификата(ДвоичныеДанныеСертификата);
		
		Если СтруктураСертификата.ДействителенДо < ОбщегоНазначенияКлиент.ДатаСеанса() Тогда
			Продолжить;
		КонецЕсли;
	
		Если ЗначениеЗаполнено(СтруктураСертификата) Тогда
			ШаблонПредставления = НСтр("ru = '%1, до %2'");
			ДатаСтрокой = Формат(СтруктураСертификата.ДействителенДО, "ДФ=MM.yyyy");
			Представление = СтрШаблон(ШаблонПредставления, СтруктураСертификата.КомуВыдан, ДатаСтрокой);
			ВыборкаСертификатов.Вставить(ИдентификаторСертификата, Представление);
		КонецЕсли;
		
	КонецЦикла;

	Если ВыборкаСертификатов.Количество() = 1 Тогда
		Для Каждого КлючЗначение Из ВыборкаСертификатов Цикл
			ДополнительныеПараметры.Вставить("ИдентификаторСертификата", КлючЗначение.Ключ);
			Оповещение = Новый ОписаниеОповещения(
				"ПослеПолученияДвоичныхДанныхСертификатаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ОбменСБанкамиСлужебныйКлиент.ПолучитьДвоичныеДанныеСертификатаСбербанк(
				Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, КлючЗначение.Ключ);
			Возврат;
		КонецЦикла
	КонецЕсли;
	
	Для Каждого Элемент Из ВыборкаСертификатов Цикл
		СписокСертификатов.Добавить(Элемент.Ключ, Элемент.Значение);
	КонецЦикла;
	
	ОповещениеПослеВыбораСертификата = Новый ОписаниеОповещения(
		"ОбработкаВыбораСертификатаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ЗаголовокФормыВыбора = НСтр("ru = 'Выберите сертификат'");

	СписокСертификатов.ПоказатьВыборЭлемента(ОповещениеПослеВыбораСертификата, ЗаголовокФормыВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораСертификатаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ИдентификаторСертификата", Результат.Значение);
	Оповещение = Новый ОписаниеОповещения(
		"ПослеПолученияДвоичныхДанныхСертификатаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ОбменСБанкамиСлужебныйКлиент.ПолучитьДвоичныеДанныеСертификатаСбербанк(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, Результат.Значение);

КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияДвоичныхДанныхСертификатаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	ДанныеСертификата = Новый Структура;
	ДанныеСертификата.Вставить("ИдентификаторСертификата", ДополнительныеПараметры.ИдентификаторСертификата);
	ДанныеСертификата.Вставить("ДвоичныеДанныеСертификата", Результат);
	
	Оповещение = Новый ОписаниеОповещения("ПослеАутентификацииПоСертификатуСбербанк", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.АутентификацияПоСертификатуСбербанк(
		Оповещение, ИмяВнешнегоМодуля, ДанныеСертификата, НастройкаОбмена(), Истина);

КонецПроцедуры

&НаКлиенте
Процедура ПослеБазовойАутентификацииСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Успех Тогда
		ПолучитьНастройкиОбменаСбербанк();
	ИначеЕсли Результат.ТребуетсяТокен Тогда
		ИспользуетсяКриптография = Истина;
		Обработчик = Новый ОписаниеОповещения("УстановитьКаналПослеАутентификацииНаТокенеСбербанк", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.АутентифицироватьсяНаТокенеСбербанка(
			Обработчик, ИмяВнешнегоМодуля, НастройкаОбмена(), Истина);
	Иначе
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьНастройкиОбменаСбербанк()
	
	Ошибка = Ложь;
	ОтправитьЗапросПерсональныхДанныхСбербанк(НастройкаОбмена(), УникальныйИдентификатор, ТикетОбменаСбербанк, Ошибка);
	
	Если Ошибка Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	ОжиданиеОперацийБЭДКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);

	ПодключитьОбработчикОжидания("Подключаемый_ПолучитьЛичнуюИнформациюСбербанк", 1, Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПолучитьТипыПисемПоТикетуЗапроса(Знач НастройкаОбмена, Знач ИдентификаторОрганизации, Знач КлючСессии, Знач Тикет, ТипыПисем, Ожидать, Ошибка)
	
	ВидОперации = НСтр("ru = 'Формирование запроса статуса документа'");

	Попытка
		
		МассивТикетов = Новый Массив;
		МассивТикетов.Добавить(Тикет);
		ИдентификаторСессии = ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(КлючСессии).ИдентификаторСессии;
		МассивОтветов = Неопределено;
		
		ОбменСБанкамиСлужебный.ПолучитьОтветыПоТикетамСбербанк(
			НастройкаОбмена, МассивТикетов, ИдентификаторСессии, ИдентификаторОрганизации, МассивОтветов);
		
	Исключение
		
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ШаблонСообщения = Нстр("ru = 'При получении статуса запроса типа писем произошла ошибка:
								|%1'");
		ТекстОшибки = СтрШаблон(ШаблонСообщения, КраткаяИнформация);
		ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(ВидОперации, 
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками, ПодробноеПредставлениеОшибки, ТекстОшибки);
		Ошибка = Истина;
		Возврат;
		
	КонецПопытки;
	
	Ответ = МассивОтветов.Получить(0);
	
	Если Ответ = "<!--NOT PROCESSED YET-->" ИЛИ Ответ = "<!--NOT_PROCESSED_YET-->" Тогда
		Ожидать = Истина;
	ИначеЕсли Ответ = "<!--REQUEST NOT FOUND-->" ИЛИ Ответ = "<!--REQUEST_NOT_FOUND-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос не найден.
							|Повторите операцию позже.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , , , Ошибка);
	ИначеЕсли Ответ = "<!--REQUESTID DUBLIC-->" ИЛИ Ответ = "<!--REQUESTID_DUBLIC-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос с таким идентификатором уже существует.
							|Повторите операцию позже.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , , , Ошибка);
	Иначе
		ТипыПисем = ОбменСБанкамиСлужебныйВызовСервера.ТипыПисемСбербанк(Ответ, КлючСессии, НастройкаОбмена);
		Если ТипыПисем = Неопределено Тогда
			Ошибка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьТипыПисемСбербанк()
	
	Ошибка = Ложь;
	ОтправитьЗапросТиповПисемСбербанк(
		НастройкаОбмена(), ИдентификаторОрганизации, УникальныйИдентификатор, ТикетОбменаСбербанк, Ошибка);
	
	Если Ошибка Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	ОжиданиеОперацийБЭДКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
	
	ПодключитьОбработчикОжидания("Подключаемый_ПолучитьТипыПисемСбербанк", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьТипыПисемЧерезТокенСбербанк()
	
	ЗапросТиповПисем = ЗапросТиповПисемСбербанк(ИдентификаторОрганизации);
	
	Если Не ЗначениеЗаполнено(ЗапросТиповПисем) Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиЗапросаТиповПисемЧерезТокенСбербанк", ЭтотОбъект);
	ОбменСБанкамиСлужебныйКлиент.ВыполнитьОтправкуДанныхЧерезТокенСбербанк(Оповещение, ЗапросТиповПисем);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиЗапросаСертификатовСбербанк(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Ответ) Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	ТикетОбменаСбербанк = Ответ;
	
	ОжиданиеОперацийБЭДКлиент.ИнициализироватьПараметрыОбработчикаОжидания(
		ПараметрыОбработчикаОжидания);

	ПодключитьОбработчикОжидания("Подключаемый_ПолучитьСертификатыССервераСбербанк", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиЗапросаТиповПисемЧерезТокенСбербанк(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Ответ) Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	ТикетОбменаСбербанк = Ответ;
	
	ОжиданиеОперацийБЭДКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
	
	ПодключитьОбработчикОжидания("Подключаемый_ПолучитьТипыПисемЧерезТокенСбербанк", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиЗапросаПерсональныхДанныхСбербанк(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Ответ) Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	ТикетОбменаСбербанк = Ответ;
	
	ОжиданиеОперацийБЭДКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);

	ПодключитьОбработчикОжидания("Подключаемый_ПолучитьЛичнуюИнформациюЧерезТокенСбербанк", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТестНастройкиОбменаСбербанк()
	
	Если Не ИспользуетсяКриптография Тогда
		Ошибка = Ложь;
		ОтправитьТестовыйЗапросСбербанк(НастройкаОбмена, ИдентификаторОрганизации, УникальныйИдентификатор, Ошибка);
		ОбработатьРезультатТестаНастройки(Не Ошибка);
	ИначеЕсли НастройкиОбменаСБанком <> Неопределено И ЗначениеЗаполнено(НастройкиОбменаСБанком.АдресСервера) Тогда
		Оповещение = Новый ОписаниеОповещения("ПодписатьТестовыйЗапросПослеОпределенияСертификатаСбербанк", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.ОпределитьСертификатПодписиСбербанк(Оповещение, ИмяВнешнегоМодуля, НастройкаОбмена);
	Иначе // настройки загружены из файла
		Оповещение = Новый ОписаниеОповещения("ТестНастройкиПослеУстановкиКаналаСбербанк", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.УстановитьСоединениеИАутентифицироватьсяНаСервереЧерезТокенСбербанк(Оповещение, НастройкаОбмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТестНастройкиПослеУстановкиКаналаСбербанк(Успех, ДополнительныеПараметры) Экспорт
	
	Если Не Успех Тогда
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;

	Оповещение = Новый ОписаниеОповещения("ПодписатьТестовыйЗапросПослеОпределенияСертификатаСбербанк", ЭтотОбъект);
	ОбменСБанкамиСлужебныйКлиент.ОпределитьСертификатПодписиСбербанк(Оповещение, ИмяВнешнегоМодуля, НастройкаОбмена);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьКаналПослеАутентификацииНаТокенеСбербанк(Успех, ДополнительныеПараметры) Экспорт
	
	Если Не Успех Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("АутентификацияНаСервереПослеУстановкиКаналаСбербанк", ЭтотОбъект);
	ОбменСБанкамиСлужебныйКлиент.УстановитьКаналНаТокенеСбербанк(Оповещение, ИмяВнешнегоМодуля);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьТестовыйЗапросПослеУстановкиПодписиСбербанк(ПодписьВерна, ДополнительныеПараметры) Экспорт
	
	Если Не ПодписьВерна Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Установленная подпись неверна.'"));
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
	
	ДанныеПодписи = Новый Структура;
	ДанныеПодписи.Вставить("ЭП", ДополнительныеПараметры.ДанныеПодписи);
	ДанныеПодписи.Вставить("СертификатПодписи", ДополнительныеПараметры.СертификатСсылка);
	
	Попытка
		ТестоваяСтрока = ОбменСБанкамиСлужебныйВызовСервера.ТекстЗапросаНочнойВыписки(НастройкаОбмена,
			ДополнительныеПараметры.ИдентификаторЗапроса, ИдентификаторОрганизации, ДанныеПодписи);
	Исключение
		ВидОперации = НСтр("ru = 'Формирование запроса новых документов'");
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(ВидОперации, 
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками, ТекстОшибки, ТекстСообщения);
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецПопытки;
	
	ТикетОбменаСбербанк = Неопределено;
	
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиТестовогоЗапросаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбменСБанкамиСлужебныйКлиент.ВыполнитьОтправкуДанныхЧерезТокенСбербанк(Оповещение, ТестоваяСтрока, НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьТестовыйЗапросПослеОпределенияСертификатаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ Результат = КодВозвратаДиалога.Отмена Тогда
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
	
	ИдентификаторЗапроса = Строка(Новый УникальныйИдентификатор);
	СтрокаПодписи = "ATTRIBUTES" + Символ(10) + "OrgId=" + ИдентификаторОрганизации + Символ(10) + "RequestId="
		+ ИдентификаторЗапроса;

	СтрокаПодписиBase64 = ОбменСБанкамиСлужебныйВызовСервера.СтрокаBase64БезBOM(СтрокаПодписи);
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СтрокаПодписиBase64", СтрокаПодписиBase64);
	ДополнительныеПараметры.Вставить("СертификатСсылка", Результат.СертификатСсылка);
	ДополнительныеПараметры.Вставить("ИдентификаторЗапроса", ИдентификаторЗапроса);
	
	Представление = НСтр("ru = 'Тестовый запрос'");
	
	РеквизитыСертификата = Новый Структура("ДанныеСертификата");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовСертификата(
		Результат.СертификатСсылка, РеквизитыСертификата);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПроверитьПодписьПослеУстановкиСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ОбменСБанкамиСлужебныйКлиент.ПодписатьДанныеСбербанк(ОписаниеОповещения, ИмяВнешнегоМодуля, СтрокаПодписиBase64,
		Результат.ИдентификаторСертификата, Представление, РеквизитыСертификата.ДанныеСертификата);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодписьПослеУстановкиСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ Результат = КодВозвратаДиалога.Отмена Тогда // возврат из формы сенсорного токена
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
	
	Если Результат.Успех Тогда
		
		ПараметрыСертификата = Новый Структура("ДанныеСертификата");
		
		ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовСертификата(
			ДополнительныеПараметры.СертификатСсылка, ПараметрыСертификата);
		
		ДополнительныеПараметры.Вставить("ДанныеПодписи", Результат.ЭП);
		
		Оповещение = Новый ОписаниеОповещения(
			"ОтправитьТестовыйЗапросПослеУстановкиПодписиСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		
		ОбменСБанкамиСлужебныйКлиент.ПроверитьПодписьНаТокенеСбербанк(Оповещение,
			ДополнительныеПараметры.СтрокаПодписиBase64, Результат.ЭП, ПараметрыСертификата.ДанныеСертификата,
			ИмяВнешнегоМодуля);
	Иначе
		Если Не ПустаяСтрока(Результат.ТекстОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ТекстОшибки);
		КонецЕсли;
		ОбработатьРезультатТестаНастройки(Ложь);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиТестовогоЗапросаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	ТикетОбменаСбербанк = Результат;
	
	Если ЗначениеЗаполнено(ТикетОбменаСбербанк) Тогда
		ОбработатьРезультатТестаНастройки(Истина);
	Иначе
		ОбработатьРезультатТестаНастройки(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеАутентификацииПоСертификатуСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;

	ЗапросПерсональныхДанных = ОбменСБанкамиСлужебныйВызовСервера.ЗапросПерсональныхДанныхОрганизацииСбербанк();
	
	Если ЗапросПерсональныхДанных = Неопределено Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеОтправкиЗапросаПерсональныхДанныхСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбменСБанкамиСлужебныйКлиент.ВыполнитьОтправкуДанныхЧерезТокенСбербанк(Оповещение, ЗапросПерсональныхДанных);
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьИдентификаторВКСбербанк()
	
	Результат = ЗаданиеПоПолучениюИдентификатораВКСбербанк(URLВК, УникальныйИдентификатор);
	
	ИдентификаторЗадания = Результат.ИдентификаторЗадания;
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	Оповещение = Новый ОписаниеОповещения("ПослеОпределенияИдентификатораВКСбербанк", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолучитьЛичнуюИнформациюСбербанк()
	
	Ошибка = Ложь; Ожидать = Ложь; ПараметрыОбмена = Неопределено;
	ПолучитьПерсональныеДанныеОрганизацииПоТикетуЗапроса(НастройкаОбмена(),
		УникальныйИдентификатор, ТикетОбменаСбербанк, ПараметрыОбмена, Ожидать, Ошибка);
	
	Если Ошибка Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
		
	Если Ожидать Тогда
		ОжиданиеОперацийБЭДКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПолучитьЛичнуюИнформациюСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	Иначе
		ИдентификаторОрганизации = ПараметрыОбмена.ИдентификаторОрганизации;
		НаименованиеБанка = ПараметрыОбмена.НаименованиеБанка;
		НомерПодразделенияБанка = ПараметрыОбмена.НомерПодразделенияБанка;
		СпособАутентификации = 1;
		ИспользуетсяКриптография = Ложь;
		ПолучитьТипыПисемСбербанк();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолучитьТипыПисемСбербанк()
	
	Ошибка = Ложь; Ожидать = Ложь;
	ПолучитьТипыПисемПоТикетуЗапроса(НастройкаОбмена(), ИдентификаторОрганизации, УникальныйИдентификатор,
		ТикетОбменаСбербанк, ТипыПисемСсылкаСбербанк, Ожидать, Ошибка);
	
	Если Ошибка Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
		
	Если Ожидать Тогда
		ОжиданиеОперацийБЭДКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПолучитьТипыПисемСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	Иначе
		ОбработатьРезультатПолученияНастроекССервераБанка(Истина);
		Элементы.СозданиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
		ПодключитьОбработчикОжидания("СоздатьНастройкуОбмена", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолучитьТипыПисемЧерезТокенСбербанк()
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияОтветаНаЗапросТиповПисемЧерезТокенСбербанк", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьСтатусЗапросаСбербанк(Оповещение, ИдентификаторОрганизации, ТикетОбменаСбербанк);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолучитьСертификатыССервераСбербанк()
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияОтветаНаЗапросСертификатовСбербанк", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьСтатусЗапросаСбербанк(Оповещение, ИдентификаторОрганизации, ТикетОбменаСбербанк);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСертификатыССервераСбербанка()
	
	ЗапросСертификатов = ОбменСБанкамиСлужебныйВызовСервера.ТекстЗапросаСертификатовСбербанк();

	Если Не ЗначениеЗаполнено(ЗапросСертификатов) Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиЗапросаСертификатовСбербанк", ЭтотОбъект);
	ОбменСБанкамиСлужебныйКлиент.ВыполнитьОтправкуДанныхЧерезТокенСбербанк(Оповещение, ЗапросСертификатов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОпределенияИдентификатораВКСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		ИмяВнешнегоМодуля = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		ВыполнитьАутентификациюПоЛогинуСбербанк();
	Иначе // Ошибка
		ВидОперации = НСтр("ru = 'Скачивание внешней компоненты.'");
		ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(ВидОперации, 
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками, Результат.ПодробноеПредставлениеОшибки,
			Результат.КраткоеПредставлениеОшибки, НастройкаОбмена());
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияОтветаНаЗапросСертификатовСбербанк(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = Неопределено Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	Если Ответ = "<!--NOT PROCESSED YET-->" ИЛИ Ответ = "<!--NOT_PROCESSED_YET-->" Тогда
		ОжиданиеОперацийБЭДКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПолучитьСертификатыССервераСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	ИначеЕсли Ответ = "<!--REQUEST NOT FOUND-->" ИЛИ Ответ = "<!--REQUEST_NOT_FOUND-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос не найден.
							|Повторите операцию позже.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
	ИначеЕсли Ответ = "<!--REQUESTID DUBLIC-->" ИЛИ Ответ = "<!--REQUESTID_DUBLIC-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос с таким идентификатором уже существует.
							|Повторите операцию позже.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
	Иначе
		СертификатыСбербанка = Новый Массив;
		Результат = ОбменСБанкамиСлужебныйВызовСервера.ПрочитатьСертификатыСбербанка(
			Ответ, УникальныйИдентификатор, Организация, ЗаблокированныеКриптопрофилиСбербанк);
		
		Если Результат = Неопределено Тогда
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
			Возврат;
		КонецЕсли;
		
		Если Не Результат.СертификатыБанка.Количество() Тогда
			ТекстСообщения = НСтр("ru = 'Не получен сертификат банка.
										|Повторите операцию позже или обратитесь в техподдержку банка.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
			Возврат;
		КонецЕсли;
		
		Если НЕ Результат.СертификатыКлиента.Количество() Тогда
			ТекстСообщения = НСтр("ru = 'Не получен ни один действующий клиентский сертификат из банка.
										|Повторите операцию позже или обратитесь в техподдержку банка.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
			Возврат;
		КонецЕсли;
		
		Для Каждого СертификатКлиента Из Результат.СертификатыКлиента Цикл
			НовСтрока = Сертификаты.Добавить();
			НовСтрока.Сертификат = СертификатКлиента;
		КонецЦикла;
		
		СертификатыСбербанка = Результат.СертификатыБанка;
		
		ПолучитьТипыПисемЧерезТокенСбербанк();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияОтветаНаЗапросТиповПисемЧерезТокенСбербанк(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = Неопределено Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	Если Ответ = "<!--NOT PROCESSED YET-->" ИЛИ Ответ = "<!--NOT_PROCESSED_YET-->" Тогда
		ОжиданиеОперацийБЭДКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПолучитьТипыПисемЧерезТокенСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	ИначеЕсли Ответ = "<!--REQUEST NOT FOUND-->" ИЛИ Ответ = "<!--REQUEST_NOT_FOUND-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос не найден.
							|Повторите операцию позже.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
	ИначеЕсли Ответ = "<!--REQUESTID DUBLIC-->" ИЛИ Ответ = "<!--REQUESTID_DUBLIC-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос с таким идентификатором уже существует.
							|Повторите операцию позже.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
	Иначе
		ТипыПисемСсылкаСбербанк = ОбменСБанкамиСлужебныйВызовСервера.ТипыПисемСбербанк(
			Ответ, УникальныйИдентификатор, НастройкаОбмена());
		Если ТипыПисемСсылкаСбербанк = Неопределено Тогда
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
			Возврат;
		КонецЕсли;
		
		ОбработатьРезультатПолученияНастроекССервераБанка(Истина);
		Элементы.СозданиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
		#Если ВебКлиент Тогда
			ОбновитьОтображениеДанных();
		#КонецЕсли
		ПодключитьОбработчикОжидания("СоздатьНастройкуОбмена", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияСтатусаЗапросаСбербанк(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = Неопределено Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	Если Ответ = "<!--NOT PROCESSED YET-->" ИЛИ Ответ = "<!--NOT_PROCESSED_YET-->" Тогда
		ОжиданиеОперацийБЭДКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПолучитьЛичнуюИнформациюЧерезТокенСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	ИначеЕсли Ответ = "<!--REQUEST NOT FOUND-->" ИЛИ Ответ = "<!--REQUEST_NOT_FOUND-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос не найден.
							|Повторите операцию позже.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
		Элементы.ПолучениеНастроекОбменаКартинка.Картинка = БиблиотекаКартинок.Ошибка32;
		Элементы.ДекорацияНеПодключено.Видимость = Истина;
	ИначеЕсли Ответ = "<!--REQUESTID DUBLIC-->" ИЛИ Ответ = "<!--REQUESTID_DUBLIC-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос с таким идентификатором уже существует.
							|Повторите операцию позже.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
		Элементы.ПолучениеНастроекОбменаКартинка.Картинка = БиблиотекаКартинок.Ошибка32;
		Элементы.ДекорацияНеПодключено.Видимость = Истина;
	Иначе
		ПараметрыОбменаСбербанк = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыОбменаСбербанк(Ответ);
		Если Не ЗначениеЗаполнено(ПараметрыОбменаСбербанк) Тогда
			ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
			Возврат;
		КонецЕсли;
		ИдентификаторОрганизации = ПараметрыОбменаСбербанк.ИдентификаторОрганизации;
		НаименованиеБанка = ПараметрыОбменаСбербанк.НаименованиеБанка;
		НомерПодразделенияБанка = ПараметрыОбменаСбербанк.НомерПодразделенияБанка;
		ЗаблокированныеКриптопрофилиСбербанк = ПараметрыОбменаСбербанк.ЗаблокированныеКриптопрофили;
		ПолучитьСертификатыССервераСбербанка();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтправитьТестовыйЗапросСбербанк(Знач НастройкаОбмена, Знач ИдентификаторОрганизации, Знач УникальныйИдентификатор, Ошибка)
	
	ИдентификаторЗапроса = Строка(Новый УникальныйИдентификатор);
	
	ТестоваяСтрока = ОбменСБанкамиСлужебныйВызовСервера.ТекстЗапросаНочнойВыписки(
		НастройкаОбмена, ИдентификаторЗапроса, ИдентификаторОрганизации);
		
	Данные = Новый Массив;
	Данные.Добавить(ТестоваяСтрока);
	Тикеты = Неопределено; ТребуетсяАутентификация = Ложь;
	ТекущаяСессия = ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(УникальныйИдентификатор);

	Попытка
		ОбменСБанкамиСлужебный.ОтправитьДанныеВСбербанк(НастройкаОбмена, ТекущаяСессия.ИдентификаторСессии, Данные, Тикеты,
			ТребуетсяАутентификация);
	Исключение
		ВидОперации = НСтр("ru = 'Отправка тестового запроса в банк'");
		ПодробныйТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(ВидОперации, 
				ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками, ПодробныйТекстОшибки,
				ТекстСообщения, НастройкаОбмена);
		Ошибка = Истина;
	КонецПопытки;
	
	Если ТребуетсяАутентификация Тогда
		ТекстСообщения = НСтр("ru = 'Требуется аутентификация'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Ошибка = Истина;
	КонецЕсли;

	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПолучитьПерсональныеДанныеОрганизацииПоТикетуЗапроса(Знач НастройкаОбмена, Знач КлючСессии, Знач Тикет, ПараметрыОбмена, Ожидать, Ошибка)
	
	ВидОперации = НСтр("ru = 'Формирование запроса статуса документа'");

	Попытка
		
		МассивТикетов = Новый Массив;
		МассивТикетов.Добавить(Тикет);
		ИдентификаторСессии = ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(КлючСессии).ИдентификаторСессии;
		МассивОтветов = Неопределено;
		
		ОбменСБанкамиСлужебный.ПолучитьОтветыПоТикетамСбербанк(НастройкаОбмена, МассивТикетов, ИдентификаторСессии,
			"00000000-0000-0000-0000-000000000000", МассивОтветов);
		
	Исключение
		
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ШаблонСообщения = Нстр("ru = 'При получении статуса запроса персональных данных произошла ошибка:
								|%1'");
		ТекстОшибки = СтрШаблон(ШаблонСообщения, КраткаяИнформация);
		ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(ВидОперации, 
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками, ПодробноеПредставлениеОшибки, ТекстОшибки);
		Ошибка = Истина;
		Возврат;
		
	КонецПопытки;
	
	Ответ = МассивОтветов.Получить(0);
	
	Если Ответ = "<!--NOT PROCESSED YET-->" ИЛИ Ответ = "<!--NOT_PROCESSED_YET-->" Тогда
		Ожидать = Истина;
	ИначеЕсли Ответ = "<!--REQUEST NOT FOUND-->" ИЛИ Ответ = "<!--REQUEST_NOT_FOUND-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос не найден.
							|Повторите операцию позже.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , , , Ошибка);
	ИначеЕсли Ответ = "<!--REQUESTID DUBLIC-->" ИЛИ Ответ = "<!--REQUESTID_DUBLIC-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос с таким идентификатором уже существует.
							|Повторите операцию позже.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , , , Ошибка);
	Иначе
		ПараметрыОбмена = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыОбменаСбербанк(Ответ);
		Если ПараметрыОбмена = Неопределено Тогда
			Ошибка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолучитьЛичнуюИнформациюЧерезТокенСбербанк()
	
	ИдентификаторОрганизации = "00000000-0000-0000-0000-000000000000";
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияСтатусаЗапросаСбербанк", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьСтатусЗапросаСбербанк(Оповещение, ИдентификаторОрганизации, ТикетОбменаСбербанк);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтправитьЗапросПерсональныхДанныхСбербанк(Знач НастройкаОбмена, Знач КлючСессии, ТикетОбменаСбербанк, Ошибка)
	
	ДанныеЗапрос = ОбменСБанкамиСлужебныйВызовСервера.ЗапросПерсональныхДанныхОрганизацииСбербанк();
	
	Если ДанныеЗапрос = Неопределено Тогда
		Ошибка = Истина;
		Возврат;
	КонецЕсли;
	
	МассивДанных = Новый Массив;
	МассивДанных.Добавить(ДанныеЗапрос);
	МассивТикетов = Неопределено;
	ИдентификаторСессии = ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(КлючСессии).ИдентификаторСессии;
	
	Попытка
		ОбменСБанкамиСлужебный.ОтправитьДанныеВСбербанк(НастройкаОбмена, ИдентификаторСессии, МассивДанных, МассивТикетов);
	Исключение
		ВидОперации = НСтр("ru = 'Получение настроек обмена из банка'");
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(ВидОперации, 
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками, ТекстОшибки, ТекстСообщения);
		Ошибка = Истина;
		Возврат
	КонецПопытки;
	
	Если ЗначениеЗаполнено(МассивТикетов) Тогда
		ТикетОбменаСбербанк = МассивТикетов.Получить(0);
	Иначе
		ТекстСообщения = НСтр("ru = 'Получен некорректный ответ из банка в ответ на запрос персональных данных.
									|Повторите попытку подключения или обратитесь в техническую поддержку.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Ошибка);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтправитьЗапросТиповПисемСбербанк(Знач НастройкаОбмена, Знач ИдентификаторОрганизации, Знач КлючСессии, ТикетОбменаСбербанк, Ошибка)
	
	ДанныеЗапрос = ЗапросТиповПисемСбербанк(ИдентификаторОрганизации);
	
	Если ДанныеЗапрос = Неопределено Тогда
		Ошибка = Истина;
		Возврат;
	КонецЕсли;
	
	МассивДанных = Новый Массив;
	МассивДанных.Добавить(ДанныеЗапрос);
	МассивТикетов = Неопределено;
	ИдентификаторСессии = ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(КлючСессии).ИдентификаторСессии;
		
	Попытка
		ОбменСБанкамиСлужебный.ОтправитьДанныеВСбербанк(НастройкаОбмена, ИдентификаторСессии, МассивДанных, МассивТикетов);
		ТикетОбменаСбербанк = МассивТикетов.Получить(0);
	Исключение
		ВидОперации = НСтр("ru = 'Получение настроек обмена из банка'");
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(ВидОперации, 
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками, ТекстОшибки, ТекстСообщения);
		Ошибка = Истина;
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапросТиповПисемСбербанк(Знач ИдентификаторОрганизации)
	
	Операция = НСтр("ru = 'Формирование запроса типов писем.'");
	
	ТекстОшибки = "";
	Request = ОбменСБанкамиСлужебный.RequestСбербанк(ИдентификаторОрганизации, ТекстОшибки);
	
	Dict = ОбменСБанкамиСлужебный.ОбъектТипаCML(ФабрикаXDTO, "Dict", "http://bssys.com/upg/request");
	ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(Dict, "dictId", "LetterType", , ТекстОшибки);
	Request.Dict.Добавить(Dict);
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(Операция, 
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками, ТекстОшибки, ТекстОшибки);
		Возврат Неопределено;
	КонецЕсли;

	Попытка
		Request.Проверить();
	Исключение
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(Операция, 
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками, ТекстОшибки, ТекстСообщения);
		Возврат Неопределено;
	КонецПопытки;
	
	Запись = Новый ЗаписьXML;
	Запись.УстановитьСтроку("UTF-8");
	Запись.ЗаписатьОбъявлениеXML();
	ФабрикаXDTO.ЗаписатьXML(Запись, Request);
	ТекстЗапроса = Запись.Закрыть();
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьАутентификациюПоЛогинуСбербанк()

	НастройкаОбмена = НастройкаОбмена();
	
	Если НЕ ЗначениеЗаполнено(НастройкаОбмена) Тогда
		ДополнительныеПараметры = РеквизитыНовойНастройкиОбмена(); 
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеБазовойАутентификацииСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ЛогинПароль = Новый Структура("Логин, Пароль", Логин, Пароль);
	ТекстПояснения = НСтр("ru = 'Для прямого обмена с банком ""ПАО СБЕРБАНК"" требуется
								|внешняя компонента ""VpnKey-TLS для 1С:Предприятия 8"".'");
	ОбменСБанкамиСлужебныйКлиент.ВыполнитьАутентификациюПоЛогинуСбербанк(
		Оповещение, ИмяВнешнегоМодуля, УникальныйИдентификатор, НастройкаОбмена, ЛогинПароль, ТекстПояснения, Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбменСИспользованиемДополнительнойОбработки

&НаКлиенте
Процедура ТестНастройкиОбменаЧерезДополнительнуюОбработку()
	
	ОтпечатокBase64 = ОбменСБанкамиСлужебныйВызовСервера.СтрокаBase64БезBOM(Отпечаток);
	ДвоичныеДанные = Base64Значение(ОтпечатокBase64);
	МассивПодписи = Новый Массив;
	МассивПодписи.Добавить(ДвоичныеДанные);
	Попытка
		МассивПодписей = ПодключаемыйМодуль.Подписать(СертификатXML, МассивПодписи);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка установки подписи.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Установка подписи'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(Операция, 
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками, ПодробноеПредставлениеОшибки, ,НастройкаОбмена);
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецПопытки;

	Попытка
		ДопПараметры = Новый Структура("ИдентификаторХранилища", ИдентификаторХранилища);
		ПодключаемыйМодуль.ПроверитьПодпись(СертификатXML, ДвоичныеДанные, МассивПодписей[0], ДопПараметры);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка проверки подписи.
								|Код ошибки: ДО-%1
								|%2'");
		ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Проверка подписи'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(Операция, 
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками, ПодробноеПредставлениеОшибки, ,НастройкаОбмена);
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецПопытки;
	
	Попытка
		ПодключаемыйМодуль.ОтправитьЗапрос(СертификатXML, 1);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка отправки тестового запроса.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Отправка тестового запроса'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(Операция, 
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками, ПодробноеПредставлениеОшибки, ,НастройкаОбмена);
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецПопытки;
	
	ОбработатьРезультатТестаНастройки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораКлючаЧерезДополнительнуюОбработку(ВыбранныйЭлемент, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторХранилища = ВыбранныйЭлемент.Значение;
	
	ТребуетсяУстановкаPINКода = ОбменСБанкамиСлужебныйКлиент.НеобходимаУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
		ПодключаемыйМодуль, ИдентификаторХранилища);

	Если ТребуетсяУстановкаPINКода = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("ИдентификаторХранилища", ИдентификаторХранилища);
	Если ТребуетсяУстановкаPINКода Тогда
		ОбработчикВводаPIN = Новый ОписаниеОповещения(
			"ОбработатьВводPINЧерезДополнительнуюОбработку", ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросPINКода", ДополнительныеПараметры, , , , , ОбработчикВводаPIN);
	Иначе
		ПослеУстановкиPINЧерезДополнительнуюОбработку(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВводPINЧерезДополнительнуюОбработку(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	PINУстановлен = ОбменСБанкамиСлужебныйКлиент.УстановитьPINКодХранилищаЧерезДополнительнуюОбработку(
		ПодключаемыйМодуль, ДополнительныеПараметры.ИдентификаторХранилища, Результат);

	Если PINУстановлен Тогда
		ПослеУстановкиPINЧерезДополнительнуюОбработку(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеУстановкиPINЧерезДополнительнуюОбработку(ДополнительныеПараметры)
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеВыбораСертификатаЧерезДополнительнуюОбработку", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьСертификатЧерезДополнительнуюОбработку(
		Оповещение, ПодключаемыйМодуль, ДополнительныеПараметры.ИдентификаторХранилища);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораСертификатаЧерезДополнительнуюОбработку(ДанныеСертификата, ДополнительныеПараметры) Экспорт
	
	Если ДанныеСертификата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СертификатXML = ДанныеСертификата.СертификатXML;
	Отпечаток = ДанныеСертификата.Отпечаток;
	
	НовыйСертификат = НайтиСоздатьСертификатВнешнегоМодуля(СертификатXML, Организация, ПрограммаБанка, ДанныеСертификата);
	
	Если Сертификаты.НайтиСтроки(Новый Структура("Сертификат", НовыйСертификат)).Количество() Тогда
		ТекстСообщения = НСтр("ru = 'Выбранный сертификат уже есть в списке.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	Иначе
		НовСтрока = Сертификаты.Добавить();
		НовСтрока.Сертификат = НовыйСертификат;
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПодключитьДополнительнуюОбработку(Знач ВнешняяОбработка)
	
	Возврат ДополнительныеОтчетыИОбработки.ПодключитьВнешнююОбработку(ВнешняяОбработка);
		
КонецФункции

&НаКлиенте
Процедура НачатьИнициализациюДополнительнойОбработки(Оповещение)

	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ВремФайл = ПолучитьИмяВременногоФайла("epf");
		ДвоичныеДанныеОбработки = ОбменСБанкамиСлужебныйВызовСервера.АдресДополнительнойОбработки(ДополнительнаяОбработка);
		ДвоичныеДанныеОбработки.Записать(ВремФайл);
		ПодключаемыйМодуль = ВнешниеОбработки.ПолучитьФорму(ВремФайл);
		ИмяВнешнегоМодуля = ПодключаемыйМодуль.ЭтотОбъект;
		ПозицияТочки = СтрНайти(ИмяВнешнегоМодуля, ".");
		ИмяВнешнегоМодуля = Сред(ИмяВнешнегоМодуля, ПозицияТочки + 1);
	#Иначе
		
		ИмяВнешнегоМодуля = ПодключитьДополнительнуюОбработку(ДополнительнаяОбработка);
	
		Если ИмяВнешнегоМодуля = Неопределено Тогда
			ВыполнитьОбработкуОповещения(Оповещение, Ложь);
			Возврат;
		КонецЕсли;

		ИмяФормыОбработки = "ВнешняяОбработка." + ИмяВнешнегоМодуля + ".Форма";
		Попытка
			ПараметрыФормыОбработки = Новый Структура("РежимЭДО", Истина);
			ПодключаемыйМодуль = ПолучитьФорму(ИмяФормыОбработки, ПараметрыФормыОбработки, , УникальныйИдентификатор);
		Исключение
			Операция = НСтр("ru = 'Получение формы внешней обработки.'");
			ТекстСообщения = НСтр("ru = 'Не удалось подключить указанный внешний модуль'");
			ПодробныйТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(Операция, 
				ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками, ПодробныйТекстОшибки);
			ВыполнитьОбработкуОповещения(Оповещение, Ложь);
			Возврат;
		КонецПопытки;
	#КонецЕсли

	ИнициализироватьДополнительнуюОбработкуПослеПодключения(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьДополнительнуюОбработкуПослеПодключения(Оповещение)
	
	Попытка
		ОписаниеОбработки = ПодключаемыйМодуль.ОписаниеОбработки();
		ВерсияAPI = ОписаниеОбработки.ВерсияAPI;
	Исключение
		ВерсияAPI = 1;
	КонецПопытки;
	
	Если ВерсияAPI = 1 Тогда
		Попытка
			ПодключаемыйМодуль.Инициализировать();
			ВыполнитьОбработкуОповещения(Оповещение, Истина);
		Исключение
			ШаблонОшибки = НСтр("ru = 'Ошибка инициализации внешнего модуля.
										|Код ошибки: ДО-%1
										|%2'");
			ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
			ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
			Операция = НСтр("ru = 'Инициализация внешней обработки'");
			ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(Операция, 
				ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками, ПодробноеПредставлениеОшибки);
			ВыполнитьОбработкуОповещения(Оповещение, Ложь);
		КонецПопытки;
	Иначе
		Попытка
			АдресКомпонентыДополнительнойОбработки = "";
			ИнициализацияВыполнена = ПодключаемыйМодуль.НачатьИнициализацию(АдресКомпонентыДополнительнойОбработки);
		Исключение
			ШаблонОшибки = НСтр("ru = 'Ошибка инициализации внешнего модуля.
										|Код ошибки: ДО-%1
										|%2'");
			ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
			ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
			Операция = НСтр("ru = 'Инициализация внешней обработки'");
			ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(Операция, 
				ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками, ПодробноеПредставлениеОшибки);
			ВыполнитьОбработкуОповещения(Оповещение, Ложь);
			Возврат;
		КонецПопытки;
		
		Если ИнициализацияВыполнена Тогда
			ЗавершитьИнициализациюВнешнейКомпонентыДополнительнойОбработки(Оповещение);
		Иначе
			ОбменСБанкамиСлужебныйКлиент.УстановитьВнешнююКомпонентуДляОбменаЧерезОбработку(
				Оповещение, АдресКомпонентыДополнительнойОбработки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьИнициализациюВнешнейКомпонентыДополнительнойОбработки(Оповещение)
	
	Попытка
		ПодключаемыйМодуль.ЗавершитьИнициализацию();
		ВыполнитьОбработкуОповещения(Оповещение, Истина);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка завершения инициализации внешнего модуля.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Завершение инициализации внешней обработки'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(Операция, 
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками, ПодробноеПредставлениеОшибки);
		ВыполнитьОбработкуОповещения(Оповещение, Ложь);
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеИнициализацииДополнительнойОбработки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
		ХронологияПереключенияСтраниц.Добавить(Элементы.СтраницаЗапросИспользованияВнешнегоМодуля);
		СменитьСтраницуШаги(Элементы.СтраницаСертификаты);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбменСИспользованиемВнешнейКомпоненты

&НаСервереБезКонтекста
Функция ИдентификаторВнешнейКомпоненты(Знач ВнешняяКомпонента)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВнешняяКомпонента, "Идентификатор");
	
КонецФункции

&НаКлиенте
Процедура ПослеЗагрузкиВКИзФайла(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Загружена Тогда
		КомпонентаОбработка = 0;
		ИмяВнешнегоМодуля = Результат.Идентификатор;
		ШаблонПредставленияВК =  НСтр("ru = '%1. Версия %2'");
		ПредставлениеВнешнейКомпоненты = СтрШаблон(ШаблонПредставленияВК, Результат.Наименование, Результат.Версия);
		Элементы.ПредставлениеВнешнейКомпоненты.Видимость = Истина;
		Элементы.ДекорацияЗагрузитьВКИзФайла.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьВКПослеОпределенияИдентификатора(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	ИмяВнешнегоМодуля = Результат.Идентификатор;
	
	ПолучитьНастройкиОбменаССервераБанкаЧерезВК();
	
КонецПроцедуры

&НаКлиенте
Процедура ТестНастройкиОбменаВК()
	
	// Удаляем признак прерывания процесса
	ОбменСБанкамиСлужебныйКлиент.ПроцессПрерван(НастройкаОбмена);

	ПараметрыСоединения = Неопределено;
	Если СпособСозданияНастройки <> 1 Тогда // аутентификация на токене уже выполнена
		ПараметрыСоединения = Новый Структура;
		ПараметрыСоединения.Вставить("ИдентификаторХранилища", ИдентификаторХранилища);
		ПараметрыСоединения.Вставить("Отпечаток", Отпечаток);
		ПараметрыСоединения.Вставить("СертификатBase64", Base64Строка(СертификатВК));
		ПараметрыСоединения.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеТестаНастройкиВК", ЭтотОбъект);
	ОбменСБанкамиСлужебныйКлиент.ТестНастройкиОбменаВК(Оповещение, НастройкаОбмена, ПараметрыСоединения);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьСертификатыПослеПодключенияВК(Результат, ДополнительныеПараметры) Экспорт

	Если НЕ Результат.Подключено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ОписаниеОшибки);
		ОбработатьРезультатСозданияНастройкиОбмена(Ложь);
		Возврат;
	КонецЕсли;
	
	ПодключаемыйМодуль = Результат.ПодключаемыйМодуль;
	
	СохранитьОчереднойСертификатВИнформационнойБазеВК(ДополнительныеПараметры.ДвоичныеДанныеСертификатовИзФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьСертификатПослеПолученияДанныхКлючаВК(СтруктураСертификата, ДополнительныеПараметры) Экспорт
	
	Если СтруктураСертификата = Неопределено Тогда
		ОбработатьРезультатСозданияНастройкиОбмена(Ложь);
		Возврат;
	КонецЕсли;
	
	Успех = ДобавитьСертификатВНастройкуОбменаВК(
		НастройкаОбмена, ДополнительныеПараметры.СертификатBase64, СтруктураСертификата);
		
	Если Успех Тогда
		СохранитьОчереднойСертификатВИнформационнойБазеВК(ДополнительныеПараметры.ДанныеСертификатов);
	Иначе
		ОбработатьРезультатСозданияНастройкиОбмена(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьОчереднойСертификатВИнформационнойБазеВК(ДанныеСертификатов)
	
	Если ДанныеСертификатов.Количество() = 0 Тогда
		ОбработатьРезультатСозданияНастройкиОбмена(Истина);
		Элементы.ТестированиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
		#Если ВебКлиент Тогда
			ОбновитьОтображениеДанных();
		#КонецЕсли
		ПодключитьОбработчикОжидания("ТестНастройкиОбменаВК", 0.1, Истина);
		Возврат;
	КонецЕсли;
	
	ДвоичныеДанныеСертификата = ДанныеСертификатов.Получить(0);
	СертификатBase64 = Base64Строка(ДвоичныеДанныеСертификата);
	ДанныеСертификатов.Удалить(0);
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("СертификатBase64", СертификатBase64);
	ДополнительныеПараметры.Вставить("ДанныеСертификатов", ДанныеСертификатов);
	
	Обработчик = Новый ОписаниеОповещения(
		"СохранитьСертификатПослеПолученияДанныхКлючаВК", ЭтотОбъект, ДополнительныеПараметры);
		
	ОбменСБанкамиСлужебныйКлиент.ПолучитьДанныеКлючаЧерезВК(Обработчик, ПодключаемыйМодуль, СертификатBase64);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияНастроекИзБанкаВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	АдресФайлаНастроекОбмена = ПоместитьВоВременноеХранилище(Base64Значение(РезультатВызова), УникальныйИдентификатор);
	
	УчастникиОбменаИзФайла = УчастникиОбменаИзФайла(АдресФайлаНастроекОбмена, Организация, , Банк);
		
	Если УчастникиОбменаИзФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если УчастникиОбменаИзФайла.Организация <> Организация Тогда
		ТекстСообщения = НСтр("ru = 'Указана неверная учетная запись.
									|Требуются настройки для организации: %1
									|Получены настройки для организации: %2'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Организация, УчастникиОбменаИзФайла.Организация);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;

	Если УчастникиОбменаИзФайла.Банк <> Банк Тогда
		ТекстСообщения = НСтр("ru = 'Подключено неверное аппаратное устройство.
									|Требуются настройки для банка: %1
									|Получены настройки для банка: %2'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Банк, УчастникиОбменаИзФайла.Банк);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	ОбработатьРезультатПолученияНастроекССервераБанка(Истина);
	Элементы.СозданиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
	#Если ВебКлиент Тогда
		ОбновитьОтображениеДанных();
	#КонецЕсли
	ПодключитьОбработчикОжидания("СоздатьНастройкуОбмена", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеИнициализацииВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат);
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ЗагрузитьНастройкиОбменаССервераБанкаПослеПолученияДанныхСертификата", ЭтотОбъект);
		
	ПараметрыСоединения = Новый Структура();
	ПараметрыСоединения.Вставить("ИдентификаторОрганизации", "");
	ПараметрыСоединения.Вставить("БИК", БИКБанка(Банк));
	ПараметрыСоединения.Вставить("КлючУникальности", "");
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьДанныеСертификатаСТокенаВК(Оповещение, ПодключаемыйМодуль, ПараметрыСоединения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьНастройкиОбменаССервераБанкаЧерезВК()
	
	Оповещение = Новый ОписаниеОповещения("ИнициализироватьПослеПодключенияВК", ЭтотОбъект);
	ОбменСБанкамиСлужебныйКлиент.ПодключитьКомпоненту(Оповещение, ИмяВнешнегоМодуля);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОшибкуПолученияНастроекИзБанкаВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстСообщения = НСтр("ru = 'При получении настроек обмена из банка произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Получение настроек обмена с сервера банка.'");
	
	Оповещение = Новый ОписаниеОповещения("СообщитьОшибкуПользователю", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьИнформациюОбОшибкеВК(
		Оповещение, ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
		
	ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиСоздатьСертификатВнешнегоМодуля(Знач ДанныеСертификата, Знач Организация, Знач ПрограммаБанка, Знач СвойстваСертификата)
	
	Возврат ОбменСБанкамиСлужебный.НайтиСоздатьСертификатВнешнегоМодуля(
		ДанныеСертификата, Организация, ПрограммаБанка, СвойстваСертификата);
	
КонецФункции

&НаКлиенте
Процедура ПослеТестаНастройкиВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат);
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
	
	Если Результат = Неопределено ИЛИ Результат = Ложь Тогда
		ОбработатьРезультатТестаНастройки(Ложь);
		Возврат;
	КонецЕсли;
	
	ОбработатьРезультатТестаНастройки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьПослеПодключенияВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Подключено Тогда
		Если ЗначениеЗаполнено(Результат.ОписаниеОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ОписаниеОшибки);
		КонецЕсли;
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	ПодключаемыйМодуль = Результат.ПодключаемыйМодуль;
	
	Оповещение = Новый ОписаниеОповещения("ПослеИнициализацииВК", ЭтотОбъект);

	ОбменСБанкамиСлужебныйКлиент.ИнициализироватьВК(Оповещение, ПодключаемыйМодуль);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНастройкиОбменаССервераБанкаПослеПолученияДанныхСертификата(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат);
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	Если Результат = Неопределено Тогда
		ОбработатьРезультатПолученияНастроекССервераБанка(Ложь);
		Возврат;
	КонецЕсли;
	
	// Кэширование для использование в тесте настроек.
	СертификатВК = Base64Значение(Результат.СертификатBase64);
	Отпечаток = Результат.Отпечаток;
	КомуВыданСертификатВК = Результат.ВладелецФИО;
	ИдентификаторХранилища = Результат.ИдентификаторХранилища;
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияНастроекИзБанкаВК", ЭтотОбъект, ДополнительныеПараметры,
		"ОбработатьОшибкуПолученияНастроекИзБанкаВК", ЭтотОбъект);
	
	ПодключаемыйМодуль.НачатьВызовНастройкиЭДО(Оповещение);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДобавитьСертификатВНастройкуОбменаВК(Знач НастройкаОбмена, Знач СертификатBase64, Знач СтруктураСертификата)
	
	Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаОбмена, "Организация");
	
	НовыйСертификат = ОбменСБанкамиСлужебный.НайтиСоздатьСертификатВнешнегоМодуля(
		СертификатBase64, Организация, Перечисления.ПрограммыБанка.ОбменЧерезВК, СтруктураСертификата);
		
	Попытка
		НастройкаОбменаОбъект = НастройкаОбмена.ПолучитьОбъект();
		НастройкаОбменаОбъект.Прочитать();
		
		НовСтрока = НастройкаОбменаОбъект.СертификатыПодписейОрганизации.Добавить();
		НовСтрока.СертификатЭП = НовыйСертификат;
	
		НастройкаОбменаОбъект.Записать();
	Исключение
		Операция = НСтр("ru = 'Добавление сертификата в настройку обмена'");
		ШаблонСообщения = НСтр("ru = 'При добавлении сертификата в настройку обмена произошла ошибка.
									|%1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(Операция, 
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками, ТекстОшибки, ТекстСообщения, НастройкаОбмена);
		Возврат Ложь;
	КонецПопытки;
	
	НастройкаОбменаОбъект.Разблокировать();
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область УниверсальныеМетоды

&НаКлиенте
Функция ВведенЛогинИПароль()
	
	ЕстьОшибка = Ложь;
	Если НЕ ЗначениеЗаполнено(Логин)Тогда
		ТекстСообщения = НСтр("ru = 'Не указан логин для аутентификации на сервере банка'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Логин", , ЕстьОшибка);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Пароль)Тогда
		ТекстСообщения = НСтр("ru = 'Не указан пароль'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Пароль", , ЕстьОшибка);
	КонецЕсли;
	
	Возврат НЕ ЕстьОшибка;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ПрерватьДлительнуюОперацию(ИдентификаторЗадания)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяВнешнегоМодуля(Знач НастройкаОбмена)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаОбмена, "ИмяВнешнегоМодуля");
	
КонецФункции

&НаСервереБезКонтекста
Функция УчастникиОбменаИзФайла(Знач АдресФайла, Знач Организация, Знач ЛокальныйФайл = Ложь, БанкФормы = Неопределено)
	
	Попытка
		ЭД = ОбменСБанкамиСлужебный.XDTOДанныеФайлаНастроек(АдресФайла);
	Исключение
		ОбщегоНазначения.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Неопределено;
	КонецПопытки;
	
	ЭтоНастройкиСбербанка = ЭД.Свойства().Получить("Data") = Неопределено;
	
	ИННОрганизацииФайла = ?(ЭтоНастройкиСбербанка, ЗначениеСвойстваXDTO(ЭД, "Customer.INN"), 
		ЗначениеСвойстваXDTO(ЭД, "Recipient.inn"));
	
	ИННОрганизацииБазы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, 
							ОбменСБанкамиСлужебный.ИмяПрикладногоРеквизита("ИННОрганизации"));
	
	Если ИННОрганизацииБазы = ИННОрганизацииФайла Тогда
		СсылкаНаОрганизацию = Организация;
	Иначе
		СтруктураПоискаОрганизации = Новый Структура;
		
		Если ЗначениеЗаполнено(ИННОрганизацииФайла) Тогда
			СтруктураПоискаОрганизации.Вставить("ИНН", ИННОрганизацииФайла);
		КонецЕсли;
		
		КППОрганизации = ?(ЭтоНастройкиСбербанка, ЗначениеСвойстваXDTO(ЭД, "Customer.KPP"), 
			ЗначениеСвойстваXDTO(ЭД, "Recipient.kpp"));
		Если КППОрганизации = "000000000" Тогда // Для Сбербанка
			КППОрганизации = "0";
		КонецЕсли;
		Если ЗначениеЗаполнено(КППОрганизации) Тогда
			СтруктураПоискаОрганизации.Вставить("КПП", КППОрганизации);
		КонецЕсли;
		НаименованиеОрганизации = ?(ЭтоНастройкиСбербанка, ЗначениеСвойстваXDTO(ЭД, "Customer.Name"), 
			ЗначениеСвойстваXDTO(ЭД, "Recipient.name"));
		Если ЗначениеЗаполнено(НаименованиеОрганизации) Тогда
			СтруктураПоискаОрганизации.Вставить("Наименование", НаименованиеОрганизации);
		КонецЕсли;
		
		Если СтруктураПоискаОрганизации.Количество() > 0 Тогда
			СсылкаНаОрганизацию = ОбщегоНазначенияБЭД.НайтиСсылку("Организации", , СтруктураПоискаОрганизации);
		ИначеЕсли НЕ ЗначениеЗаполнено(Организация) Тогда
			ТекстСообщения = НСтр("ru = 'В файле настроек нет информации об организации.
			|Обратитесь в свой банк.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			Возврат Неопределено;
		Иначе
			СсылкаНаОрганизацию = Организация;
		КонецЕсли;	
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СсылкаНаОрганизацию) Тогда
		ТекстСообщения = НСтр("ru = 'Файл содержит настройки для неизвестной организации: %1%2%3'");
		Если СтруктураПоискаОрганизации.Свойство("ИНН") Тогда
			ТекстИНН = Символы.ПС + НСтр("ru = 'ИНН: %1'");
			ТекстИНН = СтрШаблон(ТекстИНН, СтруктураПоискаОрганизации.ИНН);
		Иначе
			ТекстИНН = "";
		КонецЕсли;
		Если СтруктураПоискаОрганизации.Свойство("КПП") Тогда
			ТекстКПП = Символы.ПС + НСтр("ru = 'КПП: %1'");
			ТекстКПП = СтрШаблон(ТекстКПП, СтруктураПоискаОрганизации.КПП);
		Иначе
			ТекстКПП = "";
		КонецЕсли;
		Если СтруктураПоискаОрганизации.Свойство("Наименование") Тогда
			ТекстНаименование = Символы.ПС + НСтр("ru = 'Наименование: %1'");
			ТекстНаименование = СтрШаблон(
				ТекстНаименование, СтруктураПоискаОрганизации.Наименование);
		Иначе
			ТекстНаименование = "";
		КонецЕсли;
		ТекстСообщения = СтрШаблон(
			ТекстСообщения, ТекстИНН, ТекстКПП, ТекстНаименование);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли;

	НаименованиеБанка = "";
	СтруктураПоискаБанка = Новый Структура;
	БИК = ?(ЭтоНастройкиСбербанка, ЗначениеСвойстваXDTO(ЭД, "Bank.BIC"), ЗначениеСвойстваXDTO(ЭД, "Sender.bic"));
	СтруктураПоискаБанка.Вставить("Код", БИК);
	НаименованиеБанка = ?(ЭтоНастройкиСбербанка, ЗначениеСвойстваXDTO(ЭД, "Bank.Name"), 
		ЗначениеСвойстваXDTO(ЭД, "Sender.name"));
	СтруктураПоискаБанка.Вставить("Наименование", НаименованиеБанка);
	
	СсылкаНаБанк = Неопределено;
	Если ЗначениеЗаполнено(БанкФормы) Тогда
		БИКБанкФормы = ОбменСБанкамиСлужебный.БИК(БанкФормы);
		Если БИКБанкФормы = БИК Тогда
			СсылкаНаБанк = БанкФормы;
		КонецЕсли;
	КонецЕсли;
	Если СсылкаНаБанк = Неопределено Тогда
		СсылкаНаБанк = ОбщегоНазначенияБЭД.НайтиСсылку("Банки", БИК, СтруктураПоискаБанка);
	КонецЕсли;
		
	Если НЕ ЗначениеЗаполнено(СсылкаНаБанк) Тогда
		ТекстСообщения = НСтр("ru = 'Файл содержит настройки для неизвестного банка: %1%2'");
		Если СтруктураПоискаБанка.Свойство("Код") Тогда
			ТекстБИК = Символы.ПС + НСтр("ru = 'БИК: %1'");
			ТекстБИК = СтрШаблон(ТекстБИК, СтруктураПоискаБанка.Код);
		Иначе
			ТекстБИК = "";
		КонецЕсли;
		Если СтруктураПоискаБанка.Свойство("Наименование") Тогда
			ТекстНаименование = Символы.ПС + НСтр("ru = 'Наименование: %1'");
			ТекстНаименование = СтрШаблон(
				ТекстНаименование, СтруктураПоискаБанка.Наименование);
		Иначе
			ТекстНаименование = "";
		КонецЕсли;
		ТекстСообщения = СтрШаблон(
			ТекстСообщения, ТекстБИК, ТекстНаименование);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли;

	СтруктураВозврата = Новый Структура;
 	СтруктураВозврата.Вставить("Организация", СсылкаНаОрганизацию);
	СтруктураВозврата.Вставить("Банк", СсылкаНаБанк);
	СтруктураВозврата.Вставить("ЭтоНастройкиСбербанка", ЭтоНастройкиСбербанка);
	Если ЭтоНастройкиСбербанка Тогда
		СтруктураВозврата.Вставить("URLВК", ЗначениеСвойстваXDTO(ЭД, "SignatureParameters.AddIn.__content"));
	КонецЕсли;
		
	Возврат СтруктураВозврата;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗначениеСвойстваXDTO(ОбъектXDTO, Путь)

	Возврат РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(ОбъектXDTO, Путь);

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ВывестиСсылкуНаСайтБанка(ИнформацияНаСайтеБанка, АдресСайта)

	ИнформацияНаСайтеБанка.Видимость = Истина;
	ИнформацияНаСайтеБанка.Заголовок = Новый ФорматированнаяСтрока(
		НСтр("ru = 'Подробную информацию о порядке и условиях подключения на стороне банка можно получить'"), " ",
		Новый ФорматированнаяСтрока(НСтр("ru = 'здесь.'"), , , , АдресСайта));

КонецПроцедуры

&НаКлиенте
Процедура ВернутьсяНаПредыдущуюСтраницу()
	
	ПредыдущийШаг = ХронологияПереключенияСтраниц[ХронологияПереключенияСтраниц.ВГраница()];
	ХронологияПереключенияСтраниц.Удалить(ХронологияПереключенияСтраниц.ВГраница());
	СменитьСтраницуШаги(ПредыдущийШаг);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатПолученияНастроекССервераБанка(Успех)
	
	Если Успех Тогда
		Если УпрощенныйРежимНастройки Тогда
			Элементы.ПолучениеНастроекОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.Успешно32;
			Элементы.СозданиеНастройкиОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.НезавершившаясяПроверка;
			Элементы.ГруппаСозданиеНастройкиОбменаУРН.Видимость = Истина;
		Иначе
			Элементы.ПолучениеНастроекОбменаКартинка.Картинка = БиблиотекаКартинок.Успешно32;
		КонецЕсли;
	Иначе
		Если УпрощенныйРежимНастройки Тогда
			АдресФайлаНастроекОбмена = "";
			Элементы.ПодключитьСервис.Доступность = Истина;
			Элементы.ПолучениеНастроекОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.Ошибка32;
			Элементы.СтраницыРезультатПодключенияУРН.ТекущаяСтраница = Элементы.СтраницаНеПодключеноУРН;
		Иначе
			Элементы.ПолучениеНастроекОбменаКартинка.Картинка = БиблиотекаКартинок.Ошибка32;
			Элементы.СтраницыРезультатПодключения.ТекущаяСтраница = Элементы.СтраницаНеПодключено;
			Элементы.ПолучитьНастройки.Видимость = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатСозданияНастройкиОбмена(Успех)
	
	Если Успех Тогда
		
		ОповеститьОбИзменении(НастройкаОбмена);
		Элементы.Заголовок.Заголовок = Строка(НастройкаОбмена);
		
		Если УпрощенныйРежимНастройки Тогда
			Элементы.СозданиеНастройкиОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.Успешно32;
			Элементы.ОткрытьНастройкуОбменаУРН.Видимость = Истина;
			Элементы.ТестированиеНастройкиОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.НезавершившаясяПроверка;
			Элементы.ГруппаТестированиеНастройкиОбменаУРН.Видимость = Истина;
		Иначе
			Элементы.СозданиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.Успешно32;
			Элементы.ОткрытьНастройкуОбмена.Видимость = Истина;
		КонецЕсли;
	Иначе
		Если УпрощенныйРежимНастройки Тогда
			Элементы.ПодключитьСервис.Доступность = Истина;
			Элементы.СозданиеНастройкиОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.Ошибка32;
			Элементы.СтраницыРезультатПодключенияУРН.ТекущаяСтраница = Элементы.СтраницаНеПодключеноУРН;
		Иначе
			Элементы.СозданиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.Ошибка32;
			Элементы.СтраницыРезультатПодключения.ТекущаяСтраница = Элементы.СтраницаНеПодключено;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗаполнениеПрограммКриптографии(Оповещение)
	
	ПрограммыКриптографии = ПрограммыКриптографии();
		
	МассивПрограмм = Новый Массив;
	МассивПрограмм.Добавить("CryptoPro");
	МассивПрограмм.Добавить("SignalComECGOST");
	МассивПрограмм.Добавить("eToken");
	
	МассивПрограммДляСоздания = Новый Массив;
	
	ПроверитьОчереднуюПрограммуКриптографии(МассивПрограмм, ПрограммыКриптографии, Оповещение, МассивПрограммДляСоздания);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПрограммыКриптографии()
	
	НастройкиПрограмм = Справочники.ПрограммыЭлектроннойПодписиИШифрования.ПоставляемыеНастройкиПрограмм();
	
	Настройка = НастройкиПрограмм.Добавить();
	Настройка.Представление       = НСтр("ru = 'КриптоПро eToken CSP'");
	Настройка.ИмяПрограммы        = "eToken Base Cryptographic Provider";
	Настройка.ТипПрограммы        = 1;
	Настройка.АлгоритмПодписи     = "RSA_SIGN";
	Настройка.АлгоритмХеширования = "SHA-1";
	Настройка.АлгоритмШифрования  = "DES";
	Настройка.Идентификатор       = "eToken";
	
	ТипизированнаяТаблица = Новый ТаблицаЗначений;
	КвалификаторСтроки = Новый КвалификаторыСтроки(150);
	ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка", , КвалификаторСтроки);
	ОписаниеТиповЧисло = Новый ОписаниеТипов("Число");
	
	ТипизированнаяТаблица.Колонки.Добавить("ИмяПрограммы", ОписаниеТиповСтрока);
	ТипизированнаяТаблица.Колонки.Добавить("ТипПрограммы", ОписаниеТиповЧисло);
	
	Для Каждого Строка Из НастройкиПрограмм Цикл
		НовСтрока = ТипизированнаяТаблица.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтрока, Строка);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПрограммыКриптографии.ИмяПрограммы,
	|	ПрограммыКриптографии.ТипПрограммы
	|ПОМЕСТИТЬ ПрограммыКриптографии
	|ИЗ
	|	&ПрограммыКриптографии КАК ПрограммыКриптографии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрограммыЭлектроннойПодписиИШифрования.ИмяПрограммы
	|ИЗ
	|	Справочник.ПрограммыЭлектроннойПодписиИШифрования КАК ПрограммыЭлектроннойПодписиИШифрования
	|ГДЕ
	|	(ПрограммыЭлектроннойПодписиИШифрования.ИмяПрограммы, ПрограммыЭлектроннойПодписиИШифрования.ТипПрограммы) В
	|			(ВЫБРАТЬ
	|				ПрограммыКриптографии.ИмяПрограммы,
	|				ПрограммыКриптографии.ТипПрограммы
	|			ИЗ
	|				ПрограммыКриптографии КАК ПрограммыКриптографии)";
	Запрос.УстановитьПараметр("ПрограммыКриптографии", ТипизированнаяТаблица);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрокаПрограммы = НастройкиПрограмм.Найти(Выборка.ИмяПрограммы, "ИмяПрограммы");
		Если НЕ СтрокаПрограммы = Неопределено Тогда
			НастройкиПрограмм.Удалить(СтрокаПрограммы);
		КонецЕсли;
	КонецЦикла;
	
	СтруктураВозврата = Новый Структура;
	Для Каждого Строка Из НастройкиПрограмм Цикл
		СтруктураЗаписи = Новый Структура;
		СтруктураЗаписи.Вставить("ИмяПрограммы", Строка.ИмяПрограммы);
		СтруктураЗаписи.Вставить("Представление", Строка.Представление);
		СтруктураЗаписи.Вставить("ТипПрограммы", Строка.ТипПрограммы);
		СтруктураЗаписи.Вставить("АлгоритмПодписи", Строка.АлгоритмПодписи);
		СтруктураЗаписи.Вставить("АлгоритмХеширования", Строка.АлгоритмХеширования);
		СтруктураЗаписи.Вставить("АлгоритмШифрования", Строка.АлгоритмШифрования);
		СтруктураВозврата.Вставить(Строка.Идентификатор, СтруктураЗаписи);
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьОрганизациюВСертификате(Сертификат, Организация)
	
	Попытка
		СертификатОбъект = Сертификат.ПолучитьОбъект();
		СертификатОбъект.Организация = Организация;
		СертификатОбъект.Записать();
	Исключение
		Возврат;
		// операция не критичная, исключение не обрабатывается
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Функция СтруктураСозданияНастройкиОбмена()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("НастройкаОбмена", НастройкаОбмена);
	СтруктураПараметров.Вставить("Организация", Организация);
	СтруктураПараметров.Вставить("Банк", Банк);
	СтруктураПараметров.Вставить("АдресСервера", АдресСервера);
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен") Тогда
		СтруктураПараметров.Вставить("АутентификацияПоСертификату", СпособАутентификации = 0);
	КонецЕсли;
	СтруктураПараметров.Вставить("АутентификацияПоСертификату", СпособАутентификации = 0);
	СтруктураПараметров.Вставить("ИмяВнешнегоМодуля", ИмяВнешнегоМодуля);
	СтруктураПараметров.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизации);
	Если СохранитьЛогин Тогда
		СтруктураПараметров.Вставить("ИмяПользователя", Логин);
	КонецЕсли;
	СтруктураПараметров.Вставить("ИспользуетсяКриптография", ИспользуетсяКриптография);
	СтруктураПараметров.Вставить("ПрограммаБанка", ПрограммаБанка);
	СтруктураПараметров.Вставить("РесурсВходящихДокументов", РесурсИсточник);
	СтруктураПараметров.Вставить("РесурсИсходящихДокументов", РесурсПриемник);
	СтруктураПараметров.Вставить("СертификатыБанка", Новый Массив);
	Для Каждого Элемент Из СертификатыСбербанка Цикл
		СтруктураПараметров.СертификатыБанка.Добавить(ПолучитьИзВременногоХранилища(Элемент));
	КонецЦикла;
	МассивСертификатов = Новый Массив;
	Для Каждого Элемент Из Сертификаты Цикл
		МассивСертификатов.Добавить(Элемент.Сертификат);
	КонецЦикла;
	СтруктураПараметров.Вставить("Сертификаты", МассивСертификатов);
	СтруктураПараметров.Вставить("СертификатАутентификации", СертификатАутентификации);
	СтруктураПараметров.Вставить("Недействительна", Истина);
	СтруктураПараметров.Вставить("ВерсияФормата", ВерсияФормата);
	
	СтруктураПараметров.Вставить("НаименованиеБанка", НаименованиеБанка);
	СтруктураПараметров.Вставить("НомерПодразделенияБанка", НомерПодразделенияБанка);
	СтруктураПараметров.Вставить("ТипыПисемСсылкаСбербанк", ТипыПисемСсылкаСбербанк);
	СтруктураПараметров.Вставить("ТипыПисемСбербанк");
	
	СтруктураПараметров.Вставить("ДополнительнаяОбработка", ДополнительнаяОбработка);

	Возврат СтруктураПараметров;

КонецФункции

&НаКлиенте
Процедура СоздатьНастройкуОбмена()
	
	Если (ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен")
			И (СпособСозданияНастройки = 1 ИЛИ СпособСозданияНастройки = 0))
		ИЛИ ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК")
		ИЛИ (СпособСозданияНастройки = 2 И НастройкиЗагруженыИзФайла) Тогда //настройки получены пробной операцией
		ЗагрузитьНастройкуОбменаИзФайла();
		Возврат;
	КонецЕсли;
	
	ПараметрыНастройкиОбмена = СтруктураСозданияНастройкиОбмена();
	
	Результат = ЗапуститьСозданиеНастройкиОбмена(ПараметрыНастройкиОбмена, УникальныйИдентификатор);
	
	ИдентификаторЗадания = Результат.ИдентификаторЗадания;
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	Оповещение = Новый ОписаниеОповещения("ПослеСозданияНастройкиОбмена", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапуститьСозданиеНастройкиОбмена(Знач СтруктураПараметров, Знач УникальныйИдентификатор)
	
	НаименованиеЗадания = НСтр("ru = 'Создание настройки прямого обмена с банком'");

	Если ЗначениеЗаполнено(СтруктураПараметров.ТипыПисемСсылкаСбербанк) Тогда
		ТипыПисемСбербанк = ПолучитьИзВременногоХранилища(СтруктураПараметров.ТипыПисемСсылкаСбербанк);
		СтруктураПараметров.Вставить("ТипыПисемСбербанк", ТипыПисемСбербанк);
	КонецЕсли;

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиСлужебный.СоздатьНастройкуОбмена", СтруктураПараметров, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ПослеСозданияНастройкиОбмена(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗадания = Неопределено Тогда
		ОбработатьРезультатСозданияНастройкиОбмена(Ложь);
	ИначеЕсли РезультатЗадания.Статус = "Выполнено" Тогда
		СтруктураВозврата = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		НастройкаОбмена = СтруктураВозврата.НастройкаОбмена;
		Если Не ЗначениеЗаполнено(НастройкаОбмена) Тогда
			ОбработатьРезультатСозданияНастройкиОбмена(Ложь);
			Возврат;
		КонецЕсли;

		Оповестить("ИзмененаНастройкаОбменСБанками", НастройкаОбмена);
	
		РеквизитыНастройкиОбмена = РеквизитыНастройкиОбмена(НастройкаОбмена);
		
		ПрограммаБанка = РеквизитыНастройкиОбмена.ПрограммаБанка;
		ДоступноАвтоматическоеПолучениеВыписки = РеквизитыНастройкиОбмена.ДоступноАвтоматическоеПолучениеВыписки;
		
		Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен") Тогда
			ОбработатьРезультатСозданияНастройкиОбмена(Истина);
			Элементы.ТестированиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
			#Если ВебКлиент Тогда
				ОбновитьОтображениеДанных();
			#КонецЕсли
			ПодключитьОбработчикОжидания("ТестНастройкиАсинхронныйОбмен", 0.1, Истина);
		ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
			ОбработатьРезультатСозданияНастройкиОбмена(Истина);
			Элементы.ТестированиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
			#Если ВебКлиент Тогда
				ОбновитьОтображениеДанных();
			#КонецЕсли
			ПодключитьОбработчикОжидания("ТестНастройкиОбменаСбербанк", 0.1, Истина);
		ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК") Тогда
			Если СпособСозданияНастройки = 1 Тогда // загрузка из файла
				
				ИмяВнешнегоМодуля = ИмяВнешнегоМодуля(НастройкаОбмена);
				
				ДополнительныеПараметры = Новый Структура;
				ДополнительныеПараметры.Вставить("ДвоичныеДанныеСертификатовИзФайла", СтруктураВозврата.ДанныеСертификатов);
				
				Оповещение = Новый ОписаниеОповещения(
					"СохранитьСертификатыПослеПодключенияВК", ЭтотОбъект, ДополнительныеПараметры);
				
				ВнешниеКомпонентыКлиент.ПодключитьКомпоненту(Оповещение, ИмяВнешнегоМодуля);
				
			Иначе //соединение на токене уже установлено
				СохранитьОчереднойСертификатВИнформационнойБазеВК(СтруктураВозврата.ДанныеСертификатов);
			КонецЕсли;
		ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АльфаБанкОнлайн") Тогда
			ОбработатьРезультатСозданияНастройкиОбмена(Истина);
			ОбработатьРезультатТестаНастройки(Истина);
		ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
			ОбработатьРезультатСозданияНастройкиОбмена(Истина);
			Элементы.ТестированиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
			#Если ВебКлиент Тогда
				ОбновитьОтображениеДанных();
			#КонецЕсли
			ПодключитьОбработчикОжидания("ТестНастройкиОбменаЧерезДополнительнуюОбработку", 0.1, Истина);
		ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.FintechIntegration") Тогда
			ОбработатьРезультатСозданияНастройкиОбмена(Истина);
			Элементы.ТестированиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
			ПодключитьОбработчикОжидания("ТестНастройкиОбменаFintech", 0.1, Истина);
		КонецЕсли;
	Иначе // Ошибка
		ВидОперации = НСтр("ru = 'Создание настройки обмена с банком'");
		ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(ВидОперации, 
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками,
			РезультатЗадания.ПодробноеПредставлениеОшибки, РезультатЗадания.КраткоеПредставлениеОшибки, НастройкаОбмена);
		ОбработатьРезультатСозданияНастройкиОбмена(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция МожноПоказатьПодключениеАвтоматическойПолучениеВыписки(НастройкаОбменСБанками)
	
	Возврат ОбменСБанкамиСлужебный.ДоступенЗапросВыписки(НастройкаОбменСБанками);
	
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультатТестаНастройки(Успех)
	
	РаботаПомощникаЗавершена = Ложь;
	Если Успех Тогда
		УспешноВыполненТест = Истина;
		НастройкаВключена = Ложь;
		ВключитьНастройкуОбмена(НастройкаОбмена, НастройкаВключена);
		Если НастройкаВключена Тогда
			ОповеститьОбИзменении(НастройкаОбмена);
			Оповестить("ИзмененаНастройкаОбменСБанками", НастройкаОбмена);
			РаботаПомощникаЗавершена = Истина;
			
			Если УпрощенныйРежимНастройки Тогда
				
				Элементы.ПодключитьСервис.КнопкаПоУмолчанию = Ложь;
				Элементы.ПодключитьСервис.Доступность = Ложь;
				Элементы.ТестированиеНастройкиОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.Успешно32;
				Элементы.СтраницыРезультатПодключенияУРН.ТекущаяСтраница = Элементы.СтраницаПодключеноУРН;
				Элементы.ДекорацияУспешноеПодключениеУРН.Видимость = Истина;
				
				СменитьСтраницуКомандНавигации(Элементы.Готово);
				
				Элементы.ГруппаДоступноАвтоматическоеПолучениеВыписки.Видимость = Ложь;
				Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен")
					И ДоступноАвтоматическоеПолучениеВыписки 
					И МожноПоказатьПодключениеАвтоматическойПолучениеВыписки(НастройкаОбмена) Тогда
					Элементы.ГруппаДоступноАвтоматическоеПолучениеВыписки.Видимость = Истина;
				КонецЕсли;
				
			Иначе
				Элементы.ТестированиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.Успешно32;
				Элементы.СтраницыРезультатПодключения.ТекущаяСтраница = Элементы.СтраницаПодключено;
				Элементы.ДекорацияУспешноеПодключение.Видимость = Истина;
				Если Элементы.КомандыНавигации.ТекущаяСтраница = Элементы.Отмена Тогда
					СменитьСтраницуКомандНавигации(Элементы.Готово);
				Иначе
					СменитьСтраницуКомандНавигации(Элементы.НазадГотово);
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			Если УпрощенныйРежимНастройки Тогда
				Элементы.ПодключитьСервис.Доступность = Истина;
				Элементы.СтраницыРезультатПодключенияУРН.ТекущаяСтраница = Элементы.СтраницаНеПодключеноУРН;
				Элементы.ТестированиеНастройкиОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.Ошибка32;
			Иначе
				Элементы.СтраницыРезультатПодключения.ТекущаяСтраница = Элементы.СтраницаНеПодключено;
				Элементы.ТестированиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.Ошибка32;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если УпрощенныйРежимНастройки Тогда
			Элементы.ПодключитьСервис.Доступность = Истина;
			Элементы.ДекорацияНеПодключеноУРН.Видимость = Истина;
			Элементы.ТестированиеНастройкиОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.Ошибка32;
		Иначе
			Элементы.ДекорацияНеПодключено.Видимость = Истина;
			Элементы.ТестированиеНастройкиОбменаКартинка.Картинка = БиблиотекаКартинок.Ошибка32;
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьИнтерфейс();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВключитьНастройкуОбмена(Знач НастройкаОбмена, Успех)
	
	Объект = НастройкаОбмена.ПолучитьОбъект();
	Объект.Прочитать();
	Попытка
		Объект.Недействительна = Ложь;
		Объект.Записать();
		Успех = Истина;
	Исключение
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВидОперации = НСтр("ru = 'Включение настройки.'");
		ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(ВидОперации, 
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками,
			ТекстОшибки, ТекстСообщения, НастройкаОбмена);
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПриЗакрытииНаСервере(Знач ИдентификаторЗадания)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура СообщитьОшибкуПользователю(ТекстСообщения, ДополнительныеПараметры) Экспорт
	
	ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытииПомощника(Результат, ДополнительныеПараметры) Экспорт
	
	ПриЗакрытииНаСервере(ИдентификаторЗадания);

КонецПроцедуры

// Формирует таблицу банков и организаций для которых есть банковские счета.
//
// Параметры:
//  ПараметрыОтбора - Структура - структура с ключами:
//   * Организация  - ОпределяемыйТип.Организация - организация, по которой необходимо установить фильтр (необязательный);
//   * Банк         - ОпределяемыйТип.БанкОбменСБанками - банк, по которому необходимо установить фильтр (необязательный);
//
// Возвращаемое значение:
//   ТаблицаЗначений - банки доступные для настройки обмена через 1С:ДиректБанк. Содержит колонки:
//     * Банк - ОпределяемыйТип.БанкОбменСБанками - ссылка на справочник банков.
//     * БИК - Строка - БИК банка.
//     * Организация - ОпределяемыйТип.Организация - ссылка на организацию.
//
&НаСервере
Функция УчастникиОбменаПоСчетам(ПараметрыОтбора)
	
	ИмяСправочникаБанковскиеСчета = ОбщегоНазначенияБЭД.ИмяПрикладногоСправочника(
		"БанковскиеСчетаОрганизаций");
	ИмяСправочникаОрганизации = ОбщегоНазначенияБЭД.ИмяПрикладногоСправочника("Организации");
	
	Если ИмяСправочникаБанковскиеСчета = Неопределено
		ИЛИ ИмяСправочникаОрганизации  = Неопределено Тогда
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	БанковскиеСчета.Банк КАК Банк,
	               |	БанковскиеСчета.Банк.Код КАК БИК,
	               |	БанковскиеСчета.Владелец КАК Организация,
	               |	НастройкиОбменСБанками.Ссылка КАК НастройкаОбмена
	               |ИЗ
	               |	&БанковскиеСчетаОрганизаций КАК БанковскиеСчета
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиОбменСБанками КАК НастройкиОбменСБанками
	               |		ПО БанковскиеСчета.Владелец = НастройкиОбменСБанками.Организация
	               |			И БанковскиеСчета.Банк = НастройкиОбменСБанками.Банк
	               |			И (НЕ НастройкиОбменСБанками.Недействительна)
	               |			И (НЕ НастройкиОбменСБанками.ПометкаУдаления)
	               |ГДЕ
	               |	НЕ БанковскиеСчета.ПометкаУдаления
	               |	И &УсловиеСчетЗакрыт
	               |	И БанковскиеСчета.Владелец = &Ссылка_Справочник_Организации
	               |	И БанковскиеСчета.Банк <> &ПустаяСсылкаБанка
	               |	И НастройкиОбменСБанками.Ссылка ЕСТЬ NULL
	               |{ГДЕ
	               |	БанковскиеСчета.Владелец КАК Организация,
	               |	БанковскиеСчета.Банк КАК Банк}";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&БанковскиеСчетаОрганизаций",
		"Справочник." + ИмяСправочникаБанковскиеСчета);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "= &Ссылка_Справочник_Организации",
		"ССЫЛКА Справочник." + ИмяСправочникаОрганизации);
	
	НазваниеРеквизита = ОбменСБанкамиСлужебный.ИмяПрикладногоРеквизита("БанковскийСчетОрганизации.Закрыт", Ложь);
	
	Если ЗначениеЗаполнено(НазваниеРеквизита) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСчетЗакрыт", "НЕ БанковскиеСчета." + НазваниеРеквизита);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСчетЗакрыт", "ИСТИНА");
	КонецЕсли;
	
	Построитель = Новый ПостроительЗапроса(ТекстЗапроса);
	Построитель.Параметры.Вставить("ПустаяСсылкаБанка", ОбменСБанкамиСлужебный.ПустаяСсылкаНаСправочникБанки());
	
	Если ЗначениеЗаполнено(ПараметрыОтбора) Тогда
		Для Каждого Параметр Из ПараметрыОтбора Цикл
			ЭлементОтбора = Построитель.Отбор.Добавить(Параметр.Ключ);
			ЭлементОтбора.Использование = Истина;
			ЭлементОтбора.ВидСравнения = ВидСравнения.Равно;
			ЭлементОтбора.Значение = Параметр.Значение;
		КонецЦикла;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Построитель.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	РезультатЗапроса = Построитель.Результат;
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

&НаСервереБезКонтекста
Функция РеквизитыНастройкиОбмена(Знач НастройкаОбмена)

	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		НастройкаОбмена, "ПрограммаБанка, ДоступноАвтоматическоеПолучениеВыписки");

КонецФункции

#КонецОбласти

#Область УпрощенныйРежимНастройки

&НаСервере
Процедура НастройкаФормыУпрощенногоРежима()
	
	Элементы.СтраницаОписание.Видимость = Ложь;
	Элементы.СтраницаВыборСпособаСозданияНастройки.Видимость = Ложь;
	Элементы.СтраницаПолучениеССервера.Видимость = Ложь;
	Элементы.СтраницаВыборФайлаНастроек.Видимость = Ложь;
	Элементы.СтраницаЗапросИспользованияВнешнегоМодуля.Видимость = Ложь;
	Элементы.СтраницаАсинхронныйОбмен.Видимость = Ложь;
	Элементы.СтраницаСертификаты.Видимость = Ложь;
	Элементы.СтраницаФинал.Видимость = Ложь;
	
	Если НеПоказыватьСтраницуВыборОрганизацииИБанка Тогда
		Элементы.СтраницаВыборОрганизацииИБанка.Видимость = Ложь;
		Элементы.НазадГотово.Видимость = Ложь;
	КонецЕсли;
		
	Элементы.НазадДалееОтмена.Видимость = Ложь;
		
	Элементы.Заголовок.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.Подчеркивание);
	
КонецПроцедуры

&НаКлиенте
Функция СменитьСтраницуНаУпрощеннуюНастройку(НоваяСтраница)
	
	Если УпрощенныйРежимНастройки
		И НоваяСтраница <> Элементы.СтраницаПодождите
		И НоваяСтраница <> Элементы.СтраницаВыборОрганизацииИБанка Тогда
		
		Если НоваяСтраница = Элементы.Шаги.ТекущаяСтраница Тогда
			Возврат Истина;
		КонецЕсли;
		
		НастройкаСтраницыУпрощенногоРежима();
		Если ХронологияПереключенияСтраниц.Количество() Тогда
			СменитьСтраницуКомандНавигации(Элементы.НазадОтмена);
		Иначе
			СменитьСтраницуКомандНавигации(Элементы.Отмена);
		КонецЕсли;
		
		Элементы.СтраницаУпрощенныйРежимНастройки.Видимость = Истина;
		Элементы.Шаги.ТекущаяСтраница = Элементы.СтраницаУпрощенныйРежимНастройки;
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура НастройкаСтраницыУпрощенногоРежима()
	
	ЭтоСбербанк = ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн; 
	
	Если ЭтоСбербанк Тогда
		
		УстановитьСтраницуЭтапа(Элементы.СтраницыСпособыПолученияНастроек, Элементы.СтраницаНастройкиПоЛогину);
		
	ИначеЕсли ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезВК Тогда
		
		УстановитьСтраницуЭтапа(Элементы.СтраницыСпособыПолученияНастроек, Элементы.СтраницаНастройкиПустая);
		Элементы.КартинкаПолучениеНастроек.Видимость = Ложь;
		Элементы.КартинкаПодключитьСервис.Картинка = БиблиотекаКартинок.Этап3СинийБЭД;
		
	Иначе
		
		Если НастройкиОбменаСБанком.СпособАутентификации = "ИзФайла" Тогда
			УстановитьСтраницуЭтапа(Элементы.СтраницыСпособыПолученияНастроек, Элементы.СтраницаНастройкиИзФайла);
		ИначеЕсли НастройкиОбменаСБанком.СпособАутентификации = "ПоЛогину"
			ИЛИ (НастройкиОбменаСБанком.СпособАутентификации = "ПоЛогинуИлиСертификату"
			И НЕ ЕстьСертификаты) Тогда
			УстановитьСтраницуЭтапа(Элементы.СтраницыСпособыПолученияНастроек, Элементы.СтраницаНастройкиПоЛогину);
		Иначе
			УстановитьСтраницуЭтапа(Элементы.СтраницыСпособыПолученияНастроек, Элементы.СтраницаНастройкиПоСертификату);
		КонецЕсли;
		
	КонецЕсли;
	
	ЕстьИнструкция = НЕ ПустаяСтрока(НастройкиОбменаСБанком.АдресИнструкции);
	Элементы.НадписьПодробнееОзнакомлениеСУсловиями.Видимость  = ЕстьИнструкция;
	Элементы.НадписьПодробнееНастройкаНаСторонеБанка.Видимость = ЕстьИнструкция;
	
	Элементы.ГруппаЗагрузкаПараметровОбменаСБанкомУРН.Видимость = Ложь;
	Элементы.ГруппаСозданиеНастройкиОбменаУРН.Видимость = Ложь;	
	Элементы.ГруппаТестированиеНастройкиОбменаУРН.Видимость = Ложь;
	Элементы.СтраницыРезультатПодключенияУРН.ТекущаяСтраница = Элементы.СтраницаНеЗавершеноУРН;

	Элементы.ПодключитьСервис.КнопкаПоУмолчанию = Истина;
	
	Элементы.ГруппаВыбратьСервисСбербанка.Видимость = ЭтоСбербанк И ОбменСБанкамиСлужебный.ИспользуетсяFintech();
	Элементы.ГруппаПолучениеНастроек.Видимость = НЕ ЭтоСбербанк ИЛИ НЕ ОбменСБанкамиСлужебный.ИспользуетсяFintech();
	
	Если Не ОбменСБанкамиСлужебный.ИспользуетсяFintech() Тогда
		СервисСбербанк = "УПШ"
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСтраницуЭтапа(ЭтапНастройки, НоваяСтраница)
	
	ЭтапНастройки.ТекущаяСтраница = НоваяСтраница;
	
	Для Каждого Страница Из ЭтапНастройки.ПодчиненныеЭлементы Цикл
		Если Страница <> НоваяСтраница Тогда
			Страница.Видимость = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	НоваяСтраница.Видимость = Истина;
	ЭтапНастройки.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПодробнееУсловияСбербанкНажатие(Элемент)
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(НастройкиОбменаСБанком.АдресИнструкции);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьПодробнееНастройкаНаСторонеБанкаНажатие(Элемент)
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(НастройкиОбменаСБанком.АдресИнструкции + "#instruction");
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатУННачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПодключитьОбработчикОжидания("ЗаполнитьСписокВыбораСертификатовАутентификацииСВыводомОшибки", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередНачаломПодключенияСервиса()
	
	ОчиститьСообщения();

	Элементы.ПодключитьСервис.Доступность = Ложь;
	
	Элементы.ГруппаСозданиеНастройкиОбменаУРН.Видимость = Ложь;
	Элементы.ГруппаТестированиеНастройкиОбменаУРН.Видимость= Ложь;
	
	Элементы.ГруппаЗагрузкаПараметровОбменаСБанкомУРН.Видимость = Истина;
	Элементы.ПолучениеНастроекОбменаКартинкаУРН.Картинка = БиблиотекаКартинок.НезавершившаясяПроверка;
	Элементы.СтраницыРезультатПодключенияУРН.ТекущаяСтраница = Элементы.СтраницаНеЗавершеноУРН;
		
КонецПроцедуры

#КонецОбласти

#Область ВнешниеКомпоненты

&НаКлиенте
Процедура ПолучитьИдентификаторВнешнейКомпоненты(Оповещение)
	
	Результат = ЗапускЗаданияПоПолучениюИнформацииОВнешнейКомпоненте(URLВК, УникальныйИдентификатор);
	ДополнительныеПараметры = Новый Структура("ОповещениеПослеСкачиванияВК", Оповещение);
	
	ИдентификаторЗадания = Результат.ИдентификаторЗадания;
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПослеПолученияИнформацииОВнешнейКомпоненте = Новый ОписаниеОповещения(
		"ПослеПолученияИнформацииОВнешнейКомпоненте", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, ПослеПолученияИнформацииОВнешнейКомпоненте, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияИнформацииОВнешнейКомпоненте(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСкачиванияВК, Новый Структура("Успех", Ложь));
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		ИнформацияОВК = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("Успех", Истина);
		СтруктураВозврата.Вставить("Идентификатор", ИнформацияОВК.ИмяМодуля);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСкачиванияВК, СтруктураВозврата);
	Иначе // Ошибка
		ВидОперации = НСтр("ru = 'Получение информации о внешней компоненте.'");
		ОбработкаНеисправностейБЭДВызовСервера.ОбработатьОшибку(ВидОперации, 
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСБанками,
			Результат.ПодробноеПредставлениеОшибки, Результат.КраткоеПредставлениеОшибки, НастройкаОбмена());
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСкачиванияВК, Новый Структура("Успех", Ложь));
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапускЗаданияПоПолучениюИнформацииОВнешнейКомпоненте(Знач URLИнфоФайла, Знач УникальныйИдентификатор)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Скачивание информационного файла внешней компоненты через интернет.'");
	ПараметрыПроцедуры = Новый Структура("URLИнфоФайла", URLИнфоФайла);
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиСлужебный.ПолучитьИнформациюОВнешнейКомпоненте", ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область Инициализация

СертификатыСбербанка = Новый Массив;

#КонецОбласти