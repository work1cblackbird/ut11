
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	Элементы.ГруппаСертификатыБанка.Видимость = Объект.ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн
		И Объект.ИспользуетсяКриптография;
	
	ПроставитьВозможномстьВыбораСпособаПодтверждения();
	
	УстановитьУсловноеОформление();
	
	Элементы.СтраницыВидыБанковскихСистем.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.СтраницыВнешнегоМодуля.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	
	ПанельАдминистрированияБСП = Метаданные.НайтиПоПолномуИмени("Обработка.ПанельАдминистрированияБСП");
	Элементы.ДекорацияВнешниеОбработкиНеИспользуются.Гиперссылка = ПанельАдминистрированияБСП <> Неопределено;
	
	Если Объект.ИспользуетсяКриптография
		И Объект.ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн Тогда
		ЗаполнитьТаблицуСертификатовБанка();
	КонецЕсли;
	
	ИспользуетсяFintech = ОбменСБанкамиСлужебный.ИспользуетсяFintech();
	
	// Изменим подсказку и доступность флажка 
	Если Объект.ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн Тогда
		Если ТекущаяДатаСеанса() < ОбменСБанкамиСлужебныйКлиентСервер.ДатаОтключенияПодписанияSMSСбербанк() Тогда
			Элементы.ОтправлятьДокументыБезПодписиSMS.Подсказка = 
				НСтр("ru = 'С 01.06.2023 в целях повышения безопасности Сбербанком будет отключена SMS-подпись платежей.
						   |После этого флажок будет установлен в обязательном порядке.'");
		Иначе
			// Принудительно взведем флажок и отключим его доступность.
			// Модифицированность формы устанавливать не будем, если перезапишут настройку, то флажок сохранится, 
			// в коде значение флажка после указанной даты не используется.
			Если НЕ Объект.ОтправлятьДокументыБезПодписиSMS Тогда
				Объект.ОтправлятьДокументыБезПодписиSMS = Истина;
			КонецЕсли;
			Элементы.ОтправлятьДокументыБезПодписиSMS.Доступность = Ложь;
		КонецЕсли;
	КонецЕсли;

	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ИспользуетсяFintech = ОбменСБанкамиСлужебный.ИспользуетсяFintech();
	ЗаполнитьСлужебныеРеквизитыТаблицыИсходящихДокументов();
	
	ЕстьПодтверждаемыйДокумент = ЕстьПодтверждаемыйДокумент(Объект);
	
	НастроитьЭлементы(ЭтотОбъект, Объект.ПрограммаБанка, Объект.ИспользуетсяКриптография,
		Объект.АутентификацияПоСертификату, Объект.ДоступноАвтоматическоеПолучениеВыписки, Объект.Ссылка);
	
	ПроставитьВозможномстьВыбораСпособаПодтверждения();
	
	УстановитьДоступностьЭлементовПоРолям();
	
	ОформитьВерсиюФорматаОбмена();
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьИнформациюОВнешнемМодуле();
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьИнформациюОбОтсутствииВнешнегоМодуля()
	
	Элементы.ИнформацияОВнешнемМодуле.Заголовок = НСтр(
		"ru = 'Отсутствует внешний модуль. Работа не возможна.'");
	Элементы.ИнформацияОВнешнемМодуле.ЦветТекста = ЦветаСтиля.ЦветОтрицательногоЧисла;
	Элементы.ЗагрузитьВнешнийМодульИзФайла.Заголовок = НСтр("ru = 'Загрузить из файла ...'");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ОчиститьСообщения();
	
	УдалитьПустыеСтрокиТаблиц();
	
	Если НЕ Объект.ИспользуетсяКриптография Тогда
		Объект.АутентификацияПоСертификату = Ложь;
	КонецЕсли;
	
	Если Не ПараметрыЗаписи.Свойство("ПропуститьПроверки") Тогда
		// Выполняем серверный вызов, так как нужно проверить валидность маршрута
		// и в случае ошибок спросить у пользователя, записывать ли его.
		ЕстьОшибкиЗаполнения = Ложь;
		Если Не НастройкаВалидирована(ЕстьОшибкиЗаполнения) Тогда
			Отказ = Истина;
			
			Если Не ЕстьОшибкиЗаполнения Тогда
				ОписаниеОповещения = Новый ОписаниеОповещения(
					"ВопросОЗаписиПолученОтвет", ЭтотОбъект, ПараметрыЗаписи);
				ТекстВопроса = НСтр("ru = 'Обнаружены возможные ошибки в настройках маршрутов. Продолжить запись?'");
				ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет, 
					НСтр("ru = 'Настройка некорректна'"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Объект.ИспользуетсяКриптография
		И Объект.ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн Тогда
			Для Каждого Элемент Из СертификатыБанка Цикл
				Если НЕ Элемент.СохраненВИнформационнойБазе Тогда
					НовСтрока = ТекущийОбъект.СертификатыБанка.Добавить();
					ЗаполнитьЗначенияСвойств(НовСтрока, Элемент);
					ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(Элемент.Данные);
					НовСтрока.Данные = Новый ХранилищеЗначения(ДвоичныеДанныеСертификата, Новый СжатиеДанных(9));
				КонецЕсли;
			КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьИнтерфейс();
	
	Оповестить("ИзмененаНастройкаОбменСБанками", Объект.Ссылка);

	Если НЕ ОписаниеОповещенияОЗакрытии = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии, Объект.Ссылка);
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство("ЗакрытьПослеЗаписи") Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	УстановитьПривилегированныйРежим(Истина);
		
	ИспользуютсяЭП = ОбщегоНазначенияБЭД.ЗначениеФункциональнойОпции(
		"ИспользоватьЭлектронныеПодписиЭД");
	
	Если НЕ ИспользуютсяЭП И Объект.ИспользуетсяКриптография
		И (Объект.ПрограммаБанка = Перечисления.ПрограммыБанка.АльфаБанкОнлайн
			ИЛИ Объект.ПрограммаБанка = Перечисления.ПрограммыБанка.АсинхронныйОбмен) Тогда
		Константы.ИспользоватьЭлектронныеПодписи.Установить(Истина);
	КонецЕсли;
	
	Если НЕ Объект.ПометкаУдаления И НЕ Объект.Недействительна Тогда
		ИспользуетсяОбменЭДСБанками = ОбщегоНазначенияБЭД.ЗначениеФункциональнойОпции(
			"ИспользоватьОбменСБанками");
		Если НЕ ИспользуетсяОбменЭДСБанками Тогда
			Константы.ИспользоватьОбменСБанками.Установить(Истина);
		КонецЕсли;
	КонецЕсли;
	ОбновитьПовторноИспользуемыеЗначения();
	
	ЗаполнитьСлужебныеРеквизитыТаблицыИсходящихДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененаНастройкаОбменСБанками" И Параметр = Объект.Ссылка Тогда
		Прочитать();
		НастроитьЭлементы(ЭтотОбъект, Объект.ПрограммаБанка, Объект.ИспользуетсяКриптография,
			Объект.АутентификацияПоСертификату, Объект.ДоступноАвтоматическоеПолучениеВыписки, Объект.Ссылка);
		ЗаполнитьИнформациюОВнешнемМодуле();
		
		ПроставитьВозможномстьВыбораСпособаПодтверждения();
		
		Если Объект.ИспользуетсяКриптография
			И Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
				ЗаполнитьТаблицуСертификатовБанка();
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "Запись_НаборКонстант" И Источник = "ИспользоватьДополнительныеОтчетыИОбработки" Тогда
		НастроитьЭлементы(ЭтотОбъект, Объект.ПрограммаБанка, Объект.ИспользуетсяКриптография,
			Объект.АутентификацияПоСертификату, Объект.ДоступноАвтоматическоеПолучениеВыписки, Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Если (Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АльфаБанкОнлайн")
			ИЛИ Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен"))
		И НЕ ОбменСБанкамиКлиентСервер.ПравильныйФорматАдреса(Объект.АдресСервера) И НЕ Объект.Недействительна Тогда
		ТекстСообщения = НСтр("ru = 'Адрес сервера банка должен начинаться с ""https://"" или ""http://""'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "АдресСервера", "Объект", Отказ);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗагрузитьВнешнийМодульИзФайлаНажатие(Элемент)
	
	ОчиститьСообщения();
	
	ПараметрыЗагрузки = ВнешниеКомпонентыКлиент.ПараметрыЗагрузки();
	ПараметрыЗагрузки.Идентификатор = Объект.ИмяВнешнегоМодуля;
	Оповещение = Новый ОписаниеОповещения("ПослеЗагрузкиКомпонентыИзФайла", ЭтотОбъект);
	ВнешниеКомпонентыКлиент.ЗагрузитьКомпонентуИзФайла(Оповещение, ПараметрыЗагрузки);

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВнешниеОбработкиНеИспользуютсяНажатие(Элемент)
	
	ФормаНастроекБСП = "Обработка.ПанельАдминистрированияБСП.Форма.ПечатныеФормыОтчетыИОбработки";
	ОткрытьФорму(ФормаНастроекБСП);
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьАвтоматическоеПолучениеВыпискиНажатие(Элемент)

	ПараметрыФормы = Новый Структура("НастройкаОбмена", Объект.Ссылка);
	Оповещение = Новый ОписаниеОповещения("ПослеВключенияАвтоматическогоПолученияВыписки", ЭтотОбъект);
	ОткрытьФорму("Справочник.НастройкиОбменСБанками.Форма.АвтоматическоеПолучениеВыписки", ПараметрыФормы, , , , ,
		Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ОстановитьАвтоматическоеПолучениеВыпискиНажатие(Элемент)

	Элементы.ГруппаДоступноАвтоматическоеПолучениеВыписки.Видимость = Истина;
	Элементы.ГруппаЗапущеноАвтоматическоеПолучениеВыписки.Видимость = Ложь;
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОбменСБанкамиСлужебныйВызовСервера.ОстановитьАвтоматическоеПолучениеВыписки(Объект.Ссылка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОшибкуНажатие(Элемент)
	
	ПустойОбработчик = Новый ОписаниеОповещения;
	ПоказатьЗначение(ПустойОбработчик, ТекстОшибкиВыписки);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресЛичногоКабинетаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Текст = СокрЛП(Текст);
	Если ЗначениеЗаполнено(Текст) И ВРег(Сред(Текст,1,4)) <> "HTTP" Тогда
		Объект.АдресЛичногоКабинета = "http://" + Текст;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВерсияФорматаПриИзменении(Элемент)
	
	Если НЕ ОбменСБанкамиКлиентСервер.ЕстьПоддержкаОбменаПисьмами(Объект.ВерсияФормата) Тогда
		ВидЭДПисьмо = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.Письмо");
		ПараметрыОтбора = Новый Структура("ИсходящийДокумент", ВидЭДПисьмо);
		СтрокиПоиска = Объект.ИсходящиеДокументы.НайтиСтроки(ПараметрыОтбора);
		Если СтрокиПоиска.Количество() Тогда
			УдаляемаяСтрока = СтрокиПоиска.Получить(0);
			Объект.ИсходящиеДокументы.Удалить(УдаляемаяСтрока.ИсходныйНомерСтроки - 1);
		КонецЕсли;
	КонецЕсли;
	
	ОформитьВерсиюФорматаОбмена();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИсходящиеДокументы

&НаКлиенте
Процедура ИсходящиеДокументыМаршрутПодписанияПриИзменении(Элемент)
	
	ТекДанные = Элементы.ИсходящиеДокументы.ТекущиеДанные;
	ТекДанные.МаршрутЗависимОтОрганизации = МаршрутЗависимОтОрганизации(ТекДанные.МаршрутПодписания);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсходящиеДокументыПодписыватьВБанкеПриИзменении(Элемент)
	
	ОтобразитьГруппуПодтвержденияПлатежа(Объект, Элементы.ГруппаЛичногоКабинета);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсходящиеДокументыСпособПодтвержденияПодставитьНужныйВариантВыбора(ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = ПолучитьПравилоПодписанияДокумента(
		Элементы.ИсходящиеДокументы.ТекущиеДанные.ИспользоватьЭП,
		Элементы.ИсходящиеДокументы.ТекущиеДанные.ИспользоватьSMS,
		Элементы.ИсходящиеДокументы.ТекущиеДанные.ИсходящийДокумент);
	Элементы.ИсходящиеДокументыСпособПодтвержденияДокумента.СписокВыбора.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсходящиеДокументыСпособПодтвержденияДокументаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ИсходящиеДокументыСпособПодтвержденияПодставитьНужныйВариантВыбора(ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсходящиеДокументыСпособПодтвержденияДокументаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ИсходящиеДокументыСпособПодтвержденияПодставитьНужныйВариантВыбора(ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсходящиеДокументыСпособПодтвержденияДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИсходящиеДокументыСпособПодтвержденияПодставитьНужныйВариантВыбора(ДанныеВыбора, СтандартнаяОбработка)
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСертификатыБанка

&НаКлиенте
Процедура СертификатыБанкаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СертификатыБанкаВыгрузить" Тогда
		ПараметрыСохранения = ФайловаяСистемаКлиент.ПараметрыСохраненияФайла();
		ПараметрыСохранения.Диалог.Заголовок = НСтр("ru = 'Выберите файл для сохранения сертификата'");
		ПараметрыСохранения.Диалог.Фильтр = НСтр("ru = 'Файлы сертификатов (*.cer)|*.cer|Все файлы (*.*)|*.*'");
		ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(Элемент.ТекущиеДанные.Данные);
		ИмяФайла = "BankCertificate.cer";
		АдресСертификата = ПоместитьВоВременноеХранилище(ДвоичныеДанныеСертификата, УникальныйИдентификатор);
		ФайловаяСистемаКлиент.СохранитьФайл(Неопределено, АдресСертификата, ИмяФайла, ПараметрыСохранения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ПараметрыЗаписи = Новый Структура("ЗакрытьПослеЗаписи", Истина);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСертификат(Команда)
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = НСтр("ru = 'Необходимо выбрать организацию'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Объект.Организация", , Отказ);
	КонецЕсли;
	
	Если (Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку")
			ИЛИ Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК"))
		И НЕ ЗначениеЗаполнено(Объект.ИмяВнешнегоМодуля) Тогда
		ТекстСообщения = НСтр("ru = 'Необходимо загрузить внешний модуль'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;

	Если НЕ Отказ Тогда
		ЗаписатьНастройкуОбмена();
		Если Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
			НачатьЗагрузкуСертификатаЧерезДополнительнуюОбработку(Истина);
		ИначеЕсли Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК") Тогда
			ЗагрузитьСертификатЧерезВК();
		ИначеЕсли Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
			ПолучитьДанныеСертификатаНаТокенеСбербанка();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТестНастроек(Команда)
	
	ОчиститьСообщения();
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПровестиТестНастройкиОбмена", ЭтотОбъект);
	Если Модифицированность ИЛИ НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ТекстВопроса = НСтр("ru = 'Необходимо сохранить текущую настройку обмена. Продолжить выполнение теста?'");
		СписокКнопок = Новый СписокЗначений();
		СписокКнопок.Добавить(Истина, НСтр("ru = 'Сохранить и выполнить тест'"));
		СписокКнопок.Добавить(Ложь, НСтр("ru = 'Отменить тест'"));
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, СписокКнопок, , Истина, НСтр("ru = 'Тест настроек'"));
	Иначе
		ПровестиТестНастройкиОбмена();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДополнительныеПараметры(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаОбмена", Объект.Ссылка);
	ПараметрыФормы.Вставить("РежимОткрытияОкна", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ОткрытьФорму("РегистрСведений.ПараметрыОбменСБанками.Форма.РедактированиеЗаписи", ПараметрыФормы, ЭтотОбъект,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНастройки(Команда)

	ОчиститьСообщения();
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("Банк", Объект.Банк);
	ПараметрыФормы.Вставить("ИдентификаторОрганизации", Объект.ИдентификаторОрганизации);
	ПараметрыФормы.Вставить("НеПоказыватьСтраницуИнформации", Истина);
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ПослеЗавершенияРаботыПомощника", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.НастройкиОбменСБанками.Форма.ПомощникСозданияНастройкиОбмена", ПараметрыФормы, , , , ,
		ОбработчикОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСертификатБанка(Команда)
	
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.Диалог.Заголовок = НСтр("ru = 'Выберите сертификат банка'");
	ПараметрыЗагрузки.Диалог.Фильтр = НСтр("ru = 'Файлы сертификата (*.cer)|*.cer|Все файлы (*.*)|*.*'");
	ПослеВыбораФайла = Новый ОписаниеОповещения("ПослеВыбораФайлаСертификатаБанка", ЭтотОбъект);
	ФайловаяСистемаКлиент.ЗагрузитьФайл(ПослеВыбораФайла, ПараметрыЗагрузки);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВерсииФормата

&НаСервере
Процедура ОформитьВерсиюФорматаОбмена()
	
	ИзвестныеВерсииФормата = Новый Массив;
	АктуальныеВерсииФормата = ОбменСБанкамиСлужебный.АктуальныеВерсииФормата();
	СписокЗаполнен = Элементы.ВерсияФормата.СписокВыбора.Количество() > 0;
	Если Объект.ПрограммаБанка = Перечисления.ПрограммыБанка.АсинхронныйОбмен
		ИЛИ Объект.ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезВК Тогда
		ВерсииСхемАсинхронногоОбмена = ОбменСБанкамиСлужебный.ВерсииСхемАсинхронногоОбмена();
		Для каждого ЭлементКоллекции Из ВерсииСхемАсинхронногоОбмена Цикл
			ИзвестныеВерсииФормата.Добавить(ЭлементКоллекции.Ключ);
			Если НЕ СписокЗаполнен Тогда
				Если АктуальныеВерсииФормата.Найти(ЭлементКоллекции.Ключ) = Неопределено Тогда
					Элементы.ВерсияФормата.СписокВыбора.Добавить(
						ЭлементКоллекции.Ключ, ЭлементКоллекции.Ключ + " " + НСтр("ru = '(устарела)'"));
				Иначе
					Элементы.ВерсияФормата.СписокВыбора.Добавить(ЭлементКоллекции.Ключ, ЭлементКоллекции.Ключ);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Элементы.ВерсияФормата.СписокВыбора.СортироватьПоЗначению();
		Если ИзвестныеВерсииФормата.Найти(Объект.ВерсияФормата) = Неопределено Тогда
			ШаблонПредупреждения = НСтр("ru = 'Указана неизвестная версия формата обмена.
											|Документы будут отправляться в банк в формате %1'");
			БазоваяВерсияФормата = ОбменСБанкамиКлиентСервер.БазоваяВерсияФорматаАсинхронногоОбмена();
			ТекстПредупреждения = СтрШаблон(ШаблонПредупреждения, БазоваяВерсияФормата);
		КонецЕсли;
		Если ИзвестныеВерсииФормата.Найти(Объект.ВерсияФормата) <> Неопределено
			И АктуальныеВерсииФормата.Найти(Объект.ВерсияФормата) = Неопределено Тогда
			ТекстПредупреждения = НСтр("ru = 'Используется устаревшая версия формата обмена.'");
		КонецЕсли;
		Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
			Элементы.ПредупреждениеВерсииФормата.Заголовок = ТекстПредупреждения;
			Элементы.ГруппаДоступныеВидыЭД.Картинка = БиблиотекаКартинок.ПредупреждениеБЭД;
		Иначе
			Элементы.ПредупреждениеВерсииФормата.Заголовок = "";
			Элементы.ГруппаДоступныеВидыЭД.Картинка = Новый Картинка();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область Сбербанк

&НаСервере
Процедура ЗаполнитьТаблицуСертификатовБанка()
	
	СправочникОбъект = РеквизитФормыВЗначение("Объект");
	
	СертификатыБанка.Очистить();
	
	Для Каждого Элемент Из СправочникОбъект.СертификатыБанка Цикл
		НовСтрока = СертификатыБанка.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтрока, Элемент);
		НовСтрока.Данные = ПоместитьВоВременноеХранилище(Элемент.Данные.Получить(), УникальныйИдентификатор);
		НовСтрока.СохраненВИнформационнойБазе = Истина;
	КонецЦикла
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайлаСертификатаБанка(ПомещенныйФайл, ДополнительныеПараметры) Экспорт
	
	Если ПомещенныйФайл = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьСертификатБанкаНаСервере(ПомещенныйФайл.Хранение);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСертификатБанкаНаСервере(Знач Хранение)

	ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(Хранение);
	ДвоичныеДанныеСертификата = ОбменСБанкамиСлужебный.ДанныеСертификатаВФорматеDER(ДвоичныеДанныеСертификата);
	Если ДвоичныеДанныеСертификата = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Выбранный файл не содержит сертификат криптографии.
									|Необходимо выбрать файл сертификата, скачанного из личного кабинета банка.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;

	Попытка	
		СертификатПодписи = Новый СертификатКриптографии(ДвоичныеДанныеСертификата);
	Исключение
		ТекстСообщения = НСтр("ru = 'Выбранный файл не содержит сертификат криптографии.
									|Необходимо выбрать файл сертификата, скачанного из личного кабинета банка.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецПопытки;
	СвойстваСертификата = ЭлектроннаяПодпись.СвойстваСертификата(СертификатПодписи);
	СерийныйНомер = НРег(СтрЗаменить(Строка(СвойстваСертификата.СерийныйНомер), " ", "")); 
	
	ПараметрыОтбора = Новый Структура("СерийныйНомер", СерийныйНомер);
	Если СертификатыБанка.НайтиСтроки(ПараметрыОтбора).Количество() Тогда
		ТекстСообщения = НСтр("ru = 'Выбранный сертификат уже есть в списке'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	НовСтрока = СертификатыБанка.Добавить();
	НовСтрока.Наименование = СвойстваСертификата.Представление;
	НовСтрока.СерийныйНомер = СерийныйНомер;
	НовСтрока.Данные = ПоместитьВоВременноеХранилище(ДвоичныеДанныеСертификата, УникальныйИдентификатор);
	НовСтрока.ДатаОкончания = СвойстваСертификата.ДатаОкончания;
	НовСтрока.СохраненВИнформационнойБазе = Ложь;
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьДанныеСертификатаНаТокенеСбербанка()
	
	Обработчик = Новый ОписаниеОповещения("ПолучитьИдентификаторСертификатаСбербанка", ЭтотОбъект);
	ОбменСБанкамиСлужебныйКлиент.АутентифицироватьсяНаТокенеСбербанка(
		Обработчик, Объект.ИмяВнешнегоМодуля, Объект.Ссылка, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораСертификатаСбербанка(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторСертификата = Результат.Значение;
	
	ДвоичныеДанныеСертификата = ДополнительныеПараметры.СоответствиеСертификатов.Получить(ИдентификаторСертификата);
	
	НовыйСертификат = ОбменСБанкамиСлужебныйВызовСервера.СоздатьСертификатСбербанка(
		ДвоичныеДанныеСертификата, Объект.Организация);
	Если НЕ ЗначениеЗаполнено(НовыйСертификат) Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура("СертификатЭП", НовыйСертификат);
	Если Объект.СертификатыПодписейОрганизации.НайтиСтроки(Отбор).Количество() Тогда
		ТекстСообщения = НСтр("ru = 'Выбранный сертификат уже есть в списке.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	Иначе
		СтрокаНастройки = Объект.СертификатыПодписейОрганизации.Добавить();
		СтрокаНастройки.СертификатЭП = НовыйСертификат;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИдентификаторСертификатаСбербанка(АутентификацияВыполнена, ДополнительныеПараметры = Неопределено) Экспорт

	Если Не АутентификацияВыполнена Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеПодключенияВКСбербанк", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.ПодключитьКомпонентуСбербанк(Оповещение, Объект.ИмяВнешнегоМодуля);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодключенияВКСбербанк(Результат, ДополнительныеПараметры) Экспорт

	Если Не Результат.Подключено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ОписаниеОшибки);
		Возврат;
	КонецЕсли;
	
	ПодключаемыйМодуль = Результат.ПодключаемыйМодуль;

	Оповещение = Новый ОписаниеОповещения(
		"ВыбратьСертификатПослеПолученияСпискаСертификатовСТокенаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьДанныеСертификатовСТокенаСбербанк(Оповещение, ПодключаемыйМодуль);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСертификатПослеПолученияСпискаСертификатовСТокенаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	СоответствиеСертификатов = Результат.СоответствиеСертификатов;
	ДополнительныеПараметры = Новый Структура("СоответствиеСертификатов", СоответствиеСертификатов);

	
	СписокСертификатов = Новый СписокЗначений;
	Если СоответствиеСертификатов.Количество() = 1 Тогда
		Для Каждого КлючЗначение Из СоответствиеСертификатов Цикл
			ОбработкаВыбораСертификатаСбербанка(СписокСертификатов.Добавить(КлючЗначение.Ключ), ДополнительныеПараметры);
			Возврат;
		КонецЦикла
	КонецЕсли;
	
	ВыборкаСертификатов = Новый Соответствие;
	
	Для Каждого Элемент Из СоответствиеСертификатов Цикл
		
		ИдентификаторСертификата = Элемент.Ключ;
		ДвоичныеДанныеСертификата = Элемент.Значение;
		
		Если ДвоичныеДанныеСертификата = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		СтруктураСертификата = ОбменСБанкамиСлужебныйВызовСервера.СтруктураСертификата(ДвоичныеДанныеСертификата);
		
		Если ЗначениеЗаполнено(СтруктураСертификата) Тогда
			ШаблонПредставления = НСтр("ru = '%1, до %2'");
			ДатаСтрокой = Формат(СтруктураСертификата.ДействителенДО, "ДФ=MM.yyyy");
			Представление = СтрШаблон(ШаблонПредставления, СтруктураСертификата.КомуВыдан, ДатаСтрокой);
			ВыборкаСертификатов.Вставить(ИдентификаторСертификата, Представление);
			
		КонецЕсли;
		
	КонецЦикла;

	Если ВыборкаСертификатов.Количество() = 1 Тогда
		Для Каждого КлючЗначение Из ВыборкаСертификатов Цикл
			ОбработкаВыбораСертификатаСбербанка(СписокСертификатов.Добавить(КлючЗначение.Ключ), ДополнительныеПараметры);
			Возврат;
		КонецЦикла
	КонецЕсли;
	
	Для Каждого Элемент Из ВыборкаСертификатов Цикл
		СписокСертификатов.Добавить(Элемент.Ключ, Элемент.Значение);
	КонецЦикла;
	
	
	Оповещение = Новый ОписаниеОповещения("ОбработкаВыбораСертификатаСбербанка", ЭтотОбъект, ДополнительныеПараметры);
	
	ЗаголовокФормыВыбора = НСтр("ru = 'Выберите сертификат подписи'");

	СписокСертификатов.ПоказатьВыборЭлемента(Оповещение, ЗаголовокФормыВыбора);
	
КонецПроцедуры

#КонецОбласти

#Область ОбменЧерезВК

&НаКлиенте
Процедура ПослеПолученияСертификатаЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат);
		Возврат;
	КонецЕсли;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	НовыйСертификат = НайтиСоздатьСертификатВнешнегоМодуля(
		Результат.СертификатBase64, Объект.Организация, Объект.ПрограммаБанка, Результат);
	
	Отбор = Новый Структура("СертификатЭП", НовыйСертификат);
	Если Объект.СертификатыПодписейОрганизации.НайтиСтроки(Отбор).Количество() Тогда
		ТекстСообщения = НСтр("ru = 'Выбранный сертификат уже есть в списке.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	Иначе
		СтрокаСертификата = Объект.СертификатыПодписейОрганизации.Добавить();
		СтрокаСертификата.СертификатЭП = НовыйСертификат;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодключенияВК(ПодключаемыйМодуль, ДополнительныеПараметры) Экспорт
	
	Если ПодключаемыйМодуль = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияСертификатаЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыСоединения = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыСоединенияВК(Объект.Ссылка);
	ОбменСБанкамиСлужебныйКлиент.ПолучитьДанныеСертификатаСТокенаВК(Оповещение, ПодключаемыйМодуль, ПараметрыСоединения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСертификатЧерезВК()
	
	Оповещение = Новый ОписаниеОповещения("ПослеПодключенияВК", ЭтотОбъект);
	ОбменСБанкамиСлужебныйКлиент.ПодключитьИИнициализироватьВК(Оповещение, Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбменЧерезДополнительнуюОбработку

&НаКлиенте
Процедура ПродолжитьПолучениеСертификатаПослеВводаPinКода(PINКод, ПараметрыПолучения) Экспорт
	
	ИдентификаторХранилища = ПараметрыПолучения.ИдентификаторХранилища;
	
	Если PINКод = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВнешнийПодключаемыйМодуль = ОбменСБанкамиСлужебныйКлиент.ВнешнийПодключаемыйМодульЧерезДополнительнуюОбработку(
		Объект.ДополнительнаяОбработка);
		
	PINУстановлен = ОбменСБанкамиСлужебныйКлиент.УстановитьPINКодХранилищаЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль, ИдентификаторХранилища, PINКод);
		
	Если Не PINУстановлен Тогда
		Возврат;
	КонецЕсли;
	
	ПродолжитьПолучениеСертификата(ПараметрыПолучения.ИдентификаторХранилища);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияСертификатаЧерезДополнительнуюОбработку(ДанныеСертификата, ДополнительныеПараметры) Экспорт
	
	Если ДанныеСертификата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НовыйСертификат = НайтиСоздатьСертификатВнешнегоМодуля(
		ДанныеСертификата.СертификатXML, Объект.Организация, Объект.ПрограммаБанка, ДанныеСертификата);
		
	Отбор = Новый Структура("СертификатЭП", НовыйСертификат);
	Если Объект.СертификатыПодписейОрганизации.НайтиСтроки(Отбор).Количество() Тогда
		ТекстСообщения = НСтр("ru = 'Выбранный сертификат уже есть в списке.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	Иначе
		СтрокаСертификата = Объект.СертификатыПодписейОрганизации.Добавить();
		СтрокаСертификата.СертификатЭП = НовыйСертификат;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗагрузкуСертификатаЧерезДополнительнуюОбработку(Результат, ПараметрыОбработки = Неопределено) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	АдресВК = Неопределено;
	ВнешнийПодключаемыйМодуль = ОбменСБанкамиСлужебныйКлиент.ВнешнийПодключаемыйМодульЧерезДополнительнуюОбработку(
		Объект.ДополнительнаяОбработка, АдресВК);

	Если ВнешнийПодключаемыйМодуль = Неопределено Тогда
		Если ЗначениеЗаполнено(АдресВК) Тогда
			Обработчик = Новый ОписаниеОповещения("НачатьЗагрузкуСертификатаЧерезДополнительнуюОбработку", ЭтотОбъект);
			ОбменСБанкамиСлужебныйКлиент.УстановитьВнешнююКомпонентуДляОбменаЧерезОбработку(Обработчик, АдресВК);
		КонецЕсли;
		Возврат;
	КонецЕсли;

	Устройства = ОбменСБанкамиСлужебныйКлиент.ПодключенныеХранилищаЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль);
	Если Устройства=Неопределено ИЛИ Устройства.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ПараметрыВыбораТокена = Новый Структура;
	ПараметрыВыбораТокена.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
	
	Если Устройства.Количество() = 1 Тогда
		ИдентификаторХранилища = Устройства[0];
		ОбработкаВыбораХранилищаЧерезДополнительнуюОбработку(ИдентификаторХранилища, ПараметрыВыбораТокена);
	Иначе
		Обработчик = Новый ОписаниеОповещения(
			"ОбработкаВыбораХранилищаЧерезДополнительнуюОбработку", ЭтотОбъект, ПараметрыВыбораТокена);
		ОбменСБанкамиСлужебныйКлиент.ВыбратьХранилищеЧерезДополнительнуюОбработку(
			Объект.Ссылка, Обработчик, ПараметрыВыбораТокена);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораХранилищаЧерезДополнительнуюОбработку(ИдентификаторХранилища, ПараметрыОбработки) Экспорт
	
	Если Не ЗначениеЗаполнено(ИдентификаторХранилища) Тогда
		Возврат;
	КонецЕсли;
		
	ПараметрыОбработки.Вставить("ИдентификаторХранилища", ИдентификаторХранилища);
	
	ВнешнийПодключаемыйМодуль = ПараметрыОбработки.ВнешнийПодключаемыйМодуль;
	
	ТребуетсяУстановкаPINКода = ОбменСБанкамиСлужебныйКлиент.НеобходимаУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль, ИдентификаторХранилища);
		
	Если ТребуетсяУстановкаPINКода = Неопределено Тогда
		Возврат;
	ИначеЕсли ТребуетсяУстановкаPINКода Тогда
		ОповещениеОЗакрытии = Новый ОписаниеОповещения(
			"ПродолжитьПолучениеСертификатаПослеВводаPinКода", ЭтотОбъект, ПараметрыОбработки);
		ОбменСБанкамиСлужебныйКлиент.НачатьУстановкуPINКодаХранилища(
			Объект.Ссылка, ИдентификаторХранилища, ОповещениеОЗакрытии);
		Возврат;
	КонецЕсли;
	
	ПродолжитьПолучениеСертификата(ПараметрыОбработки.ИдентификаторХранилища)
	
КонецПроцедуры

#КонецОбласти

#Область ВнешнийМодуль

&НаКлиенте
Процедура ПослеЗагрузкиКомпонентыИзФайла(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Загружена Тогда
		Если Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
			Если НЕ ОбменСБанкамиСлужебныйКлиент.ПоддерживаетсяВерсияКомпонентыСбербанк(Результат.Версия) Тогда
				Возврат;
			КонецЕсли;
			ОбменСБанкамиСлужебныйКлиент.ОчиститьДанныеАвторизацииСбербанк();
		КонецЕсли;
		ЗавершитьОбновлениеКомпонентыИзФайла(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьОбновлениеКомпонентыИзФайла(ИнформацияОВКБанка)
	
	Объект.ИмяВнешнегоМодуля = ИнформацияОВКБанка.Идентификатор;
	
	Модифицированность = Истина;
	ПредставлениеКомпоненты = НСтр("ru = '%1. Версия %2'");
	Элементы.ИнформацияОВнешнемМодуле.Заголовок = СтрШаблон(
		ПредставлениеКомпоненты, ИнформацияОВКБанка.Наименование, ИнформацияОВКБанка.Версия);
	Элементы.ИнформацияОВнешнемМодуле.Видимость = Истина;
	Элементы.ЗагрузитьВнешнийМодульИзФайла.Заголовок = НСтр("ru = 'Обновить ...'");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИнформациюОВнешнемМодуле()

	ИспользуетсяВнешняяКомпонента = 
		Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК")
			ИЛИ Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн");
			
	Если ИспользуетсяВнешняяКомпонента Тогда
		Если ЗначениеЗаполнено(Объект.ИмяВнешнегоМодуля) Тогда
			ФлагПоказыватьВнешнююКомпоненту = Истина;
			Если Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
				Если ОбщегоНазначенияКлиент.ЭтоWindowsКлиент() Тогда
					ЭтоWindows7 = ОбменСБанкамиСлужебныйКлиент.ЭтоWindows7();
					ФлагПоказыватьВнешнююКомпоненту = ?(ЭтоWindows7 = Истина, Ложь, Истина);
				КонецЕсли;
				ЭтотОбъект.Элементы.ЗагрузитьВнешнийМодульИзФайла.Видимость = ФлагПоказыватьВнешнююКомпоненту;
				Если НЕ ФлагПоказыватьВнешнююКомпоненту Тогда
					ЭтотОбъект.Элементы.ИнформацияОВнешнемМодуле.Заголовок = НСтр("ru = 'VpnKey-TLS для 1С:Предприятия 8. Версия 1.2.9.6.'");
				КонецЕсли;
			КонецЕсли;
			Если ФлагПоказыватьВнешнююКомпоненту Тогда
				ДанныеВнешнегоМодуля = ВнешниеКомпонентыВызовСервера.ИнформацияОКомпоненте(Объект.ИмяВнешнегоМодуля);
				Если ДанныеВнешнегоМодуля.Существует Тогда
					Шаблон = НСтр("ru = '%1. Версия %2.'");
					ЭтотОбъект.Элементы.ИнформацияОВнешнемМодуле.Заголовок = СтрШаблон(
						Шаблон, ДанныеВнешнегоМодуля.Наименование, ДанныеВнешнегоМодуля.Версия);
				Иначе
					ЭтотОбъект.Элементы.ИнформацияОВнешнемМодуле.Заголовок = ДанныеВнешнегоМодуля.ОписаниеОшибки;
				КонецЕсли;
			КонецЕсли;
		Иначе
			ПоказатьИнформациюОбОтсутствииВнешнегоМодуля();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиСоздатьСертификатВнешнегоМодуля(Знач ДанныеСертификата, Знач Организация, Знач ПрограммаБанка, Знач СвойстваСертификата)
	
	Возврат ОбменСБанкамиСлужебный.НайтиСоздатьСертификатВнешнегоМодуля(
		ДанныеСертификата, Организация, ПрограммаБанка, СвойстваСертификата);
	
КонецФункции

#КонецОбласти

#Область ТестНастройки

&НаКлиенте
Процедура ПровестиТестНастройкиОбмена(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Истина Тогда
		Записать();
	ИначеЕсли Результат = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ОбменСБанкамиКлиентСервер.ЗаполненыРеквизитыНастройкиОбмена(Объект, Истина) Тогда
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ПослеТестаНастройкиОбмена", ЭтотОбъект);
	ОбменСБанкамиСлужебныйКлиент.ПровестиТестНастройки(Обработчик, Объект.Ссылка, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ПослеТестаНастройкиОбмена(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена ИЛИ Результат = Неопределено Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Тест не завершен.'"));
	ИначеЕсли ТипЗнч(Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат);
	ИначеЕсли Результат = Истина ИЛИ (ТипЗнч(Результат) = Тип("Структура") И Результат.Успех) Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Тестирование выполнено успешно.'"));
	Иначе
		Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("МассивСообщений") Тогда
			Для Каждого Сообщение Из Результат.МассивСообщений Цикл
				Сообщение.Сообщить();
			КонецЦикла
		КонецЕсли;
		ПоказатьОповещениеПользователя(НСтр("ru = 'Тест не пройден.'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Сертификаты

&НаСервере
Процедура УдалитьПустыеСтрокиТаблиц()
	
	СписокСтрокКУдалению = Новый СписокЗначений;
	Для каждого СтрокаСертификата Из Объект.СертификатыПодписейОрганизации Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаСертификата.СертификатЭП) Тогда
			СписокСтрокКУдалению.Добавить(СтрокаСертификата.НомерСтроки);
		КонецЕсли;
	КонецЦикла;

	СписокСтрокКУдалению.СортироватьПоЗначению(НаправлениеСортировки.Убыв);
	
	Для Каждого Запись Из СписокСтрокКУдалению Цикл
		Объект.СертификатыПодписейОрганизации.Удалить(Запись.Значение-1);
	КонецЦикла

КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьПолучениеСертификата(ИдентификаторХранилища)
	
	Обработчик = Новый ОписаниеОповещения("ПослеПолученияСертификатаЧерезДополнительнуюОбработку", ЭтотОбъект);
	ВнешнийПодключаемыйМодуль = ОбменСБанкамиСлужебныйКлиент.ВнешнийПодключаемыйМодульЧерезДополнительнуюОбработку(
		Объект.ДополнительнаяОбработка);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьСертификатЧерезДополнительнуюОбработку(
		Обработчик, ВнешнийПодключаемыйМодуль, ИдентификаторХранилища);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЭлементы(
	Форма,
	Знач ПрограммаБанка,
	Знач ИспользуетсяКриптография,
	Знач АутентификацияПоСертификату = Ложь,
	ДоступноАвтоматическоеПолучениеВыписки = Ложь,
	Ссылка = Неопределено)
	
	Форма.Элементы.ГруппаЛичногоКабинета.Видимость = Ложь;
	Форма.Элементы.СтраницыРазделов.Видимость = Истина;
	Форма.Элементы.ГруппаАвтоматическоеПолучениеВыписки.Видимость = Ложь;
	
	ЭтоСбербанк = ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн");
	ЭтоОбменЧерезДопОбработку = ПрограммаБанка = ПредопределенноеЗначение(
		"Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку");
	ЭтоАсинхронныйОбмен = ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен");
	ЭтоСинхронныйОбмен = ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АльфаБанкОнлайн");
	ЭтоОбменЧерезВК = ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК");
	ЭтоFintech = ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.FintechIntegration");
	
	Если ЭтоСинхронныйОбмен Тогда
		Форма.Элементы.СтраницыВидыБанковскихСистем.ТекущаяСтраница = Форма.Элементы.СтраницаСинхронный;
	ИначеЕсли ЭтоСбербанк Тогда
		НастроитьЭлементыСберБанка(Форма, ИспользуетсяКриптография)
	ИначеЕсли ЭтоАсинхронныйОбмен Тогда
		НастроитьЭлементыАсинхронногоОбмена(Форма, ИспользуетсяКриптография, АутентификацияПоСертификату,
			ДоступноАвтоматическоеПолучениеВыписки, Ссылка);
	ИначеЕсли ЭтоОбменЧерезВК Тогда
		Форма.Элементы.СтраницыВидыБанковскихСистем.ТекущаяСтраница = Форма.Элементы.СтраницаВнешнийМодуль;
		Форма.Элементы.СтраницыВнешнегоМодуля.ТекущаяСтраница = Форма.Элементы.СтраницаВнешняяКомпонента;
	ИначеЕсли ЭтоОбменЧерезДопОбработку Тогда
		Форма.Элементы.ДекорацияВнешниеОбработкиНеИспользуются.Видимость = НЕ ОбменСБанкамиСлужебныйВызовСервера.ИспользуютсяДополнительныеОтчетыИОбработки();
		Форма.Элементы.СтраницыВидыБанковскихСистем.ТекущаяСтраница = Форма.Элементы.СтраницаВнешнийМодуль;
		Форма.Элементы.СтраницыВнешнегоМодуля.ТекущаяСтраница = Форма.Элементы.СтраницаДополнительнаяОбработка;
	ИначеЕсли ЭтоFintech Тогда
		Форма.Элементы.СтраницаДоступныхНастроек.Видимость = Ложь;
		Форма.Элементы.ДекорацияСервисБанка.Заголовок = НСтр("ru = 'Сервис банка: Fintech API'");
		Форма.Элементы.ИсходящиеДокументыИспользоватьЭП.Видимость = Ложь;
		ОтобразитьГруппуПодтвержденияПлатежа(Форма.Объект, Форма.Элементы.ГруппаЛичногоКабинета);
	КонецЕсли;
	
	Форма.Элементы.СтраницыВнешнегоМодуля.Видимость = ЭтоСбербанк ИЛИ ЭтоОбменЧерезВК ИЛИ ЭтоОбменЧерезДопОбработку;
	Форма.Элементы.ТестНастроек.Видимость = НЕ ЭтоСинхронныйОбмен;
	Форма.Элементы.ТестНастроек.Доступность = ОбменСБанкамиСлужебныйВызовСервера.ПравоВыполненияОбмена()
		ИЛИ ОбменСБанкамиСлужебныйВызовСервера.ПравоНастройкиОбмена();
	Форма.Элементы.ОткрытьДополнительныеПараметры.Видимость = ЭтоАсинхронныйОбмен ИЛИ ЭтоСбербанк ИЛИ ЭтоОбменЧерезВК
														ИЛИ ЭтоFintech;
	Форма.Элементы.ЗагрузитьВнешнийМодульИзФайла.Видимость = (ЭтоОбменЧерезВК ИЛИ ЭтоСбербанк);
	Форма.Элементы.ИнформацияОВнешнемМодуле.Видимость = (ЭтоОбменЧерезВК ИЛИ ЭтоСбербанк);
	Форма.Элементы.ГруппаВерсияФормата.Видимость = ЭтоОбменЧерезВК ИЛИ ЭтоАсинхронныйОбмен;
	Форма.Элементы.ИсходящиеДокументыМаршрутПодписания.Видимость = ИспользуетсяКриптография;
	Форма.Элементы.ДекорацияПометкаУдаления.Видимость = Форма.Объект.ПометкаУдаления;
	Форма.Элементы.ДекорацияСервисБанка.Видимость = (ЭтоFintech ИЛИ ЭтоСбербанк) И Форма.ИспользуетсяFintech;
	
КонецПроцедуры

&НаСервере
Процедура ПроставитьВозможномстьВыбораСпособаПодтверждения()
	
	Для Каждого СтрокаИсходящиеДокументы Из Объект.ИсходящиеДокументы Цикл
		СписокВыбора = ПолучитьПравилоПодписанияДокумента(
			СтрокаИсходящиеДокументы.ИспользоватьЭП,
			СтрокаИсходящиеДокументы.ИспользоватьSMS,
			СтрокаИсходящиеДокументы.ИсходящийДокумент);
		СтрокаИсходящиеДокументы.ЕстьВыборСпособаПодписания = ?(СписокВыбора.Количество() > 1, Истина, Ложь);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЭлементыСберБанка(Форма, ИспользуетсяКриптография)
	
	Форма.Элементы.СтраницыВидыБанковскихСистем.ТекущаяСтраница = Форма.Элементы.СтраницаСбербанк;
	Форма.Элементы.КриптографияСбербанк.Видимость = ИспользуетсяКриптография;
	Форма.Элементы.ИсходящиеДокументыИспользоватьЭП.Видимость = ИспользуетсяКриптография;
	Форма.Элементы.СтраницыВнешнегоМодуля.ТекущаяСтраница = Форма.Элементы.СтраницаВнешняяКомпонента;
	Форма.Элементы.ИмяПользователя.Видимость = НЕ ИспользуетсяКриптография;
	Форма.Элементы.ИсходящиеДокументыСпособПодтвержденияДокумента.Видимость = Ложь;
	// 
	// Показываем флажок про подписание документов в личном кабинете Сбербанка в течение двух месяцев
	// с момента отключения этой возможности на стороне банка.
	Форма.Элементы.ОтправлятьДокументыБезПодписиSMS.Видимость = НЕ ИспользуетсяКриптография
		И ТекущаяДата() < ДобавитьМесяц(ОбменСБанкамиСлужебныйКлиентСервер.ДатаОтключенияПодписанияSMSСбербанк(), 2);
	
	Форма.Элементы.ГруппаСертификатыБанка.Видимость = ИспользуетсяКриптография;
	Форма.Элементы.ДекорацияСервисБанка.Заголовок = НСтр("ru = 'Сервис банка: Универсальный платежный шлюз (УПШ)'");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЭлементыАсинхронногоОбмена(
	Форма,
	ИспользуетсяКриптография,
	АутентификацияПоСертификату,
	ДоступноАвтоматическоеПолучениеВыписки,
	Ссылка)
	
	Форма.Элементы.СтраницыВидыБанковскихСистем.ТекущаяСтраница = Форма.Элементы.СтраницаАсинхронный;
	Если ИспользуетсяКриптография Тогда
		Если АутентификацияПоСертификату Тогда
			Форма.Элементы.ЛогинАсинхронныйОбмен.Видимость = Ложь;
		Иначе
			Форма.Элементы.ЛогинАсинхронныйОбмен.Видимость = Истина;
		КонецЕсли;
		Форма.Элементы.ГруппаСертификатыАсинхронный.Видимость = Истина;
	Иначе
		Форма.Элементы.ГруппаСертификатыАсинхронный.Видимость = Ложь;
		Форма.Элементы.ЛогинАсинхронныйОбмен.Видимость = Истина;
	КонецЕсли;
	
	Если ДоступноАвтоматическоеПолучениеВыписки Тогда
		ПараметрыАвтоматическогоПолученияВыписки = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыАвтоматическогоПолученияВыписки(
			Ссылка);
		Если ПараметрыАвтоматическогоПолученияВыписки.АвтоматическоеПолучениеВыписки = Истина Тогда
			Форма.Элементы.ГруппаДоступноАвтоматическоеПолучениеВыписки.Видимость = Ложь;
			Форма.Элементы.ГруппаЗапущеноАвтоматическоеПолучениеВыписки.Видимость = Истина;
			МассивСтрокОписаний = Новый Массив;
			МассивСтрокОписаний.Добавить(НСтр("ru = '* Запущено автоматическое получение выписки банка.
													|Последняя выписка получена:'") + " ");
			ЖирныйШрифтБЭД = ЖирныйШрифтБЭДНаСервере();
			ДатаПолученияВыписки = ПараметрыАвтоматическогоПолученияВыписки.ОтносительнаяДатаПолученияВыписки;
			МассивСтрокОписаний.Добавить(Новый ФорматированнаяСтрока(ДатаПолученияВыписки, ЖирныйШрифтБЭД));
			Форма.Элементы.ЗапущеноАвтоматическоеПолучениеВыписки.Заголовок = Новый ФорматированнаяСтрока(
				МассивСтрокОписаний);
		Иначе
			Форма.Элементы.ГруппаЗапущеноАвтоматическоеПолучениеВыписки.Видимость = Ложь;
			Форма.Элементы.ГруппаДоступноАвтоматическоеПолучениеВыписки.Видимость = Ложь;
			Если МожноПоказатьПодключениеАвтоматическойПолучениеВыписки(Ссылка) Тогда
				Форма.Элементы.ГруппаДоступноАвтоматическоеПолучениеВыписки.Видимость = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыАвтоматическогоПолученияВыписки.ТекстОшибки) Тогда
			Форма.ТекстОшибкиВыписки = ПараметрыАвтоматическогоПолученияВыписки.ТекстОшибки;
		Иначе
			Форма.Элементы.ГруппаОшибка.Видимость = Ложь;
		КонецЕсли;
		Форма.Элементы.ГруппаАвтоматическоеПолучениеВыписки.Видимость = Истина;
	КонецЕсли;
	
	ОтобразитьГруппуПодтвержденияПлатежа(Форма.Объект, Форма.Элементы.ГруппаЛичногоКабинета);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция МожноПоказатьПодключениеАвтоматическойПолучениеВыписки(НастройкаОбменСБанками)
	
	Возврат ОбменСБанкамиСлужебный.ДоступенЗапросВыписки(НастройкаОбменСБанками);
	
КонецФункции

&НаСервереБезКонтекста
Функция ЖирныйШрифтБЭДНаСервере()
	
	Возврат ШрифтыСтиля.ЖирныйШрифтБЭД;
	
КонецФункции

&НаСервере
Процедура ЗаписатьНастройкуОбмена()
	
	СправочникОбъект = РеквизитФормыВЗначение("Объект");
	ПередЗаписьюНаСервере(Неопределено, СправочникОбъект, Неопределено);
	СправочникОбъект.Записать();
	ЗначениеВРеквизитФормы(СправочникОбъект, "Объект");
	ЗаполнитьСлужебныеРеквизитыТаблицыИсходящихДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗавершенияРаботыПомощника(НастройкаОбмена, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(НастройкаОбмена) Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьОбъектФормы(НастройкаОбмена);
	
	Прочитать();
	
	НастроитьЭлементы(ЭтотОбъект, Объект.ПрограммаБанка, Объект.ИспользуетсяКриптография,
		Объект.АутентификацияПоСертификату, Объект.ДоступноАвтоматическоеПолучениеВыписки, Объект.Ссылка);

	ПроставитьВозможномстьВыбораСпособаПодтверждения();
	
	ЗаполнитьИнформациюОВнешнемМодуле();
	
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьОбъектФормы(НастройкаОбмена)
	
	НастройкаОбменаОбъект = НастройкаОбмена.ПолучитьОбъект();
	ЗначениеВРеквизитФормы(НастройкаОбменаОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	СнятьПометкуНезаполненныхПолейДляНедействительнойНастройки();
	
	НастроитьДоступностьМаршрутаПодписания();
	
	ПроставитьВозможномстьВыбораСпособаПодтверждения();
	
	НастроитьПодсвечиваниеВыбораСпособаПодтверждения();
	
	НастроитьДоступностьФлажкаПодтвержденияДокументовВЛичномКабинетеБанка();
	
	УстановитьНеобходимостьУказанияАдресаПодтвержденияПлатежей();
	
КонецПроцедуры

&НаСервере
Процедура СнятьПометкуНезаполненныхПолейДляНедействительнойНастройки()
 
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Организация.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.РесурсПриемник.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.РесурсИсточник.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Банк.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.АдресСервера.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СертификатыБанка.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИдентификаторОрганизации.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.АдресСервераАсинхронный.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИдентификаторОрганизацииАсинхронный.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Недействительна");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);

КонецПроцедуры

&НаСервере
Процедура НастроитьДоступностьМаршрутаПодписания()
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИсходящиеДокументыМаршрутПодписания.Имя);
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ИсходящиеДокументы.ИспользоватьЭП");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ИсходящиеДокументы.РазрешенВыборМаршрута");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступныйДляВыбораЭлементБЭД);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьПодсвечиваниеВыбораСпособаПодтверждения()
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИсходящиеДокументыСпособПодтвержденияДокумента.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ИсходящиеДокументы.ЕстьВыборСпособаПодписания");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступныеДанныеЭДЦвет);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИсходящиеДокументыСпособПодтвержденияДокумента.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ИсходящиеДокументы.СпособПодтвержденияДокумента");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СпособыПодтвержденияДокументовОбменСБанками.ПустаяСсылка();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", "Авто");
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступныеДанныеЭДЦвет);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьДоступностьФлажкаПодтвержденияДокументовВЛичномКабинетеБанка()
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИсходящиеДокументыПодписыватьВБанке.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ИсходящиеДокументы.ИспользоватьЭП");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ИсходящиеДокументы.ИсходящийДокумент");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;
	СписокОтбора = Новый СписокЗначений;
	СписокОтбора.ЗагрузитьЗначения(ОбменСБанкамиСлужебный.ВидыПлатежныхДокументов());
	ОтборЭлемента.ПравоеЗначение = СписокОтбора;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИсходящиеДокументыПодписыватьВБанке.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ПрограммаБанка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ПрограммыБанка.АсинхронныйОбмен;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЕстьПодтверждаемыйДокумент");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНеобходимостьУказанияАдресаПодтвержденияПлатежей()
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.АдресЛичногоКабинета.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ПоказыватьОкноПодтвержденияПлатежей");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Недействительна");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
 КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыТаблицыИсходящихДокументов()
	ВидыДокументовПодписываемыхПоМаршруту = ОбменСБанкамиСлужебныйПовтИсп.ВидыДокументовПодписываемыхПоМаршруту();
	
	Для Каждого СтрокаТаблицы Из Объект.ИсходящиеДокументы Цикл
		СтрокаТаблицы.МаршрутЗависимОтОрганизации = МаршрутЗависимОтОрганизации(СтрокаТаблицы.МаршрутПодписания);
		СтрокаТаблицы.РазрешенВыборМаршрута = ВидыДокументовПодписываемыхПоМаршруту.Найти(СтрокаТаблицы.ИсходящийДокумент) <> Неопределено;
	КонецЦикла;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЕстьПодтверждаемыйДокумент(Объект)
	
	ЕстьПодтверждаемыйДокумент = Ложь;
	Для каждого ЭлементКоллекции Из Объект.ИсходящиеДокументы Цикл
		Если ЭлементКоллекции.ПодтвердитьВБанке Тогда
			ЕстьПодтверждаемыйДокумент = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЕстьПодтверждаемыйДокумент;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОтобразитьГруппуПодтвержденияПлатежа(Объект, Группа)
	
	ЕстьПодтверждаемыйДокумент = ЕстьПодтверждаемыйДокумент(Объект);
	
	Группа.Видимость = ЕстьПодтверждаемыйДокумент;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементовПоРолям()
	
	Если НЕ Пользователи.ЭтоПолноправныйПользователь() Тогда
		Элементы.СертификатыБанкаВыгрузить.Видимость = Ложь;
	КонецЕсли;
	
	Если НЕ ПравоДоступа("Изменение", Метаданные.Справочники.НастройкиОбменСБанками) Тогда
		Элементы.ЗагрузитьВнешнийМодульИзФайла.Видимость = Ложь;
		Элементы.ЗагрузитьНастройки.Доступность = Ложь;
		Элементы.ВключитьАвтоматическоеПолучениеВыписки.Доступность = Ложь;
		Элементы.ОстановитьАвтоматическоеПолучениеВыписки.Доступность = Ложь;
		Элементы.ИсходящиеДокументыПодписыватьВБанке.Доступность = Ложь;
		Элементы.ОткрытьДополнительныеПараметры.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область АсинхронныйОбмен

&НаКлиенте
Процедура ПослеВключенияАвтоматическогоПолученияВыписки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		НастроитьЭлементы(ЭтотОбъект, Объект.ПрограммаБанка, Объект.ИспользуетсяКриптография,
			Объект.АутентификацияПоСертификату, Истина, Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область МаршрутыПодписания

&НаКлиенте
Процедура ВопросОЗаписиПолученОтвет(Ответ, ПараметрыЗаписи) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОчиститьСообщения();
		НовыеПараметрыЗаписи = Новый Структура;
		НовыеПараметрыЗаписи.Вставить("ПропуститьПроверки");
		Для Каждого КлючИЗначение Из ПараметрыЗаписи Цикл
			НовыеПараметрыЗаписи.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;

		Записать(НовыеПараметрыЗаписи);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция НастройкаВалидирована(ЕстьОшибкиЗаполнения = Ложь)
	Если НЕ ПроверитьЗаполнение() Тогда
		ЕстьОшибкиЗаполнения = Истина;	
	КонецЕсли;
	
	ЕстьОшибкиВЗависимыхНастройках = Ложь;
	Если Не ЕстьОшибкиЗаполнения Тогда
		// Проверим, что все указанные маршруты возможно выполнить
		ПроверитьСоответствиеСертификатовМаршрутам(ЕстьОшибкиВЗависимыхНастройках);
	КонецЕсли;
	
	Возврат НЕ ЕстьОшибкиЗаполнения И Не ЕстьОшибкиВЗависимыхНастройках;

КонецФункции

&НаСервере
Процедура ПроверитьСоответствиеСертификатовМаршрутам(Отказ)
	
	СертификатыДляПодписи = Объект.СертификатыПодписейОрганизации.Выгрузить(,"СертификатЭП").ВыгрузитьКолонку("СертификатЭП");
	
	// Подготовим соответствие Маршрут - виды документов маршрута
	СоответствиеВидовЭДМаршрутам = Новый Соответствие;
	Для Каждого СтрокаИсходящегоДокумента Из Объект.ИсходящиеДокументы Цикл
		Если СтрокаИсходящегоДокумента.СпособПодтвержденияДокумента = Перечисления.СпособыПодтвержденияДокументовОбменСБанками.ЭлектроннаяПодпись Тогда
			МаршрутПодписания 	= СтрокаИсходящегоДокумента.МаршрутПодписания;
			ВидЭД				= СтрокаИсходящегоДокумента.ИсходящийДокумент;
			
			МассивЭД = СоответствиеВидовЭДМаршрутам[МаршрутПодписания];
			Если МассивЭД = Неопределено Тогда
				МассивЭД = Новый Массив;
				СоответствиеВидовЭДМаршрутам.Вставить(МаршрутПодписания, МассивЭД);
			КонецЕсли;
			МассивЭД.Добавить(ВидЭД);
		КонецЕсли;
	КонецЦикла;
	
	// Проверим валидность каждого маршрута в соответствии с установленными настройками
	Для Каждого ЭлементСоответствия Из СоответствиеВидовЭДМаршрутам Цикл
		МаршрутПодписания = ЭлементСоответствия.Ключ;
		ВидыЭД            = ЭлементСоответствия.Значение;
		
		РезультатыПроверки = МаршрутыПодписанияБЭД.РезультатыПроверкиМаршрутаПоПараметрамНастройки(
			МаршрутПодписания, СертификатыДляПодписи, ВидыЭД);
			
		МаршрутыПодписанияБЭД.ВывестиРезультатыПроверкиМаршрута(РезультатыПроверки, 
			Объект.Ссылка, МаршрутПодписания, Отказ);
	КонецЦикла;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция МаршрутЗависимОтОрганизации(Маршрут)

	Результат = Ложь;
	Если ЗначениеЗаполнено(Маршрут) Тогда
		Результат = Не МаршрутПредопределенный(Маршрут);
	КонецЕсли;
		
	Возврат Результат;

КонецФункции

&НаСервереБезКонтекста
Функция МаршрутПредопределенный(Маршрут)

	УстановитьПривилегированныйРежим(Истина);
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Маршрут, "Предопределенный");

КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьПравилоПодписанияДокумента(ИспользоватьЭП, ИспользоватьSMS, ИсходящийДокумент)
	
	Возврат ОбменСБанками.ПолучитьПравилоПодписанияДокумента(ИспользоватьЭП, ИспользоватьSMS, ИсходящийДокумент);
	
КонецФункции


#КонецОбласти

#КонецОбласти
