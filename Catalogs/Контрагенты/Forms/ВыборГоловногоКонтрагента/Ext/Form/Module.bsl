
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПравоИзмененияКонтрагента = ПравоДоступа("Изменение", Метаданные.Справочники.Контрагенты);
	Элементы.ФормаВыбрать.Видимость = ПравоИзмененияКонтрагента;
	Элементы.ФормаЗакрыть.Видимость = ПравоИзмененияКонтрагента;
	
	Если Не ПравоИзмененияКонтрагента Тогда
		Элементы.СписокВыбора.ТолькоПросмотр = Истина;
		Элементы.ДекорацияПодсказка.Заголовок = НСтр("ru = 'Выбор головного контрагента недоступен, так как нет прав на изменение.'");
	КонецЕсли;
	
	ПартнерыИКонтрагентыЛокализация.ВыборГоловногоКонтрагентаПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокВыбора

&НаКлиенте
Процедура СписокВыбораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ПравоИзмененияКонтрагента Тогда
		Закрыть(Элемент.ТекущиеДанные.Значение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ТекущиеДанные = Элементы.СписокВыбора.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Контрагент не выбран.'"));
	Иначе
		Закрыть(ТекущиеДанные.Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНового(Команда)
	
	Закрыть(ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"));
	
КонецПроцедуры

#КонецОбласти
