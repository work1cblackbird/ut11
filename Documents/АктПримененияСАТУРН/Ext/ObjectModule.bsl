#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("Структура")Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ПланПримененияСАТУРН") Тогда
		
		ЗаполнитьПоПлануПримененияСАТУРН(ДанныеЗаполнения);
		
	КонецЕсли;
	
	ИнтеграцияСАТУРНПереопределяемый.ОбработкаЗаполненияДокумента(ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	
	ЗаполнитьОбъектПоСтатистике();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	МассивНепроверяемыхРеквизитов.Добавить("Товары.Серия");
	МассивНепроверяемыхРеквизитов.Добавить("МестаПрименения.МестоПрименения");
	
	ШаблонСообщения = НСтр("ru = 'Не заполнено поле ""%1"" в строке %2 списка ""%3""'");
	Для Каждого СтрокаТаблицы Из МестаПрименения Цикл
		
		Если СтрокаТаблицы.ТипМестаПрименения = Перечисления.ТипыМестПримененияСАТУРН.ЗарегистрованноеМестоПрименения
			И Не ЗначениеЗаполнено(СтрокаТаблицы.МестоПрименения) Тогда
			ТекстСообщения = СтрШаблон(
				ШаблонСообщения,
				Метаданные.Документы.АктПримененияСАТУРН.ТабличныеЧасти.МестаПрименения.Реквизиты.МестоПрименения.Синоним,
				СтрокаТаблицы.НомерСтроки,
				Метаданные.Документы.АктПримененияСАТУРН.ТабличныеЧасти.МестаПрименения.Синоним);
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					Метаданные.Документы.АктПримененияСАТУРН.ТабличныеЧасти.МестаПрименения.Имя,
					СтрокаТаблицы.НомерСтроки, Метаданные.Документы.АктПримененияСАТУРН.ТабличныеЧасти.МестаПрименения.Реквизиты.МестоПрименения.Имя),,
					Отказ);
		
		ИначеЕсли Не СтрокаТаблицы.ТипМестаПрименения = Перечисления.ТипыМестПримененияСАТУРН.ЗарегистрованноеМестоПрименения
			И Не ЗначениеЗаполнено(СтрокаТаблицы.ОписаниеМестаПрименения) Тогда
			
			ТекстСообщения = СтрШаблон(
				ШаблонСообщения,
				Метаданные.Документы.АктПримененияСАТУРН.ТабличныеЧасти.МестаПрименения.Реквизиты.ОписаниеМестаПрименения.Синоним,
				СтрокаТаблицы.НомерСтроки,
				Метаданные.Документы.АктПримененияСАТУРН.ТабличныеЧасти.МестаПрименения.Синоним);
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					Метаданные.Документы.АктПримененияСАТУРН.ТабличныеЧасти.МестаПрименения.Имя,
					СтрокаТаблицы.НомерСтроки, Метаданные.Документы.АктПримененияСАТУРН.ТабличныеЧасти.МестаПрименения.Реквизиты.ОписаниеМестаПрименения.Имя),,
					Отказ);
					
		КонецЕсли;
		
	КонецЦикла;
	
	Если МестаПрименения.Количество() Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.МестоПрименения");
	Иначе
		ЕстьМестаПримененияВТоварах = Ложь;
		Для Каждого СтрокаТаблицы Из Товары Цикл
			Если ЗначениеЗаполнено(СтрокаТаблицы.МестоПрименения) Тогда
				ЕстьМестаПримененияВТоварах = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ЕстьМестаПримененияВТоварах Тогда
			МассивНепроверяемыхРеквизитов.Добавить("МестаПрименения");
		Иначе
			МассивНепроверяемыхРеквизитов.Добавить("Товары.МестоПрименения");
		КонецЕсли;
	КонецЕсли;
	
	ИнтеграцияСАТУРНПереопределяемый.ПриОпределенииОбработкиПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	ИнтеграцияСАТУРН.ЗаписатьСоответствиеНоменклатуры(ЭтотОбъект);
	
	ИнтеграцияИСПереопределяемый.ПередЗаписьюОбъекта(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	ИнтеграцияСАТУРН.ЗаписатьСтатусДокументаПоУмолчанию(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Для Каждого СтрокаТаблицы Из Товары Цикл
		СтрокаТаблицы.Партия = Неопределено;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа
	ИнтеграцияИС.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	// Инициализация данных документа
	Документы.АктПримененияСАТУРН.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ИнтеграцияИС.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	РегистрыНакопления.ОстаткиПартийСАТУРН.ОтразитьДвижения(ДополнительныеСвойства, Движения, Отказ);
	ИнтеграцияИСПереопределяемый.ОтразитьДвижения(ДополнительныеСвойства, Движения, Отказ);
	
	ИнтеграцияИС.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ИнтеграцияИСПереопределяемый.ОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения);
	
	ИнтеграцияИС.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаЗаполнения

Процедура ЗаполнитьОбъектПоСтатистике()
	
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтатистики = ЗаполнениеОбъектовПоСтатистикеСАТУРН.ДанныеЗаполненияАктПримененияСАТУРН(ОрганизацияСАТУРН);
	
	Для Каждого КлючИЗначение Из ДанныеСтатистики Цикл
		ЗаполнениеОбъектовПоСтатистикеСАТУРН.ЗаполнитьПустойРеквизит(ЭтотОбъект, ДанныеСтатистики, КлючИЗначение.Ключ);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПоПлануПримененияСАТУРН(ДанныеЗаполнения)
	
	ДанныеШапки = ВыборкаДанныхШапкиПланПрименения(ДанныеЗаполнения);
	
	Если ДанныеШапки.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеШапки);
		
	КонецЕсли;
	
	ДанныеЗаполненияТЧ = ВыборкаДанныхМестПрименения(ДанныеЗаполнения);
	
	ЗаполнитьДанныеМестПрименения(ДанныеЗаполненияТЧ.МестаПрименения, ДанныеЗаполненияТЧ.Препараты);
	
	ИнтеграцияСАТУРН.ЗаполнитьТипИзмеряемойВеличиныИКоличествоВУпаковке(Товары);
	
КонецПроцедуры

Процедура ЗаполнитьДанныеМестПрименения(ВыборкаПлановПрименения, ВыборкаПрепаратов)
	
	МестаПрименения.Очистить();
	Товары.Очистить();
	
	Пока ВыборкаПлановПрименения.Следующий() Цикл
		
		СтрокаТЧ = МестаПрименения.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, ВыборкаПлановПрименения);
		
	КонецЦикла;
	
	Пока ВыборкаПрепаратов.Следующий() Цикл
		
		СтрокаТЧ = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, ВыборкаПрепаратов);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВыборкаДанныхШапкиПланПрименения(МассивПлановПрименения)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", МассивПлановПрименения);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПланПримененияСАТУРН.ОрганизацияСАТУРН      КАК ОрганизацияСАТУРН,
	|	ПланПримененияСАТУРН.ПлановаяДатаПрименения КАК ДатаПрименения,
	|	ПланПримененияСАТУРН.Комментарий            КАК Комментарий,
	|	ПланПримененияСАТУРН.СпособПримененияПАТ    КАК СпособПримененияПАТ,
	|	ПланПримененияСАТУРН.Ссылка                 КАК ДокументОснование
	|ИЗ
	|	Документ.ПланПримененияСАТУРН КАК ПланПримененияСАТУРН
	|ГДЕ
	|	ПланПримененияСАТУРН.Ссылка В (&Ссылка)";
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Функция ВыборкаДанныхМестПрименения(МассивПлановПрименения)
	
	РезультатВозврата = Новый Структура("МестаПрименения, Препараты");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", МассивПлановПрименения);
	
	СписокЗапросов = Новый СписокЗначений;
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	МестаПрименения.МестоПрименения         КАК МестоПрименения,
	|	МестаПрименения.ТипМестаПрименения      КАК ТипМестаПрименения,
	|	МестаПрименения.ОписаниеМестаПрименения КАК ОписаниеМестаПрименения
	|ИЗ
	|	Документ.ПланПримененияСАТУРН.МестаПрименения КАК МестаПрименения
	|ГДЕ
	|	МестаПрименения.Ссылка В (&Ссылка)",
	"МестаПрименения");
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	Товары.Номенклатура             КАК Номенклатура,
	|	Товары.Характеристика           КАК Характеристика,
	|	Товары.Серия                    КАК Серия,
	|	Товары.ПАТ                      КАК ПАТ,
	|	Товары.Дозировка                КАК Дозировка,
	|	Товары.Количество               КАК Количество,
	|	Товары.КоличествоУпаковок       КАК КоличествоУпаковок,
	|	Товары.Упаковка                 КАК Упаковка
	|ИЗ
	|	Документ.ПланПримененияСАТУРН.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка В (&Ссылка)",
	"Препараты");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", МассивПлановПрименения);
	
	РезультатЗапроса = ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов);
	
	//@skip-warning
	ВыборкаМестаПрименения  = РезультатЗапроса["МестаПрименения"].Выбрать();
	//@skip-warning
	ВыборкаПрепараты = РезультатЗапроса["Препараты"].Выбрать();
	
	РезультатВозврата.МестаПрименения = ВыборкаМестаПрименения;
	РезультатВозврата.Препараты       = ВыборкаПрепараты;
	
	Возврат РезультатВозврата;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
