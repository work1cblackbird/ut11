&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.ЗначениеКопирования.Пустая() = Ложь 
		И Параметры.ЗначениеКопирования.ЭтоВходящий = Истина Тогда
        Отказ = Истина; 
        СтандартнаяОбработка = Ложь;
    КонецЕсли;
	
	Если НЕ Параметры.Свойство("ФормаБезОбработки") Тогда
		
		// СтандартныеПодсистемы.ПодключаемыеКоманды
		ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
		// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
		
		ОбменСГИСЭПД.ПриСозданииНаСервереПодчиненнойФормы(ЭтотОбъект, Отказ, СтандартнаяОбработка);	
		
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			ОбменСГИСЭПД.ЗаполнитьТаблицуВерсийТитулов(Объект.Ссылка, Объект.Организация, ВерсииТитулов);
			ОбменСГИСЭПД.ЗаполнитьТаблицуЗначенийРеквизитов(Объект.Ссылка, Объект.Организация, СтруктураРеквизитов);
		Иначе
			ОбменСГИСЭПД.ЗаполнитьТаблицуЗначенийРеквизитовПоПараметрамФормы(Параметры, СтруктураРеквизитов);	
		КонецЕсли;
		
		ОбменСГИСЭПДКлиентСервер.УстановитьДоступностьЭлементовЭСВ(ЭтотОбъект);	
		
		ОрганизацияОписаниеТипов = Метаданные.ОпределяемыеТипы.Организация.Тип;
		КонтрагентОписаниеТипов = Метаданные.ОпределяемыеТипы.КонтрагентБЭД.Тип;
		ФизЛицоОписаниеТипов = Метаданные.ОпределяемыеТипы.ФизическоеЛицо.Тип;
		
		Если Объект.ЭтоВходящий = Ложь Тогда	
			Элементы.СсылкаТитулПеревозчикаПеревозчик.ОграничениеТипа = ОрганизацияОписаниеТипов;		
			Элементы.СсылкаТитулПеревозчикаГрузополучатель.ОграничениеТипа = КонтрагентОписаниеТипов;
			Элементы.СсылкаТитулПеревозчикаГрузоотправитель.ОграничениеТипа = КонтрагентОписаниеТипов;
			Элементы.СсылкаТитулПеревозчикаЛицоОсуществляющееПогрузку.ОграничениеТипа = КонтрагентОписаниеТипов;
			Элементы.СсылкаТитулПеревозчикаЛицоОсуществляющееПриемку.ОграничениеТипа = КонтрагентОписаниеТипов;
			Элементы.СсылкаТитулПеревозчикаЛицоОтветственноеЗаПеревозку.ОграничениеТипа = ФизЛицоОписаниеТипов;
			
			Элементы.СсылкаДопТитулПеревозчикаПеревозчик.ОграничениеТипа = ОрганизацияОписаниеТипов;		
			Элементы.СсылкаДопТитулПеревозчикаГрузополучатель.ОграничениеТипа = КонтрагентОписаниеТипов;
			Элементы.СсылкаДопТитулПеревозчикаГрузоотправитель.ОграничениеТипа = КонтрагентОписаниеТипов;
			Элементы.СсылкаДопТитулПеревозчикаЛицоОсуществляющееПогрузку.ОграничениеТипа = КонтрагентОписаниеТипов;
			Элементы.СсылкаДопТитулПеревозчикаЛицоОсуществляющееПриемку.ОграничениеТипа = КонтрагентОписаниеТипов;
			Элементы.СсылкаДопТитулПеревозчикаЛицоОтветственноеЗаПеревозку.ОграничениеТипа = ФизЛицоОписаниеТипов;
			
			Элементы.ХранимыеДанныеТитулПеревозчикаТранспортноеСредство.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.ХранимыеДанныеЭПД");
			
			Если ЗначениеЗаполнено(Объект.СсылкаТитулПеревозчикаПеревозчик) = Ложь Тогда
				Объект.СсылкаТитулПеревозчикаПеревозчик = ИнтеграцияЭДО.ОрганизацияПоУмолчанию();
				Если ЭтотОбъект.ОписаниеРеквизитовФормы = Неопределено Тогда
					ЭтотОбъект.ОписаниеРеквизитовФормы = Новый Структура("ЕстьОбъект", Истина);
				КонецЕсли;
				ОбменСГИСЭПДКлиентСервер.ЗаполнитьРеквизитыПоСсылке(Элементы.СсылкаТитулПеревозчикаПеревозчик, ЭтотОбъект);
			КонецЕсли;
			
			МассивЭлементов = Новый Массив;
			МассивЭлементов.Добавить(Элементы.СсылкаТитулПеревозчикаПеревозчик);
			МассивЭлементов.Добавить(Элементы.СсылкаТитулПеревозчикаГрузополучатель);
			МассивЭлементов.Добавить(Элементы.СсылкаТитулПеревозчикаГрузоотправитель);
			МассивЭлементов.Добавить(Элементы.СсылкаТитулПеревозчикаЛицоОсуществляющееПогрузку);
			МассивЭлементов.Добавить(Элементы.СсылкаТитулПеревозчикаЛицоОсуществляющееПриемку);
			
			МассивЭлементов.Добавить(Элементы.СсылкаДопТитулПеревозчикаПеревозчик);
			МассивЭлементов.Добавить(Элементы.СсылкаДопТитулПеревозчикаГрузополучатель);
			МассивЭлементов.Добавить(Элементы.СсылкаДопТитулПеревозчикаГрузоотправитель);
			МассивЭлементов.Добавить(Элементы.СсылкаДопТитулПеревозчикаЛицоОсуществляющееПогрузку);
			МассивЭлементов.Добавить(Элементы.СсылкаДопТитулПеревозчикаЛицоОсуществляющееПриемку);
			ОбменСГИСЭПДКлиентСервер.ПоказатьОшибкиПоКонтрагентам(ЭтотОбъект, МассивЭлементов);
			
			Элементы.ГруппаРеквизитаТитулПеревозчикаПорядковыйНомерСопроводительнойВедомости.Видимость = Ложь;
			Элементы.ЗаголовокНомер.Заголовок = "Номер";
		Иначе
			Элементы.ГруппаРеквизитаНомер.Видимость = Ложь;
		КонецЕсли;
		
		ЦветВыделенияУчастника = ЦветаСтиля.ЦветВыделенияУчастникаЭПД;
		Если Объект.РольУчастника = 1 Тогда
			Элементы.ГруппаРеквизитаСсылкаТитулПеревозчикаПеревозчик.ЦветФона = ЦветВыделенияУчастника;
			Элементы.ГруппаРеквизитаСсылкаДопТитулПеревозчикаПеревозчик.ЦветФона = ЦветВыделенияУчастника;
		ИначеЕсли Объект.РольУчастника = 2 Тогда
			Элементы.ГруппаРеквизитаСсылкаТитулПеревозчикаГрузополучатель.ЦветФона = ЦветВыделенияУчастника;
			Элементы.ГруппаРеквизитаСсылкаДопТитулПеревозчикаГрузополучатель.ЦветФона = ЦветВыделенияУчастника;
		ИначеЕсли Объект.РольУчастника = 3 Тогда
			Элементы.ГруппаРеквизитаСсылкаТитулПеревозчикаГрузоотправитель.ЦветФона = ЦветВыделенияУчастника;
			Элементы.ГруппаРеквизитаСсылкаДопТитулПеревозчикаГрузоотправитель.ЦветФона = ЦветВыделенияУчастника;
		КонецЕсли;
		
		ИзмененСоставУчастников = ЗначениеЗаполнено(Объект.ИдентификаторПеревозчика) = Ложь
									Или ЗначениеЗаполнено(Объект.ИдентификаторГрузоотправителя) = Ложь
									Или ЗначениеЗаполнено(Объект.ИдентификаторГрузополучателя) = Ложь;
									
		ЗаполнитьЗначенияПоУмолчаниюДляТекущегоТитула();
		УстановитьТекущийШагПомощника();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Оповестить("Запись_ДокументыЭПД", Объект.Ссылка);
	
КонецПроцедуры


&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(Объект.ТекущийТитул) = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство("НеТребуетсяИдентификацияФайла") = Ложь Тогда
		ЗаполнитьУчастников();
		ИнформацияПоПрефиксамТитула = ОбменСГИСЭПДКлиентСервер.ПрефиксТитулаПоЭлементуРегламентаЭДО(Объект.ТекущийТитул);
		ЗаполнитьИдентификациюФайлаОбмена(ИнформацияПоПрефиксамТитула, ИдентификаторОтправителя());	
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура Проверить(Команда)
	
	Если ЗначениеЗаполнено(Объект.ТекущийТитул) = Ложь Тогда
		Возврат;	
	КонецЕсли;
	
	ЗаполнитьУчастников();
	
	ИнформацияПоПрефиксамТитула = ОбменСГИСЭПДКлиентСервер.ПрефиксТитулаПоЭлементуРегламентаЭДО(Объект.ТекущийТитул);
	ЗаполнитьИдентификациюФайлаОбмена(ИнформацияПоПрефиксамТитула, ИдентификаторОтправителя());
	
КонецПроцедуры


&НаКлиенте
Процедура НадписьОшибкиЗакрытьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ЗакрытьОшибки" Тогда
		СтандартнаяОбработка = Ложь;
		Элементы.ГруппаОшибки.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура СледующаяОшибка(Команда)
	
	ОбменСГИСЭПДКлиент.НавигацияПоОшибкамЗаполнения(ЭтотОбъект, 
													ОшибкиЗаполнения, 
													Элементы.ОшибкиЗаполнения.ТекущаяСтрока);

КонецПроцедуры

&НаКлиенте
Процедура ПредыдущаяОшибка(Команда)
	
	ОбменСГИСЭПДКлиент.НавигацияПоОшибкамЗаполнения(ЭтотОбъект, 
													ОшибкиЗаполнения, 
													Элементы.ОшибкиЗаполнения.ТекущаяСтрока,
													Истина);
	
КонецПроцедуры


&НаКлиенте
Процедура ОшибкиЗаполненияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = ОшибкиЗаполнения.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ЭтотОбъект.ПереходыПоОшибкам = ТекущиеДанные.Описание.Переходы;
	ОбменСГИСЭПДКлиент.ПоказатьОшибкуЗаполнения(ЭтотОбъект, ТекущиеДанные, ИдентификаторОтправителя());

КонецПроцедуры


// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды


&НаКлиенте
Процедура ХранимыеДанныеТитулПеревозчикаТранспортноеСредствоОткрытие(Элемент, СтандартнаяОбработка)
	
	Если ТипЗнч(ХранимыеДанныеТитулПеревозчикаТранспортноеСредство) <> Тип("СправочникСсылка.ХранимыеДанныеЭПД") Тогда
		СтандартнаяОбработка = Ложь;
		ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Элемент.Имя, , Ложь);
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьПредставленияАдресов(Знач Титул = Неопределено)
	
	Если Титул = Неопределено Или Титул = Перечисления.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул2_1
		Или Титул = Перечисления.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул2_2 Тогда
		ОбменСГИСЭПДКлиентСервер.ЗаполнитьПредставлениеАдреса("ПредставлениеТитулГрузоотправителяАдресПогрузки", ЭтотОбъект);
	КонецЕсли;
	
	Если Титул = Неопределено Или Титул = Перечисления.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул3_1
		Или Титул = Перечисления.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул3_2 Тогда
		ОбменСГИСЭПДКлиентСервер.ЗаполнитьПредставлениеАдреса("ПредставлениеТитулГрузополучателяАдресВыгрузки", ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыОснования(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	МассивДокументов = Новый Массив;
	Для Каждого СтрТаб Из Объект.ДокументыОснования Цикл
		МассивДокументов.Добавить(СтрТаб.ДокументОснование);	
	КонецЦикла;
	
	ПараметрыФормы.Вставить("МассивДокументов", МассивДокументов);
	ПараметрыФормы.Вставить("ТолькоПросмотр", Объект.ТекущийТитул <> ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_1")
											И Объект.ТекущийТитул <> ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_2")
											И Объект.ТекущийТитул <> ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_5"));
	
	ОписаниеДокументыОснование_Закрытие = Новый ОписаниеОповещения("ДокументыОснование_Закрытие", ЭтотОбъект, ПараметрыФормы);
	
	ОткрытьФорму("Документ.ЭлектроннаяСопроводительнаяВедомость.Форма.ДокументыОснования", ПараметрыФормы, ЭтотОбъект, 
					УникальныйИдентификатор, , , ОписаниеДокументыОснование_Закрытие, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
					
КонецПроцедуры

&НаКлиенте
Процедура ДокументыОснование_Закрытие(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Массив") Тогда
		Объект.ДокументыОснования.Очистить();
		Для Каждого Документ Из Результат Цикл
			НовСтр = Объект.ДокументыОснования.Добавить();	
			НовСтр.ДокументОснование = Документ;	
		КонецЦикла;	
		Модифицированность = Истина;
		ОбменСГИСЭПДКлиентПереопределяемый.ОбработкаЗаполненияНаФормеЭСВ(ЭтотОбъект, Результат);
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьЗначенияПоУмолчаниюДляТекущегоТитула()
	
	Если (ЗначениеЗаполнено(Объект.Ссылка) = Ложь 
		Или Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_1")
		Или Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_2")
		Или Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_5")) Тогда
				
		Если ЗначениеЗаполнено(Объект.ВидДокумента) = Ложь Тогда
			Объект.ВидДокумента = ЭлектронныеДокументыЭДО.ВидДокументаПоТипу(ПредопределенноеЗначение("Перечисление.ТипыДокументовЭДО.ЭСВ"));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект.ТитулПеревозчикаДатаСоставленияСопроводительнойВедомости) = Ложь Тогда
			Объект.ТитулПеревозчикаДатаСоставленияСопроводительнойВедомости = ТекущаяДатаСеанса();	
		КонецЕсли;
		
	ИначеЕсли Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_3")
		Или Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_4") Тогда
			
			МассивИсключений = Новый Массив;
			МассивИсключений.Добавить("ТитулПеревозчикаИдентификаторФайла");
			МассивИсключений.Добавить("ТитулПеревозчикаДатаФормированияФайла");
			МассивИсключений.Добавить("ТитулПеревозчикаВремяФормированияФайла");
			МассивИсключений.Добавить("ТитулПеревозчикаВерсияПрограммы");
			МассивИсключений.Добавить("ТитулПеревозчикаВерсияФормата");
			МассивИсключений.Добавить("ТитулПеревозчикаКодДокументаПоКНД");
			МассивИсключений.Добавить("ТитулПеревозчикаУИДМинтранс");
			
			ОбменСГИСЭПД.СкопироватьРеквизитыИзТитулаВТитул(ЭтотОбъект, "ТитулПеревозчика", "ДопТитулПеревозчика", Истина, МассивИсключений);
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	ОбменСГИСЭПДКлиентСервер.УстановитьДоступностьЭлементовЭСВ(ЭтотОбъект);
	УстановитьТекущийШагПомощника();
	
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбменСГИСЭПДКлиент.ПриОткрытииПодчиненнойФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОФормированииЭПД_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.ОК Тогда
		Закрыть();	
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура НадписьСтатусаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ТекущиеДелаПоЭДО" Тогда
		СтандартнаяОбработка = Ложь;
		ИнтерфейсДокументовЭДОКлиент.ОткрытьЭлектронныйДокументОбъектаУчета(Объект.Ссылка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЭлектронныйДокумент(Команда)
	
	ИнтерфейсДокументовЭДОКлиент.ОткрытьЭлектронныйДокументОбъектаУчета(Объект.Ссылка);
	
КонецПроцедуры


&НаКлиенте
Процедура НадписьСтатусаОшибкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОписаниеОшибки" Тогда
		СтандартнаяОбработка = Ложь;
		ОбменСГИСЭПДКлиент.ОткрытьОписаниеОшибки(Объект.Ссылка, ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры


&НаКлиенте
Функция ОписаниеРеквизитовФормы() Экспорт
	
	Возврат ОписаниеРеквизитовФормыСервер();
	
КонецФункции

&НаСервере
Функция ОписаниеРеквизитовФормыСервер()
	
	Возврат ОбменСГИСЭПД.ОписаниеРеквизитовФормы(ЭтаФорма);
		
КонецФункции

&НаКлиенте
Процедура ЗаполнитьТитулПеревозчикаСведенияОГрузе(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТитулПеревозчикаСведенияОКонтейнерахСдача(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТитулПеревозчикаВыгрузкаПогрузкаКонтейнера(Команда)
	
	Если Объект.ВидОперации = 0 Тогда
		ИмяКоманды = "ТитулПеревозчикаВыгрузкаКонтейнера";
	Иначе
		ИмяКоманды = "ТитулПеревозчикаПогрузкаКонтейнера";
	КонецЕсли; 
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, ИмяКоманды);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДопТитулПеревозчикаВыгрузкаПогрузкаКонтейнера(Команда)
	
	Если (Объект.ВидОперации = 1 И ЗначениеЗаполнено(Объект.ТекущийПолученныйТитул)) Тогда
		ИмяКоманды = "ДопТитулПеревозчикаВыгрузкаКонтейнера";
	Иначе
		ИмяКоманды = "ДопТитулПеревозчикаПогрузкаКонтейнера";
	КонецЕсли; 
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, ИмяКоманды);
	
КонецПроцедуры


&НаКлиенте
Процедура ЗаполнитьТитулПеревозчикаСведенияОКонтейнерахПрием(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТитулПеревозчикаПриемКонтейнера(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТитулПеревозчикаВодители(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТитулПеревозчикаСведенияОПутевомЛистеПутевыхЛистах(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТитулГрузоотправителяСведенияОбУказанияхГрузоотправителя(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТитулГрузоотправителяСведенияОКонтейнерах(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТитулГрузополучателяСведенияОКонтейнерах(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаТитулПеревозчикаГрузоотправительПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
	ОбменСГИСЭПДКлиентСервер.ЗаполнитьРеквизитыПоСсылке(Элемент, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеТитулГрузополучателяАдресВыгрузкиПриИзменении(Элемент)
	
	Если ПустаяСтрока(ПредставлениеТитулГрузополучателяАдресВыгрузки) Тогда
		Элементы.ПредставлениеТитулГрузополучателяАдресВыгрузки_Ошибка.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеТитулГрузоотправителяАдресПогрузкиПриИзменении(Элемент)
	
	Если ПустаяСтрока(ПредставлениеТитулГрузоотправителяАдресПогрузки) Тогда
		Элементы.ПредставлениеТитулГрузоотправителяАдресПогрузки_Ошибка.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры



&НаКлиенте
Процедура СсылкаТитулПеревозчикаЛицоОсуществляющееПогрузкуПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
	ОбменСГИСЭПДКлиентСервер.ЗаполнитьРеквизитыПоСсылке(Элемент, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаТитулПеревозчикаГрузополучательПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
	ОбменСГИСЭПДКлиентСервер.ЗаполнитьРеквизитыПоСсылке(Элемент, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаТитулПеревозчикаЛицоОсуществляющееПриемкуПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
	ОбменСГИСЭПДКлиентСервер.ЗаполнитьРеквизитыПоСсылке(Элемент, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаТитулПеревозчикаПеревозчикПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
	ОбменСГИСЭПДКлиентСервер.ЗаполнитьРеквизитыПоСсылке(Элемент, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаТитулПеревозчикаСведенияОЛицеОтветственномЗаПеревозкуПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
	ОбменСГИСЭПДКлиентСервер.ЗаполнитьРеквизитыПоСсылке(Элемент, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТитулГрузоотправителяДатаИВремяПрибытияПриИзменении(Элемент)
		
	ПриИзмененииДанныхНаФорме(Элемент.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ТитулГрузоотправителяДатаИВремяПодачиПриИзменении(Элемент)
		
	ПриИзмененииДанныхНаФорме(Элемент.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ТитулГрузоотправителяДатаИВремяУбытияПриИзменении(Элемент)
		
	ПриИзмененииДанныхНаФорме(Элемент.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ТитулГрузоотправителяСостояниеКонтейнераПриИзменении(Элемент)
		
	ПриИзмененииДанныхНаФорме(Элемент.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ТитулГрузоотправителяКоличествоГрузовыхМестПриИзменении(Элемент)
		
	ПриИзмененииДанныхНаФорме(Элемент.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ТитулГрузоотправителяКоличествоПаллетПриИзменении(Элемент)
		
	ПриИзмененииДанныхНаФорме(Элемент.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ТитулГрузоотправителяМассаНеттоГрузаПриИзменении(Элемент)
		
	ПриИзмененииДанныхНаФорме(Элемент.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ТитулГрузоотправителяМассаБруттоГрузаПриИзменении(Элемент)
		
	ПриИзмененииДанныхНаФорме(Элемент.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ТитулГрузополучателяДатаИВремяПрибытияПриИзменении(Элемент)
		
	ПриИзмененииДанныхНаФорме(Элемент.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ТитулГрузополучателяДатаИВремяПодачиПриИзменении(Элемент)
		
	ПриИзмененииДанныхНаФорме(Элемент.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ТитулГрузополучателяДатаИВремяУбытияПриИзменении(Элемент)
		
	ПриИзмененииДанныхНаФорме(Элемент.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ТитулГрузополучателяФактическоеСостояниеКонтейнераПриИзменении(Элемент)
		
	ПриИзмененииДанныхНаФорме(Элемент.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ТитулГрузополучателяКоличествоГрузовыхМестПриИзменении(Элемент)
		
	ПриИзмененииДанныхНаФорме(Элемент.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ТитулГрузополучателяМассаНеттоПриИзменении(Элемент)
		
	ПриИзмененииДанныхНаФорме(Элемент.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ТитулГрузополучателяМассаБруттоГрузаПриИзменении(Элемент)
		
	ПриИзмененииДанныхНаФорме(Элемент.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ТитулГрузополучателяКоличествоПаллетПриИзменении(Элемент)
		
	ПриИзмененииДанныхНаФорме(Элемент.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ХранимыеДанныеТитулПеревозчикаТранспортноеСредствоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ХранимыеДанныеТитулПеревозчикаТранспортноеСредствоПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
	ЗначениеРеквизита = ЭтотОбъект.ХранимыеДанныеТитулПеревозчикаТранспортноеСредство;
	ГруппаДанных = СтрЗаменить(Элемент.Имя, "ХранимыеДанные", "");
	Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
		ВходящийКонтекст = Новый Структура;
		ВходящийКонтекст.Вставить("ЗапретитьИзменение", Ложь);
		ВходящийКонтекст.Вставить("Форма", ЭтотОбъект);
		ВходящийКонтекст.Вставить("ГруппаДанных", ГруппаДанных);
		ОбменСГИСЭПДКлиент.ОткрытиеФормыПоГиперссылке_Завершение(ЗначениеРеквизита, ВходящийКонтекст);	
	Иначе
		ОбменСГИСЭПДКлиентСервер.УдалитьРеквизитыПоГруппеДанных(ЭтотОбъект, ГруппаДанных);
		ОбменСГИСЭПДКлиентСервер.ИзменитьОформлениеЭлементовФормы(ЭтотОбъект, СтрЗаменить(Элемент.Имя, "ХранимыеДанные", ""));	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ХранимыеДанныеТитулПеревозчикаТранспортноеСредствоАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ГруппаДанных = СтрЗаменить(Элемент.Имя, "ХранимыеДанные", "");
	ПараметрыПолученияДанных.Отбор = ОбменСГИСЭПДКлиент.ПолучитьОтборХранимыхДанных(ЭтотОбъект, ЭтотОбъект, ГруппаДанных);
	ПараметрыПолученияДанных.СпособПоискаСтроки = ПредопределенноеЗначение("СпособПоискаСтрокиПриВводеПоСтроке.ЛюбаяЧасть");
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеТитулГрузоотправителяАдресПогрузкиАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.КонтактнаяИнформация") Тогда
		МодульУправлениеКонтактнойИнформациейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент");
		МодульУправлениеКонтактнойИнформациейКлиент.АвтоПодборАдреса(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеТитулГрузоотправителяАдресПогрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбменСГИСЭПДКлиент.ВводАдреса(Элемент.Имя, ЭтотОбъект, Объект.СсылкаТитулПеревозчикаГрузоотправитель);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеТитулГрузоотправителяАдресПогрузкиОбработкаВыбора(Элемент, Результат, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбменСГИСЭПДКлиентСервер.ЗаполнитьРеквизитыАдреса(ЭтотОбъект, Результат, Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеТитулГрузополучателяАдресВыгрузкиАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.КонтактнаяИнформация") Тогда
		МодульУправлениеКонтактнойИнформациейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент");
		МодульУправлениеКонтактнойИнформациейКлиент.АвтоПодборАдреса(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеТитулГрузополучателяАдресВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбменСГИСЭПДКлиент.ВводАдреса(Элемент.Имя, ЭтотОбъект, Объект.СсылкаТитулПеревозчикаГрузополучатель);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеТитулГрузополучателяАдресВыгрузкиОбработкаВыбора(Элемент, Результат, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбменСГИСЭПДКлиентСервер.ЗаполнитьРеквизитыАдреса(ЭтотОбъект, Результат, Элемент.Имя);
	
КонецПроцедуры


////////////////////////////

#Область Навигация

&НаКлиенте
Процедура Шаг1ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьИзменениеШага("Шаг1", НавигационнаяСсылкаФорматированнойСтроки);

КонецПроцедуры

&НаКлиенте
Процедура Шаг2ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьИзменениеШага("Шаг2", НавигационнаяСсылкаФорматированнойСтроки);

КонецПроцедуры

&НаКлиенте
Процедура Шаг3ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьИзменениеШага("Шаг3", НавигационнаяСсылкаФорматированнойСтроки);

КонецПроцедуры

&НаКлиенте
Процедура Шаг4ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьИзменениеШага("Шаг4", НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

#КонецОбласти


#Область НавигацияПоСтраницам

&НаКлиенте
Процедура ИнкрементироватьОтображаемуюВерсиюТитула(ТитулСтрока, Инкремент)
	
	Если Инкремент = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ОтображаемыеВерсииТитулов = Неопределено Тогда
		ОтображаемыеВерсииТитулов = Новый Структура;
	Иначе
		ТекущееЗначениеВерсии = Неопределено;
		ОтображаемыеВерсииТитулов.Свойство(ТитулСтрока, ТекущееЗначениеВерсии);
		Если ТекущееЗначениеВерсии <> Неопределено Тогда
			ОтображаемыеВерсииТитулов.Вставить(ТитулСтрока, ТекущееЗначениеВерсии + Инкремент);
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОтображаемаяВерсияТитула(СтрокиВерсий, ТитулСтрока)
	
	МаксВерсия = 0;
	Для Каждого СтрокаТЗ Из СтрокиВерсий Цикл
		Если СтрокаТЗ.НомерВерсии > МаксВерсия Тогда
			МаксВерсия = СтрокаТЗ.НомерВерсии;
		КонецЕсли;
	КонецЦикла;
	
	Если ОтображаемыеВерсииТитулов = Неопределено Тогда
		ОтображаемыеВерсииТитулов = Новый Структура;
		ОтображаемыеВерсииТитулов.Вставить(ТитулСтрока, МаксВерсия);
		Возврат МаксВерсия;
	КонецЕсли;
	
	ТекущееЗначениеВерсии = Неопределено;
	ОтображаемыеВерсииТитулов.Свойство(ТитулСтрока, ТекущееЗначениеВерсии);
	
	Если ТекущееЗначениеВерсии = Неопределено Тогда
		ОтображаемыеВерсииТитулов.Вставить(ТитулСтрока, МаксВерсия);
		Возврат МаксВерсия;
	Иначе
		Возврат ТекущееЗначениеВерсии;		
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьСтраницы(ОтображаемаяСтраница)
	
	ОтображаемаяСтраницаЦелевая = ПодменитьСтраницуДляТестированияФормы(ОтображаемаяСтраница);

	// Чтобы не отрастали лишние скролл-бары по высоте из-за разного числа элементов
	// на разных страницах, оставляем видимость только у одной - отображаемой страницы.
	
	Для каждого СтраницаЭСВ Из Элементы.Страницы.ПодчиненныеЭлементы Цикл
		СтраницаЭСВ.Видимость = (СтраницаЭСВ = ОтображаемаяСтраницаЦелевая);
	КонецЦикла;

	Элементы.Страницы.ТекущаяСтраница  = ОтображаемаяСтраницаЦелевая;
	
	
	ОбменСГИСЭПД.ПодготовитьФормуДляОтметкиОбязательныхПолей(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ПодменитьСтраницуДляТестированияФормы(ОтображаемаяСтраница)
	
	Возврат ОтображаемаяСтраница;
	
КонецФункции

&НаСервере
Процедура УстановитьТекущийШагПомощника(ПредыдущийТекущийТитул = Неопределено)
	
	Если Параметры.Свойство("ТитулСтраницы") Тогда
		ТитулСтраницы = Параметры.ТитулСтраницы;
	Иначе
		ТитулСтраницы = ?(ЗначениеЗаполнено(Объект.ТекущийТитул), Объект.ТекущийТитул, ПредыдущийТекущийТитул);
	КонецЕсли;
	
	НачатьСПервойСтраницы = ТолькоПросмотр ИЛИ НЕ ЗначениеЗаполнено(Объект.Ссылка);

	Если НачатьСПервойСтраницы Тогда		
		УстановитьВидимостьСтраницыТитулПеревозчика("Шаг1");		
	Иначе		
		Если ТитулСтраницы = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул2_1")
			Или ТитулСтраницы = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул2_2") Тогда			
			УстановитьВидимостьСтраницыТитулГрузоотправителя("Шаг2"); 			
		ИначеЕсли ТитулСтраницы = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул3_1") Тогда
			УстановитьВидимостьСтраницыТитулГрузополучателя("Шаг4");
		ИначеЕсли ТитулСтраницы = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул3_2") Тогда
			УстановитьВидимостьСтраницыТитулГрузополучателя("Шаг2");								
		ИначеЕсли ТитулСтраницы = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_3")
			Или ТитулСтраницы = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_4") Тогда
			УстановитьВидимостьСтраницыДопТитулПеревозчика("Шаг3");				
		Иначе
			УстановитьВидимостьСтраницыТитулПеревозчика("Шаг1");			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьТекущийШагПомощника()
	
	Если ТолькоПросмотр ИЛИ НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьСтраницыТитулПеревозчика(ИмяШага)

	УстановитьВидимостьСтраницы(Элементы.СтраницаСодСВИнфПрв);
	
	СтруктураНавигации = НавигацияПомощника();
	ИмяШагаПараметр = СтруктураНавигации[ИмяШага].СтруктураПараметровФормы.НавигацияПараметрФормы;
	
	РазместитьНавигацию(ИмяШагаПараметр, СтруктураНавигации[ИмяШага].ТипЭлементаРегламента);
	
	СохранитьТекущийШагПомощника();

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьСтраницыДопТитулПеревозчика(ИмяШага)

	УстановитьВидимостьСтраницы(Элементы.СтраницаДопСодСВИнфПрв);
	
	СтруктураНавигации = НавигацияПомощника();
	ИмяШагаПараметр = СтруктураНавигации[ИмяШага].СтруктураПараметровФормы.НавигацияПараметрФормы;
	
	РазместитьНавигацию(ИмяШагаПараметр, СтруктураНавигации[ИмяШага].ТипЭлементаРегламента);
	
	СохранитьТекущийШагПомощника();

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьСтраницыТитулГрузоотправителя(ИмяШага)

	УстановитьВидимостьСтраницы(Элементы.СтраницаСодСВИнфГО);
	
	СтруктураНавигации = НавигацияПомощника();
	ИмяШагаПараметр = СтруктураНавигации[ИмяШага].СтруктураПараметровФормы.НавигацияПараметрФормы;
	
	РазместитьНавигацию(ИмяШагаПараметр, СтруктураНавигации[ИмяШага].ТипЭлементаРегламента);
	
	СохранитьТекущийШагПомощника();

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьСтраницыТитулГрузополучателя(ИмяШага)

	УстановитьВидимостьСтраницы(Элементы.СтраницаСодСВИнфГП);
	
	СтруктураНавигации = НавигацияПомощника();
	ИмяШагаПараметр = СтруктураНавигации[ИмяШага].СтруктураПараметровФормы.НавигацияПараметрФормы;
	
	РазместитьНавигацию(ИмяШагаПараметр, СтруктураНавигации[ИмяШага].ТипЭлементаРегламента);
	
	СохранитьТекущийШагПомощника();
	

КонецПроцедуры



&НаСервере
Функция ИмяПомощника()

	Возврат "ЭлектроннаяСопроводительнаяВедомость";

КонецФункции


&НаСервере
Функция НавигацияПомощника()
	
	СтруктураНавигации = Новый Структура;
	
	СтруктураНавигации.Вставить(НавигацияПомощниковСВозвратомЭПД.ИмяШага(1), СтруктураШагаНачало(1));
	
	Если Объект.ВидОперации = 2 Тогда
		СтруктураНавигации.Вставить(НавигацияПомощниковСВозвратомЭПД.ИмяШага(2), СтруктураШагаГрузополучатель(2));
	Иначе
		СтруктураНавигации.Вставить(НавигацияПомощниковСВозвратомЭПД.ИмяШага(2), СтруктураШагаГрузоотправитель(2));
	КонецЕсли;
	
	Если Объект.ВидОперации = 1 Тогда
		СтруктураНавигации.Вставить(НавигацияПомощниковСВозвратомЭПД.ИмяШага(3), СтруктураШагаПеревозчикаПриемка(3));
	ИначеЕсли Объект.ВидОперации = 2 Тогда
		СтруктураНавигации.Вставить(НавигацияПомощниковСВозвратомЭПД.ИмяШага(3), СтруктураШагаПеревозчикаВозвратКонтейнера(3));
	КонецЕсли;
	
	Если Объект.ВидОперации = 1 Тогда
		СтруктураНавигации.Вставить(НавигацияПомощниковСВозвратомЭПД.ИмяШага(4), СтруктураШагаГрузополучатель(4));	
	КонецЕсли;

	Для Каждого КиЗ Из СтруктураНавигации Цикл
		ШагВыполнен = Ложь;
		Для Каждого СтрВерсия Из ВерсииТитулов Цикл
			Если СтрВерсия.Титул = КиЗ.Значение.ТипЭлементаРегламента И СтрВерсия.ИдентификаторФайла <> "" Тогда
				ШагВыполнен = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		КиЗ.Значение.Выполнен = ШагВыполнен; 	
		КиЗ.Значение.Доступен = КиЗ.Значение.Доступен Или КиЗ.Значение.Выполнен;
	КонецЦикла;
	
	Возврат СтруктураНавигации;
	
КонецФункции

&НаСервере
Функция СтруктураШагаНачало(НомерШага)
	
	Если Объект.ВидОперации = 0 Тогда
		ИмяПараметра = "ТитулПеревозчика"; 
		Титул = Перечисления.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_1;
	ИначеЕсли Объект.ВидОперации = 1 Тогда
		ИмяПараметра = "ТитулПеревозчика"; 
		Титул = Перечисления.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_2;
	ИначеЕсли Объект.ВидОперации = 2 Тогда
		ИмяПараметра = "ТитулПеревозчика";	
		Титул = Перечисления.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_5;
	КонецЕсли;
	
	Отбор = Новый Структура("Титул", Титул);
	СтрокиВерсий = ВерсииТитулов.Выгрузить(Отбор, "НомерВерсии");
		
	СтруктураШага                = НавигацияПомощниковСВозвратомЭПД.НовыйСтруктураШага();
	СтруктураШага.ИмяПомощника   = ИмяПомощника();
	СтруктураШага.НомерШага      = НомерШага;
	СтруктураШага.ТекстНавигации = НСтр("ru='Оформление'");
	СтруктураШага.СтруктураПараметровФормы.НавигацияПараметрФормы = ИмяПараметра;
	СтруктураШага.ТипЭлементаРегламента = Титул;
	СтруктураШага.Выполнен = ЗначениеЗаполнено(Объект.ТитулПеревозчикаИдентификаторФайла); 
	СтруктураШага.Доступен = Истина;
	СтруктураШага.КоличествоВерсий = Макс(СтрокиВерсий.Количество(), 1);
	СтруктураШага.НомерВерсии = ОтображаемаяВерсияТитула(СтрокиВерсий, ИмяПараметра);
	Возврат СтруктураШага;
	
КонецФункции

&НаСервере
Функция СтруктураШагаПеревозчикаПриемка(НомерШага)
	
	Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_3");
	Отбор = Новый Структура("Титул", Титул);
	СтрокиВерсий = ВерсииТитулов.Выгрузить(Отбор, "НомерВерсии");
	
	СтруктураШага                = НавигацияПомощниковСВозвратомЭПД.НовыйСтруктураШага();
	СтруктураШага.ИмяПомощника   = ИмяПомощника();
	СтруктураШага.НомерШага      = НомерШага;
	СтруктураШага.ТекстНавигации = НСтр("ru='Перевозчик (передача груженого контейнера)'");
	СтруктураШага.СтруктураПараметровФормы.НавигацияПараметрФормы = "ДопТитулПеревозчика"; 
	СтруктураШага.ТипЭлементаРегламента = Титул;
	СтруктураШага.Выполнен = ЗначениеЗаполнено(Объект.ДопТитулПеревозчикаИдентификаторФайла); 
	СтруктураШага.Доступен = СтруктураШага.Выполнен 
								Или Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_3");
	СтруктураШага.КоличествоВерсий = Макс(СтрокиВерсий.Количество(), 1);
	СтруктураШага.НомерВерсии = ОтображаемаяВерсияТитула(СтрокиВерсий, "ДопТитулПеревозчика");
	Возврат СтруктураШага;
	
КонецФункции

&НаСервере
Функция СтруктураШагаПеревозчикаВозвратКонтейнера(НомерШага)
	
	Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_4");
	Отбор = Новый Структура("Титул", Титул);
	СтрокиВерсий = ВерсииТитулов.Выгрузить(Отбор, "НомерВерсии");
		
	СтруктураШага                = НавигацияПомощниковСВозвратомЭПД.НовыйСтруктураШага();
	СтруктураШага.ИмяПомощника   = ИмяПомощника();
	СтруктураШага.НомерШага      = НомерШага;
	СтруктураШага.ТекстНавигации = НСтр("ru='Перевозчик (получение пустого контейнера)'");
	СтруктураШага.СтруктураПараметровФормы.НавигацияПараметрФормы = "ДопТитулПеревозчика"; 
	СтруктураШага.ТипЭлементаРегламента = Титул;
	СтруктураШага.Выполнен = ЗначениеЗаполнено(Объект.ДопТитулПеревозчикаИдентификаторФайла); 
	СтруктураШага.Доступен = СтруктураШага.Выполнен 
								Или Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_4");
	СтруктураШага.КоличествоВерсий = Макс(СтрокиВерсий.Количество(), 1);
	СтруктураШага.НомерВерсии = ОтображаемаяВерсияТитула(СтрокиВерсий, "ДопТитулПеревозчика");
	Возврат СтруктураШага;
	
КонецФункции

&НаСервере
Функция СтруктураШагаГрузоотправитель(НомерШага)
	
	Если Объект.ВидОперации = 0 Тогда
		ИмяПараметра = "ТитулГрузоотправителя"; 
		Титул = Перечисления.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул2_1;
		ИмяШага = НСтр("ru='Грузоотправитель (получение пустого контейнера)'");
	Иначе
		ИмяПараметра = "ТитулГрузоотправителя"; 
		Титул = Перечисления.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул2_2;
		ИмяШага = НСтр("ru='Грузоотправитель (передача груженого контейнера)'");	
	КонецЕсли;
	
	Отбор = Новый Структура("Титул", Титул);
	СтрокиВерсий = ВерсииТитулов.Выгрузить(Отбор, "НомерВерсии");
	
	СтруктураШага                = НавигацияПомощниковСВозвратомЭПД.НовыйСтруктураШага();
	СтруктураШага.ИмяПомощника   = ИмяПомощника();
	СтруктураШага.НомерШага      = НомерШага;
	СтруктураШага.ТекстНавигации = ИмяШага;
	СтруктураШага.СтруктураПараметровФормы.НавигацияПараметрФормы = ИмяПараметра;
	СтруктураШага.ТипЭлементаРегламента = Титул;
	СтруктураШага.Выполнен = ЗначениеЗаполнено(Объект.ТитулГрузоотправителяИдентификаторФайла);
	СтруктураШага.Доступен = СтруктураШага.Выполнен 
								Или Объект.ТекущийТитул = Титул;
	СтруктураШага.КоличествоВерсий = Макс(СтрокиВерсий.Количество(), 1);
	СтруктураШага.НомерВерсии = ОтображаемаяВерсияТитула(СтрокиВерсий, ИмяПараметра);
	Возврат СтруктураШага;
	
КонецФункции

&НаСервере
Функция СтруктураШагаГрузополучатель(НомерШага)
	
	Если Объект.ВидОперации = 1 Тогда
		ИмяПараметра = "ТитулГрузополучателя"; 
		Титул = Перечисления.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул3_1;
		ИмяШага = НСтр("ru='Грузополучатель (приемка контейнера)'");
	Иначе
		ИмяПараметра = "ТитулГрузополучателя"; 
		Титул = Перечисления.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул3_2;
		ИмяШага = НСтр("ru='Грузополучатель (передача пустого контейнера)'");	
	КонецЕсли;
	
	Отбор = Новый Структура("Титул", Титул);
	СтрокиВерсий = ВерсииТитулов.Выгрузить(Отбор, "НомерВерсии");
	
	СтруктураШага                = НавигацияПомощниковСВозвратомЭПД.НовыйСтруктураШага();
	СтруктураШага.ИмяПомощника   = ИмяПомощника();
	СтруктураШага.НомерШага      = НомерШага;
	СтруктураШага.ТекстНавигации = ИмяШага;
	СтруктураШага.СтруктураПараметровФормы.НавигацияПараметрФормы = ИмяПараметра;
	СтруктураШага.ТипЭлементаРегламента = Титул;
	СтруктураШага.Выполнен = ЗначениеЗаполнено(Объект.ТитулГрузополучателяИдентификаторФайла);
	СтруктураШага.Доступен = СтруктураШага.Выполнен 
								Или Объект.ТекущийТитул = Титул;
	СтруктураШага.КоличествоВерсий = Макс(СтрокиВерсий.Количество(), 1);
	СтруктураШага.НомерВерсии = ОтображаемаяВерсияТитула(СтрокиВерсий, ИмяПараметра);
	Возврат СтруктураШага;
	
КонецФункции

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(ТекущийОбъект.ТекущийТитул) = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	ИнформацияПоПрефиксамТитула = ОбменСГИСЭПДКлиентСервер.ПрефиксТитулаПоЭлементуРегламентаЭДО(Объект.ТекущийТитул);
	
	Если ИнформацияПоПрефиксамТитула.ВПрограмме = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КонтейнерОтображаемойВерсии = Неопределено;
	
	ОбменСГИСЭПД.ЗаполнитьДополнительныеРеквизитыФормы(ЭтотОбъект, ИнформацияПоПрефиксамТитула.ВПрограмме, КонтейнерОтображаемойВерсии);
	
	НомерВерсии = ОбменСГИСЭПДКлиентСервер.ПолучитьЗначениеРеквизитаИзСтруктурыФормы(ЭтотОбъект, 
		ИнформацияПоПрефиксамТитула.ВПрограмме + "НомерИсправления", 0, КонтейнерОтображаемойВерсии);
	
	Если ПараметрыЗаписи.Свойство("НеДобавлятьВерсию") = Ложь Тогда
		// Запишем версию при необходимости
		ТекущийОбъект.ДополнительныеСвойства.Вставить("НомерВерсии", НомерВерсии);
		
		ИдентификаторФайлаВерсии = Объект[ИнформацияПоПрефиксамТитула.ВПрограмме + "ИдентификаторФайла"];
		
		ДатаВерсии = Объект[ИнформацияПоПрефиксамТитула.ВПрограмме + "ДатаФормированияФайла"]
						+ 60 * 60 * Час(Объект[ИнформацияПоПрефиксамТитула.ВПрограмме + "ВремяФормированияФайла"]) 
						+ 60 * Минута(Объект[ИнформацияПоПрефиксамТитула.ВПрограмме + "ВремяФормированияФайла"]) 
						+ Секунда(Объект[ИнформацияПоПрефиксамТитула.ВПрограмме + "ВремяФормированияФайла"]);
		
		ЭтоНоваяВерсия = Истина;
		Для Каждого СтрокаВерсии Из ВерсииТитулов Цикл
			Если СтрокаВерсии.Титул = ТекущийОбъект.ТекущийТитул 
			И СтрокаВерсии.НомерВерсии = НомерВерсии Тогда
				СтрокаВерсии.ИдентификаторФайла = ИдентификаторФайлаВерсии;	
				ЭтоНоваяВерсия = Ложь;
			КонецЕсли;	
		КонецЦикла;
	
		Если ЭтоНоваяВерсия = Истина Тогда
			НовСтрокаВерсии = ВерсииТитулов.Добавить();
			НовСтрокаВерсии.ДатаВерсии = ДатаВерсии;
			Если НовСтрокаВерсии.ДатаВерсии = Дата(1,1,1) Тогда
				НовСтрокаВерсии.ДатаВерсии = ТекущаяДатаСеанса();
			КонецЕсли;
			НовСтрокаВерсии.ИдентификаторФайла = ИдентификаторФайлаВерсии;
			НовСтрокаВерсии.НомерВерсии = НомерВерсии;
			НовСтрокаВерсии.Титул = ТекущийОбъект.ТекущийТитул;		
		КонецЕсли;
			
		ВерсияТитула = Новый Структура;
		ВерсияТитула.Вставить("НомерВерсии", НомерВерсии);
		ВерсияТитула.Вставить("ИдентификаторФайла", ИдентификаторФайлаВерсии);
		ВерсияТитула.Вставить("Титул", ТекущийОбъект.ТекущийТитул);
		ВерсияТитула.Вставить("ДатаВерсии", ДатаВерсии);
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ВерсияТитула", ВерсияТитула);
	КонецЕсли;
	
	МассивВерсийТитула = Неопределено;
	СтруктураРеквизитов.Свойство(ИнформацияПоПрефиксамТитула.ВПрограмме, МассивВерсийТитула);
	Если МассивВерсийТитула <> Неопределено Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("СтруктураРеквизитов", МассивВерсийТитула[Мин(НомерВерсии, МассивВерсийТитула.ВГраница())]);
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ТитулПеревозчикаПорядковыйНомерСопроводительнойВедомостиПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ТитулПеревозчикаДатаСоставленияСопроводительнойВедомостиПриИзменении(Элемент)
		
	ПриИзмененииДанныхНаФорме(Элемент.Имя);

КонецПроцедуры



&НаКлиенте
Процедура ПриИзмененииДанныхНаФорме(ИмяЭлемента)
	
	НомерВерсии = 0;
	Для Каждого СтрВерсия Из ВерсииТитулов Цикл
		Если СтрВерсия.Титул = Объект.ТекущийТитул И СтрВерсия.НомерВерсии > НомерВерсии Тогда
			НомерВерсии = СтрВерсия.НомерВерсии;
		КонецЕсли;	
	КонецЦикла;
	
	СтруктураРеквизитовФормы = Новый Структура(ЭтотОбъект.ОписаниеРеквизитовФормы.ПараметрыФормы);
	СтруктураРеквизитовОбъекта = Новый Структура(ЭтотОбъект.ОписаниеРеквизитовФормы.РеквизитыОбъекта);
	
	Если СтруктураРеквизитовФормы.Свойство(ИмяЭлемента) Тогда
		ЗначениеРеквизита = ЭтотОбъект[ИмяЭлемента];
	ИначеЕсли СтруктураРеквизитовОбъекта.Свойство(ИмяЭлемента) Тогда
		ЗначениеРеквизита = Объект[ИмяЭлемента];
	КонецЕсли;
	
	ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект, ИмяЭлемента, ЗначениеРеквизита);
	
	Если ИмяЭлемента = "СсылкаТитулПеревозчикаГрузоотправитель"
		Или ИмяЭлемента = "СсылкаТитулПеревозчикаГрузополучатель" Тогда
		ОбновитьДополнительныеРеквизитыОсновнойФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДополнительныеРеквизитыОсновнойФормы()
	
	ОбменСГИСЭПД.ДобавитьОписаниеДополнительныхРеквизитов(ЭтотОбъект, , "ОсновнаяФорма");
	ОбменСГИСЭПД.ПоказатьДополнительныеРеквизитыОсновнойФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыОтображаемойВерсии(ВыбранныйТитулСтрока, ВыбранныйТитул, ОтмечатьТекущий)
	
	Если ЗначениеЗаполнено(ВыбранныйТитул) Тогда
		Отбор = Новый Структура("Титул", ВыбранныйТитул);
		СтрокиВерсий = ВерсииТитулов.Выгрузить(Отбор, "НомерВерсии");
		СтрокиВерсий.Сортировать("НомерВерсии");	
		НомерВерсии = ОтображаемаяВерсияТитула(СтрокиВерсий, ВыбранныйТитулСтрока);
		
		РеквизитыТекущейВерсииТитула = ОбменСГИСЭПД.ПолучитьВерсиюТитулаДокумента(СтруктураРеквизитов, ВыбранныйТитул, НомерВерсии);
		ОбменСГИСЭПД.ЗаполнитьРеквизитыПоВерсии(ЭтотОбъект, ВыбранныйТитул, НомерВерсии, РеквизитыТекущейВерсииТитула);
		ЗаполнитьПредставленияАдресов(ВыбранныйТитул);
		Если ОтмечатьТекущий = Истина Тогда
			СтруктураСТекущимиДанными = ОбменСГИСЭПДКлиентСервер.ПолучитьСтруктуруПоТитулуИВерсии(ЭтотОбъект);
			СтруктураДанныхОбъекта = Неопределено;
			СтруктураСДаннымиФормыДляОформленияКнопок = ОбменСГИСЭПДКлиентСервер.СтруктураСДаннымиФормыДляОформленияКнопок(ЭтотОбъект);
			
			Результат = ИзменитьОформлениеКнопокНаСервере(СтруктураСТекущимиДанными,
				Неопределено,
				Неопределено,
				СтруктураДанныхОбъекта,
				СтруктураСДаннымиФормыДляОформленияКнопок);
				
			Если Результат.Успешно Тогда
				ЭтотОбъект.АдресДереваСоответствийИтаблицыКнопок = Результат.НовыйАдресВХранилище;	
				МассивОформления = Результат.МассивОформления;
				ОбменСГИСЭПДКлиентСервер.ОформлениеКнопокНаФорме(ЭтотОбъект, СтруктураСТекущимиДанными, МассивОформления);	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура РазместитьНавигацию(ВыбранныйТитулСтрока, Титул)

	НавигацияПараметрФормы = ВыбранныйТитулСтрока;
	
	ЗаполнитьРеквизитыОтображаемойВерсии(ВыбранныйТитулСтрока, Титул, Истина);

	СтруктураНавигацииПомощника = НавигацияПомощника();
	НавигацияПомощниковСВозвратомЭПД.РазместитьНавигацию(ЭтотОбъект, СтруктураНавигацииПомощника, Параметры);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеШага(ИмяШага, НавигационнаяСсылка)
	
	ИнкрементВерсии = 0;
	Если НавигационнаяСсылка = "Влево" Тогда
		ИнкрементВерсии = -1;
	ИначеЕсли НавигационнаяСсылка = "Вправо" Тогда
		ИнкрементВерсии = 1;
	КонецЕсли;
	
	Если ИмяШага = "Шаг1" Тогда
		ИнкрементироватьОтображаемуюВерсиюТитула("ТитулПеревозчика", ИнкрементВерсии);
		УстановитьВидимостьСтраницыТитулПеревозчика(ИмяШага);
	ИначеЕсли ИмяШага = "Шаг2" Тогда
		Если Объект.ВидОперации = 2 Тогда
			ИнкрементироватьОтображаемуюВерсиюТитула("ТитулГрузополучателя", ИнкрементВерсии);
			УстановитьВидимостьСтраницыТитулГрузополучателя(ИмяШага);	
		Иначе
			ИнкрементироватьОтображаемуюВерсиюТитула("ТитулГрузоотправителя", ИнкрементВерсии);
			УстановитьВидимостьСтраницыТитулГрузоотправителя(ИмяШага);
		КонецЕсли;
	ИначеЕсли ИмяШага = "Шаг3" Тогда
		ИнкрементироватьОтображаемуюВерсиюТитула("ДопТитулПеревозчика", ИнкрементВерсии);
		УстановитьВидимостьСтраницыДопТитулПеревозчика(ИмяШага);
	ИначеЕсли ИмяШага = "Шаг4" Тогда
		ИнкрементироватьОтображаемуюВерсиюТитула("ТитулГрузополучателя", ИнкрементВерсии);
		УстановитьВидимостьСтраницыТитулГрузополучателя(ИмяШага);		
	КонецЕсли;
	
	ОбменСГИСЭПДКлиентСервер.ИзменитьОформлениеФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти


///////////////////////////////



&НаКлиенте
Функция ИдентификаторОтправителя()
	
	ТипОрганизация = ОбменСГИСЭПДВызовСервера.ПолучитьТипОрганизация();
	Если ТипЗнч(Объект.СсылкаТитулПеревозчикаПеревозчик) = ТипОрганизация Тогда
		Результат = Объект.ИдентификаторПеревозчика;
	ИначеЕсли ТипЗнч(Объект.СсылкаТитулПеревозчикаГрузоотправитель) = ТипОрганизация Тогда
		Результат = Объект.ИдентификаторГрузоотправителя;
	Иначе
		Результат = Объект.ИдентификаторГрузополучателя;	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


&НаКлиенте
Процедура ЗаполнитьУчастников_ПослеНастроек(Настройка, ДополнительныеПараметры) Экспорт
	
	Если Настройка = Неопределено Тогда
		ЭтотОбъект.Доступность = Истина;
		Возврат;
	КонецЕсли;
	
	Если Настройка.Отправитель = Объект.СсылкаТитулПеревозчикаПеревозчик Тогда
		Объект.ИдентификаторПеревозчика = Настройка.ИдентификаторОтправителя;
	КонецЕсли;
	
	Если Настройка.Отправитель = Объект.СсылкаТитулПеревозчикаГрузоотправитель Тогда
		Объект.ИдентификаторГрузоотправителя = Настройка.ИдентификаторОтправителя;
	КонецЕсли;
	
	Если Настройка.Отправитель = Объект.СсылкаТитулПеревозчикаГрузополучатель Тогда
		Объект.ИдентификаторГрузополучателя = Настройка.ИдентификаторОтправителя;
	КонецЕсли;
	
	Если Настройка.Получатель = Объект.СсылкаТитулПеревозчикаПеревозчик Тогда
		Объект.ИдентификаторПеревозчика = Настройка.ИдентификаторПолучателя;
	КонецЕсли;
	
	Если Настройка.Получатель = Объект.СсылкаТитулПеревозчикаГрузоотправитель Тогда
		Объект.ИдентификаторГрузоотправителя = Настройка.ИдентификаторПолучателя;
	КонецЕсли;
	
	Если Настройка.Получатель = Объект.СсылкаТитулПеревозчикаГрузополучатель Тогда
		Объект.ИдентификаторГрузополучателя = Настройка.ИдентификаторПолучателя;
	КонецЕсли;	
	
	ЭтоНастройкаПередОтправкой = Неопределено;
	ДополнительныеПараметры.Свойство("ЭтоНастройкаПередОтправкой", ЭтоНастройкаПередОтправкой);
	Если ДополнительныеПараметры <> Неопределено И ДополнительныеПараметры.Свойство("КлючНастроекОтправкиСледующий") = Истина Тогда
		ДополнительныеПараметрыСледующий = Новый Структура;
		ДополнительныеПараметрыСледующий.Вставить("ПараметрыЗаписи", ДополнительныеПараметры.ПараметрыЗаписи);
		ДополнительныеПараметрыСледующий.Вставить("ЭтоНастройкаПередОтправкой", ЭтоНастройкаПередОтправкой);
		ОповещениеОкончаниеНастройки = Новый ОписаниеОповещения("ЗаполнитьУчастников_ПослеНастроек", ЭтотОбъект, ДополнительныеПараметрыСледующий);
		
		НастройкиОтправкиСледующий = НастройкиОтправкиЭДОСлужебныйВызовСервера.НастройкиОтправки(ДополнительныеПараметры.КлючНастроекОтправкиСледующий);	
		Если НастройкиОтправкиСледующий = Неопределено И ЭтоНастройкаПередОтправкой = Истина Тогда		
			НастройкиЭДОКлиент.НастроитьОбменСКонтрагентом(ДополнительныеПараметры.КлючНастроекОтправкиСледующий, 
																ОповещениеОкончаниеНастройки, 
																ДополнительныеПараметры.ПараметрыНастройкиОбменаСКонтрагентом);
		Иначе
			ВыполнитьОбработкуОповещения(ОповещениеОкончаниеНастройки, НастройкиОтправкиСледующий);	
		КонецЕсли;
		Возврат;
	КонецЕсли;
																		
	ИзмененСоставУчастников = Ложь;
	
	Если ЭтоНастройкаПередОтправкой = Истина
		И ЗначениеЗаполнено(Объект.ИдентификаторГрузоотправителя)
		И ЗначениеЗаполнено(Объект.ИдентификаторГрузополучателя)
		И ЗначениеЗаполнено(Объект.ИдентификаторПеревозчика) Тогда
			ОтправитьЧерезЭДОПослеЗаписи();
	Иначе
		ЭтотОбъект.Доступность = Истина;
		ПараметрыЗаписи = Новый Структура;
		ПараметрыЗаписи.Вставить("НеТребуетсяИдентификацияФайла");
		Записать(ПараметрыЗаписи);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры



&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьИмяФайла(Шаблон, ИдГрузоотправителя, ИдГрузополучателя, ИдПеревозчика, НаличиеДругихПолучателей = Ложь)
	
	#Если Сервер Тогда
		ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	#Иначе
		ТекущаяДатаСеанса = ОбщегоНазначенияКлиент.ДатаСеанса();
	#КонецЕсли
	
	Результат = Шаблон;
	Результат = СтрЗаменить(Результат, "ИдГО", ИдГрузоотправителя);
	Результат = СтрЗаменить(Результат, "ИдПЕР", ИдПеревозчика);
	Результат = СтрЗаменить(Результат, "ИдГП", ИдГрузополучателя);
	Результат = СтрЗаменить(Результат, "GGGGMMDD", Формат(ТекущаяДатаСеанса, "ДФ=ггггММдд"));
	Результат = СтрЗаменить(Результат, "ГУИДФайла", Строка(Новый УникальныйИдентификатор()));
	Результат = СтрЗаменить(Результат, "НалДр", ?(НаличиеДругихПолучателей = Истина, "1", "0"));
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция УстановитьНомерДокумента()
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.УстановитьНовыйНомер();
	
	Возврат ДокументОбъект.Номер;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьИндексПоТекущемуТитулу(ТекущийТитул)
	
	Индекс = Неопределено;
	
	Если ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_1") Тогда
		Индекс = 1;
	ИначеЕсли ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_2") Тогда
		Индекс = 2;
	ИначеЕсли ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_3") Тогда
		Индекс = 3;
	ИначеЕсли ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_4") Тогда
		Индекс = 4;
	ИначеЕсли ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_5") Тогда
		Индекс = 5;
	ИначеЕсли ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул2_1") Тогда
		Индекс = 1;
	ИначеЕсли ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул2_2") Тогда
		Индекс = 2;
	ИначеЕсли ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул3_1") Тогда
		Индекс = 1;
	ИначеЕсли ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул3_2") Тогда
		Индекс = 2;
	КонецЕсли;
	
	Если Индекс = Неопределено Тогда
		Индекс = 1;
	КонецЕсли;
	
	Возврат Индекс;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьИдентификациюФайлаОбмена(ИнформацияПоПрефиксамТитула, ИдентификаторОтправителя, Переформировать = Ложь, ОбработкаЗавершения = Неопределено)

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИнформацияПоПрефиксамТитула", ИнформацияПоПрефиксамТитула);
	ДополнительныеПараметры.Вставить("ИдентификаторОтправителя", ИдентификаторОтправителя);
	ДополнительныеПараметры.Вставить("Переформировать", Переформировать);
	ДополнительныеПараметры.Вставить("ОбработкаЗавершения", ОбработкаЗавершения);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьИдентификациюФайлаОбмена_ПослеПолученияУИД", ЭтотОбъект, ДополнительныеПараметры);
	
	Если ЗначениеЗаполнено(Объект.УИДМинтранс) Тогда
		ЗаполнитьИдентификациюФайлаОбмена_ПослеПолученияУИД(Объект.УИДМинтранс, ДополнительныеПараметры);
	Иначе
		Если Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_1")
			Или Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_2")
			Или Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_5") Тогда
			ОбменСГИСЭПДКлиент.ПолучитьУИДМинтранса(ОписаниеОповещения, Объект.ИдентификаторПеревозчика);
		Иначе
			ОбменСГИСЭПДКлиент.ПолучитьУИДМинтранса(ОписаниеОповещения, ИдентификаторОтправителя(), Истина, Объект.Ссылка);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИдентификациюФайлаОбмена_ПослеПолученияУИД(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Объект.УИДМинтранс) = Ложь Тогда
		Объект.УИДМинтранс = Результат;
	КонецЕсли;
	
	ИнформацияПоПрефиксамТитула = ДополнительныеПараметры.ИнформацияПоПрефиксамТитула;
	ИдентификаторОтправителя = ДополнительныеПараметры.ИдентификаторОтправителя;
	Переформировать = ДополнительныеПараметры.Переформировать;	
	
	ТребуетсяЗаполнить = Переформировать = Истина
			Или ОбменСГИСЭПДКлиент.ТребуетсяИдентификацияФайлаТитула(Объект.Ссылка, Объект.ТекущийТитул, ВерсииТитулов);
	
	Если ТребуетсяЗаполнить = Истина Тогда
		Индекс = ПолучитьИндексПоТекущемуТитулу(Объект.ТекущийТитул);
	
		КонтейнерОтображаемойВерсии = ОбменСГИСЭПДКлиентСервер.КонтейнерОтображаемойВерсииРеквизита(ЭтотОбъект,
			ИнформацияПоПрефиксамТитула.ВПрограмме, Индекс);
		СтруктураВерсииТитула = КонтейнерОтображаемойВерсии.СтруктураРеквизитовЗаполнение[КонтейнерОтображаемойВерсии.ПрефиксТитула][КонтейнерОтображаемойВерсии.НомерВерсии];
		
		ДатаИВремяФормирования = ОбщегоНазначенияКлиент.ДатаСеанса();
		ДатаФормирования = НачалоДня(ДатаИВремяФормирования);
		ВремяФормирования = Дата('00010101') + (ДатаИВремяФормирования - ДатаФормирования);
	
		Если Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_1")
			Или Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_2")
			Или Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_5") Тогда	
			Если ЗначениеЗаполнено(Объект.Ссылка) = Ложь Тогда
				Объект.Номер = УстановитьНомерДокумента();
			КонецЕсли;	
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(
				ЭтотОбъект, "ТитулПеревозчикаДатаСоставленияСопроводительнойВедомости", Объект.Дата, Ложь, КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(
				ЭтотОбъект, "ТитулПеревозчикаПорядковыйНомерСопроводительнойВедомости", Объект.Номер, Ложь, КонтейнерОтображаемойВерсии);
			
			ИтераторСтроки = 1;
			КлючСтруктуры = "ТитулПеревозчикаОписаниеГруза__" + ИтераторСтроки + "__ИдентификаторСтроки";
			ИдентификаторСтроки = Неопределено;
			Пока СтруктураВерсииТитула.Свойство(КлючСтруктуры, ИдентификаторСтроки) Цикл
				
				ЗначенияСтроки = Новый Структура("МаркировкаГруза", "Отсутствует");
				ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеПодчиненнойСтрокиВСтруктуреФормы(
					ЭтотОбъект, "ТитулПеревозчикаМаркировка", ИдентификаторСтроки, ЗначенияСтроки, Ложь, КонтейнерОтображаемойВерсии);
				
				ИтераторСтроки = ИтераторСтроки + 1;
				КлючСтруктуры = "ТитулПеревозчикаОписаниеГруза__" + ИтераторСтроки + "__ИдентификаторСтроки";
			КонецЦикла;
			
		ИначеЕсли Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_3")
			Или Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_4") Тогда
				ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
					"ДопТитулПеревозчикаДатаСоставленияСопроводительнойВедомости",
					Объект.ТитулПеревозчикаДатаСоставленияСопроводительнойВедомости, ,
					КонтейнерОтображаемойВерсии);
				ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
					"ДопТитулПеревозчикаПорядковыйНомерСопроводительнойВедомости",
					Объект.ТитулПеревозчикаПорядковыйНомерСопроводительнойВедомости, ,
					КонтейнерОтображаемойВерсии);
					
				ИтераторСтроки = 1;
				КлючСтруктуры = "ДопТитулПеревозчикаОписаниеГруза__" + ИтераторСтроки + "__ИдентификаторСтроки";
				ИдентификаторСтроки = Неопределено;
				Пока СтруктураВерсииТитула.Свойство(КлючСтруктуры, ИдентификаторСтроки) Цикл
					
					ЗначенияСтроки = Новый Структура("МаркировкаГруза", "Отсутствует");
					ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеПодчиненнойСтрокиВСтруктуреФормы(
						ЭтотОбъект, "ДопТитулПеревозчикаМаркировка", ИдентификаторСтроки, ЗначенияСтроки, Ложь, КонтейнерОтображаемойВерсии);
					
					ИтераторСтроки = ИтераторСтроки + 1;
					КлючСтруктуры = "ДопТитулПеревозчикаОписаниеГруза__" + ИтераторСтроки + "__ИдентификаторСтроки";
				КонецЦикла;
					
		ИначеЕсли Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул2_1")
			Или Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул2_2") Тогда
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулГрузоотправителяИдентификаторФайлаОбменаИнформацииПеревозчика",
				Объект.ТитулПеревозчикаИдентификаторФайла, ,
				КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулГрузоотправителяДатаФормированияФайлаОбменаИнформацииПеревозчика",
				Объект.ТитулПеревозчикаДатаФормированияФайла, ,
				КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулГрузоотправителяВремяФормированияФайлаОбменаИнформацииПеревозчика",
				Объект.ТитулПеревозчикаВремяФормированияФайла, ,
				КонтейнерОтображаемойВерсии);
			Если Объект.ВидОперации = 0 Тогда
				ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
					"ТитулГрузоотправителяЭлектроннаяПодписьФайлаОбменаИнформацииПеревозчика",
					ПолучитьПодписьТитулаBase64(ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_1")), ,
					КонтейнерОтображаемойВерсии);
			ИначеЕсли Объект.ВидОперации = 1 Тогда
				ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
					"ТитулГрузоотправителяЭлектроннаяПодписьФайлаОбменаИнформацииПеревозчика",
					ПолучитьПодписьТитулаBase64(ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_2")), ,
					КонтейнерОтображаемойВерсии);
			КонецЕсли;
			
		ИначеЕсли Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул3_1") Тогда
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулГрузополучателяИдентификаторФайлаОбменаИнформацииПеревозчика",
				Объект.ДопТитулПеревозчикаИдентификаторФайла, ,
				КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулГрузополучателяДатаФормированияФайлаОбменаИнформацииПеревозчика",
				Объект.ДопТитулПеревозчикаДатаФормированияФайла, ,
				КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулГрузополучателяВремяФормированияФайлаОбменаИнформацииПеревозчика",
				Объект.ДопТитулПеревозчикаВремяФормированияФайла, ,
				КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулГрузополучателяЭлектроннаяПодписьФайлаОбменаИнформацииПеревозчика",
				ПолучитьПодписьТитулаBase64(ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_3")), ,
				КонтейнерОтображаемойВерсии);	
		ИначеЕсли Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул3_2") Тогда
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулГрузополучателяИдентификаторФайлаОбменаИнформацииПеревозчика",
				Объект.ТитулПеревозчикаИдентификаторФайла, ,
				КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулГрузополучателяДатаФормированияФайлаОбменаИнформацииПеревозчика",
				Объект.ТитулПеревозчикаДатаФормированияФайла, ,
				КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулГрузополучателяВремяФормированияФайлаОбменаИнформацииПеревозчика",
				Объект.ТитулПеревозчикаВремяФормированияФайла, ,
				КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулГрузополучателяЭлектроннаяПодписьФайлаОбменаИнформацииПеревозчика",
				ПолучитьПодписьТитулаBase64(ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_5")), ,
				КонтейнерОтображаемойВерсии);
		КонецЕсли;
		
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
			ИнформацияПоПрефиксамТитула.ВПрограмме + "ВерсияПрограммы",
			ОбменСГИСЭПДКлиентСервер.ВерсияПрограммы(), ,
			КонтейнерОтображаемойВерсии);
		
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
			ИнформацияПоПрефиксамТитула.ВПрограмме + "ВерсияФормата",
			ОбменСГИСЭПДКлиентСервер.ВерсияФормата("ЭлектроннаяСопроводительнаяВедомость"), ,
			КонтейнерОтображаемойВерсии);
		
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
			ИнформацияПоПрефиксамТитула.ВПрограмме + "КодДокументаПоКНД",
			ИнформацияПоПрефиксамТитула.КНД, ,
			КонтейнерОтображаемойВерсии);
		
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(
				ЭтотОбъект, ИнформацияПоПрефиксамТитула.ПолеУИД, Объект.УИДМинтранс, Ложь, КонтейнерОтображаемойВерсии);
		
		ДанныеСертификатаУчетнойЗаписиЭДО = ОбменСГИСЭПДКлиентСервер.ДанныеСертификатаУчетнойЗаписиЭДО(ИдентификаторОтправителя);

		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
			ИнформацияПоПрефиксамТитула.ВПрограмме + "ПодписантФамилия",
			ДанныеСертификатаУчетнойЗаписиЭДО.Фамилия, ,
			КонтейнерОтображаемойВерсии);
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
			ИнформацияПоПрефиксамТитула.ВПрограмме + "ПодписантИмя",
			ДанныеСертификатаУчетнойЗаписиЭДО.Имя, ,
			КонтейнерОтображаемойВерсии);
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
			ИнформацияПоПрефиксамТитула.ВПрограмме + "ПодписантОтчество",
			ДанныеСертификатаУчетнойЗаписиЭДО.Отчество, ,
			КонтейнерОтображаемойВерсии);
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
			ИнформацияПоПрефиксамТитула.ВПрограмме + "ПодписантДолжность",
			ДанныеСертификатаУчетнойЗаписиЭДО.Должность, ,
			КонтейнерОтображаемойВерсии);
			
		Если ЗначениеЗаполнено(ДанныеСертификатаУчетнойЗаписиЭДО.НомерДоверенности) Тогда
			Если ЗначениеЗаполнено(ДанныеСертификатаУчетнойЗаписиЭДО.СведенияОбИнформационнойСистеме) Тогда
				ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
					ИнформацияПоПрефиксамТитула.ВПрограмме + "ПодписантДоверенностьИнформационнаяСистема",
					ДанныеСертификатаУчетнойЗаписиЭДО.СведенияОбИнформационнойСистеме, ,
					КонтейнерОтображаемойВерсии);	
			Иначе
				ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
					ИнформацияПоПрефиксамТитула.ВПрограмме + "ПодписантДоверенностьИдентификаторФайла",
					ДанныеСертификатаУчетнойЗаписиЭДО.НомерДоверенности, ,
					КонтейнерОтображаемойВерсии);
				ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
					ИнформацияПоПрефиксамТитула.ВПрограмме + "ПодписантДоверенностьДата",
					ДанныеСертификатаУчетнойЗаписиЭДО.ДатаДоверенности, ,
					КонтейнерОтображаемойВерсии);
				ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
					ИнформацияПоПрефиксамТитула.ВПрограмме + "ПодписантДоверенностьНомер",
					ДанныеСертификатаУчетнойЗаписиЭДО.ПорядковыйНомерДоверенности, ,
					КонтейнерОтображаемойВерсии);
			КонецЕсли;
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				ИнформацияПоПрефиксамТитула.ВПрограмме + "ПодписантКодСтатуса",
				"2", ,
				КонтейнерОтображаемойВерсии);
		Иначе
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				ИнформацияПоПрефиксамТитула.ВПрограмме + "ПодписантКодСтатуса",
				"1", ,
				КонтейнерОтображаемойВерсии);
		КонецЕсли;
		
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(
				ЭтотОбъект, ИнформацияПоПрефиксамТитула.ВПрограмме + "ИдентификаторФайла", ПолучитьИмяФайла(ИнформацияПоПрефиксамТитула.ШаблонИмениФайла, 
																			Объект.ИдентификаторГрузоотправителя,
																			Объект.ИдентификаторГрузополучателя,
																			Объект.ИдентификаторПеревозчика), , КонтейнерОтображаемойВерсии);
																			
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(
					ЭтотОбъект, ИнформацияПоПрефиксамТитула.ВПрограмме + "ДатаФормированияФайла", 
					ДатаФормирования, , 
					КонтейнерОтображаемойВерсии); 
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(
					ЭтотОбъект, ИнформацияПоПрефиксамТитула.ВПрограмме + "ВремяФормированияФайла", 
					ВремяФормирования, ,
					КонтейнерОтображаемойВерсии);
	КонецЕсли;
				
	// Проверим заполнение
	Если ОбменСГИСЭПДКлиент.ЗаполнитьТаблицуОшибокЭПД(ЭтотОбъект, ОшибкиЗаполнения, Истина) = Истина Тогда	
		Элементы.ГруппаОшибки.Видимость = Истина;
		Элементы.НадписьОшибки.Заголовок = "Ошибки заполнения (" + Строка(ОшибкиЗаполнения.Количество()) + ")";
		Если ТребуетсяЗаполнить = Истина Тогда
			ОчиститьИдФайлТекущегоТитула();
		КонецЕсли;
	Иначе
		Элементы.НадписьОшибки.Заголовок = "Ошибки заполнения";
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("ОбработкаЗавершения") 
		И ДополнительныеПараметры.ОбработкаЗавершения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОбработкаЗавершения);
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Функция ПолучитьПодписьТитулаBase64(ТипЭлементаРегламента) 
	
	ПодписьBase64 = "";
	
	СообщениеТитула = ОбменСГИСЭПД.ПолучитьСообщениеТитула(Объект.Ссылка, ТипЭлементаРегламента);
	
	Если ЗначениеЗаполнено(СообщениеТитула) Тогда
		УстановленныеПодписи = ЭлектронныеДокументыЭДО.УстановленныеПодписи(СообщениеТитула);
		Для Каждого ОписаниеПодписи Из УстановленныеПодписи Цикл
			ПодписьBase64 = Base64Строка(ОписаниеПодписи.Подпись);
			Прервать;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ПодписьBase64;
	
КонецФункции

&НаКлиенте
Процедура ОтправитьЧерезЭДО(Команда)
	
	ЭтотОбъект.Доступность = Ложь;
		
	Если ЗначениеЗаполнено(Объект.ИдентификаторГрузоотправителя) = Ложь
		Или ЗначениеЗаполнено(Объект.ИдентификаторГрузополучателя) = Ложь
		Или ЗначениеЗаполнено(Объект.ИдентификаторПеревозчика) = Ложь
		Или ИзмененСоставУчастников = Истина Тогда
		ЗаполнитьУчастников(Истина);
	Иначе
		ОтправитьЧерезЭДОПослеЗаписи();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьУчастников(ЭтоНастройкаПередОтправкой = Ложь)
	
	КлючНастроекОтправки = НастройкиЭДОКлиентСервер.НовыйКлючНастроекОтправки();
	КлючНастроекОтправки.ВидДокумента = Объект.ВидДокумента;
	
	КлючНастроекОтправкиСледующий = НастройкиЭДОКлиентСервер.НовыйКлючНастроекОтправки();
	КлючНастроекОтправкиСледующий.ВидДокумента = Объект.ВидДокумента;
	
	ТипОрганизация = ОбменСГИСЭПДВызовСервера.ПолучитьТипОрганизация();
	Если Объект.ЭтоВходящий = Истина Тогда
		Если ТипЗнч(Объект.СсылкаТитулПеревозчикаГрузоотправитель) = ТипОрганизация Тогда
			КлючНастроекОтправки.Отправитель = Объект.СсылкаТитулПеревозчикаГрузоотправитель;
			КлючНастроекОтправки.Получатель = Объект.СсылкаТитулПеревозчикаПеревозчик;
			КлючНастроекОтправкиСледующий.Отправитель = Объект.СсылкаТитулПеревозчикаГрузоотправитель;
			КлючНастроекОтправкиСледующий.Получатель = Объект.СсылкаТитулПеревозчикаГрузополучатель;
		Иначе
			КлючНастроекОтправки.Отправитель = Объект.СсылкаТитулПеревозчикаГрузополучатель;
			КлючНастроекОтправки.Получатель = Объект.СсылкаТитулПеревозчикаПеревозчик;
			КлючНастроекОтправкиСледующий.Отправитель = Объект.СсылкаТитулПеревозчикаГрузополучатель;
			КлючНастроекОтправкиСледующий.Получатель = Объект.СсылкаТитулПеревозчикаГрузоотправитель;
		КонецЕсли;
	Иначе
		КлючНастроекОтправки.Отправитель = Объект.СсылкаТитулПеревозчикаПеревозчик;
		КлючНастроекОтправки.Получатель = Объект.СсылкаТитулПеревозчикаГрузоотправитель; 
		КлючНастроекОтправкиСледующий.Отправитель = Объект.СсылкаТитулПеревозчикаПеревозчик;
		КлючНастроекОтправкиСледующий.Получатель = Объект.СсылкаТитулПеревозчикаГрузополучатель;
	КонецЕсли;
	
	НастройкиОтправки = НастройкиОтправкиЭДОСлужебныйВызовСервера.НастройкиОтправки(КлючНастроекОтправки);
	
	ПараметрыНастройкиОбменаСКонтрагентом = НастройкиЭДОКлиент.НовыеПараметрыНастройкиОбменаСКонтрагентом();
	ПараметрыНастройкиОбменаСКонтрагентом.НастройкаОдногоДокумента = Истина;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("КлючНастроекОтправки", КлючНастроекОтправки);
	ДополнительныеПараметры.Вставить("КлючНастроекОтправкиСледующий", КлючНастроекОтправкиСледующий);
	ДополнительныеПараметры.Вставить("ПараметрыНастройкиОбменаСКонтрагентом", ПараметрыНастройкиОбменаСКонтрагентом);
	ДополнительныеПараметры.Вставить("ПараметрыЗаписи", Новый Структура);
	ДополнительныеПараметры.Вставить("ЭтоНастройкаПередОтправкой", ЭтоНастройкаПередОтправкой);
	
	ОповещениеОкончаниеНастройки = Новый ОписаниеОповещения("ЗаполнитьУчастников_ПослеНастроек", ЭтотОбъект, ДополнительныеПараметры);
	Если НастройкиОтправки = Неопределено И ЭтоНастройкаПередОтправкой = Истина Тогда	
		НастройкиЭДОКлиент.НастроитьОбменСКонтрагентом(КлючНастроекОтправки, ОповещениеОкончаниеНастройки, ПараметрыНастройкиОбменаСКонтрагентом);
	Иначе
		ВыполнитьОбработкуОповещения(ОповещениеОкончаниеНастройки, НастройкиОтправки); 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЧерезЭДОПослеЗаписи(Повторно = Ложь)
	
	ДанныеСостоянияЭДО = ИнтеграцияЭДОВызовСервера.ДанныеСостоянияЭДОДляФормыОбъектаУчета(Объект.Ссылка);
	
	ИнформацияПоПрефиксамТитула = ОбменСГИСЭПДКлиентСервер.ПрефиксТитулаПоЭлементуРегламентаЭДО(Объект.ТекущийТитул);
	ИндексТитула = ПолучитьИндексПоТекущемуТитулу(Объект.ТекущийТитул);
	КонтейнерОтображаемойВерсии = ОбменСГИСЭПДКлиентСервер.КонтейнерОтображаемойВерсииРеквизита(ЭтотОбъект,
		ИнформацияПоПрефиксамТитула.ВПрограмме, ИндексТитула);
					
	Если ДанныеСостоянияЭДО.ПредставлениеСостояния = "Требуется уточнение"
		Или ДанныеСостоянияЭДО.ПредставлениеСостояния = "Требуется повторная отправка" Тогда
		Если Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_1") 
			Или Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_2")
			Или Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_5") Тогда
			
			ЗначениеРеквизита = ОбменСГИСЭПДКлиентСервер.ПолучитьЗначениеРеквизитаИзСтруктурыФормы(ЭтотОбъект, 
				"ТитулПеревозчикаНомерИсправления", 0, КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулПеревозчикаНомерИсправления",
				Число(ЗначениеРеквизита) + 1, ,
				КонтейнерОтображаемойВерсии);
			
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулПеревозчикаДатаИсправления",
				ОбщегоНазначенияКлиент.ДатаСеанса(), ,
				КонтейнерОтображаемойВерсии);
			
		ИначеЕсли Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_3")
			Или Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул1_4") Тогда
				
				ЗначениеРеквизита = ОбменСГИСЭПДКлиентСервер.ПолучитьЗначениеРеквизитаИзСтруктурыФормы(ЭтотОбъект, 
				"ДопТитулПеревозчикаНомерИсправления", 0, КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ДопТитулПеревозчикаНомерИсправления",
				Число(ЗначениеРеквизита) + 1, ,
				КонтейнерОтображаемойВерсии);
							
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ДопТитулПеревозчикаДатаИсправления",
				ОбщегоНазначенияКлиент.ДатаСеанса(), ,
				КонтейнерОтображаемойВерсии);
							
		ИначеЕсли Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул2_1")
			Или Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул2_2") Тогда
				
			ЗначениеРеквизита = ОбменСГИСЭПДКлиентСервер.ПолучитьЗначениеРеквизитаИзСтруктурыФормы(ЭтотОбъект, 
				"ТитулГрузоотправителяНомерИсправления", 0, КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулГрузоотправителяНомерИсправления",
				Число(ЗначениеРеквизита) + 1, ,
				КонтейнерОтображаемойВерсии);
							
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулГрузоотправителяДатаИсправления",
				ОбщегоНазначенияКлиент.ДатаСеанса(), ,
				КонтейнерОтображаемойВерсии);
							
		ИначеЕсли Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул3_1")
			Или Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭСВ_Титул3_2") Тогда
				
			ЗначениеРеквизита = ОбменСГИСЭПДКлиентСервер.ПолучитьЗначениеРеквизитаИзСтруктурыФормы(ЭтотОбъект, 
				"ТитулГрузополучателяНомерИсправления", 0, КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулГрузополучателяНомерИсправления",
				Число(ЗначениеРеквизита) + 1, ,
				КонтейнерОтображаемойВерсии);
							
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулГрузополучателяДатаИсправления",
				ОбщегоНазначенияКлиент.ДатаСеанса(), ,
				КонтейнерОтображаемойВерсии);
							
		КонецЕсли;
		Повторно = Истина;	
	КонецЕсли;
	
	Если Повторно = Истина Тогда
		ТитулыПоДокументу = ОбменСГИСЭПДВызовСервера.ПолучитьТитулыПоДокументу(Объект.Ссылка, Истина);
		ОбменСГИСЭПДКлиентСервер.УстановитьТекущийДоступныйТитулЭСВ(Объект, ТитулыПоДокументу, Истина);
	КонецЕсли;
	ИнформацияПоПрефиксамТитула = ОбменСГИСЭПДКлиентСервер.ПрефиксТитулаПоЭлементуРегламентаЭДО(Объект.ТекущийТитул);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДанныеСостоянияЭДО", ДанныеСостоянияЭДО);
	ДополнительныеПараметры.Вставить("ЭтоИсправление", Повторно);
	ДополнительныеПараметры.Вставить("Повторно", Повторно);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьЧерезЭДОПослеЗаписи_Завершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ЗаполнитьИдентификациюФайлаОбмена(ИнформацияПоПрефиксамТитула, ИдентификаторОтправителя(), Повторно, ОписаниеОповещения);	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЧерезЭДОПослеЗаписи_Завершение(Результат, ДополнительныеПарамтеры) Экспорт
	
	ДанныеСостоянияЭДО = ДополнительныеПарамтеры.ДанныеСостоянияЭДО;
	Повторно = ДополнительныеПарамтеры.Повторно;
	ЭтоИсправление = ДополнительныеПарамтеры.ЭтоИсправление;
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("НеТребуетсяИдентификацияФайла");
	Записать(ПараметрыЗаписи);
	
	Если ЗначениеЗаполнено(Объект.ТекущийТитул) = Ложь Тогда
		Доступность = Истина;
		Возврат;
	КонецЕсли;
	
	Если ОшибкиЗаполнения.Количество() > 0 Тогда	
		Элементы.ГруппаОшибки.Видимость = Истина;
		Элементы.НадписьОшибки.Заголовок = "Ошибки заполнения (" + Строка(ОшибкиЗаполнения.Количество()) + ")";
		Доступность = Истина;
		ПоказатьПредупреждение(, НСтр("ru='Документ не отправлен, обнаружены ошибки заполнения. Исправьте ошибки и повторите попытку'"));
		Возврат;
	Иначе
		Элементы.НадписьОшибки.Заголовок = "Ошибки заполнения";
	КонецЕсли;
	
	НаборДействий = Новый Соответствие;		
	
	Если Повторно = Истина 
		Или ДанныеСостоянияЭДО.ПредставлениеСостояния = "Требуется повторная отправка" 
		Или ЗначениеЗаполнено(ОбменСГИСЭПДКлиентСервер.ПолучитьСообщениеТитула(Объект.Ссылка, Объект.ТекущийТитул)) = Ложь Тогда
			Если Объект.ЭтоВходящий = Ложь И ДанныеСостоянияЭДО.СуществуетАктуальныйДокумент = Ложь Тогда
				ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
					"Перечисление.ДействияПоЭДО.Сформировать"));	
			Иначе
				ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
					"Перечисление.ДействияПоЭДО.СформироватьОтвет"));
			КонецЕсли;	   
			
			ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
					"Перечисление.ДействияПоЭДО.Подписать"));
			ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
				"Перечисление.ДействияПоЭДО.ПодготовитьКОтправке"));  
	Иначе		
		Если ДанныеСостоянияЭДО.ПредставлениеСостояния <> "Требуется подготовка к отправке" 
			И ДанныеСостоянияЭДО.ПредставлениеСостояния <> "Требуется отправка" Тогда
				ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
					"Перечисление.ДействияПоЭДО.Подписать"));
		КонецЕсли;
		
		Если ДанныеСостоянияЭДО.ПредставлениеСостояния <> "Требуется отправка" Тогда
			ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
				"Перечисление.ДействияПоЭДО.ПодготовитьКОтправке"));
		КонецЕсли;		
	КонецЕсли;
	
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.Отправить"));
	
	ПараметрыВыполненияДействийПоЭДО = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();	
	ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ОбъектыУчета.Добавить(Объект.Ссылка);
	ПараметрыВыполненияДействийПоЭДО.НаборДействий = НаборДействий;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеУспешногоЗавершения",
		Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ЭтотОбъект, 
									НаборДействий, 
									"ПослеВыполненияДействийПоЭДО_ОбработкаОшибки", ЭтотОбъект));

	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ИнтерфейсДокументовЭДОКлиент, ДополнительныеПараметры);
	
	ЭлектронныеДокументыЭДОКлиент.НачатьВыполнениеДействийПоЭДО(Оповещение, ПараметрыВыполненияДействийПоЭДО);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЧерезЭДОПовторно(Команда)
	
	ЭтотОбъект.Доступность = Ложь;
	
	// Сейчас при повторной отправке происходит ошибка на стороне оператора,
	// у них это считается дубликатом (одинаковый титул в рамках одного документооборота)
	// Это не верно, т.к. повторная отправка возможна (после УОУ или ошибки от ГИС)
	ОтправитьЧерезЭДОПослеЗаписи(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыполненияДействийПоЭДО(Результат, НаборДействий) Экспорт
	
	ДействиеОтправить = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Отправить");
	КоличествоОтправлено = Результат.ОбработаноПоДействиям.Получить(ДействиеОтправить);
	
	Если КоличествоОтправлено <> Неопределено И КоличествоОтправлено > 0 Тогда
		ОтображаемыеВерсииТитулов = Неопределено;
		ПараметрыЗаписи = Новый Структура;
		ПараметрыЗаписи.Вставить("НеТребуетсяИдентификацияФайла");
		Записать(ПараметрыЗаписи);
		
		//@skip-check bsl-nstr-string-literal-format
		ТекстОповещения = НСтр("ru = 'Титул """ + Объект.ТекущийТитул + """ отправлен'");		
		ЗаголовокОповещения = НСтр("ru = 'Электронные документы'");	
		ДействиеПриНажатии = Неопределено;
	
		КартинкаОповещения = БиблиотекаКартинок.ЭмблемаСервиса1СЭДО48;
		
		ПодключитьОбработчикОжидания("ОбновлениеДоступностиЭлементов", 2, Истина);
		
		#Если Не МобильноеПриложениеКлиент Тогда
		ПоказатьОповещениеПользователя(ЗаголовокОповещения, ДействиеПриНажатии, ТекстОповещения, КартинкаОповещения,
			СтатусОповещенияПользователя.Важное);
		#КонецЕсли
	Иначе
		ЭтотОбъект.Доступность = Истина;
	КонецЕсли;
	
	Если НаборДействий.Получить(ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.ОтправитьВАрхив")) = Истина Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыполненияДействийПоЭДО_ОбработкаОшибки(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	ОчиститьИдФайлТекущегоТитула(Истина);
	
	ПодключитьОбработчикОжидания("ОбновлениеДоступностиЭлементов", 2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьИдФайлТекущегоТитула(Записать = Ложь)
	
	ИнформацияПоПрефиксамТитула = ОбменСГИСЭПДКлиентСервер.ПрефиксТитулаПоЭлементуРегламентаЭДО(Объект.ТекущийТитул);
	
	ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(
		ЭтотОбъект, 
		ИнформацияПоПрефиксамТитула.ВПрограмме + "ИдентификаторФайла", 
		Неопределено);
	
	Если Записать = Истина Тогда
		ПараметрыЗаписи = Новый Структура;
		ПараметрыЗаписи.Вставить("НеТребуетсяИдентификацияФайла");
		Записать(ПараметрыЗаписи);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеДоступностиЭлементов()

	ЭтотОбъект.Доступность = Истина;
	ОбменСГИСЭПДКлиентСервер.УстановитьДоступностьЭлементовЭСВ(ЭтотОбъект);
	УстановитьТекущийШагПомощника();
	
КонецПроцедуры

///////////////////////////////


&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОтметитьОбязательныеНеЗаполненныеЭлементыФормы" Тогда
		Если УникальныйИдентификатор <> Параметр Тогда
			Возврат;
		КонецЕсли;
		ОтметитьОбязательныеНеЗаполненныеЭлементыФормы(Параметр);
	ИначеЕсли ИмяСобытия = "ИзменитьОформлениеКнопокФормы" Тогда
		Если УникальныйИдентификатор <> Параметр.УникальныйИдентификаторОбновляемойФормы Тогда
			Возврат;
		КонецЕсли;
		ИзменитьОформлениеКнопок(Параметр);	 
	КонецЕсли;
	
КонецПроцедуры

#Область ОбъектыОбязательныеДляЗаполнения

&НаКлиенте
Процедура ИзменитьОформлениеКнопок(Параметр) Экспорт

	Если Не ЭтотОбъект.НачальноеОформлениеВыполнено Тогда
		ЭтотОбъект.ТребуетсяДополнительноеОформлениеКнопок = Истина;
		Если ЭтотОбъект.СтруктураДополнительногоОформленияКнопок <> Неопределено Тогда
			ЭтотОбъект.СтруктураДополнительногоОформленияКнопок = 
				Новый ФиксированнаяСтруктура("ИмяКнопки, ИдентификаторСтроки");
		Иначе
			ЭтотОбъект.СтруктураДополнительногоОформленияКнопок = Параметр;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	СтруктураСТекущимиДаннымиРеквизитов = ОбменСГИСЭПДКлиентСервер.ПолучитьСтруктуруПоТитулуИВерсии(ЭтотОбъект);
	СтруктураДанныхОбъекта = Неопределено;
	СтруктураСДаннымиФормыДляОформленияКнопок = 
		ОбменСГИСЭПДКлиентСервер.СтруктураСДаннымиФормыДляОформленияКнопок(ЭтотОбъект);
	
	Результат = ИзменитьОформлениеКнопокНаСервере(СтруктураСТекущимиДаннымиРеквизитов,
		Параметр.ИмяКнопки,
		Параметр.ИдентификаторСтроки,
		СтруктураДанныхОбъекта,
		СтруктураСДаннымиФормыДляОформленияКнопок);
		
	Если Результат.Успешно Тогда
		ЭтотОбъект.АдресДереваСоответствийИтаблицыКнопок = Результат.НовыйАдресВХранилище;	
		МассивОформления = Результат.МассивОформления;
		ОбменСГИСЭПДКлиентСервер.ОформлениеКнопокНаФорме(ЭтотОбъект,
			СтруктураСТекущимиДаннымиРеквизитов, МассивОформления);	
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИзменитьОформлениеКнопокНаСервере(Знач СтруктураСТекущимиДаннымиРеквизитов,
	ИмяКнопки = Неопределено,
	ИдентификаторСтроки = Неопределено,
	Знач СтруктураДанныхОбъекта,
	Знач СтруктураСДаннымиФормыДляОформленияКнопок)
	
	НовыйАдресВХранилище = ОбменСГИСЭПД.ЗапуститьИзменениеОформленияКнопок(СтруктураСДаннымиФормыДляОформленияКнопок,
		СтруктураСТекущимиДаннымиРеквизитов, ИмяКнопки, ИдентификаторСтроки, СтруктураДанныхОбъекта);
	
	Результат = ОбменСГИСЭПД.ОбработатьРезультатИзмененияОформленияКнопок(НовыйАдресВХранилище);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОтметитьОбязательныеНеЗаполненныеЭлементыФормы(Параметр)
	
	СтруктураСТекущимиДаннымиРеквизитов = ОбменСГИСЭПДКлиентСервер.ПолучитьСтруктуруПоТитулуИВерсии(ЭтотОбъект);
	ОтметитьОбязательныеНеЗаполненныеЭлементыФормыНаСервере(СтруктураСТекущимиДаннымиРеквизитов);
	
КонецПроцедуры

&НаСервере
Процедура ОтметитьОбязательныеНеЗаполненныеЭлементыФормыНаСервере(Знач СтруктураСТекущимиДанными)
	
	ОбменСГИСЭПД.ОтметитьОбязательныеНеЗаполненныеЭлементыФормы(ЭтотОбъект, СтруктураСТекущимиДанными);
	
КонецПроцедуры

#КонецОбласти