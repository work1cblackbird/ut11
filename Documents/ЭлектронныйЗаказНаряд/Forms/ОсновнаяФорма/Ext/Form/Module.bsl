&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.ЗначениеКопирования.Пустая() = Ложь 
		И Параметры.ЗначениеКопирования.ЭтоВходящий = Истина Тогда
        Отказ = Истина; 
        СтандартнаяОбработка = Ложь;
    КонецЕсли;
	
	КонтейнерОтображаемойВерсии = Неопределено;
	
	Если НЕ Параметры.Свойство("ФормаБезОбработки") Тогда
		// СтандартныеПодсистемы.ПодключаемыеКоманды
		ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
		// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
		
		ОбменСГИСЭПД.ПриСозданииНаСервереПодчиненнойФормы(ЭтотОбъект, Отказ, СтандартнаяОбработка);	
		
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			ОбменСГИСЭПД.ЗаполнитьТаблицуВерсийТитулов(Объект.Ссылка, Объект.Организация, ВерсииТитулов);
			ОбменСГИСЭПД.ЗаполнитьТаблицуЗначенийРеквизитов(Объект.Ссылка, Объект.Организация, СтруктураРеквизитов);
		Иначе
			ОбменСГИСЭПД.ЗаполнитьТаблицуЗначенийРеквизитовПоПараметрамФормы(Параметры, СтруктураРеквизитов);	
		КонецЕсли;
		
		ОбменСГИСЭПДКлиентСервер.УстановитьДоступностьЭлементовЭЗН(ЭтотОбъект);
		
		ОрганизацияОписаниеТипов = Метаданные.ОпределяемыеТипы.Организация.Тип;
		КонтрагентОписаниеТипов = Метаданные.ОпределяемыеТипы.КонтрагентБЭД.Тип;
		ФизическоеЛицоОписаниеТипов = Метаданные.ОпределяемыеТипы.ФизическоеЛицо.Тип;
		
		Если Объект.ЭтоВходящий = Ложь Тогда
			Элементы.СсылкаТитулФрахтователяФрахтователь.ОграничениеТипа = ОрганизацияОписаниеТипов;		
			Элементы.СсылкаТитулФрахтователяФрахтовщик.ОграничениеТипа = КонтрагентОписаниеТипов;
			
			Если ЗначениеЗаполнено(Объект.СсылкаТитулФрахтователяФрахтователь) = Ложь Тогда
				Объект.СсылкаТитулФрахтователяФрахтователь = ИнтеграцияЭДО.ОрганизацияПоУмолчанию();
				Если ЭтотОбъект.ОписаниеРеквизитовФормы = Неопределено Тогда
					ЭтотОбъект.ОписаниеРеквизитовФормы = Новый Структура("ЕстьОбъект", Истина);
				КонецЕсли;
				ОбменСГИСЭПДКлиентСервер.ЗаполнитьРеквизитыПоСсылке(Элементы.СсылкаТитулФрахтователяФрахтователь, ЭтотОбъект);
			КонецЕсли;
			
			Элементы.ХранимыеДанныеТитулФрахтователяПараметрыТранспортногоСредства.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.ХранимыеДанныеЭПД");
			
			МассивЭлементов = Новый Массив;
			МассивЭлементов.Добавить(Элементы.СсылкаТитулФрахтователяФрахтователь);
			МассивЭлементов.Добавить(Элементы.СсылкаТитулФрахтователяФрахтовщик);
			ОбменСГИСЭПДКлиентСервер.ПоказатьОшибкиПоКонтрагентам(ЭтотОбъект, МассивЭлементов);
			
			Элементы.ЗаголовокНомер.Заголовок = "Номер:";
			Элементы.ГруппаРеквизитаТитулФрахтователяПорядковыйНомерЗаказНаряда.Видимость = Ложь;
			//Объект.ТитулФрахтователяДатаСоставленияЗаказНаряда = ТекущаяДатаСеанса();
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(
				ЭтотОбъект, "ТитулФрахтователяДатаСоставленияЗаказНаряда", ТекущаяДатаСеанса(), , КонтейнерОтображаемойВерсии); 
		Иначе
			Элементы.СсылкаТитулФрахтовщикаФрахтовщик.ОграничениеТипа = ОрганизацияОписаниеТипов;
			Элементы.СсылкаТитулФрахтовщикаПринялЗаказНарядКИсполнению.ОграничениеТипа = ФизическоеЛицоОписаниеТипов;
			
			МассивЭлементов = Новый Массив;
			МассивЭлементов.Добавить(Элементы.СсылкаТитулФрахтовщикаФрахтовщик);
			ОбменСГИСЭПДКлиентСервер.ПоказатьОшибкиПоКонтрагентам(ЭтотОбъект, МассивЭлементов);
		КонецЕсли;
			
		ЦветВыделенияУчастника = ЦветаСтиля.ЦветВыделенияУчастникаЭПД;
		Если Объект.ЭтоВходящий = Ложь Тогда
			Элементы.ГруппаРеквизитаСсылкаТитулФрахтователяФрахтователь.ЦветФона = ЦветВыделенияУчастника;
		Иначе
			Элементы.ГруппаРеквизитаСсылкаТитулФрахтователяФрахтовщик.ЦветФона = ЦветВыделенияУчастника;
		КонецЕсли;
		
		ИзмененСоставУчастников = ЗначениеЗаполнено(Объект.ИдентификаторФрахтователя) = Ложь
									Или ЗначениеЗаполнено(Объект.ИдентификаторФрахтовщика) = Ложь;
									
		ЗаполнитьЗначенияПоУмолчаниюДляТекущегоТитула();
		УстановитьТекущийШагПомощника();
	КонецЕсли;
								
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(Объект.ТекущийТитул) = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство("НеТребуетсяИдентификацияФайла") = Ложь Тогда
		ЗаполнитьУчастников();
		ИнформацияПоПрефиксамТитула = ОбменСГИСЭПДКлиентСервер.ПрефиксТитулаПоЭлементуРегламентаЭДО(Объект.ТекущийТитул);
		ЗаполнитьИдентификациюФайлаОбмена(ИнформацияПоПрефиксамТитула, ИдентификаторОтправителя());	
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура Проверить(Команда)
	
	Если ЗначениеЗаполнено(Объект.ТекущийТитул) = Ложь Тогда
		Возврат;	
	КонецЕсли;
	
	ЗаполнитьУчастников();
	
	ИнформацияПоПрефиксамТитула = ОбменСГИСЭПДКлиентСервер.ПрефиксТитулаПоЭлементуРегламентаЭДО(Объект.ТекущийТитул);
	ЗаполнитьИдентификациюФайлаОбмена(ИнформацияПоПрефиксамТитула, ИдентификаторОтправителя());
	
КонецПроцедуры


&НаКлиенте
Процедура НадписьОшибкиЗакрытьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ЗакрытьОшибки" Тогда
		СтандартнаяОбработка = Ложь;
		Элементы.ГруппаОшибки.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура СледующаяОшибка(Команда)
	
	ОбменСГИСЭПДКлиент.НавигацияПоОшибкамЗаполнения(ЭтотОбъект, 
													ОшибкиЗаполнения, 
													Элементы.ОшибкиЗаполнения.ТекущаяСтрока);

КонецПроцедуры

&НаКлиенте
Процедура ПредыдущаяОшибка(Команда)
	
	ОбменСГИСЭПДКлиент.НавигацияПоОшибкамЗаполнения(ЭтотОбъект, 
													ОшибкиЗаполнения, 
													Элементы.ОшибкиЗаполнения.ТекущаяСтрока,
													Истина);
	
КонецПроцедуры


&НаКлиенте
Процедура ОшибкиЗаполненияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = ОшибкиЗаполнения.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ЭтотОбъект.ПереходыПоОшибкам = ТекущиеДанные.Описание.Переходы;
	ОбменСГИСЭПДКлиент.ПоказатьОшибкуЗаполнения(ЭтотОбъект, ТекущиеДанные, ИдентификаторОтправителя());

КонецПроцедуры



// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды


&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(ТекущийОбъект.ТекущийТитул) = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	ИнформацияПоПрефиксамТитула = ОбменСГИСЭПДКлиентСервер.ПрефиксТитулаПоЭлементуРегламентаЭДО(Объект.ТекущийТитул);
	
	Если ИнформацияПоПрефиксамТитула.ВПрограмме = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КонтейнерОтображаемойВерсии = Неопределено;
	
	ОбменСГИСЭПД.ЗаполнитьДополнительныеРеквизитыФормы(ЭтотОбъект, ИнформацияПоПрефиксамТитула.ВПрограмме, КонтейнерОтображаемойВерсии);
	
	НомерВерсии = ОбменСГИСЭПДКлиентСервер.ПолучитьЗначениеРеквизитаИзСтруктурыФормы(ЭтотОбъект, 
		ИнформацияПоПрефиксамТитула.ВПрограмме + "НомерИсправления", 0, КонтейнерОтображаемойВерсии);
	
	Если ПараметрыЗаписи.Свойство("НеДобавлятьВерсию") = Ложь Тогда
		// Запишем версию при необходимости
		ТекущийОбъект.ДополнительныеСвойства.Вставить("НомерВерсии", НомерВерсии);
		
		ИдентификаторФайлаВерсии = Объект[ИнформацияПоПрефиксамТитула.ВПрограмме + "ИдентификаторФайла"];
		
		ДатаВерсии = Объект[ИнформацияПоПрефиксамТитула.ВПрограмме + "ДатаФормированияФайла"]
						+ 60 * 60 * Час(Объект[ИнформацияПоПрефиксамТитула.ВПрограмме + "ВремяФормированияФайла"]) 
						+ 60 * Минута(Объект[ИнформацияПоПрефиксамТитула.ВПрограмме + "ВремяФормированияФайла"]) 
						+ Секунда(Объект[ИнформацияПоПрефиксамТитула.ВПрограмме + "ВремяФормированияФайла"]);
		
		ЭтоНоваяВерсия = Истина;
		Для Каждого СтрокаВерсии Из ВерсииТитулов Цикл
			Если СтрокаВерсии.Титул = ТекущийОбъект.ТекущийТитул 
			И СтрокаВерсии.НомерВерсии = НомерВерсии Тогда
				СтрокаВерсии.ИдентификаторФайла = ИдентификаторФайлаВерсии;	
				ЭтоНоваяВерсия = Ложь;
			КонецЕсли;	
		КонецЦикла;
	
		Если ЭтоНоваяВерсия = Истина Тогда
			НовСтрокаВерсии = ВерсииТитулов.Добавить();
			НовСтрокаВерсии.ДатаВерсии = ДатаВерсии;
			Если НовСтрокаВерсии.ДатаВерсии = Дата(1,1,1) Тогда
				НовСтрокаВерсии.ДатаВерсии = ТекущаяДатаСеанса();
			КонецЕсли;
			НовСтрокаВерсии.ИдентификаторФайла = ИдентификаторФайлаВерсии;
			НовСтрокаВерсии.НомерВерсии = НомерВерсии;
			НовСтрокаВерсии.Титул = ТекущийОбъект.ТекущийТитул;		
		КонецЕсли;
			
		ВерсияТитула = Новый Структура;
		ВерсияТитула.Вставить("НомерВерсии", НомерВерсии);
		ВерсияТитула.Вставить("ИдентификаторФайла", ИдентификаторФайлаВерсии);
		ВерсияТитула.Вставить("Титул", ТекущийОбъект.ТекущийТитул);
		ВерсияТитула.Вставить("ДатаВерсии", ДатаВерсии);
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ВерсияТитула", ВерсияТитула);
	КонецЕсли;
	
	МассивВерсийТитула = Неопределено;
	СтруктураРеквизитов.Свойство(ИнформацияПоПрефиксамТитула.ВПрограмме, МассивВерсийТитула);
	Если МассивВерсийТитула <> Неопределено Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("СтруктураРеквизитов", МассивВерсийТитула[Мин(НомерВерсии, МассивВерсийТитула.ВГраница())]);
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ДокументыОснования(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	МассивДокументов = Новый Массив;
	Для Каждого СтрТаб Из Объект.ДокументыОснования Цикл
		МассивДокументов.Добавить(СтрТаб.ДокументОснование);	
	КонецЦикла;
	
	ПараметрыФормы.Вставить("МассивДокументов", МассивДокументов);
	ПараметрыФормы.Вставить("ТолькоПросмотр", Объект.ТекущийТитул <> ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул1"));
	
	ОписаниеДокументыОснование_Закрытие = Новый ОписаниеОповещения("ДокументыОснование_Закрытие", ЭтотОбъект, ПараметрыФормы);
	
	ОткрытьФорму("Документ.ЭлектронныйЗаказНаряд.Форма.ДокументыОснования", ПараметрыФормы, ЭтотОбъект, 
					УникальныйИдентификатор, , , ОписаниеДокументыОснование_Закрытие, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
					
КонецПроцедуры

&НаКлиенте
Процедура ДокументыОснование_Закрытие(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Массив") Тогда
		Объект.ДокументыОснования.Очистить();
		Для Каждого Документ Из Результат Цикл
			НовСтр = Объект.ДокументыОснования.Добавить();	
			НовСтр.ДокументОснование = Документ;	
		КонецЦикла;	
		Модифицированность = Истина;
		ОбменСГИСЭПДКлиентПереопределяемый.ОбработкаЗаполненияНаФормеЭЗН(ЭтотОбъект, Результат);
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьЗначенияПоУмолчаниюДляТекущегоТитула()
	
	КонтейнерОтображаемойВерсии = Неопределено;
	
	Если (ЗначениеЗаполнено(Объект.Ссылка) = Ложь 
		Или Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул1")) Тогда
				
		Если ЗначениеЗаполнено(Объект.ВидДокумента) = Ложь Тогда
			Объект.ВидДокумента = ЭлектронныеДокументыЭДО.ВидДокументаПоТипу(ПредопределенноеЗначение("Перечисление.ТипыДокументовЭДО.ЭЗН"));
		КонецЕсли;

	ИначеЕсли Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул2") Тогда
	
		ЗаполнитьРеквизитыФрахтовщика = Ложь;
		Если ЗначениеЗаполнено(Объект.СсылкаТитулФрахтовщикаФрахтовщик) = Ложь Тогда
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(
				ЭтотОбъект, "СсылкаТитулФрахтовщикаФрахтовщик", Объект.СсылкаТитулФрахтователяФрахтовщик, Ложь, КонтейнерОтображаемойВерсии);
			ЗаполнитьРеквизитыФрахтовщика = Истина;	
		КонецЕсли;
		
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(
				ЭтотОбъект, "ТитулФрахтовщикаДатаПринятияЗаказНарядаКИсполнению", ТекущаяДатаСеанса(), Ложь, КонтейнерОтображаемойВерсии);	
	
		Если ЗаполнитьРеквизитыФрахтовщика = Истина Тогда
			ОбменСГИСЭПДКлиентСервер.ЗаполнитьРеквизитыПоСсылке(Элементы.СсылкаТитулФрахтовщикаФрахтовщик, ЭтотОбъект);	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПредставленияАдресов(Знач Титул = Неопределено)
	
	Если Титул = Неопределено Тогда
		ПрефиксТитула = ОбменСГИСЭПДКлиентСервер.ПолучитьПрефиксТитулаПоСтраницеОсновнойФормы(Элементы.Страницы.ТекущаяСтраница.Имя, 
			ОбменСГИСЭПДКлиентСервер.ТипДокументаЗаказНаряд());	
		Титул = ОбменСГИСЭПДКлиентСервер.ТитулПоПрефиксу("Документ.ЭлектронныйЗаказНаряд", ПрефиксТитула);
	КонецЕсли;
	
	Если Титул = Неопределено Или Титул = Перечисления.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул1 Тогда
		ОбменСГИСЭПДКлиентСервер.ЗаполнитьПредставлениеАдреса("ПредставлениеТитулФрахтователяМестаПодачиТСАдрес", ЭтотОбъект);
	КонецЕсли;
	
	Если Объект.ТекущийТитул = Перечисления.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул1 Тогда
		ОбменСГИСЭПДКлиентСервер.ЗаполнениеСпискаСохраненныхАдресов(ЭтотОбъект, 
						Объект.СсылкаТитулФрахтователяФрахтователь,
						Элементы.ПредставлениеТитулФрахтователяМестаПодачиТСАдрес);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбменСГИСЭПДКлиент.ПриОткрытииПодчиненнойФормы(ЭтотОбъект);
		
КонецПроцедуры

&НаКлиенте
Процедура ВопросОФормированииЭПД_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.ОК Тогда
		Закрыть();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ОписаниеРеквизитовФормы() Экспорт
	
	Возврат ОписаниеРеквизитовФормыСервер();
	
КонецФункции

&НаСервере
Функция ОписаниеРеквизитовФормыСервер()
	
	Возврат ОбменСГИСЭПД.ОписаниеРеквизитовФормы(ЭтаФорма);
		
КонецФункции

&НаКлиенте
Процедура ЗаполнитьТитулФрахтователяУсловияФрахтования(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТитулФрахтователяСведенияОНаименованииГруза(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТитулФрахтователяСопроводительныеДокументыНаГруз(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТитулФрахтователяСведенияОМаршрутеПеревозки(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТитулФрахтовщикаСведенияОТранспортномСредстве(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТитулФрахтовщикаСведенияОПрочихУсловиях(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТитулФрахтовщикаРазмерПлатыЗаПользованиеТС(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТитулФрахтователяПодачаОтметкиФрахтователя(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТитулФрахтовщикаВозвратОтметкиФрахтовщика(Команда)
	
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры


&НаКлиенте
Процедура СсылкаТитулФрахтователяФрахтовательПриИзменении(Элемент)
		
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
	ОбменСГИСЭПДКлиентСервер.ЗаполнитьРеквизитыПоСсылке(Элемент, ЭтотОбъект);
	
	ОбменСГИСЭПДКлиентСервер.ЗаполнениеСпискаСохраненныхАдресов(ЭтотОбъект, 
					Объект.СсылкаТитулФрахтователяФрахтователь,
					Элементы.ПредставлениеТитулФрахтователяМестаПодачиТСАдрес,
					Элементы.ПредставлениеТитулФрахтователяМестаПодачиТСАдрес.СписокВыбора, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТитулФрахтователяСрокВыполненияПеревозкиЦелыхЧасовПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ТитулФрахтователяУказанияНеобходимыеДляВыполненияНормПеревозкиПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ТитулФрахтователяДатаИВремяПодачиТранспортногоСредстваПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
КонецПроцедуры




&НаКлиенте
Процедура ПредставлениеТитулФрахтователяМестаПодачиТСАдресПриИзменении(Элемент)
	
	Если ПустаяСтрока(ПредставлениеТитулФрахтователяМестаПодачиТСАдрес) Тогда
		Элементы.ПредставлениеТитулФрахтователяМестаПодачиТСАдрес_Ошибка.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииДанныхНаФорме(ИмяЭлемента)
	
	НомерВерсии = 0;
	Для Каждого СтрВерсия Из ВерсииТитулов Цикл
		Если СтрВерсия.Титул = Объект.ТекущийТитул И СтрВерсия.НомерВерсии > НомерВерсии Тогда
			НомерВерсии = СтрВерсия.НомерВерсии;
		КонецЕсли;	
	КонецЦикла;
	
	СтруктураРеквизитовФормы = Новый Структура(ЭтотОбъект.ОписаниеРеквизитовФормы.ПараметрыФормы);
	СтруктураРеквизитовОбъекта = Новый Структура(ЭтотОбъект.ОписаниеРеквизитовФормы.РеквизитыОбъекта);
	
	Если СтруктураРеквизитовФормы.Свойство(ИмяЭлемента) Тогда
		ЗначениеРеквизита = ЭтотОбъект[ИмяЭлемента];
	ИначеЕсли СтруктураРеквизитовОбъекта.Свойство(ИмяЭлемента) Тогда
		ЗначениеРеквизита = Объект[ИмяЭлемента];
	КонецЕсли;
	
	ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект, ИмяЭлемента, ЗначениеРеквизита);
	
	Если ИмяЭлемента = "СсылкаТитулФрахтовщикаФрахтовщик" Тогда
		ОбновитьДополнительныеРеквизитыОсновнойФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДополнительныеРеквизитыОсновнойФормы()
	
	ОбменСГИСЭПД.ДобавитьОписаниеДополнительныхРеквизитов(ЭтотОбъект, , "ОсновнаяФорма");
	ОбменСГИСЭПД.ПоказатьДополнительныеРеквизитыОсновнойФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТитулФрахтователяПорядковыйНомерЗаказНарядаПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ТитулФрахтователяДатаСоставленияЗаказНарядаПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
КонецПроцедуры


&НаКлиенте
Процедура ХранимыеДанныеТитулФрахтователяПараметрыНеобходимогоТСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбменСГИСЭПДКлиент.ХранимыеДанныеНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ХранимыеДанныеТитулФрахтователяПараметрыНеобходимогоТСПриИзменении(Элемент)
	
	ОбменСГИСЭПДКлиент.ХранимыеДанныеПриИзменении(ЭтотОбъект, ХранимыеДанныеТитулФрахтователяПараметрыТранспортногоСредства, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ХранимыеДанныеТитулФрахтователяПараметрыНеобходимогоТСОткрытие(Элемент, СтандартнаяОбработка)
	
	ОбменСГИСЭПДКлиент.ХранимыеДанныеОткрытие(ЭтотОбъект, ХранимыеДанныеТитулФрахтователяПараметрыТранспортногоСредства, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ХранимыеДанныеТитулФрахтователяПараметрыНеобходимогоТСАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ОбменСГИСЭПДКлиент.ХранимыеДанныеАвтоПодбор(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеТитулФрахтователяМестаПодачиТСАдресАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Или ЗначениеЗаполнено(Элемент.ТекстРедактирования) Тогда
		Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.КонтактнаяИнформация") Тогда
			МодульУправлениеКонтактнойИнформациейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент");
			МодульУправлениеКонтактнойИнформациейКлиент.АвтоПодборАдреса(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
		КонецЕсли;
	КонецЕсли;
	
	ОбменСГИСЭПДКлиентСервер.ЗаполнениеСпискаСохраненныхАдресов(ЭтотОбъект, 
					Объект.СсылкаТитулФрахтователяФрахтователь,
					Элементы.ПредставлениеТитулФрахтователяМестаПодачиТСАдрес,
					ДанныеВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеТитулФрахтователяМестаПодачиТСАдресНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбменСГИСЭПДКлиент.ВводАдреса(Элемент.Имя, ЭтотОбъект, Объект.СсылкаТитулФрахтователяФрахтователь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеТитулФрахтователяМестаПодачиТСАдресОбработкаВыбора(Элемент, Результат, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбменСГИСЭПДКлиентСервер.ЗаполнитьРеквизитыАдреса(ЭтотОбъект, Результат, Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаТитулФрахтовщикаФрахтовщикПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
	ОбменСГИСЭПДКлиентСервер.ЗаполнитьРеквизитыПоСсылке(Элемент, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТитулФрахтовщикаДатаПринятияЗаказНарядаКИсполнениюПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
КонецПроцедуры


&НаКлиенте
Процедура СсылкаТитулФрахтовщикаПринялЗаказНарядКИсполнениюПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
	ОбменСГИСЭПДКлиентСервер.ЗаполнитьРеквизитыПоСсылке(Элемент, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТитулФрахтовщикаДолжностьЛицаПринявшегоЗаказНарядКИсполнениюПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ТитулФрахтователяПодачаДатаИВремяПодачиТСПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ТитулФрахтовщикаВозвратДатаИВремяЗавершенияПользованияТСПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ТитулФрахтовщикаВозвратИзменениеДатыИВремениВыполненияПеревозкиПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ТитулФрахтовщикаВозвратИзменениеСроковВыполненияПеревозкиПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ТитулФрахтовщикаВозвратИзменениеМаршрутаВыполненияПеревозкиПриИзменении(Элемент)
	
	ПриИзмененииДанныхНаФорме(Элемент.Имя);
	
КонецПроцедуры








////////////////////////////

#Область Навигация

&НаКлиенте
Процедура Шаг1ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьИзменениеШага("Шаг1", НавигационнаяСсылкаФорматированнойСтроки);

КонецПроцедуры

&НаКлиенте
Процедура Шаг2ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьИзменениеШага("Шаг2", НавигационнаяСсылкаФорматированнойСтроки);

КонецПроцедуры

&НаКлиенте
Процедура Шаг3ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьИзменениеШага("Шаг3", НавигационнаяСсылкаФорматированнойСтроки);

КонецПроцедуры

&НаКлиенте
Процедура Шаг4ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ОбработатьИзменениеШага("Шаг4", НавигационнаяСсылкаФорматированнойСтроки);

КонецПроцедуры

#КонецОбласти


#Область НавигацияПоСтраницам

&НаКлиенте
Процедура ИнкрементироватьОтображаемуюВерсиюТитула(ТитулСтрока, Инкремент)
	
	Если Инкремент = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ОтображаемыеВерсииТитулов = Неопределено Тогда
		ОтображаемыеВерсииТитулов = Новый Структура;
	Иначе
		ТекущееЗначениеВерсии = Неопределено;
		ОтображаемыеВерсииТитулов.Свойство(ТитулСтрока, ТекущееЗначениеВерсии);
		Если ТекущееЗначениеВерсии <> Неопределено Тогда
			ОтображаемыеВерсииТитулов.Вставить(ТитулСтрока, ТекущееЗначениеВерсии + Инкремент);
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьСтраницы(ОтображаемаяСтраница)
	
	ОтображаемаяСтраницаЦелевая = ПодменитьСтраницуДляТестированияФормы(ОтображаемаяСтраница);

	// Чтобы не отрастали лишние скролл-бары по высоте из-за разного числа элементов
	// на разных страницах, оставляем видимость только у одной - отображаемой страницы.
	
	Для каждого СтраницаЭЗН Из Элементы.Страницы.ПодчиненныеЭлементы Цикл
		СтраницаЭЗН.Видимость = (СтраницаЭЗН = ОтображаемаяСтраницаЦелевая);
	КонецЦикла;

	Элементы.Страницы.ТекущаяСтраница  = ОтображаемаяСтраницаЦелевая;
	
	
	ОбменСГИСЭПД.ПодготовитьФормуДляОтметкиОбязательныхПолей(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ПодменитьСтраницуДляТестированияФормы(ОтображаемаяСтраница)
	
	Возврат ОтображаемаяСтраница;
	
КонецФункции

&НаСервере
Процедура УстановитьТекущийШагПомощника(ПредыдущийТекущийТитул = Неопределено)
	
	Если Параметры.Свойство("ТитулСтраницы") Тогда
		ТитулСтраницы = Параметры.ТитулСтраницы;
	Иначе
		ТитулСтраницы = ?(ЗначениеЗаполнено(Объект.ТекущийТитул), Объект.ТекущийТитул, ПредыдущийТекущийТитул);
	КонецЕсли;
	
	НачатьСПервойСтраницы = ТолькоПросмотр ИЛИ НЕ ЗначениеЗаполнено(Объект.Ссылка);

	Если НачатьСПервойСтраницы Тогда		
		УстановитьВидимостьСтраницыНачало("Шаг1");		
	Иначе		
		Если ТитулСтраницы = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул2") Тогда			
			УстановитьВидимостьСтраницыТитулФрахтовщика("Шаг2"); 			
		ИначеЕсли ТитулСтраницы = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул3") Тогда			
			УстановитьВидимостьСтраницыТитулФрахтователяПодача("Шаг3");				
		ИначеЕсли ТитулСтраницы = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул4") Тогда			
			УстановитьВидимостьСтраницыТитулФрахтовщикаВозврат("Шаг4");			
		Иначе			
			УстановитьВидимостьСтраницыНачало("Шаг1");			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьТекущийШагПомощника()
	
	Если ТолькоПросмотр ИЛИ НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	// ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ВРег("ЭлектроннаяТранспортнаяНакладнаяТекущийШагПомощника"),
	//	Объект.Ссылка.УникальныйИдентификатор(), НавигацияПараметрФормы);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьСтраницыНачало(ИмяШага)

	УстановитьВидимостьСтраницы(Элементы.СтраницаСодЗНИнфФт);
	
	СтруктураНавигации = НавигацияПомощника();
	ИмяШагаПараметр = СтруктураНавигации[ИмяШага].СтруктураПараметровФормы.НавигацияПараметрФормы;
	
	РазместитьНавигацию(ИмяШагаПараметр);
	
	СохранитьТекущийШагПомощника();

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьСтраницыТитулФрахтовщика(ИмяШага)

	УстановитьВидимостьСтраницы(Элементы.СтраницаСодЗНИнфФщ);
	
	СтруктураНавигации = НавигацияПомощника();
	ИмяШагаПараметр = СтруктураНавигации[ИмяШага].СтруктураПараметровФормы.НавигацияПараметрФормы;
	
	РазместитьНавигацию(ИмяШагаПараметр);
	
	СохранитьТекущийШагПомощника();

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьСтраницыТитулФрахтователяПодача(ИмяШага)

	УстановитьВидимостьСтраницы(Элементы.СтраницаСодЗНИнфФтПодача);
	
	СтруктураНавигации = НавигацияПомощника();
	ИмяШагаПараметр = СтруктураНавигации[ИмяШага].СтруктураПараметровФормы.НавигацияПараметрФормы;
	
	РазместитьНавигацию(ИмяШагаПараметр);
	
	СохранитьТекущийШагПомощника();
	

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьСтраницыТитулФрахтовщикаВозврат(ИмяШага)

	УстановитьВидимостьСтраницы(Элементы.СтраницаСодЗНИнфФщВозврат);
	
	СтруктураНавигации = НавигацияПомощника();
	ИмяШагаПараметр = СтруктураНавигации[ИмяШага].СтруктураПараметровФормы.НавигацияПараметрФормы;
	
	РазместитьНавигацию(ИмяШагаПараметр);
	
	СохранитьТекущийШагПомощника();

КонецПроцедуры



&НаСервере
Функция ИмяПомощника()

	Возврат "ЭлектронныйЗаказНаряд";

КонецФункции


&НаСервере
Функция НавигацияПомощника()

	СтруктураНавигации = Новый Структура;
	
	СтруктураНавигации.Вставить(НавигацияПомощниковСВозвратомЭПД.ИмяШага(1), СтруктураШагаНачало(1));
	СтруктураНавигации.Вставить(НавигацияПомощниковСВозвратомЭПД.ИмяШага(2), СтруктураШагаПодтверждение(2));
	СтруктураНавигации.Вставить(НавигацияПомощниковСВозвратомЭПД.ИмяШага(3), СтруктураШагаПодача(3));
	СтруктураНавигации.Вставить(НавигацияПомощниковСВозвратомЭПД.ИмяШага(4), СтруктураШагаВозврат(4));

	Для Каждого КиЗ Из СтруктураНавигации Цикл
		ШагВыполнен = Ложь;
		Для Каждого СтрВерсия Из ВерсииТитулов Цикл
			Если СтрВерсия.Титул = КиЗ.Значение.ТипЭлементаРегламента И СтрВерсия.ИдентификаторФайла <> "" Тогда
				ШагВыполнен = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		КиЗ.Значение.Выполнен = ШагВыполнен; 	
		КиЗ.Значение.Доступен = КиЗ.Значение.Доступен Или КиЗ.Значение.Выполнен;
	КонецЦикла;
	
	Возврат СтруктураНавигации;
	
КонецФункции

&НаСервере
Функция СтруктураШагаНачало(НомерШага)
	
	Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул1");
	Отбор = Новый Структура("Титул", Титул);
	СтрокиВерсий = ВерсииТитулов.Выгрузить(Отбор, "НомерВерсии");
	
	СтруктураШага                = НавигацияПомощниковСВозвратомЭПД.НовыйСтруктураШага();
	СтруктураШага.ИмяПомощника   = ИмяПомощника();
	СтруктураШага.НомерШага      = НомерШага;
	СтруктураШага.ТекстНавигации = НСтр("ru='Оформление'");
	СтруктураШага.СтруктураПараметровФормы.НавигацияПараметрФормы = "ТитулФрахтователя";
	СтруктураШага.ТипЭлементаРегламента = Титул;
	СтруктураШага.Выполнен = ЗначениеЗаполнено(Объект.ТитулФрахтователяИдентификаторФайла); 
	СтруктураШага.Доступен = Истина;
	СтруктураШага.КоличествоВерсий = Макс(СтрокиВерсий.Количество(), 1);
	СтруктураШага.НомерВерсии = ОтображаемаяВерсияТитула(СтрокиВерсий, "ТитулФрахтователя");
	Возврат СтруктураШага;
	
КонецФункции

&НаСервере
Функция СтруктураШагаПодтверждение(НомерШага)
	
	Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул2");
	Отбор = Новый Структура("Титул", Титул);
	СтрокиВерсий = ВерсииТитулов.Выгрузить(Отбор, "НомерВерсии");
	
	СтруктураШага                = НавигацияПомощниковСВозвратомЭПД.НовыйСтруктураШага();
	СтруктураШага.ИмяПомощника   = ИмяПомощника();
	СтруктураШага.НомерШага      = НомерШага;
	СтруктураШага.ТекстНавигации = НСтр("ru='Подтверждение'");
	СтруктураШага.СтруктураПараметровФормы.НавигацияПараметрФормы = "ТитулФрахтовщика";
	СтруктураШага.ТипЭлементаРегламента = Титул;
	СтруктураШага.Выполнен = ЗначениеЗаполнено(Объект.ТитулФрахтовщикаИдентификаторФайла);
	СтруктураШага.Доступен = СтруктураШага.Выполнен Или Объект.ТекущийТитул = Титул;
	СтруктураШага.КоличествоВерсий = Макс(СтрокиВерсий.Количество(), 1);
	СтруктураШага.НомерВерсии = ОтображаемаяВерсияТитула(СтрокиВерсий, "ТитулФрахтовщика");
	Возврат СтруктураШага;
	
КонецФункции

&НаСервере
Функция СтруктураШагаПодача(НомерШага)
	
	Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул3");
	Отбор = Новый Структура("Титул", Титул);
	СтрокиВерсий = ВерсииТитулов.Выгрузить(Отбор, "НомерВерсии");
	
	СтруктураШага                = НавигацияПомощниковСВозвратомЭПД.НовыйСтруктураШага();
	СтруктураШага.ИмяПомощника   = ИмяПомощника();
	СтруктураШага.НомерШага      = НомерШага;
	СтруктураШага.ТекстНавигации = НСтр("ru='Подача'");
	СтруктураШага.СтруктураПараметровФормы.НавигацияПараметрФормы = "ТитулФрахтователяПодача";
	СтруктураШага.ТипЭлементаРегламента = Титул;
	СтруктураШага.Выполнен = ЗначениеЗаполнено(Объект.ТитулФрахтователяПодачаИдентификаторФайла);
	СтруктураШага.Доступен = СтруктураШага.Выполнен Или Объект.ТекущийТитул = Титул;
	СтруктураШага.КоличествоВерсий = Макс(СтрокиВерсий.Количество(), 1);
	СтруктураШага.НомерВерсии = ОтображаемаяВерсияТитула(СтрокиВерсий, "ТитулФрахтователяПодача");
	Возврат СтруктураШага;
	
КонецФункции

&НаСервере
Функция СтруктураШагаВозврат(НомерШага)
	
	Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул4");
	Отбор = Новый Структура("Титул", Титул);
	СтрокиВерсий = ВерсииТитулов.Выгрузить(Отбор, "НомерВерсии");
	
	СтруктураШага                = НавигацияПомощниковСВозвратомЭПД.НовыйСтруктураШага();
	СтруктураШага.ИмяПомощника   = ИмяПомощника();
	СтруктураШага.НомерШага      = НомерШага;
	СтруктураШага.ТекстНавигации = НСтр("ru='Возврат'");
	СтруктураШага.СтруктураПараметровФормы.НавигацияПараметрФормы = "ТитулФрахтовщикаВозврат";
	СтруктураШага.ТипЭлементаРегламента = Титул;
	СтруктураШага.Выполнен = ЗначениеЗаполнено(Объект.ТитулФрахтовщикаВозвратИдентификаторФайла);
	СтруктураШага.Доступен = СтруктураШага.Выполнен Или Объект.ТекущийТитул = Титул;
	СтруктураШага.КоличествоВерсий = Макс(СтрокиВерсий.Количество(), 1);
	СтруктураШага.НомерВерсии = ОтображаемаяВерсияТитула(СтрокиВерсий, "ТитулФрахтовщикаВозврат");
	Возврат СтруктураШага;
	
КонецФункции

&НаСервере
Функция ОтображаемаяВерсияТитула(СтрокиВерсий, ТитулСтрока)
	
	МаксВерсия = 0;
	Для Каждого СтрокаТЗ Из СтрокиВерсий Цикл
		Если СтрокаТЗ.НомерВерсии > МаксВерсия Тогда
			МаксВерсия = СтрокаТЗ.НомерВерсии;
		КонецЕсли;
	КонецЦикла;
	
	Если ОтображаемыеВерсииТитулов = Неопределено Тогда
		ОтображаемыеВерсииТитулов = Новый Структура;
		ОтображаемыеВерсииТитулов.Вставить(ТитулСтрока, МаксВерсия);
		Возврат МаксВерсия;
	КонецЕсли;
	
	ТекущееЗначениеВерсии = Неопределено;
	ОтображаемыеВерсииТитулов.Свойство(ТитулСтрока, ТекущееЗначениеВерсии);
	
	Если ТекущееЗначениеВерсии = Неопределено Тогда
		ОтображаемыеВерсииТитулов.Вставить(ТитулСтрока, МаксВерсия);
		Возврат МаксВерсия;
	Иначе
		Возврат ТекущееЗначениеВерсии;		
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьРеквизитыОтображаемойВерсии(ВыбранныйТитулСтрока, ОтмечатьТекущий)
	
	ВыбранныйТитул = ОбменСГИСЭПДКлиентСервер.ТитулПоПрефиксу("Документ.ЭлектронныйЗаказНаряд", ВыбранныйТитулСтрока); 
	
	Если ЗначениеЗаполнено(ВыбранныйТитул) Тогда
		Отбор = Новый Структура("Титул", ВыбранныйТитул);
		СтрокиВерсий = ВерсииТитулов.Выгрузить(Отбор, "НомерВерсии");
		СтрокиВерсий.Сортировать("НомерВерсии");	
		НомерВерсии = ОтображаемаяВерсияТитула(СтрокиВерсий, ВыбранныйТитулСтрока);
		
		РеквизитыТекущейВерсииТитула = ОбменСГИСЭПД.ПолучитьВерсиюТитулаДокумента(СтруктураРеквизитов, ВыбранныйТитул, НомерВерсии);
		ОбменСГИСЭПД.ЗаполнитьРеквизитыПоВерсии(ЭтотОбъект, ВыбранныйТитул, НомерВерсии, РеквизитыТекущейВерсииТитула);
		ЗаполнитьПредставленияАдресов(ВыбранныйТитул);
		Если ОтмечатьТекущий = Истина Тогда
			СтруктураСТекущимиДанными = ОбменСГИСЭПДКлиентСервер.ПолучитьСтруктуруПоТитулуИВерсии(ЭтотОбъект);
			СтруктураДанныхОбъекта = ОбменСГИСЭПДКлиентСервер.ПолучитьСериализуемыйОбъектСДаннымиДокумента(ЭтотОбъект);
			СтруктураСДаннымиФормыДляОформленияКнопок = ОбменСГИСЭПДКлиентСервер.СтруктураСДаннымиФормыДляОформленияКнопок(ЭтотОбъект);
			
			Результат = ИзменитьОформлениеКнопокНаСервере(СтруктураСТекущимиДанными,
				Неопределено,
				Неопределено,
				СтруктураДанныхОбъекта,
				СтруктураСДаннымиФормыДляОформленияКнопок);
				
			Если Результат.Успешно Тогда
				ЭтотОбъект.АдресДереваСоответствийИтаблицыКнопок = Результат.НовыйАдресВХранилище;	
				МассивОформления = Результат.МассивОформления;
				ОбменСГИСЭПДКлиентСервер.ОформлениеКнопокНаФорме(ЭтотОбъект, СтруктураСТекущимиДанными, МассивОформления);	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура РазместитьНавигацию(ВыбранныйТитулСтрока)

	НавигацияПараметрФормы = ВыбранныйТитулСтрока;
	
	ЗаполнитьРеквизитыОтображаемойВерсии(ВыбранныйТитулСтрока, Истина);

	СтруктураНавигацииПомощника = НавигацияПомощника();
	НавигацияПомощниковСВозвратомЭПД.РазместитьНавигацию(ЭтотОбъект, СтруктураНавигацииПомощника, Параметры);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеШага(ИмяШага, НавигационнаяСсылка)
	
	ИнкрементВерсии = 0;
	Если НавигационнаяСсылка = "Влево" Тогда
		ИнкрементВерсии = -1;
	ИначеЕсли НавигационнаяСсылка = "Вправо" Тогда
		ИнкрементВерсии = 1;
	КонецЕсли;
	
	Если ИмяШага = "Шаг1" Тогда
		ИнкрементироватьОтображаемуюВерсиюТитула("ТитулФрахтователя", ИнкрементВерсии);
		УстановитьВидимостьСтраницыНачало(ИмяШага);
	ИначеЕсли ИмяШага = "Шаг2" Тогда
		ИнкрементироватьОтображаемуюВерсиюТитула("ТитулФрахтовщика", ИнкрементВерсии);
		УстановитьВидимостьСтраницыТитулФрахтовщика(ИмяШага);
	ИначеЕсли ИмяШага = "Шаг3" Тогда
		ИнкрементироватьОтображаемуюВерсиюТитула("ТитулФрахтовщикаПодача", ИнкрементВерсии);
		УстановитьВидимостьСтраницыТитулФрахтователяПодача(ИмяШага);
	ИначеЕсли ИмяШага = "Шаг4" Тогда
		ИнкрементироватьОтображаемуюВерсиюТитула("ТитулФрахтовщикаВозврат", ИнкрементВерсии);
		УстановитьВидимостьСтраницыТитулФрахтовщикаВозврат(ИмяШага);	
	КонецЕсли;
	
	ОбменСГИСЭПДКлиентСервер.ИзменитьОформлениеФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти


&НаКлиенте
Процедура ВыгрузитьВФайл(Команда)
	
	Результат = ОбменСГИСЭПДВызовСервера.ДвоичныеДанныеДокументаЭПД(Объект.Ссылка, УникальныйИдентификатор);
	
	Если Результат.ДвоичныеДанные <> Неопределено Тогда
		ПараметрыДиалога = Новый ПараметрыДиалогаПолученияФайлов;
		ПараметрыДиалога.Заголовок = НСтр("ru = 'Выберите путь для сохранения файла'");
		
		НачатьПолучениеФайлаССервера(Результат.ДвоичныеДанные, Результат.ИмяФайла, ПараметрыДиалога);
	Иначе
		ПоказатьПредупреждение(, НСтр("ru='Нет доступного титула'"), , );	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ИдентификаторОтправителя()
	
	ТипОрганизация = ОбменСГИСЭПДВызовСервера.ПолучитьТипОрганизация();
	Если ТипЗнч(Объект.СсылкаТитулФрахтователяФрахтователь) = ТипОрганизация Тогда
		Результат = Объект.ИдентификаторФрахтователя;
	Иначе
		Результат = Объект.ИдентификаторФрахтовщика;	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


&НаКлиенте
Процедура ЗаполнитьУчастников_ПослеНастроек(Настройка, ДополнительныеПараметры) Экспорт
	
	Если Настройка = Неопределено Тогда
		ЭтотОбъект.Доступность = Истина;
		Возврат;
	КонецЕсли;
	
	Если Настройка.Отправитель = Объект.СсылкаТитулФрахтователяФрахтователь Тогда
		Объект.ИдентификаторФрахтователя = Настройка.ИдентификаторОтправителя;
	КонецЕсли;
	
	Если Настройка.Отправитель = Объект.СсылкаТитулФрахтователяФрахтовщик Тогда
		Объект.ИдентификаторФрахтовщика = Настройка.ИдентификаторОтправителя;
	КонецЕсли;
	
	Если Настройка.Получатель = Объект.СсылкаТитулФрахтователяФрахтователь Тогда
		Объект.ИдентификаторФрахтователя = Настройка.ИдентификаторПолучателя;
	КонецЕсли;
	
	Если Настройка.Получатель = Объект.СсылкаТитулФрахтователяФрахтовщик Тогда
		Объект.ИдентификаторФрахтовщика = Настройка.ИдентификаторПолучателя;
	КонецЕсли;	
																		
	ИзмененСоставУчастников = Ложь;
	
	ЭтоНастройкаПередОтправкой = Неопределено;
	ДополнительныеПараметры.Свойство("ЭтоНастройкаПередОтправкой", ЭтоНастройкаПередОтправкой);
	Если ЭтоНастройкаПередОтправкой = Истина
		И ЗначениеЗаполнено(Объект.ИдентификаторФрахтователя)
		И ЗначениеЗаполнено(Объект.ИдентификаторФрахтовщика) Тогда
			ОтправитьЧерезЭДОПослеЗаписи();
	Иначе
		ЭтотОбъект.Доступность = Истина;
		ПараметрыЗаписи = Новый Структура;
		ПараметрыЗаписи.Вставить("НеТребуетсяИдентификацияФайла");
		Записать(ПараметрыЗаписи);
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры


&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьИмяФайла(Шаблон, ИдФрахтователя, ИдФрахтовщика, НаличиеДругихПолучателей = Ложь)
	
	#Если Сервер Тогда
		ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	#Иначе
		ТекущаяДатаСеанса = ОбщегоНазначенияКлиент.ДатаСеанса();
	#КонецЕсли
	
	Результат = Шаблон;
	Результат = СтрЗаменить(Результат, "ИдГО", ИдФрахтователя);
	Результат = СтрЗаменить(Результат, "ИдПЕР", ИдФрахтовщика);
	Результат = СтрЗаменить(Результат, "GGGGMMDD", Формат(ТекущаяДатаСеанса, "ДФ=ггггММдд"));
	Результат = СтрЗаменить(Результат, "ГУИДФайла", Строка(Новый УникальныйИдентификатор()));
	Результат = СтрЗаменить(Результат, "НалДр", ?(НаличиеДругихПолучателей = Истина, "1", "0"));
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция УстановитьНомерДокумента()
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.УстановитьНовыйНомер();
	
	Возврат ДокументОбъект.Номер;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьИдентификациюФайлаОбмена(ИнформацияПоПрефиксамТитула, ИдентификаторОтправителя, Переформировать = Ложь, ОбработкаЗавершения = Неопределено)

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИнформацияПоПрефиксамТитула", ИнформацияПоПрефиксамТитула);
	ДополнительныеПараметры.Вставить("ИдентификаторОтправителя", ИдентификаторОтправителя);
	ДополнительныеПараметры.Вставить("Переформировать", Переформировать);
	ДополнительныеПараметры.Вставить("ОбработкаЗавершения", ОбработкаЗавершения);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьИдентификациюФайлаОбмена_ПослеПолученияУИД", ЭтотОбъект, ДополнительныеПараметры);
	
	Если ЗначениеЗаполнено(Объект.УИДМинтранс) Тогда
		ЗаполнитьИдентификациюФайлаОбмена_ПослеПолученияУИД(Объект.УИДМинтранс, ДополнительныеПараметры);
	Иначе
		Если Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул1") Тогда
			ОбменСГИСЭПДКлиент.ПолучитьУИДМинтранса(ОписаниеОповещения, Объект.ИдентификаторФрахтователя);
		ИначеЕсли Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул2") Тогда
			ОбменСГИСЭПДКлиент.ПолучитьУИДМинтранса(ОписаниеОповещения, Объект.ИдентификаторФрахтователя, Истина, Объект.Ссылка);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИдентификациюФайлаОбмена_ПослеПолученияУИД(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Объект.УИДМинтранс) = Ложь Тогда
		Объект.УИДМинтранс = Результат;
	КонецЕсли;
	
	ИнформацияПоПрефиксамТитула = ДополнительныеПараметры.ИнформацияПоПрефиксамТитула;
	ИдентификаторОтправителя = ДополнительныеПараметры.ИдентификаторОтправителя;
	Переформировать = ДополнительныеПараметры.Переформировать;
	
	ТребуетсяЗаполнить = Переформировать = Истина
			Или ОбменСГИСЭПДКлиент.ТребуетсяИдентификацияФайлаТитула(Объект.Ссылка, Объект.ТекущийТитул, ВерсииТитулов);
	
	Если ТребуетсяЗаполнить = Истина Тогда																
		КонтейнерОтображаемойВерсии = ОбменСГИСЭПДКлиентСервер.КонтейнерОтображаемойВерсииРеквизита(ЭтотОбъект,
			ИнформацияПоПрефиксамТитула.ВПрограмме);
		СтруктураВерсииТитула = КонтейнерОтображаемойВерсии.СтруктураРеквизитовЗаполнение[КонтейнерОтображаемойВерсии.ПрефиксТитула][КонтейнерОтображаемойВерсии.НомерВерсии];
		
		ДатаИВремяФормирования = ОбщегоНазначенияКлиент.ДатаСеанса();
		ДатаФормирования = НачалоДня(ДатаИВремяФормирования);
		ВремяФормирования = Дата('00010101') + (ДатаИВремяФормирования - ДатаФормирования); 
		
		Если Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул1") Тогда
			КонтейнерОтображаемойВерсии = ОбменСГИСЭПДКлиентСервер.КонтейнерОтображаемойВерсииРеквизита(ЭтотОбъект, "ТитулФрахтователя");
			
			Если ЗначениеЗаполнено(Объект.Ссылка) = Ложь Тогда
				Объект.Номер = УстановитьНомерДокумента();
			КонецЕсли;
			
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулФрахтователяДатаСоставленияЗаказНаряда",
				Объект.Дата, Ложь,
				КонтейнерОтображаемойВерсии);	
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулФрахтователяПорядковыйНомерЗаказНаряда",
				Объект.Номер, Ложь,
				КонтейнерОтображаемойВерсии);
				
			ИтераторСтроки = 1;
			КлючСтруктуры = "ТитулФрахтователяОписаниеГруза__" + ИтераторСтроки + "__ИдентификаторСтроки";
			ИдентификаторСтроки = Неопределено;
			Пока СтруктураВерсииТитула.Свойство(КлючСтруктуры, ИдентификаторСтроки) Цикл
				
				ЗначенияСтроки = Новый Структура("МаркировкаГруза", "Отсутствует");
				ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеПодчиненнойСтрокиВСтруктуреФормы(
					ЭтотОбъект, "ТитулФрахтователяМаркировка", ИдентификаторСтроки, ЗначенияСтроки, Ложь, КонтейнерОтображаемойВерсии);
				
				ИтераторСтроки = ИтераторСтроки + 1;
				КлючСтруктуры = "ТитулФрахтователяОписаниеГруза__" + ИтераторСтроки + "__ИдентификаторСтроки";
			КонецЦикла;
			
		ИначеЕсли Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул2") Тогда
			КонтейнерОтображаемойВерсии = ОбменСГИСЭПДКлиентСервер.КонтейнерОтображаемойВерсииРеквизита(ЭтотОбъект, "ТитулФрахтовщика");
			
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулФрахтовщикаКоличествоТС",
				1, ,
				КонтейнерОтображаемойВерсии);
	
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулФрахтовщикаИдентификаторФайлаОбменаИнформацияФрахтователя",
				Объект.ТитулФрахтователяИдентификаторФайла, ,
				КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулФрахтовщикаДатаФормированияФайлаИнформацияФрахтователя",
				Объект.ТитулФрахтователяДатаФормированияФайла, ,
				КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулФрахтовщикаВремяФормированияФайлаИнформацииФрахтователя",
				Объект.ТитулФрахтователяВремяФормированияФайла, ,
				КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулФрахтовщикаЭлектроннаяПодписьФайлаОбменаИнформацииФрахтователя",
				ПолучитьПодписьТитулаBase64(ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул1")), ,
				КонтейнерОтображаемойВерсии);
		ИначеЕсли Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул3") Тогда
			КонтейнерОтображаемойВерсии = ОбменСГИСЭПДКлиентСервер.КонтейнерОтображаемойВерсииРеквизита(ЭтотОбъект, "ТитулФрахтователяПодача");
			
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулФрахтователяПодачаИдентификаторФайлаОбменаИнформацияФрахтовщика",
				Объект.ТитулФрахтовщикаИдентификаторФайла, ,
				КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулФрахтователяПодачаДатаФормированияФайлаИнформацияФрахтовщика",
				Объект.ТитулФрахтовщикаДатаФормированияФайла, ,
				КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулФрахтователяПодачаВремяФормированияФайлаИнформацииФрахтовщика",
				Объект.ТитулФрахтовщикаВремяФормированияФайла, ,
				КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулФрахтователяПодачаЭлектроннаяПодписьФайлаОбменаИнформацияФрахтовщика",
				ПолучитьПодписьТитулаBase64(ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул2")), ,
				КонтейнерОтображаемойВерсии);
		ИначеЕсли Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул4") Тогда
			КонтейнерОтображаемойВерсии = ОбменСГИСЭПДКлиентСервер.КонтейнерОтображаемойВерсииРеквизита(ЭтотОбъект, "ТитулФрахтовщикаВозврат");
			
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулФрахтовщикаВозвратИдентификацияТитулаФрахтователяИдентификаторФайла",
				Объект.ТитулФрахтователяПодачаИдентификаторФайла, ,
				КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулФрахтовщикаВозвратИдентификацияТитулаФрахтователяДатаФормирования",
				Объект.ТитулФрахтователяПодачаДатаФормированияФайла, ,
				КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулФрахтовщикаВозвратИдентификацияТитулаФрахтователяВремяФормирования",
				Объект.ТитулФрахтователяПодачаВремяФормированияФайла, ,
				КонтейнерОтображаемойВерсии);
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулФрахтовщикаВозвратИдентификацияТитулаФрахтователяЭП",
				ПолучитьПодписьТитулаBase64(ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул3")), ,
				КонтейнерОтображаемойВерсии);
		КонецЕсли;
		
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(
				ЭтотОбъект, ИнформацияПоПрефиксамТитула.ПолеУИД, Объект.УИДМинтранс, , КонтейнерОтображаемойВерсии);
				
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
			ИнформацияПоПрефиксамТитула.ВПрограмме + "ВерсияПрограммы",
			ОбменСГИСЭПДКлиентСервер.ВерсияПрограммы(), ,
			КонтейнерОтображаемойВерсии);
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
			ИнформацияПоПрефиксамТитула.ВПрограмме + "ВерсияФормата",
			ОбменСГИСЭПДКлиентСервер.ВерсияФормата("ЭлектронныйЗаказНаряд"), ,
			КонтейнерОтображаемойВерсии);
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
			ИнформацияПоПрефиксамТитула.ВПрограмме + "КодДокументаПоКНД",
			ИнформацияПоПрефиксамТитула.КНД, ,
			КонтейнерОтображаемойВерсии);																
		
		ДанныеСертификатаУчетнойЗаписиЭДО = ОбменСГИСЭПДКлиентСервер.ДанныеСертификатаУчетнойЗаписиЭДО(ИдентификаторОтправителя);
		
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
			ИнформацияПоПрефиксамТитула.ВПрограмме + "ПодписантФамилия",
			ДанныеСертификатаУчетнойЗаписиЭДО.Фамилия, ,
			КонтейнерОтображаемойВерсии);
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
			ИнформацияПоПрефиксамТитула.ВПрограмме + "ПодписантИмя",
			ДанныеСертификатаУчетнойЗаписиЭДО.Имя, ,
			КонтейнерОтображаемойВерсии);
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
			ИнформацияПоПрефиксамТитула.ВПрограмме + "ПодписантОтчество",
			ДанныеСертификатаУчетнойЗаписиЭДО.Отчество, ,
			КонтейнерОтображаемойВерсии);
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
			ИнформацияПоПрефиксамТитула.ВПрограмме + "ПодписантДолжность",
			ДанныеСертификатаУчетнойЗаписиЭДО.Должность, ,
			КонтейнерОтображаемойВерсии);
			
		Если ЗначениеЗаполнено(ДанныеСертификатаУчетнойЗаписиЭДО.НомерДоверенности) Тогда
			Если ЗначениеЗаполнено(ДанныеСертификатаУчетнойЗаписиЭДО.СведенияОбИнформационнойСистеме) Тогда
				ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				ИнформацияПоПрефиксамТитула.ВПрограмме + "ПодписантДоверенностьИнформационнаяСистема",
				ДанныеСертификатаУчетнойЗаписиЭДО.СведенияОбИнформационнойСистеме, ,
				КонтейнерОтображаемойВерсии);	
			Иначе
				ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
					ИнформацияПоПрефиксамТитула.ВПрограмме + "ПодписантДоверенностьИдентификаторФайла",
					ДанныеСертификатаУчетнойЗаписиЭДО.НомерДоверенности, ,
					КонтейнерОтображаемойВерсии);
				ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
					ИнформацияПоПрефиксамТитула.ВПрограмме + "ПодписантДоверенностьДата",
					ДанныеСертификатаУчетнойЗаписиЭДО.ДатаДоверенности, ,
					КонтейнерОтображаемойВерсии);
				ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
					ИнформацияПоПрефиксамТитула.ВПрограмме + "ПодписантДоверенностьНомер",
					ДанныеСертификатаУчетнойЗаписиЭДО.ПорядковыйНомерДоверенности, ,
					КонтейнерОтображаемойВерсии);	
			КонецЕсли;	
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				ИнформацияПоПрефиксамТитула.ВПрограмме + "ПодписантСтатус",
				"2", ,
				КонтейнерОтображаемойВерсии);
		Иначе
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				ИнформацияПоПрефиксамТитула.ВПрограмме + "ПодписантСтатус",
				"1", ,
				КонтейнерОтображаемойВерсии);
		КонецЕсли;
		
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(
				ЭтотОбъект, ИнформацияПоПрефиксамТитула.ВПрограмме + "ИдентификаторФайла", ПолучитьИмяФайла(ИнформацияПоПрефиксамТитула.ШаблонИмениФайла, 
																			Объект.ИдентификаторФрахтователя,
																			Объект.ИдентификаторФрахтовщика), Ложь, КонтейнерОтображаемойВерсии);
																			
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
			ИнформацияПоПрефиксамТитула.ВПрограмме + "ДатаФормированияФайла",
			ДатаФормирования, ,	
			КонтейнерОтображаемойВерсии);
			
		ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
			ИнформацияПоПрефиксамТитула.ВПрограмме + "ВремяФормированияФайла",
			ВремяФормирования, ,	
			КонтейнерОтображаемойВерсии);
	КонецЕсли;
	
	// Проверим заполнение
	Если ОбменСГИСЭПДКлиент.ЗаполнитьТаблицуОшибокЭПД(ЭтотОбъект, ОшибкиЗаполнения, Истина) = Истина Тогда	
		Элементы.ГруппаОшибки.Видимость = Истина;
		Элементы.НадписьОшибки.Заголовок = "Ошибки заполнения (" + Строка(ОшибкиЗаполнения.Количество()) + ")";
		Если ТребуетсяЗаполнить = Истина Тогда
			ОчиститьИдФайлТекущегоТитула();
		КонецЕсли;
	Иначе
		Элементы.НадписьОшибки.Заголовок = "Ошибки заполнения";
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("ОбработкаЗавершения") 
		И ДополнительныеПараметры.ОбработкаЗавершения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОбработкаЗавершения);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура НадписьСтатусаОшибкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОписаниеОшибки" Тогда
		СтандартнаяОбработка = Ложь;
		ОбменСГИСЭПДКлиент.ОткрытьОписаниеОшибки(Объект.Ссылка, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьСтатусаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ТекущиеДелаПоЭДО" Тогда
		СтандартнаяОбработка = Ложь;
		ИнтерфейсДокументовЭДОКлиент.ОткрытьЭлектронныйДокументОбъектаУчета(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ОтправитьУОУ(Команда)
	
	ОповещениеПослеВводаТекста = Новый ОписаниеОповещения("ОтправитьУОУ_ПослеВводаТекстаУточнения", ЭтотОбъект);
	ПоказатьВводСтроки(ОповещениеПослеВводаТекста, "", "Текст запроса уточнения (отклонения)", , Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьУОУ_ПослеВводаТекстаУточнения(Текст, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Текст) = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.ТекущийПолученныйТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул1") Тогда
		Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_ОткФщ");	
	КонецЕсли;
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("НеТребуетсяИдентификацияФайла");
	Записать(ПараметрыЗаписи);
	
	НаборДействий = Новый Соответствие();
	
	ДействиеОтклонить = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Отклонить");
	
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействиеОтклонить);
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
					"Перечисление.ДействияПоЭДО.Подписать"));
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
					"Перечисление.ДействияПоЭДО.ПодготовитьКОтправке"));
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
					"Перечисление.ДействияПоЭДО.Отправить"));
	
	ПараметрыВыполненияДействийПоЭДО = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();	
	ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ОбъектыУчета.Добавить(Объект.Ссылка);
	ПараметрыВыполненияДействийПоЭДО.НаборДействий = НаборДействий;
	
	ДополнительныеПараметрыОтклонения = Новый Структура;
	ДополнительныеПараметрыОтклонения.Вставить("Комментарий", Текст);
	ДополнительныеПараметрыОтклонения.Вставить("ЭлементДляОтклонения", Объект.ТекущийПолученныйТитул);
	ПараметрыВыполненияДействийПоЭДО.ДополнительныеПараметрыДействий.Вставить(ДействиеОтклонить, ДополнительныеПараметрыОтклонения);
	
	ДополнительныеПараметрыУспешногоЗавершения = Новый Структура;
	ДополнительныеПараметрыУспешногоЗавершения.Вставить("ЭтоИсправление", Ложь);
	ДополнительныеПараметрыУспешногоЗавершения.Вставить("НаборДействий", НаборДействий);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЭтоИсправление", Ложь);
	ДополнительныеПараметры.Вставить("ОповещениеУспешногоЗавершения",
		Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ЭтотОбъект, 
									ДополнительныеПараметрыУспешногоЗавершения));

	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ИнтерфейсДокументовЭДОКлиент, ДополнительныеПараметры);
	
	ЭлектронныеДокументыЭДОКлиент.НачатьВыполнениеДействийПоЭДО(Оповещение, ПараметрыВыполненияДействийПоЭДО);
	
КонецПроцедуры


&НаСервере
Функция ПолучитьПодписьТитулаBase64(ТипЭлементаРегламента) 
	
	ПодписьBase64 = "";
	
	СообщениеТитула = ОбменСГИСЭПД.ПолучитьСообщениеТитула(Объект.Ссылка, ТипЭлементаРегламента);
	
	Если ЗначениеЗаполнено(СообщениеТитула) Тогда
		УстановленныеПодписи = ЭлектронныеДокументыЭДО.УстановленныеПодписи(СообщениеТитула);
		Для Каждого ОписаниеПодписи Из УстановленныеПодписи Цикл
			ПодписьBase64 = Base64Строка(ОписаниеПодписи.Подпись);
			Прервать;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ПодписьBase64;
	
КонецФункции

&НаКлиенте
Процедура ОтправитьЧерезЭДО(Команда)
	
	ЭтотОбъект.Доступность = Ложь;
		
	Если ЗначениеЗаполнено(Объект.ИдентификаторФрахтователя) = Ложь
		Или ЗначениеЗаполнено(Объект.ИдентификаторФрахтовщика) = Ложь
		Или ИзмененСоставУчастников = Истина Тогда
		ЗаполнитьУчастников(Истина);
	Иначе
		ОтправитьЧерезЭДОПослеЗаписи();
  	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьУчастников(ЭтоНастройкаПередОтправкой = Ложь)
	
	КлючНастроекОтправки = НастройкиЭДОКлиентСервер.НовыйКлючНастроекОтправки();
	КлючНастроекОтправки.ВидДокумента = Объект.ВидДокумента;
	
	Если Объект.ЭтоВходящий = Истина Тогда
		КлючНастроекОтправки.Отправитель = Объект.СсылкаТитулФрахтователяФрахтовщик;
		КлючНастроекОтправки.Получатель = Объект.СсылкаТитулФрахтователяФрахтователь;
	Иначе
		КлючНастроекОтправки.Отправитель = Объект.СсылкаТитулФрахтователяФрахтователь;
		КлючНастроекОтправки.Получатель = Объект.СсылкаТитулФрахтователяФрахтовщик; 
	КонецЕсли;
	
	НастройкиОтправки = НастройкиОтправкиЭДОСлужебныйВызовСервера.НастройкиОтправки(КлючНастроекОтправки);
	
	ПараметрыНастройкиОбменаСКонтрагентом = НастройкиЭДОКлиент.НовыеПараметрыНастройкиОбменаСКонтрагентом();
	ПараметрыНастройкиОбменаСКонтрагентом.НастройкаОдногоДокумента = Истина;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("КлючНастроекОтправки", КлючНастроекОтправки);
	ДополнительныеПараметры.Вставить("ПараметрыНастройкиОбменаСКонтрагентом", ПараметрыНастройкиОбменаСКонтрагентом);
	ДополнительныеПараметры.Вставить("ПараметрыЗаписи", Новый Структура);
	ДополнительныеПараметры.Вставить("ЭтоНастройкаПередОтправкой", ЭтоНастройкаПередОтправкой);
	
	ОповещениеОкончаниеНастройки = Новый ОписаниеОповещения("ЗаполнитьУчастников_ПослеНастроек", ЭтотОбъект, ДополнительныеПараметры);
	Если НастройкиОтправки = Неопределено И ЭтоНастройкаПередОтправкой = Истина Тогда	
		НастройкиЭДОКлиент.НастроитьОбменСКонтрагентом(КлючНастроекОтправки, ОповещениеОкончаниеНастройки, ПараметрыНастройкиОбменаСКонтрагентом);
	Иначе
		ВыполнитьОбработкуОповещения(ОповещениеОкончаниеНастройки, НастройкиОтправки); 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЧерезЭДОПослеЗаписи(Повторно = Ложь)
		
	ДанныеСостоянияЭДО = ИнтеграцияЭДОВызовСервера.ДанныеСостоянияЭДОДляФормыОбъектаУчета(Объект.Ссылка);
	
	ЭтоИсправление = Ложь;
	Если ДанныеСостоянияЭДО.ПредставлениеСостояния = "Требуется уточнение"
		Или ДанныеСостоянияЭДО.ПредставлениеСостояния = "Требуется повторная отправка" Тогда
		Если Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул1") Тогда
			КонтейнерОтображаемойВерсии = ОбменСГИСЭПДКлиентСервер.КонтейнерОтображаемойВерсииРеквизита(ЭтотОбъект,
				"ТитулФрахтователя");
				 
			ЗначениеРеквизита = ОбменСГИСЭПДКлиентСервер.ПолучитьЗначениеРеквизитаИзСтруктурыФормы(ЭтотОбъект, 
				"ТитулФрахтователяНомерИсправления", 0, КонтейнерОтображаемойВерсии);			
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулФрахтователяНомерИсправления",
				Число(ЗначениеРеквизита + 1), ,
				КонтейнерОтображаемойВерсии);
				
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулФрахтователяДатаИсправления",
				ОбщегоНазначенияКлиент.ДатаСеанса(), ,
				КонтейнерОтображаемойВерсии);				
			
		ИначеЕсли Объект.ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭЗН_Титул4") Тогда
			КонтейнерОтображаемойВерсии = ОбменСГИСЭПДКлиентСервер.КонтейнерОтображаемойВерсииРеквизита(ЭтотОбъект,
				"ТитулФрахтовщикаВозврат");
							
			ЗначениеРеквизита = ОбменСГИСЭПДКлиентСервер.ПолучитьЗначениеРеквизитаИзСтруктурыФормы(ЭтотОбъект, 
				"ТитулФрахтовщикаВозвратНомерИсправления", 0, КонтейнерОтображаемойВерсии);			
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулФрахтовщикаВозвратНомерИсправления",
				Число(ЗначениеРеквизита + 1), ,
				КонтейнерОтображаемойВерсии);
			
			ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(ЭтотОбъект,
				"ТитулФрахтовщикаВозвратДатаИсправления",
				ОбщегоНазначенияКлиент.ДатаСеанса(), ,
				КонтейнерОтображаемойВерсии);			
		КонецЕсли;
		ЭтоИсправление = Истина;
		Повторно = Истина;	
	КонецЕсли;
	
	Если Повторно = Истина Тогда
		ТитулыПоДокументу = ОбменСГИСЭПДВызовСервера.ПолучитьТитулыПоДокументу(Объект.Ссылка, Истина);
		ОбменСГИСЭПДКлиентСервер.УстановитьТекущийДоступныйТитулЭЗН(Объект, ТитулыПоДокументу, Истина);
	КонецЕсли;
	ИнформацияПоПрефиксамТитула = ОбменСГИСЭПДКлиентСервер.ПрефиксТитулаПоЭлементуРегламентаЭДО(Объект.ТекущийТитул);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДанныеСостоянияЭДО", ДанныеСостоянияЭДО);
	ДополнительныеПараметры.Вставить("ЭтоИсправление", ЭтоИсправление);
	ДополнительныеПараметры.Вставить("Повторно", Повторно);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьЧерезЭДОПослеЗаписи_Завершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ЗаполнитьИдентификациюФайлаОбмена(ИнформацияПоПрефиксамТитула, ИдентификаторОтправителя(), Повторно, ОписаниеОповещения);
		
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЧерезЭДОПослеЗаписи_Завершение(Результат, ДополнительныеПарамтеры) Экспорт
	
	ДанныеСостоянияЭДО = ДополнительныеПарамтеры.ДанныеСостоянияЭДО;
	Повторно = ДополнительныеПарамтеры.Повторно;
	ЭтоИсправление = ДополнительныеПарамтеры.ЭтоИсправление;
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("НеТребуетсяИдентификацияФайла");
	Записать(ПараметрыЗаписи);
	
	Если ЗначениеЗаполнено(Объект.ТекущийТитул) = Ложь Тогда
		Доступность = Истина;
		Возврат;
	КонецЕсли;
	
	Если ОшибкиЗаполнения.Количество() > 0 Тогда	
		Элементы.ГруппаОшибки.Видимость = Истина;
		Элементы.НадписьОшибки.Заголовок = "Ошибки заполнения (" + Строка(ОшибкиЗаполнения.Количество()) + ")";
		Доступность = Истина;
		ПоказатьПредупреждение(, НСтр("ru='Документ не отправлен, обнаружены ошибки заполнения. Исправьте ошибки и повторите попытку'"));
		Возврат;
	Иначе
		Элементы.НадписьОшибки.Заголовок = "Ошибки заполнения";
	КонецЕсли;
	
	НаборДействий = Новый Соответствие;		
	
	Если Повторно = Истина 
		Или ДанныеСостоянияЭДО.ПредставлениеСостояния = "Требуется повторная отправка" 
		Или ЗначениеЗаполнено(ОбменСГИСЭПДКлиентСервер.ПолучитьСообщениеТитула(Объект.Ссылка, Объект.ТекущийТитул)) = Ложь Тогда
			Если Объект.ЭтоВходящий = Ложь И ДанныеСостоянияЭДО.СуществуетАктуальныйДокумент = Ложь Тогда
				ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
					"Перечисление.ДействияПоЭДО.Сформировать"));	
			Иначе
				ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
					"Перечисление.ДействияПоЭДО.СформироватьОтвет"));
			КонецЕсли;	   
			
			ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
					"Перечисление.ДействияПоЭДО.Подписать"));
			ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
				"Перечисление.ДействияПоЭДО.ПодготовитьКОтправке"));  
	Иначе		
		Если ДанныеСостоянияЭДО.ПредставлениеСостояния <> "Требуется подготовка к отправке" 
			И ДанныеСостоянияЭДО.ПредставлениеСостояния <> "Требуется отправка" Тогда
				ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
					"Перечисление.ДействияПоЭДО.Подписать"));
		КонецЕсли;
		
		Если ДанныеСостоянияЭДО.ПредставлениеСостояния <> "Требуется отправка" Тогда
			ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
				"Перечисление.ДействияПоЭДО.ПодготовитьКОтправке"));
		КонецЕсли;		
	КонецЕсли;
	
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.Отправить"));
	
	ПараметрыВыполненияДействийПоЭДО = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();	
	ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ОбъектыУчета.Добавить(Объект.Ссылка);
	ПараметрыВыполненияДействийПоЭДО.НаборДействий = НаборДействий;
	
	ДополнительныеПараметрыУспешногоЗавершения = Новый Структура;
	ДополнительныеПараметрыУспешногоЗавершения.Вставить("НаборДействий", НаборДействий);
	ДополнительныеПараметрыУспешногоЗавершения.Вставить("ЭтоИсправление", ЭтоИсправление);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЭтоИсправление", ЭтоИсправление);
	ДополнительныеПараметры.Вставить("ОповещениеУспешногоЗавершения",
		Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ЭтотОбъект, 
									ДополнительныеПараметрыУспешногоЗавершения, 
									"ПослеВыполненияДействийПоЭДО_ОбработкаОшибки", ЭтотОбъект));

	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ИнтерфейсДокументовЭДОКлиент, ДополнительныеПараметры);
	
	ЭлектронныеДокументыЭДОКлиент.НачатьВыполнениеДействийПоЭДО(Оповещение, ПараметрыВыполненияДействийПоЭДО);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Оповестить("Запись_ДокументыЭПД", Объект.Ссылка);
	
КонецПроцедуры


&НаКлиенте
Процедура ОткрытьЭлектронныйДокумент(Команда)
	
	ИнтерфейсДокументовЭДОКлиент.ОткрытьЭлектронныйДокументОбъектаУчета(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЧерезЭДОПовторно(Команда)
	
	ЭтотОбъект.Доступность = Ложь;
	
	// Сейчас при повторной отправке происходит ошибка на стороне оператора,
	// у них это считается дубликатом (одинаковый титул в рамках одного документооборота)
	// Это не верно, т.к. повторная отправка возможна (после УОУ или ошибки от ГИС)
	ОтправитьЧерезЭДОПослеЗаписи(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыполненияДействийПоЭДО(Результат, ДополнительныеПараметры) Экспорт
	
	ДействиеОтправить = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Отправить");
	КоличествоОтправлено = Результат.ОбработаноПоДействиям.Получить(ДействиеОтправить);
	
	Если КоличествоОтправлено <> Неопределено И КоличествоОтправлено > 0 Тогда
		Если ДополнительныеПараметры.ЭтоИсправление Тогда
			ОбменСГИСЭПДВызовСервера.ОтметитьИсправлениеДокумента(Объект.Ссылка);	
		КонецЕсли;
		ПараметрыЗаписи = Новый Структура;
		ПараметрыЗаписи.Вставить("НеТребуетсяИдентификацияФайла");
		Записать(ПараметрыЗаписи);
		
		ОтображаемыеВерсииТитулов = Неопределено;
		
		ТекстОповещения = НСтр("ru = 'Титул """ + Объект.ТекущийТитул + """ отправлен'");		
		ЗаголовокОповещения = НСтр("ru = 'Электронные документы'");	
		ДействиеПриНажатии = Неопределено;
		КартинкаОповещения = БиблиотекаКартинок.ЭмблемаСервиса1СЭДО48;
		
		ПодключитьОбработчикОжидания("ОбновлениеДоступностиЭлементов", 2, Истина);	
		
		#Если Не МобильноеПриложениеКлиент Тогда
		ПоказатьОповещениеПользователя(ЗаголовокОповещения, ДействиеПриНажатии, ТекстОповещения, КартинкаОповещения,
			СтатусОповещенияПользователя.Важное);
		#КонецЕсли
	Иначе
		ЭтотОбъект.Доступность = Истина;
	КонецЕсли;
	
	Если ДополнительныеПараметры.НаборДействий.Получить(ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.ОтправитьВАрхив")) = Истина Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыполненияДействийПоЭДО_ОбработкаОшибки(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	ОчиститьИдФайлТекущегоТитула(Истина);
	
	ПодключитьОбработчикОжидания("ОбновлениеДоступностиЭлементов", 2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьИдФайлТекущегоТитула(Записать = Ложь)
	
	ИнформацияПоПрефиксамТитула = ОбменСГИСЭПДКлиентСервер.ПрефиксТитулаПоЭлементуРегламентаЭДО(Объект.ТекущийТитул);
	
	ОбменСГИСЭПДКлиентСервер.УстановитьЗначениеРеквизитаВСтруктуреФормы(
		ЭтотОбъект, 
		ИнформацияПоПрефиксамТитула.ВПрограмме + "ИдентификаторФайла", 
		Неопределено);
	
	Если Записать = Истина Тогда
		ПараметрыЗаписи = Новый Структура;
		ПараметрыЗаписи.Вставить("НеТребуетсяИдентификацияФайла");
		Записать(ПараметрыЗаписи);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеДоступностиЭлементов()

	ЭтотОбъект.Доступность = Истина;
	ОбменСГИСЭПДКлиентСервер.УстановитьДоступностьЭлементовЭЗН(ЭтотОбъект);
	УстановитьТекущийШагПомощника();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОтметитьОбязательныеНеЗаполненныеЭлементыФормы" Тогда
		Если УникальныйИдентификатор <> Параметр Тогда
			Возврат;
		КонецЕсли;
		ОтметитьОбязательныеНеЗаполненныеЭлементыФормы(Параметр);
	ИначеЕсли ИмяСобытия = "ИзменитьОформлениеКнопокФормы" Тогда
		Если УникальныйИдентификатор <> Параметр.УникальныйИдентификаторОбновляемойФормы Тогда
			Возврат;
		КонецЕсли;
		ИзменитьОформлениеКнопок(Параметр);	 
	КонецЕсли;
	
КонецПроцедуры

#Область ОбъектыОбязательныеДляЗаполнения

&НаКлиенте
Процедура ИзменитьОформлениеКнопок(Параметр) Экспорт

	Если Не ЭтотОбъект.НачальноеОформлениеВыполнено Тогда
		ЭтотОбъект.ТребуетсяДополнительноеОформлениеКнопок = Истина;
		Если ЭтотОбъект.СтруктураДополнительногоОформленияКнопок <> Неопределено Тогда
			ЭтотОбъект.СтруктураДополнительногоОформленияКнопок = 
				Новый ФиксированнаяСтруктура("ИмяКнопки, ИдентификаторСтроки");
		Иначе
			ЭтотОбъект.СтруктураДополнительногоОформленияКнопок = Параметр;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	СтруктураСТекущимиДаннымиРеквизитов = ОбменСГИСЭПДКлиентСервер.ПолучитьСтруктуруПоТитулуИВерсии(ЭтотОбъект);
	СтруктураДанныхОбъекта = Неопределено;
	СтруктураСДаннымиФормыДляОформленияКнопок = 
		ОбменСГИСЭПДКлиентСервер.СтруктураСДаннымиФормыДляОформленияКнопок(ЭтотОбъект);
	
	Результат = ИзменитьОформлениеКнопокНаСервере(СтруктураСТекущимиДаннымиРеквизитов,
		Параметр.ИмяКнопки,
		Параметр.ИдентификаторСтроки,
		СтруктураДанныхОбъекта,
		СтруктураСДаннымиФормыДляОформленияКнопок);
		
	Если Результат.Успешно Тогда
		ЭтотОбъект.АдресДереваСоответствийИтаблицыКнопок = Результат.НовыйАдресВХранилище;	
		МассивОформления = Результат.МассивОформления;
		ОбменСГИСЭПДКлиентСервер.ОформлениеКнопокНаФорме(ЭтотОбъект,
			СтруктураСТекущимиДаннымиРеквизитов, МассивОформления);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИзменитьОформлениеКнопокНаСервере(Знач СтруктураСТекущимиДаннымиРеквизитов,
	ИмяКнопки = Неопределено,
	ИдентификаторСтроки = Неопределено,
	Знач СтруктураДанныхОбъекта,
	Знач СтруктураСДаннымиФормыДляОформленияКнопок)
	
	НовыйАдресВХранилище = ОбменСГИСЭПД.ЗапуститьИзменениеОформленияКнопок(СтруктураСДаннымиФормыДляОформленияКнопок,
		СтруктураСТекущимиДаннымиРеквизитов, ИмяКнопки, ИдентификаторСтроки, СтруктураДанныхОбъекта);

	Результат = ОбменСГИСЭПД.ОбработатьРезультатИзмененияОформленияКнопок(НовыйАдресВХранилище);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОтметитьОбязательныеНеЗаполненныеЭлементыФормы(Параметр)
	
	СтруктураСТекущимиДаннымиРеквизитов = ОбменСГИСЭПДКлиентСервер.ПолучитьСтруктуруПоТитулуИВерсии(ЭтотОбъект);
	ОтметитьОбязательныеНеЗаполненныеЭлементыФормыНаСервере(СтруктураСТекущимиДаннымиРеквизитов);
	
КонецПроцедуры

&НаСервере
Процедура ОтметитьОбязательныеНеЗаполненныеЭлементыФормыНаСервере(Знач СтруктураСТекущимиДанными)
	
	ОбменСГИСЭПД.ОтметитьОбязательныеНеЗаполненныеЭлементыФормы(ЭтотОбъект, СтруктураСТекущимиДанными);
	
КонецПроцедуры

#КонецОбласти