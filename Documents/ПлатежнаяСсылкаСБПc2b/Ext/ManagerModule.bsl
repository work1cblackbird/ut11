///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Возвращает параметры оплаты частичной оплаты.
//
// Параметры:
//  ПлатежнаяСсылкаСБП - Документ.ПлатежнаяСсылкаСБПc2b - документ частичной оплаты;
//
// Возвращаемое значение:
//  Структура - данные оплат по документу в Системе быстрых платежей:
//    * ДокументОснование - ОпределяемыйТип.ДокументОперацииСБП - документ,
//      который отражает продажу в информационной базе;
//
Функция ПараметрыОплатыПлатежнойСсылки(ПлатежнаяСсылкаСБП) Экспорт
	
	ДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПлатежнаяСсылкаСБП, "ОснованиеПлатежа");
	
	ПараметрыОплаты = Новый Структура;
	ПараметрыОплаты.Вставить("ДокументОснование", ДокументОснование);
	
	Возврат ПараметрыОплаты;
	
КонецФункции

// Возвращает данные частичных оплат по документу основанию платежа.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП - документ, который отражает
//    продажу в информационной базе;
//
// Возвращаемое значение:
//  Массив Из Структура - данные частичных оплат по документу основания в Системе быстрых платежей:
//    * Ссылка - Документ.ПлатежнаяСсылкаСБПc2b - документ частичной оплаты;
//    * Дата - Дата - Дата создания частичной оплаты;
//    * Сумма - Число - Сумма частичной оплаты;
//    * СтатусОперации - Строка - идентификатор статуса операции;
//    * НастройкаПодключения -  СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей -
//       настройка выполнения операции СБП;
//    * ПредставлениеОперации - Строка - Представление частичной оплаты (содержит дату и сумму операции).
//
Функция ПолучитьДанныеЧастичныхОплат(ОснованиеПлатежа) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПлатежнаяСсылкаСБПc2b.Ссылка КАК Ссылка,
	|	ПлатежнаяСсылкаСБПc2b.Дата КАК Дата,
	|	ПлатежнаяСсылкаСБПc2b.Сумма КАК Сумма,
	|	ЕСТЬNULL(ИдентификаторыОперацийСБПc2b.СтатусОперации, """") КАК СтатусОперации,
	|	ИдентификаторыОперацийСБПc2b.НастройкаПодключения КАК НастройкаПодключения
	|ИЗ
	|	Документ.ПлатежнаяСсылкаСБПc2b КАК ПлатежнаяСсылкаСБПc2b
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИдентификаторыОперацийСБПc2b КАК ИдентификаторыОперацийСБПc2b
	|		ПО ПлатежнаяСсылкаСБПc2b.Ссылка = ИдентификаторыОперацийСБПc2b.ДокументОперации
	|ГДЕ
	|	ПлатежнаяСсылкаСБПc2b.ОснованиеПлатежа = &ОснованиеПлатежа
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПлатежнаяСсылкаСБПc2b.МоментВремени";
	
	Запрос.УстановитьПараметр("ОснованиеПлатежа",     ОснованиеПлатежа);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ДанныеЧастичныхОплат = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		
		ОписаниеЧастичнойОплаты = Новый Структура;
		ОписаниеЧастичнойОплаты.Вставить("Ссылка",               Выборка.Ссылка);
		ОписаниеЧастичнойОплаты.Вставить("Дата",                 Выборка.Дата);
		ОписаниеЧастичнойОплаты.Вставить("Сумма",                Выборка.Сумма);
		ОписаниеЧастичнойОплаты.Вставить("СтатусОперации",       Выборка.СтатусОперации);
		ОписаниеЧастичнойОплаты.Вставить("НастройкаПодключения", Выборка.НастройкаПодключения);
		
		ОписаниеЧастичнойОплаты.Вставить(
			"ПредставлениеОперации",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Оплата от %1 на сумму %2 руб.'"),
				Выборка.Дата,
				Выборка.Сумма));
		
		ДанныеЧастичныхОплат.Добавить(ОписаниеЧастичнойОплаты);
		
	КонецЦикла;
	
	Возврат ДанныеЧастичныхОплат;
	
КонецФункции

#КонецОбласти

#КонецЕсли