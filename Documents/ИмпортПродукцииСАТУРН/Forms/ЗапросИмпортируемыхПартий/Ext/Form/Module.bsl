
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ввезено;
	ОрганизацияСАТУРН = Параметры.Организация;
	
	УправлениеФормой(ЭтотОбъект);
	
	СтандартныеПодсистемыСервер.СброситьРазмерыИПоложениеОкна(ЭтотОбъект);
	
	СобытияФормСАТУРН.УстановитьПараметрыВыбораОрганизации(ЭтотОбъект);
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ВариантЗагрузки = 0
		И Не ЗначениеЗаполнено(НачалоПериодаОтбора) Тогда
		
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Укажите начало периода отбора.'"),, "НачалоПериодаОтбора",, Отказ);
		
	КонецЕсли;
	
	Если ВариантЗагрузки = 1
		И Не ЗначениеЗаполнено(ИдентификаторОтбора) Тогда
		
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Укажите идентификатор документа.'"),, "ИдентификаторОтбора",, Отказ);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантЗагрузкиПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Далее(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнениеЗаявкиСАТУРННачало();

КонецПроцедуры

&НаКлиенте
Процедура УказатьПериод(Команда)
	
	// Редактирование текущего периода.
	Диалог = Новый ДиалогРедактированияСтандартногоПериода;
	Диалог.Период.ДатаНачала    = НачалоПериодаОтбора;
	Диалог.Период.ДатаОкончания = КонецПериодаОтбора;
	
	Оповещение = Новый ОписаниеОповещения("УстановитьИнтервалДатЗавершение", ЭтотОбъект);
	Диалог.Показать(Оповещение);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Если Форма.ВариантЗагрузки = 1 Тогда
		
		Элементы.СтраницаЗагрузкаПоИдентификатору.Видимость = Истина;
		Элементы.СтраницаЗагрузкаЗаПериод.Видимость         = Ложь;
		
	Иначе
		
		Элементы.СтраницаЗагрузкаПоИдентификатору.Видимость = Ложь;
		Элементы.СтраницаЗагрузкаЗаПериод.Видимость         = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервалДатЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НачалоПериодаОтбора = Результат.ДатаНачала;
	КонецПериодаОтбора  = Результат.ДатаОкончания;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнениеЗаявкиСАТУРННачало()
	
	ПараметрыЗагрузкиДокументов = ИнтеграцияСАТУРНСлужебныйКлиентСервер.ПараметрыПередачиДанныхКлассификаторов();
	ПараметрыЗагрузкиДокументов.ОрганизацияСАТУРН  = ОрганизацияСАТУРН;
	ПараметрыЗагрузкиДокументов.ДальнейшееДействие = ПредопределенноеЗначение("Перечисление.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ВыполнитеОбмен");
	ПараметрыЗагрузкиДокументов.Ссылка             = ПредопределенноеЗначение("Справочник.ИмпортируемаяПартияСАТУРН.ПустаяСсылка");
	
	ПараметрыЗагрузкиДокументов.ДополнительныеПараметры = Новый Структура();
	
	Если ВариантЗагрузки = 0 Тогда
	
		ПараметрыЗагрузкиДокументов.ДополнительныеПараметры.Вставить("ДатаНачала",    НачалоПериодаОтбора);
		
		Если ЗначениеЗаполнено(КонецПериодаОтбора) Тогда
			ПараметрыЗагрузкиДокументов.ДополнительныеПараметры.Вставить("ДатаОкончания", КонецДня(КонецПериодаОтбора));
		КонецЕсли;
		
	Иначе
		
		ПараметрыЗагрузкиДокументов.ДополнительныеПараметры.Вставить("ИдентификаторОтбора", ИдентификаторОтбора);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Статус) Тогда
		
		ПараметрыЗагрузкиДокументов.ДополнительныеПараметры.Вставить("Статус", Статус);
		
	КонецЕсли;
	
	ОписаниеПриЗавершении = Новый ОписаниеОповещения(
		"Подключаемый_ПриЗавершенииОперации", ЭтотОбъект, ПараметрыЗагрузкиДокументов);

	ИнтеграцияСАТУРНКлиент.ПодготовитьКПередаче(
		ЭтотОбъект,
		ПараметрыЗагрузкиДокументов,
		ОписаниеПриЗавершении);
	
КонецПроцедуры

#Область ВыполнениеОбмена

&НаКлиенте
Процедура Подключаемый_ПриЗавершенииОперации(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ТипЗнч(Результат) = Тип("Массив") Тогда
		Возврат;
	КонецЕсли;

	Закрыть();
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьОбменОбработкаОжидания()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПолученияРезультатОбработкиЗаявки", ЭтотОбъект);
	ИнтеграцияСАТУРНСлужебныйКлиент.ПродолжитьВыполнениеОбмена(ЭтотОбъект,, ОписаниеОповещения, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияРезультатОбработкиЗаявки(Изменения, ДополнительныеПараметры) Экспорт
	
	Если Изменения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти