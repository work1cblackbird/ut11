#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Организации") Тогда
		Организации.ЗагрузитьЗначения(Параметры.Организации);
	КонецЕсли;
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	ОбновитьПредставлениеОрганизации(Организации, ОрганизацииПредставление);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОрганизацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("Организации", ЭтотОбъект.Организации.ВыгрузитьЗначения());
	
	ОткрытьФорму(
		"ОбщаяФорма.ФормаВыбораСпискаОрганизацийИСМП",
		ПараметрыОткрытияФормы,
		ЭтотОбъект,,,,
		Новый ОписаниеОповещения("ПослеЗавершенияВыбораОрганизаций", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	ЭтотОбъект.Организации.Очистить();
	ОбновитьПредставлениеОрганизации(Организации, ОрганизацииПредставление);
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОбновитьПредставлениеОрганизации(Организации, ОрганизацииПредставление);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьВходящиеДокументы(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		
		Если Не ИнтервалКорректен() Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Организация",   ИнтеграцияИСМПКлиент.ОрганизацииДляОбмена(ЭтотОбъект));
		ПараметрыОтбора.Вставить("НачалоПериода", НачалоПериода);
		ПараметрыОтбора.Вставить("КонецПериода",  КонецПериода);
		
		Закрыть(ПараметрыОтбора);
		
	Иначе
		
		Закрыть(Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПредставлениеОрганизации(Организации, ОрганизацииПредставление)
	ОрганизацииПредставление = Строка(Организации);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗавершенияВыбораОрганизаций(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Организации.ЗагрузитьЗначения(Результат.Организации);
		ОбновитьПредставлениеОрганизации(Организации, ОрганизацииПредставление);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ИнтервалКорректен()
	
	Отказ = Ложь;
	ОчиститьСообщения();
	
	Если ЗначениеЗаполнено(КонецПериода) И НачалоПериода > КонецПериода Тогда
		ТекстОшибки = НСтр("ru='Дата окончания периода должна быть больше даты начала. Укажите корректный интервал загрузки документов'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			ТекстОшибки,
			, 
			"КонецПериода",
			, 
			Отказ);
		
	КонецЕсли;
	
	Возврат Не Отказ;
	
КонецФункции

#КонецОбласти
