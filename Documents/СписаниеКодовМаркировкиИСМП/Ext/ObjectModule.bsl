#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если Не ДополнительныеСвойства.Свойство("НеЗаполнятьТабличнуюЧасть") Тогда
		Товары.Очистить();
		ШтрихкодыУпаковок.Очистить();
	КонецЕсли;
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаказНаЭмиссиюКодовМаркировкиСУЗ") Тогда
		ЗаполнитьНепришедшиеКоды(ДанныеЗаполнения);
	КонецЕсли;
	
	ИнтеграцияИСМППереопределяемый.ОбработкаЗаполненияДокумента(ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	
	ЗаполнитьОбъектПоСтатистике();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если ПричинаСписания = Перечисления.ПричиныСписанияКодовМаркировкиИСМП.ВыбытиеДоОбязательностиМаркировки Тогда
		Операция = Перечисления.ВидыОперацийИСМП.СписаниеКодовВыбывшихДоОбязательностиМаркировки;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	ИнтеграцияИСПереопределяемый.ПередЗаписьюОбъекта(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	МассивНепроверяемыхРеквизитов.Добавить("Товары.КоличествоПотребительскихУпаковок");
	
	ЭтоСписаниеПриПоступленииКИЗ = Операция = Перечисления.ВидыОперацийИСМП.СписаниеЭмитированныхКодовМаркировкиПриПоступлении;
	ЭтоВыбытиеДоОбязательностиМаркировки = ПричинаСписания = Перечисления.ПричиныСписанияКодовМаркировкиИСМП.ВыбытиеДоОбязательностиМаркировки;
	
	ЭтоМех = ВидПродукции = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха;
	
	Если ОтчетПроизводственнойЛинии Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары");
	ИначеЕсли ЭтоСписаниеПриПоступленииКИЗ Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары");
		МассивНепроверяемыхРеквизитов.Добавить("ДанныеОтчетаПроизводственнойЛинии");
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("ДанныеОтчетаПроизводственнойЛинии");
	КонецЕсли;
	
	Если Не ЭтоСписаниеПриПоступленииКИЗ Тогда
		МассивНепроверяемыхРеквизитов.Добавить("GLNОрганизации");
	КонецЕсли;

	Если ОбщегоНазначенияИСПовтИсп.ЭтоПродукцияМОТП(ВидПродукции)
		Или ПричинаСписания <> Перечисления.ПричиныСписанияКодовМаркировкиИСМП.ВыбытиеДоОбязательностиМаркировки Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("ВидПервичногоДокумента");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаПервичногоДокумента");
		МассивНепроверяемыхРеквизитов.Добавить("НомерПервичногоДокумента");
		МассивНепроверяемыхРеквизитов.Добавить("НаименованиеПервичногоДокумента");
		
	ИначеЕсли ВидПервичногоДокумента <> Перечисления.ВидыПервичныхДокументовИСМП.Прочее Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("НаименованиеПервичногоДокумента");
		
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("Товары.GTIN");

	Если ИнтеграцияИСКлиентСервер.ВидПродукцииИспользуетПередачуОтчетаОбОтбраковкеЧерезСУЗ(ВидПродукции)
		Или ЭтоСписаниеПриПоступленииКИЗ
		Или ЭтоВыбытиеДоОбязательностиМаркировки Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.ПричинаСписания");
	Иначе
		
		МассивНепроверяемыхРеквизитов.Добавить("ПроизводственныйОбъект");
		Если ЭтоМех И Не ЭтоСписаниеПриПоступленииКИЗ Тогда
			МассивНепроверяемыхРеквизитов.Добавить("ПричинаСписания");
		Иначе
			МассивНепроверяемыхРеквизитов.Добавить("Товары.ПричинаСписания");
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбщегоНазначенияИСПовтИсп.ЭтоПродукцияМОТП(ВидПродукции)
		И ПричинаСписания = Перечисления.ПричиныСписанияКодовМаркировкиИСМП.ВыбытиеДоОбязательностиМаркировки Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("GLNОрганизации");
		МассивНепроверяемыхРеквизитов.Добавить("ИдентификаторПроизводственнойЛинии"); 
		
	ИначеЕсли ОбщегоНазначенияИСПовтИсп.ЭтоПродукцияИСМП(ВидПродукции) Тогда
		
		Если Не ((ВидПродукции = Перечисления.ВидыПродукцииИС.Пиво
				Или ВидПродукции = Перечисления.ВидыПродукцииИС.ПивоВПотребительскихУпаковках)
			И ЭтоВыбытиеДоОбязательностиМаркировки) Тогда
			МассивНепроверяемыхРеквизитов.Добавить("ПроизводственныйОбъектАдресСтрокой");
		КонецЕсли;
		
		МассивНепроверяемыхРеквизитов.Добавить("ПроизводственныйОбъектИдентификатор");
		МассивНепроверяемыхРеквизитов.Добавить("ИдентификаторПроизводственногоЗаказа");
		МассивНепроверяемыхРеквизитов.Добавить("ИдентификаторПроизводственнойЛинии");
		
		Если Не ЭтоСписаниеПриПоступленииКИЗ Тогда
			
			Для Каждого СтрокаТовары Из Товары Цикл
				Если Не ЗначениеЗаполнено(СтрокаТовары.GTIN)
					И Не ЗначениеЗаполнено(СтрокаТовары.Номенклатура) Тогда
					ТекстСообщения = СтрШаблон(
						НСтр(
							"ru = 'В строке %1 табличной части Товары не заполнено поле GTIN'"),
							СтрокаТовары.НомерСтроки);
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
						ЭтотОбъект,
						ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
							"Товары", СтрокаТовары.НомерСтроки, "GTIN"),,
						Отказ);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ((ВидПродукции = Перечисления.ВидыПродукцииИС.Пиво
			Или ВидПродукции = Перечисления.ВидыПродукцииИС.ПивоВПотребительскихУпаковках)
		И ЭтоВыбытиеДоОбязательностиМаркировки) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КодФИАС");
	КонецЕсли;
	
	ИнтеграцияИСМППереопределяемый.ПриОпределенииОбработкиПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	Если Не ЭтоСписаниеПриПоступленииКИЗ Тогда
		ИнтеграцияИСМПСлужебный.ПроверитьЗаполнениеШтрихкодовУпаковок(ЭтотОбъект, Отказ);
	КонецЕсли;
	
	ИнтеграцияИСМП.ПроверкаЗаполненияКоличестваПотребительскихУпаковок(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияИСМП.ЗаписатьСтатусДокументаПоУмолчанию(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДокументОснование   = Неопределено;
	ИдентификаторЗаявки = Неопределено;
	ШтрихкодыУпаковок.Очистить();
	ИдентификаторПроизводственногоЗаказа = "";
	ИдентификаторПроизводственнойЛинии   = "";
	ДанныеОтчетаПроизводственнойЛинии.Очистить();
	
	ВидПервичногоДокумента          = Неопределено;
	ДатаПервичногоДокумента         = "";
	НомерПервичногоДокумента        = "";
	НаименованиеПервичногоДокумента = "";
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаЗаполнения

Процедура ЗаполнитьОбъектПоСтатистике()
	
	ДанныеСтатистики = ЗаполнениеОбъектовПоСтатистикеИСМП.ДанныеЗаполненияСписанияКодовМаркировкиИСМП(Организация);
	
	Для Каждого КлючИЗначение Из ДанныеСтатистики Цикл
		ЗаполнениеОбъектовПоСтатистикеИСМП.ЗаполнитьПустойРеквизит(ЭтотОбъект, ДанныеСтатистики, КлючИЗначение.Ключ);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьНепришедшиеКоды(ДанныеЗаполнения)
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
