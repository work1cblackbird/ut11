#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	ИнтеграцияЕГАИСВызовСервера.ПриПолученииФормыДокумента(
		"ТТНИсходящаяЕГАИС",
		ВидФормы,
		Параметры,
		ВыбраннаяФорма,
		ДополнительнаяИнформация,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Подбирает справки Б в табличную часть товары документа по остаткам.
// 
// Параметры:
//   Объект - ДокументОбъект.ТТНИсходящаяЕГАИС - Документ-объект.
//
// Возвращаемое значение:
//  Булево - Истина, если в табличной части все справки заполнены.
//
Функция ПодобратьСправки2(Объект) Экспорт
	
	СтруктураПересчетаСуммы = ИнтеграцияЕГАИСКлиентСервер.СтруктураПересчетаСуммы("Сумма, КоличествоУпаковок, КоличествоФакт");
	
	ИнтеграцияЕГАИС.ПодобратьСправки2ДляСписанияИзРегистра1(
		Объект.Товары,
		Объект.Грузоотправитель,
		Неопределено,
		СтруктураПересчетаСуммы);
	
	Возврат ИнтеграцияЕГАИС.Справки2ЗаполненыВТабличнойЧасти(Объект.Товары);
	
КонецФункции

#Область ДействияПриОбменеЕГАИС

// Статус после подготовки к передаче данных
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ТТНИсходящаяЕГАИС - Ссылка на документ
//  Операция - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция ЕГАИС
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * НовыйСтатус - ПеречислениеСсылка.СтатусыОбработкиТТНИсходящейЕГАИС - Новый статус.
//   * ДальнейшееДействие1 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 1.
//   * ДальнейшееДействие2 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 2.
//   * ДальнейшееДействие3 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 3.
//
Функция СтатусПослеПодготовкиКПередачеДанных(ДокументСсылка, Операция) Экспорт
	
	Если Операция = Перечисления.ВидыДокументовЕГАИС.ТТН Тогда
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусыКПередаче(
			ДокументСсылка,
			Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.КПередаче);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.АктТТНОтказ Тогда
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусыКПередаче(
			ДокументСсылка,
			Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктОтказаКПередаче);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхожденийПодтверждение Тогда
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусыКПередаче(
			ДокументСсылка,
			Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийПодтверждениеКПередаче);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхожденийОтказ Тогда
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусыКПередаче(
			ДокументСсылка,
			Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийОтказКПередаче);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТНПодтверждение Тогда
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусыКПередаче(
			ДокументСсылка,
			Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияПодтверждениеКПередаче);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТНОтказ Тогда
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусыКПередаче(
			ДокументСсылка,
			Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияОтказКПередаче);
		
	Иначе
		ВызватьИсключение ОбщегоНазначенияИС.ТекстИсключенияОбработкиСтатуса(ДокументСсылка, Операция);
	КонецЕсли;
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Статус после передачи данных
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ТТНИсходящаяЕГАИС - Ссылка на документ
//  Операция - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция ЕГАИС
//  СтатусОбработки - ПеречислениеСсылка.СтатусыОбработкиСообщенийЕГАИС - Статус обработки сообщения
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * НовыйСтатус - ПеречислениеСсылка.СтатусыОбработкиТТНИсходящейЕГАИС - Новый статус.
//   * ДальнейшееДействие1 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 1.
//   * ДальнейшееДействие2 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 2.
//   * ДальнейшееДействие3 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 3.
//
Функция СтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки) Экспорт
	
	Если СтатусОбработки = Неопределено Тогда
		СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПереданоВУТМ;
	КонецЕсли;
	
	Если Операция = Перечисления.ВидыДокументовЕГАИС.ТТН Тогда
		
		СтатусыБазовыйПроцесс = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
		
		СтатусыБазовыйПроцесс.Принят = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ПереданВУТМ;
		СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПолученЕГАИС);
		СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПроведенЕГАИС);
		СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеУведомленияОРегистрацииДвижения);
		
		СтатусыБазовыйПроцесс.Ошибка = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОшибкаПередачи;
		СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПередайтеДанные);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, СтатусыБазовыйПроцесс);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.АктТТНОтказ Тогда
		
		СтатусыБазовыйПроцесс = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
		
		СтатусыБазовыйПроцесс.Принят = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктОтказаПереданВУТМ;
		СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПолученЕГАИС);
		СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПроведенЕГАИС);
		
		СтатусыБазовыйПроцесс.Ошибка = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктОтказаОшибка;
		СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОткажитесьОтНакладной);
		СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОтменитеОперацию);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, СтатусыБазовыйПроцесс);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхожденийПодтверждение Тогда
		
		СтатусыБазовыйПроцесс = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
		
		СтатусыБазовыйПроцесс.Принят = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийПодтверждениеПереданВУТМ;
		СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПолученЕГАИС);
		СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПроведенЕГАИС);
		
		СтатусыБазовыйПроцесс.Ошибка = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийПодтверждениеОшибка;
		СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПодтвердитеАктОРасхождениях);
		СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОтменитеОперацию);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, СтатусыБазовыйПроцесс);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхожденийОтказ Тогда
		
		СтатусыБазовыйПроцесс = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
		
		СтатусыБазовыйПроцесс.Принят = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийОтказПереданВУТМ;
		СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПолученЕГАИС);
		СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПроведенЕГАИС);
		
		СтатусыБазовыйПроцесс.Ошибка = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийОтказОшибка;
		СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОткажитесьОтАктаОРасхождениях);
		СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОтменитеОперацию);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, СтатусыБазовыйПроцесс);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТНПодтверждение Тогда
		
		СтатусыБазовыйПроцесс = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
		
		СтатусыБазовыйПроцесс.Принят = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияПодтверждениеПереданВУТМ;
		СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПолученЕГАИС);
		СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПроведенЕГАИС);
		
		СтатусыБазовыйПроцесс.Ошибка = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияПодтверждениеОшибка;
		СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПодтвердитеЗапросНаОтменуПроведения);
		СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОтменитеОперацию);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, СтатусыБазовыйПроцесс);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТНОтказ Тогда
		
		СтатусыБазовыйПроцесс = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
		
		СтатусыБазовыйПроцесс.Принят = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияОтказПереданВУТМ;
		СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПолученЕГАИС);
		
		СтатусыБазовыйПроцесс.Ошибка = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияОтказОшибка;
		СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОткажитесьОтЗапросаНаОтменуПроведения);
		СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОтменитеОперацию);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, СтатусыБазовыйПроцесс);
		
	Иначе
		ВызватьИсключение ОбщегоНазначенияИС.ТекстИсключенияОбработкиСтатуса(ДокументСсылка, Операция);
	КонецЕсли;
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Статус после получения данных из ЕГАИС.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ТТНИсходящаяЕГАИС - Документ, для которого требуется обновить статус.
//  Операция - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция обмена с ЕГАИС.
//  ДополнительныеПараметры - Неопределено, Структура - со свойствами:
//   * СтатусОбработки - ПеречислениеСсылка.СтатусыОбработкиСообщенийЕГАИС - Статус обработки сообщения.
//   * ОперацияКвитанции - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция, на которую получена квитанция.
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * НовыйСтатус - ПеречислениеСсылка.СтатусыОбработкиТТНИсходящейЕГАИС - Новый статус.
//   * ДальнейшееДействие1 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 1.
//   * ДальнейшееДействие2 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 2.
//   * ДальнейшееДействие3 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 3.
//
Функция СтатусПослеПолученияДанных(ДокументСсылка, Операция, ДополнительныеПараметры = Неопределено) Экспорт
	
	СтатусыБазовыйПроцесс = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
	СтатусыБазовыйПроцесс.Принят           = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОбрабатываетсяКлиентом;
	СтатусыБазовыйПроцесс.Обрабатывается   = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОбрабатываетсяЕГАИС;
	СтатусыБазовыйПроцесс.ОтменаПроведения = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОтмененКлиентом;
	СтатусыБазовыйПроцесс.Ошибка           = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОшибкаПередачи;
	СтатусыБазовыйПроцесс.ОшибкаПроведения = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОшибкаПередачи;
	СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПередайтеДанные);
	СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолученияПодтверждения);
	СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОткажитесьОтНакладной);
	СтатусыБазовыйПроцесс.УведомлениеОРегистрацииДвижения = Истина;
	
	СтатусыАктТТНОтказ = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
	СтатусыАктТТНОтказ.Принят           = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.Отменен;
	СтатусыАктТТНОтказ.Обрабатывается   = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктОтказаОбрабатываетсяЕГАИС;
	СтатусыАктТТНОтказ.ОтменаПроведения = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.Отменен;
	СтатусыАктТТНОтказ.Ошибка           = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктОтказаОшибка;
	СтатусыАктТТНОтказ.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОткажитесьОтНакладной);
	СтатусыАктТТНОтказ.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОтменитеОперацию);
	СтатусыАктТТНОтказ.УведомлениеОРегистрацииДвижения = Ложь;
	
	СтатусыКвитанцияАктаРасхожденийПодтверждение = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
	СтатусыКвитанцияАктаРасхожденийПодтверждение.Принят           = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ПодтвержденСРасхождениями;
	СтатусыКвитанцияАктаРасхожденийПодтверждение.Обрабатывается   = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийПодтверждениеОбрабатываетсяЕГАИС;
	СтатусыКвитанцияАктаРасхожденийПодтверждение.Ошибка           = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийПодтверждениеОшибка;
	СтатусыКвитанцияАктаРасхожденийПодтверждение.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПодтвердитеАктОРасхождениях);
	СтатусыКвитанцияАктаРасхожденийПодтверждение.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОтменитеОперацию);
	СтатусыКвитанцияАктаРасхожденийПодтверждение.УведомлениеОРегистрацииДвижения = Ложь;
	
	СтатусыКвитанцияАктаРасхожденийОтказ = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
	СтатусыКвитанцияАктаРасхожденийОтказ.Принят           = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.Отменен;
	СтатусыКвитанцияАктаРасхожденийОтказ.Обрабатывается   = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийОтказОбрабатываетсяЕГАИС;
	СтатусыКвитанцияАктаРасхожденийОтказ.ОтменаПроведения = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.Отменен;
	СтатусыКвитанцияАктаРасхожденийОтказ.Ошибка           = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийОтказОшибка;
	СтатусыКвитанцияАктаРасхожденийОтказ.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОткажитесьОтАктаОРасхождениях);
	СтатусыКвитанцияАктаРасхожденийОтказ.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОтменитеОперацию);
	СтатусыКвитанцияАктаРасхожденийОтказ.УведомлениеОРегистрацииДвижения = Ложь;
	
	СтатусыЗапросНаОтменуПроведенияТТНПодтверждение = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
	СтатусыЗапросНаОтменуПроведенияТТНПодтверждение.Принят           = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОбрабатываетсяКлиентом;
	СтатусыЗапросНаОтменуПроведенияТТНПодтверждение.Обрабатывается   = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияПодтверждениеОбрабатываетсяЕГАИС;
	СтатусыЗапросНаОтменуПроведенияТТНПодтверждение.ОтменаПроведения = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОбрабатываетсяКлиентом;
	СтатусыЗапросНаОтменуПроведенияТТНПодтверждение.Ошибка           = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияПодтверждениеОшибка;
	СтатусыЗапросНаОтменуПроведенияТТНПодтверждение.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПодтвердитеЗапросНаОтменуПроведения);
	СтатусыЗапросНаОтменуПроведенияТТНПодтверждение.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОтменитеОперацию);
	СтатусыЗапросНаОтменуПроведенияТТНПодтверждение.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолученияПодтверждения);
	СтатусыЗапросНаОтменуПроведенияТТНПодтверждение.ОтменаПроведенияДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолученияПодтверждения);
	СтатусыЗапросНаОтменуПроведенияТТНПодтверждение.УведомлениеОРегистрацииДвижения = Ложь;
	
	СтатусыЗапросНаОтменуПроведенияТТНОтказ = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
	СтатусыЗапросНаОтменуПроведенияТТНОтказ.Принят           = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.Подтвержден;
	СтатусыЗапросНаОтменуПроведенияТТНОтказ.Обрабатывается   = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияОтказОбрабатываетсяЕГАИС;
	СтатусыЗапросНаОтменуПроведенияТТНОтказ.Ошибка           = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияОтказОшибка;
	СтатусыЗапросНаОтменуПроведенияТТНОтказ.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОткажитесьОтЗапросаНаОтменуПроведения);
	СтатусыЗапросНаОтменуПроведенияТТНОтказ.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОтменитеОперацию);
	СтатусыЗапросНаОтменуПроведенияТТНОтказ.УведомлениеОРегистрацииДвижения = Ложь;
	
	ВыполнитьРасчетТекущегоСостояния = Истина;
	Если ДополнительныеПараметры <> Неопределено
		И ДополнительныеПараметры.Свойство("ТекущееСостояние")
		И ДополнительныеПараметры.ТекущееСостояние <> Неопределено Тогда
		ВыполнитьРасчетТекущегоСостояния = ДополнительныеПараметры.ТекущееСостояние;
	КонецЕсли;
	
	Если Операция = Перечисления.ВидыДокументовЕГАИС.АктТТНПодтверждение Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
		Статусы.Принят = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.Подтвержден;
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусы(
			ДокументСсылка,
			Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС,
			Статусы);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.АктТТНОтказ Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
		Статусы.Принят = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОтмененКлиентом;
		
		Если ДополнительныеПараметры.Свойство("ТекущееСостояние")
			И ДополнительныеПараметры.ТекущееСостояние <> Неопределено Тогда
			ТекущееСостояние = ДополнительныеПараметры.ТекущееСостояние;
		Иначе
			ТекущееСостояние = РегистрыСведений.СтатусыДокументовЕГАИС.ТекущееСостояние(ДокументСсылка);
		КонецЕсли;
		
		Если ТекущееСостояние.Статус <> Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.Отменен Тогда
			
			ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусы(
				ДокументСсылка,
				Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС,
				Статусы);
			
		КонецЕсли;
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.АктТТНРасхождения Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
		Статусы.Принят = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийПринят;
		Статусы.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПодтвердитеАктОРасхождениях);
		Статусы.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОткажитесьОтАктаОРасхождениях);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусы(
			ДокументСсылка,
			Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС,
			Статусы);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.ЗапросНаОтменуПроведенияТТН Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
		Статусы.Принят = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияПринят;
		Статусы.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПодтвердитеЗапросНаОтменуПроведения);
		Статусы.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОткажитесьОтЗапросаНаОтменуПроведения);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусы(
			ДокументСсылка,
			Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС,
			Статусы);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.УведомлениеОРегистрацииДвиженияТТН Тогда
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусыПриПолученииКвитанции(
			ДокументСсылка,
			"УведомлениеОРегистрацииДвижения", Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС,
			СтатусыБазовыйПроцесс, ВыполнитьРасчетТекущегоСостояния);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияПолученЕГАИС Тогда
		
		Статусы = Неопределено;
		Если ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыДокументовЕГАИС.ТТН Тогда
			
			Статусы = СтатусыБазовыйПроцесс;
			
		ИначеЕсли ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыДокументовЕГАИС.АктТТНОтказ Тогда
			
			Статусы = СтатусыАктТТНОтказ;
			
		ИначеЕсли ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхожденийПодтверждение Тогда
			
			Статусы = СтатусыКвитанцияАктаРасхожденийПодтверждение;
			
		ИначеЕсли ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхожденийОтказ Тогда
			
			Статусы = СтатусыКвитанцияАктаРасхожденийОтказ;
			
		ИначеЕсли ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТНПодтверждение Тогда
			
			Статусы = СтатусыЗапросНаОтменуПроведенияТТНПодтверждение;
			
		ИначеЕсли ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТНОтказ Тогда
			
			Статусы = СтатусыЗапросНаОтменуПроведенияТТНОтказ;
			
		Иначе
			ВызватьИсключение ОбщегоНазначенияИС.ТекстИсключенияОбработкиСтатуса(ДокументСсылка, Операция);
		КонецЕсли;
		
		Если Статусы <> Неопределено Тогда
			ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусыПриПолученииКвитанции(
				ДокументСсылка,
				"КвитанцияПолученЕГАИС", ДополнительныеПараметры.СтатусОбработки,
				Статусы, ВыполнитьРасчетТекущегоСостояния);
		КонецЕсли;
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияПроведенЕГАИС Тогда
		
		Статусы = Неопределено;
		Если ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыДокументовЕГАИС.ТТН Тогда
			
			Если ДополнительныеПараметры.Свойство("ТекущееСостояние")
				И ДополнительныеПараметры.ТекущееСостояние <> Неопределено Тогда
				ТекущееСостояние = ДополнительныеПараметры.ТекущееСостояние;
			Иначе
				ТекущееСостояние = РегистрыСведений.СтатусыДокументовЕГАИС.ТекущееСостояние(ДокументСсылка);
			КонецЕсли;
			
			Если ТекущееСостояние.Статус = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийПодтверждениеОбрабатываетсяЕГАИС Тогда
				Статусы = СтатусыКвитанцияАктаРасхожденийПодтверждение;
			ИначеЕсли ТекущееСостояние.Статус = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийОтказОбрабатываетсяЕГАИС Тогда
				Статусы = СтатусыКвитанцияАктаРасхожденийОтказ;
			ИначеЕсли ТекущееСостояние.Статус = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктОтказаОбрабатываетсяЕГАИС Тогда
				Статусы = СтатусыАктТТНОтказ;
			ИначеЕсли ТекущееСостояние.Статус = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияПодтверждениеОбрабатываетсяЕГАИС Тогда
				Статусы = СтатусыЗапросНаОтменуПроведенияТТНПодтверждение;
			ИначеЕсли ТекущееСостояние.Статус = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияОтказОбрабатываетсяЕГАИС Тогда
				Статусы = СтатусыЗапросНаОтменуПроведенияТТНОтказ;
			Иначе
				Статусы = СтатусыБазовыйПроцесс;
			КонецЕсли;
			
		ИначеЕсли ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыДокументовЕГАИС.АктТТНОтказ Тогда
			
			Статусы = СтатусыАктТТНОтказ;
			
		ИначеЕсли ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхожденийПодтверждение Тогда
			
			// Приходит квитанция по операции ТТН (см. выше)
			Статусы = СтатусыКвитанцияАктаРасхожденийПодтверждение;
			
		ИначеЕсли ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхожденийОтказ Тогда
			
			Статусы = СтатусыКвитанцияАктаРасхожденийОтказ;
			
		ИначеЕсли ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТНПодтверждение Тогда
			
			Статусы = СтатусыЗапросНаОтменуПроведенияТТНПодтверждение;
			
		ИначеЕсли ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТНОтказ Тогда
			
			Статусы = СтатусыЗапросНаОтменуПроведенияТТНОтказ;
			
		Иначе
			ВызватьИсключение ОбщегоНазначенияИС.ТекстИсключенияОбработкиСтатуса(ДокументСсылка, Операция);
		КонецЕсли;
		
		Если Статусы <> Неопределено Тогда
			ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусыПриПолученииКвитанции(
				ДокументСсылка,
				"КвитанцияПроведенЕГАИС", ДополнительныеПараметры.СтатусОбработки,
				Статусы, ВыполнитьРасчетТекущегоСостояния);
		КонецЕсли;
		
	Иначе
		ВызватьИсключение ОбщегоНазначенияИС.ТекстИсключенияОбработкиСтатуса(ДокументСсылка, Операция);
	КонецЕсли;
	
	Возврат ПараметрыОбновления;
	
КонецФункции


// Обновить статус после подготовки к передаче данных
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ТТНИсходящаяЕГАИС - Ссылка на документ
//  Операция - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция ЕГАИС
//  ДополнительныеПараметры - Неопределено, Структура - дополнительные параметры для расчета статуса документа
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыОбработкиТТНИсходящейЕГАИС - Новый статус.
//
Функция ОбновитьСтатусПослеПодготовкиКПередачеДанных(ДокументСсылка, Операция, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыОбновления = СтатусПослеПодготовкиКПередачеДанных(ДокументСсылка, Операция);
	
	Если ПараметрыОбновления = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НовыйСтатусПослеОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.ОбновитьСтатус(
		ДокументСсылка,
		ПараметрыОбновления, ДополнительныеПараметры);
	
	Возврат НовыйСтатусПослеОбновления;
	
КонецФункции

// Обновить статус после передачи данных
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ТТНИсходящаяЕГАИС - Ссылка на документ
//  Операция - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция ЕГАИС
//  СтатусОбработки - ПеречислениеСсылка.СтатусыОбработкиСообщенийЕГАИС - Статус обработки сообщения
//  ДополнительныеПараметры - Неопределено, Структура - дополнительные параметры для расчета статуса документа
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыОбработкиТТНИсходящейЕГАИС - Новый статус.
//
Функция ОбновитьСтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыОбновления = СтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки);
	
	Если ПараметрыОбновления = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НовыйСтатусПослеОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.ОбновитьСтатус(
		ДокументСсылка,
		ПараметрыОбновления, ДополнительныеПараметры);
	
	Возврат НовыйСтатусПослеОбновления;
	
КонецФункции

// Обновить статус после получения данных из ЕГАИС.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ТТНИсходящаяЕГАИС - Документ, для которого требуется обновить статус.
//  Операция - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция обмена с ЕГАИС.
//  ДополнительныеПараметры - Неопределено, Структура - со свойствами:
//   * СтатусОбработки - ПеречислениеСсылка.СтатусыОбработкиСообщенийЕГАИС - Статус обработки сообщения.
//   * ОперацияКвитанции - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция, на которую получена квитанция.
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыОбработкиТТНИсходящейЕГАИС - Новый статус.
//
Функция ОбновитьСтатусПослеПолученияДанных(ДокументСсылка, Операция, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыОбновления = СтатусПослеПолученияДанных(ДокументСсылка, Операция, ДополнительныеПараметры);
	
	Если ПараметрыОбновления = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НовыйСтатусПослеОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.ОбновитьСтатус(
		ДокументСсылка,
		ПараметрыОбновления, ДополнительныеПараметры);
	
	Возврат НовыйСтатусПослеОбновления;
	
КонецФункции

// Изменяет и возвращает статус документа ЕГАИС.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ТТНИсходящаяЕГАИС - Документ, для которого требуется обновить статус.
//  ПараметрыОбновления - Структура - со свойствами:
//   * НовыйСтатус - ПеречислениеСсылка.СтатусыИнформированияЕГАИС - Новый статус.
//   * ДальнейшееДействие1 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 1.
//   * ДальнейшееДействие2 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 2.
//   * ДальнейшееДействие3 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 3.
//  ДополнительныеПараметры - Неопределено, Структура - со свойствами:
//   * СтатусОбработки - ПеречислениеСсылка.СтатусыОбработкиСообщенийЕГАИС - Статус обработки сообщения.
//   * ОперацияКвитанции - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция, на которую получена квитанция.
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыИнформированияЕГАИС - новый статус документа ЕГАИС.
Функция ОбновитьСтатус(ДокументСсылка, ПараметрыОбновления, ДополнительныеПараметры) Экспорт
	
	НовыйСтатусПослеОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.ОбновитьСтатус(
		ДокументСсылка,
		ПараметрыОбновления, ДополнительныеПараметры);
	
	Возврат НовыйСтатусПослеОбновления;
	
КонецФункции

// Получить последовательность операций в течении жизненного цикла документа.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ТТНИсходящаяЕГАИС - Документ, для которого требуется обновить статус.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - см. функцию ОбменДаннымиЕГАИС.ПустаяТаблицаПоследовательностьОпераций().
//
Функция ПоследовательностьОпераций(ДокументСсылка) Экспорт
	
	Таблица = ОбменДаннымиЕГАИС.ПустаяТаблицаПоследовательностьОпераций();
	
	Исходящий = Перечисления.ТипыЗапросовИС.Исходящий;
	Входящий  = Перечисления.ТипыЗапросовИС.Входящий;
	
	ОбменДаннымиЕГАИС.ДобавитьОперациюВПоследовательность(Таблица,  0,   Исходящий, Перечисления.ВидыДокументовЕГАИС.ТТН, ДокументСсылка);
	ОбменДаннымиЕГАИС.ДобавитьОперациюВПоследовательность(Таблица,  0,   Входящий,  Перечисления.ВидыДокументовЕГАИС.УведомлениеОРегистрацииДвиженияТТН);
	ОбменДаннымиЕГАИС.ДобавитьОперациюВПоследовательность(Таблица, -1,   Исходящий, Перечисления.ВидыДокументовЕГАИС.АктТТНОтказ, ДокументСсылка, Истина, Ложь);
	ОбменДаннымиЕГАИС.ДобавитьОперациюВПоследовательность(Таблица,  0,   Входящий,  Перечисления.ВидыДокументовЕГАИС.АктТТН);
	
	ОбменДаннымиЕГАИС.ДобавитьОперациюВПоследовательность(Таблица, -2,   Входящий,  Перечисления.ВидыДокументовЕГАИС.АктТТНОтказ);
	
	ОбменДаннымиЕГАИС.ДобавитьОперациюВПоследовательность(Таблица, -3,   Входящий,  Перечисления.ВидыДокументовЕГАИС.АктТТНПодтверждение);
	ОбменДаннымиЕГАИС.ДобавитьОперациюВПоследовательность(Таблица, -31,  Входящий,  Перечисления.ВидыДокументовЕГАИС.ЗапросНаОтменуПроведенияТТН);
	ОбменДаннымиЕГАИС.ДобавитьОперациюВПоследовательность(Таблица, -31,  Исходящий, Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТН,              ДокументСсылка);
	ОбменДаннымиЕГАИС.ДобавитьОперациюВПоследовательность(Таблица, -311, Исходящий, Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТНОтказ,         ДокументСсылка);
	ОбменДаннымиЕГАИС.ДобавитьОперациюВПоследовательность(Таблица, -312, Исходящий, Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТНПодтверждение, ДокументСсылка);
	
	ОбменДаннымиЕГАИС.ДобавитьОперациюВПоследовательность(Таблица, -4,   Входящий,  Перечисления.ВидыДокументовЕГАИС.АктТТНРасхождения);
	ОбменДаннымиЕГАИС.ДобавитьОперациюВПоследовательность(Таблица, -4,   Исходящий, Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхождений, ДокументСсылка);
	
	ОбменДаннымиЕГАИС.ДобавитьОперациюВПоследовательность(Таблица, -41,  Исходящий, Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхожденийПодтверждение, ДокументСсылка);
	ОбменДаннымиЕГАИС.ДобавитьОперациюВПоследовательность(Таблица, -41,  Входящий,  Перечисления.ВидыДокументовЕГАИС.ЗапросНаОтменуПроведенияТТН);
	ОбменДаннымиЕГАИС.ДобавитьОперациюВПоследовательность(Таблица, -41,  Исходящий, Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТН,              ДокументСсылка);
	ОбменДаннымиЕГАИС.ДобавитьОперациюВПоследовательность(Таблица, -411, Исходящий, Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТНОтказ,         ДокументСсылка);
	ОбменДаннымиЕГАИС.ДобавитьОперациюВПоследовательность(Таблица, -412, Исходящий, Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТНПодтверждение, ДокументСсылка);
	
	ОбменДаннымиЕГАИС.ДобавитьОперациюВПоследовательность(Таблица, -42,  Исходящий, Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхожденийОтказ, ДокументСсылка, Истина, Ложь);
	
	Возврат Таблица;
	
КонецФункции

// Обработчик изменения статуса документа.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ТТНИсходящаяЕГАИС - Документ.
//  ПредыдущийСтатус - ПеречислениеСсылка.СтатусыОбработкиТТНИсходящейЕГАИС - Предыдущий статус.
//  НовыйСтатус - ПеречислениеСсылка.СтатусыОбработкиТТНИсходящейЕГАИС - Предыдущий статус.
//  ПараметрыОбновленияСтатуса - Структура - см. функцию ОбменДаннымиЕГАИС.ПараметрыОбновленияСтатуса().
//
Процедура ПриИзмененииСтатусаДокумента(ДокументСсылка, ПредыдущийСтатус, НовыйСтатус, ПараметрыОбновленияСтатуса) Экспорт
	
	СтатусыДвиженийСвободныйОстаток = СтатусыДвиженийСвободныйОстаток();
	СтатусыДвиженийКоличество       = СтатусыДвиженийКоличество();
	
	ОбновитьДвижения = ИнтеграцияЕГАИС.СтатусТребуетОбновленияДвижений(СтатусыДвиженийСвободныйОстаток, ПредыдущийСтатус, НовыйСтатус)
	               Или ИнтеграцияЕГАИС.СтатусТребуетОбновленияДвижений(СтатусыДвиженийКоличество, ПредыдущийСтатус, НовыйСтатус);
	
	Если ПараметрыОбновленияСтатуса.ОбновлятьДвижения
		И ОбновитьДвижения Тогда
		
		ИмяРегистра = "ОстаткиАлкогольнойПродукцииЕГАИС";
		
		НаборЗаписей = РегистрыНакопления[ИмяРегистра].СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
		
		ДополнительныеСвойстваДляПроведения = Новый Структура;
		ИнтеграцияИС.ИнициализироватьДополнительныеСвойстваДляПроведения(ДокументСсылка, ДополнительныеСвойстваДляПроведения);
		
		ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойстваДляПроведения, ИмяРегистра);
		НаборЗаписей.Загрузить(ДополнительныеСвойстваДляПроведения.ТаблицыДляДвижений["Таблица" + ИмяРегистра]);
		НаборЗаписей.Записать();
		
	КонецЕсли;
	
	// Резервирование при отгрузке
	Если ИнтеграцияЕГАИС.СтатусТребуетДобавленияДвижений(СтатусыДвиженийСвободныйОстаток, ПредыдущийСтатус, НовыйСтатус)
		И ИнтеграцияЕГАИС.НетДвижений(СтатусыДвиженийКоличество, ПредыдущийСтатус, НовыйСтатус) Тогда
		
		Если ПараметрыОбновленияСтатуса.ДокументОбъект = Неопределено Тогда
			Товары = ИнтеграцияЕГАИС.Справки2ПоДокументу(ДокументСсылка);
		Иначе
			Товары = ПараметрыОбновленияСтатуса.ДокументОбъект.Товары;
		КонецЕсли;
		
		ШтрихкодыУпаковок = ШтрихкодированиеЕГАИС.ВложенныеШтрихкодыУпаковокПоДокументу(ДокументСсылка);
		Для Каждого СтрокаТЧ Из ШтрихкодыУпаковок.МаркированныеТовары Цикл
			
			Если ЗначениеЗаполнено(СтрокаТЧ.Справка2)
				И СтрокаТЧ.Статус = Перечисления.СтатусыАкцизныхМарок.ОшибкаЧтенияСтатуса Тогда
				АкцизныеМаркиЕГАИС.СообщитьОбОшибкеЧтенияСтатуса(СтрокаТЧ.ШтрихкодУпаковки);
				Продолжить;
			КонецЕсли;
			
			СтрокаТовара = Неопределено;
			
			ДанныеЗаписиСтатуса = РегистрыСведений.АкцизныеМаркиЕГАИС.ПоляЗаписиСтатусаУпаковки();
			ДанныеЗаписиСтатуса.ОрганизацияЕГАИС     = СтрокаТЧ.ОрганизацияЕГАИС;
			ДанныеЗаписиСтатуса.АкцизнаяМарка        = СтрокаТЧ.ШтрихкодУпаковки;
			ДанныеЗаписиСтатуса.Статус               = Перечисления.СтатусыАкцизныхМарок.ВРезерве;
			
			Если ЗначениеЗаполнено(СтрокаТЧ.Справка2) Тогда
				ДанныеЗаписиСтатуса.Справка2 = СтрокаТЧ.Справка2;
			Иначе
				Если СтрокаТовара = Неопределено Тогда
					СтрокаТовара = Товары.Найти(СтрокаТЧ.ИдентификаторСтроки);
				КонецЕсли;
				Если ЗначениеЗаполнено(СтрокаТовара) Тогда
					ДанныеЗаписиСтатуса.Справка2 = СтрокаТовара.Справка2;
				Иначе
					АкцизныеМаркиЕГАИС.СообщитьОбОшибкеЗаполненияСправки2(СтрокаТЧ.ШтрихкодУпаковки);
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаТЧ.АлкогольнаяПродукция) Тогда
				ДанныеЗаписиСтатуса.АлкогольнаяПродукция = СтрокаТЧ.АлкогольнаяПродукция;
			Иначе
				Если СтрокаТовара = Неопределено Тогда
					СтрокаТовара = Товары.Найти(СтрокаТЧ.ИдентификаторСтроки);
				КонецЕсли;
				Если ЗначениеЗаполнено(СтрокаТовара) Тогда
					ДанныеЗаписиСтатуса.АлкогольнаяПродукция = СтрокаТовара.АлкогольнаяПродукция;
				Иначе
					АкцизныеМаркиЕГАИС.СообщитьОбОшибкеЗаполненияСправки2(СтрокаТЧ.ШтрихкодУпаковки);
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			ДанныеЗаписиСтатуса.Основание = ДокументСсылка;
			РегистрыСведений.АкцизныеМаркиЕГАИС.ВыполнитьЗаписьВРегистр(ДанныеЗаписиСтатуса);
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Отгрузка проведена (или подтвержден акт о расхождениях)
	Если ИнтеграцияЕГАИС.СтатусТребуетДобавленияДвижений(СтатусыДвиженийКоличество, ПредыдущийСтатус, НовыйСтатус) Тогда
		
		Если ПараметрыОбновленияСтатуса.ДокументОбъект = Неопределено Тогда
			Товары = ИнтеграцияЕГАИС.Справки2ПоДокументу(ДокументСсылка);
			ЕстьРасхождения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ЕстьРасхождения");
		Иначе
			Товары = ПараметрыОбновленияСтатуса.ДокументОбъект.Товары;
			ЕстьРасхождения = ПараметрыОбновленияСтатуса.ДокументОбъект.ЕстьРасхождения;
		КонецЕсли;
		
		Если ЕстьРасхождения Тогда
			НеподтвержденныеАкцизныеМарки = НеподтвержденныеАкцизныеМарки(ДокументСсылка);
		КонецЕсли;
		
		ШтрихкодыУпаковок = ШтрихкодированиеЕГАИС.ВложенныеШтрихкодыУпаковокПоДокументу(ДокументСсылка);
		Для Каждого СтрокаТЧ Из ШтрихкодыУпаковок.МаркированныеТовары Цикл
			
			Если ЗначениеЗаполнено(СтрокаТЧ.Справка2)
				И СтрокаТЧ.Статус = Перечисления.СтатусыАкцизныхМарок.ОшибкаЧтенияСтатуса Тогда
				АкцизныеМаркиЕГАИС.СообщитьОбОшибкеЧтенияСтатуса(СтрокаТЧ.ШтрихкодУпаковки);
				Продолжить;
			КонецЕсли;
			
			СтрокаТовара = Неопределено;
			
			ДанныеЗаписиСтатуса = РегистрыСведений.АкцизныеМаркиЕГАИС.ПоляЗаписиСтатусаУпаковки();
			ДанныеЗаписиСтатуса.ОрганизацияЕГАИС     = СтрокаТЧ.ОрганизацияЕГАИС;
			ДанныеЗаписиСтатуса.АкцизнаяМарка        = СтрокаТЧ.ШтрихкодУпаковки;
			Если ЕстьРасхождения Тогда
				Если НеподтвержденныеАкцизныеМарки.Получить(СтрокаТЧ.Штрихкод) <> Неопределено Тогда
					ДанныеЗаписиСтатуса.Статус = Перечисления.СтатусыАкцизныхМарок.ВНаличии;
				Иначе
					ДанныеЗаписиСтатуса.Статус = Перечисления.СтатусыАкцизныхМарок.Реализована;
				КонецЕсли;
			Иначе
				ДанныеЗаписиСтатуса.Статус = Перечисления.СтатусыАкцизныхМарок.Реализована;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаТЧ.Справка2) Тогда
				ДанныеЗаписиСтатуса.Справка2 = СтрокаТЧ.Справка2;
			Иначе
				Если СтрокаТовара = Неопределено Тогда
					СтрокаТовара = Товары.Найти(СтрокаТЧ.ИдентификаторСтроки);
				КонецЕсли;
				Если ЗначениеЗаполнено(СтрокаТовара) Тогда
					ДанныеЗаписиСтатуса.Справка2 = СтрокаТовара.Справка2;
				Иначе
					АкцизныеМаркиЕГАИС.СообщитьОбОшибкеЗаполненияСправки2(СтрокаТЧ.ШтрихкодУпаковки);
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаТЧ.АлкогольнаяПродукция) Тогда
				ДанныеЗаписиСтатуса.АлкогольнаяПродукция = СтрокаТЧ.АлкогольнаяПродукция;
			Иначе
				Если СтрокаТовара = Неопределено Тогда
					СтрокаТовара = Товары.Найти(СтрокаТЧ.ИдентификаторСтроки);
				КонецЕсли;
				Если ЗначениеЗаполнено(СтрокаТовара) Тогда
					ДанныеЗаписиСтатуса.АлкогольнаяПродукция = СтрокаТовара.АлкогольнаяПродукция;
				Иначе
					АкцизныеМаркиЕГАИС.СообщитьОбОшибкеЗаполненияСправки2(СтрокаТЧ.ШтрихкодУпаковки);
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			ДанныеЗаписиСтатуса.Основание = ДокументСсылка;
			РегистрыСведений.АкцизныеМаркиЕГАИС.ВыполнитьЗаписьВРегистр(ДанныеЗаписиСтатуса);
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Отмена резервирования при ошибке или отказе от накладной
	Если ИнтеграцияЕГАИС.СтатусТребуетУдаленияДвижений(СтатусыДвиженийСвободныйОстаток, ПредыдущийСтатус, НовыйСтатус)
		И ИнтеграцияЕГАИС.НетДвижений(СтатусыДвиженийКоличество, ПредыдущийСтатус, НовыйСтатус) Тогда
		
		Если ПараметрыОбновленияСтатуса.ДокументОбъект = Неопределено Тогда
			Товары = ИнтеграцияЕГАИС.Справки2ПоДокументу(ДокументСсылка);
		Иначе
			Товары = ПараметрыОбновленияСтатуса.ДокументОбъект.Товары;
		КонецЕсли;
		
		ШтрихкодыУпаковок = ШтрихкодированиеЕГАИС.ВложенныеШтрихкодыУпаковокПоДокументу(ДокументСсылка);
		Для Каждого СтрокаТЧ Из ШтрихкодыУпаковок.МаркированныеТовары Цикл
			
			Если ЗначениеЗаполнено(СтрокаТЧ.Справка2)
				И СтрокаТЧ.Статус = Перечисления.СтатусыАкцизныхМарок.ОшибкаЧтенияСтатуса Тогда
				АкцизныеМаркиЕГАИС.СообщитьОбОшибкеЧтенияСтатуса(СтрокаТЧ.ШтрихкодУпаковки);
				Продолжить;
			КонецЕсли;
			
			СтрокаТовара = Неопределено;
			
			ДанныеЗаписиСтатуса = РегистрыСведений.АкцизныеМаркиЕГАИС.ПоляЗаписиСтатусаУпаковки();
			ДанныеЗаписиСтатуса.ОрганизацияЕГАИС     = СтрокаТЧ.ОрганизацияЕГАИС;
			ДанныеЗаписиСтатуса.АкцизнаяМарка        = СтрокаТЧ.ШтрихкодУпаковки;
			ДанныеЗаписиСтатуса.Статус               = Перечисления.СтатусыАкцизныхМарок.ВНаличии;
			
			Если ЗначениеЗаполнено(СтрокаТЧ.Справка2) Тогда
				ДанныеЗаписиСтатуса.Справка2 = СтрокаТЧ.Справка2;
			Иначе
				Если СтрокаТовара = Неопределено Тогда
					СтрокаТовара = Товары.Найти(СтрокаТЧ.ИдентификаторСтроки);
				КонецЕсли;
				Если ЗначениеЗаполнено(СтрокаТовара) Тогда
					ДанныеЗаписиСтатуса.Справка2 = СтрокаТовара.Справка2;
				Иначе
					АкцизныеМаркиЕГАИС.СообщитьОбОшибкеЗаполненияСправки2(СтрокаТЧ.ШтрихкодУпаковки);
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаТЧ.АлкогольнаяПродукция) Тогда
				ДанныеЗаписиСтатуса.АлкогольнаяПродукция = СтрокаТЧ.АлкогольнаяПродукция;
			Иначе
				Если СтрокаТовара = Неопределено Тогда
					СтрокаТовара = Товары.Найти(СтрокаТЧ.ИдентификаторСтроки);
				КонецЕсли;
				Если ЗначениеЗаполнено(СтрокаТовара) Тогда
					ДанныеЗаписиСтатуса.АлкогольнаяПродукция = СтрокаТовара.АлкогольнаяПродукция;
				Иначе
					АкцизныеМаркиЕГАИС.СообщитьОбОшибкеЗаполненияСправки2(СтрокаТЧ.ШтрихкодУпаковки);
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			ДанныеЗаписиСтатуса.Основание = ДокументСсылка;
			РегистрыСведений.АкцизныеМаркиЕГАИС.ВыполнитьЗаписьВРегистр(ДанныеЗаписиСтатуса);
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Отмена акта подтверждения
	Если ИнтеграцияЕГАИС.СтатусТребуетУдаленияДвижений(СтатусыДвиженийКоличество, ПредыдущийСтатус, НовыйСтатус)
		И ИнтеграцияЕГАИС.ЕстьДвижения(СтатусыДвиженийСвободныйОстаток, ПредыдущийСтатус, НовыйСтатус) Тогда
		
		Если ПараметрыОбновленияСтатуса.ДокументОбъект = Неопределено Тогда
			Товары = ИнтеграцияЕГАИС.Справки2ПоДокументу(ДокументСсылка);
		Иначе
			Товары = ПараметрыОбновленияСтатуса.ДокументОбъект.Товары;
		КонецЕсли;
		
		ШтрихкодыУпаковок = ШтрихкодированиеЕГАИС.ВложенныеШтрихкодыУпаковокПоДокументу(ДокументСсылка);
		Для Каждого СтрокаТЧ Из ШтрихкодыУпаковок.МаркированныеТовары Цикл
			
			Если ЗначениеЗаполнено(СтрокаТЧ.Справка2)
				И СтрокаТЧ.Статус = Перечисления.СтатусыАкцизныхМарок.ОшибкаЧтенияСтатуса Тогда
				АкцизныеМаркиЕГАИС.СообщитьОбОшибкеЧтенияСтатуса(СтрокаТЧ.ШтрихкодУпаковки);
				Продолжить;
			КонецЕсли;
			
			СтрокаТовара = Неопределено;
			
			ДанныеЗаписиСтатуса = РегистрыСведений.АкцизныеМаркиЕГАИС.ПоляЗаписиСтатусаУпаковки();
			ДанныеЗаписиСтатуса.ОрганизацияЕГАИС     = СтрокаТЧ.ОрганизацияЕГАИС;
			ДанныеЗаписиСтатуса.АкцизнаяМарка        = СтрокаТЧ.ШтрихкодУпаковки;
			ДанныеЗаписиСтатуса.Статус               = Перечисления.СтатусыАкцизныхМарок.ВРезерве;
			
			Если ЗначениеЗаполнено(СтрокаТЧ.Справка2) Тогда
				ДанныеЗаписиСтатуса.Справка2 = СтрокаТЧ.Справка2;
			Иначе
				Если СтрокаТовара = Неопределено Тогда
					СтрокаТовара = Товары.Найти(СтрокаТЧ.ИдентификаторСтроки);
				КонецЕсли;
				Если ЗначениеЗаполнено(СтрокаТовара) Тогда
					ДанныеЗаписиСтатуса.Справка2 = СтрокаТовара.Справка2;
				Иначе
					АкцизныеМаркиЕГАИС.СообщитьОбОшибкеЗаполненияСправки2(СтрокаТЧ.ШтрихкодУпаковки);
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаТЧ.АлкогольнаяПродукция) Тогда
				ДанныеЗаписиСтатуса.АлкогольнаяПродукция = СтрокаТЧ.АлкогольнаяПродукция;
			Иначе
				Если СтрокаТовара = Неопределено Тогда
					СтрокаТовара = Товары.Найти(СтрокаТЧ.ИдентификаторСтроки);
				КонецЕсли;
				Если ЗначениеЗаполнено(СтрокаТовара) Тогда
					ДанныеЗаписиСтатуса.АлкогольнаяПродукция = СтрокаТовара.АлкогольнаяПродукция;
				Иначе
					АкцизныеМаркиЕГАИС.СообщитьОбОшибкеЗаполненияСправки2(СтрокаТЧ.ШтрихкодУпаковки);
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			ДанныеЗаписиСтатуса.Основание = ДокументСсылка;
			РегистрыСведений.АкцизныеМаркиЕГАИС.ВыполнитьЗаписьВРегистр(ДанныеЗаписиСтатуса);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ИнтеграцияЕГАИСПереопределяемый.ПриИзмененииСтатусаДокумента(ДокументСсылка, ПредыдущийСтатус, НовыйСтатус, ПараметрыОбновленияСтатуса);
	
	//Получение конечного статуса требующего повторного оформления
	Если КонечныеСтатусы().Найти(НовыйСтатус)<>Неопределено
		И КонечныеСтатусы().Найти(ПредыдущийСтатус)=Неопределено Тогда
		РасчетСтатусовОформленияЕГАИС.РассчитатьСтатусыОформленияДокументовЕГАИС(ДокументСсылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Статусы

// Возвращает статус по умолчанию.
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыОбработкиТТНИсходящейЕГАИС - Статус по-умолчанию.
//
Функция СтатусПоУмолчанию() Экспорт
	
	Возврат Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.Черновик;
	
КонецФункции

// Возвращает статусы ошибок.
//
// Возвращаемое значение:
//  Массив - Статусы ошибок.
//
Функция СтатусыОшибок() Экспорт
	
	Статусы = Новый Массив;
	
	Статусы.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОшибкаПередачи);
	Статусы.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктОтказаОшибка);
	Статусы.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийПодтверждениеОшибка);
	Статусы.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийОтказОшибка);
	Статусы.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияПодтверждениеОшибка);
	Статусы.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияОтказОшибка);
	
	Возврат Статусы;
	
КонецФункции

// Возвращает конечные статусы.
//
// Возвращаемое значение:
//  Массив - Конечные статусы.
//
Функция КонечныеСтатусы(ТребуетсяПовторноеОформление = Истина) Экспорт
	
	Статусы = Новый Массив;
	
	Статусы.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.Отменен);
	Статусы.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОтмененКлиентом);
	Если Не ТребуетсяПовторноеОформление Тогда
		Статусы.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ПодтвержденСРасхождениями);
		Статусы.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.Подтвержден);
	КонецЕсли;
	
	Возврат Статусы;
	
КонецФункции

// Возвращает дальнейшее действие по умолчанию.
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие по-умолчанию.
//
Функция ДальнейшееДействиеПоУмолчанию() Экспорт
	
	Возврат Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПередайтеДанные;
	
КонецФункции

#КонецОбласти

#Область ПанельОбменСЕГАИС

// Возвращает массив дальнейших действий с документом, требующих участия пользователя
// 
// Возвращаемое значение:
// 	Массив из ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - дальшейшие действия
//
Функция ВсеТребующиеДействия() Экспорт
	
	МассивДействий = Новый Массив;
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПередайтеДанные);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОткажитесьОтНакладной);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПодтвердитеАктОРасхождениях);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОткажитесьОтАктаОРасхождениях);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПодтвердитеЗапросНаОтменуПроведения);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОткажитесьОтЗапросаНаОтменуПроведения);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ВыполнитеОбмен);
	
	Возврат МассивДействий;
	
КонецФункции

Функция ВсеТребующиеОжидания() Экспорт
	
	МассивДействий = Новый Массив;
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПередачуДанныхРегламентнымЗаданием);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПолученЕГАИС);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПроведенЕГАИС);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеУведомленияОРегистрацииДвижения);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолученияПодтверждения);
	
	Возврат МассивДействий;
	
КонецФункции

// Возвращает текст запроса для получения количества документов для оформления
// 
// Возвращаемое значение:
//  Строка - Текст запроса
//
Функция ТекстЗапросаПанельОбменСЕГАИСОформите() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	 0 КАК КоличествоДокументов
	|";
	ИнтеграцияЕГАИСПереопределяемый.ТекстЗапросаТТНИсходящаяЕГАИСОформите(ТекстЗапроса);
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса для получения количества документов для отработки
// 
// Возвращаемое значение:
//  Строка - Текст запроса
//
Функция ТекстЗапросаПанельОбменСЕГАИСОтработайте() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыДокументовЕГАИС.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.ТТНИсходящаяЕГАИС КАК ТТНИсходящаяЕГАИС
	|ПО
	|	СтатусыДокументовЕГАИС.Документ = ТТНИсходящаяЕГАИС.Ссылка
	|ГДЕ
	|	ТТНИсходящаяЕГАИС.Ссылка ЕСТЬ НЕ NULL
	|	И НЕ ТТНИсходящаяЕГАИС.ПометкаУдаления
	|	И СтатусыДокументовЕГАИС.ДальнейшееДействие1 В(&ВсеТребующиеДействия)
	|	И (ТТНИсходящаяЕГАИС.Грузоотправитель В(&ОрганизацияЕГАИС)
	|		ИЛИ &БезОтбораПоОрганизацииЕГАИС)
	|	И (ТТНИсходящаяЕГАИС.Ответственный = &Ответственный
	|		ИЛИ &Ответственный = НЕОПРЕДЕЛЕНО)
	|";
	ИнтеграцияЕГАИСПереопределяемый.ТекстЗапросаТТНИсходящаяЕГАИСОтработайте(ТекстЗапроса);
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса для получения количества документов, находящихся в состоянии ожидания
// 
// Возвращаемое значение:
//  Строка - Текст запроса
//
Функция ТекстЗапросаПанельОбменСЕГАИСОжидайте() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыДокументовЕГАИС.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.ТТНИсходящаяЕГАИС КАК ТТНИсходящаяЕГАИС
	|ПО
	|	СтатусыДокументовЕГАИС.Документ = ТТНИсходящаяЕГАИС.Ссылка
	|ГДЕ
	|	ТТНИсходящаяЕГАИС.Ссылка ЕСТЬ НЕ NULL
	|	И НЕ ТТНИсходящаяЕГАИС.ПометкаУдаления
	|	И СтатусыДокументовЕГАИС.ДальнейшееДействие1 В(&ВсеТребующиеОжидания)
	|	И (ТТНИсходящаяЕГАИС.Грузоотправитель В(&ОрганизацияЕГАИС)
	|		ИЛИ &БезОтбораПоОрганизацииЕГАИС)
	|	И (ТТНИсходящаяЕГАИС.Ответственный = &Ответственный
	|		ИЛИ &Ответственный = НЕОПРЕДЕЛЕНО)
	|";
	ИнтеграцияЕГАИСПереопределяемый.ТекстЗапросаТТНИсходящаяЕГАИСОжидайте(ТекстЗапроса);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область СообщенияЕГАИС

// Сообщение к передаче XML
//
// Параметры:
//  ДокументСсылка - ДокументСсылка - Ссылка на документ
//  ДальнейшееДействие - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Операция ЕГАИС
//  ДополнительныеПараметры - Неопределено, Структура - дополнительные параметры формирования сообщения XML
//
// Возвращаемое значение:
//  Строка - Текст сообщения XML
//
Функция СообщениеКПередачеXML(ДокументСсылка, ДальнейшееДействие, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПередайтеДанные Тогда
		
		Возврат ТТНИсходящаяЕГАИСXML(ДокументСсылка);
		
	ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОткажитесьОтНакладной Тогда
		
		Возврат АктОтказаXML(ДокументСсылка);
		
	ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПодтвердитеАктОРасхождениях Тогда
		
		Возврат КвитанцияПодтвержденияАктаРасхожденийXML(ДокументСсылка);
		
	ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОткажитесьОтАктаОРасхождениях Тогда
		
		Возврат КвитанцияОтказаОтАктаРасхожденийXML(ДокументСсылка);
		
	ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПодтвердитеЗапросНаОтменуПроведения Тогда
		
		Возврат КвитанцияПодтверждениеЗапросаНаОтменуПроведенияXML(ДокументСсылка);
		
	ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОткажитесьОтЗапросаНаОтменуПроведения Тогда
		
		Возврат КвитанцияОтказаОтЗапросаНаОтменуПроведенияXML(ДокументСсылка);
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СообщенияЕГАИС

Функция ТТНИсходящаяЕГАИСXML(ДокументСсылка)
	
	СообщенияXML = Новый Массив;
	
	Операция = Перечисления.ВидыДокументовЕГАИС.ТТН;
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ЕГАИСПрисоединенныеФайлы.Документ      КАК Ссылка,
		|	КОЛИЧЕСТВО(ЕГАИСПрисоединенныеФайлы.Ссылка) КАК ПоследнийНомер
		|ПОМЕСТИТЬ Версии
		|ИЗ
		|	Справочник.ЕГАИСПрисоединенныеФайлы КАК ЕГАИСПрисоединенныеФайлы
		|ГДЕ
		|	ЕГАИСПрисоединенныеФайлы.Документ = &Ссылка
		|	И ЕГАИСПрисоединенныеФайлы.Операция = &Операция
		|	И ЕГАИСПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
		|СГРУППИРОВАТЬ ПО
		|	ЕГАИСПрисоединенныеФайлы.Документ
		|;
		|
		|//#РезультатЗапроса#////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Шапка.Номер                        КАК Номер,
		|	Шапка.Дата                         КАК Дата,
		|	ЕСТЬNULL(Версии.ПоследнийНомер, 0) КАК ПоследнийНомерВерсии,
		|	Шапка.Идентификатор                КАК Идентификатор,
		|	Шапка.ДокументОснование            КАК ДокументОснование,
		|	
		|	Шапка.ВидОперации                КАК ВидОперации,
		|	Шапка.Упакована                  КАК Упакована,
		|	Шапка.НомерТТН                   КАК НомерТТН,
		|	Шапка.ДатаТТН                    КАК ДатаТТН,
		|	Шапка.ДатаОтгрузки               КАК ДатаОтгрузки,
		|	Шапка.ТипДоставки                КАК ТипДоставки,
		|	Шапка.Перевозчик                 КАК Перевозчик,
		|	Шапка.ТипТранспорта              КАК ТипТранспорта,
		|	Шапка.ТоварВПутиПринадлежитГрузополучателю КАК ТоварВПутиПринадлежитГрузополучателю,
		|	Шапка.НомерТранспортногоСредства КАК НомерТранспортногоСредства,
		|	Шапка.Прицеп                     КАК Прицеп,
		|	Шапка.Заказчик                   КАК Заказчик,
		|	Шапка.Водитель                   КАК Водитель,
		|	Шапка.ПунктПогрузки              КАК ПунктПогрузки,
		|	Шапка.ПунктРазгрузки             КАК ПунктРазгрузки,
		|	Шапка.Перенаправление            КАК Перенаправление,
		|	Шапка.Экспедитор                 КАК Экспедитор,
		|	
		|	Грузоотправители.Ссылка                                         КАК ГрузоотправительСсылка,
		|	Грузоотправители.Код                                            КАК ГрузоотправительКод,
		|	ВЫРАЗИТЬ(Грузоотправители.Наименование КАК Строка(64))          КАК ГрузоотправительНаименование,
		|	ВЫРАЗИТЬ(Грузоотправители.НаименованиеПолное КАК Строка(255))   КАК ГрузоотправительНаименованиеПолное,
		|	Грузоотправители.ИНН                                            КАК ГрузоотправительИНН,
		|	Грузоотправители.КПП                                            КАК ГрузоотправительКПП,
		|	Грузоотправители.КодСтраны                                      КАК ГрузоотправительКодСтраны,
		|	Грузоотправители.КодРегиона                                     КАК ГрузоотправительКодРегиона,
		|	Грузоотправители.ПочтовыйИндекс                                 КАК ГрузоотправительПочтовыйИндекс,
		|	ВЫРАЗИТЬ(Грузоотправители.ПредставлениеАдреса КАК Строка(1000)) КАК ГрузоотправительПредставлениеАдреса,
		|	Грузоотправители.ТипОрганизации                                 КАК ГрузоотправительТипОрганизации,
		|	Грузоотправители.ИдентификаторОрганизацииТС                     КАК ГрузоотправительИдентификаторОрганизацииТС,
		|	
		|	Грузополучатели.Ссылка                                          КАК ГрузополучательСсылка,
		|	Грузополучатели.Код                                             КАК ГрузополучательКод,
		|	ВЫРАЗИТЬ(Грузополучатели.Наименование КАК Строка(64))           КАК ГрузополучательНаименование,
		|	ВЫРАЗИТЬ(Грузополучатели.НаименованиеПолное КАК Строка(255))    КАК ГрузополучательНаименованиеПолное,
		|	Грузополучатели.ИНН                                             КАК ГрузополучательИНН,
		|	Грузополучатели.КПП                                             КАК ГрузополучательКПП,
		|	Грузополучатели.КодСтраны                                       КАК ГрузополучательКодСтраны,
		|	Грузополучатели.КодРегиона                                      КАК ГрузополучательКодРегиона,
		|	Грузополучатели.ПочтовыйИндекс                                  КАК ГрузополучательПочтовыйИндекс,
		|	ВЫРАЗИТЬ(Грузополучатели.ПредставлениеАдреса КАК Строка(1000))  КАК ГрузополучательПредставлениеАдреса,
		|	Грузополучатели.ТипОрганизации                                  КАК ГрузополучательТипОрганизации,
		|	Грузополучатели.ИдентификаторОрганизацииТС                      КАК ГрузополучательИдентификаторОрганизацииТС,
		|	
		|	Поставщики.Ссылка                                               КАК ПоставщикСсылка,
		|	Поставщики.Код                                                  КАК ПоставщикКод,
		|	ВЫРАЗИТЬ(Поставщики.Наименование КАК Строка(64))                КАК ПоставщикНаименование,
		|	ВЫРАЗИТЬ(Поставщики.НаименованиеПолное КАК Строка(255))         КАК ПоставщикНаименованиеПолное,
		|	Поставщики.ИНН                                                  КАК ПоставщикИНН,
		|	Поставщики.КПП                                                  КАК ПоставщикКПП,
		|	Поставщики.КодСтраны                                            КАК ПоставщикКодСтраны,
		|	Поставщики.КодРегиона                                           КАК ПоставщикКодРегиона,
		|	Поставщики.ПочтовыйИндекс                                       КАК ПоставщикПочтовыйИндекс,
		|	ВЫРАЗИТЬ(Поставщики.ПредставлениеАдреса КАК Строка(1000))       КАК ПоставщикПредставлениеАдреса,
		|	Поставщики.ТипОрганизации                                       КАК ПоставщикТипОрганизации,
		|	Поставщики.ИдентификаторОрганизацииТС                           КАК ПоставщикИдентификаторОрганизацииТС,
		|	
		|	ВЫРАЗИТЬ(Шапка.Комментарий КАК Строка(200)) КАК Комментарий,
		|	ВЫРАЗИТЬ(Шапка.Основание КАК Строка(200))   КАК Основание,
		|	
		|	Шапка.Грузоотправитель              КАК ОрганизацияЕГАИС,
		|	Шапка.Грузоотправитель.Код          КАК ИдентификаторФСРАР,
		|	Шапка.Грузоотправитель.ФорматОбмена КАК ФорматОбменаГрузоотправителя,
		|	Шапка.Грузополучатель.ФорматОбмена  КАК ФорматОбменаГрузополучателя,
		|	Шапка.Ответственный                 КАК Ответственный
		|ИЗ
		|	Документ.ТТНИсходящаяЕГАИС КАК Шапка,
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Версии КАК Версии
		|		ПО Шапка.Ссылка = Версии.Ссылка
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК Грузополучатели
		|		ПО Грузополучатели.Ссылка = Шапка.Грузополучатель
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК Поставщики
		|		ПО Поставщики.Ссылка = Шапка.Поставщик
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК Грузоотправители
		|		ПО Грузоотправители.Ссылка = Шапка.Грузоотправитель
		|ГДЕ
		|	Шапка.Ссылка = &Ссылка
		|",
		"Шапка");
	
	ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	Товары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	Товары.Номенклатура         КАК Номенклатура,
		|	Товары.Характеристика       КАК Характеристика,
		|	Товары.Серия                КАК Серия,
		|	Товары.Справка2             КАК Справка2
		|ПОМЕСТИТЬ ВТТовары
		|ИЗ
		|	Документ.ТТНИсходящаяЕГАИС.Товары КАК Товары
		|ГДЕ
		|	Товары.Ссылка = &Ссылка");
	
	ТекстыЗапроса.Добавить(
		ИнтеграцияЕГАИС.ТекстЗапросаВТКоэффициентыПересчетаВЕдиницыЕГАИС(
			"ВТТовары",
			"ВТКоэффициентыПересчетаВЕдиницыЕГАИС"));
	
	ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.Справка2 КАК Ссылка
		|ПОМЕСТИТЬ Справки2
		|ИЗ
		|	ВТТовары КАК Товары
		|ИНДЕКСИРОВАТЬ ПО
		|	Товары.Справка2
		|;
		|
		|//#РезультатЗапроса#////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Диапазоны.Ссылка         КАК Справка2,
		|	Диапазоны.ТипМарки       КАК ТипМарки,
		|	Диапазоны.СерияМарки     КАК СерияМарки,
		|	Диапазоны.НачальныйНомер КАК НачальныйНомер,
		|	Диапазоны.КонечныйНомер  КАК КонечныйНомер
		|ИЗ
		|	Справочник.Справки2ЕГАИС.ДиапазоныНомеровАкцизныхМарок КАК Диапазоны
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справки2 КАК Справки2
		|		ПО Справки2.Ссылка = Диапазоны.Ссылка
		|ИТОГИ ПО
		|	Справка2,
		|	ТипМарки
		|",
		"Справки2");
	
	ТекстЗапросаАлкогольнаяПродукция = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.АлкогольнаяПродукция КАК АлкогольнаяПродукция
		|ПОМЕСТИТЬ АлкогольнаяПродукция
		|ИЗ
		|	ВТТовары КАК Товары
		|;
		|
		|//#РезультатЗапроса#////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	
		|	Продукция.Код                                   КАК ПродукцияКод,
		|	ВЫРАЗИТЬ(Продукция.Наименование КАК Строка(64)) КАК ПродукцияНаименование,
		|	Продукция.Объем                                 КАК ПродукцияОбъем,
		|	Продукция.Крепость                              КАК ПродукцияКрепость,
		|	Продукция.НаименованиеПолное                    КАК ПродукцияНаименованиеПолное,
		|	ЕСТЬNULL(ВидыПродукции.Код, """")               КАК ПродукцияКодВидаПродукции,
		|	Продукция.ТипПродукции                          КАК ПродукцияТипПродукции,
		|	ЕСТЬNULL(ВидыПродукции.ВидЛицензии, ЗНАЧЕНИЕ(Перечисление.ВидыЛицензийАлкогольнойПродукции.ПустаяСсылка)) КАК ПродукцияВидЛицензии,
		|	
		|	Производители.Ссылка                                         КАК ПроизводительСсылка,
		|	Производители.Код                                            КАК ПроизводительКод,
		|	ВЫРАЗИТЬ(Производители.Наименование КАК Строка(64))          КАК ПроизводительНаименование,
		|	ВЫРАЗИТЬ(Производители.НаименованиеПолное КАК Строка(255))   КАК ПроизводительНаименованиеПолное,
		|	Производители.ИНН                                            КАК ПроизводительИНН,
		|	Производители.КПП                                            КАК ПроизводительКПП,
		|	Производители.КодСтраны                                      КАК ПроизводительКодСтраны,
		|	Производители.КодРегиона                                     КАК ПроизводительКодРегиона,
		|	Производители.ПочтовыйИндекс                                 КАК ПроизводительПочтовыйИндекс,
		|	ВЫРАЗИТЬ(Производители.ПредставлениеАдреса КАК Строка(1000)) КАК ПроизводительПредставлениеАдреса,
		|	Производители.ТипОрганизации                                 КАК ПроизводительТипОрганизации,
		|	Производители.ИдентификаторОрганизацииТС                     КАК ПроизводительИдентификаторОрганизацииТС,
		|	
		|	Импортеры.Ссылка                                         КАК ИмпортерСсылка,
		|	Импортеры.Код                                            КАК ИмпортерКод,
		|	ВЫРАЗИТЬ(Импортеры.Наименование КАК Строка(64))          КАК ИмпортерНаименование,
		|	ВЫРАЗИТЬ(Импортеры.НаименованиеПолное КАК Строка(255))   КАК ИмпортерНаименованиеПолное,
		|	Импортеры.ИНН                                            КАК ИмпортерИНН,
		|	Импортеры.КПП                                            КАК ИмпортерКПП,
		|	Импортеры.КодСтраны                                      КАК ИмпортерКодСтраны,
		|	Импортеры.КодРегиона                                     КАК ИмпортерКодРегиона,
		|	Импортеры.ПочтовыйИндекс                                 КАК ИмпортерПочтовыйИндекс,
		|	ВЫРАЗИТЬ(Импортеры.ПредставлениеАдреса КАК Строка(1000)) КАК ИмпортерПредставлениеАдреса,
		|	Импортеры.ТипОрганизации                                 КАК ИмпортерТипОрганизации,
		|	Импортеры.ИдентификаторОрганизацииТС                     КАК ИмпортерИдентификаторОрганизацииТС
		|ИЗ
		|	АлкогольнаяПродукция КАК Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК Продукция
		|		ПО Продукция.Ссылка = Товары.АлкогольнаяПродукция
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыАлкогольнойПродукции КАК ВидыПродукции
		|		ПО Продукция.ВидПродукции = ВидыПродукции.Ссылка
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК Производители
		|		ПО Производители.Ссылка = Продукция.Производитель
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК Импортеры
		|		ПО Импортеры.Ссылка = Продукция.Импортер
		|";
	
	ТекстыЗапроса.Добавить(
		ТекстЗапросаАлкогольнаяПродукция,
		"АлкогольнаяПродукция");
	
	ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	Товары.НомерСтроки                      КАК НомерСтроки,
		|	Товары.АлкогольнаяПродукция             КАК АлкогольнаяПродукция,
		|	Товары.ИдентификаторУпаковки            КАК ИдентификаторУпаковки,
		|	Товары.Номенклатура                     КАК Номенклатура,
		|	Товары.Количество
		|	* ЕСТЬNULL(ЕдиницыЕГАИС.Коэффициент, 1) КАК Количество,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ЕдиницыЕГАИС.Коэффициент, 1) <> 0
		|			ТОГДА Товары.Цена / ЕСТЬNULL(ЕдиницыЕГАИС.Коэффициент, 1)
		|		ИНАЧЕ Товары.Цена
		|	КОНЕЦ КАК Цена,
		|	Товары.Сумма                            КАК Сумма,
		|	Товары.НомерПартии                      КАК НомерПартии,
		|	Товары.Справка2                         КАК Справка2,
		|	Товары.Справка2.НомерСправки1           КАК НомерСправки1,
		|	Товары.Справка2.РегистрационныйНомер    КАК НомерСправки2,
		|	
		|	ЕСТЬNULL(ЕдиницыЕГАИС.ПроверятьОбъемДАЛ, ЛОЖЬ) КАК ПроверятьОбъемДАЛ,
		|	ЕСТЬNULL(ЕдиницыЕГАИС.ОбъемДАЛ, 0)             КАК ОбъемДАЛ
		|ИЗ
		|	Документ.ТТНИсходящаяЕГАИС.Товары КАК Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКоэффициентыПересчетаВЕдиницыЕГАИС КАК ЕдиницыЕГАИС
		|		ПО ЕдиницыЕГАИС.АлкогольнаяПродукция = Товары.АлкогольнаяПродукция
		|		 И ЕдиницыЕГАИС.Номенклатура = Товары.Номенклатура
		|		 И ЕдиницыЕГАИС.Характеристика = Товары.Характеристика
		|		 И ЕдиницыЕГАИС.Серия = Товары.Серия
		|ГДЕ
		|	Товары.Ссылка = &Ссылка
		|",
		"Товары");
	
	ПараметрыФормированияТекстаЗапроса = ШтрихкодированиеЕГАИС.ПараметрыФормированияТекстаЗапросаВложенныхШтрихкодов();
	ПараметрыФормированияТекстаЗапроса.ДокументСсылка                  = ДокументСсылка;
	ПараметрыФормированияТекстаЗапроса.ИспользоватьИдентификаторСтроки = Ложь;
	ПараметрыФормированияТекстаЗапроса.ИмяПоляОрганизацияЕГАИС         = "Грузоотправитель";
	ТекстыЗапроса.Добавить(
		ШтрихкодированиеЕГАИС.ТекстЗапросаВложенныхШтрихкодовПоДокументу(ПараметрыФормированияТекстаЗапроса),
		"ВложенныеШтрихкоды");
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка",   ДокументСсылка);
	Запрос.УстановитьПараметр("Операция", Операция);
	Запрос.УстановитьПараметр("ПустыеЗначенияНоменклатуры", ИнтеграцияИС.НезаполненныеЗначенияОпределяемогоТипа("Номенклатура"));
	РезультатыЗапроса = ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, ТекстыЗапроса);
	
	//@skip-warning
	Шапка                = РезультатыЗапроса["Шапка"].Выбрать();
	//@skip-warning
	ВыборкаПоСправкам2   = РезультатыЗапроса["Справки2"].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	//@skip-warning
	АлкогольнаяПродукция = РезультатыЗапроса["АлкогольнаяПродукция"].Выгрузить();
	//@skip-warning
	Товары               = РезультатыЗапроса["Товары"].Выгрузить();
	
	Если Не Шапка.Следующий()
		Или Товары.Количество() = 0 Тогда
		
		СообщениеXML = ОбменДаннымиЕГАИС.СтруктураСообщенияXML();
		СообщениеXML.Документ = ДокументСсылка;
		СообщениеXML.Описание = ОбменДаннымиЕГАИС.ОписаниеОперацииПередачиДанных(
			Операция, ДокументСсылка);
		
		ОбменДаннымиЕГАИСКлиентСервер.ДобавитьТекстОшибки(СообщениеXML, НСтр("ru = 'Нет данных для выгрузки.'"));
		
		СообщенияXML.Добавить(СообщениеXML);
		Возврат СообщенияXML;
		
	КонецЕсли;
	
	НомерВерсии = Шапка.ПоследнийНомерВерсии + 1;
	ФорматОбмена = ФорматОбмена(Шапка);
	
	СообщениеXML = ОбменДаннымиЕГАИС.СтруктураСообщенияXML();
	СообщениеXML.Описание = ОбменДаннымиЕГАИС.ОписаниеОперацииПередачиДанных(
		Операция, ДокументСсылка, НомерВерсии);
	
	ПространствоИмен = Перечисления.ВидыДокументовЕГАИС.ПространствоИмен(Операция, ФорматОбмена);
	ИмяТипа          = Перечисления.ВидыДокументовЕГАИС.ТипЕГАИС(Операция, ФорматОбмена);
	
	Если ПространствоИмен = Неопределено
		Или ИмяТипа = Неопределено Тогда
		ОбменДаннымиЕГАИСКлиентСервер.ДобавитьТекстОшибки(
			СообщениеXML,
			СтрШаблон(НСтр("ru = 'Операция не поддерживается в версии формата обмена: %1.'"), ФорматОбмена));
		СообщенияXML.Добавить(СообщениеXML);
		Возврат СообщенияXML;
	КонецЕсли;
	
	#Область ПодготовкаДанных
	
	ОписаниеНоменклатуры = ОбщегоНазначенияИС.ОписаниеНоменклатуры(Товары.ВыгрузитьКолонку("Номенклатура"));
	
	ТоварыИтоги = Новый ТаблицаЗначений;
	ТоварыИтоги.Колонки.Добавить("АлкогольнаяПродукция");
	ТоварыИтоги.Колонки.Добавить("ИдентификаторУпаковки");
	ТоварыИтоги.Колонки.Добавить("НомерПартии");
	ТоварыИтоги.Колонки.Добавить("НомерСправки1");
	ТоварыИтоги.Колонки.Добавить("НомерСправки2");
	ТоварыИтоги.Колонки.Добавить("Справка2");
	ТоварыИтоги.Колонки.Добавить("Количество");
	ТоварыИтоги.Колонки.Добавить("Цена");
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		
		Если СтрокаТЧ.ПроверятьОбъемДАЛ
			И Не ЗначениеЗаполнено(СтрокаТЧ.ОбъемДАЛ) Тогда
			ОбменДаннымиЕГАИСКлиентСервер.ДобавитьТекстОшибки(СообщениеXML, СтрШаблон(НСтр("ru = 'Для номенклатуры в строке %1 не установлен объем в декалитрах.'"), СтрокаТЧ.НомерСтроки));
		КонецЕсли;
		
		ДанныеОписанияНоменклатуры = ОписаниеНоменклатуры[СтрокаТЧ.Номенклатура];
		Если ДанныеОписанияНоменклатуры = Неопределено
			Или ДанныеОписанияНоменклатуры.КоличествоВПотребительскойУпаковке = 0 Тогда
			КоличествоВПотребительскойУпаковке = 1;
		Иначе
			КоличествоВПотребительскойУпаковке = ДанныеОписанияНоменклатуры.КоличествоВПотребительскойУпаковке;
		КонецЕсли;
		
		НоваяСтрока = ТоварыИтоги.Добавить();
		НоваяСтрока.АлкогольнаяПродукция  = СтрокаТЧ.АлкогольнаяПродукция;
		НоваяСтрока.ИдентификаторУпаковки = СтрокаТЧ.ИдентификаторУпаковки;
		НоваяСтрока.НомерПартии           = СтрокаТЧ.НомерПартии;
		НоваяСтрока.НомерСправки1         = СтрокаТЧ.НомерСправки1;
		НоваяСтрока.НомерСправки2         = СтрокаТЧ.НомерСправки2;
		НоваяСтрока.Справка2              = СтрокаТЧ.Справка2;
		Если СтрокаТЧ.ПроверятьОбъемДАЛ Тогда
			НоваяСтрока.Количество = СтрокаТЧ.Количество;
			НоваяСтрока.Цена       = СтрокаТЧ.Цена;
		Иначе
			НоваяСтрока.Количество = Окр(СтрокаТЧ.Количество / КоличествоВПотребительскойУпаковке, 0);
			НоваяСтрока.Цена       = Окр(СтрокаТЧ.Сумма / НоваяСтрока.Количество, 2);
		КонецЕсли;
		
	КонецЦикла;
	
	ТоварыИтоги.Свернуть(
		"АлкогольнаяПродукция,
		|ИдентификаторУпаковки,
		|НомерПартии,
		|НомерСправки1,
		|НомерСправки2,
		|Справка2,
		|Цена",
		"Количество");
	
	#КонецОбласти
	
	#Область ТТН
	
	ТипыТТН = Новый Соответствие;
	ТипыТТН.Вставить(Перечисления.ВидыОперацийТТНИсходящейЕГАИС.РасходнаяНакладная, "WBInvoiceFromMe");
	ТипыТТН.Вставить(Перечисления.ВидыОперацийТТНИсходящейЕГАИС.ВозвратПоставщику, "WBReturnFromMe");
	
	Если ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V1 Тогда
		
		#Область ФорматОбмена_V1
		
		ДокументXDTO = РаботаСXMLЕГАИС.ОбъектXDTO(ПространствоИмен, "WayBillType");
		
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO, "Identity", Шапка.Идентификатор, СообщениеXML, 3);
		
		ДокументXDTO.Header = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(ДокументXDTO, "Header");
		
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO.Header, "Type",         ТипыТТН[Шапка.ВидОперации],               СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO.Header, "UnitType",     ?(Шапка.Упакована, "Packed", "Unpacked"), СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO.Header, "NUMBER",       СокрЛП(Шапка.НомерТТН),                   СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO.Header, "Date",         Шапка.ДатаТТН,                            СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO.Header, "ShippingDate", Шапка.ДатаОтгрузки,                       СообщениеXML);
		
		ДокументXDTO.Header.Transport = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(ДокументXDTO.Header, "Transport");
		ИнтеграцияЕГАИС.ЗаполнитьВXDTOТранспортныйРазделТТН(ДокументXDTO.Header.Transport, Шапка, СообщениеXML);
		
		ИнтеграцияЕГАИС.ЗаполнитьВXDTOОрганизацию_v1(ДокументXDTO.Header, "Shipper",   Шапка, "Грузоотправитель", СообщениеXML);
		ИнтеграцияЕГАИС.ЗаполнитьВXDTOОрганизацию_v1(ДокументXDTO.Header, "Consignee", Шапка, "Грузополучатель",  СообщениеXML);
		ИнтеграцияЕГАИС.ЗаполнитьВXDTOОрганизацию_v1(ДокументXDTO.Header, "Supplier",  Шапка, "Поставщик",        СообщениеXML);
		
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO.Header, "Base", Шапка.Основание,   СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO.Header, "Note", Шапка.Комментарий, СообщениеXML);
		
		ДокументXDTO.Content = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(ДокументXDTO, "Content");
		
		Для Каждого СтрокаТЧ Из ТоварыИтоги Цикл
			
			НомерСтроки = Формат(ТоварыИтоги.Индекс(СтрокаТЧ) + 1, "ЧГ=0");
			
			Position = РаботаСXMLЕГАИС.ОбъектXDTO(ПространствоИмен, "PositionType");
			
			Position.Product = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(Position, "Product");
			
			ИнтеграцияЕГАИС.ЗаполнитьВXDTOАлкогольнуюПродукцию_v1(
				Position.Product,
				АлкогольнаяПродукция.Найти(СтрокаТЧ.АлкогольнаяПродукция, "АлкогольнаяПродукция"),
				"Продукция", СообщениеXML);
			
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Pack_ID",  СтрокаТЧ.ИдентификаторУпаковки, СообщениеXML);
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Quantity", СтрокаТЧ.Количество,            СообщениеXML);
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Price"   , СтрокаТЧ.Цена,                  СообщениеXML);
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Party",    СтрокаТЧ.НомерПартии,           СообщениеXML);
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Identity", НомерСтроки,                    СообщениеXML, 5);
			
			Position.InformA = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(Position, "InformA");
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position.InformA, "RegId", СтрокаТЧ.НомерСправки1, СообщениеXML);
			
			Position.InformB = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(Position, "InformB");
			Position.InformB.InformBItem = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(Position.InformB, "InformBItem");
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position.InformB.InformBItem, "BRegId", СтрокаТЧ.НомерСправки2, СообщениеXML);
			
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("Справка2", СтрокаТЧ.Справка2);
			ВыборкаПоСправкам2.Сбросить();
			Если ВыборкаПоСправкам2.НайтиСледующий(СтруктураПоиска) Тогда
				
				ВыборкаПоТипам = ВыборкаПоСправкам2.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаПоТипам.Следующий() Цикл
					
					Position.InformB.InformBItem.MarkInfo = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(Position.InformB.InformBItem, "MarkInfo");
					РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position.InformB.InformBItem.MarkInfo, "Type", ВыборкаПоТипам.ТипМарки, СообщениеXML);
					Position.InformB.InformBItem.MarkInfo.Ranges = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(Position.InformB.InformBItem.MarkInfo, "Ranges");
					
					ВыборкаПоДиапазонам = ВыборкаПоТипам.Выбрать();
					Пока ВыборкаПоДиапазонам.Следующий() Цикл
						
						Range = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(Position.InformB.InformBItem.MarkInfo.Ranges, "Range");
						РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Range, "Rank",  ВыборкаПоДиапазонам.СерияМарки,             СообщениеXML);
						РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Range, "Start", СокрЛП(ВыборкаПоДиапазонам.НачальныйНомер), СообщениеXML);
						РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Range, "Last",  СокрЛП(ВыборкаПоДиапазонам.КонечныйНомер),  СообщениеXML);
						Position.InformB.InformBItem.MarkInfo.Ranges.Range.Добавить(Range);
						
					КонецЦикла;
					
				КонецЦикла;
				
			КонецЕсли;
			
			ДокументXDTO.Content.Position.Добавить(Position);
			
		КонецЦикла;
		
		#КонецОбласти
		
	ИначеЕсли ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V2 Тогда
		
		#Область ФорматОбмена_V2
		
		ДокументXDTO = РаботаСXMLЕГАИС.ОбъектXDTO(ПространствоИмен, "WayBillType_v2");
		
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO, "Identity", Шапка.Идентификатор, СообщениеXML, 3);
		
		ДокументXDTO.Header = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(ДокументXDTO, "Header");
		
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO.Header, "Type",         ТипыТТН[Шапка.ВидОперации], СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO.Header, "NUMBER",       СокрЛП(Шапка.НомерТТН),     СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO.Header, "Date",         Шапка.ДатаТТН,              СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO.Header, "ShippingDate", Шапка.ДатаОтгрузки,         СообщениеXML);
		
		ДокументXDTO.Header.Transport = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(ДокументXDTO.Header, "Transport");
		ИнтеграцияЕГАИС.ЗаполнитьВXDTOТранспортныйРазделТТН(ДокументXDTO.Header.Transport, Шапка, СообщениеXML);
		
		ИнтеграцияЕГАИС.ЗаполнитьВXDTOОрганизацию_v2(ДокументXDTO.Header, "Shipper",   Шапка, "Грузоотправитель", СообщениеXML);
		ИнтеграцияЕГАИС.ЗаполнитьВXDTOОрганизацию_v2(ДокументXDTO.Header, "Consignee", Шапка, "Грузополучатель",  СообщениеXML);
		
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO.Header, "Base", Шапка.Основание,   СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO.Header, "Note", Шапка.Комментарий, СообщениеXML);
		
		ДокументXDTO.Content = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(ДокументXDTO, "Content");
		
		Для Каждого СтрокаТЧ Из ТоварыИтоги Цикл
			
			НомерСтроки = Формат(ТоварыИтоги.Индекс(СтрокаТЧ) + 1, "ЧГ=0");
			
			Position = РаботаСXMLЕГАИС.ОбъектXDTO(ПространствоИмен, "PositionType");
			
			Position.Product = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(Position, "Product");
			
			ИнтеграцияЕГАИС.ЗаполнитьВXDTOАлкогольнуюПродукцию_v2(
				Position.Product,
				АлкогольнаяПродукция.Найти(СтрокаТЧ.АлкогольнаяПродукция, "АлкогольнаяПродукция"),
				"Продукция", СообщениеXML);
			
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Pack_ID",  СтрокаТЧ.ИдентификаторУпаковки, СообщениеXML);
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Quantity", СтрокаТЧ.Количество,            СообщениеXML);
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Price"   , СтрокаТЧ.Цена,                  СообщениеXML);
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Party",    СтрокаТЧ.НомерПартии,           СообщениеXML);
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Identity", НомерСтроки,                    СообщениеXML, 5);
			
			Position.InformF1 = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(Position, "InformF1");
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position.InformF1, "RegId", СтрокаТЧ.НомерСправки1, СообщениеXML);
			
			Position.InformF2 = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(Position, "InformF2");
			Position.InformF2.InformF2Item = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(Position.InformF2, "InformF2Item");
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position.InformF2.InformF2Item, "F2RegId", СтрокаТЧ.НомерСправки2, СообщениеXML);
			
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("Справка2", СтрокаТЧ.Справка2);
			ВыборкаПоСправкам2.Сбросить();
			Если ВыборкаПоСправкам2.НайтиСледующий(СтруктураПоиска) Тогда
				
				ВыборкаПоТипам = ВыборкаПоСправкам2.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаПоТипам.Следующий() Цикл
					
					Position.InformF2.InformF2Item.MarkInfo = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(Position.InformF2.InformF2Item, "MarkInfo");
					РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position.InformF2.InformF2Item.MarkInfo, "Type", ВыборкаПоТипам.ТипМарки, СообщениеXML);
					Position.InformF2.InformF2Item.MarkInfo.Ranges = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(Position.InformF2.InformF2Item.MarkInfo, "Ranges");
					
					ВыборкаПоДиапазонам = ВыборкаПоТипам.Выбрать();
					Пока ВыборкаПоДиапазонам.Следующий() Цикл
						
						Range = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(Position.InformF2.InformF2Item.MarkInfo.Ranges, "Range");
						РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Range, "Rank",  ВыборкаПоДиапазонам.СерияМарки,             СообщениеXML);
						РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Range, "Start", СокрЛП(ВыборкаПоДиапазонам.НачальныйНомер), СообщениеXML);
						РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Range, "Last",  СокрЛП(ВыборкаПоДиапазонам.КонечныйНомер),  СообщениеXML);
						Position.InformF2.InformF2Item.MarkInfo.Ranges.Range.Добавить(Range);
						
					КонецЦикла;
					
				КонецЦикла;
				
			КонецЕсли;
			
			ДокументXDTO.Content.Position.Добавить(Position);
			
		КонецЦикла;
		
		#КонецОбласти
		
	ИначеЕсли ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V3
		Или ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V4 Тогда
		
		#Область ПодготовкаДанных
		
		АкцизныеМарки = Новый ТаблицаЗначений;
		АкцизныеМарки.Колонки.Добавить("Справка2");
		АкцизныеМарки.Колонки.Добавить("ШтрихкодУпаковки");
		АкцизныеМарки.Колонки.Добавить("КодАкцизнойМарки");
		АкцизныеМарки.Колонки.Добавить("Родитель");
		
		//@skip-warning
		Выборка = РезультатыЗапроса["ВложенныеШтрихкоды"].Выбрать();
		ВложенныеШтрихкодыУпаковок = ШтрихкодированиеЕГАИС.ВложенныеШтрихкодыУпаковокПоВыборкеИМенеджеруВТ(
			Выборка, МенеджерВременныхТаблиц);
		
		Для Каждого СтрокаТЧ Из ВложенныеШтрихкодыУпаковок.МаркированныеТовары Цикл
			
			Если Не ЗначениеЗаполнено(СтрокаТЧ.Статус) Тогда
				Продолжить;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаТЧ.Справка2) Тогда
				ОбменДаннымиЕГАИСКлиентСервер.ДобавитьТекстОшибки(
					СообщениеXML,
					СтрШаблон(НСтр("ru = 'Для акцизной марки %1 не определена справка 2.'"), СтрокаТЧ.Штрихкод));
				Продолжить;
			ИначеЕсли СтрокаТЧ.Статус <> Перечисления.СтатусыАкцизныхМарок.ВНаличии Тогда
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Акцизная марка %1 со статусом %2 не может быть отгружена.'"),
					СтрокаТЧ.Штрихкод, 
					?(ЗначениеЗаполнено(СтрокаТЧ.Статус), СтрокаТЧ.Статус, НСтр("ru = '<статус отсутствует>'")));
				ОбменДаннымиЕГАИСКлиентСервер.ДобавитьТекстОшибки(
					СообщениеXML, ТекстСообщения);
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = АкцизныеМарки.Добавить();
			НоваяСтрока.Справка2 = СтрокаТЧ.Справка2;
			Если СтрокаТЧ.СтрокаДерева.Родитель = Неопределено Тогда
				НоваяСтрока.ШтрихкодУпаковки = "";
			Иначе
				НоваяСтрока.ШтрихкодУпаковки = СтрокаТЧ.СтрокаДерева.Родитель.Штрихкод;
			КонецЕсли;
			НоваяСтрока.КодАкцизнойМарки = СтрокаТЧ.Штрихкод;
			НоваяСтрока.Родитель         = СтрокаТЧ.СтрокаДерева.Родитель;
			
		КонецЦикла;
		
		АкцизныеМарки.Сортировать("Справка2, ШтрихкодУпаковки");
		
		#КонецОбласти
		
		#Область ФорматОбмена_V3_V4
		
		Если ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V3 Тогда
			ДокументXDTO = РаботаСXMLЕГАИС.ОбъектXDTO(ПространствоИмен, "WayBillType_v3");
		ИначеЕсли ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V4 Тогда
			ДокументXDTO = РаботаСXMLЕГАИС.ОбъектXDTO(ПространствоИмен, "WayBillType_v4");
		КонецЕсли;
		
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO, "Identity", Шапка.Идентификатор, СообщениеXML, 3);
		
		ДокументXDTO.Header = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(ДокументXDTO, "Header");
		
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO.Header, "Type",         ТипыТТН[Шапка.ВидОперации], СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO.Header, "NUMBER",       СокрЛП(Шапка.НомерТТН),     СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO.Header, "Date",         Шапка.ДатаТТН,              СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO.Header, "ShippingDate", Шапка.ДатаОтгрузки,         СообщениеXML);
		
		ТранспортныйРаздел = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(ДокументXDTO.Header, "Transport");
		ТранспортныйРазделЗаполнен = Ложь;
		Если ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V3 Тогда
			ТранспортныйРазделЗаполнен = ИнтеграцияЕГАИС.ЗаполнитьВXDTOТранспортныйРазделТТН(ТранспортныйРаздел, Шапка, СообщениеXML);
		ИначеЕсли ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V4 Тогда
			ТранспортныйРазделЗаполнен = ИнтеграцияЕГАИС.ЗаполнитьВXDTOТранспортныйРазделТТН_v4(ТранспортныйРаздел, Шапка, СообщениеXML);
		КонецЕсли;
		Если ТранспортныйРазделЗаполнен Тогда
			ДокументXDTO.Header.Transport = ТранспортныйРаздел;
		КонецЕсли;
		
		ИнтеграцияЕГАИС.ЗаполнитьВXDTOОрганизацию_v2(ДокументXDTO.Header, "Shipper",   Шапка, "Грузоотправитель", СообщениеXML);
		ИнтеграцияЕГАИС.ЗаполнитьВXDTOОрганизацию_v2(ДокументXDTO.Header, "Consignee", Шапка, "Грузополучатель",  СообщениеXML);
		
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO.Header, "Base", Шапка.Основание,   СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(ДокументXDTO.Header, "Note", Шапка.Комментарий, СообщениеXML);
		
		ДокументXDTO.Content = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(ДокументXDTO, "Content");
		
		Для Каждого СтрокаТЧ Из ТоварыИтоги Цикл
			
			НомерСтроки = Формат(ТоварыИтоги.Индекс(СтрокаТЧ) + 1, "ЧГ=0");
			
			Position = РаботаСXMLЕГАИС.ОбъектXDTO(ПространствоИмен, "PositionType");
			
			Position.Product = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(Position, "Product");
			
			ИнтеграцияЕГАИС.ЗаполнитьВXDTOАлкогольнуюПродукцию_v2(
				Position.Product,
				АлкогольнаяПродукция.Найти(СтрокаТЧ.АлкогольнаяПродукция, "АлкогольнаяПродукция"),
				"Продукция", СообщениеXML);
			
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Pack_ID",  СтрокаТЧ.ИдентификаторУпаковки, СообщениеXML);
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Quantity", СтрокаТЧ.Количество,            СообщениеXML);
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Price"   , СтрокаТЧ.Цена,                  СообщениеXML);
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Party",    СтрокаТЧ.НомерПартии,           СообщениеXML);
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Identity", НомерСтроки,                    СообщениеXML, 5);
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position, "FARegId",  СтрокаТЧ.НомерСправки1,         СообщениеXML);
			
			Position.InformF2 = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(Position, "InformF2");
			РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(Position.InformF2, "F2RegId", СтрокаТЧ.НомерСправки2, СообщениеXML);
			
			#Область АкцизныеМарки
			
			ИспользованныеУзлыДереваУпаковок = Новый Соответствие;
			
			НайденныеАкцизныеМарки  = АкцизныеМарки.НайтиСтроки(Новый Структура("Справка2", СтрокаТЧ.Справка2));
			ТекущийШтрихкодУпаковки = Неопределено;
			boxpos                  = Неопределено;
			
			Если НайденныеАкцизныеМарки.Количество() > 0 Тогда
				
				Если НайденныеАкцизныеМарки.Количество() > СтрокаТЧ.Количество Тогда
					ОбменДаннымиЕГАИСКлиентСервер.ДобавитьТекстОшибки(
						СообщениеXML,
						СтрШаблон(
							НСтр("ru = 'По алкогольной продукции %1 (номер справки 2: %2) количество акцизных марок (%3) превышает количество в ТТН (%4).'"),
							СтрокаТЧ.АлкогольнаяПродукция,
							СтрокаТЧ.НомерСправки2,
							НайденныеАкцизныеМарки.Количество(),
							СтрокаТЧ.Количество));
				КонецЕсли;
				
				Position.InformF2.MarkInfo = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(Position.InformF2, "MarkInfo");
				
				ТребуетсяОписаниеИерархииУпаковок = Ложь;
				Для Каждого НайденнаяАкцизнаяМарка Из НайденныеАкцизныеМарки Цикл
					
					Если ТекущийШтрихкодУпаковки = Неопределено
						Или ТекущийШтрихкодУпаковки <> НайденнаяАкцизнаяМарка.ШтрихкодУпаковки Тогда
						
						ТекущийШтрихкодУпаковки = НайденнаяАкцизнаяМарка.ШтрихкодУпаковки;
						
						Если boxpos <> Неопределено Тогда
							Position.InformF2.MarkInfo.boxpos.Добавить(boxpos);
						КонецЕсли;
						
						boxpos = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(Position.InformF2.MarkInfo, "boxpos");
						boxpos.amclist = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(boxpos, "amclist");
						
						Если Не ПустаяСтрока(НайденнаяАкцизнаяМарка.ШтрихкодУпаковки) Тогда
							
							ТекущийРодитель = НайденнаяАкцизнаяМарка.Родитель;
							Если ИспользованныеУзлыДереваУпаковок.Получить(ТекущийРодитель) = Неопределено Тогда
								ИспользованныеУзлыДереваУпаковок.Вставить(ТекущийРодитель, Ложь);
							КонецЕсли;
							
							Пока ТекущийРодитель <> Неопределено Цикл
								
								Если ТекущийРодитель.Родитель <> Неопределено Тогда
									ИспользованныеУзлыДереваУпаковок.Вставить(ТекущийРодитель.Родитель, Истина);
									ТребуетсяОписаниеИерархииУпаковок = Истина;
								КонецЕсли;
								ТекущийРодитель = ТекущийРодитель.Родитель;
								
							КонецЦикла;
							
							РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(boxpos, "boxnumber", НайденнаяАкцизнаяМарка.ШтрихкодУпаковки, СообщениеXML);
							
						КонецЕсли;
						
					КонецЕсли;
					
					boxpos.amclist.amc.Добавить(НайденнаяАкцизнаяМарка.КодАкцизнойМарки);
					
				КонецЦикла;
				
				Если boxpos <> Неопределено Тогда
					Position.InformF2.MarkInfo.boxpos.Добавить(boxpos);
				КонецЕсли;
				
				Если ТребуетсяОписаниеИерархииУпаковок Тогда
					
					boxInfo = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(Position, "boxInfo");
					
					ТребуетсяОписаниеИерархииУпаковок = Ложь;
					Для Каждого СтрокаДерева Из ВложенныеШтрихкодыУпаковок.ДеревоУпаковок.Строки Цикл
						
						//Бутылки без упаковки И коробки без иерархии
						Если ИспользованныеУзлыДереваУпаковок.Получить(СтрокаДерева) <> Истина Тогда
							Продолжить;
						КонецЕсли;
						
						boxtree = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(boxInfo, "boxtree");
						boxtree.boxnum.Добавить(СтрокаДерева.Штрихкод);
						
						ПостроитьBoxInfo(СтрокаДерева, ИспользованныеУзлыДереваУпаковок, boxtree);
						
						boxInfo.boxtree.Добавить(boxtree);
						
					КонецЦикла;
					
					Position.boxInfo = boxInfo;
					
				КонецЕсли;
				
			КонецЕсли;
			
			#КонецОбласти
			
			ДокументXDTO.Content.Position.Добавить(Position);
			
		КонецЦикла;
		
		#КонецОбласти
		
	КонецЕсли;
	
	#КонецОбласти
	
	ТекстСообщенияXML = РаботаСXMLЕГАИС.ОбъектXDTOВXML(ДокументXDTO, Шапка.ИдентификаторФСРАР, ПространствоИмен, ИмяТипа);
	
	СообщениеXML.ТекстСообщенияXML = ТекстСообщенияXML;
	СообщениеXML.ТипСообщения      = Перечисления.ТипыЗапросовИС.Исходящий;
	СообщениеXML.ОрганизацияЕГАИС  = Шапка.ОрганизацияЕГАИС;
	СообщениеXML.Операция          = Операция;
	СообщениеXML.ФорматОбмена      = ФорматОбмена;
	СообщениеXML.Документ          = ДокументСсылка;
	СообщениеXML.ДокументОснование = Шапка.ДокументОснование;
	СообщениеXML.Версия            = НомерВерсии;
	
	СообщенияXML.Добавить(СообщениеXML);
	
	Возврат СообщенияXML;
	
КонецФункции

Функция АктОтказаXML(ДокументСсылка)
	
	СообщенияXML = Новый Массив;
	
	Операция = Перечисления.ВидыДокументовЕГАИС.АктТТНОтказ;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЕГАИСПрисоединенныеФайлы.Документ      КАК Ссылка,
	|	КОЛИЧЕСТВО(ЕГАИСПрисоединенныеФайлы.Ссылка) КАК ПоследнийНомер
	|ПОМЕСТИТЬ Версии
	|ИЗ
	|	Справочник.ЕГАИСПрисоединенныеФайлы КАК ЕГАИСПрисоединенныеФайлы
	|ГДЕ
	|	ЕГАИСПрисоединенныеФайлы.Документ = &Ссылка
	|	И ЕГАИСПрисоединенныеФайлы.Операция = &Операция
	|	И ЕГАИСПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
	|СГРУППИРОВАТЬ ПО
	|	ЕГАИСПрисоединенныеФайлы.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Шапка.Номер                        КАК Номер,
	|	Шапка.Дата                         КАК Дата,
	|	ЕСТЬNULL(Версии.ПоследнийНомер, 0) КАК ПоследнийНомерВерсии,
	|	Шапка.ДокументОснование            КАК ДокументОснование,
	|	
	|	Шапка.ИдентификаторЕГАИС   КАК ИдентификаторЕГАИС,
	|	ВЫРАЗИТЬ(Шапка.Комментарий КАК Строка(500)) КАК Комментарий,
	|	
	|	Шапка.Грузоотправитель              КАК ОрганизацияЕГАИС,
	|	Шапка.Грузоотправитель.Код          КАК ИдентификаторФСРАР,
	|	Шапка.Грузоотправитель.ФорматОбмена КАК ФорматОбменаГрузоотправителя,
	|	Шапка.Грузополучатель.ФорматОбмена  КАК ФорматОбменаГрузополучателя,
	|	Шапка.Ответственный                 КАК Ответственный
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК Шапка,
	|		ЛЕВОЕ СОЕДИНЕНИЕ Версии КАК Версии
	|		ПО Шапка.Ссылка = Версии.Ссылка
	|ГДЕ
	|	Шапка.Ссылка = &Ссылка
	|");
	
	Запрос.УстановитьПараметр("Ссылка",   ДокументСсылка);
	Запрос.УстановитьПараметр("Операция", Операция);
	
	Шапка = Запрос.Выполнить().Выбрать();
	
	Если Не Шапка.Следующий() Тогда
		
		СообщениеXML = ОбменДаннымиЕГАИС.СтруктураСообщенияXML();
		СообщениеXML.Документ = ДокументСсылка;
		СообщениеXML.Описание = ОбменДаннымиЕГАИС.ОписаниеОперацииПередачиДанных(
			Операция, ДокументСсылка);
		
		ОбменДаннымиЕГАИСКлиентСервер.ДобавитьТекстОшибки(СообщениеXML, НСтр("ru = 'Нет данных для выгрузки.'"));
		
		СообщенияXML.Добавить(СообщениеXML);
		Возврат СообщенияXML;
		
	КонецЕсли;
	
	НомерВерсии = Шапка.ПоследнийНомерВерсии + 1;
	ФорматОбмена = ФорматОбмена(Шапка);
	
	СообщениеXML = ОбменДаннымиЕГАИС.СтруктураСообщенияXML();
	СообщениеXML.Описание = ОбменДаннымиЕГАИС.ОписаниеОперацииПередачиДанных(
		Операция, ДокументСсылка, НомерВерсии);
	
	ПространствоИмен = Перечисления.ВидыДокументовЕГАИС.ПространствоИмен(Операция, ФорматОбмена);
	ИмяТипа          = Перечисления.ВидыДокументовЕГАИС.ТипЕГАИС(Операция, ФорматОбмена);
	
	Если ПространствоИмен = Неопределено
		Или ИмяТипа = Неопределено Тогда
		ОбменДаннымиЕГАИСКлиентСервер.ДобавитьТекстОшибки(
			СообщениеXML,
			СтрШаблон(НСтр("ru = 'Операция не поддерживается в версии формата обмена: %1.'"), ФорматОбмена));
		СообщенияXML.Добавить(СообщениеXML);
		Возврат СообщенияXML;
	КонецЕсли;
	
	#Область АктТТНОтказ
	
	Если ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V1 Тогда
		
		#Область ФорматОбмена_V1
		
		АктXDTO = РаботаСXMLЕГАИС.ОбъектXDTO(ПространствоИмен, "WayBillActType");
		
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO, "Identity", ИнтеграцияЕГАИС.НовыйИдентификаторДокумента(ДокументСсылка, "cwb"), СообщениеXML, 3);
		
		АктXDTO.Header = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(АктXDTO, "Header");
		
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "IsAccept",  "Rejected",               СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "ACTNUMBER", СокрЛП(Шапка.Номер),      СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "ActDate",   ТекущаяДатаСеанса(),      СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "WBRegId",   Шапка.ИдентификаторЕГАИС, СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "Note",      Шапка.Комментарий,        СообщениеXML);
		
		#КонецОбласти
		
	ИначеЕсли ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V2 Тогда
		
		#Область ФорматОбмена_V2
		
		АктXDTO = РаботаСXMLЕГАИС.ОбъектXDTO(ПространствоИмен, "WayBillActType_v2");
		
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO, "Identity", ИнтеграцияЕГАИС.НовыйИдентификаторДокумента(ДокументСсылка, "cwb"), СообщениеXML, 3);
		
		АктXDTO.Header = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(АктXDTO, "Header");
		
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "IsAccept",  "Rejected",               СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "ACTNUMBER", СокрЛП(Шапка.Номер),      СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "ActDate",   ТекущаяДатаСеанса(),      СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "WBRegId",   Шапка.ИдентификаторЕГАИС, СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "Note",      Шапка.Комментарий,        СообщениеXML);
		
		#КонецОбласти
		
	Иначе
		
		Если ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V3 Тогда
			АктXDTO = РаботаСXMLЕГАИС.ОбъектXDTO(ПространствоИмен, "WayBillActType_v3");
		Иначе
			АктXDTO = РаботаСXMLЕГАИС.ОбъектXDTO(ПространствоИмен, "WayBillActType_v4");
		КонецЕсли;
		
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO, "Identity", ИнтеграцияЕГАИС.НовыйИдентификаторДокумента(ДокументСсылка, "cwb"), СообщениеXML, 3);
		
		АктXDTO.Header = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(АктXDTO, "Header");
		
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "IsAccept",  "Rejected",               СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "ACTNUMBER", СокрЛП(Шапка.Номер),      СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "ActDate",   ТекущаяДатаСеанса(),      СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "WBRegId",   Шапка.ИдентификаторЕГАИС, СообщениеXML);
		РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "Note",      Шапка.Комментарий,        СообщениеXML);
		
		Если ФорматОбмена <> Перечисления.ФорматыОбменаЕГАИС.V3 Тогда
			
			АктXDTO.Transport = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(АктXDTO, "Transport");
			АктXDTO.Transport.ChangeOwnership = "NotChange";
			
		КонецЕсли;
		
	КонецЕсли;
	
	#КонецОбласти
	
	ТекстСообщенияXML = РаботаСXMLЕГАИС.ОбъектXDTOВXML(АктXDTO, Шапка.ИдентификаторФСРАР, ПространствоИмен, ИмяТипа);
	
	СообщениеXML.ТекстСообщенияXML = ТекстСообщенияXML;
	СообщениеXML.ТипСообщения      = Перечисления.ТипыЗапросовИС.Исходящий;
	СообщениеXML.ОрганизацияЕГАИС  = Шапка.ОрганизацияЕГАИС;
	СообщениеXML.Операция          = Операция;
	СообщениеXML.ФорматОбмена      = ФорматОбмена;
	СообщениеXML.Документ          = ДокументСсылка;
	СообщениеXML.ДокументОснование = Шапка.ДокументОснование;
	СообщениеXML.Версия            = НомерВерсии;
	
	СообщенияXML.Добавить(СообщениеXML);
	
	Возврат СообщенияXML;
	
КонецФункции

Функция КвитанцияПодтвержденияАктаРасхожденийXML(ДокументСсылка)
	
	СообщенияXML = Новый Массив;
	
	Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхожденийПодтверждение;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЕГАИСПрисоединенныеФайлы.Документ КАК Ссылка,
	|	КОЛИЧЕСТВО(ЕГАИСПрисоединенныеФайлы.Ссылка) КАК ПоследнийНомер
	|ПОМЕСТИТЬ Версии
	|ИЗ
	|	Справочник.ЕГАИСПрисоединенныеФайлы КАК ЕГАИСПрисоединенныеФайлы
	|ГДЕ
	|	ЕГАИСПрисоединенныеФайлы.Документ = &Ссылка
	|	И ЕГАИСПрисоединенныеФайлы.Операция = &Операция
	|	И ЕГАИСПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
	|СГРУППИРОВАТЬ ПО
	|	ЕГАИСПрисоединенныеФайлы.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Шапка.Номер                         КАК Номер,
	|	Шапка.Дата                          КАК Дата,
	|	ЕСТЬNULL(Версии.ПоследнийНомер, 0)  КАК ПоследнийНомерВерсии,
	|	Шапка.ДокументОснование             КАК ДокументОснование,
	|	
	|	Шапка.ИдентификаторЕГАИС            КАК ИдентификаторЕГАИС,
	|	Шапка.Грузоотправитель              КАК ОрганизацияЕГАИС,
	|	Шапка.Грузоотправитель.Код          КАК ИдентификаторФСРАР,
	|	Шапка.Грузоотправитель.ФорматОбмена КАК ФорматОбменаГрузоотправителя,
	|	Шапка.Грузополучатель.ФорматОбмена  КАК ФорматОбменаГрузополучателя,
	|	Шапка.Комментарий                   КАК Комментарий,
	|	Шапка.Ответственный                 КАК Ответственный
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК Шапка,
	|		ЛЕВОЕ СОЕДИНЕНИЕ Версии КАК Версии
	|		ПО Шапка.Ссылка = Версии.Ссылка
	|ГДЕ
	|	Шапка.Ссылка = &Ссылка
	|");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Операция", Операция);
	
	Шапка = Запрос.Выполнить().Выбрать();
	
	Если Не Шапка.Следующий() Тогда
		
		СообщениеXML = ОбменДаннымиЕГАИС.СтруктураСообщенияXML();
		СообщениеXML.Документ = ДокументСсылка;
		СообщениеXML.Описание = ОбменДаннымиЕГАИС.ОписаниеОперацииПередачиДанных(
			Операция, ДокументСсылка);
		
		ОбменДаннымиЕГАИСКлиентСервер.ДобавитьТекстОшибки(СообщениеXML, НСтр("ru = 'Нет данных для выгрузки.'"));
		
		СообщенияXML.Добавить(СообщениеXML);
		Возврат СообщенияXML;
		
	КонецЕсли;
	
	НомерВерсии = Шапка.ПоследнийНомерВерсии + 1;
	ФорматОбмена = ФорматОбмена(Шапка);
	
	СообщениеXML = ОбменДаннымиЕГАИС.СтруктураСообщенияXML();
	СообщениеXML.Описание = ОбменДаннымиЕГАИС.ОписаниеОперацииПередачиДанных(
		Операция, ДокументСсылка, НомерВерсии);
	
	ПространствоИмен = Перечисления.ВидыДокументовЕГАИС.ПространствоИмен(Операция, ФорматОбмена);
	ИмяТипа          = Перечисления.ВидыДокументовЕГАИС.ТипЕГАИС(Операция, ФорматОбмена);
	
	Если ПространствоИмен = Неопределено
		Или ИмяТипа = Неопределено Тогда
		ОбменДаннымиЕГАИСКлиентСервер.ДобавитьТекстОшибки(
			СообщениеXML,
			СтрШаблон(НСтр("ru = 'Операция не поддерживается в версии формата обмена: %1.'"), ФорматОбмена));
		СообщенияXML.Добавить(СообщениеXML);
		Возврат СообщенияXML;
	КонецЕсли;
	
	#Область КвитанцияАктаРасхожденийПодтверждение
	
	КвитанцияXDTO = РаботаСXMLЕГАИС.ОбъектXDTO(ПространствоИмен, "ConfirmTicketType");
	
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO, "Identity", ИнтеграцияЕГАИС.НовыйИдентификаторДокумента(ДокументСсылка, "cct"), СообщениеXML, 3);
	
	КвитанцияXDTO.Header = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(КвитанцияXDTO, "Header");
	
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO.Header, "IsConfirm",    "Accepted",               СообщениеXML);
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO.Header, "TicketNumber", СокрЛП(Шапка.Номер),      СообщениеXML);
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO.Header, "TicketDate",   ТекущаяДатаСеанса(),      СообщениеXML);
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO.Header, "WBRegId",      Шапка.ИдентификаторЕГАИС, СообщениеXML);
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO.Header, "Note",         Шапка.Комментарий,        СообщениеXML);
	
	#КонецОбласти
	
	ТекстСообщенияXML = РаботаСXMLЕГАИС.ОбъектXDTOВXML(КвитанцияXDTO, Шапка.ИдентификаторФСРАР, ПространствоИмен, ИмяТипа);
	
	СообщениеXML.ТекстСообщенияXML = ТекстСообщенияXML;
	СообщениеXML.ТипСообщения      = Перечисления.ТипыЗапросовИС.Исходящий;
	СообщениеXML.ОрганизацияЕГАИС  = Шапка.ОрганизацияЕГАИС;
	СообщениеXML.Операция          = Операция;
	СообщениеXML.ФорматОбмена      = ФорматОбмена;
	СообщениеXML.Документ          = ДокументСсылка;
	СообщениеXML.ДокументОснование = Шапка.ДокументОснование;
	СообщениеXML.Версия            = НомерВерсии;
	
	СообщенияXML.Добавить(СообщениеXML);
	
	Возврат СообщенияXML;
	
КонецФункции

Функция КвитанцияОтказаОтАктаРасхожденийXML(ДокументСсылка)
	
	СообщенияXML = Новый Массив;
	
	Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхожденийОтказ;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЕГАИСПрисоединенныеФайлы.Документ КАК Ссылка,
	|	КОЛИЧЕСТВО(ЕГАИСПрисоединенныеФайлы.Ссылка) КАК ПоследнийНомер
	|ПОМЕСТИТЬ Версии
	|ИЗ
	|	Справочник.ЕГАИСПрисоединенныеФайлы КАК ЕГАИСПрисоединенныеФайлы
	|ГДЕ
	|	ЕГАИСПрисоединенныеФайлы.Документ = &Ссылка
	|	И ЕГАИСПрисоединенныеФайлы.Операция = &Операция
	|	И ЕГАИСПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
	|СГРУППИРОВАТЬ ПО
	|	ЕГАИСПрисоединенныеФайлы.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Шапка.Номер                         КАК Номер,
	|	Шапка.Дата                          КАК Дата,
	|	ЕСТЬNULL(Версии.ПоследнийНомер, 0)  КАК ПоследнийНомерВерсии,
	|	Шапка.ДокументОснование             КАК ДокументОснование,
	|	
	|	Шапка.ИдентификаторЕГАИС            КАК ИдентификаторЕГАИС,
	|	Шапка.Грузоотправитель              КАК ОрганизацияЕГАИС,
	|	Шапка.Грузоотправитель.Код          КАК ИдентификаторФСРАР,
	|	Шапка.Грузоотправитель.ФорматОбмена КАК ФорматОбменаГрузоотправителя,
	|	Шапка.Грузополучатель.ФорматОбмена  КАК ФорматОбменаГрузополучателя,
	|	Шапка.Комментарий                   КАК Комментарий,
	|	Шапка.Ответственный                 КАК Ответственный
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК Шапка,
	|		ЛЕВОЕ СОЕДИНЕНИЕ Версии КАК Версии
	|		ПО Шапка.Ссылка = Версии.Ссылка
	|ГДЕ
	|	Шапка.Ссылка = &Ссылка
	|");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Операция", Операция);
	
	Шапка = Запрос.Выполнить().Выбрать();
	
	Если Не Шапка.Следующий() Тогда
		
		СообщениеXML = ОбменДаннымиЕГАИС.СтруктураСообщенияXML();
		СообщениеXML.Документ = ДокументСсылка;
		СообщениеXML.Описание = ОбменДаннымиЕГАИС.ОписаниеОперацииПередачиДанных(
			Операция, ДокументСсылка);
		
		ОбменДаннымиЕГАИСКлиентСервер.ДобавитьТекстОшибки(СообщениеXML, НСтр("ru = 'Нет данных для выгрузки.'"));
		
		СообщенияXML.Добавить(СообщениеXML);
		Возврат СообщенияXML;
		
	КонецЕсли;
	
	НомерВерсии = Шапка.ПоследнийНомерВерсии + 1;
	ФорматОбмена = ФорматОбмена(Шапка);
	
	СообщениеXML = ОбменДаннымиЕГАИС.СтруктураСообщенияXML();
	СообщениеXML.Описание = ОбменДаннымиЕГАИС.ОписаниеОперацииПередачиДанных(
		Операция, ДокументСсылка, НомерВерсии);
	
	ПространствоИмен = Перечисления.ВидыДокументовЕГАИС.ПространствоИмен(Операция, ФорматОбмена);
	ИмяТипа          = Перечисления.ВидыДокументовЕГАИС.ТипЕГАИС(Операция, ФорматОбмена);
	
	Если ПространствоИмен = Неопределено
		Или ИмяТипа = Неопределено Тогда
		ОбменДаннымиЕГАИСКлиентСервер.ДобавитьТекстОшибки(
			СообщениеXML,
			СтрШаблон(НСтр("ru = 'Операция не поддерживается в версии формата обмена: %1.'"), ФорматОбмена));
		СообщенияXML.Добавить(СообщениеXML);
		Возврат СообщенияXML;
	КонецЕсли;
	
	#Область КвитанцияАктаРасхожденийОтказ
	
	КвитанцияXDTO = РаботаСXMLЕГАИС.ОбъектXDTO(ПространствоИмен, "ConfirmTicketType");
	
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO, "Identity", ИнтеграцияЕГАИС.НовыйИдентификаторДокумента(ДокументСсылка, "rct"), СообщениеXML, 3);
	
	КвитанцияXDTO.Header = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(КвитанцияXDTO, "Header");
	
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO.Header, "IsConfirm",    "Rejected",               СообщениеXML);
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO.Header, "TicketNumber", СокрЛП(Шапка.Номер),      СообщениеXML);
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO.Header, "TicketDate",   ТекущаяДатаСеанса(),      СообщениеXML);
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO.Header, "WBRegId",      Шапка.ИдентификаторЕГАИС, СообщениеXML);
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO.Header, "Note",         Шапка.Комментарий,        СообщениеXML);
	
	#КонецОбласти
	
	ТекстСообщенияXML = РаботаСXMLЕГАИС.ОбъектXDTOВXML(КвитанцияXDTO, Шапка.ИдентификаторФСРАР, ПространствоИмен, ИмяТипа);
	
	СообщениеXML.ТекстСообщенияXML = ТекстСообщенияXML;
	СообщениеXML.ТипСообщения      = Перечисления.ТипыЗапросовИС.Исходящий;
	СообщениеXML.ОрганизацияЕГАИС  = Шапка.ОрганизацияЕГАИС;
	СообщениеXML.Операция          = Операция;
	СообщениеXML.ФорматОбмена      = ФорматОбмена;
	СообщениеXML.Документ          = ДокументСсылка;
	СообщениеXML.ДокументОснование = Шапка.ДокументОснование;
	СообщениеXML.Версия            = НомерВерсии;
	
	СообщенияXML.Добавить(СообщениеXML);
	
	Возврат СообщенияXML;
	
КонецФункции

Функция КвитанцияПодтверждениеЗапросаНаОтменуПроведенияXML(ДокументСсылка)
	
	СообщенияXML = Новый Массив;
	
	Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТНПодтверждение;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЕГАИСПрисоединенныеФайлы.Документ КАК Ссылка,
	|	КОЛИЧЕСТВО(ЕГАИСПрисоединенныеФайлы.Ссылка) КАК ПоследнийНомер
	|ПОМЕСТИТЬ Версии
	|ИЗ
	|	Справочник.ЕГАИСПрисоединенныеФайлы КАК ЕГАИСПрисоединенныеФайлы
	|ГДЕ
	|	ЕГАИСПрисоединенныеФайлы.Документ = &Ссылка
	|	И ЕГАИСПрисоединенныеФайлы.Операция = &Операция
	|	И ЕГАИСПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
	|СГРУППИРОВАТЬ ПО
	|	ЕГАИСПрисоединенныеФайлы.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Шапка.Номер                         КАК Номер,
	|	Шапка.Дата                          КАК Дата,
	|	ЕСТЬNULL(Версии.ПоследнийНомер, 0)  КАК ПоследнийНомерВерсии,
	|	Шапка.ДокументОснование             КАК ДокументОснование,
	|	
	|	Шапка.ИдентификаторЕГАИС            КАК ИдентификаторЕГАИС,
	|	Шапка.Грузоотправитель              КАК ОрганизацияЕГАИС,
	|	Шапка.Грузоотправитель.Код          КАК ИдентификаторФСРАР,
	|	Шапка.Грузоотправитель.ФорматОбмена КАК ФорматОбменаГрузоотправителя,
	|	Шапка.Грузополучатель.ФорматОбмена  КАК ФорматОбменаГрузополучателя,
	|	Шапка.Комментарий                   КАК Комментарий,
	|	Шапка.Ответственный                 КАК Ответственный
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК Шапка,
	|		ЛЕВОЕ СОЕДИНЕНИЕ Версии КАК Версии
	|		ПО Шапка.Ссылка = Версии.Ссылка
	|ГДЕ
	|	Шапка.Ссылка = &Ссылка
	|");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Операция", Операция);
	
	Шапка = Запрос.Выполнить().Выбрать();
	
	Если Не Шапка.Следующий() Тогда
		
		СообщениеXML = ОбменДаннымиЕГАИС.СтруктураСообщенияXML();
		СообщениеXML.Документ = ДокументСсылка;
		СообщениеXML.Описание = ОбменДаннымиЕГАИС.ОписаниеОперацииПередачиДанных(
			Операция, ДокументСсылка);
		
		ОбменДаннымиЕГАИСКлиентСервер.ДобавитьТекстОшибки(СообщениеXML, НСтр("ru = 'Нет данных для выгрузки.'"));
		
		СообщенияXML.Добавить(СообщениеXML);
		Возврат СообщенияXML;
		
	КонецЕсли;
	
	НомерВерсии = Шапка.ПоследнийНомерВерсии + 1;
	ФорматОбмена = ФорматОбмена(Шапка);
	
	СообщениеXML = ОбменДаннымиЕГАИС.СтруктураСообщенияXML();
	СообщениеXML.Описание = ОбменДаннымиЕГАИС.ОписаниеОперацииПередачиДанных(
		Операция, ДокументСсылка, НомерВерсии);
	
	ПространствоИмен = Перечисления.ВидыДокументовЕГАИС.ПространствоИмен(Операция, ФорматОбмена);
	ИмяТипа          = Перечисления.ВидыДокументовЕГАИС.ТипЕГАИС(Операция, ФорматОбмена);
	
	Если ПространствоИмен = Неопределено
		Или ИмяТипа = Неопределено Тогда
		ОбменДаннымиЕГАИСКлиентСервер.ДобавитьТекстОшибки(
			СообщениеXML,
			СтрШаблон(НСтр("ru = 'Операция не поддерживается в версии формата обмена: %1.'"), ФорматОбмена));
		СообщенияXML.Добавить(СообщениеXML);
		Возврат СообщенияXML;
	КонецЕсли;
	
	#Область КвитанцияЗапросаНаОтменуПроведенияТТНПодтверждение
	
	КвитанцияXDTO = РаботаСXMLЕГАИС.ОбъектXDTO(ПространствоИмен, "ConfirmRepealWB");
	
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO, "Identity", ИнтеграцияЕГАИС.НовыйИдентификаторДокумента(ДокументСсылка, "acr"), СообщениеXML, 3);
	
	КвитанцияXDTO.Header = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(КвитанцияXDTO, "Header");
	
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO.Header, "IsConfirm",     "Accepted",               СообщениеXML);
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO.Header, "ConfirmNumber", СокрЛП(Шапка.Номер),      СообщениеXML);
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO.Header, "ConfirmDate",   ТекущаяДатаСеанса(),      СообщениеXML);
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO.Header, "WBRegId",       Шапка.ИдентификаторЕГАИС, СообщениеXML);
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO.Header, "Note",          Шапка.Комментарий,        СообщениеXML);
	
	#КонецОбласти
	
	ТекстСообщенияXML = РаботаСXMLЕГАИС.ОбъектXDTOВXML(КвитанцияXDTO, Шапка.ИдентификаторФСРАР, ПространствоИмен, ИмяТипа);
	
	СообщениеXML.ТекстСообщенияXML = ТекстСообщенияXML;
	СообщениеXML.ТипСообщения      = Перечисления.ТипыЗапросовИС.Исходящий;
	СообщениеXML.ОрганизацияЕГАИС  = Шапка.ОрганизацияЕГАИС;
	СообщениеXML.Операция          = Операция;
	СообщениеXML.ФорматОбмена      = ФорматОбмена;
	СообщениеXML.Документ          = ДокументСсылка;
	СообщениеXML.ДокументОснование = Шапка.ДокументОснование;
	СообщениеXML.Версия            = НомерВерсии;
	
	СообщенияXML.Добавить(СообщениеXML);
	
	Возврат СообщенияXML;
	
КонецФункции

Функция КвитанцияОтказаОтЗапросаНаОтменуПроведенияXML(ДокументСсылка)
	
	СообщенияXML = Новый Массив;
	
	Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТНОтказ;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЕГАИСПрисоединенныеФайлы.Документ КАК Ссылка,
	|	КОЛИЧЕСТВО(ЕГАИСПрисоединенныеФайлы.Ссылка) КАК ПоследнийНомер
	|ПОМЕСТИТЬ Версии
	|ИЗ
	|	Справочник.ЕГАИСПрисоединенныеФайлы КАК ЕГАИСПрисоединенныеФайлы
	|ГДЕ
	|	ЕГАИСПрисоединенныеФайлы.Документ = &Ссылка
	|	И ЕГАИСПрисоединенныеФайлы.Операция = &Операция
	|	И ЕГАИСПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
	|СГРУППИРОВАТЬ ПО
	|	ЕГАИСПрисоединенныеФайлы.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Шапка.Номер                         КАК Номер,
	|	Шапка.Дата                          КАК Дата,
	|	ЕСТЬNULL(Версии.ПоследнийНомер, 0)  КАК ПоследнийНомерВерсии,
	|	Шапка.ДокументОснование             КАК ДокументОснование,
	|	
	|	Шапка.ИдентификаторЕГАИС            КАК ИдентификаторЕГАИС,
	|	Шапка.Грузоотправитель              КАК ОрганизацияЕГАИС,
	|	Шапка.Грузоотправитель.Код          КАК ИдентификаторФСРАР,
	|	Шапка.Грузоотправитель.ФорматОбмена КАК ФорматОбменаГрузоотправителя,
	|	Шапка.Грузополучатель.ФорматОбмена  КАК ФорматОбменаГрузополучателя,
	|	Шапка.Комментарий                   КАК Комментарий,
	|	Шапка.Ответственный                 КАК Ответственный
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК Шапка,
	|		ЛЕВОЕ СОЕДИНЕНИЕ Версии КАК Версии
	|		ПО Шапка.Ссылка = Версии.Ссылка
	|ГДЕ
	|	Шапка.Ссылка = &Ссылка
	|");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Операция", Операция);
	
	Шапка = Запрос.Выполнить().Выбрать();
	
	Если Не Шапка.Следующий() Тогда
		
		СообщениеXML = ОбменДаннымиЕГАИС.СтруктураСообщенияXML();
		СообщениеXML.Документ = ДокументСсылка;
		СообщениеXML.Описание = ОбменДаннымиЕГАИС.ОписаниеОперацииПередачиДанных(
			Операция, ДокументСсылка);
		
		ОбменДаннымиЕГАИСКлиентСервер.ДобавитьТекстОшибки(СообщениеXML, НСтр("ru = 'Нет данных для выгрузки.'"));
		
		СообщенияXML.Добавить(СообщениеXML);
		Возврат СообщенияXML;
		
	КонецЕсли;
	
	НомерВерсии = Шапка.ПоследнийНомерВерсии + 1;
	ФорматОбмена = ФорматОбмена(Шапка);
	
	СообщениеXML = ОбменДаннымиЕГАИС.СтруктураСообщенияXML();
	СообщениеXML.Описание = ОбменДаннымиЕГАИС.ОписаниеОперацииПередачиДанных(
		Операция, ДокументСсылка, НомерВерсии);
	
	ПространствоИмен = Перечисления.ВидыДокументовЕГАИС.ПространствоИмен(Операция, ФорматОбмена);
	ИмяТипа          = Перечисления.ВидыДокументовЕГАИС.ТипЕГАИС(Операция, ФорматОбмена);
	
	Если ПространствоИмен = Неопределено
		Или ИмяТипа = Неопределено Тогда
		ОбменДаннымиЕГАИСКлиентСервер.ДобавитьТекстОшибки(
			СообщениеXML,
			СтрШаблон(НСтр("ru = 'Операция не поддерживается в версии формата обмена: %1.'"), ФорматОбмена));
		СообщенияXML.Добавить(СообщениеXML);
		Возврат СообщенияXML;
	КонецЕсли;
	
	#Область КвитанцияЗапросаНаОтменуПроведенияТТНОтказ
	
	КвитанцияXDTO = РаботаСXMLЕГАИС.ОбъектXDTO(ПространствоИмен, "ConfirmRepealWB");
	
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO, "Identity", ИнтеграцияЕГАИС.НовыйИдентификаторДокумента(ДокументСсылка, "rcr"), СообщениеXML, 3);
	
	КвитанцияXDTO.Header = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(КвитанцияXDTO, "Header");
	
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO.Header, "IsConfirm",     "Rejected",               СообщениеXML);
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO.Header, "ConfirmNumber", СокрЛП(Шапка.Номер),      СообщениеXML);
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO.Header, "ConfirmDate",   ТекущаяДатаСеанса(),      СообщениеXML);
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO.Header, "WBRegId",       Шапка.ИдентификаторЕГАИС, СообщениеXML);
	РаботаСXMLЕГАИС.ЗаполнитьСвойствоXDTO(КвитанцияXDTO.Header, "Note",          Шапка.Комментарий,        СообщениеXML);
	
	#КонецОбласти
	
	ТекстСообщенияXML = РаботаСXMLЕГАИС.ОбъектXDTOВXML(КвитанцияXDTO, Шапка.ИдентификаторФСРАР, ПространствоИмен, ИмяТипа);
	
	СообщениеXML.ТекстСообщенияXML = ТекстСообщенияXML;
	СообщениеXML.ТипСообщения      = Перечисления.ТипыЗапросовИС.Исходящий;
	СообщениеXML.ОрганизацияЕГАИС  = Шапка.ОрганизацияЕГАИС;
	СообщениеXML.Операция          = Операция;
	СообщениеXML.ФорматОбмена      = ФорматОбмена;
	СообщениеXML.Документ          = ДокументСсылка;
	СообщениеXML.ДокументОснование = Шапка.ДокументОснование;
	СообщениеXML.Версия            = НомерВерсии;
	
	СообщенияXML.Добавить(СообщениеXML);
	
	Возврат СообщенияXML;
	
КонецФункции

#КонецОбласти

#Область СканированиеАлкогольнойПродукции

Функция ТаблицаАлкогольнойПродукцииКОпределениюСправок2(ДокументСсылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТТНИсходящаяЕГАИСТовары.АлкогольнаяПродукция         КАК АлкогольнаяПродукция,
	|	ТТНИсходящаяЕГАИСТовары.Номенклатура                 КАК Номенклатура,
	|	ТТНИсходящаяЕГАИСТовары.Характеристика               КАК Характеристика,
	|	ТТНИсходящаяЕГАИСТовары.Серия                        КАК Серия,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВидыАлкогольнойПродукции.Маркируемый, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Справки2ЕГАИС.ПустаяСсылка)
	|		ИНАЧЕ ТТНИсходящаяЕГАИСТовары.Справка2
	|	КОНЕЦ                                                КАК Справка2,
	|	СУММА(ТТНИсходящаяЕГАИСТовары.Количество)            КАК Количество,
	|	ЕСТЬNULL(ВидыАлкогольнойПродукции.Маркируемый, ЛОЖЬ) КАК Маркируемая
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ТТНИсходящаяЕГАИСТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
	|		ПО ТТНИсходящаяЕГАИСТовары.АлкогольнаяПродукция = КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыАлкогольнойПродукции КАК ВидыАлкогольнойПродукции
	|		ПО (КлассификаторАлкогольнойПродукцииЕГАИС.ВидПродукции = ВидыАлкогольнойПродукции.Ссылка)
	|ГДЕ
	|	ТТНИсходящаяЕГАИСТовары.Ссылка = &ДокументСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТТНИсходящаяЕГАИСТовары.Номенклатура,
	|	ТТНИсходящаяЕГАИСТовары.АлкогольнаяПродукция,
	|	ТТНИсходящаяЕГАИСТовары.Характеристика,
	|	ТТНИсходящаяЕГАИСТовары.Серия,
	|	ЕСТЬNULL(ВидыАлкогольнойПродукции.Маркируемый, ЛОЖЬ),
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВидыАлкогольнойПродукции.Маркируемый, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Справки2ЕГАИС.ПустаяСсылка)
	|		ИНАЧЕ ТТНИсходящаяЕГАИСТовары.Справка2
	|	КОНЕЦ";
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ШтрихкодыУпаковок(ДокументСсылка, ЗаполнитьСправки2ИзРегистра = Ложь) Экспорт
	
	Возврат ШтрихкодированиеЕГАИС.ВложенныеШтрихкодыУпаковокПоДокументу(ДокументСсылка, ЗаполнитьСправки2ИзРегистра);
	
КонецФункции

Функция ОбработатьДанныеШтрихкода(Форма, ДанныеШтрихкода, ПараметрыСканирования, ВложенныеШтрихкоды = Неопределено) Экспорт
	
	Если ИнтеграцияИСКлиентСервер.ЭтоУпаковка(ДанныеШтрихкода.ТипУпаковки) Тогда
		
		Возврат ОбработатьДанныеШтрихкодаЛогистическойУпаковки(Форма, ДанныеШтрихкода, ВложенныеШтрихкоды, ПараметрыСканирования);
		
	ИначеЕсли ДанныеШтрихкода.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар Тогда
		
		Возврат ОбработатьДанныеШтрихкодаПотребительскойУпаковки(Форма, ДанныеШтрихкода, ПараметрыСканирования);
		
	ИначеЕсли ДанныеШтрихкода.ТипШтрихкода = Перечисления.ТипыШтрихкодов.DataMatrix Тогда
		
		Возврат ОбработатьДанныеШтрихкодаСНомеромИСерией(Форма, ДанныеШтрихкода, ПараметрыСканирования);
		
	ИначеЕсли ДанныеШтрихкода.ВидУпаковки = Перечисления.ВидыУпаковокИС.Потребительская
		И ДанныеШтрихкода.МаркируемаяПродукция <> Истина
		И ДанныеШтрихкода.ЭтоШтрихкодНоменклатуры Тогда
		
		Возврат ОбработатьДанныеШтрихкодаПотребительскойУпаковки(Форма, ДанныеШтрихкода, ПараметрыСканирования);
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ОбработатьДанныеШтрихкодаПотребительскойУпаковки(Форма, ДанныеШтрихкода, ПараметрыСканирования) Экспорт
	
	Результат = ШтрихкодированиеОбщегоНазначенияИС.ИнициализироватьРезультатОбработкиШтрихкода(ПараметрыСканирования, ДанныеШтрихкода);
	
	ПараметрыЗаполнения = АкцизныеМаркиЕГАИС.ПараметрыЗаполненияТоваровИАкцизныхМарок(Форма, Истина, ПараметрыСканирования);
	Если Форма.Элементы.Товары.ТекущаяСтрока <> Неопределено Тогда
		ПараметрыЗаполнения.ТекущаяСтрока = Форма.Объект.Товары.НайтиПоИдентификатору(Форма.Элементы.Товары.ТекущаяСтрока);
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Номенклатура",   ДанныеШтрихкода.Номенклатура);
	ПараметрыОтбора.Вставить("Характеристика", ДанныеШтрихкода.Характеристика);
	ПараметрыОтбора.Вставить("Серия",          ДанныеШтрихкода.Серия);
	
	Если ДанныеШтрихкода.Коэффициент <> Неопределено И ДанныеШтрихкода.Коэффициент > 0 Тогда
		КоличествоПродукции = ДанныеШтрихкода.Коэффициент;
	Иначе
		КоличествоПродукции = ДанныеШтрихкода.Количество;
	КонецЕсли;
	
	РаспределенноеКоличество = 0;
	Пока РаспределенноеКоличество < КоличествоПродукции Цикл
		
		ОсталосьРаспределить = КоличествоПродукции - РаспределенноеКоличество;
	
		РезультатПоиска = АкцизныеМаркиЕГАИС.НайтиСтрокиТоваров(
			Форма.Объект.Товары, ПараметрыОтбора,
			ДанныеШтрихкода.Справка2, ДанныеШтрихкода.АлкогольнаяПродукция, ПараметрыЗаполнения);
		
		СтрокаТЧ                        = РезультатПоиска.ПолноеСоответствие;
		СтрокиТЧДляУменьшенияКоличества = РезультатПоиска.КЗаполнению;
		
		Если СтрокиТЧДляУменьшенияКоличества = Неопределено
			И СтрокаТЧ = Неопределено Тогда
			
			СтрокаТЧ = Форма.Объект.Товары.Добавить();
			СтрокаТЧ.Номенклатура         = ДанныеШтрихкода.Номенклатура;
			СтрокаТЧ.Характеристика       = ДанныеШтрихкода.Характеристика;
			СтрокаТЧ.Серия                = ДанныеШтрихкода.Серия;
			
			СтрокаТЧ.АлкогольнаяПродукция = ДанныеШтрихкода.АлкогольнаяПродукция;
			СтрокаТЧ.Справка2             = ДанныеШтрихкода.Справка2;
			
			АкцизныеМаркиЕГАИС.ОбработатьДобавленнуюСтроку(
				СтрокаТЧ, ПараметрыЗаполнения,
				ОсталосьРаспределить, ДанныеШтрихкода.МаркируемаяПродукция);
			
			РаспределенноеКоличество = РаспределенноеКоличество + ОсталосьРаспределить;
			
		ИначеЕсли СтрокиТЧДляУменьшенияКоличества = Неопределено
			И СтрокаТЧ <> Неопределено Тогда
			
			АкцизныеМаркиЕГАИС.ОбработатьИзмененнуюСтроку(
				СтрокаТЧ, ПараметрыЗаполнения,
				ОсталосьРаспределить, ДанныеШтрихкода.МаркируемаяПродукция);
			
			РаспределенноеКоличество = РаспределенноеКоличество + ОсталосьРаспределить;
			
		Иначе
			
			Для Каждого СтрокаТЧДляУменьшенияКоличества Из СтрокиТЧДляУменьшенияКоличества Цикл
				
				Если СтрокаТЧДляУменьшенияКоличества.Количество >= ОсталосьРаспределить Тогда
					КоличествоКОбработке = ОсталосьРаспределить;
				Иначе
					КоличествоКОбработке = СтрокаТЧДляУменьшенияКоличества.Количество;
				КонецЕсли;
				
				Если СтрокаТЧДляУменьшенияКоличества.Количество <= КоличествоКОбработке Тогда
					
					Если СтрокаТЧ = Неопределено Тогда
						
						СтрокаТЧ = СтрокаТЧДляУменьшенияКоличества;
						
						СтрокаТЧ.АлкогольнаяПродукция = ДанныеШтрихкода.АлкогольнаяПродукция;
						СтрокаТЧ.Справка2             = ДанныеШтрихкода.Справка2;
						
					Иначе
						
						Форма.Объект.Товары.Удалить(СтрокаТЧДляУменьшенияКоличества);
						
						Индекс = ПараметрыЗаполнения.ИзмененныеСтроки.Найти(СтрокаТЧДляУменьшенияКоличества);
						Если Индекс <> Неопределено Тогда
							ПараметрыЗаполнения.ИзмененныеСтроки.Удалить(Индекс);
						КонецЕсли;
						
						АкцизныеМаркиЕГАИС.ОбработатьИзмененнуюСтроку(
							СтрокаТЧ, ПараметрыЗаполнения,
							КоличествоКОбработке, ДанныеШтрихкода.МаркируемаяПродукция);
						
					КонецЕсли;
					
					РаспределенноеКоличество = РаспределенноеКоличество + КоличествоКОбработке;
					
				Иначе
					
					СтрокаТЧДляУменьшенияКоличества.Количество = СтрокаТЧДляУменьшенияКоличества.Количество - КоличествоКОбработке;
					ПараметрыЗаполнения.ИзмененныеСтроки.Добавить(СтрокаТЧДляУменьшенияКоличества);
					
					Если СтрокаТЧ = Неопределено Тогда
						
						СтрокаТЧ = Форма.Объект.Товары.Вставить(Форма.Объект.Товары.Индекс(СтрокаТЧДляУменьшенияКоличества) + 1);
						ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтрокаТЧДляУменьшенияКоличества,,"Количество, КоличествоУпаковок, ИдентификаторСтроки");
						
						СтрокаТЧ.АлкогольнаяПродукция = ДанныеШтрихкода.АлкогольнаяПродукция;
						СтрокаТЧ.Справка2             = ДанныеШтрихкода.Справка2;
						
						АкцизныеМаркиЕГАИС.ОбработатьДобавленнуюСтроку(
							СтрокаТЧ, ПараметрыЗаполнения,
							КоличествоКОбработке, ДанныеШтрихкода.МаркируемаяПродукция);
						
					Иначе
						
						АкцизныеМаркиЕГАИС.ОбработатьИзмененнуюСтроку(
							СтрокаТЧ, ПараметрыЗаполнения,
							КоличествоКОбработке, ДанныеШтрихкода.МаркируемаяПродукция);
						
					КонецЕсли;
					
					РаспределенноеКоличество = РаспределенноеКоличество + КоличествоКОбработке;
					
				КонецЕсли;
				
				Прервать;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаТЧ.ИдентификаторСтроки) Тогда
			СтрокаТЧ.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ДанныеШтрихкода.МаркируемаяПродукция = Истина Тогда
		
		АкцизныеМаркиСтрокаТЧ = Форма.Объект.АкцизныеМарки.Добавить();
		АкцизныеМаркиСтрокаТЧ.АкцизнаяМарка = ДанныеШтрихкода.ШтрихкодУпаковки;
		АкцизныеМаркиСтрокаТЧ.Количество    = 1;
		Если ЗначениеЗаполнено(ДанныеШтрихкода.Справка2) Тогда
			АкцизныеМаркиСтрокаТЧ.Справка2 = ДанныеШтрихкода.Справка2;
		Иначе
			АкцизныеМаркиСтрокаТЧ.ИдентификаторСтроки = СтрокаТЧ.ИдентификаторСтроки;
		КонецЕсли;
		
	КонецЕсли;
	
	Результат.ДобавленныеСтроки = ПараметрыЗаполнения.ДобавленныеСтроки;
	Результат.ИзмененныеСтроки  = ПараметрыЗаполнения.ИзмененныеСтроки;
	
	Возврат Результат;
	
КонецФункции

Функция ОбработатьДанныеШтрихкодаЛогистическойУпаковки(Форма, ДанныеШтрихкода, ВложенныеШтрихкоды, ПараметрыСканирования) Экспорт
	
	Результат = ШтрихкодированиеОбщегоНазначенияИС.ИнициализироватьРезультатОбработкиШтрихкода(ПараметрыСканирования, ДанныеШтрихкода);
	
	ПараметрыЗаполнения = АкцизныеМаркиЕГАИС.ПараметрыЗаполненияТоваровИАкцизныхМарок(Форма, Истина, ПараметрыСканирования);
	Если Форма.Элементы.Товары.ТекущаяСтрока <> Неопределено Тогда
		ПараметрыЗаполнения.ТекущаяСтрока = Форма.Объект.Товары.НайтиПоИдентификатору(Форма.Элементы.Товары.ТекущаяСтрока);
	КонецЕсли;
	
	АкцизныеМаркиЕГАИС.ЗаполнитьТоварыИАкцизныеМарки(Форма.Объект, ВложенныеШтрихкоды.ДеревоУпаковок, ПараметрыЗаполнения);
	
	Результат.ИзмененныеСтроки  = ПараметрыЗаполнения.ИзмененныеСтроки;
	Результат.ДобавленныеСтроки = ПараметрыЗаполнения.ДобавленныеСтроки;
	
	Возврат Результат;
	
КонецФункции

Функция ОбработатьДанныеШтрихкодаСНомеромИСерией(Форма, ДанныеШтрихкода, ПараметрыСканирования) Экспорт
	
	Результат = ШтрихкодированиеОбщегоНазначенияИС.ИнициализироватьРезультатОбработкиШтрихкода(ПараметрыСканирования, ДанныеШтрихкода);
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Номенклатура",         ДанныеШтрихкода.Номенклатура);
	ПараметрыОтбора.Вставить("Характеристика",       ДанныеШтрихкода.Характеристика);
	ПараметрыОтбора.Вставить("АлкогольнаяПродукция", ДанныеШтрихкода.АлкогольнаяПродукция);
	ПараметрыОтбора.Вставить("Серия",                ДанныеШтрихкода.Серия);
	ПараметрыОтбора.Вставить("Справка2",             ДанныеШтрихкода.Справка2);
	
	МассивСтрок = Форма.Объект.Товары.НайтиСтроки(ПараметрыОтбора);
	Если МассивСтрок.Количество() > 0 Тогда
		СтрокаТЧ = МассивСтрок[0];
	Иначе
		СтрокаТЧ = Неопределено;
	КонецЕсли;
	
	Если СтрокаТЧ <> Неопределено Тогда
		
		СтрокаТЧ.Количество = СтрокаТЧ.Количество + 1;
		
		Результат.ИзмененныеСтроки.Добавить(СтрокаТЧ);
		
	Иначе
		
		СтрокаТЧ = Форма.Объект.Товары.Добавить();
		СтрокаТЧ.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
		
		СтрокаТЧ.АлкогольнаяПродукция = ДанныеШтрихкода.АлкогольнаяПродукция;
		СтрокаТЧ.Номенклатура         = ДанныеШтрихкода.Номенклатура;
		СтрокаТЧ.Характеристика       = ДанныеШтрихкода.Характеристика;
		СтрокаТЧ.Серия                = ДанныеШтрихкода.Серия;
		СтрокаТЧ.Справка2             = ДанныеШтрихкода.Справка2;
		
		СтрокаТЧ.КоличествоУпаковок = 1;
		
		Результат.ДобавленныеСтроки.Добавить(СтрокаТЧ);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ЗавершениеПроверкиИПодбораМаркируемойПродукции

// Отражает результаты проверки и подбора в документе, из которого была вызвана соответствующая форма.
//
// Параметры:
// 	ПараметрыОкончанияПроверки - Структура - (См. ПроверкаИПодборЕГАИС.ЗафиксироватьРезультатПроверкиИПодбора)
Процедура ОтразитьРезультатыПроверкиИПодбора(ПараметрыОкончанияПроверки) Экспорт
	Если ПараметрыОкончанияПроверки.ПостфиксСохранения = "АктРасхождений" Тогда
		ОтразитьРезультатыПроверкиИПодбораАктаРасхождений(ПараметрыОкончанияПроверки);
	Иначе
		ОтразитьРезультатыПроверкиИПодбораОтгрузки(ПараметрыОкончанияПроверки);
	КонецЕсли;
КонецПроцедуры

Процедура ОтразитьРезультатыПроверкиИПодбораОтгрузки(ПараметрыОкончанияПроверки)
	
	ТаблицаНеМаркируемойПродукции = ПараметрыОкончанияПроверки.ТаблицаНеМаркируемойПродукции;
	ДеревоМаркируемойПродукции    = ПараметрыОкончанияПроверки.ДеревоМаркируемойПродукции;
	
	ДокументОбъект = ПараметрыОкончанияПроверки.ПроверяемыйДокумент.ПолучитьОбъект();
	ДокументОбъект.СтатусПроверкиИПодбора = Перечисления.СтатусыПроверкиИПодбораИС.Завершено;
	
	ОчиститьДанныеПередЗаполнением(ДокументОбъект);
	
	ПараметрыЗаполнения = АкцизныеМаркиЕГАИС.ПараметрыЗаполненияТоваровИАкцизныхМарок(ДокументОбъект, Ложь);
	
	АкцизныеМаркиЕГАИС.ЗаполнитьТоварыИАкцизныеМарки(
		ДокументОбъект, ДеревоМаркируемойПродукции, ПараметрыЗаполнения);
	
	АкцизныеМаркиЕГАИС.ЗаполнитьНеМаркируемыеТовары(
		ДокументОбъект, ТаблицаНеМаркируемойПродукции, ПараметрыЗаполнения);
	
	ОбработатьСтрокиТЧ(ДокументОбъект, ПараметрыЗаполнения);
	
	СтруктураПоискаПустыхСтрок = Новый Структура("Количество", 0);
	ПустыеСтроки = ДокументОбъект.Товары.НайтиСтроки(СтруктураПоискаПустыхСтрок);
	
	Для Каждого ПустаяСтрока Из ПустыеСтроки Цикл
		ДокументОбъект.Товары.Удалить(ПустаяСтрока);
	КонецЦикла;
	
	Если ДокументОбъект.Проведен Тогда
		Если ДокументОбъект.ПроверитьЗаполнение() Тогда
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Иначе
			ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		КонецЕсли;
	Иначе
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтразитьРезультатыПроверкиИПодбораАктаРасхождений(ПараметрыОкончанияПроверки)
	
	ДокументОбъект = ПараметрыОкончанияПроверки.ПроверяемыйДокумент.ПолучитьОбъект();
	ДокументОбъект.СтатусПроверкиИПодбораАктРасхождений = Перечисления.СтатусыПроверкиИПодбораИС.Завершено;
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("ДеревоМаркированнойПродукции", ПараметрыОкончанияПроверки.ДеревоМаркируемойПродукции);
	РезультатПроверки.Вставить("ТаблицаНеМаркируемойПродукции", ПараметрыОкончанияПроверки.ТаблицаНеМаркируемойПродукции);
	ДокументОбъект.ДанныеПроверкиИПодбораАктРасхождений = Новый ХранилищеЗначения(РезультатПроверки);
	
	Если ДокументОбъект.Проведен Тогда
		Если ДокументОбъект.ПроверитьЗаполнение() Тогда
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		КонецЕсли;
	Иначе
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОчиститьДанныеПередЗаполнением(ДокументОбъект)
	
	ДокументОбъект.АкцизныеМарки.Очистить();
	
	ИменаКолонокДляОчистки = Новый Массив();
	ИменаКолонокДляОчистки.Добавить("Количество");
	ИменаКолонокДляОчистки.Добавить("КоличествоУпаковок");
	
	Для Каждого СтрокаТовары Из ДокументОбъект.Товары Цикл
		Для Каждого ИмяКолонки Из ИменаКолонокДляОчистки Цикл
			СтрокаТовары[ИмяКолонки] = 0;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьСтрокиТЧ(ДокументОбъект, ПараметрыЗаполнения)
	
	ПараметрыУказанияСерий = ПараметрыУказанияСерий(ДокументОбъект);
	ПустойСклад = ОбщегоНазначенияИС.ПустоеЗначениеОпределяемогоТипа("Склад");
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
	СтруктураДействий.Вставить("ПроверитьСериюРассчитатьСтатус", Новый Структура("Склад, ПараметрыУказанияСерий", ПустойСклад, ПараметрыУказанияСерий));
	СтруктураДействий.Вставить("ЗаполнитьАлкогольнуюПродукцию", ПараметрыЗаполнения);
	
	Для Каждого СтрокаТЧ Из ПараметрыЗаполнения.ДобавленныеСтроки Цикл
		
		ИнтеграцияИСПереопределяемый.ОбработатьСтрокуТабличнойЧасти(СтрокаТЧ, СтруктураДействий);
		
	КонецЦикла;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	Для Каждого СтрокаТЧ Из ПараметрыЗаполнения.ИзмененныеСтроки Цикл
		
		ИнтеграцияИСПереопределяемый.ОбработатьСтрокуТабличнойЧасти(СтрокаТЧ, СтруктураДействий);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных;

КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаОстаткиАлкогольнойПродукцииЕГАИС(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДвиженияСерийТоваров(Запрос, ТекстыЗапроса, Регистры);
	
	ИнтеграцияИС.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеШапки.Дата                    КАК Период,
	|	ДанныеШапки.Ссылка                  КАК Ссылка,
	|	ДанныеШапки.Грузоотправитель        КАК Грузоотправитель,
	|	ДанныеШапки.ДатаРегистрацииДвижений КАК ДатаРегистрацииДвижений,
	|	ДанныеШапки.ЕстьРасхождения         КАК ЕстьРасхождения,
	|	СтатусыДокументовЕГАИС.Статус       КАК СтатусОбработки
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК ДанныеШапки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ДанныеШапки.Ссылка
	|ГДЕ
	|	ДанныеШапки.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",                  Реквизиты.Период);
	Запрос.УстановитьПараметр("Ссылка",                  Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("УдалитьСтатусОбработки",  Реквизиты.СтатусОбработки);
	Запрос.УстановитьПараметр("Грузоотправитель",        Реквизиты.Грузоотправитель);
	Запрос.УстановитьПараметр("ДатаРегистрацииДвижений", Реквизиты.ДатаРегистрацииДвижений);
	Запрос.УстановитьПараметр("ЕстьРасхождения",         Реквизиты.ЕстьРасхождения);
	
	Запрос.УстановитьПараметр("СтатусыДвиженийСвободныйОстаток", СтатусыДвиженийСвободныйОстаток());
	Запрос.УстановитьПараметр("СтатусыДвиженийКоличество",       СтатусыДвиженийКоличество());
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаОстаткиАлкогольнойПродукцииЕГАИС(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОстаткиАлкогольнойПродукцииЕГАИС";
	
	Если НЕ ИнтеграцияИС.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ИнтеграцияЕГАИС.ЕстьТаблицаЗапроса("ВТТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если НЕ ИнтеграцияЕГАИС.ЕстьТаблицаЗапроса("ВТКоэффициентыПересчетаВЕдиницыЕГАИС", ТекстыЗапроса) Тогда
		ТекстЗапросаВТКоэффициентыПересчетаВЕдиницыЕГАИС(Запрос, ТекстыЗапроса);
	КонецЕсли;

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)  КАК ВидДвижения,
	|	&Период                                 КАК Период,
	|	&Грузоотправитель                       КАК ОрганизацияЕГАИС,
	|	ТаблицаТовары.АлкогольнаяПродукция      КАК АлкогольнаяПродукция,
	|	ТаблицаТовары.Справка2                  КАК Справка2,
	|	ВЫБОР КОГДА &ЕстьРасхождения ТОГДА
	|		ТаблицаТовары.КоличествоФакт
	|	ИНАЧЕ
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(ОписаниеНоменклатуры.КоличествоВПотребительскойУпаковке, 0) > 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.Количество * ЕСТЬNULL(ЕдиницыЕГАИС.Коэффициент, 1) /
	|				ОписаниеНоменклатуры.КоличествоВПотребительскойУпаковке КАК ЧИСЛО(12, 0))
	|			ИНАЧЕ ТаблицаТовары.Количество * ЕСТЬNULL(ЕдиницыЕГАИС.Коэффициент, 1)
	|		КОНЕЦ
	|	КОНЕЦ КАК СвободныйОстаток,
	|	0                                       КАК Количество,
	|	ТаблицаТовары.НомерСтроки               КАК НомерСтроки
	|ИЗ
	|	ВТТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКоэффициентыПересчетаВЕдиницыЕГАИС КАК ЕдиницыЕГАИС
	|		ПО ЕдиницыЕГАИС.АлкогольнаяПродукция = ТаблицаТовары.АлкогольнаяПродукция
	|		 И ЕдиницыЕГАИС.Номенклатура = ТаблицаТовары.Номенклатура
	|		 И ЕдиницыЕГАИС.Характеристика = ТаблицаТовары.Характеристика
	|		 И ЕдиницыЕГАИС.Серия = ТаблицаТовары.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеНоменклатуры
	|			ПО ТаблицаТовары.Номенклатура = ОписаниеНоменклатуры.Номенклатура
	|			 И ТаблицаТовары.АлкогольнаяПродукция.ТипПродукции <> ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.Неупакованная)
	|ГДЕ
	|	&УдалитьСтатусОбработки В(&СтатусыДвиженийСвободныйОстаток)
	|	И ВЫБОР КОГДА &ЕстьРасхождения ТОГДА
	|		ТаблицаТовары.КоличествоФакт
	|	ИНАЧЕ
	|		ТаблицаТовары.Количество
	|	КОНЕЦ <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)  КАК ВидДвижения,
	|	&ДатаРегистрацииДвижений                КАК ДатаРегистрацииДвижений,
	|	&Грузоотправитель                       КАК ОрганизацияЕГАИС,
	|	ТаблицаТовары.АлкогольнаяПродукция      КАК АлкогольнаяПродукция,
	|	ТаблицаТовары.Справка2                  КАК Справка2,
	|	0                                       КАК СвободныйОстаток,
	|	ВЫБОР КОГДА &ЕстьРасхождения ТОГДА
	|		ТаблицаТовары.КоличествоФакт
	|	ИНАЧЕ
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(ОписаниеНоменклатуры.КоличествоВПотребительскойУпаковке, 0) > 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.Количество * ЕСТЬNULL(ЕдиницыЕГАИС.Коэффициент, 1) /
	|				ОписаниеНоменклатуры.КоличествоВПотребительскойУпаковке КАК ЧИСЛО(12, 0))
	|			ИНАЧЕ ТаблицаТовары.Количество * ЕСТЬNULL(ЕдиницыЕГАИС.Коэффициент, 1)
	|		КОНЕЦ
	|	КОНЕЦ КАК Количество,
	|	ТаблицаТовары.НомерСтроки               КАК НомерСтроки
	|ИЗ
	|	ВТТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКоэффициентыПересчетаВЕдиницыЕГАИС КАК ЕдиницыЕГАИС
	|		ПО ЕдиницыЕГАИС.АлкогольнаяПродукция = ТаблицаТовары.АлкогольнаяПродукция
	|		 И ЕдиницыЕГАИС.Номенклатура = ТаблицаТовары.Номенклатура
	|		 И ЕдиницыЕГАИС.Характеристика = ТаблицаТовары.Характеристика
	|		 И ЕдиницыЕГАИС.Серия = ТаблицаТовары.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеНоменклатуры
	|			ПО ТаблицаТовары.Номенклатура = ОписаниеНоменклатуры.Номенклатура
	|			 И ТаблицаТовары.АлкогольнаяПродукция.ТипПродукции <> ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.Неупакованная)
	|ГДЕ
	|	&УдалитьСтатусОбработки В(&СтатусыДвиженийКоличество)
	|	И ВЫБОР КОГДА &ЕстьРасхождения ТОГДА
	|		ТаблицаТовары.КоличествоФакт
	|	ИНАЧЕ
	|		ТаблицаТовары.Количество
	|	КОНЕЦ <> 0
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВТТовары";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка               КАК Ссылка,
	|	ТаблицаТовары.НомерСтроки          КАК НомерСтроки,
	|	ТаблицаТовары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТаблицаТовары.Номенклатура         КАК Номенклатура,
	|	ТаблицаТовары.Характеристика       КАК Характеристика,
	|	ТаблицаТовары.Серия                КАК Серия,
	|	ТаблицаТовары.Количество           КАК Количество,
	|	ТаблицаТовары.КоличествоФакт       КАК КоличествоФакт,
	|	ТаблицаТовары.Справка2             КАК Справка2
	|ПОМЕСТИТЬ ВТТовары
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТКоэффициентыПересчетаВЕдиницыЕГАИС(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВТКоэффициентыПересчетаВЕдиницыЕГАИС";
	
	ТекстЗапроса = ИнтеграцияЕГАИС.ТекстЗапросаВТКоэффициентыПересчетаВЕдиницыЕГАИС("ВТТовары", ИмяРегистра);
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция СтатусыДвиженийСвободныйОстаток()
	
	Результат = Новый Массив;
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.КПередаче);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ПереданВУТМ);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОбрабатываетсяЕГАИС);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОбрабатываетсяКлиентом);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.Подтвержден);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ПодтвержденСРасхождениями);
	
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктОтказаКПередаче);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктОтказаПереданВУТМ);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктОтказаОбрабатываетсяЕГАИС);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктОтказаОшибка);
	
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийПринят);
	
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийПодтверждениеКПередаче);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийПодтверждениеПереданВУТМ);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийПодтверждениеОбрабатываетсяЕГАИС);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийПодтверждениеОшибка);
	
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийОтказКПередаче);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийОтказПереданВУТМ);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийОтказОбрабатываетсяЕГАИС);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийОтказОшибка);
	
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияПринят);
	
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияПодтверждениеКПередаче);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияПодтверждениеПереданВУТМ);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияПодтверждениеОбрабатываетсяЕГАИС);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияПодтверждениеОшибка);
	
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияОтказКПередаче);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияОтказПереданВУТМ);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияОтказОбрабатываетсяЕГАИС);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияОтказОшибка);
	
	Возврат Результат;
	
КонецФункции

Функция СтатусыДвиженийКоличество()
	
	Результат = Новый Массив;
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОбрабатываетсяКлиентом);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.Подтвержден);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ПодтвержденСРасхождениями);
	
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияПринят);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияПодтверждениеКПередаче);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияПодтверждениеПереданВУТМ);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияПодтверждениеОбрабатываетсяЕГАИС);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияПодтверждениеОшибка);
	
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияОтказКПередаче);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияОтказПереданВУТМ);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияОтказОбрабатываетсяЕГАИС);
	Результат.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияОтказОшибка);
	
	Возврат Результат;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияСерийТоваров(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияСерийТоваров";
	
	Если Не ИнтеграцияИС.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = "";
	ИнтеграцияЕГАИСПереопределяемый.ПриЗаполненииТекстаЗапросаДвижениеСерийТоваров(ТекстЗапроса, Метаданные.Документы.ТТНИсходящаяЕГАИС.Имя);
	
	Если ЗначениеЗаполнено(ТекстЗапроса) Тогда
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Серии

//Имена реквизитов, от значений которых зависят параметры указания серий
//
//	Возвращаемое значение:
//		Строка - имена реквизитов, перечисленные через запятую
//
Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	
	Возврат ИнтеграцияИС.ИменаРеквизитовДляЗаполненияПараметровУказанияСерий(Метаданные.Документы.ТТНИсходящаяЕГАИС);
	
КонецФункции

// Возвращает параметры указания серий для товаров, указанных в документе.
//
// Параметры:
//  Объект	 - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий.
// 
// Возвращаемое значение:
//  см. ИнтеграцияИС.ПараметрыУказанияСерий
//
Функция ПараметрыУказанияСерий(Объект) Экспорт
	
	Возврат ИнтеграцияИС.ПараметрыУказанияСерий(Метаданные.Документы.ТТНИсходящаяЕГАИС, Объект);
	
КонецФункции

// Возвращает текст запроса для расчета статусов указания серий
// Параметры:
//   ПараметрыУказанияСерий - см. ИнтеграцияИС.ПараметрыУказанияСерий
// Возвращаемое значение:
//   Строка - текст запроса
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий) Экспорт
	
	Возврат ИнтеграцияИС.ТекстЗапросаЗаполненияСтатусовУказанияСерий(Метаданные.Документы.ТТНИсходящаяЕГАИС, ПараметрыУказанияСерий);

КонецФункции

#КонецОбласти

#Область Отчеты

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ИнтеграцияИСПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	ИнтеграцияИСПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);
	
	КомандаОтчет = Отчеты.АнализРасхожденийПриОтгрузкеАлкогольнойПродукции.ДобавитьКомандуОтчета(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
		КомандаОтчет.Важность = "Обычное";
		КомандаОтчет.Порядок = 1;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Раздел "Б" справки к ТТН
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "ИнтеграцияЕГАИСКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьРазделаБСправкиТТН";
	КомандаПечати.Идентификатор = "ТТНСправкаРазделБ";
	КомандаПечати.Представление = НСтр("ru = 'Раздел ""Б"" справки к ТТН'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

// Сформировать печатные формы объектов.
//
// Параметры:
//   МассивОбъектов        - Массив из Произвольный         - ссылки на объекты, которые нужно распечатать
//   ПараметрыПечати       - Структура                      - дополнительные настройки печати
//   КоллекцияПечатныхФорм - ТаблицаЗначений                - сформированные табличные документы (выходной параметр)
//   ОбъектыПечати         - СписокЗначений из Произвольный - имя области, в которой был выведен объект (выходной параметр)
//   ПараметрыВывода       - Структура                      - дополнительные параметры сформированных табличных документов (выходной параметр)
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений();
	ТекстыЗапросаВременныхТаблиц = Новый Массив;
	ПолноеИмяДокумента = "Документ.ТТНИсходящаяЕГАИС";
	
	Если ИмяРегистра = "ОстаткиАлкогольнойПродукцииЕГАИС" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаОстаткиАлкогольнойПродукцииЕГАИС(Запрос, ТекстыЗапроса, ИмяРегистра);
		ТекстыЗапросаВременныхТаблиц.Добавить(Новый Структура("Ключ, Значение", "ВТКоэффициентыПересчетаВЕдиницыЕГАИС", ТекстЗапросаВТКоэффициентыПересчетаВЕдиницыЕГАИС(Запрос, ТекстыЗапроса)));
		ТекстыЗапросаВременныхТаблиц.Добавить(Новый Структура("Ключ, Значение", "ВТТовары", ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса)));
		СинонимТаблицыДокумента = "ТаблицаТовары";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	
	Результат = ОбновлениеИнформационнойБазыЕГАИС.РезультатАдаптацииЗапроса();
	
	Результат.ЗначенияПараметров.Вставить("СтатусыДвиженийСвободныйОстаток", СтатусыДвиженийСвободныйОстаток());
	Результат.ЗначенияПараметров.Вставить("СтатусыДвиженийКоличество",       СтатусыДвиженийКоличество());
	
	Результат.ТекстЗапроса = ОбновлениеИнформационнойБазыЕГАИС.АдаптироватьЗапросМеханизмаПроведения(
		ТекстЗапроса,
		ПолноеИмяДокумента,
		СинонимТаблицыДокумента,
		ПереопределениеРасчетаПараметров,
		ТекстыЗапросаВременныхТаблиц);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Прочее

Функция ФорматОбмена(Шапка)
	
	ФорматОбмена = ОбменДаннымиЕГАИСКлиентСервер.ФорматОбмена();
	
	Если Шапка.ФорматОбменаГрузоотправителя = Перечисления.ФорматыОбменаЕГАИС.V1
		Или Шапка.ФорматОбменаГрузополучателя = Перечисления.ФорматыОбменаЕГАИС.V1 Тогда
		ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V1;
	ИначеЕсли Шапка.ФорматОбменаГрузоотправителя = Перечисления.ФорматыОбменаЕГАИС.V3
		И Шапка.ФорматОбменаГрузополучателя = Перечисления.ФорматыОбменаЕГАИС.V3 Тогда
		ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V3;
	ИначеЕсли Шапка.ФорматОбменаГрузоотправителя = Перечисления.ФорматыОбменаЕГАИС.V4
		И Шапка.ФорматОбменаГрузополучателя = Перечисления.ФорматыОбменаЕГАИС.V4 Тогда
		ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V4;
	КонецЕсли;
	
	Возврат ФорматОбмена;
	
КонецФункции

Процедура ПостроитьBoxInfo(СтрокаДереваРодитель, ИспользованныеУзлыДереваУпаковок, boxtree)
	
	КоробкиБезИерархии = Новый Массив;
	КоробкиСИерархией  = Новый Массив;
	
	Для Каждого СтрокаДерева Из СтрокаДереваРодитель.Строки Цикл
		
		Если ИспользованныеУзлыДереваУпаковок.Получить(СтрокаДерева) = Неопределено Тогда
		ИначеЕсли ИспользованныеУзлыДереваУпаковок.Получить(СтрокаДерева) = Истина Тогда
			КоробкиСИерархией.Добавить(СтрокаДерева);
		ИначеЕсли ИспользованныеУзлыДереваУпаковок.Получить(СтрокаДерева) = Ложь Тогда
			КоробкиБезИерархии.Добавить(СтрокаДерева);
		КонецЕсли;
	КонецЦикла;
	
	Если КоробкиБезИерархии.Количество() > 0 Тогда
		bl = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(boxtree, "bl");
		Для Каждого СтрокаДерева Из КоробкиБезИерархии Цикл
			bl.boxnum.Добавить(СтрокаДерева.Штрихкод);
		КонецЦикла;
		boxtree.bl.Добавить(bl);
	КонецЕсли;
	
	Если КоробкиСИерархией.Количество() > 0 Тогда
		Для Каждого СтрокаДерева Из КоробкиСИерархией Цикл
			bl = РаботаСXMLИС.ОбъектXDTOПоИмениТипа(boxtree, "bl");
			bl.boxnum.Добавить(СтрокаДерева.Штрихкод);
			ПостроитьBoxInfo(СтрокаДерева, ИспользованныеУзлыДереваУпаковок, bl);
			boxtree.bl.Добавить(bl);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ВходящееСообщениеАктРасхождений(ДокументСсылка) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЕГАИСПрисоединенныеФайлы.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЕГАИСПрисоединенныеФайлы КАК ЕГАИСПрисоединенныеФайлы
	|ГДЕ
	|	ЕГАИСПрисоединенныеФайлы.Документ = &Документ
	|	И ЕГАИСПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Входящий)
	|	И ЕГАИСПрисоединенныеФайлы.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыДокументовЕГАИС.АктТТНРасхождения)
	|УПОРЯДОЧИТЬ ПО
	|	ЕГАИСПрисоединенныеФайлы.ДатаСоздания УБЫВ
	|");
	
	Запрос.УстановитьПараметр("Документ", ДокументСсылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция НеподтвержденныеАкцизныеМарки(ДокументСсылка) Экспорт
	
	АкцизныеМарки = Новый Соответствие;
	
	ВходящееСообщениеАктРасхождений = ВходящееСообщениеАктРасхождений(ДокументСсылка);
	
	Если ВходящееСообщениеАктРасхождений = Неопределено Тогда
		Возврат АкцизныеМарки;
	КонецЕсли;
	
	ТекстСообщенияXML = ПротоколОбменаИС.ТекстСообщенияXMLИзПротокола(ВходящееСообщениеАктРасхождений);
	
	ТекстОшибки = "";
	ОбъектXDTO  = Неопределено;
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ТекстСообщенияXML);
	
	Попытка
		ОбъектXDTO = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML, РаботаСXMLИС.ОбъектXDTOПоИмениСвойства(
			РаботаСXMLЕГАИС.КорневоеПространствоИмен(), "Documents").Тип());
	Исключение
		
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		Попытка
			ОбъектXDTO = РаботаСXMLЕГАИС.ПреобразоватьПроизвольныйОбъектXDTOВОбъектXDTO(
				РаботаСXMLИС.ПроизвольныйОбъектXDTOПоТекстуСообщенияXML(ТекстСообщенияXML),
				РаботаСXMLИС.ОбъектXDTOПоИмениСвойства(РаботаСXMLЕГАИС.КорневоеПространствоИмен(), "Documents", Неопределено));
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки;
		КонецПопытки;
	КонецПопытки;
	
	ДанныеДокументаТТН = Неопределено;
	
	Если ОбъектXDTO <> Неопределено Тогда
		
		ТипЕГАИС           = Неопределено;
		ДанныеДокументаТТН = Неопределено;
		
		Если ОбъектXDTO <> Неопределено Тогда
			Попытка
				ДокументыПоТипамЕГАИС = РаботаСXMLИС.ОбъектXDTOВСтруктуру(ОбъектXDTO.Document);
				Для Каждого КлючИЗначение Из ДокументыПоТипамЕГАИС Цикл
					Если КлючИЗначение.Значение <> Неопределено Тогда
						ТипЕГАИС           = КлючИЗначение.Ключ;
						ДанныеДокументаТТН = ДокументыПоТипамЕГАИС[ТипЕГАИС];
						Прервать;
					КонецЕсли;
				КонецЦикла;
			Исключение
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			КонецПопытки;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстОшибки) Или ДанныеДокументаТТН = Неопределено Тогда
		Возврат АкцизныеМарки;
	КонецЕсли;
	
	Если ДанныеДокументаТТН.Content <> Неопределено Тогда
		
		Для Каждого СтрокаРасхожденияXDTO Из ДанныеДокументаТТН.Content.Position Цикл
			
			Если СтрокаРасхожденияXDTO.MarkInfo = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Для Каждого Штрихкод Из СтрокаРасхожденияXDTO.MarkInfo.amc Цикл
				АкцизныеМарки.Вставить(Штрихкод, Истина);
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат АкцизныеМарки;
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
	Возврат;
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

Функция ТекстЗапросаПродукцииДляПроверкиЦенВСпискеДокументов() Экспорт
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия,
	|	Товары.Цена,
	|	Товары.ИННКонтрагента,
	|	Товары.КППКонтрагента,
	|	Товары.КоэффициентЕГАИС
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДокументТовары.Номенклатура КАК Номенклатура,
	|		ДокументТовары.Характеристика КАК Характеристика,
	|		ДокументТовары.Серия КАК Серия,
	|		ДокументТовары.Цена КАК Цена,
	|		ДокументТовары.Ссылка.Грузоотправитель.ИНН КАК ИННКонтрагента,
	|		ДокументТовары.Ссылка.Грузоотправитель.КПП КАК КППКонтрагента,
	|		ВЫБОР
	|			КОГДА ДокументТовары.КоличествоУпаковок > 0
	|				ТОГДА ДокументТовары.Количество / ДокументТовары.КоличествоУпаковок
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК КоэффициентЕГАИС
	|	ИЗ
	|		Документ.ТТНИсходящаяЕГАИС.Товары КАК ДокументТовары
	|	ГДЕ
	|		ДокументТовары.Ссылка В (&СписокДокументов)
	|		И ДокументТовары.Ссылка.Грузополучатель.ТипОрганизации В (&ТипыОрганизаций)
	|		И ДокументТовары.АлкогольнаяПродукция.ВидПродукции.Маркируемый = ИСТИНА) КАК Товары
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия,
	|	Товары.Цена,
	|	Товары.ИННКонтрагента,
	|	Товары.КППКонтрагента,
	|	Товары.КоэффициентЕГАИС";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ВЫБОР КОГДА Грузоотправитель.Сопоставлено И Грузоотправитель.СоответствуетОрганизации Тогда ЗначениеРазрешено(Грузоотправитель.Контрагент)
	|	КОГДА Грузоотправитель.Сопоставлено И НЕ Грузоотправитель.СоответствуетОрганизации Тогда ЗначениеРазрешено(Грузоотправитель.ТорговыйОбъект)
	|	ИНАЧЕ ИСТИНА КОНЕЦ  И ВЫБОР КОГДА Грузополучатель.Сопоставлено И Грузополучатель.СоответствуетОрганизации ТОГДА ЗначениеРазрешено(Грузополучатель.Контрагент)
	|	КОГДА Грузополучатель.Сопоставлено И НЕ Грузополучатель.СоответствуетОрганизации ТОГДА ЗначениеРазрешено(Грузополучатель.ТорговыйОбъект)
	|	ИНАЧЕ ИСТИНА КОНЕЦ ";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти


