#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	Организация   = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.СчетФактураВыданныйАванс") Тогда
		
		РеквизитыСчетаФактуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Организация, Контрагент, ДокументОснование");
		
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеНДССПолученногоАванса;
		
		Организация = РеквизитыСчетаФактуры.Организация;
		Контрагент = РеквизитыСчетаФактуры.Контрагент;
		ДокументОплаты = РеквизитыСчетаФактуры.ДокументОснование;
		
		Документы.СписаниеНДСНаРасходы.ЗаполнитьТаблицуАвансыПолученные(ЭтотОбъект);
		
	ИначеЕсли ДанныеЗаполнения <> Неопределено 
		И ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("ХозяйственнаяОперация") Тогда
		
		ХозяйственнаяОперация = ДанныеЗаполнения.ХозяйственнаяОперация;
		
		Если ДанныеЗаполнения.Свойство("Организация") Тогда
			Организация = ДанныеЗаполнения.Организация;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("Контрагент") Тогда
			Контрагент = ДанныеЗаполнения.Контрагент;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("СчетФактура") 
				И ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеНДСПоПриобретеннымЦенностям Тогда
			СчетФактура = ДанныеЗаполнения.СчетФактура;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("ДокументОплаты") 
				И ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеНДССПолученногоАванса Тогда
			ДокументОплаты = ДанныеЗаполнения.ДокументОплаты;
		КонецЕсли;
		
		Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеНДСПоПриобретеннымЦенностям
				И ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Контрагент) И ЗначениеЗаполнено(СчетФактура) Тогда
			Документы.СписаниеНДСНаРасходы.ЗаполнитьТаблицуЦенности(ЭтотОбъект);
		КонецЕсли;
		Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеНДССПолученногоАванса
				И ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Контрагент) И ЗначениеЗаполнено(ДокументОплаты) Тогда
			Документы.СписаниеНДСНаРасходы.ЗаполнитьТаблицуЦенности(ЭтотОбъект);
		КонецЕсли;
	
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка." + Метаданные().Имя) Тогда
		
		ИсправлениеДокументов.ЗаполнитьИсправление(ЭтотОбъект, ДанныеЗаполнения);

	КонецЕсли;
	
	ПараметрыВыбораСтатейИАналитик = Документы.СписаниеНДСНаРасходы.ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ОбработкаЗаполнения(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
	СписаниеНДСНаРасходыЛокализация.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	Если ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.СписаниеНДСПоПриобретеннымЦенностям Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СчетФактура");
		МассивНепроверяемыхРеквизитов.Добавить("Ценности");
	КонецЕсли;
	
	Если ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.СписаниеНДССПолученногоАванса Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДокументОплаты");
		МассивНепроверяемыхРеквизитов.Добавить("АвансыПолученные");
	КонецЕсли;
	
	ПараметрыВыбораСтатейИАналитик = Документы.СписаниеНДСНаРасходы.ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, ПараметрыВыбораСтатейИАналитик);
 
	ИсправлениеДокументов.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
	Если Не Отказ Тогда
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	КонецЕсли;
	
	СписаниеНДСНаРасходыЛокализация.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
	СписаниеНДСНаРасходыЛокализация.ОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
	СписаниеНДСНаРасходыЛокализация.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ИсправлениеДокументов.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	Для каждого Строка Из Ценности Цикл
		Если НЕ ЗначениеЗаполнено(Строка.РеализацияЭкспорт) И Строка.РеализацияЭкспорт <> Неопределено Тогда
			Строка.РеализацияЭкспорт = Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	Если ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.СписаниеНДСПоПриобретеннымЦенностям Тогда
		Если ЗначениеЗаполнено(СчетФактура) Тогда
			СчетФактура = Неопределено;
		КонецЕсли;
		Если Ценности.Количество() > 0 Тогда
			Ценности.Очистить();
		КонецЕсли;
	КонецЕсли;
	
	Если ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.СписаниеНДССПолученногоАванса Тогда
		Если ЗначениеЗаполнено(ДокументОплаты) Тогда
			ДокументОплаты = Неопределено;
		КонецЕсли;
		Если АвансыПолученные.Количество() > 0 Тогда
			АвансыПолученные.Очистить();
		КонецЕсли;
	КонецЕсли;
	
	СуммаНДС = Ценности.Итог("НДС") + АвансыПолученные.Итог("НДС");
	СуммаНДСУпр = Ценности.Итог("НДСУпр") + АвансыПолученные.Итог("НДСУпр");
	СуммаБезНДС = Ценности.Итог("СуммаБезНДС") + АвансыПолученные.Итог("СуммаБезНДС");
	
	ОбщегоНазначенияУТ.ЗаполнитьИдентификаторыДокумента(ЭтотОбъект, "Ценности,АвансыПолученные");
	
	ПараметрыВыбораСтатейИАналитик = Документы.СписаниеНДСНаРасходы.ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ПередЗаписью(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
	СписаниеНДСНаРасходыЛокализация.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
	СписаниеНДСНаРасходыЛокализация.ПриЗаписи(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИсправлениеДокументов.ПриКопировании(ЭтотОбъект, ОбъектКопирования);
	
	Ответственный = Пользователи.ТекущийПользователь();
	Ценности.Очистить();
	СуммаБезНДС 			= 0;
	СуммаНДС    			= 0;
	
	ОбщегоНазначенияУТ.ОчиститьИдентификаторыДокумента(ЭтотОбъект, "Ценности,АвансыПолученные");
	
	СписаниеНДСНаРасходыЛокализация.ПриКопировании(ЭтотОбъект, ОбъектКопирования);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
