
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	СтруктураБыстрогоОтбора = Неопределено;
	Параметры.Свойство("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора);
	
	Если СтруктураБыстрогоОтбора <> Неопределено Тогда
		СтруктураБыстрогоОтбора.Свойство("Организация", Организация);
	КонецЕсли;
	
	ОтборыСписковКлиентСервер.ОтборПоЗначениюСпискаПриСозданииНаСервере(СписокРаспоряженийНаСписаниеСХранения, "Организация", Организация, СтруктураБыстрогоОтбора);
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	НастроитьСсылкуСмТакже();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ОтчетОСписанииТоваровСХранения" Тогда
		Элементы.СписокРаспоряженийНаСписаниеСХранения.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если СтруктураБыстрогоОтбора <> Неопределено Тогда
		СтруктураБыстрогоОтбора.Свойство("Организация", Организация);
		Настройки.Удалить("Организация");
	Иначе
		Организация = Настройки.Получить("Организация");
	КонецЕсли;
	
	МассивСписков = Новый Массив;
	МассивСписков.Добавить("СписокРаспоряженийНаСписаниеСХранения");
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ФормаСписка", ЭтаФорма);
	ПараметрыОтбора.Вставить("МассивСписков", МассивСписков);
	ПараметрыОтбора.Вставить("ИмяКолонки", "Организация");
	ПараметрыОтбора.Вставить("Значение", Организация);
	ПараметрыОтбора.Вставить("Настройки", Настройки);
	
	ОтборыСписковКлиентСервер.УстановитьОтборыПоЗначениюСпискаПриЗагрузкеИзНастроек(ПараметрыОтбора, СтруктураБыстрогоОтбора);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		СписокРаспоряженийНаСписаниеСХранения,
		"Организация",
		Организация,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(Организация));
	
КонецПроцедуры

&НаКлиенте
Процедура ПоставщикПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		СписокРаспоряженийНаСписаниеСХранения,
		"Партнер",
		Партнер,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(Партнер));
		
КонецПроцедуры

&НаКлиенте
Процедура СмТакжеВРаботеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтруктураБыстрогоОтбора = Новый Структура;
	СтруктураБыстрогоОтбора.Вставить("Организация", Организация);
	
	ПараметрыЖурнала = Новый Структура;
	ПараметрыЖурнала.Вставить("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора);
	ПараметрыЖурнала.Вставить("КлючНазначенияИспользования", "ОтчетОСписанииТоваровСХранения");
	
	ПараметрыЖурнала.Вставить("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора);
	
	ОткрытьФорму(НавигационнаяСсылкаФорматированнойСтроки, ПараметрыЖурнала); 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОформитьСписаниеСХранения(Команда)
	
	ТекущиеДанные = Элементы.СписокРаспоряженийНаСписаниеСХранения.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФормыПоступление = "Документ.ОтчетОСписанииТоваровСХранения.ФормаОбъекта";
	
	Основание = Новый Структура("Организация, Партнер, Контрагент, Договор");
	ЗаполнитьЗначенияСвойств(Основание, ТекущиеДанные);
	
	ПараметрыЗаполнения = Новый Структура("Основание", Основание);
	
	ОткрытьФорму(ИмяФормыПоступление, ПараметрыЗаполнения, ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьСсылкуСмТакже()
	
	ПараметрыФормирования = Новый Структура;
	ПараметрыФормирования.Вставить("КлючНазначенияИспользования", "ОтчетОСписанииТоваровСХранения");
	
	МассивМенеджеровРасчетаСмТакжеВРаботе = Новый Массив();
	МассивМенеджеровРасчетаСмТакжеВРаботе.Добавить("Обработка.ЖурналДокументовЗакупки");
	
	СмТакжеВРаботе = ОбщегоНазначенияУТ.СформироватьГиперСсылкуСмТакжеВРаботе(МассивМенеджеровРасчетаСмТакжеВРаботе, ПараметрыФормирования);
	Элементы.СмТакжеВРаботе.Видимость = ЗначениеЗаполнено(СмТакжеВРаботе);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	//
	
КонецПроцедуры

#КонецОбласти
