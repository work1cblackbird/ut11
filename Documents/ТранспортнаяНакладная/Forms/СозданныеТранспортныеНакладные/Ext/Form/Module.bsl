
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТранспортныеНакладные = Новый Массив;
	
	Если Параметры.Свойство("ТранспортныеНакладные", ТранспортныеНакладные) Тогда
		Для Каждого ТранспортнаяНакладная Из ТранспортныеНакладные Цикл
			СписокТранспортныхНакладных.Добавить(ТранспортнаяНакладная);
		КонецЦикла;
		Заголовок = НСтр("ru = 'Созданные транспортные накладные'");
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Печать") Тогда
		Элементы.ФормаОК.Заголовок = НСтр("ru = 'Перейти к печати'");
	КонецЕсли;
	
	ЗаданиеНаПеревозку = Документы.ЗаданиеНаПеревозку.ПустаяСсылка();
	Если Параметры.Свойство("ЗаданиеНаПеревозку", ЗаданиеНаПеревозку) Тогда
		
		АдресДоставки = "";
		Параметры.Свойство("АдресДоставки", АдресДоставки);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТранспортнаяНакладная.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ТранспортнаяНакладная КАК ТранспортнаяНакладная
		|ГДЕ
		|	ТранспортнаяНакладная.ЗаданиеНаПеревозку = &ЗаданиеНаПеревозку
		|	И (ТранспортнаяНакладная.АдресДоставки = &АдресДоставки
		|			ИЛИ &АдресДоставкиНеЗаполнен)
		|	И НЕ ТранспортнаяНакладная.ПометкаУдаления
		|	И ТранспортнаяНакладная.Проведен";
		
		Запрос.УстановитьПараметр("ЗаданиеНаПеревозку", ЗаданиеНаПеревозку);
		Запрос.УстановитьПараметр("АдресДоставки", АдресДоставки);
		Запрос.УстановитьПараметр("АдресДоставкиНеЗаполнен", НЕ ЗначениеЗаполнено(АдресДоставки));
		РезультатЗапроса = Запрос.Выполнить().Выбрать();
		
		Пока РезультатЗапроса.Следующий() Цикл
			СписокТранспортныхНакладных.Добавить(РезультатЗапроса.Ссылка);	
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТранспортныеНакладные

&НаКлиенте
Процедура ТранспортныеНакладныеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТранспортнаяНакладная = СписокТранспортныхНакладных.Получить(ВыбраннаяСтрока).Значение;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", ТранспортнаяНакладная);
	ОткрытьФорму("Документ.ТранспортнаяНакладная.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти
