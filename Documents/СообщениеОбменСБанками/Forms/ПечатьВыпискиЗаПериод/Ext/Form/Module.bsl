
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	НастройкиОбменСБанками.Организация КАК Организация,
	               |	НастройкиОбменСБанками.Банк КАК Банк
	               |ИЗ
	               |	Справочник.НастройкиОбменСБанками КАК НастройкиОбменСБанками";
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ТекстСообщения = НСтр("ru = 'Не найдено ни одной настройки обмена с банками через сервис 1С:ДиректБанк.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
	КонецЕсли;
	Выборка = Результат.Выбрать();
	
	МассивОрганизаций = Новый Массив;
	МассивБанков = Новый Массив;
	Пока Выборка.Следующий() Цикл
		МассивБанков.Добавить(Выборка.Банк);
		МассивОрганизаций.Добавить(Выборка.Организация);
	КонецЦикла;
	МассивОрганизаций = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивОрганизаций);
	МассивБанков = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивБанков);
	Организации.ЗагрузитьЗначения(МассивОрганизаций);
	Банки.ЗагрузитьЗначения(МассивБанков);
	
	Если Параметры.ВидОперации = "ПечатьВыписки" Тогда
		Заголовок = НСтр("ru = 'Печать выписки'");
	Иначе
		Заголовок = НСтр("ru = 'Печать комплекта документов'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СчетОрганизацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	Отбор = Новый Структура;
	Если Организации.Количество() = 1 И Банки.Количество() = 1 Тогда
		Отбор.Вставить("Организация", Организации.Получить(0).Значение);
		Отбор.Вставить("Банк", Банки.Получить(0).Значение);
	Иначе
		Отбор.Вставить("СписокОрганизаций", Организации.ВыгрузитьЗначения());
		Отбор.Вставить("СписокБанков", Банки.ВыгрузитьЗначения());
	КонецЕсли;
	
	ПараметрыФормы.Вставить("Отбор", Отбор);

	НазваниеСчетаВМетаданных = ОбменСБанкамиСлужебныйВызовСервера.НазваниеСчетаВМетаданных();
	
	ОткрытьФорму("Справочник." + НазваниеСчетаВМетаданных + ".ФормаВыбора", ПараметрыФормы, Элементы.СчетОрганизации);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Печать(Команда)
	
	ОчиститьСообщения();
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ЗапускЗаданияПоискаВыписокЗаПериод(Период, СчетОрганизации, УникальныйИдентификатор);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияВыписокЗаПериод", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ЗапускЗаданияПоискаВыписокЗаПериод(Знач Период, Знач СчетОрганизации, Знач УникальныйИдентификатор)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Поиск выписок за произвольный период.'");
	
	ПараметрыСинхронизации = Новый Структура;
	ПараметрыСинхронизации.Вставить("Период", Период);
	ПараметрыСинхронизации.Вставить("СчетОрганизации", СчетОрганизации);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиСлужебный.НайтиВыпискиЗаПроизвольныйПериод", ПараметрыСинхронизации, ПараметрыВыполнения);

КонецФункции

&НаКлиенте
Процедура ПослеПолученияВыписокЗаПериод(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда // задание было отменено
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.КраткоеПредставлениеОшибки);
	Иначе // выполнено
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если Не РезультатОперации.Количество() Тогда
			ТекстСообщения = НСтр("ru = 'Отсутствуют электронные выписки за указанный период'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		ИмяМенеджераПечати = "Документ.СообщениеОбменСБанками";
		ПараметрыПечати = Новый Структура;
		
		Если Параметры.ВидОперации = "ПечатьВыписки" Тогда
			ИмяМакета = "ВыпискаЗаПроизвольныйПериод";
			ПараметрыПечати.Вставить("Идентификатор", "ВыпискаЗаПроизвольныйПериод");
			Шаблон = НСтр("ru = 'Печать выписки за период %1'");
			ЗаголовокФормы = СтрШаблон(Шаблон, ПредставлениеПериода(Период.ДатаНачала, Период.ДатаОкончания));
			ПараметрыПечати.Вставить("ЗаголовокФормы", ЗаголовокФормы);
			ПараметрыПечати.Вставить("Представление", Заголовок);
		Иначе
			ИмяМакета = "КомплектДокументовЗаПроизвольныйПериод";
			ПараметрыПечати.Вставить("Идентификатор", "КомплектДокументовЗаПроизвольныйПериод");
			Шаблон = НСтр("ru = 'Печать комплекта документов за период %1'");
			ЗаголовокФормы = СтрШаблон(Шаблон, ПредставлениеПериода(Период.ДатаНачала, Период.ДатаОкончания));
			ПараметрыПечати.Вставить("ЗаголовокФормы", ЗаголовокФормы);
			ПараметрыПечати.Вставить("Представление", Заголовок);
		КонецЕсли;

		ПараметрыПечати.Вставить("Период", Период);

		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
			ИмяМенеджераПечати, ИмяМакета, РезультатОперации, ЭтотОбъект, ПараметрыПечати);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти