
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПризнакУведомления =              Параметры.ПризнакУведомления;
	КодФормыРеорганизации =           Параметры.КодФормыРеорганизации;
	ИННДоРеорганизации =              Параметры.ИННДоРеорганизации;
	КППДоРеорганизации =              Параметры.КППДоРеорганизации;
	НаименованиеПервичногоДокумента = Параметры.НаименованиеПервичногоДокумента;
	ДатаПервичногоДокумента =         Параметры.ДатаПервичногоДокумента;
	НомерПервичногоДокумента =        Параметры.НомерПервичногоДокумента;
	КодТНВЭД =                        Параметры.КодТНВЭД;
	ОКПД2 =                           Параметры.КодОКПД2;
	ЕдиницаИзмерения =                Параметры.ЕдиницаИзмерения;
	ЕдиницаПрослеживаемости =         Параметры.ЕдиницаПрослеживаемости;
	Количество =                      Параметры.Количество;
	КоличествоПрослеживаемости =      Параметры.КоличествоПрослеживаемости;
	Сумма =                           Параметры.Сумма;
	ДатаДокумента =                   Параметры.ДатаДокумента;
	Организация =                     Параметры.Организация;
	
	ФормаОткрытаИзКорректировочногоУведомления = Параметры.ФормаОткрытаИзКорректировочногоУведомления;
	
	Если Параметры.Свойство("Продавцы") Тогда
		
		Для каждого ТекущаяСтрока Из Параметры.Продавцы Цикл
			
			НоваяСтрока = Продавцы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Параметры.Свойство("ВидыЗапасов") Тогда
		
		Для каждого ТекущаяСтрока Из Параметры.ВидыЗапасов Цикл
			
			НоваяСтрока = ВидыЗапасов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			
		КонецЦикла;
		
	КонецЕсли;
	
	СписокКодовФормы = Элементы.ВыборКодаРеорганизации.СписокВыбора;
	
	ПрослеживаемостьБРУ.СписокКодовФормыРеорганизации(СписокКодовФормы);
	
	УправлениеФормой(ЭтотОбъект)
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область СтраницаПризнакУведомления

&НаКлиенте
Процедура ПризнакУведомленияПриИзменении(Элемент)
	
	ПриИзмененииПризнакаУведомления();
	
КонецПроцедуры

&НаКлиенте
Процедура ПризнакУведомления1ПриИзменении(Элемент)
	
	ПриИзмененииПризнакаУведомления();
	
КонецПроцедуры

&НаКлиенте
Процедура ПризнакУведомления2ПриИзменении(Элемент)
	
	ПриИзмененииПризнакаУведомления();
	
КонецПроцедуры

#КонецОбласти

#Область СтраницаФормаРеорганизации

&НаКлиенте
Процедура ИННДоРеорганизацииПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КППДоРеорганизацииПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура СписокКодовРеорганизацииПриИзменении(Элемент)
	
	ИННДоРеорганизации = "";
	КППДоРеорганизации = "";
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборКодаРеорганизацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОповещениеВыбора = Новый ОписаниеОповещения("ВыборИзСпискаЗавершение",ЭтотОбъект);
	ПоказатьВыборИзСписка(ОповещениеВыбора, Элемент.СписокВыбора, Элемент, КодФормыРеорганизации);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(КодФормыРеорганизации)
		И Не ЗначениеЗаполнено(ИННДоРеорганизации) Тогда
		
		ОбщегоНазначения.СообщитьПользователю(
		НСтр("ru = 'Не заполнен ИНН'"),
		,
		,
		,
		Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КодФормыРеорганизации)
		И Не ЗначениеЗаполнено(КППДоРеорганизации) Тогда
		
		ОбщегоНазначения.СообщитьПользователю(
		НСтр("ru = 'Не заполнен КПП'"),
		,
		,
		,
		Отказ);
	КонецЕсли;
	
	СписокПродавцов = Новый СписокЗначений;
	
	Для Индекс = 0 По Продавцы.Количество() - 1 Цикл
		
		СтрокаПродавцы = Продавцы[Индекс];
		
		ИННПродавца = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаПродавцы.Продавец, "ИНН");
		
		Если НЕ ЗначениеЗаполнено(ИННПродавца) Тогда
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В строке %1 продавца %2 не заполнен ИНН'"),
				Индекс + 1, СтрокаПродавцы.Продавец);
			ОбщегоНазначения.СообщитьПользователю(
				Текст,
				,
				,
				,
				Отказ);
		КонецЕсли;
		
		Если СписокПродавцов.НайтиПоЗначению(СтрокаПродавцы.Продавец)<> Неопределено Тогда
			
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В строке %1 повторно указан продавец %2.'"),
				Индекс + 1, СтрокаПродавцы.Продавец);
			ОбщегоНазначения.СообщитьПользователю(
				Текст,
				,
				,
				,
				Отказ);
			
		КонецЕсли;
		
		СписокПродавцов.Добавить(СтрокаПродавцы.Продавец);
		
	КонецЦикла;
	
	Если ПризнакУведомления = 3 
		И Продавцы.Количество() = 0 Тогда
		
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Не указан продавец'"),
			,
			,
			,
			Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		
		ПараметрыПередачи = Новый Структура;
		
		ПараметрыПередачи.Вставить("КодФормыРеорганизации", КодФормыРеорганизации);
		ПараметрыПередачи.Вставить("ИННДоРеорганизации", ИННДоРеорганизации);
		ПараметрыПередачи.Вставить("КППДоРеорганизации", КППДоРеорганизации);
		ПараметрыПередачи.Вставить("ПризнакУведомления", ПризнакУведомления);
		ПараметрыПередачи.Вставить("Продавцы", Продавцы);
		ПараметрыПередачи.Вставить("КодТНВЭД", КодТНВЭД);
		ПараметрыПередачи.Вставить("КодОКПД2", ОКПД2);
		ПараметрыПередачи.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
		ПараметрыПередачи.Вставить("ЕдиницаПрослеживаемости", ЕдиницаПрослеживаемости);
		ПараметрыПередачи.Вставить("Количество", Количество);
		ПараметрыПередачи.Вставить("КоличествоПрослеживаемости", КоличествоПрослеживаемости);
		ПараметрыПередачи.Вставить("Сумма", Сумма);
		ПараметрыПередачи.Вставить("НаименованиеПервичногоДокумента", НаименованиеПервичногоДокумента);
		ПараметрыПередачи.Вставить("ДатаПервичногоДокумента", ДатаПервичногоДокумента);
		ПараметрыПередачи.Вставить("НомерПервичногоДокумента", НомерПервичногоДокумента);
	
		ОповеститьОВыборе(ПараметрыПередачи);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаДобавитьПродавца(Команда)
	
	Элементы.Продавцы.ДобавитьСтроку();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьСуммы(Команда)
	
	Сумма = ЗаполнитьСуммыНаСервере();
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьСуммыНаСервере()
	
	Если ЭтотОбъект.ФормаОткрытаИзКорректировочногоУведомления Тогда
		Возврат Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров.ПолучитьСуммуПоКорректировочномуУведомлению(ЭтотОбъект.Количество,
			ЭтотОбъект.ДокументУведомлениеОбОстатках);
	Иначе
		Возврат Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров.ПолучитьСуммуПоУведомлению(ЭтотОбъект.ВидыЗапасов.Выгрузить(),
			ЭтотОбъект.ДатаДокумента , ЭтотОбъект.Организация);
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриИзмененииПризнакаУведомления()
	
	Продавцы.Очистить();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.Продавцы.Доступность = Форма.ПризнакУведомления <> 0;
	
	Если Форма.Продавцы.Количество() = 0 Тогда
		Элементы.Продавцы.ОтметкаНезаполненного = Форма.ПризнакУведомления = 3;
		Элементы.Продавцы.АвтоВводНовойСтроки = Форма.ПризнакУведомления = 3;
	КонецЕсли;
	
	Элементы.ФормаРеорганизации.Видимость = Форма.ФормаОткрытаИзКорректировочногоУведомления;
	Элементы.ГруппаДополнительныхРеквизитов.ОтображениеСтраниц = 
		?(Форма.ФормаОткрытаИзКорректировочногоУведомления,
			ОтображениеСтраницФормы.ЗакладкиСверху, ОтображениеСтраницФормы.Нет);
			
	Элементы.ДекорацияЗаголовокПризнакУведомления.Видимость = Не Форма.ФормаОткрытаИзКорректировочногоУведомления;
	
	Если Форма.ФормаОткрытаИзКорректировочногоУведомления Тогда
		
		Элементы.Продавцы.ВысотаВСтрокахТаблицы = 3;
		
		Элементы.ОписаниеДополнительныхРеквизитов.Заголовок = 
			НСтр("ru='Реквизиты заполняются, если ранее собственниками товара были физические лица или плательщики НПД 
			|или государственные органы. Или, если подается корректировочное уведомление за реорганизованную организацию'");
		
		ТекстПризнакаУведомленияДоИзменения = НСтр("ru='%1'"); 
				
		ЗаполненКодФормыРеорганизации = ЗначениеЗаполнено(Форма.КодФормыРеорганизации);
		Элементы.ИННДоРеорганизации.Доступность = ЗаполненКодФормыРеорганизации;
		Элементы.КППДоРеорганизации.Доступность = ЗаполненКодФормыРеорганизации;
		
		Если Не ЗаполненКодФормыРеорганизации Тогда
			
			Элементы.ДекорацияОписаниеКода.Заголовок = НСтр("ru='Отсутствует'");
			
		Иначе
			
			Элементы.ДекорацияОписаниеКода.Заголовок = 
			ПрослеживаемостьФормыВызовСервера.ОписаниеКодаРеорганизации(Форма.КодФормыРеорганизации);
			
		КонецЕсли;
		
		Элементы.ИННДоРеорганизации.ОтметкаНезаполненного = 
		?(ЗначениеЗаполнено(Форма.ИННДоРеорганизации), Ложь, ЗаполненКодФормыРеорганизации);
		
		Элементы.КППДоРеорганизации.ОтметкаНезаполненного = 
		?(ЗначениеЗаполнено(Форма.КППДоРеорганизации), Ложь, ЗаполненКодФормыРеорганизации);;
		
	Иначе
		
		Элементы.ОписаниеДополнительныхРеквизитов.Заголовок = 
			НСтр("ru='Реквизиты заполняются, если ранее собственниками товара были физические лица или плательщики НПД 
			|или государственные органы'");
		Элементы.Продавцы.ВысотаВСтрокахТаблицы = 6;
		
	КонецЕсли;
		
КонецПроцедуры
	
&НаКлиенте
Процедура ВыборИзСпискаЗавершение(ВыбранныйКод, ДополнительныеПараметры) Экспорт
	
	Модифицированность = Истина;
	КодФормыРеорганизации = ВыбранныйКод.Значение;

КонецПроцедуры

#КонецОбласти
