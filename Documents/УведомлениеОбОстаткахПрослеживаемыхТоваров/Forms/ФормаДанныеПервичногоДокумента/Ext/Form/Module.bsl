
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЭтотОбъект.Параметры.Свойство("ЗаполнениеПоОстаткам") Тогда
		Элементы.ДекорацияШапка.Заголовок =
			НСтр("ru='Уведомление заполняется на основании первичного учетного документа, подтверждающего право собственности участника оборота товаров, подлежащих прослеживаемости, на товары, подлежащие прослеживаемости.'")
	КонецЕсли;
	
	Элементы.ИнвентаризационнаяОпись.Видимость = ЭтотОбъект.Параметры.Свойство("ЗаполнениеПоОстаткам");
	Элементы.ВозвратТоваров.Видимость =  ЭтотОбъект.Параметры.Свойство("ЗаполнениеПоВозвратам");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИнвентаризационнаяОписьПриИзменении(Элемент)
	ИнвентаризационнаяОписьПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВозвратТоваровПриИзменении(Элемент)
	ВозвратТоваровПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодтвердитьВвод(Команда)
	
	ПараметрыЗакрытия = Новый Структура;
	
	Если ЗначениеЗаполнено(ИнвентаризационнаяОпись) Тогда
		ПараметрыЗакрытия.Вставить("ПервичныйДокумент", ИнвентаризационнаяОпись);
	ИначеЕсли ЗначениеЗаполнено(ВозвратТоваров) Тогда
		ПараметрыЗакрытия.Вставить("ПервичныйДокумент", ВозвратТоваров);
	Иначе
		ПараметрыЗакрытия.Вставить("ПервичныйДокумент", Неопределено);
	КонецЕсли;
	
	ПараметрыЗакрытия.Вставить("НаименованиеПервичногоДокумента", НаименованиеПервичногоДокумента);
	ПараметрыЗакрытия.Вставить("НомерПервичногоДокумента", НомерПервичногоДокумента);
	ПараметрыЗакрытия.Вставить("ДатаПервичногоДокумента", ДатаПервичногоДокумента);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнвентаризационнаяОписьПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(ИнвентаризационнаяОпись) Тогда
		
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ИнвентаризационнаяОпись, "Номер,Дата");
		
		НаименованиеПервичногоДокумента = ИнвентаризационнаяОпись.Метаданные().Синоним;
		НомерПервичногоДокумента = РеквизитыДокумента.Номер;
		ДатаПервичногоДокумента = РеквизитыДокумента.Дата;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВозвратТоваровПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(ВозвратТоваров) Тогда
		
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВозвратТоваров, "Номер,Дата");
		
		НаименованиеПервичногоДокумента = ВозвратТоваров.Метаданные().Синоним;
		НомерПервичногоДокумента = РеквизитыДокумента.Номер;
		ДатаПервичногоДокумента = РеквизитыДокумента.Дата;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
