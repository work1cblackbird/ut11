#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует параметры заполнения видов запасов дополнительных свойств документа, используемых при записи документа
// в режиме 'Проведения' или 'Отмены проведения'.
//
// Параметры:
//	ДокументОбъект - ДокументОбъект.УведомлениеОбОстаткахПрослеживаемыхТоваров - документ, для которого выполняется 
//																					инициализация параметров.
//	РежимЗаписи -РежимЗаписиДокумента - режим записи документа.
//
Процедура ИнициализироватьПараметрыЗаполненияВидовЗапасовДляПроведения(ДокументОбъект, РежимЗаписи = Неопределено) Экспорт
	
	ДокументОбъект.ДополнительныеСвойства.Вставить("ПараметрыЗаполненияВидовЗапасов", ПараметрыЗаполненияВидовЗапасов());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
	ИнициализироватьДокумент(ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Не Отказ
		И ОбщегоНазначенияУТ.ПроверитьЗаполнениеРеквизитовОбъекта(ЭтотОбъект, ПроверяемыеРеквизиты) Тогда
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение И ЗначениеЗаполнено(РНПТ) Тогда
		ШаблонСообщения = НСтр("ru = 'В строке %1 заполните Страну происхождения в Номере ГТД или в Номенклатуре.'");
		ТЧВидыЗапасов =  ВидыЗапасов.Выгрузить(, "НомерСтроки,АналитикаУчетаНоменклатуры,НомерГТД,РНПТ");
		ТЧВидыЗапасов.Колонки.Добавить("СтранаПроисхождения", Новый ОписаниеТипов("СправочникСсылка.СтраныМира")); 
		Запрос = Новый Запрос();
		Запрос.Текст = "ВЫБРАТЬ
		|	ВидыЗапасов.НомерСтроки КАК НомерСтроки,
		|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ВидыЗапасов.НомерГТД КАК НомерГТД,
		|	ВидыЗапасов.РНПТ КАК РНПТ
		|ПОМЕСТИТЬ ВидыЗапасов
		|ИЗ
		|	&ВидыЗапасов КАК ВидыЗапасов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВидыЗапасов.НомерСтроки КАК НомерСтроки,
		|	ВидыЗапасов.РНПТ КАК НомерГТД,
		|	ВидыЗапасов.РНПТ КАК РНПТ,
		|	ВЫБОР
		|		КОГДА СтраныМира.Ссылка ЕСТЬ NULL
		|			ТОГДА ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.СтранаПроисхождения
		|		ИНАЧЕ СтраныМира.Ссылка
		|	КОНЕЦ КАК СтранаПроисхождения
		|ИЗ
		|	ВидыЗапасов КАК ВидыЗапасов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК НомераГТД
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМира
		|			ПО НомераГТД.СтранаПроисхождения = СтраныМира.Ссылка
		|		ПО ВидыЗапасов.НомерГТД = НомераГТД.Ссылка";
		Запрос.УстановитьПараметр("ВидыЗапасов",ТЧВидыЗапасов);
		ТЧВидыЗапасов = Запрос.Выполнить().Выгрузить();
		Для Каждого Стр Из ТЧВидыЗапасов Цикл
			Если Не ЗначениеЗаполнено(Стр.СтранаПроисхождения) Тогда
				ТекстСообщения = СтрШаблон(ШаблонСообщения,	Стр.НомерСтроки);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,, Отказ);
			КонецЕсли;	
		КонецЦикла;
		
		ТЧОсновныеСредства =  ОсновныеСредства.Выгрузить(, "НомерСтроки, НомерГТД, СтранаПроисхождения");
		
		ТЧТМЦВЭксплуатации =  ТМЦВЭксплуатации.Выгрузить(, "НомерСтроки,Номенклатура,НомерГТД, РНПТ");
		ТЧТМЦВЭксплуатации.Колонки.Добавить("СтранаПроисхождения", Новый ОписаниеТипов("СправочникСсылка.СтраныМира"));
		ДанныеПоГТД = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ТЧТМЦВЭксплуатации.ВыгрузитьКолонку("НомерГТД") ,"СтранаПроисхождения");
		ДанныеПоНоменклатуре = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ТЧТМЦВЭксплуатации.ВыгрузитьКолонку("Номенклатура") ,"СтранаПроисхождения");
		Для Каждого Стр Из ТЧТМЦВЭксплуатации Цикл
			Стр.СтранаПроисхождения = ДанныеПоГТД[Стр.НомерГТД];
			Если Не ЗначениеЗаполнено(Стр.СтранаПроисхождения) Тогда
				Стр.СтранаПроисхождения = ДанныеПоНоменклатуре[Стр.Номенклатура];
			КонецЕсли;	
			Если Не ЗначениеЗаполнено(Стр.СтранаПроисхождения) Тогда
				ТекстСообщения = СтрШаблон(ШаблонСообщения,	Стр.НомерСтроки);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,, Отказ);
			КонецЕсли;	
			Стр.НомерГТД = Стр.РНПТ;
		КонецЦикла;
		
		Если Не Отказ Тогда
			УчетПрослеживаемыхТоваровЛокализация.ЗаполнитьНомера(ЭтотОбъект, ТЧВидыЗапасов, "ВидыЗапасов", "РНПТ");
			УчетПрослеживаемыхТоваровЛокализация.ЗаполнитьНомера(ЭтотОбъект, ТЧОсновныеСредства, "ОсновныеСредства", "НомерГТД");				
			УчетПрослеживаемыхТоваровЛокализация.ЗаполнитьНомера(ЭтотОбъект, ТЧТМЦВЭксплуатации, "ТМЦВЭксплуатации", "РНПТ");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ИнициализироватьПараметрыЗаполненияВидовЗапасовДляПроведения(ЭтотОбъект);
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ИнициализироватьПараметрыЗаполненияВидовЗапасовДляПроведения(ЭтотОбъект);
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ВидыЗапасов.Очистить();
	
	ОформленНаОснованииВозвратаТоваров = Ложь;
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Ответственный	= Пользователи.ТекущийПользователь();
	Организация		= ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	
	ЗаполнениеОбъектовПоСтатистике.ЗаполнитьРеквизитыОбъекта(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#Область ВидыЗапасов

Функция ПараметрыЗаполненияВидовЗапасов()
	
	ПараметрыЗаполнения = ЗапасыСервер.ПараметрыЗаполненияВидовЗапасов();
	ПараметрыЗаполнения.ПодбиратьВидыЗапасовПоИнтеркампани = Ложь;
	ПараметрыЗаполнения.ПодбиратьВТЧТоварыПринятыеНаОтветственноеХранение.Вставить(Перечисления.ТипыЗапасов.ТоварНаХраненииСПравомПродажи, "Всегда");
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
