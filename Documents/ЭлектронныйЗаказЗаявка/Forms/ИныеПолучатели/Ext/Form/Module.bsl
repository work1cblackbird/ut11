
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Организация") Тогда
		Организация = Параметры.Организация;
	КонецЕсли;
	
	Если Параметры.Свойство("ВидДокумента") Тогда
		ВидДокумента = Параметры.ВидДокумента;
	КонецЕсли;
	
	Если Параметры.Свойство("МассивПолучателей") Тогда
		Для Каждого ИДПолучателя Из Параметры.МассивПолучателей Цикл
			НовСтр = ИныеПолучатели.Добавить();	
			НовСтр.ИдентификаторИногоПолучателя = ИДПолучателя;
			НастройкиОбмена = ОбменСГИСЭПД.НастройкиУчетнойЗаписиСОтбором("Получатель", 
									Новый Структура("Отправитель,ИдентификаторПолучателя", Организация, ИДПолучателя));
			НовСтр.Контрагент = НастройкиОбмена.Получатель;	
		КонецЦикла;		
	КонецЕсли;
	
	Если Параметры.Свойство("ТолькоПросмотр") Тогда
		Элементы.ФормаОК.Видимость = Параметры.ТолькоПросмотр = Ложь;
		Элементы.ФормаДобавитьПолучателя.Доступность = Параметры.ТолькоПросмотр = Ложь;
		Элементы.ИныеПолучатели.ТолькоПросмотр = Параметры.ТолькоПросмотр;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	МассивПолучателей = Новый Массив;
	Для Каждого СтрТаб Из ИныеПолучатели Цикл
		Если МассивПолучателей.Найти(СтрТаб.ИдентификаторИногоПолучателя) = Неопределено Тогда
			МассивПолучателей.Добавить(СтрТаб.ИдентификаторИногоПолучателя);
		КонецЕсли;	
	КонецЦикла;
	
	Закрыть(МассивПолучателей);
	
КонецПроцедуры


&НаКлиенте
Процедура ДобавитьПолучателя(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("МножественныйВыбор", Ложь);
	
	ОписаниеДобавитьПолучателя_Закрытие = Новый ОписаниеОповещения("ДобавитьПолучателя_Закрытие", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.Контрагенты.ФормаВыбора", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор, , , 
					ОписаниеДобавитьПолучателя_Закрытие, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПолучателя_Закрытие(ЗначениеВыбора, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеНастроитьОбменСКонтрагентом_Завершение = Новый ОписаниеОповещения("НастроитьОбменСКонтрагентом_Завершение", ЭтотОбъект);
	ОбменСГИСЭПДКлиент.ПолучитьНастройкиОтправкиЭДО(Организация, ЗначениеВыбора, ВидДокумента, ОписаниеНастроитьОбменСКонтрагентом_Завершение);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОбменСКонтрагентом_Завершение(Настройка, ДополнительныеПараметры) Экспорт
	
	Если Настройка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НовСтр = ИныеПолучатели.Добавить();
	НовСтр.Контрагент = Настройка.Получатель;
	НовСтр.ИдентификаторИногоПолучателя = Настройка.ИдентификаторПолучателя;
	
КонецПроцедуры
