#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьПараметрыОтчета(ПараметрКоманды, ИмяОтчета, ПараметрыФормы)

	РеквизитыДокумента = Документы.ПередачаТоваровМеждуОрганизациями.РеквизитыДокумента(ПараметрКоманды);
	Если РеквизитыДокумента.РасчетыЧерезОтдельногоКонтрагента Тогда
		ИмяОтчета = "РасчетыСПартнерами";
		ПараметрыФормы.КлючВарианта = "РасчетыСПартнерами";
		Если РеквизитыДокумента.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссиюВДругуюОрганизацию Тогда
			ПараметрыФормы.Отбор = Новый Структура("Партнер, Контрагент",
				РеквизитыДокумента.Партнер,
				РеквизитыДокумента.Контрагент);
			ПараметрыФормы.КлючНазначенияИспользования = "ПартнерКонтрагент";
		Иначе
			ПараметрыФормы.Отбор = Новый Структура("Заказ", ПараметрКоманды);
			ПараметрыФормы.КлючНазначенияИспользования = "ЗаказКлиента";
		КонецЕсли;
	Иначе
		ИмяОтчета = "РасчетыМеждуОрганизациями";
		ПараметрыФормы.КлючВарианта = "РасчетыПоПродажам";
		Если РеквизитыДокумента.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссиюВДругуюОрганизацию Тогда
			ПараметрыФормы.Отбор = Новый Структура("Продавец, Покупатель",
				РеквизитыДокумента.ОрганизацияОтправитель,
				РеквизитыДокумента.ОрганизацияПолучатель,);
			ПараметрыФормы.КлючНазначенияИспользования = "ПродавецПокупатель";
		Иначе
			ПараметрыФормы.Отбор = Новый Структура("ДокументРасчетов", ПараметрКоманды);
			ПараметрыФормы.КлючНазначенияИспользования = "Документ";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // СформироватьПараметрыОтчета()

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Перем ИмяОтчета;
	
	ПараметрыФормы = Новый Структура("Отбор, ФиксированныеНастройки, КлючНазначенияИспользования, КлючВарианта, СформироватьПриОткрытии");
	ПараметрыФормы.СформироватьПриОткрытии = Истина;
	
	СформироватьПараметрыОтчета(ПараметрКоманды, ИмяОтчета, ПараметрыФормы);
		
	ОткрытьФорму("Отчет." + ИмяОтчета + ".Форма",
		ПараметрыФормы,
		ПараметрыВыполненияКоманды.Источник,
		ПараметрыВыполненияКоманды.Уникальность,
		ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры // ОбработкаКоманды()

#КонецОбласти