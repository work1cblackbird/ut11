
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ЗаполнитьДоступныеДействия();
	
	СтандартныеПодсистемыСервер.УстановитьУсловноеОформлениеПоляДата(ЭтотОбъект, "Список.Дата", Элементы.Дата.Имя);
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	ОбеспечениеВДокументахКлиент.ПроверитьЗапуститьФоновоеЗаданиеРаспределенияЗапасов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.Список);
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры


// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(
		Команда,
		ЭтотОбъект,
		Элементы.Список);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

// МенюОтчеты


&НаКлиенте
Процедура СоздатьСнятиеРезерваПоНазначению(Команда)
	
	СоздатьКорректировку(ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкиНазначения.СнятьРезерв"));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСнятиеРезерваМногихНазначений(Команда)
	
	СоздатьКорректировку(ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкиНазначения.СнятьРезервПоМногимНазначениям"));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРезервированиеНаСкладахИПеремещениеРезервов(Команда)
	
	СоздатьКорректировку(ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкиНазначения.РезервироватьИКорректировать"));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРезервированиеНаСкладах(Команда)
	
	СоздатьКорректировку(ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкиНазначения.Резервировать"));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКорректировкуНазначения(Команда)
	
	СоздатьКорректировку(ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкиНазначения.ПроизвольнаяКорректировкаНазначений"));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКорректировку(ВидОперации)
	
	СтруктураОснование = Новый Структура("ВидОперации", ВидОперации);
	СтруктураПараметры = Новый Структура("Основание", СтруктураОснование);
	
	ОткрытьФорму("Документ.КорректировкаНазначенияТоваров.ФормаОбъекта", СтруктураПараметры, Элементы.Список);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДоступныеДействия()
	
	ДоступныеДействия = Документы.КорректировкаНазначенияТоваров.ДоступныеДействия();
	
	Если ДоступныеДействия.Найти(Перечисления.ВидыОперацийКорректировкиНазначения.СнятьРезервПоМногимНазначениям)
			= Неопределено Тогда
		Элементы.ФормаСоздатьСнятиеМногихНазначений.Видимость = Ложь;
	КонецЕсли;
	
	Если ДоступныеДействия.Найти(Перечисления.ВидыОперацийКорректировкиНазначения.РезервироватьИКорректировать)
			= Неопределено Тогда
		Элементы.ФормаСоздатьРезервированиеНаСкладахИПеремещениеРезервов.Видимость = Ложь;
	КонецЕсли;
	
	Если ДоступныеДействия.Найти(Перечисления.ВидыОперацийКорректировкиНазначения.ПроизвольнаяКорректировкаНазначений)
			= Неопределено Тогда
		Элементы.СоздатьКорректировкуНазначения.Видимость = Ложь;
	КонецЕсли;
	
	Если Не ПравоДоступа("Добавление", Метаданные.Документы.КорректировкаНазначенияТоваров) Тогда
		
		Элементы.ФормаСоздатьСнятиеРезерваПоНазначению.Доступность = Ложь;
		Элементы.ФормаСоздатьСнятиеМногихНазначений.Доступность = Ложь;
		Элементы.ФормаСоздатьРезервированиеНаСкладах.Доступность = Ложь;
		Элементы.ФормаСоздатьРезервированиеНаСкладахИПеремещениеРезервов.Доступность = Ложь;
		Элементы.ФормаГруппаСоздать.Доступность = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти