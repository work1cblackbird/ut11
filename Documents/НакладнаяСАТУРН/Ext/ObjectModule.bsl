#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ИнтеграцияСАТУРНПереопределяемый.ОбработкаЗаполненияДокумента(ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	
	ЗаполнитьОбъектПоСтатистике();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	НепроверяемыеРеквизиты.Добавить("Товары.Серия");
	НепроверяемыеРеквизиты.Добавить("ДатаПолучения");
	НепроверяемыеРеквизиты.Добавить("ДатаВозврата");
	НепроверяемыеРеквизиты.Добавить("ГрузополучательМестоХранения");
	
	ОбязательныеРеквизиты = Новый Массив;
	
	ТекущееСостояние = РегистрыСведений.СтатусыДокументовСАТУРН.ТекущееСостояние(Ссылка);
	СтатусСАТУРН = ТекущееСостояние.Статус;
	
	ГрузополучательСопоставленСОрганизацией  = Ложь;
	ГрузоотправительСопоставленСОрганизацией = Ложь;
	
	Организации = Новый Массив;
	Организации.Добавить(ГрузоотправительОрганизацияСАТУРН);
	Организации.Добавить(ГрузополучательОрганизацияСАТУРН);
	ЗначенияСопоставлений = Справочники.КлассификаторОрганизацийСАТУРН.ОрганизацииКонтрагентыПоКлассификаторамСАТУРН(
		Организации);
	
	СвязанноеЗначение = ЗначенияСопоставлений.Получить(ГрузополучательОрганизацияСАТУРН);
	Если СвязанноеЗначение <> Неопределено И ЗначениеЗаполнено(СвязанноеЗначение.Организация) Тогда
		ГрузополучательСопоставленСОрганизацией = Истина;
	КонецЕсли;
	
	СвязанноеЗначение = ЗначенияСопоставлений.Получить(ГрузоотправительОрганизацияСАТУРН);
	Если СвязанноеЗначение <> Неопределено И ЗначениеЗаполнено(СвязанноеЗначение.Организация) Тогда
		ГрузоотправительСопоставленСОрганизацией = Истина;
	КонецЕсли;
	
	Если Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПеревозка Тогда
		Если ГрузополучательСопоставленСОрганизацией
			И УказыватьДатуПолучения(СтатусСАТУРН) Тогда
			ОбязательныеРеквизиты.Добавить("ДатаПолучения");
		КонецЕсли;
		Если ГрузоотправительСопоставленСОрганизацией
			И УказыватьДатуВозврата(СтатусСАТУРН) Тогда
			ОбязательныеРеквизиты.Добавить("ДатаВозврата");
		КонецЕсли;
		
		ТекущееСостояние = РегистрыСведений.СтатусыДокументовСАТУРН.ТекущееСостояние(Ссылка);
		СтатусСАТУРН = ТекущееСостояние.Статус;
		
		Если СтатусСАТУРН = Перечисления.СтатусыОбработкиНакладнойСАТУРН.ВПути
			Или СтатусСАТУРН = Перечисления.СтатусыОбработкиНакладнойСАТУРН.ПодтверждениеОшибка Тогда
			
			Для Каждого СтрокаТовары Из Товары Цикл
				
				Если Не ЗначениеЗаполнено(СтрокаТовары.ПричинаРасхождения) 
					И (СтрокаТовары.КоличествоУпаковокПринято <> СтрокаТовары.КоличествоУпаковок
					   Или СтрокаТовары.КоличествоСАТУРН <> СтрокаТовары.КоличествоПринятоСАТУРН)
					Тогда
					
					ОбщегоНазначения.СообщитьПользователю(
							НСтр("ru = 'Укажите причину расхождения'"),
							ЭтотОбъект, 
							ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
								"Товары", СтрокаТовары.НомерСтроки, "ПричинаРасхождения"),,
							Отказ);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортЧерезПунктДосмотра Тогда
		Если УказыватьДатуПолучения(СтатусСАТУРН) Тогда
			ОбязательныеРеквизиты.Добавить("ДатаПолучения");
			ОбязательныеРеквизиты.Добавить("ГрузополучательМестоХранения");
		КонецЕсли;
		Если УказыватьДатуЭкспорта(СтатусСАТУРН)
			И Не ЗначениеЗаполнено(ДатаВозврата) Тогда
			ТекстСообщения  = Нстр("ru = 'Поле ""Дата окончания досмотра"" не заполнено'");
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				"ДатаВозврата",,
				Отказ);
		КонецЕсли;
	КонецЕсли;
	
	НепроверяемыеРеквизиты = ОбщегоНазначенияКлиентСервер.РазностьМассивов(НепроверяемыеРеквизиты, ОбязательныеРеквизиты);
	ИнтеграцияСАТУРНПереопределяемый.ПриОпределенииОбработкиПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	ИнтеграцияСАТУРН.ЗаписатьСоответствиеНоменклатуры(ЭтотОбъект,, "ВыделеннаяПартия");
	
	ИнтеграцияИСПереопределяемый.ПередЗаписьюОбъекта(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	ИнтеграцияСАТУРН.ЗаписатьСтатусДокументаПоУмолчанию(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Идентификатор = Неопределено;
	ДатаПолучения = Дата(1, 1, 1);
	ДатаВозврата  = Дата(1, 1, 1);
	ВходящаяНакладная = Ложь;
	
	ОчищаемыеРеквизиты = Новый Структура;
	ОчищаемыеРеквизиты.Вставить("Партия",                       Неопределено);
	ОчищаемыеРеквизиты.Вставить("ВыделеннаяПартия",             Неопределено);
	ОчищаемыеРеквизиты.Вставить("ПартияВозврат",                Неопределено);
	ОчищаемыеРеквизиты.Вставить("КоличествоПринятоСАТУРН",      0);
	ОчищаемыеРеквизиты.Вставить("КоличествоВозвращеноСАТУРН",   0);
	ОчищаемыеРеквизиты.Вставить("КоличествоУпаковокПринято",    0);
	ОчищаемыеРеквизиты.Вставить("КоличествоУпаковокВозвращено", 0);
	ОчищаемыеРеквизиты.Вставить("КоличествоПринято",            0);
	ОчищаемыеРеквизиты.Вставить("КоличествоВозвращено",         0);
	ОчищаемыеРеквизиты.Вставить("ИдентификаторСтрокиСАТУРН",    0);
	ОчищаемыеРеквизиты.Вставить("КодПартии",                    "");
	ОчищаемыеРеквизиты.Вставить("ПричинаРасхождения",           "");
	
	Для Каждого СтрокаТовары Из Товары Цикл
		ЗаполнитьЗначенияСвойств(СтрокаТовары, ОчищаемыеРеквизиты);
		СтрокаТовары.КоличествоУпаковокПринято = СтрокаТовары.КоличествоУпаковок;
		СтрокаТовары.КоличествоПринято         = СтрокаТовары.Количество;
		СтрокаТовары.КоличествоПринятоСАТУРН   = СтрокаТовары.КоличествоСАТУРН;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа
	ИнтеграцияИС.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	// Инициализация данных документа
	Документы.НакладнаяСАТУРН.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ИнтеграцияИС.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	РегистрыНакопления.ОстаткиПартийСАТУРН.ОтразитьДвижения(ДополнительныеСвойства, Движения, Отказ);
	РегистрыСведений.МестаХраненияПартийСАТУРН.ОтразитьДвижения(ДополнительныеСвойства, Движения, Отказ);
	ИнтеграцияИСПереопределяемый.ОтразитьДвижения(ДополнительныеСвойства, Движения, Отказ);
	
	ИнтеграцияИС.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ИнтеграцияИСПереопределяемый.ОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения);
	
	ИнтеграцияИС.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаЗаполнения

Процедура ЗаполнитьОбъектПоСтатистике()
	
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтатистики = ЗаполнениеОбъектовПоСтатистикеСАТУРН.ДанныеЗаполненияНакладнаяСАТУРН(ГрузоотправительОрганизацияСАТУРН, ГрузополучательОрганизацияСАТУРН);
	
	Для Каждого КлючИЗначение Из ДанныеСтатистики Цикл
		ЗаполнениеОбъектовПоСтатистикеСАТУРН.ЗаполнитьПустойРеквизит(ЭтотОбъект, ДанныеСтатистики, КлючИЗначение.Ключ);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

Функция УказыватьДатуПолучения(СтатусСАТУРН)
	
	Если СтатусСАТУРН = Перечисления.СтатусыОбработкиНакладнойСАТУРН.Черновик
		Или СтатусСАТУРН = Перечисления.СтатусыОбработкиНакладнойСАТУРН.Обрабатывается
		Или СтатусСАТУРН = Перечисления.СтатусыОбработкиНакладнойСАТУРН.Ошибка Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция УказыватьДатуВозврата(СтатусСАТУРН)
	
	Если СтатусСАТУРН = Перечисления.СтатусыОбработкиНакладнойСАТУРН.КВозврату
		Или СтатусСАТУРН = Перечисления.СтатусыОбработкиНакладнойСАТУРН.ПодтверждениеВозвратаОшибка
		Или СтатусСАТУРН = Перечисления.СтатусыОбработкиНакладнойСАТУРН.ПодтверждениеУтратыГрузаОшибка
		Или СтатусСАТУРН = Перечисления.СтатусыОбработкиНакладнойСАТУРН.ПринятЧастично Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция УказыватьДатуЭкспорта(СтатусСАТУРН)
	
	Если СтатусСАТУРН = Перечисления.СтатусыОбработкиНакладнойСАТУРН.ЭкспортПринятНаДосмотр
		Или СтатусСАТУРН = Перечисления.СтатусыОбработкиНакладнойСАТУРН.ЭкспортПодтвержденОшибка
		Или СтатусСАТУРН = Перечисления.СтатусыОбработкиНакладнойСАТУРН.ЭкспортОтказаноОшибка Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#КонецЕсли
