#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Статусы

// Возвращает конечные статусы.
// 
// Параметры:
//  ТребуетсяПовторноеОформление - Булево - Требуется повторное оформление
// 
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП - Конечные статусы.
Функция КонечныеСтатусы(ТребуетсяПовторноеОформление = Истина) Экспорт
	
	Статусы = Новый Массив;
	Возврат Статусы;
	
КонецФункции

// Возвращает статусы ошибок.
//
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП - Статусы ошибок.
//
Функция СтатусыОшибок() Экспорт
	
	Статусы = Новый Массив;
	
	Статусы.Добавить(Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.Ошибка);
	Статусы.Добавить(Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.ОшибкаКонтрольноЛогическаяПроверкаНеПройдена);
	Статусы.Добавить(Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.ОшибкаОбработки);
	Статусы.Добавить(Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.ОшибкаОтклоненОператором);
	Статусы.Добавить(Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.ОшибкаПроверкаФорматаНеПройдена);
	
	Возврат Статусы;
	
КонецФункции

// Возвращает статус по умолчанию.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП - Статус по-умолчанию.
//
Функция СтатусПоУмолчанию() Экспорт

	Возврат Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.Черновик;

КонецФункции
	
// Возвращает дальнейшее действие по умолчанию.
//
// Параметры:
//   СтруктураПараметров - Неопределено, Структура - Параметры для расчета:
// 	 * ОбъектРасчета - ДокументСсылка, ДанныеФормыСтруктураСКоллекцией - Объект для расчета допустимых действий.
// Возвращаемое значение:
//  ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюИСМП - Дальнейшее действие по-умолчанию.
//
Функция ДальнейшееДействиеПоУмолчанию(СтруктураПараметров = Неопределено) Экспорт
	
	ДальнейшиеДействия = Новый Массив;
	
	Если СтруктураПараметров <> Неопределено И СтруктураПараметров.Свойство("ОбъектРасчета") Тогда
		ДобавитьВМассивДоступныеДействия(ДальнейшиеДействия, СтруктураПараметров.ОбъектРасчета);
	Иначе
		ДальнейшиеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗапроситеКодыМаркировки);
	КонецЕсли;
	
	Возврат ДальнейшиеДействия;

КонецФункции

// Допустимые действия.
// 
// Параметры:
//  Объект - ДокументОбъект.ЗаказНаЭмиссиюКодовМаркировкиСУЗ - Объект
// 
// Возвращаемое значение:
//  Массив Из ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюИСМП - Допустимые действия
Функция ДопустимыеДействия(Объект) Экспорт
	
	ДопустимыеДействия = Новый Массив;
	
	ДобавитьВМассивДоступныеДействия(ДопустимыеДействия, Объект);
	
	Если Объект.ВидПродукции = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха Тогда
		ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗакройтеЗаказНаЭмиссию);
		ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ВыполнитеПолучениеКодовМаркировки);
	КонецЕсли;
	
	ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОтменитеОперацию);
	ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОтменитеПередачуДанных);
	
	Возврат ДопустимыеДействия;
	
КонецФункции

// Конвертирует общий статус документа ИС МП в статус текущего документа.
// 
// Параметры:
// 	ИсходныйСтатус - ПеречислениеСсылка.СтатусыДокументовИСМП - Исходный статус.
// Возвращаемое значение:
// 	ПеречислениеСсылка.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП,
// 	ПеречислениеСсылка.СтатусыДокументовИСМП - Конвертированный статус.
//
Функция КонвертированныйОбщийСтатус(ИсходныйСтатус) Экспорт
	
	Если ИсходныйСтатус = Перечисления.СтатусыДокументовИСМП.ЕстьОшибки
		Или ИсходныйСтатус = Перечисления.СтатусыДокументовИСМП.Ошибка Тогда
		ИтоговыйСтатус = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.Ошибка;
	ИначеЕсли ИсходныйСтатус = Перечисления.СтатусыДокументовИСМП.Неопределен Тогда
		ИтоговыйСтатус = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.ИСМПНаРассмотренииОператором;
	ИначеЕсли ИсходныйСтатус = Перечисления.СтатусыДокументовИСМП.НеПроверен Тогда
		ИтоговыйСтатус = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.ОшибкаПроверкаФорматаНеПройдена;
	ИначеЕсли ИсходныйСтатус = Перечисления.СтатусыДокументовИСМП.Обрабатывается Тогда
		ИтоговыйСтатус = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.ИСМПОбрабатывается;
	ИначеЕсли ИсходныйСтатус = Перечисления.СтатусыДокументовИСМП.Проверен Тогда
		ИтоговыйСтатус = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.ИСМПGTINНаОстаткиПолучены;
	Иначе 
		ИтоговыйСтатус = ИсходныйСтатус;
	КонецЕсли;
	
	Возврат ИтоговыйСтатус;
	
КонецФункции

// Возвращет признак отправки документа в сервис.
// 
// Параметры:
// 	СсылкаНаОбъект - ДокументСсылка.ЗаказНаЭмиссиюКодовМаркировкиСУЗ - Ссылка на документ.
// Возвращаемое значение:
// 	Булево - Документ отправлен и статус не ошибочный.
Функция ДокументОтправлен(СсылкаНаОбъект) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Статусы.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.СтатусыДокументовИСМП КАК Статусы
	|ГДЕ
	|	Статусы.Документ = &Документ");
	
	Запрос.УстановитьПараметр("Документ", СсылкаНаОбъект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Статус = Выборка.Статус;
		
		Если (Статус <> Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.Черновик
			И Статус <> Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.ИСМПGTINНаОстаткиПолучены
			И СтатусыОшибок().Найти(Статус) = Неопределено) Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область ДействияПриОбменеИСМП

// Статус после подготовки к передаче данных
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ЗаказНаЭмиссиюКодовМаркировкиСУЗ - Ссылка на документ
//  Операция - ПеречислениеСсылка.ВидыОперацийИСМП - Операция ИС МП
//  ДополнительныеПараметры - Структура - Дополнительные параметры
//
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * НовыйСтатус         - ПеречислениеСсылка.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП - Новый статус.
//   * ДальнейшееДействие1 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюИСМП - Дальнейшее действие 1.
//   * ДальнейшееДействие2 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюИСМП - Дальнейшее действие 2.
//   * ДальнейшееДействие3 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюИСМП - Дальнейшее действие 3.
//
Функция СтатусПослеПодготовкиКПередачеДанных(ДокументСсылка, Операция, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Операция = Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировки
	 Или Операция = Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировкиЗапросGTINНаОстатки Тогда
		
		ВидПродукцииИС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ВидПродукции");
		
		Если ОбщегоНазначенияИСКлиентСервер.ЭтоПродукцияМОТП(ВидПродукцииИС) Тогда
			ПараметрыОбновления = РегистрыСведений.СтатусыДокументовИСМП.РассчитатьСтатусыКПередаче(
				ДокументСсылка,
				Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.КПередачеВСУЗ);
		ИначеЕсли ОбщегоНазначенияИСПовтИсп.ЭтоПродукцияИСМП(ВидПродукцииИС) Тогда
			ПараметрыОбновления = РегистрыСведений.СтатусыДокументовИСМП.РассчитатьСтатусыКПередаче(
				ДокументСсылка,
				Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.КПередачеВИСМП);
		КонецЕсли;
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийИСМП.ЗакрытиеЗаказаНаЭмиссиюСУЗ Тогда
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовИСМП.РассчитатьСтатусыКПередаче(
			ДокументСсылка,
			Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.КЗакрытиюЗаказаВСУЗ);
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийИСМП.ПолучениеКодовМаркировкиИзСУЗНезависимо Тогда
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовИСМП.РассчитатьСтатусыКПередаче(
			ДокументСсылка,
			Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.КПолучениюКодовИзСУЗ);
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийИСМП.ПовторноеПолучениеКодовМаркировкиИзСУЗ  Тогда
	ИначеЕсли Операция = Перечисления.ВидыОперацийИСМП.ПолучениеКодовМаркировкиИзСУЗ Тогда
	ИначеЕсли Операция = Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировкиРасчетСтатуса Тогда
	ИначеЕсли Операция = Перечисления.ВидыОперацийИСМП.ЗакрытиеПодзаказаНаЭмиссиюКодовМаркировкиСУЗ Тогда
	Иначе
		ВызватьИсключение ОбщегоНазначенияИС.ТекстИсключенияОбработкиСтатуса(ДокументСсылка, Операция);
	КонецЕсли;
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Статус после передачи данных
//
// Параметры:
//  ДокументСсылка  - ДокументСсылка.ЗаказНаЭмиссиюКодовМаркировкиСУЗ  - Ссылка на документ
//  Операция        - ПеречислениеСсылка.ВидыОперацийИСМП              - Операция ИС МП
//  СтатусОбработки - ПеречислениеСсылка.СтатусыОбработкиСообщенийИСМП - Статус обработки сообщения
//  ДополнительныеПараметры - Структура - Структура со свойствами:
//   * ФорматОбмена              - ПеречислениеСсылка.ВерсииФорматаОбменаСУЗ      - Формат обмена с СУЗ
//   * СтанцияУправленияЗаказами - СправочникСсылка.СтанцииУправленияЗаказамиИСМП - СУЗ
//   * Назначение                - ПеречислениеСсылка.НазначениеСообщенийИСМП     - Назначение сообщения.
//
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * НовыйСтатус - ПеречислениеСсылка.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП - Новый статус.
//   * ДальнейшееДействие1 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюИСМП - Дальнейшее действие 1.
//   * ДальнейшееДействие2 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюИСМП - Дальнейшее действие 2.
//   * ДальнейшееДействие3 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюИСМП - Дальнейшее действие 3.
//
Функция СтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если СтатусОбработки = Неопределено Тогда
		СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаПринята;
	КонецЕсли;
	
	Если Операция = Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировки Тогда
		
		ВидПродукцииИС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ВидПродукции");
		
		Если ОбщегоНазначенияИСКлиентСервер.ЭтоПродукцияМОТП(ВидПродукцииИС) Тогда
			
			СтатусыБазовыйПроцесс = РегистрыСведений.СтатусыДокументовИСМП.СтруктураСтатусы();
			
			СтатусыБазовыйПроцесс.Принят = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.СУЗПринятКОбработке;
			СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОжидайтеЗавершенияОбработкиДанныхИСМП);
			
			СтатусыБазовыйПроцесс.Ошибка = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.Ошибка;
			СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗапроситеКодыМаркировки);
			СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОтменитеОперацию);
			
			ПараметрыОбновления = РегистрыСведений.СтатусыДокументовИСМП.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, СтатусыБазовыйПроцесс);
			
		ИначеЕсли ОбщегоНазначенияИСПовтИсп.ЭтоПродукцияИСМП(ВидПродукцииИС) Тогда
			
			СтатусыБазовыйПроцесс = РегистрыСведений.СтатусыДокументовИСМП.СтруктураСтатусы();
			
			Если ДополнительныеПараметры <> Неопределено
				И ДополнительныеПараметры.Назначение = Перечисления.НазначениеСообщенийИСМП.ИСМП Тогда
				СтатусыБазовыйПроцесс.Принят = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.ИСМПКПроверкеФормата;
			Иначе
				СтатусыБазовыйПроцесс.Принят = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.СУЗПринятКОбработке;
			КонецЕсли;
			
			СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОжидайтеЗавершенияОбработкиДанныхИСМП);
			
			Если ДополнительныеПараметры <> Неопределено
				И ДополнительныеПараметры.Назначение = Перечисления.НазначениеСообщенийИСМП.ИСМП Тогда
				СтатусыБазовыйПроцесс.Ошибка = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.ОшибкаПроверкаФорматаНеПройдена;
			Иначе
				СтатусыБазовыйПроцесс.Ошибка = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.Ошибка;
			КонецЕсли;
			
			СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗапроситеКодыМаркировки);
			СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОтменитеОперацию);
			
			ПараметрыОбновления = РегистрыСведений.СтатусыДокументовИСМП.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, СтатусыБазовыйПроцесс);
			
		КонецЕсли;
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировкиЗапросGTINНаОстатки Тогда
		
		СтатусыБазовыйПроцесс = РегистрыСведений.СтатусыДокументовИСМП.СтруктураСтатусы();
			
		Если СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОтклонена Тогда
			СтатусыБазовыйПроцесс.Принят = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.Ошибка;
		Иначе
			СтатусыБазовыйПроцесс.Принят = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.ИСМПОписаниеОстатков;
		КонецЕсли;
		
		СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗапроситеGTINНаОстатки);
		СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОтменитеОперацию);
		СтатусыБазовыйПроцесс.Ошибка = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.Ошибка;
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовИСМП.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, СтатусыБазовыйПроцесс);
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийИСМП.ПолучениеКодовМаркировкиИзСУЗНезависимо Тогда
		
		СтатусыБазовыйПроцесс = РегистрыСведений.СтатусыДокументовИСМП.СтруктураСтатусы();
		СтатусыБазовыйПроцесс.Принят = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.СУЗПринятКОбработке;
		
		СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОжидайтеЗавершенияОбработкиДанныхИСМП);
		СтатусыБазовыйПроцесс.Ошибка = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.Ошибка;
		
		СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОтменитеОперацию);
		СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗакройтеЗаказНаЭмиссию);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовИСМП.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, СтатусыБазовыйПроцесс);
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийИСМП.ЗакрытиеЗаказаНаЭмиссиюСУЗ Тогда
		
		СтатусыБазовыйПроцесс = РегистрыСведений.СтатусыДокументовИСМП.СтруктураСтатусы();
		СтатусыБазовыйПроцесс.Принят = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.СУЗОбрабатывается;
		СтатусыБазовыйПроцесс.Обрабатывается = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.СУЗОбрабатывается;
		СтатусыБазовыйПроцесс.Ошибка = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.Ошибка;
		
		СтатусыБазовыйПроцесс.ОбрабатываетсяДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОжидайтеЗавершенияОбработкиДанныхИСМП);
		СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОтменитеОперацию);
		СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ВыполнитеПолучениеКодовМаркировки);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовИСМП.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, СтатусыБазовыйПроцесс);
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийИСМП.ПолучениеСтатусаПулаКодовМаркировкиИзСУЗ Тогда
		
		ОчередьСообщений = РегистрыСведений.ОчередьСообщенийИСМП.ОчередьСообщенийПоДокументу(ДокументСсылка);
		
		Если ОчередьСообщений.Количество() > 0 Тогда
			
			СтатусыБазовыйПроцесс = РегистрыСведений.СтатусыДокументовИСМП.СтруктураСтатусы();
			
			СтатусыБазовыйПроцесс.Принят = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.СУЗПринятКПроизводству;
			
			СтатусыБазовыйПроцесс.Обрабатывается = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.СУЗПринятКОбработке;
			СтатусыБазовыйПроцесс.ОбрабатываетсяДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОжидайтеЗавершенияОбработкиДанныхИСМП);
			
			СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗапроситеКодыМаркировки);
			СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОтменитеОперацию);
			
			СтатусыБазовыйПроцесс.Ошибка = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.Ошибка;
			
			ПараметрыОбновления = РегистрыСведений.СтатусыДокументовИСМП.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, СтатусыБазовыйПроцесс);
		
		КонецЕсли;
	ИначеЕсли Операция = Перечисления.ВидыОперацийИСМП.ПовторноеПолучениеКодовМаркировкиИзСУЗ Тогда
	ИначеЕсли Операция = Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировкиРасчетСтатуса Тогда
	Иначе
		ВызватьИсключение ОбщегоНазначенияИС.ТекстИсключенияОбработкиСтатуса(ДокументСсылка, Операция);
	КонецЕсли;
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Статус после получения данных из ИС МП.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ЗаказНаЭмиссиюКодовМаркировкиСУЗ - Документ, для которого требуется обновить статус.
//  Операция - ПеречислениеСсылка.ВидыОперацийИСМП - Операция обмена с ИС МП.
//  ДополнительныеПараметры - Неопределено, Структура - со свойствами:
//   * СтатусОбработки - ПеречислениеСсылка.СтатусыОбработкиСообщенийИСМП - Статус обработки сообщения.
//   * ОперацияКвитанции - ПеречислениеСсылка.ВидыОперацийИСМП - Операция, на которую получена квитанция.
//
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * НовыйСтатус - ПеречислениеСсылка.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП - Новый статус.
//   * ДальнейшееДействие1 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюИСМП - Дальнейшее действие 1.
//   * ДальнейшееДействие2 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюИСМП - Дальнейшее действие 2.
//   * ДальнейшееДействие3 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюИСМП - Дальнейшее действие 3.
//
Функция СтатусПослеПолученияДанных(ДокументСсылка, Операция, ДополнительныеПараметры) Экспорт
	
	Если Операция = Перечисления.ВидыОперацийИСМП.ПолучениеРезультатаОбработкиЗаказаНаЭмиссиюКодовМаркировки
		Или Операция = Перечисления.ВидыОперацийИСМП.ПолучениеРезультатаОбработкиДокумента 
		Или Операция = Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировкиЗапросGTINНаОстатки Тогда
		
		ВидПродукцииИС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ВидПродукции");
		
		Если ЗначениеЗаполнено(ДополнительныеПараметры.Статус) Тогда
			
			Статусы = РегистрыСведений.СтатусыДокументовИСМП.СтруктураСтатусы();
			Статусы.Принят         = ДополнительныеПараметры.Статус;
			Статусы.Обрабатывается = ДополнительныеПараметры.Статус;
			Статусы.Ошибка         = ДополнительныеПараметры.Статус;
			
			Если ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировкиЗапросGTINНаОстатки Тогда
				Статусы.ОшибкаДействия.Добавить(
					Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗапроситеGTINНаОстатки);
				Статусы.ПринятДействия.Добавить(
					Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗапроситеКодыМаркировки);
				Статусы.ОбрабатываетсяДействия.Добавить(
					Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗапроситеGTINНаОстатки);
			Иначе
				Статусы.ОбрабатываетсяДействия.Добавить(
					Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОжидайтеЗавершенияОбработкиДанныхИСМП);
				Статусы.ОшибкаДействия.Добавить(
					Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗапроситеКодыМаркировки);
			КонецЕсли;
				
			Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОтменитеОперацию);
			
			Если ВидПродукцииИС = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха Тогда
				Статусы.ПринятДействия.Добавить(
					Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗакройтеЗаказНаЭмиссию);
				Статусы.ПринятДействия.Добавить(
					Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ВыполнитеПолучениеКодовМаркировки);
			КонецЕсли;
			
		Иначе
			
			Статусы = РегистрыСведений.СтатусыДокументовИСМП.СтруктураСтатусы();
			
			Если ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыОперацийИСМП.ЗакрытиеЗаказаНаЭмиссиюСУЗ Тогда
				Статусы.Принят = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.СУЗЗакрыт;
			Иначе
				Статусы.Принят = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.СУЗКодыМаркировкиЭмитированы;
				Если ВидПродукцииИС = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха Тогда
					ДанныеСтатусаПоПулу = ДанныеРасчетаСтатусаЗаказаПоПулу(ДокументСсылка, ВидПродукцииИС);
					Если ДанныеСтатусаПоПулу.Статус = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.СУЗКодыМаркировкиЭмитированыЧастично Тогда
						Статусы.Принят = ДанныеСтатусаПоПулу.Статус;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Статусы.Обрабатывается = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.ИСМПОбрабатывается;
			Статусы.ОбрабатываетсяДействия.Добавить(
				Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОжидайтеЗавершенияОбработкиДанныхИСМП);
			
			Статусы.Ошибка = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.Ошибка;
			
			Если ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировкиЗапросGTINНаОстатки Тогда
				Статусы.ОшибкаДействия.Добавить(
					Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗапроситеGTINНаОстатки);
			ИначеЕсли ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыОперацийИСМП.ЗакрытиеЗаказаНаЭмиссиюСУЗ Тогда
				Статусы.ОшибкаДействия.Добавить(
					Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗакройтеЗаказНаЭмиссию);
				Статусы.ОшибкаДействия.Добавить(
					Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ВыполнитеПолучениеКодовМаркировки);
			Иначе
				Статусы.ОшибкаДействия.Добавить(
					Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗапроситеКодыМаркировки);
			КонецЕсли;
			
			Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОтменитеОперацию);
			
			Если ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыОперацийИСМП.ПолучениеКодовМаркировкиИзСУЗНезависимо Тогда
				Статусы.ПринятДействия.Добавить(
					Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗакройтеЗаказНаЭмиссию);
				Статусы.ПринятДействия.Добавить(
					Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ВыполнитеПолучениеКодовМаркировки);
			КонецЕсли;
			
		КонецЕсли;
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовИСМП.РассчитатьСтатусы(
			ДокументСсылка,
			ДополнительныеПараметры.СтатусОбработки,
			Статусы);
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийИСМП.ПолучениеКодовМаркировкиИзСУЗНезависимо Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовИСМП.СтруктураСтатусы();
		
		Статусы.Принят         = ДополнительныеПараметры.Статус;
		Статусы.Ошибка         = ДополнительныеПараметры.Статус;
		Статусы.Обрабатывается = ДополнительныеПараметры.Статус;
		
		Статусы.ОшибкаДействия.Добавить(
			Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОтменитеОперацию);
		Статусы.ПринятДействия.Добавить(
			Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗакройтеЗаказНаЭмиссию);
		Статусы.ПринятДействия.Добавить(
			Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ВыполнитеПолучениеКодовМаркировки);
		Статусы.ОбрабатываетсяДействия.Добавить(
			Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОжидайтеЗавершенияОбработкиДанныхИСМП);
			
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовИСМП.РассчитатьСтатусы(
			ДокументСсылка,
			ДополнительныеПараметры.СтатусОбработки,
			Статусы);
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийИСМП.ЗакрытиеЗаказаНаЭмиссиюСУЗ Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовИСМП.СтруктураСтатусы();
		Статусы.Принят         = ДополнительныеПараметры.Статус;
		Статусы.Ошибка         = ДополнительныеПараметры.Статус;
		Статусы.Обрабатывается = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.ИСМПОбрабатывается;
		
		Статусы.ОбрабатываетсяДействия.Добавить(
			Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОжидайтеЗавершенияОбработкиДанныхИСМП);
		
		Статусы.ОшибкаДействия.Добавить(
			Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗакройтеЗаказНаЭмиссию);
		
		Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОтменитеОперацию);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовИСМП.РассчитатьСтатусы(
			ДокументСсылка,
			ДополнительныеПараметры.СтатусОбработки,
			Статусы);
		
	Иначе
		ВызватьИсключение ОбщегоНазначенияИС.ТекстИсключенияОбработкиСтатуса(ДокументСсылка, Операция);
	КонецЕсли;
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Обновить статус после подготовки к передаче данных
//
// Параметры:
//  ДокументСсылка          - ДокументСсылка.ЗаказНаЭмиссиюКодовМаркировкиСУЗ - Ссылка на документ
//  Операция                - ПеречислениеСсылка.ВидыОперацийИСМП - Операция ИС МП
//  ДополнительныеПараметры - Структура - 
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП - Новый статус.
//
Функция ОбновитьСтатусПослеПодготовкиКПередачеДанных(ДокументСсылка, Операция, ДополнительныеПараметры = Неопределено) Экспорт

	ПараметрыОбновления = СтатусПослеПодготовкиКПередачеДанных(
		ДокументСсылка, Операция, ДополнительныеПараметры);

	Если ПараметрыОбновления = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	НовыйСтатусПослеОбновления = РегистрыСведений.СтатусыДокументовИСМП.ОбновитьСтатус(
		ДокументСсылка,
		ПараметрыОбновления, ДополнительныеПараметры);

	Возврат НовыйСтатусПослеОбновления;

КонецФункции

// Обновить статус после передачи данных
//
// Параметры:
//  ДокументСсылка  - ДокументСсылка.ЗаказНаЭмиссиюКодовМаркировкиСУЗ - Ссылка на документ
//  Операция        - ПеречислениеСсылка.ВидыОперацийИСМП - Операция ИС МП
//  СтатусОбработки - ПеречислениеСсылка.СтатусыОбработкиСообщенийИСМП - Статус обработки сообщения
//  ДополнительныеПараметры - Структура - Структура со свойствами:
//   * ФорматОбмена - ПеречислениеСсылка.ВерсииФорматаОбменаСУЗ - Формат обмена с СУЗ
//   * СтанцияУправленияЗаказами - СправочникСсылка.СтанцииУправленияЗаказамиИСМП - СУЗ
//   * Назначение - ПеречислениеСсылка.НазначениеСообщенийИСМП - Назначение сообщения.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП - Новый статус.
//
Функция ОбновитьСтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки, ДополнительныеПараметры = Неопределено) Экспорт

	ПараметрыОбновления = СтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки, ДополнительныеПараметры);

	Если ПараметрыОбновления = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	НовыйСтатусПослеОбновления = РегистрыСведений.СтатусыДокументовИСМП.ОбновитьСтатус(
		ДокументСсылка,
		ПараметрыОбновления, ДополнительныеПараметры);

	Возврат НовыйСтатусПослеОбновления;

КонецФункции

// Обновить статус после получения данных из ИС МП.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ЗаказНаЭмиссиюКодовМаркировкиСУЗ - Документ, для которого требуется обновить статус.
//  Операция - ПеречислениеСсылка.ВидыОперацийИСМП - Операция обмена с ИС МП.
//  ДополнительныеПараметры - Неопределено, Структура - со свойствами:
//   * СтатусОбработки - ПеречислениеСсылка.СтатусыОбработкиСообщенийИСМП - Статус обработки сообщения.
//   * ОперацияКвитанции - ПеречислениеСсылка.ВидыОперацийИСМП - Операция, на которую получена квитанция.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП - Новый статус.
//
Функция ОбновитьСтатусПослеПолученияДанных(ДокументСсылка, Операция, ДополнительныеПараметры = Неопределено) Экспорт

	ПараметрыОбновления = СтатусПослеПолученияДанных(ДокументСсылка, Операция, ДополнительныеПараметры);

	Если ПараметрыОбновления = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	НовыйСтатусПослеОбновления = РегистрыСведений.СтатусыДокументовИСМП.ОбновитьСтатус(
		ДокументСсылка,
		ПараметрыОбновления, ДополнительныеПараметры);

	Возврат НовыйСтатусПослеОбновления;

КонецФункции

// Изменяет и возвращает статус документа ИС МП.
//
// Параметры:
//  ДокументСсылка      - ДокументСсылка - Документ ИС МП.
//  ПараметрыОбновления - Структура      - Структура со свойствами:
//   * НовыйСтатус - ПеречислениеСсылка.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП - Новый статус.
//   * ДальнейшееДействие1 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюИСМП - Дальнейшее действие 1.
//   * ДальнейшееДействие2 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюИСМП - Дальнейшее действие 2.
//   * ДальнейшееДействие3 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюИСМП - Дальнейшее действие 3.
//  ДополнительныеПараметры - Структура - Дополнительные параметры.
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП - новый статус документа ИС МП.
Функция ОбновитьСтатус(ДокументСсылка, ПараметрыОбновления, ДополнительныеПараметры) Экспорт
	
	НовыйСтатусПослеОбновления = РегистрыСведений.СтатусыДокументовИСМП.ОбновитьСтатус(
		ДокументСсылка,
		ПараметрыОбновления, ДополнительныеПараметры);
	
	Возврат НовыйСтатусПослеОбновления;
	
КонецФункции

// Получить последовательность операций в течении жизненного цикла документа.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ЗаказНаЭмиссиюКодовМаркировкиСУЗ - Документ, для которого требуется обновить статус.
//  ЛинейныйСписок - Булево - не используется для документа
//
// Возвращаемое значение:
//  ТаблицаЗначений - см. функцию ПротоколОбменаИС.ПустаяТаблицаПоследовательностьОпераций().
//
Функция ПоследовательностьОпераций(ДокументСсылка, ЛинейныйСписок = Ложь) Экспорт
	
	Таблица = ПротоколОбменаИС.ПустаяТаблицаПоследовательностьОпераций();
	
	Исходящий = Перечисления.ТипыЗапросовИС.Исходящий;
	Входящий  = Перечисления.ТипыЗапросовИС.Входящий;
	
	ОперацияВПоследовательности = ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 1,
		Исходящий,
		Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировкиРасчетСтатуса);
	ОперацияВПоследовательности.АбстрактнаяОперация = Истина;
	
	ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 11,
		Исходящий,
		Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировки);
	
	ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 12,
		Исходящий,
		Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировкиЗапросGTINНаОстатки);
	
	ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 12,
		Исходящий,
		Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировки);
	
	ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 21,
		Входящий,
		Перечисления.ВидыОперацийИСМП.ПолучениеКодовМаркировкиИзСУЗНезависимо);
	
	ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 22,
		Исходящий,
		Перечисления.ВидыОперацийИСМП.ЗакрытиеЗаказаНаЭмиссиюСУЗ);
	
	Возврат Таблица;
	
КонецФункции

// Обработчик изменения статуса документа.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ЗаказНаЭмиссиюКодовМаркировкиСУЗ - Документ.
//  ПредыдущийСтатус - ПеречислениеСсылка.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП - Предыдущий статус.
//  НовыйСтатус - ПеречислениеСсылка.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП - Предыдущий статус.
//  ПараметрыОбновленияСтатуса - Структура - см. функцию ИнтеграцияИСМП.ПараметрыОбновленияСтатуса().
//
Процедура ПриИзмененииСтатусаДокумента(ДокументСсылка, ПредыдущийСтатус, НовыйСтатус, ПараметрыОбновленияСтатуса) Экспорт
	
	ИнтеграцияИСМППереопределяемый.ПриИзмененииСтатусаДокумента(ДокументСсылка, ПредыдущийСтатус, НовыйСтатус, ПараметрыОбновленияСтатуса);
	
	//Получение конечного статуса требующего повторного оформления
	Если КонечныеСтатусы().Найти(НовыйСтатус)<>Неопределено
		И КонечныеСтатусы().Найти(ПредыдущийСтатус)=Неопределено Тогда
		РасчетСтатусовОформленияИСМП.РассчитатьСтатусыОформленияДокументов(ДокументСсылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Сообщения

// Сообщение к передаче JSON
//
// Параметры:
//  ДокументСсылка          - ДокументСсылка.ЗаказНаЭмиссиюКодовМаркировкиСУЗ - Ссылка на документ эмиссии.
//  ДальнейшееДействие      - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюИСМП - Дальнейшее действие.
//  ДополнительныеПараметры - Структура -
//  
// Возвращаемое значение:
//  Массив Из Структура - Данные JSON сообщения.
//
Функция СообщениеКПередачеJSON(ДокументСсылка, ДальнейшееДействие, ДополнительныеПараметры) Экспорт
	
	Если ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗапроситеКодыМаркировки Тогда
		
		Возврат ЗаявкаНаЭмиссиюКодовМаркировкиJSON(ДокументСсылка, ДополнительныеПараметры);
	
	ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗапроситеGTINНаОстатки Тогда
		
		Возврат ПолучениеGTINНаОстаткиJSON(ДокументСсылка, ДополнительныеПараметры);
		
	ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗакройтеЗаказНаЭмиссию Тогда
		
		Если ДополнительныеПараметры.Свойство("ПараметрыОбработкиДокумента")
			И ДополнительныеПараметры.ПараметрыОбработкиДокумента.ДополнительныеПараметры.Свойство("СообщенияJSON") Тогда
			Возврат ДополнительныеПараметры.ПараметрыОбработкиДокумента.ДополнительныеПараметры.СообщенияJSON
		Иначе
			Возврат ЗакрытиеЗаказаНаЭмиссию(ДокументСсылка, ДополнительныеПараметры);
		КонецЕсли;
		
	ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ВыполнитеПолучениеКодовМаркировки Тогда
		
		Возврат ПолучениеКодовМаркировки(ДокументСсылка, ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ПанельОбменИСМП

// Возвращает массив дальнейших действий с документом, требующих участия пользователя
// 
// Возвращаемое значение:
// 	Массив из ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюИСМП - дальшейшие действия.
//
Функция ВсеТребующиеДействия() Экспорт
	
	МассивДействий = Новый Массив;
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗапроситеКодыМаркировки);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗапроситеGTINНаОстатки);
	Если ИнтеграцияИСМПКлиентСерверПовтИсп.ВестиУчетМаркируемойПродукции(Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха) Тогда
		МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ВыполнитеПолучениеКодовМаркировки);
		МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗакройтеЗаказНаЭмиссию);
	КонецЕсли;
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ВыполнитеОбмен);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОтменитеОперацию);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ПередайтеДанные);
	
	Возврат МассивДействий;
	
КонецФункции

Функция ВсеТребующиеОжидания() Экспорт
	
	МассивДействий = Новый Массив;
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОжидайтеПередачуДанныхРегламентнымЗаданием);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОжидайтеЗавершенияОбработкиДанныхИСМП);
	
	Возврат МассивДействий;
	
КонецФункции

#КонецОбласти

#Область ОбработкаКодовМаркировки

Функция ОбработатьДанныеШтрихкода(Форма, ДанныеШтрихкода, ПараметрыСканирования, ВложенныеШтрихкоды = Неопределено) Экспорт
	
	Результат = Неопределено;
	
	Если ДанныеШтрихкода.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
		
		Результат = ОбработатьДанныеШтрихкодаУпаковкиМаркируемойПродукции(Форма, ДанныеШтрихкода, ПараметрыСканирования, ВложенныеШтрихкоды);
		
	ИначеЕсли ОбщегоНазначенияИСПовтИсп.ЭтоПродукцияИСМП(ДанныеШтрихкода.ВидПродукции, Истина) Тогда
		
		Результат = ОбработатьДанныеШтрихкодаПотребительскойУпаковки(Форма, ДанныеШтрихкода, ПараметрыСканирования);
		
	КонецЕсли;
	
	Если Результат.ДобавленныеСтроки.Количество() Тогда
		ЗаполнениеОбъектовПоСтатистикеИСМП.ЗаполнитьДанныеПоТоварамЗаказаНаЭмиссиюКодовМаркировкиСУЗ(
			Форма, Результат.ДобавленныеСтроки, "GTIN");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ОбработатьДанныеШтрихкодаУпаковкиМаркируемойПродукции(Форма, ДанныеШтрихкода, ПараметрыСканирования, ВложенныеШтрихкоды)
	
	РезультатОбработки = ШтрихкодированиеОбщегоНазначенияИС.ИнициализироватьРезультатОбработкиШтрихкода(ПараметрыСканирования, ДанныеШтрихкода);
	
	ПараметрыЗаполнения = ПараметрыЗаполнения();
	
	ЗаполнитьДанныеОбъектаФормыПоДеревуУпаковок(Форма, ВложенныеШтрихкоды.ДеревоУпаковок, ПараметрыЗаполнения);
	
	РезультатОбработки.ИзмененныеСтроки  = ПараметрыЗаполнения.ИзмененныеСтроки;
	РезультатОбработки.ДобавленныеСтроки = ПараметрыЗаполнения.ДобавленныеСтроки;
	
	Возврат РезультатОбработки;
	
КонецФункции

Процедура ЗаполнитьДанныеОбъектаФормыПоДеревуУпаковок(Форма, ДеревоУпаковок, ПараметрыЗаполнения, УпаковкаВерхнегоУровня = Неопределено)
	
	Для Каждого СтрокаДерева Из ДеревоУпаковок.Строки Цикл
		
		Если УпаковкаВерхнегоУровня = Неопределено
			И СтрокаДерева.ТипУпаковки <> Перечисления.ТипыУпаковок.МаркированныйТовар
			И ЗначениеЗаполнено(СтрокаДерева.ШтрихкодУпаковки) Тогда
			УпаковкаВерхнегоУровняСтрокиДерева = СтрокаДерева.ШтрихкодУпаковки;
		Иначе
			УпаковкаВерхнегоУровняСтрокиДерева = УпаковкаВерхнегоУровня;
		КонецЕсли;
		
		ЗаполнитьДанныеОбъектаФормыПоДеревуУпаковок(
			Форма, СтрокаДерева, ПараметрыЗаполнения, УпаковкаВерхнегоУровняСтрокиДерева);
		
		Если СтрокаДерева.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар Тогда
			
			ПараметрыЗаполнения.РодительскийШтрихкод = УпаковкаВерхнегоУровня;
			ОбработатьДанныеШтрихкодаПотребительскойУпаковки(Форма, СтрокаДерева, Неопределено, ПараметрыЗаполнения);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПараметрыЗаполнения()
	
	Возврат Новый Структура("ИзмененныеСтроки, ДобавленныеСтроки, РодительскийШтрихкод", Новый Массив, Новый Массив);
	
КонецФункции

Функция ОбработатьДанныеШтрихкодаПотребительскойУпаковки(Форма, ДанныеШтрихкода, ПараметрыСканирования, ПараметрыЗаполнения = Неопределено)
	
	ИсточникДанных = Форма.Объект;
	
	Если ПараметрыЗаполнения = Неопределено Тогда
		
		ПараметрыЗаполнения  = ПараметрыЗаполнения();
		РезультатОбработки   = ШтрихкодированиеОбщегоНазначенияИС.ИнициализироватьРезультатОбработкиШтрихкода(Неопределено, ДанныеШтрихкода);
		РодительскийШтрихкод = Неопределено;
		
	Иначе
		
		РезультатОбработки   = ПараметрыЗаполнения;
		РодительскийШтрихкод = ПараметрыЗаполнения.РодительскийШтрихкод;
		
	КонецЕсли;
	
	ОбновляемаяСтрока = ПроверкаИПодборПродукцииИС.ДанныеШтрихкодаДляДобавленияВКеш(ДанныеШтрихкода, "ЗаказНаЭмиссию");
	Если ДанныеШтрихкода.СоставКодаМаркировки <> Неопределено Тогда
		ОбновляемаяСтрока.Количество = 1;
	ИначеЕсли ДанныеШтрихкода.Количество = 0 Тогда
		ОбновляемаяСтрока.Количество = 1;
	КонецЕсли;
	
	ВидУпаковки    = ДанныеШтрихкода.ВидУпаковки;
	ОписаниеGTIN   = РегистрыСведений.ОписаниеGTINИС.ПолучитьОписание(ДанныеШтрихкода.GTIN);
	ДанныеОписания = ОписаниеGTIN.Получить(ДанныеШтрихкода.GTIN);
	
	Если ДанныеОписания <> Неопределено
		И (ДанныеОписания.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая
		Или ДанныеОписания.ВидУпаковки = Перечисления.ВидыУпаковокИС.Потребительская) Тогда
		ВидУпаковки = ДанныеОписания.ВидУпаковки;
	КонецЕсли;
	
	Шаблон = ИнтеграцияИСМПСлужебныйКлиентСервер.ШаблонКодаМаркировкиПоВидуУпаковки(
		ВидУпаковки, ДанныеШтрихкода.ВидПродукции);
	
	ОбновляемаяСтрока.Вставить("Шаблон", Шаблон);
	
	Если РодительскийШтрихкод <> Неопределено Тогда
		ОбновляемаяСтрока.ШтрихкодУпаковки = РодительскийШтрихкод;
	КонецЕсли;
	
	ПараметрыПоиска = ИнтеграцияИС.ПоляПоискаМаркируемойПродукции(Ложь, Истина);
	ЗаполнитьЗначенияСвойств(ПараметрыПоиска, ДанныеШтрихкода);
	
	НайденныеСтрокиТовары = ИсточникДанных.Товары.НайтиСтроки(ПараметрыПоиска);
	
	СтрокаТовары = Неопределено;
	Для Каждого НайденнаяСтрокаТовары Из НайденныеСтрокиТовары Цикл
		Если ЗначениеЗаполнено(НайденнаяСтрокаТовары.ИдентификаторСтрокиВладельца)
			И НайденнаяСтрокаТовары.ИдентификаторСтрокиВладельца <> НайденнаяСтрокаТовары.ИдентификаторСтроки Тогда
			Продолжить;
		КонецЕсли;
		СтрокаТовары = НайденнаяСтрокаТовары;
		Прервать;
	КонецЦикла;
	
	Если СтрокаТовары <> Неопределено Тогда
		
		ОбработатьНайденныеСтрокиВТаблицеТовары(Форма, СтрокаТовары, ОбновляемаяСтрока, РезультатОбработки);
		
	Иначе
		
		СтрокаТовары = ИсточникДанных.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТовары, ОбновляемаяСтрока);
		СтрокаТовары.КоличествоУпаковок = СтрокаТовары.Количество;
		
		Если ИсточникДанных.СпособВводаВОборот = Перечисления.СпособыВводаВОборотСУЗ.МаркировкаОстатков
			И Не РазборКодаМаркировкиИССлужебныйКлиентСервер.ЭтоШтрихкодВводаОстатков(СтрокаТовары.GTIN) Тогда
			СтрокаТовары.ПолноеОписаниеОстатков = Истина;
		Иначе
			СтрокаТовары.ПолноеОписаниеОстатков = Ложь;
		КонецЕсли;
		
		РезультатОбработки.ДобавленныеСтроки.Добавить(СтрокаТовары);
		
	КонецЕсли;
		
	Если Не ЗначениеЗаполнено(ДанныеШтрихкода.Номенклатура) Тогда
		
		РегистрыСведений.КэшОписанияОстатковИСМП.ЗаполнитьТаблицуПредставленийGTINОстатки(
			ИсточникДанных.Товары, ИсточникДанных.Организация, ИсточникДанных.ВидПродукции);
		
	КонецЕсли;
	
	Возврат РезультатОбработки;
	
КонецФункции

Процедура ОбработатьНайденныеСтрокиВТаблицеТовары(Форма, СтрокаТовары, ОбновляемаяСтрока, РезультатОбработки)
	
	КоличествоМаркируемойПродукции = ОбновляемаяСтрока.Количество;
	Если КоличествоМаркируемойПродукции = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТовары.Количество         = СтрокаТовары.Количество + КоличествоМаркируемойПродукции;
	СтрокаТовары.КоличествоУпаковок = СтрокаТовары.КоличествоУпаковок + КоличествоМаркируемойПродукции;
	
	РезультатОбработки.ИзмененныеСтроки.Добавить(СтрокаТовары);
	
КонецПроцедуры

#КонецОбласти

#Область GTINМаркировкаОстатков

Функция ТребуетсяЗапросGTIN(Объект) Экспорт
	
	Если Не Объект.СпособВводаВОборот = Перечисления.СпособыВводаВОборотСУЗ.МаркировкаОстатков Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЕстьРеквизитПолноеОписаниеОстатков = Ложь;
	Если Объект.Товары.Количество() > 0 Тогда
		ЕстьРеквизитПолноеОписаниеОстатков = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(
			Объект.Товары[0], "ПолноеОписаниеОстатков");
	КонецЕсли;
	
	Для Каждого СтрокаТовары Из Объект.Товары Цикл
		Если Не ЗначениеЗаполнено(СтрокаТовары.GTIN)
			И (ЕстьРеквизитПолноеОписаниеОстатков
			И Не СтрокаТовары.ПолноеОписаниеОстатков
			Или Не ЕстьРеквизитПолноеОписаниеОстатков) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

#Область Отчеты

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//   Параметры      - Структура       - 
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ИнтеграцияИСПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
	ИнтеграцияИСПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Сформировать печатные формы объектов.
//
// Параметры:
//   МассивОбъектов        - Массив из Произвольный         - ссылки на объекты, которые нужно распечатать
//   ПараметрыПечати       - Структура                      - дополнительные настройки печати
//   КоллекцияПечатныхФорм - ТаблицаЗначений                - сформированные табличные документы (выходной параметр)
//   ОбъектыПечати         - СписокЗначений из Произвольный - имя области, в которой был выведен объект (выходной параметр)
//   ПараметрыВывода       - Структура                      - дополнительные параметры сформированных табличных документов (выходной параметр)
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
	Возврат;
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	УправлениеДоступомИСПереопределяемый.ПриЗаполненииОграниченияДоступа(
		Метаданные.Документы.ЗаказНаЭмиссиюКодовМаркировкиСУЗ, Ограничение);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТребуетсяПерейтиВПул(ДокументОснование) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Ссылка
	|ИЗ
	|	Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ КАК Т
	|ГДЕ
	|	ДокументОснование = &ДокументОснование");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	Возврат Не Запрос.Выполнить().Пустой()
		Или РегистрыСведений.ПулКодовМаркировкиСУЗ.ЕстьНераспределенныеКоды(ДокументОснование);
	
КонецФункции

// Формирует JSON сообщения для заказа на эмиссию кодов маркировки
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ЗаказНаЭмиссиюКодовМаркировкиСУЗ - Документ Маркировка товаров ИС МП.
//  ДополнительныеПараметры - Структура - Дополнительные параметры
//
// Возвращаемое значение:
//  Массив Из См. ИнтеграцияИСМПСлужебный.СтруктураСообщенияJSON - Сообщения к передаче
Функция ЗаявкаНаЭмиссиюКодовМаркировкиJSON(ДокументСсылка, ДополнительныеПараметры) Экспорт
	
	Операция = Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировки;
	
	СообщенияJSON = Новый Массив;
	
	СписокЗапросов = Новый СписокЗначений;
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	ИСМППрисоединенныеФайлы.ВладелецФайла                 КАК Ссылка,
	|	МАКСИМУМ(ЕСТЬNULL(ИСМППрисоединенныеФайлы.Версия, 0)) КАК ПоследнийНомерВерсии
	|ПОМЕСТИТЬ Версии
	|ИЗ
	|	Справочник.ИСМППрисоединенныеФайлы КАК ИСМППрисоединенныеФайлы
	|ГДЕ
	|	ИСМППрисоединенныеФайлы.ВладелецФайла = &Ссылка
	|	И ИСМППрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
	|СГРУППИРОВАТЬ ПО
	|	ИСМППрисоединенныеФайлы.ВладелецФайла
	|");
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	Шапка.Номер                              КАК Номер,
	|	Шапка.Дата                               КАК Дата,
	|	ЕСТЬNULL(Версии.ПоследнийНомерВерсии, 0) КАК ПоследнийНомерВерсии,
	|	Шапка.ДокументОснование                  КАК ДокументОснование,
	|	Шапка.Ссылка                             КАК Документ,
	|
	|	// Общие реквизиты для табака и обуви
	|	Шапка.ВидПродукции                         КАК ВидПродукции,
	|	Шапка.ИдентификаторПроизводственногоЗаказа КАК ИдентификаторПроизводственногоЗаказа,
	|	Шапка.СпособВводаВОборот                   КАК СпособВводаВОборот,
	|	Шапка.ВвезенПослеДатыОбязательнойМаркировки               КАК ВвезенПослеДатыОбязательнойМаркировки,
	|	Шапка.ПриобретенПроизведенПослеДатыОбязательнойМаркировки КАК ПриобретенПроизведенПослеДатыОбязательнойМаркировки,
	|
	|	Шапка.СервисПровайдер                                             КАК СервисПровайдер,
	|	ЕСТЬNULL(Шапка.СервисПровайдер.Идентификатор, """")               КАК СервисПровайдерИдентификатор,
	|	ЕСТЬNULL(Шапка.СервисПровайдер.ВидСервисПровайдера, Неопределено) КАК ВидСервисПровайдера,
	|
	|	// Основные реквизиты
	|	Шапка.Организация                КАК Организация,
	|	Представление(Шапка.Организация) КАК ОрганизацияПредставление,
	|	Шапка.Контрагент                 КАК Контрагент,
	|	Шапка.ТипОплатыКодовМаркировки   КАК ТипОплатыКодовМаркировки,
	|	Шапка.ПроизводственныйОбъект     КАК ПроизводственныйОбъект,
	|	Представление(Шапка.ПроизводственныйОбъект) КАК ПроизводственныйОбъектПредставление,
	|	Шапка.ПроизводственныйОбъектАдрес           КАК ПроизводственныйОбъектАдрес,
	|	Шапка.ПроизводственныйОбъектАдресСтрокой    КАК ПроизводственныйОбъектАдресСтрокой,
	|	Шапка.ПроизводственныйОбъектИдентификатор   КАК ПроизводственныйОбъектИдентификатор,
	|	Шапка.ИдентификаторПроизводственнойЛинии    КАК ИдентификаторПроизводственнойЛинии,
	|	Шапка.ОжидаемаяДатаНачалаПроизводства       КАК ОжидаемаяДатаНачалаПроизводства,
	|
	|	// Товары из натурального меха
	|	Шапка.GLNОрганизации             КАК GLNОрганизации,
	|	Шапка.СпособПолучения            КАК СпособПолучения,
	|	Шапка.НомерДоговораСОператором   КАК НомерДоговораСОператором,
	|	Шапка.ДатаДоговораСОператором    КАК ДатаДоговораСОператором,
	|
	|	Шапка.Ответственный                КАК Ответственный,
	|	Представление(Шапка.Ответственный) КАК ОтветственныйПредставление
	|	
	|ИЗ
	|	Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ КАК Шапка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Версии КАК Версии
	|		ПО Шапка.Ссылка = Версии.Ссылка
	|ГДЕ
	|	Шапка.Ссылка = &Ссылка",
	"Шапка");
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА
	|			ТЧТовары.СпособФормированияСерийногоНомера = ЗНАЧЕНИЕ(Перечисление.СпособыФормированияСерийногоНомераСУЗ.Самостоятельно)
	|				ИЛИ &ЭтоТабачнаяПродукция
	|				ИЛИ ТЧТовары.Шаблон = ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|				ИЛИ ТЧТовары.Шаблон = ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.СкоропортящаясяМолочнаяПродукцияВЕТИС)
	|				ТОГДА ТЧТовары.ИдентификаторСтроки
	|		ИНАЧЕ """"
	|	КОНЕЦ                                    КАК ИдентификаторСтроки,
	|	
	|	// Универсальные реквизиты
	|	ВЫБОР
	|		КОГДА НЕ &ЭтоТабачнаяПродукция И Шапка.БезУчетаНоменклатуры И ПОДСТРОКА(ТЧТовары.GTIN, 1, 3) = &НачалоGTIN
	|			ТОГДА &ПустаяНоменклатура
	|		ИНАЧЕ ТЧТовары.Номенклатура
	|	КОНЕЦ                                      КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА НЕ &ЭтоТабачнаяПродукция И Шапка.БезУчетаНоменклатуры И ПОДСТРОКА(ТЧТовары.GTIN, 1, 3) = &НачалоGTIN
	|			ТОГДА &ПустаяХарактеристика
	|		ИНАЧЕ ТЧТовары.Характеристика
	|	КОНЕЦ                                      КАК Характеристика,
	|	ТЧТовары.GTIN                              КАК GTIN,
	|	ТЧТовары.ВладелецGTIN                      КАК ВладелецGTIN,
	|	ТЧТовары.КодТНВЭД                          КАК КодТНВЭД,
	|	ТЧТовары.ТоварныйЗнак                      КАК ТоварныйЗнак,
	|	СУММА(ТЧТовары.Количество)                 КАК Количество,
	|	ТЧТовары.СпособФормированияСерийногоНомера КАК СпособФормированияСерийногоНомера,
	|	1                                          КАК КоличествоСтрок,
	|	ЛОЖЬ                                       КАК СтрокаДобавлена,
	|	
	|	// Табачная продукция
	|	ТЧТовары.МаксимальнаяРозничнаяЦена КАК МРЦ,
	|	ТЧТовары.Шаблон                    КАК Шаблон,
	|	
	|	// Молочная продукция
	|	ТЧТовары.СрокГодности              КАК СрокГодности,
	|
	|	// Товары из натурального меха
	|	ВЫБОР
	|		КОГДА &ЭтоМех
	|			ТОГДА ТЧТовары.СпособВводаВОборот
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.ПустаяСсылка)
	|	КОНЕЦ                              КАК СпособВводаВОборот,
	|	ВЫБОР
	|		КОГДА &ЭтоМех
	|			ТОГДА ТЧТовары.ВидКИЗ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыКиЗГИСМ.ПустаяСсылка)
	|	КОНЕЦ                              КАК ВидКИЗ,
	|	ВЫБОР
	|		КОГДА &ЭтоМех
	|			ТОГДА ТЧТовары.РазмерКИЗ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазмерыКиЗГИСМ.ПустаяСсылка)
	|	КОНЕЦ                              КАК РазмерКИЗ
	|	
	|ИЗ
	|	Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ.Товары КАК ТЧТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ КАК Шапка
	|		ПО Шапка.Ссылка = ТЧТовары.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТЧТовары.Шаблон,
	|	ТЧТовары.Ссылка.ПроизводственныйОбъектИдентификатор,
	|	ТЧТовары.СрокГодности,
	|	ТЧТовары.СпособФормированияСерийногоНомера,
	|	ТЧТовары.КодТНВЭД,
	|	ВЫБОР
	|		КОГДА ТЧТовары.СпособФормированияСерийногоНомера = ЗНАЧЕНИЕ(Перечисление.СпособыФормированияСерийногоНомераСУЗ.Самостоятельно)
	|				ИЛИ &ЭтоТабачнаяПродукция
	|				ИЛИ ТЧТовары.Шаблон = ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|				ИЛИ ТЧТовары.Шаблон = ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.СкоропортящаясяМолочнаяПродукцияВЕТИС)
	|			ТОГДА ТЧТовары.ИдентификаторСтроки
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ТЧТовары.ТоварныйЗнак,
	|	ВЫБОР
	|		КОГДА НЕ &ЭтоТабачнаяПродукция И Шапка.БезУчетаНоменклатуры И ПОДСТРОКА(ТЧТовары.GTIN, 1, 3) = &НачалоGTIN
	|			ТОГДА &ПустаяНоменклатура
	|		ИНАЧЕ ТЧТовары.Номенклатура
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ &ЭтоТабачнаяПродукция И Шапка.БезУчетаНоменклатуры И ПОДСТРОКА(ТЧТовары.GTIN, 1, 3) = &НачалоGTIN
	|			ТОГДА &ПустаяХарактеристика
	|		ИНАЧЕ ТЧТовары.Характеристика
	|	КОНЕЦ,
	|	ТЧТовары.МаксимальнаяРозничнаяЦена,
	|	ТЧТовары.GTIN,
	|	ТЧТовары.ВладелецGTIN,
	|	ВЫБОР
	|		КОГДА &ЭтоМех
	|			ТОГДА ТЧТовары.СпособВводаВОборот
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ЭтоМех
	|			ТОГДА ТЧТовары.ВидКИЗ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыКиЗГИСМ.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ЭтоМех
	|			ТОГДА ТЧТовары.РазмерКИЗ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазмерыКиЗГИСМ.ПустаяСсылка)
	|	КОНЕЦ",
	"Товары");
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	СерийныеНомера.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СерийныеНомера.СерийныйНомер       КАК СерийныйНомер
	|ИЗ
	|	Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ.СерийныеНомера КАК СерийныеНомера
	|ГДЕ
	|	СерийныеНомера.Ссылка = &Ссылка
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|",
	"СерийныеНомера");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",     ДокументСсылка);
	Запрос.УстановитьПараметр("НачалоGTIN", ИнтеграцияИСМПСлужебныйКлиентСервер.НачалоGTINМаркировкиОстатков());
	Запрос.УстановитьПараметр(
		"ПустаяНоменклатура",
		ОбщегоНазначенияИС.ПустоеЗначениеОпределяемогоТипа("Номенклатура"));
	Запрос.УстановитьПараметр(
		"ПустаяХарактеристика",
		ОбщегоНазначенияИС.ПустоеЗначениеОпределяемогоТипа("ХарактеристикаНоменклатуры"));
	
	ВидПродукции = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ВидПродукции");
	Запрос.УстановитьПараметр(
		"ЭтоТабачнаяПродукция",
		ОбщегоНазначенияИСКлиентСервер.ЭтоПродукцияМОТП(ВидПродукции));
	Запрос.УстановитьПараметр(
		"ЭтоМех",
		(ВидПродукции = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха));
	
	РезультатЗапроса = ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов);
	
	//@skip-warning
	Шапка = РезультатЗапроса["Шапка"].Выбрать();
	Шапка.Следующий();
	
	//@skip-warning
	ТабличнаяЧастьТовары         = РезультатЗапроса["Товары"].Выгрузить();
	//@skip-warning
	ТабличнаяЧастьСерийныеНомера = РезультатЗапроса["СерийныеНомера"].Выгрузить();
	
	Если ТабличнаяЧастьТовары.Количество() = 0 Тогда
		
		СообщениеJSON = ИнтеграцияИСМПСлужебный.СтруктураСообщенияJSON();
		СообщениеJSON.Документ    = ДокументСсылка;
		СообщениеJSON.Организация = Шапка.Организация;
		СообщениеJSON.Описание    = ИнтеграцияИСМПСлужебный.ОписаниеОперацииПередачиДанных(
			Операция, ДокументСсылка);
		СообщениеJSON.ТекстОшибки = НСтр("ru = 'Нет данных для выгрузки.'");
		СообщениеJSON.ТребуетсяПодписание = Ложь;
		СообщенияJSON.Добавить(СообщениеJSON);
		
		Возврат СообщенияJSON;
		
	КонецЕсли;
	
	НомерВерсии                        = Шапка.ПоследнийНомерВерсии + 1;
	СтанцияУправленияЗаказами          = Неопределено;
	
	РезультатОпределенияСУЗ = ИнтеграцияИСМПСлужебный.ОпределитьСтанциюУправленияЗаказамиУпрощенно(
		СообщенияJSON, Операция, Шапка, ДополнительныеПараметры);
	Если Не РезультатОпределенияСУЗ.Успешно Тогда
		Возврат СообщенияJSON;
	КонецЕсли;
	
	СтанцияУправленияЗаказами = РезультатОпределенияСУЗ.СтанцияУправленияЗаказами;
	ФорматОбмена              = РезультатОпределенияСУЗ.НастройкаОбмена.ФорматОбмена;
	ФорматОбменаV3            = ФорматОбмена = Перечисления.ВерсииФорматаОбменаСУЗ.V3;
	
	Если Не ИнтерфейсСУЗ.ПоддерживаетсяФорматОбменаПоВидуПродукции(ФорматОбмена, Шапка.ВидПродукции) Тогда
		
		СообщениеJSON = ИнтеграцияИСМПСлужебный.СтруктураСообщенияJSON();
		СообщениеJSON.Документ    = ДокументСсылка;
		СообщениеJSON.Организация = Шапка.Организация;
		СообщениеJSON.Описание    = ИнтеграцияИСМПСлужебный.ОписаниеОперацииПередачиДанных(
			Операция, ДокументСсылка);
		СообщениеJSON.ТекстОшибки = СтрШаблон(
			НСтр("ru = 'По виду продукции %1 не поддерживается формат обмена с СУЗ ниже V3.
			|Перейдите в список СУЗ и установите формат обмена V3.'"),
			Шапка.ВидПродукции);
		СообщениеJSON.ТребуетсяПодписание = Ложь;
		СообщенияJSON.Добавить(СообщениеJSON);
		
		Возврат СообщенияJSON;
		
	КонецЕсли;
	
	ЭтоМаркировкаОстатков    = Шапка.СпособВводаВОборот = Перечисления.СпособыВводаВОборотСУЗ.МаркировкаОстатков
		И Шапка.ВидПродукции <> Перечисления.ВидыПродукцииИС.НикотиносодержащаяПродукция;
	ЭтоМаркировкаОстатковНСП = Шапка.СпособВводаВОборот = Перечисления.СпособыВводаВОборотСУЗ.МаркировкаОстатков
		И Шапка.ВидПродукции = Перечисления.ВидыПродукцииИС.НикотиносодержащаяПродукция;
	АбстрактнаяОперация      = Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировкиРасчетСтатуса;
	
	Если ЭтоМаркировкаОстатковНСП Тогда
		РеквизитыОрганизации = РаботаСКонтрагентамиИСВызовСервера.ИННКПППоОрганизацииКонтрагенту(Шапка.Организация);
	КонецЕсли;
	
	ИспользуетсяВладелецGTIN = ИнтеграцияИСМПКлиентСервер.ИспользуетсяВладелецGTINВЗаказеНаЭмиссию(
		Шапка.СпособВводаВОборот, Шапка.ВидПродукции);
	Если ИспользуетсяВладелецGTIN Тогда
		СоответствиеВладельцевGTIN = Новый Соответствие;
	КонецЕсли;
	
	Если ОбщегоНазначенияИСКлиентСервер.ЭтоПродукцияМОТП(Шапка.ВидПродукции) Тогда
		
		АбстрактноеСообщениеJSON = ИнтеграцияИСМПСлужебный.СтруктураСообщенияJSON();
		АбстрактноеСообщениеJSON.Организация               = Шапка.Организация;
		АбстрактноеСообщениеJSON.Документ                  = ДокументСсылка;
		АбстрактноеСообщениеJSON.ДокументОснование         = Шапка.ДокументОснование;
		АбстрактноеСообщениеJSON.ВидПродукции              = Шапка.ВидПродукции;
		АбстрактноеСообщениеJSON.Операция                  = АбстрактнаяОперация;
		АбстрактноеСообщениеJSON.Версия                    = НомерВерсии;
		АбстрактноеСообщениеJSON.ПараметрыЗапроса          = Новый Массив;
		АбстрактноеСообщениеJSON.ИдентификаторЗаявки       = ИнтеграцияИСМПСлужебный.СтроковоеПредставлениеПустогоУникальногоИдентификатора();
		АбстрактноеСообщениеJSON.Идентификатор             = СокрЛП(Новый УникальныйИдентификатор());
		АбстрактноеСообщениеJSON.ТипСообщения              = Перечисления.ТипыЗапросовИС.Исходящий;
		АбстрактноеСообщениеJSON.ТребуетсяПодписание       = Ложь;
		АбстрактноеСообщениеJSON.СтанцияУправленияЗаказами = СтанцияУправленияЗаказами;
		АбстрактноеСообщениеJSON.ПроизводственныйОбъект    = Шапка.ПроизводственныйОбъект;
		АбстрактноеСообщениеJSON.Назначение = Перечисления.НазначениеСообщенийИСМП.СтанцияУправленияЗаказами;
		АбстрактноеСообщениеJSON.Описание   = ИнтеграцияИСМПСлужебный.ОписаниеОперацииПередачиДанных(
			АбстрактнаяОперация, ДокументСсылка, НомерВерсии);
		
		СообщенияJSON.Добавить(АбстрактноеСообщениеJSON);
		
		РаспределитьКоличествоПоНовымСтрокам(ТабличнаяЧастьТовары, ФорматОбмена);
		ТабличнаяЧастьТовары.Сортировать("GTIN");

		Для Каждого СтрокаТЧ Из ТабличнаяЧастьТовары Цикл
			
			СообщениеJSON = ИнтеграцияИСМПСлужебный.СтруктураСообщенияJSON();
			СообщениеJSON.Организация       = Шапка.Организация;
			СообщениеJSON.Документ          = ДокументСсылка;
			СообщениеJSON.ДокументОснование = Шапка.ДокументОснование;
			СообщениеJSON.ЗагружатьДо       = АбстрактноеСообщениеJSON.Идентификатор;
			
			СообщениеJSON.Описание = ИнтеграцияИСМПСлужебный.ОписаниеОперацииПередачиДанных(
				Операция, ДокументСсылка, НомерВерсии);
			
			ТелоЗапроса = Новый Структура;
			
			Если ЭтоМаркировкаОстатков Тогда
				
				ТелоЗапроса.Вставить("factoryId",          "NONE"); 
				ТелоЗапроса.Вставить("factoryCountry",     "NONE");
				ТелоЗапроса.Вставить("productionLineId",   "NONE");
				ТелоЗапроса.Вставить("productCode",        "NONE");
				ТелоЗапроса.Вставить("productDescription", "NONE");
				
			Иначе
				
				ТелоЗапроса.Вставить("factoryId", Шапка.ПроизводственныйОбъектИдентификатор); // GLN фабрики
				Если ЗначениеЗаполнено(Шапка.ПроизводственныйОбъектПредставление) Тогда
					ТелоЗапроса.Вставить("factoryName", Шапка.ПроизводственныйОбъектПредставление); // Наименование производства
				КонецЕсли;
				Если ЗначениеЗаполнено(Шапка.ПроизводственныйОбъектАдресСтрокой) Тогда
					ТелоЗапроса.Вставить("factoryAddress", Шапка.ПроизводственныйОбъектАдресСтрокой);  // Адрес производства
				КонецЕсли;
				
				ДанныеАдреса = Новый Структура;
				ДанныеАдреса.Вставить("Страна", "");
				ИнтеграцияИС.ДанныеИзСтрокиКонтактнойИнформации(Шапка.ПроизводственныйОбъектАдрес, ДанныеАдреса);
				Если ЗначениеЗаполнено(ДанныеАдреса.Страна) Тогда
					ТелоЗапроса.Вставить("factoryCountry", ДанныеАдреса.Страна);
				Иначе
					ТелоЗапроса.Вставить("factoryCountry", НСтр("ru = 'Россия'"));
				КонецЕсли;
				
				ТелоЗапроса.Вставить("productionLineId",   Шапка.ИдентификаторПроизводственнойЛинии);
				
				ТелоЗапроса.Вставить("productCode",        Строка(СтрокаТЧ.Номенклатура.УникальныйИдентификатор()));      // Код продукта, SKU
				ТелоЗапроса.Вставить("productDescription", Строка(СтрокаТЧ.Номенклатура)); // Описание продукции
			
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Шапка.ИдентификаторПроизводственногоЗаказа) Тогда
				ТелоЗапроса.Вставить("poNumber", Шапка.ИдентификаторПроизводственногоЗаказа);
			КонецЕсли;
			Если ЗначениеЗаполнено(Шапка.ОжидаемаяДатаНачалаПроизводства) Тогда
				ТелоЗапроса.Вставить("expectedStartDate", Формат(Шапка.ОжидаемаяДатаНачалаПроизводства, "ДФ=yyyy-MM-dd;")); // Дата начала производства продукции
			КонецЕсли;
			ТелоЗапроса.Вставить("products", Новый Массив);
			
			СтрокаЗаказаКодовМаркировки = Новый Структура;
			СтрокаЗаказаКодовМаркировки.Вставить("gtin",       СтрокаТЧ.GTIN);
			СтрокаЗаказаКодовМаркировки.Вставить("quantity",   СтрокаТЧ.Количество);
			СтрокаЗаказаКодовМаркировки.Вставить("templateId", ИнтерфейсСУЗСлужебный.ШаблонКодаМаркировки(СтрокаТЧ.Шаблон));
			
			Если ЭтоМаркировкаОстатковНСП Тогда
				НачалоGTIN = Лев(СтрокаТЧ.GTIN, 3);
				Если НачалоGTIN <> "046" И НачалоGTIN <> "029" Тогда
					СтрокаЗаказаКодовМаркировки.Вставить("producer", РеквизитыОрганизации.ИНН);
				КонецЕсли;
			КонецЕсли;
			
			СтрокаЗаказаКодовМаркировки.Вставить("serialNumberType", ИнтерфейсСУЗСлужебный.СпособФормированияСерийногоНомера(СтрокаТЧ.СпособФормированияСерийногоНомера)); // OPERATOR или SELF_MADE
			Если СтрокаТЧ.СпособФормированияСерийногоНомера = Перечисления.СпособыФормированияСерийногоНомераСУЗ.Самостоятельно Тогда
				ПараметрыОтбора = Новый Структура;
				ПараметрыОтбора.Вставить("ИдентификаторСтроки", СтрокаТЧ.ИдентификаторСтроки);
				СтрокаЗаказаКодовМаркировки.Вставить("serialNumbers", ТабличнаяЧастьСерийныеНомера.Скопировать(ПараметрыОтбора).ВыгрузитьКолонку("СерийныйНомер"));
			КонецЕсли;
			
			Если СтрокаТЧ.Шаблон = Перечисления.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка Тогда
				
				Если ЗначениеЗаполнено(СтрокаТЧ.МРЦ) Тогда
					СтрокаМРЦ = ИнтерфейсМОТП.ЗначениеМРЦСтрокой(СтрокаТЧ.МРЦ);
				Иначе
					СтрокаМРЦ = "0000";
				КонецЕсли;
				
				СтрокаЗаказаКодовМаркировки.Вставить("mrp", СтрокаМРЦ);
				
			ИначеЕсли СтрокаТЧ.Шаблон = Перечисления.ШаблоныКодовМаркировкиСУЗ.БлокТабачныхПачек Тогда
				
				Если ЗначениеЗаполнено(СтрокаТЧ.МРЦ) Тогда
					СтрокаМРЦ = ИнтерфейсМОТП.ЗначениеМРЦСтрокой(СтрокаТЧ.МРЦ);
				Иначе
					СтрокаМРЦ = "000000";
				КонецЕсли;
				
				СтрокаЗаказаКодовМаркировки.Вставить("mrp", СтрокаМРЦ);
				
			КонецЕсли;
			
			Если Шапка.ВидПродукции = Перечисления.ВидыПродукцииИС.АльтернативныйТабак Тогда
				
				СтрокаЗаказаКодовМаркировки.Вставить("cisType", ИнтерфейсСУЗСлужебный.ТипКодаМаркировки(СтрокаТЧ.Шаблон));
				
				ТелоЗапроса.Вставить("contactPerson",     Шапка.ОтветственныйПредставление);
				ТелоЗапроса.Вставить("releaseMethodType", ИнтерфейсСУЗСлужебный.СпособВыпускаВОборот(Шапка.СпособВводаВОборот, "СУЗ"));
				ТелоЗапроса.Вставить("createMethodType",  ИнтерфейсСУЗСлужебный.СпособИзготовленияКодовМаркировки(Шапка.ВидСервисПровайдера));
				ТелоЗапроса.Вставить("productionOrderId", Шапка.ИдентификаторПроизводственногоЗаказа);
				
			ИначеЕсли Шапка.ВидПродукции = Перечисления.ВидыПродукцииИС.НикотиносодержащаяПродукция Тогда
				
				СтрокаЗаказаКодовМаркировки.Вставить("cisType", ИнтерфейсСУЗСлужебный.ТипКодаМаркировки(СтрокаТЧ.Шаблон));
				
				ТелоЗапроса.Вставить("releaseMethodType", ИнтерфейсСУЗСлужебный.СпособВыпускаВОборот(Шапка.СпособВводаВОборот, "СУЗ"));
				
			ИначеЕсли Шапка.ВидПродукции = Перечисления.ВидыПродукцииИС.Табак
				И ЗначениеЗаполнено(Шапка.СпособВводаВОборот) Тогда
				
				ТелоЗапроса.Вставить("releaseMethodType", ИнтерфейсСУЗСлужебный.СпособВыпускаВОборот(Шапка.СпособВводаВОборот, "СУЗ"));
				
			КонецЕсли;
			
			Если ФорматОбменаV3
				И Не ТелоЗапроса.Свойство("cisType") Тогда
				СтрокаЗаказаКодовМаркировки.Вставить("cisType", ИнтерфейсСУЗСлужебный.ТипКодаМаркировки(СтрокаТЧ.Шаблон));
			КонецЕсли;
			
			Если ИспользуетсяВладелецGTIN
				И ЗначениеЗаполнено(СтрокаТЧ.ВладелецGTIN) Тогда
				РеквизитыВладельцаGTIN = СоответствиеВладельцевGTIN[СтрокаТЧ.ВладелецGTIN];
				Если РеквизитыВладельцаGTIN = Неопределено Тогда
					РеквизитыВладельцаGTIN = РаботаСКонтрагентамиИСВызовСервера.ИННКПППоОрганизацииКонтрагенту(СтрокаТЧ.ВладелецGTIN);
					СоответствиеВладельцевGTIN.Вставить(СтрокаТЧ.ВладелецGTIN, РеквизитыВладельцаGTIN);
				КонецЕсли;
				СтрокаЗаказаКодовМаркировки.Вставить("producer", РеквизитыВладельцаGTIN.ИНН);
			КонецЕсли;
			
			ТелоЗапроса["products"].Добавить(СтрокаЗаказаКодовМаркировки);
			
			ПараметрыЗапроса = Новый Структура;
			ПараметрыЗапроса.Вставить("ИдентификаторБизнесЗаказа", "");
			ПараметрыЗапроса.Вставить("ИдентификаторСтрокиЗаказа", СтрокаТЧ.ИдентификаторСтроки);
			ПараметрыЗапроса.Вставить("GTIN",                      СтрокаТЧ.GTIN);
			ПараметрыЗапроса.Вставить("Шаблон",                    СтрокаТЧ.Шаблон);
			ПараметрыЗапроса.Вставить("Количество",                СтрокаТЧ.Количество);
			
			ПараметрыЗапросов = Новый Массив;
			ПараметрыЗапросов.Добавить(ПараметрыЗапроса);
			
			ИнтерфейсСУЗ.ТелоЗапросаПоФорматуОбмена(ТелоЗапроса, ФорматОбмена, Операция, Шапка.ВидПродукции);
			
			ТелоСообщенияJSON = ОбщегоНазначенияИСМП.ОбъектВТекстJSON(ТелоЗапроса, Истина);
			
			СообщениеJSON.ТекстСообщенияJSON = ТелоСообщенияJSON;
			СообщениеJSON.ТипСообщения       = Перечисления.ТипыЗапросовИС.Исходящий;
			
			СообщениеJSON.ВидПродукции       = Шапка.ВидПродукции;
			СообщениеJSON.Операция           = Операция;
			
			СообщениеJSON.Версия             = НомерВерсии;
			СообщениеJSON.ПараметрыЗапроса   = ПараметрыЗапросов;
			
			СообщениеJSON.ТребуетсяПодписание       = ФорматОбменаV3 Или Шапка.ВидПродукции = Перечисления.ВидыПродукцииИС.АльтернативныйТабак;
			СообщениеJSON.Назначение                = Перечисления.НазначениеСообщенийИСМП.СтанцияУправленияЗаказами;
			СообщениеJSON.ФорматОбмена              = ФорматОбмена;
			СообщениеJSON.СтанцияУправленияЗаказами = СтанцияУправленияЗаказами;
			СообщениеJSON.ПроизводственныйОбъект    = Шапка.ПроизводственныйОбъект;
			
			СообщенияJSON.Добавить(СообщениеJSON);
			
		КонецЦикла;
		
	ИначеЕсли ОбщегоНазначенияИСПовтИсп.ЭтоПродукцияИСМП(Шапка.ВидПродукции) Тогда
		
		Если Шапка.ВидПродукции <> Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха Тогда
			
			Шаблон               = ИнтеграцияИСМПКлиентСервер.ШаблонКодаМаркировкиПоВидуПродукции(Шапка.ВидПродукции);
			ИдентификаторШаблона = ИнтерфейсСУЗСлужебный.ШаблонКодаМаркировки(Шаблон);
			
			Если ЗначениеЗаполнено(Шапка.ТипОплатыКодовМаркировки) Тогда
				ТипОплатыКодовМаркировки = ИнтерфейсСУЗСлужебный.ТипОплатыКодовМаркировки(Шапка.ТипОплатыКодовМаркировки);
			Иначе
				ТипОплатыКодовМаркировки = Неопределено;
			КонецЕсли;
			
			ТабличнаяЧастьТовары.Индексы.Добавить("СпособФормированияСерийногоНомера");
		
		КонецЕсли;
		
		ИспользуетсяСервисПровайдер = ЗначениеЗаполнено(Шапка.СервисПровайдер);
		МассивЗаказов = МассивЗаказовПоТаблицеТоваров(ТабличнаяЧастьТовары, ФорматОбмена, Шапка.ВидПродукции);
		
		НалоговыйНомерВСтранеРегистрации = Неопределено;
		Если Шапка.СпособВводаВОборот = Перечисления.СпособыВводаВОборотСУЗ.ТрансграничнаяТорговля Тогда
			
			СведенияОКонтрагенте = Новый Структура();
			СведенияОКонтрагенте.Вставить("НалоговыйНомерВСтранеРегистрации", "");
			СведенияОКонтрагенте.Вставить("Наименование",                     "");
			ИнтеграцияИСПереопределяемый.ЗаполнитьСведенияОбОрганизации(Шапка.Контрагент, СведенияОКонтрагенте);
			НалоговыйНомерВСтранеРегистрации = СведенияОКонтрагенте.НалоговыйНомерВСтранеРегистрации;
			
			Если Не ЗначениеЗаполнено(СведенияОКонтрагенте.НалоговыйНомерВСтранеРегистрации) Тогда
				
				СообщениеJSON                   = ИнтеграцияИСМПСлужебный.СтруктураСообщенияJSON();
				СообщениеJSON.Организация       = Шапка.Организация;
				СообщениеJSON.Документ          = ДокументСсылка;
				СообщениеJSON.ДокументОснование = Шапка.ДокументОснование;
				
				СообщениеJSON.Описание = ИнтеграцияИСМПСлужебный.ОписаниеОперацииПередачиДанных(
					Операция, ДокументСсылка, НомерВерсии);
				
				ОбщегоНазначенияИСКлиентСервер.ДобавитьТекстОшибки(
					СообщениеJSON,
						СтрШаблон(
							НСтр("ru = 'Не заполнено поле ""Налоговый номер в стране регистрации"".
									   |Контрагент: %1'"),
							Шапка.Контрагент));
				
				СообщенияJSON.Добавить(СообщениеJSON);
				
				Возврат СообщенияJSON;
				
			КонецЕсли;
			
		КонецЕсли;
		
		АбстрактноеСообщениеJSON = ИнтеграцияИСМПСлужебный.СтруктураСообщенияJSON();
		АбстрактноеСообщениеJSON.Организация               = Шапка.Организация;
		АбстрактноеСообщениеJSON.Документ                  = ДокументСсылка;
		АбстрактноеСообщениеJSON.ДокументОснование         = Шапка.ДокументОснование;
		АбстрактноеСообщениеJSON.ВидПродукции              = Шапка.ВидПродукции;
		АбстрактноеСообщениеJSON.Операция                  = АбстрактнаяОперация;
		АбстрактноеСообщениеJSON.Версия                    = НомерВерсии;
		АбстрактноеСообщениеJSON.ПараметрыЗапроса          = Новый Массив;
		АбстрактноеСообщениеJSON.ИдентификаторЗаявки       = ИнтеграцияИСМПСлужебный.СтроковоеПредставлениеПустогоУникальногоИдентификатора();
		АбстрактноеСообщениеJSON.Идентификатор             = СокрЛП(Новый УникальныйИдентификатор());
		АбстрактноеСообщениеJSON.ТипСообщения              = Перечисления.ТипыЗапросовИС.Исходящий;
		АбстрактноеСообщениеJSON.ТребуетсяПодписание       = Ложь;
		АбстрактноеСообщениеJSON.СтанцияУправленияЗаказами = СтанцияУправленияЗаказами;
		АбстрактноеСообщениеJSON.ПроизводственныйОбъект    = Шапка.ПроизводственныйОбъект;
		АбстрактноеСообщениеJSON.Назначение = Перечисления.НазначениеСообщенийИСМП.СтанцияУправленияЗаказами;
		АбстрактноеСообщениеJSON.Описание   = ИнтеграцияИСМПСлужебный.ОписаниеОперацииПередачиДанных(
			АбстрактнаяОперация, ДокументСсылка, НомерВерсии);
		
		СообщенияJSON.Добавить(АбстрактноеСообщениеJSON);
		
		ПорядковыйНомерЗаказа = 0;
		Для Каждого СодержаниеЗаказа Из МассивЗаказов Цикл
			
			СообщениеJSON = ИнтеграцияИСМПСлужебный.СтруктураСообщенияJSON();
			СообщениеJSON.Организация       = Шапка.Организация;
			СообщениеJSON.Документ          = ДокументСсылка;
			СообщениеJSON.ДокументОснование = Шапка.ДокументОснование;
			СообщениеJSON.ПараметрыЗапроса  = Новый Массив;
			СообщениеJSON.ЗагружатьДо       = АбстрактноеСообщениеJSON.Идентификатор;
			
			СообщениеJSON.Описание = ИнтеграцияИСМПСлужебный.ОписаниеОперацииПередачиДанных(
				Операция, ДокументСсылка, НомерВерсии);
			
			ТелоЗапроса = Новый Структура;
			
			ПорядковыйНомерЗаказа = ПорядковыйНомерЗаказа + 1;
			Если Шапка.ВидПродукции = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха Тогда
				
				Если МассивЗаказов.Количество() > 1 Тогда
					НомерЗаказа = СтрШаблон("%1-%2", Шапка.Номер, Формат(ПорядковыйНомерЗаказа, "ЧГ=0;"));
				Иначе
					НомерЗаказа = Шапка.Номер;
				КонецЕсли;
				ТелоЗапроса.Вставить("senderGln",             Шапка.GLNОрганизации);
				ТелоЗапроса.Вставить("productionOrderNumber", НомерЗаказа);
				ТелоЗапроса.Вставить("productionOrderDate",   Формат(Шапка.Дата, "ДФ=yyyy-MM-dd;"));
				ТелоЗапроса.Вставить("contractNumber",        Шапка.НомерДоговораСОператором);
				ТелоЗапроса.Вставить("contractDate",          Формат(Шапка.ДатаДоговораСОператором, "ДФ=yyyy-MM-dd;"));
				ТелоЗапроса.Вставить("receiveMethodType",     ИнтерфейсСУЗСлужебный.СпособПолученияИзготовленныхКИЗ(Шапка.СпособПолучения));
				Если ЗначениеЗаполнено(СодержаниеЗаказа[0].GTIN) Тогда
					ТелоЗапроса.Вставить("orderType", "INDIVIDUAL");
				Иначе
					ТелоЗапроса.Вставить("orderType", "NON_INDIVIDUAL");
				КонецЕсли;
				
				Если Шапка.СпособПолучения = Перечисления.СпособыПолученияИзготовленныхКИЗГИСМ.ДоставкаЭмитентомДоУказанногоМеста Тогда
					ИдентификатрыАдреса = ИнтеграцияИСМП.ИдентификаторФИАСПоДаннымАдреса(Шапка.ПроизводственныйОбъектАдрес);
					Если ЗначениеЗаполнено(ИдентификатрыАдреса.ИдентификаторАдресногоОбъекта)
						И ЗначениеЗаполнено(ИдентификатрыАдреса.ИдентификаторДома) Тогда
						ДанныеАдреса = Новый Структура();
						ДанныеАдреса.Вставить("uid",   ИдентификатрыАдреса.ИдентификаторАдресногоОбъекта);
						ДанныеАдреса.Вставить("house", ИдентификатрыАдреса.ИдентификаторДома);
						Если ЗначениеЗаполнено(ИдентификатрыАдреса.ИдентификаторКвартиры) Тогда
							ДанныеАдреса.Вставить("flat", ИдентификатрыАдреса.ИдентификаторКвартиры);
						КонецЕсли;
						ТелоЗапроса.Вставить("address", ДанныеАдреса);
					Иначе
						ТелоЗапроса.Вставить("address", Шапка.ПроизводственныйОбъектАдресСтрокой);
					КонецЕсли;
				КонецЕсли;
				
			Иначе
				ТелоЗапроса.Вставить("contactPerson",     Шапка.ОтветственныйПредставление);
				ТелоЗапроса.Вставить("releaseMethodType", ИнтерфейсСУЗСлужебный.СпособВыпускаВОборот(Шапка.СпособВводаВОборот, "СУЗ"));
				ТелоЗапроса.Вставить("createMethodType",  ИнтерфейсСУЗСлужебный.СпособИзготовленияКодовМаркировки(Шапка.ВидСервисПровайдера));
				Если ИспользуетсяСервисПровайдер Тогда
					ТелоЗапроса.Вставить("serviceProviderId", Шапка.СервисПровайдерИдентификатор);
				КонецЕсли;
				Если ТипОплатыКодовМаркировки <> Неопределено Тогда
					ТелоЗапроса.Вставить("paymentType", ТипОплатыКодовМаркировки);
				КонецЕсли;
				ТелоЗапроса.Вставить("productionOrderId", Шапка.ИдентификаторПроизводственногоЗаказа);
			КонецЕсли;
			
			ТелоЗапроса.Вставить("products", Новый Массив);
			
			Если Шапка.СпособВводаВОборот = Перечисления.СпособыВводаВОборотСУЗ.МаркировкаОстатков
				И (Шапка.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ЛегкаяПромышленность")
				Или Шапка.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Обувь")) Тогда
				ТелоЗапроса.Вставить("remainsAvailable", Шапка.ПриобретенПроизведенПослеДатыОбязательнойМаркировки);
			КонецЕсли;
			
			ОбщиеПараметрыЗапроса = Неопределено;
			
			Для Каждого СтрокаТЧ Из СодержаниеЗаказа Цикл
				
				СтрокаЗаказаКодовМаркировки = Новый Структура;
				Если ЗначениеЗаполнено(СтрокаТЧ.GTIN) Тогда
					СтрокаЗаказаКодовМаркировки.Вставить("gtin", СтрокаТЧ.GTIN);
				КонецЕсли;
				СтрокаЗаказаКодовМаркировки.Вставить("quantity", СтрокаТЧ.Количество);
				
				Если Шапка.ВидПродукции = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха Тогда
					Если ЗначениеЗаполнено(СтрокаТЧ.GTIN) Тогда
						СтрокаЗаказаКодовМаркировки.Вставить("gcp", ШтрихкодированиеИСКлиентСервер.GCPизGTIN(СтрокаТЧ.GTIN));
					КонецЕсли;
					СтрокаЗаказаКодовМаркировки.Вставить("tnvedCode",          ИнтеграцияИСКлиентСервер.КодТНВЭДДляПередачиВИСМП(СтрокаТЧ.КодТНВЭД, Шапка.ВидПродукции));
					СтрокаЗаказаКодовМаркировки.Вставить("identificationType", ИнтерфейсСУЗСлужебный.ВидКИЗ(СтрокаТЧ.ВидКИЗ));
					СтрокаЗаказаКодовМаркировки.Вставить("releaseMethodType",  ИнтерфейсСУЗСлужебный.СпособВыпускаВОборот(СтрокаТЧ.СпособВводаВОборот));
					СтрокаЗаказаКодовМаркировки.Вставить("initialMethodType",  "2");
					СтрокаЗаказаКодовМаркировки.Вставить("sizeType",           ИнтерфейсСУЗСлужебный.РазмерКИЗ(СтрокаТЧ.РазмерКИЗ));
				Иначе
					СтрокаЗаказаКодовМаркировки.Вставить("serialNumberType", ИнтерфейсСУЗСлужебный.СпособФормированияСерийногоНомера(СтрокаТЧ.СпособФормированияСерийногоНомера)); // OPERATOR или SELF_MADE
					Если ЗначениеЗаполнено(СтрокаТЧ.Шаблон) Тогда
						СтрокаЗаказаКодовМаркировки.Вставить("templateId", ИнтерфейсСУЗСлужебный.ШаблонКодаМаркировки(СтрокаТЧ.Шаблон));
					Иначе
						СтрокаЗаказаКодовМаркировки.Вставить("templateId", ИдентификаторШаблона);
					КонецЕсли;
				КонецЕсли;
				
				Если СтрокаТЧ.СпособФормированияСерийногоНомера = Перечисления.СпособыФормированияСерийногоНомераСУЗ.Самостоятельно Тогда
					ПараметрыОтбора = Новый Структура;
					ПараметрыОтбора.Вставить("ИдентификаторСтроки", СтрокаТЧ.ИдентификаторСтроки);
					СтрокаЗаказаКодовМаркировки.Вставить(
						"serialNumbers",
						ТабличнаяЧастьСерийныеНомера.Скопировать(ПараметрыОтбора).ВыгрузитьКолонку("СерийныйНомер"));
				КонецЕсли;
				
				Если Шапка.СпособВводаВОборот = Перечисления.СпособыВводаВОборотСУЗ.ТрансграничнаяТорговля Тогда
					СтрокаЗаказаКодовМаркировки.Вставить("exporterTaxpayerId", НалоговыйНомерВСтранеРегистрации);
				КонецЕсли;
				
				Если СтрокаТЧ.Шаблон = Перечисления.ШаблоныКодовМаркировкиСУЗ.МолочнаяПродукцияПодконтрольнаяВЕТИС Тогда
					СтрокаЗаказаКодовМаркировки.Вставить("expDate", Формат(СтрокаТЧ.СрокГодности, "ДФ=yyMMdd;"));
				ИначеЕсли СтрокаТЧ.Шаблон = Перечисления.ШаблоныКодовМаркировкиСУЗ.СкоропортящаясяМолочнаяПродукцияВЕТИС Тогда
					СтрокаЗаказаКодовМаркировки.Вставить("expDate72", Формат(СтрокаТЧ.СрокГодности, "ДФ=yyMMddHHmm;"));
				КонецЕсли;
				
				Если ФорматОбменаV3 И ВидПродукции <> Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха
					Или Шапка.ВидПродукции = Перечисления.ВидыПродукцииИС.БезалкогольноеПиво
					Или ОбщегоНазначенияИСКлиентСерверПовтИсп.ВидПродукцииИспользуетНаборыИлиГрупповыеУпаковки(Шапка.ВидПродукции) Тогда
					СтрокаЗаказаКодовМаркировки.Вставить("cisType", ИнтерфейсСУЗСлужебный.ТипКодаМаркировки(СтрокаТЧ.Шаблон));
				КонецЕсли;
				
				Если ИспользуетсяВладелецGTIN
					И ЗначениеЗаполнено(СтрокаТЧ.ВладелецGTIN) Тогда
					РеквизитыВладельцаGTIN = СоответствиеВладельцевGTIN[СтрокаТЧ.ВладелецGTIN];
					Если РеквизитыВладельцаGTIN = Неопределено Тогда
						РеквизитыВладельцаGTIN = РаботаСКонтрагентамиИСВызовСервера.ИННКПППоОрганизацииКонтрагенту(СтрокаТЧ.ВладелецGTIN);
						СоответствиеВладельцевGTIN.Вставить(СтрокаТЧ.ВладелецGTIN, РеквизитыВладельцаGTIN);
					КонецЕсли;
					СтрокаЗаказаКодовМаркировки.Вставить("producer", РеквизитыВладельцаGTIN.ИНН);
				КонецЕсли;
				
				ТелоЗапроса["products"].Добавить(СтрокаЗаказаКодовМаркировки);
				
				Если Не ИспользуетсяСервисПровайдер Тогда
					Если ВидПродукции = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха Тогда
						Если ОбщиеПараметрыЗапроса = Неопределено Тогда
							ОбщиеПараметрыЗапроса = Новый Структура();
							ОбщиеПараметрыЗапроса.Вставить("GTIN",                      Неопределено);
							ОбщиеПараметрыЗапроса.Вставить("ИдентификаторБизнесЗаказа", "");
							ОбщиеПараметрыЗапроса.Вставить("ИдентификаторСтрокиЗаказа", СтрокаТЧ.ИдентификаторСтроки);
							ОбщиеПараметрыЗапроса.Вставить("Шаблон",                    Неопределено);
							ОбщиеПараметрыЗапроса.Вставить("Количество",                СтрокаТЧ.Количество);
							ОбщиеПараметрыЗапроса.Вставить("ПараметрыЗаказа",           Новый Массив());
							СообщениеJSON.ПараметрыЗапроса.Добавить(ОбщиеПараметрыЗапроса);
						Иначе
							ОбщиеПараметрыЗапроса.Количество = ОбщиеПараметрыЗапроса.Количество + СтрокаТЧ.Количество;
						КонецЕсли;
						ПараметрыЗаказа = Новый Структура();
						ПараметрыЗаказа.Вставить("GTIN",               СтрокаТЧ.GTIN);
						ПараметрыЗаказа.Вставить("СпособВводаВОборот", СтрокаТЧ.СпособВводаВОборот);
						ПараметрыЗаказа.Вставить("КодТНВЭД",           СтрокаТЧ.КодТНВЭД);
						ПараметрыЗаказа.Вставить("ВидКИЗ",             СтрокаТЧ.ВидКИЗ);
						ПараметрыЗаказа.Вставить("РазмерКИЗ",          СтрокаТЧ.РазмерКИЗ);
						ПараметрыЗаказа.Вставить("Шаблон",             СтрокаТЧ.Шаблон);
						ПараметрыЗаказа.Вставить("Количество",         СтрокаТЧ.Количество);
						ОбщиеПараметрыЗапроса.ПараметрыЗаказа.Добавить(ПараметрыЗаказа);
					Иначе
						ПараметрыЗапроса = Новый Структура;
						ПараметрыЗапроса.Вставить("GTIN",                      СтрокаТЧ.GTIN);
						ПараметрыЗапроса.Вставить("ИдентификаторБизнесЗаказа", "");
						ПараметрыЗапроса.Вставить("ИдентификаторСтрокиЗаказа", СтрокаТЧ.ИдентификаторСтроки);
						ПараметрыЗапроса.Вставить("Шаблон",                    СтрокаТЧ.Шаблон);
						ПараметрыЗапроса.Вставить("Количество",                СтрокаТЧ.Количество);
						СообщениеJSON.ПараметрыЗапроса.Добавить(ПараметрыЗапроса);
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
			
			ИнтерфейсСУЗ.ТелоЗапросаПоФорматуОбмена(ТелоЗапроса, ФорматОбмена, Операция, Шапка.ВидПродукции);
			
			ТелоСообщенияJSON = ОбщегоНазначенияИСМП.ОбъектВТекстJSON(ТелоЗапроса, Истина);
			
			СообщениеJSON.ТекстСообщенияJSON = ТелоСообщенияJSON;
			СообщениеJSON.ТипСообщения       = Перечисления.ТипыЗапросовИС.Исходящий;
			
			СообщениеJSON.ВидПродукции       = Шапка.ВидПродукции;
			СообщениеJSON.Операция           = Операция;
			
			СообщениеJSON.Версия             = НомерВерсии;
			
			СообщениеJSON.ТребуетсяПодписание       = Истина;
			СообщениеJSON.Назначение                = Перечисления.НазначениеСообщенийИСМП.СтанцияУправленияЗаказами;
			СообщениеJSON.ФорматОбмена              = ФорматОбмена;
			СообщениеJSON.СтанцияУправленияЗаказами = СтанцияУправленияЗаказами;
			СообщениеJSON.ПроизводственныйОбъект    = Шапка.ПроизводственныйОбъект;
			
			СообщенияJSON.Добавить(СообщениеJSON);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат СообщенияJSON;
	
КонецФункции

Процедура РаспределитьКоличествоПоНовымСтрокам(ВходнаяТаблицаТовары, ФорматОбмена)

	РаспеределениеЗавершено = Ложь;
	
	ПределДляОднойСтроки = ИнтеграцияИСМПСлужебный.МаксимальноеКоличествоКодовПоСтрокеЗаказа(ФорматОбмена).ПределДляОднойСтроки;
	
	ИндексРаспределения = 0;
	Пока Не РаспеределениеЗавершено Цикл
		
		КонечныйИндекс = ВходнаяТаблицаТовары.Количество() - 1;
		
		РаспеределениеЗавершено = Истина;
		
		Для ИндексСтроки = ИндексРаспределения По КонечныйИндекс Цикл
			
			ТекущаяСтрока = ВходнаяТаблицаТовары.Получить(ИндексСтроки);
			Если ТекущаяСтрока.Количество <= ПределДляОднойСтроки Тогда
				Продолжить;
			КонецЕсли;
			РаспеределениеЗавершено = Ложь;
			
			НоваяСтрока = ВходнаяТаблицаТовары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			НоваяСтрока.Количество   = НоваяСтрока.Количество - ПределДляОднойСтроки;
			ТекущаяСтрока.Количество = ПределДляОднойСтроки;
			
		КонецЦикла;
		
		ИндексРаспределения = КонечныйИндекс + 1;
		
	КонецЦикла;
	
КонецПроцедуры

Функция МассивЗаказовПоТаблицеТоваров(ВходнаяТаблицаТовары, ФорматОбмена, ВидПродукции)
	
	МассивЗаказов                       = Новый Массив;
	МаксимальноеКоличествоСтрокВЗаказе  = ИнтеграцияИСМПСлужебный.МаксимальноеКоличествоСтрокВЗаказе();
	МаксимальноеКоличествоКодовПоСтроке = ИнтеграцияИСМПСлужебный.МаксимальноеКоличествоКодовПоСтрокеЗаказа(ФорматОбмена);
	ЭтоМех                              = (ВидПродукции = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха);
	
	ТабличнаяЧастьТовары = ВходнаяТаблицаТовары.Скопировать(
		Новый Структура("СпособФормированияСерийногоНомера",
		Перечисления.СпособыФормированияСерийногоНомераСУЗ.ПустаяСсылка()));
	ТабличнаяЧастьТовары.Очистить();
	
	Для Каждого СтрокаТаблицы Из ВходнаяТаблицаТовары Цикл
		
		Если Не СтрокаТаблицыГруппируетсяВЗаказеНаЭмиссию(СтрокаТаблицы) Тогда
			
			НоваяСтрока = ТабличнаяЧастьТовары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			СтрокаТаблицы.СтрокаДобавлена = Истина;
		
		ИначеЕсли ЭтоМех Тогда
			СтрокаТаблицы.КодТНВЭД = ИнтеграцияИСКлиентСервер.КодТНВЭДДляПередачиВИСМП(СтрокаТаблицы.КодТНВЭД, ВидПродукции)
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЭтоМех Тогда
		ПоляСвертки  = "GTIN, КодТНВЭД, СпособВводаВОборот, ВидКИЗ, РазмерКИЗ, Шаблон";
		ПоляПодсчета = ПоляСвертки;
	Иначе
		ПоляСвертки  = "GTIN, ВладелецGTIN, СпособФормированияСерийногоНомера, Шаблон, СрокГодности";
		ПоляПодсчета = "GTIN";
	КонецЕсли;
	
	ВременнаТаблицаТовары = ВходнаяТаблицаТовары.Скопировать(
		Новый Структура("СтрокаДобавлена", Ложь));
	ВременнаТаблицаТовары.Свернуть(ПоляСвертки + ", СтрокаДобавлена", "Количество, КоличествоСтрок");
	ВременнаТаблицаТовары.ЗаполнитьЗначения(1, "КоличествоСтрок");
	ВременнаТаблицаТовары.Колонки.Добавить(
		"ИдентификаторСтроки", Метаданные.ОпределяемыеТипы["УникальныйИдентификаторИС"].Тип);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ВременнаТаблицаТовары, ТабличнаяЧастьТовары);

	ТаблицаПодсчетаGTIN = ТабличнаяЧастьТовары.Скопировать(, ПоляПодсчета + ", КоличествоСтрок, Количество");
	ТаблицаПодсчетаGTIN.Свернуть(ПоляПодсчета, "КоличествоСтрок, Количество");
	ТабличнаяЧастьТовары.Индексы.Добавить(ПоляПодсчета + ", СтрокаДобавлена");
	
	ПоследнийЗаказИндивидуализирован = Неопределено;
	
	СодержаниеЗаказа = ТабличнаяЧастьТовары.СкопироватьКолонки();
	СодержаниеЗаказаСОднойСтрокой = ТабличнаяЧастьТовары.СкопироватьКолонки();

	Пока Истина Цикл
		
		Если ЭтоМех Тогда
			ПоляСортировки = "GTIN Убыв, КоличествоСтрок Убыв, Количество Убыв";
		Иначе
			ПоляСортировки = "КоличествоСтрок Убыв, Количество Убыв, GTIN Убыв";
		КонецЕсли;
		
		ТаблицаПодсчетаGTIN.Сортировать(ПоляСортировки);
		
		СодержаниеЗаказа.Очистить();
		
		ДобавленНовыйЗаказ = Ложь;
		
		Для Каждого СтрокаТаблицыПодсчета Из ТаблицаПодсчетаGTIN Цикл
			
			Если СтрокаТаблицыПодсчета.КоличествоСтрок = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если ПоследнийЗаказИндивидуализирован = Неопределено Тогда
				ПоследнийЗаказИндивидуализирован = ЗначениеЗаполнено(СтрокаТаблицыПодсчета.GTIN);
			ИначеЕсли ПоследнийЗаказИндивидуализирован <> ЗначениеЗаполнено(СтрокаТаблицыПодсчета.GTIN) Тогда
				Если СодержаниеЗаказа.Количество() Тогда
					СодержаниеЗаказа.Сортировать(ПоляСортировки);
					МассивЗаказов.Добавить(ОбщегоНазначения.ТаблицаЗначенийВМассив(СодержаниеЗаказа));
				КонецЕсли;
				СодержаниеЗаказа.Очистить();
				ПоследнийЗаказИндивидуализирован = ЗначениеЗаполнено(СтрокаТаблицыПодсчета.GTIN);
			КонецЕсли;
			
			СтруктураПоискаСтрок = Новый Структура(ПоляПодсчета);
			ЗаполнитьЗначенияСвойств(СтруктураПоискаСтрок, СтрокаТаблицыПодсчета);
			СтруктураПоискаСтрок.Вставить("СтрокаДобавлена", Ложь);
			
			ПоискСтрокТовары = ТабличнаяЧастьТовары.НайтиСтроки(СтруктураПоискаСтрок);
			
			ВсеСтрокиПоGTINВыбраны = Истина;
			Для Каждого СтрокаТаблицы Из ПоискСтрокТовары Цикл
				
				Если СодержаниеЗаказа.Количество() = МаксимальноеКоличествоСтрокВЗаказе Тогда
					
					ВсеСтрокиПоGTINВыбраны = Ложь;
					
				ИначеЕсли СтрокаТаблицы.Количество > МаксимальноеКоличествоКодовПоСтроке.ПределОбщий Тогда
					
					Если МаксимальноеКоличествоКодовПоСтроке.ПределДляОднойСтроки > МаксимальноеКоличествоКодовПоСтроке.ПределОбщий Тогда
						
						Если СтрокаТаблицы.Количество > МаксимальноеКоличествоКодовПоСтроке.ПределДляОднойСтроки Тогда
							
							ВсеСтрокиПоGTINВыбраны = Ложь;
							
							НоваяСтрока = ТабличнаяЧастьТовары.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
							НоваяСтрока.Количество = СтрокаТаблицы.Количество - МаксимальноеКоличествоКодовПоСтроке.ПределДляОднойСтроки;
							
							СтрокаТаблицы.Количество = МаксимальноеКоличествоКодовПоСтроке.ПределДляОднойСтроки;
							
						КонецЕсли;
						
						СтрокаТаблицы.СтрокаДобавлена = Истина;
						
						ЗаполнитьЗначенияСвойств(СодержаниеЗаказаСОднойСтрокой.Добавить(), СтрокаТаблицы);
						МассивЗаказов.Добавить(ОбщегоНазначения.ТаблицаЗначенийВМассив(СодержаниеЗаказаСОднойСтрокой));
						СодержаниеЗаказаСОднойСтрокой.Очистить();
						
						ДобавленНовыйЗаказ = Истина;
						
					Иначе
						
						ВсеСтрокиПоGTINВыбраны = Ложь;
						
						НоваяСтрока = ТабличнаяЧастьТовары.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
						НоваяСтрока.Количество = СтрокаТаблицы.Количество - МаксимальноеКоличествоКодовПоСтроке.ПределОбщий;
						
						СтрокаТаблицы.СтрокаДобавлена = Истина;
						СтрокаТаблицы.Количество      = МаксимальноеКоличествоКодовПоСтроке.ПределОбщий;
						ЗаполнитьЗначенияСвойств(СодержаниеЗаказа.Добавить(), СтрокаТаблицы);
						
					КонецЕсли;
					
				Иначе
					
					СтрокаТаблицы.СтрокаДобавлена = Истина;
					ЗаполнитьЗначенияСвойств(СодержаниеЗаказа.Добавить(), СтрокаТаблицы);
					
				КонецЕсли;
				
				Прервать;
				
			КонецЦикла;
			
			Если ВсеСтрокиПоGTINВыбраны Тогда
				СтрокаТаблицыПодсчета.КоличествоСтрок = СтрокаТаблицыПодсчета.КоличествоСтрок - 1;
			КонецЕсли;
			
		КонецЦикла;
		
		Если СодержаниеЗаказа.Количество() Тогда
			СодержаниеЗаказа.Сортировать(ПоляСортировки);
			МассивЗаказов.Добавить(ОбщегоНазначения.ТаблицаЗначенийВМассив(СодержаниеЗаказа));
		ИначеЕсли Не ДобавленНовыйЗаказ Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат МассивЗаказов;
	
КонецФункции

Функция СтрокаТаблицыГруппируетсяВЗаказеНаЭмиссию(СтрокаТаблицы)
	
	Если СтрокаТаблицы.СпособФормированияСерийногоНомера = Перечисления.СпособыФормированияСерийногоНомераСУЗ.Самостоятельно
			Или СтрокаТаблицы.Шаблон = Перечисления.ШаблоныКодовМаркировкиСУЗ.МолочнаяПродукцияПодконтрольнаяВЕТИС
			Или СтрокаТаблицы.Шаблон = Перечисления.ШаблоныКодовМаркировкиСУЗ.СкоропортящаясяМолочнаяПродукцияВЕТИС Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

// Формирует JSON сообщения для получения GTIN на остатки товаров
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ЗаказНаЭмиссиюКодовМаркировкиСУЗ - Документ Маркировка товаров ИС МП.
//  ДополнительныеПараметры - Структура - Дополнительные параметры
//
// Возвращаемое значение:
//  Массив Из См. ИнтеграцияИСМПСлужебный.СтруктураСообщенияJSON - Сообщения к передаче
Функция ПолучениеGTINНаОстаткиJSON(ДокументСсылка, ДополнительныеПараметры) Экспорт
	
	Операция = Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировкиЗапросGTINНаОстатки;
	
	СообщенияJSON = Новый Массив;
	
	СписокЗапросов = Новый СписокЗначений;
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	ИСМППрисоединенныеФайлы.ВладелецФайла                 КАК Ссылка,
	|	МАКСИМУМ(ЕСТЬNULL(ИСМППрисоединенныеФайлы.Версия, 0)) КАК ПоследнийНомерВерсии
	|ПОМЕСТИТЬ Версии
	|ИЗ
	|	Справочник.ИСМППрисоединенныеФайлы КАК ИСМППрисоединенныеФайлы
	|ГДЕ
	|	ИСМППрисоединенныеФайлы.ВладелецФайла = &Ссылка
	|	И ИСМППрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
	|СГРУППИРОВАТЬ ПО
	|	ИСМППрисоединенныеФайлы.ВладелецФайла
	|");
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	Шапка.Номер                              КАК Номер,
	|	Шапка.Дата                               КАК Дата,
	|	ЕСТЬNULL(Версии.ПоследнийНомерВерсии, 0) КАК ПоследнийНомерВерсии,
	|	Шапка.ДокументОснование                  КАК ДокументОснование,
	|	
	|	// Общие реквизиты для табака и обуви
	|	Шапка.ВидПродукции                         КАК ВидПродукции,
	|	Шапка.ИдентификаторПроизводственногоЗаказа КАК ИдентификаторПроизводственногоЗаказа,
	|
	|	// Основные реквизиты
	|	Шапка.Организация                КАК Организация,
	|	Представление(Шапка.Организация) КАК ОрганизацияПредставление
	|	
	|ИЗ
	|	Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ КАК Шапка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Версии КАК Версии
	|		ПО Шапка.Ссылка = Версии.Ссылка
	|ГДЕ
	|	Шапка.Ссылка = &Ссылка",
	"Шапка");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	РезультатЗапроса = ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов);
	
	//@skip-warning
	Шапка = РезультатЗапроса["Шапка"].Выбрать();
	Шапка.Следующий();
	
	НомерВерсии = Шапка.ПоследнийНомерВерсии + 1;
	
	АбстрактноеСообщениеJSON = ИнтеграцияИСМПСлужебный.СтруктураСообщенияJSON();
	
	АбстрактноеСообщениеJSON.Организация               = Шапка.Организация;
	АбстрактноеСообщениеJSON.Документ                  = ДокументСсылка;
	АбстрактноеСообщениеJSON.ДокументОснование         = Шапка.ДокументОснование;
	АбстрактноеСообщениеJSON.ВидПродукции              = Шапка.ВидПродукции;
	
	АбстрактноеСообщениеJSON.Операция                  = Операция;
	АбстрактноеСообщениеJSON.Версия                    = НомерВерсии;
	АбстрактноеСообщениеJSON.ПараметрыЗапроса          = Новый Массив;
	
	АбстрактноеСообщениеJSON.ИдентификаторЗаявки       = ИнтеграцияИСМПСлужебный.СтроковоеПредставлениеПустогоУникальногоИдентификатора();
	АбстрактноеСообщениеJSON.Идентификатор             = СокрЛП(Новый УникальныйИдентификатор());
	АбстрактноеСообщениеJSON.ТипСообщения              = Перечисления.ТипыЗапросовИС.Входящий;
	
	АбстрактноеСообщениеJSON.ТребуетсяПодписание = Ложь;
	АбстрактноеСообщениеJSON.Назначение          = Перечисления.НазначениеСообщенийИСМП.ИСМП;
	
	АбстрактноеСообщениеJSON.Описание = ИнтеграцияИСМПСлужебный.ОписаниеОперацииПередачиДанных(
		Операция, ДокументСсылка, НомерВерсии);
	
	СообщенияJSON.Добавить(АбстрактноеСообщениеJSON);
	
	Возврат СообщенияJSON;
	
КонецФункции

Функция ОписаниеОстатковJSON(ДокументСсылка, ДополнительныеПараметры = Неопределено) Экспорт
	
	Операция = Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировкиЗапросGTINНаОстатки;
	
	СообщенияJSON = Новый Массив;
	
	СписокЗапросов = Новый СписокЗначений;
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	ИСМППрисоединенныеФайлы.ВладелецФайла КАК Ссылка,
	|	КОЛИЧЕСТВО(ИСМППрисоединенныеФайлы.Ссылка) КАК ПоследнийНомерВерсии
	|ПОМЕСТИТЬ Версии
	|ИЗ
	|	Справочник.ИСМППрисоединенныеФайлы КАК ИСМППрисоединенныеФайлы
	|ГДЕ
	|	ИСМППрисоединенныеФайлы.ВладелецФайла = &Ссылка
	|	И ИСМППрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
	|СГРУППИРОВАТЬ ПО
	|	ИСМППрисоединенныеФайлы.ВладелецФайла
	|");
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	Шапка.Номер                              КАК Номер,
	|	Шапка.Дата                               КАК Дата,
	|	ЕСТЬNULL(Версии.ПоследнийНомерВерсии, 0) КАК ПоследнийНомерВерсии,
	|	Шапка.ДокументОснование                  КАК ДокументОснование,
	|	
	|	// Общие реквизиты для табака и обуви
	|	Шапка.ВидПродукции                         КАК ВидПродукции,
	|	Шапка.ИдентификаторПроизводственногоЗаказа КАК ИдентификаторПроизводственногоЗаказа,
	|
	|	// Основные реквизиты
	|	Шапка.Организация                КАК Организация,
	|	Представление(Шапка.Организация) КАК ОрганизацияПредставление,
	|	Шапка.ПроизводственныйОбъект     КАК ПроизводственныйОбъект,
	|
	|	Шапка.Ответственный                КАК Ответственный,
	|	Представление(Шапка.Ответственный) КАК ОтветственныйПредставление
	|
	|ИЗ
	|	Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ КАК Шапка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Версии КАК Версии
	|		ПО Шапка.Ссылка = Версии.Ссылка
	|ГДЕ
	|	Шапка.Ссылка = &Ссылка",
	"Шапка");
	
	ЗапросТовары ="
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.КодТНВЭД КАК КодТНВЭД,
	|	%1
	|ИЗ
	|	Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &Ссылка
	|	И Товары.GTIN = """"
	|";
	
	ШаблонПоляЗапроса = "Товары.%1 КАК %1";
	МассивПолейТовары = Новый Массив();
	МассивПолейТовары.Добавить(СтрШаблон(ШаблонПоляЗапроса, "GTIN"));
	
	ВидПродукции = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ВидПродукции");
	
	Если ВидПродукции = Перечисления.ВидыПродукцииИС.ЛегкаяПромышленность
		Или ВидПродукции = Перечисления.ВидыПродукцииИС.Шины
		Или ВидПродукции = Перечисления.ВидыПродукцииИС.Духи
		Или ОбщегоНазначенияИСКлиентСервер.ЭтоПродукцияМОТП(ВидПродукции) Тогда
		МассивПолейТовары.Добавить(СтрШаблон(ШаблонПоляЗапроса, "ТоварныйЗнак"));
	КонецЕсли;
	
	Если ВидПродукции = Перечисления.ВидыПродукцииИС.Обувь Тогда
		МассивПолейТовары.Добавить(СтрШаблон(ШаблонПоляЗапроса, "ЦелевойПол"));
		МассивПолейТовары.Добавить(СтрШаблон(ШаблонПоляЗапроса, "СпособВводаВОборот"));
	ИначеЕсли ВидПродукции = Перечисления.ВидыПродукцииИС.Шины Тогда
		МассивПолейТовары.Добавить(СтрШаблон(ШаблонПоляЗапроса, "Модель"));
	ИначеЕсли ВидПродукции = Перечисления.ВидыПродукцииИС.ЛегкаяПромышленность Тогда
		МассивПолейТовары.Добавить(СтрШаблон(ШаблонПоляЗапроса, "ЦелевойПол"));
		МассивПолейТовары.Добавить(СтрШаблон(ШаблонПоляЗапроса, "ВозрастнаяКатегория"));
		МассивПолейТовары.Добавить(СтрШаблон(ШаблонПоляЗапроса, "СпособВводаВОборот"));
	ИначеЕсли ВидПродукции = Перечисления.ВидыПродукцииИС.Духи
		Или ОбщегоНазначенияИСКлиентСервер.ЭтоПродукцияМОТП(ВидПродукции) Тогда
		МассивПолейТовары.Добавить("ВЫБОР
			                       |	КОГДА Товары.Наименование = """" ТОГДА Товары.Номенклатура
			                       |	ИНАЧЕ """"
			                       |КОНЕЦ КАК Номенклатура");
		МассивПолейТовары.Добавить("ВЫБОР
			                       |	КОГДА Товары.Наименование = """" ТОГДА Товары.Характеристика
			                       |	ИНАЧЕ """" КОНЕЦ
			                       |КАК Характеристика");
		МассивПолейТовары.Добавить(СтрШаблон(ШаблонПоляЗапроса, "Наименование"));
	КонецЕсли;
	
	ДополнениеТекстаЗапроса = СтрСоединить(
		МассивПолейТовары,
		СтрШаблон(",%1%2", Символы.ПС, Символы.Таб));
	
	СписокЗапросов.Добавить(
		СтрШаблон(
			ЗапросТовары,
			ДополнениеТекстаЗапроса),
		"Товары");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	РезультатЗапроса = ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов);
	
	//@skip-warning
	Шапка = РезультатЗапроса["Шапка"].Выбрать();
	Шапка.Следующий();
	
	//@skip-warning
	ТабличнаяЧастьТовары = РезультатЗапроса["Товары"].Выгрузить();
	Если ТабличнаяЧастьТовары.Количество() = 0 Тогда
		
		СообщениеJSON             = ИнтеграцияИСМПСлужебный.СтруктураСообщенияJSON();
		СообщениеJSON.Документ    = ДокументСсылка;
		СообщениеJSON.Организация = Шапка.Организация;
		СообщениеJSON.Описание    = ИнтеграцияИСМПСлужебный.ОписаниеОперацииПередачиДанных(
			Операция, ДокументСсылка);
		СообщениеJSON.ТекстОшибки = НСтр("ru = 'Нет данных для выгрузки.'");
		СообщенияJSON.Добавить(СообщениеJSON);
		
		Возврат СообщенияJSON;
		
	КонецЕсли;
	
	НомерВерсии = Шапка.ПоследнийНомерВерсии + 1;
	
	РеквизитыОрганизации = РаботаСКонтрагентамиИСВызовСервера.ИННКПППоОрганизацииКонтрагенту(Шапка.Организация);
	
	СообщениеJSON = ИнтеграцияИСМПСлужебный.СтруктураСообщенияJSON();
	СообщениеJSON.Организация       = Шапка.Организация;
	СообщениеJSON.Документ          = ДокументСсылка;
	СообщениеJSON.ДокументОснование = Шапка.ДокументОснование;
	
	СообщениеJSON.Описание = ИнтеграцияИСМПСлужебный.ОписаниеОперацииПередачиДанных(
		Операция, ДокументСсылка, НомерВерсии);
	
	ТелоЗапроса = Новый Структура;
	ТелоЗапроса.Вставить("products_list",         Новый Массив);
	ТелоЗапроса.Вставить("trade_participant_inn", РеквизитыОрганизации.ИНН);
	
	ДобавленныеСтроки = Новый Соответствие();
	
	Для Каждого СтрокаТЧ Из ТабличнаяЧастьТовары Цикл
		
		СтрокаЗаказа = ИнтеграцияИСМПСлужебный.ОбязательныеПоляОписанияОстатковПоСтрокеДокумента(
			СтрокаТЧ, Шапка.ВидПродукции);
		
		Если Шапка.ВидПродукции = Перечисления.ВидыПродукцииИС.Табак Тогда
			СтрокаЗаказа.Вставить("product_group", "TOBACCO");
		ИначеЕсли Шапка.ВидПродукции = Перечисления.ВидыПродукцииИС.АльтернативныйТабак Тогда
			СтрокаЗаказа.Вставить("product_group", "OTP");
		КонецЕсли;
		
		КлючПоиска = "";
		Для Каждого КлючИЗначение Из СтрокаЗаказа Цикл
			КлючПоиска = СтрШаблон(
				"%1%2%3",
				КлючПоиска,
				КлючИЗначение.Ключ,
				ИнтеграцияИСМПСлужебный.НормализованноеСтроковоеЗначение(КлючИЗначение.Значение));
		КонецЦикла;
		
		Если ДобавленныеСтроки.Получить(КлючПоиска) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТелоЗапроса["products_list"].Добавить(СтрокаЗаказа);
		ДобавленныеСтроки.Вставить(КлючПоиска, СтрокаЗаказа);
		
	КонецЦикла;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("ИдентификаторБизнесЗаказа", "");
	ПараметрыЗапроса.Вставить("ИдентификаторСтрокиЗаказа", "");
	
	ПараметрыЗапросов = Новый Массив;
	ПараметрыЗапросов.Добавить(ПараметрыЗапроса);
	
	ТелоСообщенияJSON = ОбщегоНазначенияИСМП.ОбъектВТекстJSON(ТелоЗапроса, Истина);
	
	СообщениеJSON.ТекстСообщенияJSON = ТелоСообщенияJSON;
	СообщениеJSON.ТипСообщения       = Перечисления.ТипыЗапросовИС.Исходящий;
	
	СообщениеJSON.ВидПродукции       = Шапка.ВидПродукции;
	СообщениеJSON.Операция           = Операция;
	
	СообщениеJSON.Версия             = НомерВерсии;
	СообщениеJSON.ПараметрыЗапроса   = ПараметрыЗапросов;
	
	СообщениеJSON.ТребуетсяПодписание       = Истина;
	СообщениеJSON.Назначение                = Перечисления.НазначениеСообщенийИСМП.ИСМП;
	СообщениеJSON.ФорматОбмена              = Неопределено;
	СообщениеJSON.СтанцияУправленияЗаказами = Неопределено;
	
	СообщенияJSON.Добавить(СообщениеJSON);
	
	Возврат СообщенияJSON;
	
КонецФункции

Функция ПолучениеКодовМаркировки(ДокументСсылка, ДополнительныеПараметры = Неопределено) Экспорт
	
	АбстрактнаяОперация = Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировкиРасчетСтатуса;
	Операция            = Перечисления.ВидыОперацийИСМП.ПолучениеКодовМаркировкиИзСУЗНезависимо;
	СообщенияJSON       = Новый Массив;
	СписокЗапросов      = Новый СписокЗначений;
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	ИСМППрисоединенныеФайлы.ВладелецФайла                 КАК Ссылка,
	|	МАКСИМУМ(ЕСТЬNULL(ИСМППрисоединенныеФайлы.Версия, 0)) КАК ПоследнийНомерВерсии
	|ПОМЕСТИТЬ Версии
	|ИЗ
	|	Справочник.ИСМППрисоединенныеФайлы КАК ИСМППрисоединенныеФайлы
	|ГДЕ
	|	ИСМППрисоединенныеФайлы.ВладелецФайла  = &Ссылка
	|	И ИСМППрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
	|	И ИСМППрисоединенныеФайлы.Операция     = &Операция
	|СГРУППИРОВАТЬ ПО
	|	ИСМППрисоединенныеФайлы.ВладелецФайла
	|");
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	ИСМППрисоединенныеФайлы.Ссылка               КАК Ссылка,
	|	ИСМППрисоединенныеФайлы.ВладелецФайла        КАК ВладелецФайла,
	|	ИСМППрисоединенныеФайлы.ИдентификаторЗапроса КАК ИдентификаторЗаявки
	|ИЗ
	|	Справочник.ИСМППрисоединенныеФайлы КАК ИСМППрисоединенныеФайлы
	|ГДЕ
	|	ИСМППрисоединенныеФайлы.ВладелецФайла     = &Ссылка
	|	И ИСМППрисоединенныеФайлы.ТипСообщения    = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
	|	И ИСМППрисоединенныеФайлы.Операция        = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировки)
	|	И ИСМППрисоединенныеФайлы.СтатусОбработки = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена)
	|	И ИСМППрисоединенныеФайлы.Версия В
	|			(ВЫБРАТЬ
	|				МАКСИМУМ(ИСМППрисоединенныеФайлыВерсия.Версия) КАК Версия
	|			ИЗ
	|				Справочник.ИСМППрисоединенныеФайлы КАК ИСМППрисоединенныеФайлыВерсия
	|			ГДЕ
	|				ИСМППрисоединенныеФайлыВерсия.ВладелецФайла  = &Ссылка
	|				И ИСМППрисоединенныеФайлыВерсия.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
	|				И ИСМППрисоединенныеФайлыВерсия.Операция     = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировки))
	|",
	"ПрисоединенныеФайлы");
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	Шапка.Номер                              КАК Номер,
	|	Шапка.Дата                               КАК Дата,
	|	ЕСТЬNULL(Версии.ПоследнийНомерВерсии, 0) КАК ПоследнийНомерВерсии,
	|
	|	// Основные реквизиты
	|	Шапка.Организация                КАК Организация,
	|	Шапка.ВидПродукции               КАК ВидПродукции,
	|	Шапка.ДокументОснование          КАК ДокументОснование,
	|	Шапка.GLNОрганизации             КАК GLNОрганизации,
	|	Представление(Шапка.Организация) КАК ОрганизацияПредставление,
	|	Шапка.ПроизводственныйОбъект     КАК ПроизводственныйОбъект
	|
	|ИЗ
	|	Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ КАК Шапка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Версии КАК Версии
	|		ПО Шапка.Ссылка = Версии.Ссылка
	|ГДЕ
	|	Шапка.Ссылка = &Ссылка",
	"Шапка");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",   ДокументСсылка);
	Запрос.УстановитьПараметр("Операция", Операция);
	
	РезультатЗапроса = ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов);
	
	//@skip-warning
	Шапка = РезультатЗапроса["Шапка"].Выбрать();
	Шапка.Следующий();
	
	//@skip-warning
	ПрисоединенныеФайлы = РезультатЗапроса["ПрисоединенныеФайлы"].Выгрузить();
	Если ПрисоединенныеФайлы.Количество() = 0 Тогда
		
		СообщениеJSON             = ИнтеграцияИСМПСлужебный.СтруктураСообщенияJSON();
		СообщениеJSON.Документ    = ДокументСсылка;
		СообщениеJSON.Организация = Шапка.Организация;
		СообщениеJSON.Описание    = ИнтеграцияИСМПСлужебный.ОписаниеОперацииПередачиДанных(
			Операция, ДокументСсылка);
		СообщениеJSON.ТекстОшибки         = НСтр("ru = 'Нет данных для дозагрузки кодов маркировки из заказа.'");
		СообщениеJSON.ТребуетсяПодписание = Ложь;
		СообщенияJSON.Добавить(СообщениеJSON);
		
		Возврат СообщенияJSON;
		
	ИначеЕсли Шапка.ВидПродукции <> Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха Тогда
		
		СообщениеJSON             = ИнтеграцияИСМПСлужебный.СтруктураСообщенияJSON();
		СообщениеJSON.Документ    = ДокументСсылка;
		СообщениеJSON.Организация = Шапка.Организация;
		СообщениеJSON.Описание    = ИнтеграцияИСМПСлужебный.ОписаниеОперацииПередачиДанных(
			Операция, ДокументСсылка);
		СообщениеJSON.ТребуетсяПодписание = Ложь;
		СообщениеJSON.ТекстОшибки         = НСтр("ru = 'Внутренняя ошибка. Не определен способ восстановления данных исходного заказа.'");
		СообщенияJSON.Добавить(СообщениеJSON);
		
		Возврат СообщенияJSON;
		
	КонецЕсли;
	
	НомерВерсии = Шапка.ПоследнийНомерВерсии + 1;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("ОсновнаяОперация", Операция);
	
	АбстрактноеСообщениеJSON = ИнтеграцияИСМПСлужебный.СтруктураСообщенияJSON();
	АбстрактноеСообщениеJSON.Организация               = Шапка.Организация;
	АбстрактноеСообщениеJSON.Документ                  = ДокументСсылка;
	АбстрактноеСообщениеJSON.ДокументОснование         = Шапка.ДокументОснование;
	АбстрактноеСообщениеJSON.ВидПродукции              = Шапка.ВидПродукции;
	АбстрактноеСообщениеJSON.Операция                  = АбстрактнаяОперация;
	АбстрактноеСообщениеJSON.Версия                    = НомерВерсии;
	АбстрактноеСообщениеJSON.ПараметрыЗапроса          = ПараметрыЗапроса;
	АбстрактноеСообщениеJSON.ИдентификаторЗаявки       = ИнтеграцияИСМПСлужебный.СтроковоеПредставлениеПустогоУникальногоИдентификатора();
	АбстрактноеСообщениеJSON.Идентификатор             = СокрЛП(Новый УникальныйИдентификатор());
	АбстрактноеСообщениеJSON.ТипСообщения              = Перечисления.ТипыЗапросовИС.Исходящий;
	АбстрактноеСообщениеJSON.ТребуетсяПодписание       = Ложь;
	АбстрактноеСообщениеJSON.ПроизводственныйОбъект    = Шапка.ПроизводственныйОбъект;
	АбстрактноеСообщениеJSON.Назначение = Перечисления.НазначениеСообщенийИСМП.СтанцияУправленияЗаказами;
	АбстрактноеСообщениеJSON.Описание   = ИнтеграцияИСМПСлужебный.ОписаниеОперацииПередачиДанных(
		АбстрактнаяОперация, ДокументСсылка, НомерВерсии);
		
	РезультатОпределенияСУЗ = ИнтеграцияИСМПСлужебный.ОпределитьСтанциюУправленияЗаказами(
		СообщенияJSON, СообщениеJSON, Шапка, ДополнительныеПараметры);
	Если Не РезультатОпределенияСУЗ.Успешно Тогда
		Возврат СообщенияJSON;
	КонецЕсли;
	
	АбстрактноеСообщениеJSON.СтанцияУправленияЗаказами = РезультатОпределенияСУЗ.СтанцияУправленияЗаказами;
	АбстрактноеСообщениеJSON.ФорматОбмена              = РезультатОпределенияСУЗ.НастройкаОбмена.ФорматОбмена;
	
	СообщенияJSON.Добавить(АбстрактноеСообщениеJSON);
	
	Для Каждого СтрокаПрисоединенныйФайл Из ПрисоединенныеФайлы Цикл
		
		ПротоколОбмена = ОбщегоНазначения.ЗначениеИзСтрокиXML(
			ПротоколОбменаИС.ТекстСообщенияXMLИзПротокола(СтрокаПрисоединенныйФайл.Ссылка));
		Если ТипЗнч(ПротоколОбмена) <> Тип("ТаблицаЗначений") Тогда
			СообщениеJSON             = ИнтеграцияИСМПСлужебный.СтруктураСообщенияJSON();
			СообщениеJSON.Документ    = ДокументСсылка;
			СообщениеJSON.Организация = Шапка.Организация;
			СообщениеJSON.Описание    = ИнтеграцияИСМПСлужебный.ОписаниеОперацииПередачиДанных(
				Операция, ДокументСсылка);
			СообщениеJSON.ТекстОшибки =  СтрШаблон(
				НСтр("ru = 'Отсутствуют данные протокола обмена по документу: %1'"),
				ДокументСсылка);
			СообщенияJSON.Добавить(СообщениеJSON);
			Возврат СообщенияJSON;
		КонецЕсли;
		
		ПараметрыЗаказа = ИнтеграцияИСМПСлужебный.НоваяТаблицаПараметровЗаказаКодовМаркировки();
		
		Для Каждого СтрокаПротокола Из ПротоколОбмена Цикл
			Если СтрокаПротокола.Операция <> Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировки Тогда
				Продолжить;
			КонецЕсли;
			ДанныеОбработки = ОбщегоНазначенияИСМП.ТекстJSONВОбъект(СтрокаПротокола.ЗапросТело, Истина);
			Если ДанныеОбработки <> Неопределено Тогда
				Для Каждого СтрокаЗаказа Из ДанныеОбработки["products"] Цикл
					НоваяСтрока = ПараметрыЗаказа.Добавить();
					НоваяСтрока.КодТНВЭД           = СтрокаЗаказа["tnvedCode"];
					НоваяСтрока.РазмерКИЗ          = ИнтерфейсСУЗСлужебный.РазмерКИЗ(СтрокаЗаказа["sizeType"]);
					НоваяСтрока.ВидКИЗ             = ИнтерфейсСУЗСлужебный.ВидКИЗ(СтрокаЗаказа["identificationType"]);
					НоваяСтрока.СпособВводаВОборот = ИнтерфейсСУЗСлужебный.СпособВыпускаВОборот(СтрокаЗаказа["releaseMethodType"]);
					НоваяСтрока.Шаблон             = Перечисления.ШаблоныКодовМаркировкиСУЗ.ПродукцияИзНатуральногоМеха;
					НоваяСтрока.GTIN               = СтрокаЗаказа["gtin"];
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
		СообщениеJSON = ИнтеграцияИСМПСлужебный.СтруктураСообщенияJSON();
		СообщениеJSON.Организация       = Шапка.Организация;
		СообщениеJSON.Документ          = ДокументСсылка;
		СообщениеJSON.ДокументОснование = Шапка.ДокументОснование;
		
		СообщениеJSON.Описание = ИнтеграцияИСМПСлужебный.ОписаниеОперацииПередачиДанных(
			Операция, ДокументСсылка, НомерВерсии);
		
		ПараметрыЗапроса = Новый Структура;
		ПараметрыЗапроса.Вставить("ИдентификаторБизнесЗаказа", СтрокаПрисоединенныйФайл.ИдентификаторЗаявки);
		ПараметрыЗапроса.Вставить("ИдентификаторСтрокиЗаказа", "");
		ПараметрыЗапроса.Вставить("ПараметрыЗаказа",           ПараметрыЗаказа);
		
		СообщениеJSON.ИдентификаторЗаявки = ИнтеграцияИСМПСлужебный.СтроковоеПредставлениеПустогоУникальногоИдентификатора();
		СообщениеJSON.ТипСообщения       = Перечисления.ТипыЗапросовИС.Исходящий;
		
		СообщениеJSON.ВидПродукции       = Шапка.ВидПродукции;
		СообщениеJSON.Операция           = Операция;
		
		СообщениеJSON.Версия             = НомерВерсии;
		СообщениеJSON.ПараметрыЗапроса   = ПараметрыЗапроса;
		
		СообщениеJSON.ТребуетсяПодписание       = Ложь;
		СообщениеJSON.Назначение                = Перечисления.НазначениеСообщенийИСМП.СтанцияУправленияЗаказами;
		СообщениеJSON.ФорматОбмена              = Неопределено;
		СообщениеJSON.СтанцияУправленияЗаказами = Неопределено;
		
		СообщениеJSON.СтанцияУправленияЗаказами = АбстрактноеСообщениеJSON.СтанцияУправленияЗаказами;
		СообщениеJSON.ФорматОбмена              = АбстрактноеСообщениеJSON.ФорматОбмена;
		СообщениеJSON.ЗагружатьДо               = АбстрактноеСообщениеJSON.Идентификатор;
		
		СообщенияJSON.Добавить(СообщениеJSON);
		
	КонецЦикла;
	
	Возврат СообщенияJSON;
	
КонецФункции

Функция ЗакрытиеЗаказаНаЭмиссию(ДокументСсылка, ДополнительныеПараметры = Неопределено) Экспорт
	
	АбстрактнаяОперация = Перечисления.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировкиРасчетСтатуса;
	Операция            = Перечисления.ВидыОперацийИСМП.ЗакрытиеЗаказаНаЭмиссиюСУЗ;
	СообщенияJSON       = Новый Массив;
	СписокЗапросов      = Новый СписокЗначений;
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	ИСМППрисоединенныеФайлы.ВладелецФайла                 КАК Ссылка,
	|	МАКСИМУМ(ЕСТЬNULL(ИСМППрисоединенныеФайлы.Версия, 0)) КАК ПоследнийНомерВерсии
	|ПОМЕСТИТЬ Версии
	|ИЗ
	|	Справочник.ИСМППрисоединенныеФайлы КАК ИСМППрисоединенныеФайлы
	|ГДЕ
	|	ИСМППрисоединенныеФайлы.ВладелецФайла  = &Ссылка
	|	И ИСМППрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
	|	И ИСМППрисоединенныеФайлы.Операция     = &Операция
	|СГРУППИРОВАТЬ ПО
	|	ИСМППрисоединенныеФайлы.ВладелецФайла
	|");
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	ИСМППрисоединенныеФайлы.Ссылка               КАК Ссылка,
	|	ИСМППрисоединенныеФайлы.ИдентификаторЗапроса КАК ИдентификаторЗаявки
	|ИЗ
	|	Справочник.ИСМППрисоединенныеФайлы КАК ИСМППрисоединенныеФайлы
	|ГДЕ
	|	ИСМППрисоединенныеФайлы.ВладелецФайла     = &Ссылка
	|	И ИСМППрисоединенныеФайлы.ТипСообщения    = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
	|	И ИСМППрисоединенныеФайлы.Операция        = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировки)
	|	И ИСМППрисоединенныеФайлы.СтатусОбработки = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена)
	|	И ИСМППрисоединенныеФайлы.Версия В
	|			(ВЫБРАТЬ
	|				МАКСИМУМ(ИСМППрисоединенныеФайлыВерсия.Версия) КАК Версия
	|			ИЗ
	|				Справочник.ИСМППрисоединенныеФайлы КАК ИСМППрисоединенныеФайлыВерсия
	|			ГДЕ
	|				ИСМППрисоединенныеФайлыВерсия.ВладелецФайла  = &Ссылка
	|				И ИСМППрисоединенныеФайлыВерсия.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
	|				И ИСМППрисоединенныеФайлыВерсия.Операция     = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ЗаказНаЭмиссиюКодовМаркировки))
	|",
	"ПрисоединенныеФайлы");
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	Шапка.Номер                              КАК Номер,
	|	Шапка.Дата                               КАК Дата,
	|	ЕСТЬNULL(Версии.ПоследнийНомерВерсии, 0) КАК ПоследнийНомерВерсии,
	|
	|	// Основные реквизиты
	|	Шапка.Организация                КАК Организация,
	|	Шапка.ВидПродукции               КАК ВидПродукции,
	|	Шапка.ДокументОснование          КАК ДокументОснование,
	|	Шапка.GLNОрганизации             КАК GLNОрганизации,
	|	Представление(Шапка.Организация) КАК ОрганизацияПредставление,
	|	Шапка.ПроизводственныйОбъект     КАК ПроизводственныйОбъект
	|
	|ИЗ
	|	Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ КАК Шапка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Версии КАК Версии
	|		ПО Шапка.Ссылка = Версии.Ссылка
	|ГДЕ
	|	Шапка.Ссылка = &Ссылка",
	"Шапка");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",   ДокументСсылка);
	Запрос.УстановитьПараметр("Операция", Операция);
	
	РезультатЗапроса = ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов);
	
	//@skip-warning
	Шапка = РезультатЗапроса["Шапка"].Выбрать();
	Шапка.Следующий();
	
	//@skip-warning
	ПрисоединенныеФайлы = РезультатЗапроса["ПрисоединенныеФайлы"].Выгрузить();
	Если ПрисоединенныеФайлы.Количество() = 0 Тогда
		
		СообщениеJSON             = ИнтеграцияИСМПСлужебный.СтруктураСообщенияJSON();
		СообщениеJSON.Документ    = ДокументСсылка;
		СообщениеJSON.Организация = Шапка.Организация;
		СообщениеJSON.Описание    = ИнтеграцияИСМПСлужебный.ОписаниеОперацииПередачиДанных(
			Операция, ДокументСсылка);
		СообщениеJSON.ТекстОшибки = НСтр("ru = 'Нет данных для закрытия заказа.'");
		СообщенияJSON.Добавить(СообщениеJSON);
		
		Возврат СообщенияJSON;
		
	КонецЕсли;
	
	НомерВерсии = Шапка.ПоследнийНомерВерсии + 1;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("ОсновнаяОперация", Операция);
	
	АбстрактноеСообщениеJSON = ИнтеграцияИСМПСлужебный.СтруктураСообщенияJSON();
	АбстрактноеСообщениеJSON.Организация               = Шапка.Организация;
	АбстрактноеСообщениеJSON.Документ                  = ДокументСсылка;
	АбстрактноеСообщениеJSON.ДокументОснование         = Шапка.ДокументОснование;
	АбстрактноеСообщениеJSON.ВидПродукции              = Шапка.ВидПродукции;
	АбстрактноеСообщениеJSON.Операция                  = АбстрактнаяОперация;
	АбстрактноеСообщениеJSON.Версия                    = НомерВерсии;
	АбстрактноеСообщениеJSON.ПараметрыЗапроса          = ПараметрыЗапроса;
	АбстрактноеСообщениеJSON.ИдентификаторЗаявки       = ИнтеграцияИСМПСлужебный.СтроковоеПредставлениеПустогоУникальногоИдентификатора();
	АбстрактноеСообщениеJSON.Идентификатор             = СокрЛП(Новый УникальныйИдентификатор());
	АбстрактноеСообщениеJSON.ТипСообщения              = Перечисления.ТипыЗапросовИС.Исходящий;
	АбстрактноеСообщениеJSON.ТребуетсяПодписание       = Ложь;
	АбстрактноеСообщениеJSON.ПроизводственныйОбъект    = Шапка.ПроизводственныйОбъект;
	АбстрактноеСообщениеJSON.Назначение = Перечисления.НазначениеСообщенийИСМП.СтанцияУправленияЗаказами;
	АбстрактноеСообщениеJSON.Описание   = ИнтеграцияИСМПСлужебный.ОписаниеОперацииПередачиДанных(
		АбстрактнаяОперация, ДокументСсылка, НомерВерсии);
		
	РезультатОпределенияСУЗ = ИнтеграцияИСМПСлужебный.ОпределитьСтанциюУправленияЗаказами(
		СообщенияJSON, СообщениеJSON, Шапка, ДополнительныеПараметры);
	Если Не РезультатОпределенияСУЗ.Успешно Тогда
		Возврат СообщенияJSON;
	КонецЕсли;
	
	АбстрактноеСообщениеJSON.СтанцияУправленияЗаказами = РезультатОпределенияСУЗ.СтанцияУправленияЗаказами;
	АбстрактноеСообщениеJSON.ФорматОбмена              = РезультатОпределенияСУЗ.НастройкаОбмена.ФорматОбмена;
	
	СообщенияJSON.Добавить(АбстрактноеСообщениеJSON);
	
	Для Каждого СтрокаПрисоединенныйФайл Из ПрисоединенныеФайлы Цикл
		
		ДатаДокументаЗакрытия = ТекущаяДатаСеанса();
		ТелоЗапроса           = Новый Структура();
		ТелоЗапроса.Вставить("orderId",         СтрокаПрисоединенныйФайл.ИдентификаторЗаявки);
		ТелоЗапроса.Вставить("senderGln",       Шапка.GLNОрганизации);
		ТелоЗапроса.Вставить("commitDocNumber", Шапка.Номер);
		ТелоЗапроса.Вставить("commitDocDate",   Формат(ДатаДокументаЗакрытия, "ДФ=yyyy-MM-dd;"));
		
		СообщениеJSON = ИнтеграцияИСМПСлужебный.СтруктураСообщенияJSON();
		СообщениеJSON.Организация       = Шапка.Организация;
		СообщениеJSON.Документ          = ДокументСсылка;
		СообщениеJSON.ДокументОснование = Шапка.ДокументОснование;
		
		СообщениеJSON.Описание = ИнтеграцияИСМПСлужебный.ОписаниеОперацииПередачиДанных(
			Операция, ДокументСсылка, НомерВерсии);
		
		ПараметрыЗапроса = Новый Структура;
		ПараметрыЗапроса.Вставить("ИдентификаторБизнесЗаказа", СтрокаПрисоединенныйФайл.ИдентификаторЗаявки);
		ПараметрыЗапроса.Вставить("ИдентификаторСтрокиЗаказа", "");
		
		СообщениеJSON.ТипСообщения       = Перечисления.ТипыЗапросовИС.Исходящий;
		
		СообщениеJSON.ВидПродукции       = Шапка.ВидПродукции;
		СообщениеJSON.Операция           = Операция;
		
		СообщениеJSON.Версия             = НомерВерсии;
		СообщениеJSON.ПараметрыЗапроса   = ПараметрыЗапроса;
		
		СообщениеJSON.ТребуетсяПодписание       = Истина;
		СообщениеJSON.Назначение                = Перечисления.НазначениеСообщенийИСМП.СтанцияУправленияЗаказами;
		
		СообщениеJSON.СтанцияУправленияЗаказами = АбстрактноеСообщениеJSON.СтанцияУправленияЗаказами;
		СообщениеJSON.ФорматОбмена              = АбстрактноеСообщениеJSON.ФорматОбмена;
		СообщениеJSON.ЗагружатьДо               = АбстрактноеСообщениеJSON.Идентификатор;
		СообщениеJSON.ТекстСообщенияJSON        = ОбщегоНазначенияИСМП.ОбъектВТекстJSON(ТелоЗапроса, Истина);
		
		СообщенияJSON.Добавить(СообщениеJSON);
		
	КонецЦикла;
	
	Возврат СообщенияJSON;
	
КонецФункции

Процедура ДобавитьВМассивДоступныеДействия(ДопустимыеДействия, Объект)
	
	ЗапроситьGTIN = Ложь;
	
	Если ТребуетсяЗапросGTIN(Объект) Тогда
		ЗапроситьGTIN = Истина;
	КонецЕсли;
		
	Если ЗапроситьGTIN Тогда
		ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗапроситеGTINНаОстатки);
	Иначе
		ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗапроситеКодыМаркировки);
	КонецЕсли;
	
КонецПроцедуры

Функция ДанныеРасчетаСтатусаЗаказаПоПулу(Документ, ВидПродукции) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура();
	ВозвращаемоеЗначение.Вставить("Статус");
	ВозвращаемоеЗначение.Вставить("СтатусОбработки", Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаВыполнена);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.GTIN КАК GTIN,
		|	СУММА(ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Количество) КАК Количество
		|ПОМЕСТИТЬ ДанныеДокумента
		|ИЗ
		|	Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ.Товары КАК ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары
		|ГДЕ
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.GTIN
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПулКодовМаркировкиСУЗ.GTIN КАК GTIN,
		|	СУММА(1) КАК КоличествоЭмитировано
		|ПОМЕСТИТЬ ДанныеПула
		|ИЗ
		|	РегистрСведений.ПулКодовМаркировкиСУЗ КАК ПулКодовМаркировкиСУЗ
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ КАК ЗаказНаЭмиссиюКодовМаркировкиСУЗ
		|		ПО ЗаказНаЭмиссиюКодовМаркировкиСУЗ.Ссылка = ПулКодовМаркировкиСУЗ.ЗаказНаЭмиссию
		|ГДЕ
		|	ПулКодовМаркировкиСУЗ.ЗаказНаЭмиссию = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ПулКодовМаркировкиСУЗ.GTIN
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.GTIN КАК GTIN,
		|	СУММА(ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Количество) КАК Количество
		|ИЗ
		|	Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ.Товары КАК ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары
		|ГДЕ
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка = &Ссылка
		|	И ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка.СервисПровайдер <> ЗНАЧЕНИЕ(Справочник.СервисПровайдерыСУЗ.ПустаяСсылка)
		|СГРУППИРОВАТЬ ПО
		|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.GTIN
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(ДанныеПула.КоличествоЭмитировано), 0) КАК КоличествоЭмитировано,
		|	ЕСТЬNULL(СУММА(ДанныеДокумента.Количество), 0) КАК Количество
		|ИЗ
		|	ДанныеДокумента КАК ДанныеДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПула КАК ДанныеПула
		|		ПО ДанныеДокумента.GTIN = ДанныеПула.GTIN";
	
	Запрос.УстановитьПараметр("Ссылка", Документ);
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаИзРезультатаЗапроса = Запрос.Выполнить().Выбрать();
	
	Если ВыборкаИзРезультатаЗапроса.Следующий() Тогда
		
		Если Не ЗначениеЗаполнено(ВыборкаИзРезультатаЗапроса.КоличествоЭмитировано) Тогда
			ВозвращаемоеЗначение.Статус          = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.Ошибка;
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОтклонена;
		ИначеЕсли ВыборкаИзРезультатаЗапроса.КоличествоЭмитировано < ВыборкаИзРезультатаЗапроса.Количество Тогда
			ВозвращаемоеЗначение.Статус = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.СУЗКодыМаркировкиЭмитированыЧастично;
		Иначе
			ВозвращаемоеЗначение.Статус = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.СУЗКодыМаркировкиЭмитированы;
		КонецЕсли;
		
	Иначе
		
		Если ВидПродукции = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха
			И Константы.РежимРаботыСТестовымКонтуромИСМП.Получить() Тогда
			ВозвращаемоеЗначение.Статус = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.СУЗКодыМаркировкиЭмитированыЧастично;
		Иначе
			ВозвращаемоеЗначение.Статус          = Перечисления.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.Ошибка;
			ВозвращаемоеЗначение.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийИСМП.ЗаявкаОтклонена;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ДанныеДоступныхGTIN(Объект, ПроверятьИНН = Ложь, ПроверятьШаблон = Ложь, ОграничениеПоВидуУпаковки = Неопределено) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура();
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("ДанныеПродукцииПоШтрихкодуEAN",  Новый Соответствие());
	ВозвращаемоеЗначение.Вставить("ПроверяемыеGTIN",                Новый Массив());
	ВозвращаемоеЗначение.Вставить("ТаблицаПроверки",                Новый ТаблицаЗначений());
	
	СтруктураОтбораПустойGTIN = Новый Структура("GTIN", "");
	ТаблицаПроверки = Объект.Товары.Выгрузить(СтруктураОтбораПустойGTIN, "Номенклатура,Характеристика");
	Если ТаблицаПроверки.Количество() = 0 Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	ТаблицаПроверки.Свернуть("Номенклатура,Характеристика");
	ТаблицаПроверки.Индексы.Добавить("Номенклатура,Характеристика");

	ПроверяемыеGTIN  = Новый Массив;
	СоответствиеGTIN = Новый Соответствие;
	
	ШтрихкодированиеИСПереопределяемый.ЗаполнитьПроверяемыеGTIN(ТаблицаПроверки, ПроверяемыеGTIN, СоответствиеGTIN);
	Для Каждого СтрокаТовары Из Объект.Товары Цикл
		Если ЗначениеЗаполнено(СтрокаТовары.GTIN)
			И ЗначениеЗаполнено(СтрокаТовары.Номенклатура)
			И Не РазборКодаМаркировкиИССлужебныйКлиентСервер.ЭтоШтрихкодВводаОстатков(СтрокаТовары.GTIN) Тогда
				
			Если ПроверяемыеGTIN.Найти(СтрокаТовары.GTIN) = Неопределено Тогда
				ПроверяемыеGTIN.Добавить(СтрокаТовары.GTIN);
				СоответствиеGTIN.Вставить(СтрокаТовары.GTIN,
					Новый Структура(
						"Номенклатура,Характеристика", СтрокаТовары.Номенклатура, СтрокаТовары.Характеристика));
			КонецЕсли;
		
		КонецЕсли;
	КонецЦикла;
	
	СписокЗначенийДляСортировки = Новый СписокЗначений;
	СписокЗначенийДляСортировки.ЗагрузитьЗначения(ПроверяемыеGTIN);
	СписокЗначенийДляСортировки.СортироватьПоЗначению();
	ПроверяемыеGTIN = СписокЗначенийДляСортировки.ВыгрузитьЗначения();
	
	ВозвращаемоеЗначение.ПроверяемыеGTIN = ПроверяемыеGTIN;
	
	Если ПроверятьШаблон Тогда
		ИменаКолонокТовары = "Номенклатура,Характеристика,Шаблон";
	Иначе
		ИменаКолонокТовары = "Номенклатура,Характеристика";
	КонецЕсли;
	
	ТаблицаПроверкиПолная = Объект.Товары.Выгрузить(СтруктураОтбораПустойGTIN, ИменаКолонокТовары);
	ТаблицаПроверкиПолная.Свернуть(ИменаКолонокТовары);
	ТаблицаПроверкиПолная.Индексы.Добавить(ИменаКолонокТовары);
	
	ТаблицаПроверкиПолная.Колонки.Добавить("ЕстьGTIN",    Новый ОписаниеТипов("Булево"));
	ТаблицаПроверкиПолная.Колонки.Добавить("ВидУпаковки", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыУпаковокИС"));
	ТаблицаПроверкиПолная.Колонки.Добавить("ДоступныеGTIN");
	ТаблицаПроверкиПолная.Индексы.Добавить("Номенклатура,Характеристика,ВидУпаковки");
	
	Для Каждого СтрокаТаблицы Из ТаблицаПроверкиПолная Цикл
		Если ПроверятьШаблон Тогда
			СтрокаТаблицы.ВидУпаковки = ИнтеграцияИСМПСлужебныйКлиентСервер.ВидУпаковкиПоШаблонуКодаМаркировки(
				СтрокаТаблицы.Шаблон);
		Иначе
			СтрокаТаблицы.ВидУпаковки = Перечисления.ВидыУпаковокИС.Потребительская;
		КонецЕсли;
	КонецЦикла;
	
	ВозвращаемоеЗначение.ТаблицаПроверки = ТаблицаПроверкиПолная;
	
	Если ПроверяемыеGTIN.Количество() > 0 Тогда
		
		Организация = Неопределено;
		Если ЗначениеЗаполнено(Объект.Организация) Тогда
			Организация = Объект.Организация;
		КонецЕсли;
		
		РезультатПроверки = ИнтерфейсИСМПОбщегоНазначения.ДанныеПродукцииПоШтрихкодуEAN(ПроверяемыеGTIN, Объект.ВидПродукции, Организация);
		
		Если РезультатПроверки.ТребуетсяОбновлениеКлючаСессии Тогда
			ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
			Возврат ВозвращаемоеЗначение;
		ИначеЕсли РезультатПроверки.ДанныеПродукцииПоШтрихкодуEAN = Неопределено Тогда
			ОбщегоНазначения.СообщитьПользователю(РезультатПроверки.ТекстОшибки);
			Возврат ВозвращаемоеЗначение;
		КонецЕсли;
		
		Если Объект.ВидПродукции = Перечисления.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС Тогда
			ВидПродукцииИСМП = Перечисления.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС;
		Иначе
			ВидПродукцииИСМП = Объект.ВидПродукции;
		КонецЕсли;
		
		ДанныеПоШтрихкодам = ШтрихкодированиеОбщегоНазначенияИС.ДанныеПоШтрихкодамEAN(ПроверяемыеGTIN);
		РезультатПроверки  = РезультатПроверки.ДанныеПродукцииПоШтрихкодуEAN;
		
		Если ПроверятьИНН Тогда
			ИНН = РаботаСКонтрагентамиИСВызовСервера.ИННКПППоОрганизацииКонтрагенту(Объект.Организация).ИНН;
		КонецЕсли;
		
		Для Каждого GTIN Из ПроверяемыеGTIN Цикл
			
			ЗначениеСтроки   = СоответствиеGTIN.Получить(GTIN);
			ДанныеРезультата = РезультатПроверки.Получить(GTIN);
			Если ДанныеРезультата = Неопределено
				Или (ПроверятьИНН И ДанныеРезультата.ИННПроизводителя <> ИНН)
				Или ДанныеРезультата.ВидПродукции <> ВидПродукцииИСМП
				Или (РазборКодаМаркировкиИССлужебныйКлиентСервер.ЭтоВнутреннийШтрихкод(GTIN)
					И Не РазборКодаМаркировкиИССлужебныйКлиентСервер.ЭтоШтрихкодВводаОстатков(GTIN)) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТипЗнч(ДанныеРезультата.ТипУпаковки) = Тип("ПеречислениеСсылка.ВидыУпаковокИС") Тогда
				ВидУпаковки = ДанныеРезультата.ТипУпаковки;
			Иначе
				ВидУпаковки = Перечисления.ВидыУпаковокИС.Потребительская;
			КонецЕсли;
			
			Если ОграничениеПоВидуУпаковки <> Неопределено
				И ОграничениеПоВидуУпаковки.Найти(ВидУпаковки) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ПоискСтрокиДанных = ДанныеПоШтрихкодам.Найти(GTIN, "ШтрихкодEAN");
			Если ПоискСтрокиДанных <> Неопределено
				И ЗначениеЗаполнено(ПоискСтрокиДанных.ВидУпаковкиИС) Тогда
				ВидУпаковки = ПоискСтрокиДанных.ВидУпаковкиИС;
			КонецЕсли;
			
			СтруктураПоиска = Новый Структура("Номенклатура,Характеристика,ВидУпаковки",
				ЗначениеСтроки.Номенклатура,
				ЗначениеСтроки.Характеристика,
				?(ЗначениеЗаполнено(ВидУпаковки), ВидУпаковки, Перечисления.ВидыУпаковокИС.Потребительская));
			
			СтрокиТаблицы = ТаблицаПроверкиПолная.НайтиСтроки(СтруктураПоиска);
			
			Для Каждого СтрокаТаблицы Из СтрокиТаблицы Цикл
				
				Если Не СтрокаТаблицы.ЕстьGTIN Тогда
					СтрокаТаблицы.ЕстьGTIN = Истина;
					СтрокаТаблицы.ДоступныеGTIN = Новый Массив;
				КонецЕсли;
				СтрокаТаблицы.ДоступныеGTIN.Добавить(ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.GTINПоШтрихкодуEAN(GTIN));
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

#КонецЕсли
