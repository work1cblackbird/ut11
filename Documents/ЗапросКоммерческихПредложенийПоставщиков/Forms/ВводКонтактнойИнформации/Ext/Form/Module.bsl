
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТолькоПросмотр = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ТолькоПросмотр", Ложь);
	Элементы.Телефон.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.Email.ТолькоПросмотр = ТолькоПросмотр;
	
	Менеджер = Параметры.Менеджер;
	Параметры.Свойство("Email", Email);
	Параметры.Свойство("Телефон", Телефон);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПроверитьЗаполнениеАдресаЭлектроннойПочты();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура EmailПриИзменении(Элемент)
	ПроверитьЗаполнениеАдресаЭлектроннойПочты();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Закрыть(КонтактнаяИнформация());
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция КонтактнаяИнформация()
	КонтактнаяИнформация = Новый Структура;
	КонтактнаяИнформация.Вставить("КонтактноеЛицо", "");
	КонтактнаяИнформация.Вставить("Email", "");
	КонтактнаяИнформация.Вставить("Телефон", "");
	ЗаполнитьЗначенияСвойств(КонтактнаяИнформация, ЭтотОбъект);
	Возврат КонтактнаяИнформация;
КонецФункции

&НаКлиенте
Процедура ПроверитьЗаполнениеАдресаЭлектроннойПочты()
	ТекстПодсказки = "";
	Если ПустаяСтрока(Email) Тогда
		ТекстПодсказки = НСтр("ru = 'Введите адрес электронной почты'");
	ИначеЕсли Не ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(Email) Тогда
		ТекстПодсказки = НСтр("ru = 'Адрес электронной почты введен неверно'");
	КонецЕсли;
	Элементы.Email.РасширеннаяПодсказка.Заголовок = ТекстПодсказки;
КонецПроцедуры

#КонецОбласти