#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ИсправлениеДокументов.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ОбщегоНазначенияУТ.ЗаполнитьИдентификаторыДокумента(ЭтотОбъект, "Кассы,КассыККМ,БанковскиеСчета");
	
	ЗаполнитьВалютуДокумента();
	
	ВводОстатковЛокализация.ВводОстатковДенежныхСредствПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	ВводОстатковЛокализация.ВводОстатковДенежныхСредствОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	ВводОстатковЛокализация.ВводОстатковДенежныхСредствОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Не ОтражатьВОперативномУчете И Не ОтражатьВБУиНУ И Не ОтражатьВУУ Тогда
		ТекстСообщения = НСтр("ru='Операция должна отражаться в одном из учетов'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , 
			"Объект.ОтражатьВОперативномУчете", , Отказ);
	КонецЕсли;
	
	Если ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ВводОстатковВКассах Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Кассы");
	Иначе
		Если ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоКасс") Тогда
			МассивНепроверяемыхРеквизитов.Добавить("Валюта");
		КонецЕсли;
	КонецЕсли;
	
	Если ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ВводОстатковВАвтономныхКассахККМКОформлениюОтчетовОРозничныхПродажах
		И ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ВводОстатковВАвтономныхКассахККМПоРозничнойВыручке Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КассыККМ");
	Иначе
		Если ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоКассККМ") Тогда
			МассивНепроверяемыхРеквизитов.Добавить("Валюта");
		КонецЕсли;
	КонецЕсли;
	
	Если ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ВводОстатковНаБанковскихСчетах Тогда
		МассивНепроверяемыхРеквизитов.Добавить("БанковскиеСчета");
	Иначе
		Если ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоРасчетныхСчетов") Тогда
			МассивНепроверяемыхРеквизитов.Добавить("Валюта");
		КонецЕсли;
	КонецЕсли;

	
	МассивНепроверяемыхРеквизитов.Добавить("Валюта");
	
	МассивНепроверяемыхРеквизитов.Добавить("Валюта");
	
	ИсправлениеДокументов.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	ВводОстатковЛокализация.ВводОстатковДенежныхСредствОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Автор = Пользователи.ТекущийПользователь();
	Ответственный = Пользователи.ТекущийПользователь();
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка." + Метаданные().Имя) Тогда
		
		ИсправлениеДокументов.ЗаполнитьИсправление(ЭтотОбъект, ДанныеЗаполнения);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		 
		Документы.ВводОстатковДенежныхСредств.ИнициализироватьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ХозяйственнаяОперация) Тогда
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковНаБанковскихСчетах;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Организация)
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		
		Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию();
		Валюта = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
		
	КонецЕсли;
	
	ВводОстатковЛокализация.ВводОстатковДенежныхСредствОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	ВводОстатковЛокализация.ВводОстатковДенежныхСредствПриЗаписи(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИсправлениеДокументов.ПриКопировании(ЭтотОбъект, ОбъектКопирования);
	
	Автор = Пользователи.ТекущийПользователь();
	Ответственный = Пользователи.ТекущийПользователь();
	
	ОбщегоНазначенияУТ.ОчиститьИдентификаторыДокумента(ЭтотОбъект, "Кассы,КассыККМ,БанковскиеСчета");

	ВводОстатковЛокализация.ВводОстатковДенежныхСредствПриКопировании(ЭтотОбъект, ОбъектКопирования);
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат
	КонецЕсли;
	
	ВводОстатковЛокализация.ВводОстатковДенежныхСредствПередУдалением(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьВалютуДокумента()

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДанных.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ИсходныеДанные
		|ИЗ
		|	&ТаблицаДанных КАК ТаблицаДанных
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИсходныеДанные.Ссылка.ВалютаДенежныхСредств КАК Валюта
		|ИЗ
		|	ИсходныеДанные КАК ИсходныеДанные";
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковВКассах Тогда
		
		ТаблицаКасс = Кассы.Выгрузить(, "Касса");
		ТаблицаКасс.Колонки.Касса.Имя = "Ссылка";
		
		Запрос.УстановитьПараметр("ТаблицаДанных", ТаблицаКасс);
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковНаБанковскихСчетах Тогда
		
		ТаблицаСчетов = БанковскиеСчета.Выгрузить(, "БанковскийСчет");
		ТаблицаСчетов.Колонки.БанковскийСчет.Имя = "Ссылка";
		
		Запрос.УстановитьПараметр("ТаблицаДанных", ТаблицаСчетов);
		
	Иначе
		
		ТаблицаКассККМ = КассыККМ.Выгрузить(, "КассаККМ");
		ТаблицаКассККМ.Колонки.КассаККМ.Имя = "Ссылка";
		
		Запрос.УстановитьПараметр("ТаблицаДанных", ТаблицаКассККМ);
		
	КонецЕсли;
	
	ТаблицаВалют = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаВалют.Количество() = 1 Тогда
		Валюта = ТаблицаВалют[0].Валюта;
	Иначе
		Валюта = Справочники.Валюты.ПустаяСсылка();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
