#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	Операция = Перечисления.ОперацииКорректировкиРегистров.РучнаяКорректировка;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Операция <> Перечисления.ОперацииКорректировкиРегистров.РучнаяКорректировка И НЕ Пользователи.РолиДоступны("ПолныеПрава") Тогда
		ТекстОшибки = НСтр("ru='Текущему пользователю запрещено изменять корректировки регистров, сформированные автоматически.'");
		
		ОбщегоНазначения.СообщитьПользователю(
			ТекстОшибки,
			ЭтотОбъект,,,
			Отказ);
	КонецЕсли;
	
	Если НЕ ЭтоНовый() И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПометкаУдаления") <> ПометкаУдаления Тогда
		// Изменился признак ПометкаУдаления у документа, надо синхронизировать свойство Активность у его движений.
		УстановитьАктивностьДвижений(НЕ ПометкаУдаления);
	ИначеЕсли ПометкаУдаления Тогда
		// У помеченного на удаление документа не должно быть движений со свойством Активность = Истина.
		УстановитьАктивностьДвижений(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьАктивностьДвижений(ФлагАктивности)
	
	// При записи из формы документа его движения уже прочитаны.
	НеобходимоПрочитатьДвижения = НЕ ДополнительныеСвойства.Свойство("ЗаписьДокументаИзФормы");
	
	Для Каждого Движение Из Движения Цикл
		
		Если НеобходимоПрочитатьДвижения Тогда
			Движение.Прочитать();
		КонецЕсли;
		
		Движение.УстановитьАктивность(ФлагАктивности);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
