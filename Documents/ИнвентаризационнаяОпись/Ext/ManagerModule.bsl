#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("РеестрДокументов");
	
	ИнвентаризационнаяОписьЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов - таблиц значений - данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.ИнвентаризационнаяОпись") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
	
	ИнвентаризационнаяОписьЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Инвентаризационная опись".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//	ТаблицаЗначений, Неопределено - сформированные команды для вывода в подменю.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.ИнвентаризационнаяОпись) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ИнвентаризационнаяОпись.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ИнвентаризационнаяОпись);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ИнвентаризационнаяОписьЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);
	
КонецПроцедуры

// Возвращает список организаций, по которым печатают инвентаризационные описи.
//
// Параметры:
//	МассивДокументов - Массив - массви сслылок на инвентаризационные описи.
//
// Возвращаемое значение:
//	СписокЗначений - список организаций.
//
Функция СписокОрганизацийИзДокументов(МассивДокументов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ИнвентаризационнаяОпись.Организация
	|ИЗ
	|	Документ.ИнвентаризационнаяОпись КАК ИнвентаризационнаяОпись
	|ГДЕ
	|	ИнвентаризационнаяОпись.Ссылка В (&МассивДокументов)";
	
	МассивОрганизаций = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Организация");
	
	СписокОрганизаций = Новый СписокЗначений;
	СписокОрганизаций.ЗагрузитьЗначения(МассивОрганизаций);
	
	Возврат СписокОрганизаций;
	
КонецФункции

// Функция получает данные документа со стоимостными показателями.
//
// Параметры:
//   ДокументСсылка - ДокументСсылка.ИнвентаризационнаяОпись - документ, данные которого надо получить.
//
// Возвращаемое значение:
//   Структура:
//      РезультатПоШапке - Результат запроса по шапке документа,
//                         в случае ошибки Структура: ДатаНачала, ДатаОкончания из документа.
//      РезультатПоТабличнойЧасти - Результат запроса по табличной части документа.
//      РезультатКурсВалюты - Результат запроса по курсам валют.
//      ОписаниеОшибки - Строка, Неопределено - если нет ошибок - Неопределено, иначе строковый идентификатор ошибки.
//      ПоПредварительномуРасчету - Булево - флаг получения данных из предварительного расчета.
//
Функция ДанныеИнвентаризации(ДокументСсылка) Экспорт
	
	ДанныеДокумента = ПолучитьДанныеДокумента(Неопределено, ДокументСсылка);
	
	Если ДанныеДокумента.ОписаниеОшибки = Неопределено Тогда
		Возврат Новый Структура(
			"РезультатПоШапке, РезультатПоТабличнойЧасти, РезультатКурсВалюты",
			ДанныеДокумента.РезультатПоШапке,
			ДанныеДокумента.РезультатПоТабличнойЧасти,
			ДанныеДокумента.РезультатКурсВалюты);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Склад)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область Проведение

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                КАК Ссылка,
	|	ДанныеДокумента.Дата                  КАК Период,
	|	ДанныеДокумента.Номер                 КАК Номер,
	|	ДанныеДокумента.Организация           КАК Организация,
	|	ДанныеДокумента.Склад                 КАК Склад,
	|	ДанныеДокумента.Ответственный         КАК Ответственный,
	|	ДанныеДокумента.Подразделение         КАК Подразделение,
	|	ДанныеДокумента.Комментарий           КАК Комментарий,
	|	ДанныеДокумента.ПометкаУдаления       КАК ПометкаУдаления,
	|	ДанныеДокумента.Проведен              КАК Проведен
	|ИЗ
	|	Документ.ИнвентаризационнаяОпись КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Номер",                       Реквизиты.Номер);
	Запрос.УстановитьПараметр("Период",                      Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация",                 Реквизиты.Организация);
	Запрос.УстановитьПараметр("Склад",                       Реквизиты.Склад);
	Запрос.УстановитьПараметр("Подразделение",               Реквизиты.Подразделение);
	Запрос.УстановитьПараметр("Ответственный",               Реквизиты.Ответственный);
	Запрос.УстановитьПараметр("Комментарий",                 Реквизиты.Комментарий);
	Запрос.УстановитьПараметр("ПометкаУдаления",             Реквизиты.ПометкаУдаления);
	Запрос.УстановитьПараметр("Проведен",                    Реквизиты.Проведен);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация",       Перечисления.ХозяйственныеОперации.ИнвентаризационнаяОпись);
	
	Запрос.УстановитьПараметр("ИдентификаторМетаданных",     ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		"Документ.ИнвентаризационнаяОпись"));
	Запрос.УстановитьПараметр("НомерНаПечать",               ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(
		Реквизиты.Номер));
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                            КАК Партнер,
	|	НЕОПРЕДЕЛЕНО                            КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО                            КАК НаправлениеДеятельности,
	|	&Склад                                  КАК МестоХранения,
	|	&Подразделение                          КАК Подразделение,
	|	&Ответственный                          КАК Ответственный,
	|	ВЫРАЗИТЬ(&Комментарий КАК СТРОКА(100))  КАК Комментарий,
	|	НЕОПРЕДЕЛЕНО                            КАК Валюта,
	|	0                                       КАК Сумма,
	|	НЕОПРЕДЕЛЕНО                            КАК Статус,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	НЕОПРЕДЕЛЕНО                            КАК Дополнительно,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	ЛОЖЬ                                    КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО                            КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО                            КАК ИсправляемыйДокумент,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	&Период                                 КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО                            КАК Приоритет
	|ИЗ
	|	Документ.ИнвентаризационнаяОпись КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента      = "Документ.ИнвентаризационнаяОпись";
	СинонимТаблицыДокумента = "";
	ВЗапросеЕстьИсточник    = Истина;
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("ИдентификаторМетаданных",
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяДокумента));
	ЗначенияПараметров.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ИнвентаризационнаяОпись);
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", 	ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", 		ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ВЗапросеЕстьИсточник,
			ПереопределениеРасчетаПараметров);
		
	Иначе
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ПереопределениеРасчетаПараметров);
		
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	ИнвентаризационнаяОписьЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

// Функция получает данные документа.
// Используется при получении данных для печатной формы ИНВ3 и для выгрузки данных при обмене.
//
// Возвращаемое значение:
// 	Структура - Описание:
// 	* РезультатПоШапке	- РезультатЗапроса - Результат запроса по шапке документа
// 						- Структура - возвращается в случае ошибки, содержит данные из документа:
// 							** ДатаНачала - Дата
// 							** ДатаОкончания - Дата
// 	* РезультатПоТабличнойЧасти - РезультатЗапроса
// 	* РезультатКурсВалюты - РезультатЗапроса
// 	* ОписаниеОшибки - Строка, Неопределено - если нет ошибок - Неопределено, иначе - строковый идентификатор ошибки.
// 	* ПоПредварительномуРасчету - Булево
// 
Функция ПолучитьДанныеДокумента(ПараметрыПечати, ДокументОснование) Экспорт
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("РезультатПоШапке");
	СтруктураВозврата.Вставить("РезультатПоТабличнойЧасти");
	СтруктураВозврата.Вставить("РезультатКурсВалюты");
	СтруктураВозврата.Вставить("ОписаниеОшибки");
	СтруктураВозврата.Вставить("ПоПредварительномуРасчету");
	
	СтруктураВозврата.ОписаниеОшибки = Неопределено;
	СтруктураВозврата.ПоПредварительномуРасчету = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
		
	ЗапросПредварительныхДанных = Новый Запрос;
	ЗапросПредварительныхДанных.УстановитьПараметр("ДокументОснование", ДокументОснование);
	ЗапросПредварительныхДанных.Текст =
	"ВЫБРАТЬ
	|	ИнвентаризационнаяОпись.Дата КАК Дата,
	|	ИнвентаризационнаяОпись.ДатаНачала,
	|	ИнвентаризационнаяОпись.ДатаОкончания,
	|	ИнвентаризационнаяОпись.ВидЦены,
	|	ИнвентаризационнаяОпись.Склад,
	|	ИнвентаризационнаяОпись.ИсточникИнформацииОЦенахДляПечати,
	|	ИнвентаризационнаяОпись.Организация,
	|	РасчетСебестоимостиТоваровОрганизации.Ссылка.ПредварительныйРасчет КАК ПредварительныйРасчет
	|ИЗ
	|	Документ.ИнвентаризационнаяОпись КАК ИнвентаризационнаяОпись
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК РасчетСебестоимостиТоваровОрганизации
	|		ПО (РасчетСебестоимостиТоваровОрганизации.Организация = ИнвентаризационнаяОпись.Организация)
	|			И (РасчетСебестоимостиТоваровОрганизации.Ссылка.Проведен)
	|			И (РасчетСебестоимостиТоваровОрганизации.Ссылка.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ИнвентаризационнаяОпись.ДатаНачала, МЕСЯЦ) И КОНЕЦПЕРИОДА(ИнвентаризационнаяОпись.ДатаОкончания, МЕСЯЦ))
	|ГДЕ
	|	ИнвентаризационнаяОпись.Ссылка = &ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	РасчетСебестоимостиТоваровОрганизации.Ссылка.Дата УБЫВ";
	
	ПредварительныеДанныеРезультат = ЗапросПредварительныхДанных.Выполнить().Выбрать();
	Если Не ПредварительныеДанныеРезультат.Следующий() Тогда
		
		СтруктураВозврата.ОписаниеОшибки = "НетИнвентаризацийВПериоде";
		Возврат СтруктураВозврата;
		
	КонецЕсли;
	
	ПредварительныйРасчет				= ПредварительныеДанныеРезультат.ПредварительныйРасчет;
	ИсточникИнформацииОЦенахДляПечати	= ПредварительныеДанныеРезультат.ИсточникИнформацииОЦенахДляПечати;
	ДатаНачала							= ПредварительныеДанныеРезультат.ДатаНачала;
	ДатаОкончания						= КонецДня(ПредварительныеДанныеРезультат.ДатаОкончания);
	КонецМесяцаПериода					= Новый Граница(КонецМесяца(ДатаОкончания), ВидГраницы.Включая);
	Склад								= ПредварительныеДанныеРезультат.Склад;	
	Организация							= ПредварительныеДанныеРезультат.Организация;
	ВидЦены								= ПредварительныеДанныеРезультат.ВидЦены;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала",			ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания",		ДатаОкончания);
	Запрос.УстановитьПараметр("Склад",				Склад);
	Запрос.УстановитьПараметр("ДатаОстатков",		ДатаОкончания + 1);
	Запрос.УстановитьПараметр("КонецМесяцаПериода",	КонецМесяцаПериода);
	Запрос.УстановитьПараметр("ДокументОснование",	ДокументОснование);
	Запрос.УстановитьПараметр("Организация",		Организация);
	Запрос.УстановитьПараметр("ВидЦены",			ВидЦены);
	
	// Ответственные лица в печатной форме
	СтруктураОтветственных = ОтветственныеЛицаСервер.ПолучитьОтветственныеЛицаОрганизации(Организация, КонецДня(ДатаОкончания));
	Запрос.УстановитьПараметр("Руководитель",						СтруктураОтветственных.Руководитель);
	Запрос.УстановитьПараметр("ДолжностьРуководителя",				СтруктураОтветственных.РуководительДолжность);
	Запрос.УстановитьПараметр("ГлавныйБухгалтер",					СтруктураОтветственных.ГлавныйБухгалтер);
	Запрос.УстановитьПараметр("СебестоимостьТоваровПоНазначениям",	РасчетСебестоимостиПовтИсп.СебестоимостьТоваровПоНазначениям(ДатаОкончания));
	
	Если ИсточникИнформацииОЦенахДляПечати = Перечисления.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости Тогда
		
		Если ПредварительныйРасчет = Null Тогда
			
			СтруктураВозврата.ОписаниеОшибки = "НетЦенПоСебестоимости";
			СтруктураВозврата.РезультатПоШапке = Новый Структура(
				"ДатаНачала, ДатаОкончания",
				ДатаНачала, ДатаОкончания
			);
			Возврат СтруктураВозврата;
			
		КонецЕсли;
		
		Если ПредварительныйРасчет Тогда
			
			СтруктураВозврата.ПоПредварительномуРасчету = Истина;
			
			ТекстЗапроса =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ВложенныйЗапрос.Организация КАК Организация
			|ПОМЕСТИТЬ ТаблицаНоменклатуры
			|ИЗ
			|	(ВЫБРАТЬ
			|		Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
			|		&Организация КАК Организация
			|	ИЗ
			|		Документ.ПересчетТоваров.Товары КАК ПересчетТоваровТовары
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
			|			ПО ПересчетТоваровТовары.Номенклатура = Аналитика.Номенклатура
			|				И ПересчетТоваровТовары.Характеристика = Аналитика.Характеристика
			|				И ПересчетТоваровТовары.Назначение = Аналитика.Назначение
			|				И (ПересчетТоваровТовары.Серия = Аналитика.Серия
			|					ИЛИ НЕ ПересчетТоваровТовары.СтатусУказанияСерий = 14)
			|				И ПересчетТоваровТовары.Ссылка.Склад = Аналитика.МестоХранения
			|	ГДЕ
			|		ПересчетТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|		И ПересчетТоваровТовары.Ссылка.Склад = &Склад
			|		И ПересчетТоваровТовары.Ссылка.Проведен
			|		И ПересчетТоваровТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПересчетовТоваров.Выполнено)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		СписаниеНедостачТоваровТовары.АналитикаУчетаНоменклатуры,
			|		&Организация
			|	ИЗ
			|		Документ.СписаниеНедостачТоваров.Товары КАК СписаниеНедостачТоваровТовары
			|	ГДЕ
			|		СписаниеНедостачТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|		И СписаниеНедостачТоваровТовары.Ссылка.Склад = &Склад
			|		И СписаниеНедостачТоваровТовары.Ссылка.Проведен
			|		И СписаниеНедостачТоваровТовары.Ссылка.Организация = &Организация
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ОприходованиеИзлишковТоваровТовары.АналитикаУчетаНоменклатуры,
			|		&Организация
			|	ИЗ
			|		Документ.ОприходованиеИзлишковТоваров.Товары КАК ОприходованиеИзлишковТоваровТовары
			|	ГДЕ
			|		ОприходованиеИзлишковТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|		И ОприходованиеИзлишковТоваровТовары.Ссылка.Склад = &Склад
			|		И ОприходованиеИзлишковТоваровТовары.Ссылка.Проведен
			|		И ОприходованиеИзлишковТоваровТовары.Ссылка.Организация = &Организация
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ПересортицаТоваровТовары.АналитикаУчетаНоменклатуры,
			|		&Организация
			|	ИЗ
			|		Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
			|	ГДЕ
			|		ПересортицаТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|		И ПересортицаТоваровТовары.Ссылка.Склад = &Склад
			|		И ПересортицаТоваровТовары.Ссылка.Проведен
			|		И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ПересортицаТоваровТовары.АналитикаУчетаНоменклатурыОприходование,
			|		&Организация
			|	ИЗ
			|		Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
			|	ГДЕ
			|		ПересортицаТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|		И ПересортицаТоваровТовары.Ссылка.Склад = &Склад
			|		И ПересортицаТоваровТовары.Ссылка.Проведен
			|		И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ПорчаТоваровТовары.АналитикаУчетаНоменклатуры,
			|		&Организация
			|	ИЗ
			|		Документ.ПорчаТоваров.Товары КАК ПорчаТоваровТовары
			|	ГДЕ
			|		ПорчаТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|		И ПорчаТоваровТовары.Ссылка.Склад = &Склад
			|		И ПорчаТоваровТовары.Ссылка.Проведен
			|		И ПорчаТоваровТовары.Ссылка.Организация = &Организация
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Аналитика.КлючАналитики,
			|		&Организация
			|	ИЗ
			|		Документ.ПорчаТоваров.Товары КАК ПорчаТоваровТоварыОприходованные
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
			|			ПО ПорчаТоваровТоварыОприходованные.НоменклатураОприходование = Аналитика.Номенклатура
			|				И ПорчаТоваровТоварыОприходованные.ХарактеристикаОприходование = Аналитика.Характеристика
			|				И ПорчаТоваровТоварыОприходованные.Назначение = Аналитика.Назначение
			|				И (ПорчаТоваровТоварыОприходованные.Серия = Аналитика.Серия
			|					ИЛИ НЕ ПорчаТоваровТоварыОприходованные.СтатусУказанияСерий = 14)
			|				И ПорчаТоваровТоварыОприходованные.Ссылка.Склад = Аналитика.МестоХранения
			|	ГДЕ
			|		ПорчаТоваровТоварыОприходованные.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|		И ПорчаТоваровТоварыОприходованные.Ссылка.Склад = &Склад
			|		И ПорчаТоваровТоварыОприходованные.Ссылка.Проведен
			|		И ПорчаТоваровТоварыОприходованные.Ссылка.Организация = &Организация) КАК ВложенныйЗапрос
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ТоварыОрганизацийОстатки.ВидЗапасов,
			|	ТоварыОрганизацийОстатки.КоличествоОстаток
			|ПОМЕСТИТЬ ТоварыОрганизацийОстатки
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций.Остатки(
			|			&ДатаОстатков,
			|			АналитикаУчетаНоменклатуры В
			|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|						ТаблицаНоменклатуры.АналитикаУчетаНоменклатуры
			|					ИЗ
			|						ТаблицаНоменклатуры)
			|				И Организация = &Организация
			|				И ВидЗапасов.ТипЗапасов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар))) КАК ТоварыОрганизацийОстатки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасов,
			|	-ТоварыОрганизаций.Количество КАК КоличествоОтклонение,
			|	0 КАК КоличествоФактическое
			|ПОМЕСТИТЬ ТоварыИнвентаризации
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
			|ГДЕ
			|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.СписаниеНедостачТоваров
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.СписаниеНедостачТоваров).Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.СписаниеНедостачТоваров).Склад = &Склад
			|	И ТоварыОрганизаций.Организация = &Организация
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры,
			|	ТоварыОрганизаций.ВидЗапасов,
			|	ТоварыОрганизаций.Количество,
			|	0
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
			|ГДЕ
			|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.ОприходованиеИзлишковТоваров
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ОприходованиеИзлишковТоваров).Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ОприходованиеИзлишковТоваров).Склад = &Склад
			|	И ТоварыОрганизаций.Организация = &Организация
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры,
			|	ТоварыОрганизаций.ВидЗапасов,
			|	ВЫБОР
			|		КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|			ТОГДА ТоварыОрганизаций.Количество
			|		ИНАЧЕ -ТоварыОрганизаций.Количество
			|	КОНЕЦ,
			|	0
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
			|ГДЕ
			|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.ПересортицаТоваров
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ПересортицаТоваров).Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ПересортицаТоваров).Склад = &Склад
			|	И ТоварыОрганизаций.Организация = &Организация
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры,
			|	ТоварыОрганизаций.ВидЗапасов,
			|	ВЫБОР
			|		КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|			ТОГДА ТоварыОрганизаций.Количество
			|		ИНАЧЕ -ТоварыОрганизаций.Количество
			|	КОНЕЦ,
			|	0
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
			|ГДЕ
			|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.ПорчаТоваров
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ПорчаТоваров).Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ПорчаТоваров).Склад = &Склад
			|	И ТоварыОрганизаций.Организация = &Организация
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТаблицаНоменклатуры.АналитикаУчетаНоменклатуры,
			|	ТоварыОрганизацийОстатки.ВидЗапасов,
			|	0,
			|	ТоварыОрганизацийОстатки.КоличествоОстаток
			|ИЗ
			|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТоварыОрганизацийОстатки КАК ТоварыОрганизацийОстатки
			|		ПО ТаблицаНоменклатуры.АналитикаУчетаНоменклатуры = ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТоварыИнвентаризации.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ТоварыИнвентаризации.ВидЗапасов КАК ВидЗапасов,
			|	&Организация КАК Организация,
			|	СУММА(ТоварыИнвентаризации.КоличествоФактическое) КАК КоличествоФактическое,
			|	СУММА(ТоварыИнвентаризации.КоличествоФактическое - ТоварыИнвентаризации.КоличествоОтклонение) КАК КоличествоПоУчету
			|ПОМЕСТИТЬ ТаблицаИнвентаризации
			|ИЗ
			|	ТоварыИнвентаризации КАК ТоварыИнвентаризации
			|
			|СГРУППИРОВАТЬ ПО
			|	ТоварыИнвентаризации.АналитикаУчетаНоменклатуры,
			|	ТоварыИнвентаризации.ВидЗапасов
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	&Руководитель КАК Руководитель,
			|	&ДолжностьРуководителя КАК ДолжностьРуководителя,
			|	&ГлавныйБухгалтер КАК ГлавныйБухгалтер,
			|	&Организация КАК Организация,
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).КодПоОКПО КАК ОрганизацияПоОКПО,
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).Префикс КАК Префикс,
			|	ИнвентаризационнаяОпись.Склад.ТекущийОтветственный КАК Кладовщик,
			|	ИнвентаризационнаяОпись.Склад.ТекущаяДолжностьОтветственного КАК ДолжностьКладовщика,
			|	ИнвентаризационнаяОпись.Склад.Наименование КАК ПодразделениеПредставление,
			|	&ДатаНачала КАК ДатаНачала,
			|	&ДатаОкончания КАК ДатаОкончания,
			|	ИнвентаризационнаяОпись.Склад КАК Склад,
			|	ИнвентаризационнаяОпись.Дата КАК ОснованиеДата,
			|	ИнвентаризационнаяОпись.Номер КАК ОснованиеНомер,
			|	ИнвентаризационнаяОпись.Номер КАК НомерДокумента,
			|	ИнвентаризационнаяОпись.Дата КАК ДатаДокумента,
			|	ИнвентаризационнаяОпись.Ссылка КАК Ссылка,
			|	&ДатаОкончания КАК ДатаСнятияОстатков
			|ИЗ
			|	Документ.ИнвентаризационнаяОпись КАК ИнвентаризационнаяОпись
			|ГДЕ
			|	ИнвентаризационнаяОпись.Ссылка = &ДокументОснование
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЦеныНоменклатуры.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ЦеныНоменклатуры.ВидЗапасов КАК ВидЗапасов,
			|	ЦеныНоменклатуры.Организация КАК Организация,
			|	(ЦеныНоменклатуры.СтоимостьРегл
			|		+ ЦеныНоменклатуры.ДопРасходыРегл
			|		+ ЦеныНоменклатуры.ТрудозатратыРегл
			|		+ ЦеныНоменклатуры.ПостатейныеПостоянныеРегл
			|		+ ЦеныНоменклатуры.ПостатейныеПеременныеРегл) КАК СтоимостьРегл
			|ПОМЕСТИТЬ ЦеныНоменклатуры
			|ИЗ
			|	РегистрСведений.СтоимостьТоваров.СрезПоследних(
			|			&ДатаОкончания,
			|			(АналитикаУчетаНоменклатуры, ВидЗапасов, РазделУчета, Организация) В
			|				(ВЫБРАТЬ
			|					Таблица.АналитикаУчетаНоменклатуры,
			|					Таблица.ВидЗапасов,
			|					ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
			|					Таблица.Организация
			|				ИЗ
			|					ТаблицаИнвентаризации КАК Таблица)) КАК ЦеныНоменклатуры
			|ГДЕ
			|	&СебестоимостьТоваровПоНазначениям
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ЦеныНоменклатуры.АналитикаУчетаНоменклатуры,
			|	ЦеныНоменклатуры.ВидЗапасов,
			|	ЦеныНоменклатуры.Организация,
			|	(ЦеныНоменклатуры.СтоимостьРегл
			|		+ ЦеныНоменклатуры.ДопРасходыРегл
			|		+ ЦеныНоменклатуры.ТрудозатратыРегл
			|		+ ЦеныНоменклатуры.ПостатейныеПостоянныеРегл
			|		+ ЦеныНоменклатуры.ПостатейныеПеременныеРегл)
			|ИЗ
			|	РегистрСведений.СтоимостьТоваров.СрезПоследних(
			|			&ДатаОкончания,
			|			(АналитикаУчетаНоменклатуры.Номенклатура, АналитикаУчетаНоменклатуры.Характеристика, АналитикаУчетаНоменклатуры.Серия, АналитикаУчетаНоменклатуры.МестоХранения, АналитикаУчетаНоменклатуры.СтатьяКалькуляции, ВидЗапасов, РазделУчета, Организация) В
			|				(ВЫБРАТЬ
			|					АналитикаУчетаНоменклатуры.Номенклатура, АналитикаУчетаНоменклатуры.Характеристика, АналитикаУчетаНоменклатуры.Серия, АналитикаУчетаНоменклатуры.МестоХранения, АналитикаУчетаНоменклатуры.СтатьяКалькуляции,
			|					Таблица.ВидЗапасов,
			|					ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
			|					Таблица.Организация
			|				ИЗ
			|					ТаблицаИнвентаризации КАК Таблица)) КАК ЦеныНоменклатуры
			|
			|ГДЕ
			|	НЕ &СебестоимостьТоваровПоНазначениям
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Серия КАК Серия,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Серия.Наименование КАК СерияНаименование,
			|	СУММА(ТаблицаИнвентаризации.КоличествоПоУчету) КАК КоличествоПоУчету,
			|	СУММА(ТаблицаИнвентаризации.КоличествоФактическое) КАК КоличествоФактическое,
			|	ЕСТЬNULL(ЦеныНоменклатуры.СтоимостьРегл, 0) КАК Цена,
			|	СУММА(ТаблицаИнвентаризации.КоличествоПоУчету * ЕСТЬNULL(ЦеныНоменклатуры.СтоимостьРегл, 0)) КАК СуммаПоУчету,
			|	СУММА(ТаблицаИнвентаризации.КоличествоФактическое * ЕСТЬNULL(ЦеныНоменклатуры.СтоимостьРегл, 0)) КАК СуммаФактическая,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.Код КАК ТоварКод,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
			|	ЛОЖЬ КАК ЭтоВозвратнаяТара
			|ИЗ
			|	ТаблицаИнвентаризации КАК ТаблицаИнвентаризации
			|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныНоменклатуры КАК ЦеныНоменклатуры
			|		ПО ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры = ЦеныНоменклатуры.АналитикаУчетаНоменклатуры
			|			И ТаблицаИнвентаризации.ВидЗапасов = ЦеныНоменклатуры.ВидЗапасов
			|			И ЦеныНоменклатуры.Организация = &Организация
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика.НаименованиеПолное,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Серия.Наименование,
			|	ЕСТЬNULL(ЦеныНоменклатуры.СтоимостьРегл, 0),
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Код,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Наименование,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.НаименованиеПолное,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.Код,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Серия
			|
			|УПОРЯДОЧИТЬ ПО
			|	ТоварНаименование,
			|	Номенклатура";
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Номенклатура.Код", "Номенклатура." + КолонкаКодов);
			Запрос.Текст = ТекстЗапроса;
			
			Результаты = Запрос.ВыполнитьПакет();
			РезультатПоШапке = Результаты[4];
			РезультатПоТабличнойЧасти = Результаты[6];
			РезультатКурсВалюты = Неопределено;
			
		Иначе
			
			// фактический расчет
			
			ТекстЗапроса =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ВложенныйЗапрос.Организация КАК Организация
			|ПОМЕСТИТЬ ТаблицаНоменклатуры
			|ИЗ
			|	(ВЫБРАТЬ
			|		Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
			|		&Организация КАК Организация
			|	ИЗ
			|		Документ.ПересчетТоваров.Товары КАК ПересчетТоваровТовары
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
			|			ПО ПересчетТоваровТовары.Номенклатура = Аналитика.Номенклатура
			|				И ПересчетТоваровТовары.Характеристика = Аналитика.Характеристика
			|				И ПересчетТоваровТовары.Назначение = Аналитика.Назначение
			|				И (ПересчетТоваровТовары.Серия = Аналитика.Серия
			|					ИЛИ НЕ ПересчетТоваровТовары.СтатусУказанияСерий = 14)
			|				И ПересчетТоваровТовары.Ссылка.Склад = Аналитика.МестоХранения
			|	ГДЕ
			|		ПересчетТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|		И ПересчетТоваровТовары.Ссылка.Склад = &Склад
			|		И ПересчетТоваровТовары.Ссылка.Проведен
			|		И ПересчетТоваровТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПересчетовТоваров.Выполнено)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		СписаниеНедостачТоваровТовары.АналитикаУчетаНоменклатуры,
			|		&Организация
			|	ИЗ
			|		Документ.СписаниеНедостачТоваров.Товары КАК СписаниеНедостачТоваровТовары
			|	ГДЕ
			|		СписаниеНедостачТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|		И СписаниеНедостачТоваровТовары.Ссылка.Склад = &Склад
			|		И СписаниеНедостачТоваровТовары.Ссылка.Проведен
			|		И СписаниеНедостачТоваровТовары.Ссылка.Организация = &Организация
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ОприходованиеИзлишковТоваровТовары.АналитикаУчетаНоменклатуры,
			|		&Организация
			|	ИЗ
			|		Документ.ОприходованиеИзлишковТоваров.Товары КАК ОприходованиеИзлишковТоваровТовары
			|	ГДЕ
			|		ОприходованиеИзлишковТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|		И ОприходованиеИзлишковТоваровТовары.Ссылка.Склад = &Склад
			|		И ОприходованиеИзлишковТоваровТовары.Ссылка.Проведен
			|		И ОприходованиеИзлишковТоваровТовары.Ссылка.Организация = &Организация
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ПересортицаТоваровТовары.АналитикаУчетаНоменклатуры,
			|		&Организация
			|	ИЗ
			|		Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
			|	ГДЕ
			|		ПересортицаТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|		И ПересортицаТоваровТовары.Ссылка.Склад = &Склад
			|		И ПересортицаТоваровТовары.Ссылка.Проведен
			|		И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ПересортицаТоваровТовары.АналитикаУчетаНоменклатурыОприходование,
			|		&Организация
			|	ИЗ
			|		Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
			|	ГДЕ
			|		ПересортицаТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|		И ПересортицаТоваровТовары.Ссылка.Склад = &Склад
			|		И ПересортицаТоваровТовары.Ссылка.Проведен
			|		И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ПорчаТоваровТовары.АналитикаУчетаНоменклатуры,
			|		&Организация
			|	ИЗ
			|		Документ.ПорчаТоваров.Товары КАК ПорчаТоваровТовары
			|	ГДЕ
			|		ПорчаТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|		И ПорчаТоваровТовары.Ссылка.Склад = &Склад
			|		И ПорчаТоваровТовары.Ссылка.Проведен
			|		И ПорчаТоваровТовары.Ссылка.Организация = &Организация
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Аналитика.КлючАналитики,
			|		&Организация
			|	ИЗ
			|		Документ.ПорчаТоваров.Товары КАК ПорчаТоваровТоварыОприходованные
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
			|			ПО ПорчаТоваровТоварыОприходованные.НоменклатураОприходование = Аналитика.Номенклатура
			|				И ПорчаТоваровТоварыОприходованные.ХарактеристикаОприходование = Аналитика.Характеристика
			|				И ПорчаТоваровТоварыОприходованные.Назначение = Аналитика.Назначение
			|				И (ПорчаТоваровТоварыОприходованные.Серия = Аналитика.Серия
			|					ИЛИ НЕ ПорчаТоваровТоварыОприходованные.СтатусУказанияСерий = 14)
			|				И ПорчаТоваровТоварыОприходованные.Ссылка.Склад = Аналитика.МестоХранения
			|	ГДЕ
			|		ПорчаТоваровТоварыОприходованные.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|		И ПорчаТоваровТоварыОприходованные.Ссылка.Склад = &Склад
			|		И ПорчаТоваровТоварыОприходованные.Ссылка.Проведен
			|		И ПорчаТоваровТоварыОприходованные.Ссылка.Организация = &Организация) КАК ВложенныйЗапрос
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ТоварыОрганизацийОстатки.ВидЗапасов,
			|	ТоварыОрганизацийОстатки.КоличествоОстаток
			|ПОМЕСТИТЬ ТоварыОрганизацийОстатки
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций.Остатки(
			|			&ДатаОстатков,
			|			АналитикаУчетаНоменклатуры В
			|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|						ТаблицаНоменклатуры.АналитикаУчетаНоменклатуры
			|					ИЗ
			|						ТаблицаНоменклатуры)
			|				И Организация = &Организация
			|				И ВидЗапасов.ТипЗапасов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар))) КАК ТоварыОрганизацийОстатки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасов,
			|	-ТоварыОрганизаций.Количество КАК КоличествоОтклонение,
			|	0 КАК КоличествоФактическое
			|ПОМЕСТИТЬ ТоварыИнвентаризации
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
			|ГДЕ
			|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.СписаниеНедостачТоваров
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.СписаниеНедостачТоваров).Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.СписаниеНедостачТоваров).Склад = &Склад
			|	И ТоварыОрганизаций.Организация = &Организация
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры,
			|	ТоварыОрганизаций.ВидЗапасов,
			|	ТоварыОрганизаций.Количество,
			|	0
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
			|ГДЕ
			|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.ОприходованиеИзлишковТоваров
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ОприходованиеИзлишковТоваров).Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ОприходованиеИзлишковТоваров).Склад = &Склад
			|	И ТоварыОрганизаций.Организация = &Организация
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры,
			|	ТоварыОрганизаций.ВидЗапасов,
			|	ВЫБОР
			|		КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|			ТОГДА ТоварыОрганизаций.Количество
			|		ИНАЧЕ -ТоварыОрганизаций.Количество
			|	КОНЕЦ,
			|	0
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
			|ГДЕ
			|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.ПересортицаТоваров
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ПересортицаТоваров).Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ПересортицаТоваров).Склад = &Склад
			|	И ТоварыОрганизаций.Организация = &Организация
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры,
			|	ТоварыОрганизаций.ВидЗапасов,
			|	ВЫБОР
			|		КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|			ТОГДА ТоварыОрганизаций.Количество
			|		ИНАЧЕ -ТоварыОрганизаций.Количество
			|	КОНЕЦ,
			|	0
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
			|ГДЕ
			|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.ПорчаТоваров
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ПорчаТоваров).Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ПорчаТоваров).Склад = &Склад
			|	И ТоварыОрганизаций.Организация = &Организация
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТаблицаНоменклатуры.АналитикаУчетаНоменклатуры,
			|	ТоварыОрганизацийОстатки.ВидЗапасов,
			|	0,
			|	ТоварыОрганизацийОстатки.КоличествоОстаток
			|ИЗ
			|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТоварыОрганизацийОстатки КАК ТоварыОрганизацийОстатки
			|		ПО ТаблицаНоменклатуры.АналитикаУчетаНоменклатуры = ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТоварыКОформлениюОтчетовКомитенту.ВидЗапасов КАК ВидЗапасов,
			|	ТоварыКОформлениюОтчетовКомитенту.КорВидЗапасов КАК НовыйВидЗапасов,
			|	ТоварыКОформлениюОтчетовКомитенту.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
			|ПОМЕСТИТЬ ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров
			|ИЗ
			|	ТоварыИнвентаризации КАК ТоварыИнвентаризации
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту КАК ТоварыКОформлениюОтчетовКомитенту
			|		ПО (ТоварыКОформлениюОтчетовКомитенту.ВидЗапасов = ТоварыИнвентаризации.ВидЗапасов)
			|			И (ТоварыКОформлениюОтчетовКомитенту.АналитикаУчетаНоменклатуры.Номенклатура = ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура)
			|			И (ТоварыКОформлениюОтчетовКомитенту.АналитикаУчетаНоменклатуры.Характеристика = ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика)
			|			И ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Серия = ТоварыКОформлениюОтчетовКомитенту.АналитикаУчетаНоменклатуры.Серия
			|ГДЕ
			|	ТоварыИнвентаризации.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТОвар)
			|	И ТоварыКОформлениюОтчетовКомитенту.КорВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.ВидЗапасов ЕСТЬ NULL 
			|			ТОГДА ТоварыИнвентаризации.АналитикаУчетаНоменклатуры
			|		ИНАЧЕ ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.АналитикаУчетаНоменклатуры
			|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
			|	ВЫБОР
			|		КОГДА ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.ВидЗапасов ЕСТЬ NULL 
			|			ТОГДА ТоварыИнвентаризации.ВидЗапасов
			|		ИНАЧЕ ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.НовыйВидЗапасов
			|	КОНЕЦ КАК ВидЗапасов,
			|	&Организация КАК Организация,
			|	СУММА(ТоварыИнвентаризации.КоличествоФактическое) КАК КоличествоФактическое,
			|	СУММА(ТоварыИнвентаризации.КоличествоФактическое - ТоварыИнвентаризации.КоличествоОтклонение) КАК КоличествоПоУчету
			|ПОМЕСТИТЬ ТаблицаИнвентаризации
			|ИЗ
			|	ТоварыИнвентаризации КАК ТоварыИнвентаризации
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров КАК ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров
			|		ПО (ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.ВидЗапасов = ТоварыИнвентаризации.ВидЗапасов)
			|			И (ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.АналитикаУчетаНоменклатуры.Номенклатура = ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура)
			|			И (ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.АналитикаУчетаНоменклатуры.Характеристика = ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика)
			|			И (ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.АналитикаУчетаНоменклатуры.Серия = ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Серия)
			|
			|СГРУППИРОВАТЬ ПО
			|	ВЫБОР
			|		КОГДА ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.ВидЗапасов ЕСТЬ NULL 
			|			ТОГДА ТоварыИнвентаризации.АналитикаУчетаНоменклатуры
			|		ИНАЧЕ ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.АналитикаУчетаНоменклатуры
			|	КОНЕЦ,
			|	ВЫБОР
			|		КОГДА ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.ВидЗапасов ЕСТЬ NULL 
			|			ТОГДА ТоварыИнвентаризации.ВидЗапасов
			|		ИНАЧЕ ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.НовыйВидЗапасов
			|	КОНЕЦ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СебестоимостьТоваровОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	СебестоимостьТоваровОстатки.ВидЗапасов КАК ВидЗапасов,
			|	СебестоимостьТоваровОстатки.Организация КАК Организация,
			|	ВЫБОР
			|		КОГДА СебестоимостьТоваровОстатки.КоличествоОстаток = 0
			|			ТОГДА 0
			|		ИНАЧЕ (СебестоимостьТоваровОстатки.СтоимостьРеглОстаток
			|				+ СебестоимостьТоваровОстатки.ДопРасходыРеглОстаток
			|				+ СебестоимостьТоваровОстатки.ТрудозатратыРеглОстаток
			|				+ СебестоимостьТоваровОстатки.ПостатейныеПостоянныеРеглОстаток
			|				+ СебестоимостьТоваровОстатки.ПостатейныеПеременныеРеглОстаток)
			|			/ СебестоимостьТоваровОстатки.КоличествоОстаток
			|	КОНЕЦ КАК Цена
			|ПОМЕСТИТЬ СебестоимостьТоваровОстатки
			|ИЗ
			|	РегистрНакопления.СебестоимостьТоваров.Остатки(
			|			&КонецМесяцаПериода,
			|			(АналитикаУчетаНоменклатуры, ВидЗапасов, Организация) В
			|				(ВЫБРАТЬ
			|					Таблица.АналитикаУчетаНоменклатуры,
			|					Таблица.ВидЗапасов,
			|					Таблица.Организация
			|				ИЗ
			|					ТаблицаИнвентаризации КАК Таблица)) КАК СебестоимостьТоваровОстатки
			|ГДЕ
			|	&СебестоимостьТоваровПоНазначениям
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	СебестоимостьТоваровОстатки.АналитикаУчетаНоменклатуры,
			|	СебестоимостьТоваровОстатки.ВидЗапасов,
			|	СебестоимостьТоваровОстатки.Организация,
			|	ВЫБОР
			|		КОГДА СебестоимостьТоваровОстатки.КоличествоОстаток = 0
			|			ТОГДА 0
			|		ИНАЧЕ (СебестоимостьТоваровОстатки.СтоимостьРеглОстаток
			|				+ СебестоимостьТоваровОстатки.ДопРасходыРеглОстаток
			|				+ СебестоимостьТоваровОстатки.ТрудозатратыРеглОстаток
			|				+ СебестоимостьТоваровОстатки.ПостатейныеПостоянныеРеглОстаток
			|				+ СебестоимостьТоваровОстатки.ПостатейныеПеременныеРеглОстаток)
			|			/ СебестоимостьТоваровОстатки.КоличествоОстаток
			|	КОНЕЦ
			|ИЗ
			|	РегистрНакопления.СебестоимостьТоваров.Остатки(
			|			&КонецМесяцаПериода,
			|			(АналитикаУчетаНоменклатуры.Номенклатура,
			|				АналитикаУчетаНоменклатуры.Характеристика,
			|				АналитикаУчетаНоменклатуры.Серия,
			|				АналитикаУчетаНоменклатуры.МестоХранения,
			|				АналитикаУчетаНоменклатуры.СтатьяКалькуляции,
			|				ВидЗапасов, Организация) В
			|				(ВЫБРАТЬ
			|					Таблица.АналитикаУчетаНоменклатуры.Номенклатура,
			|					Таблица.АналитикаУчетаНоменклатуры.Характеристика,
			|					Таблица.АналитикаУчетаНоменклатуры.Серия,
			|					Таблица.АналитикаУчетаНоменклатуры.МестоХранения,
			|					Таблица.АналитикаУчетаНоменклатуры.СтатьяКалькуляции,
			|					Таблица.ВидЗапасов,
			|					Таблица.Организация
			|				ИЗ
			|					ТаблицаИнвентаризации КАК Таблица)) КАК СебестоимостьТоваровОстатки
			|
			|ГДЕ
			|	НЕ &СебестоимостьТоваровПоНазначениям
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СтоимостьТоваровСрезПоследних.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	СтоимостьТоваровСрезПоследних.ВидЗапасов КАК ВидЗапасов,
			|	СтоимостьТоваровСрезПоследних.Организация КАК Организация,
			|	(СтоимостьТоваровСрезПоследних.СтоимостьРегл
			|		+ СтоимостьТоваровСрезПоследних.ДопРасходыРегл
			|		+ СтоимостьТоваровСрезПоследних.ТрудозатратыРегл
			|		+ СтоимостьТоваровСрезПоследних.ПостатейныеПостоянныеРегл
			|		+ СтоимостьТоваровСрезПоследних.ПостатейныеПеременныеРегл) КАК Цена
			|ПОМЕСТИТЬ СтоимостьТоваровСрезПоследних
			|ИЗ
			|	РегистрСведений.СтоимостьТоваров.СрезПоследних(
			|			&ДатаОкончания,
			|			(АналитикаУчетаНоменклатуры, ВидЗапасов, РазделУчета, Организация) В
			|				(ВЫБРАТЬ
			|					Таблица.АналитикаУчетаНоменклатуры,
			|					Таблица.ВидЗапасов,
			|					ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
			|					Таблица.Организация
			|				ИЗ
			|					ТаблицаИнвентаризации КАК Таблица)) КАК СтоимостьТоваровСрезПоследних
			|ГДЕ
			|	&СебестоимостьТоваровПоНазначениям
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	СтоимостьТоваровСрезПоследних.АналитикаУчетаНоменклатуры,
			|	СтоимостьТоваровСрезПоследних.ВидЗапасов,
			|	СтоимостьТоваровСрезПоследних.Организация,
			|	(СтоимостьТоваровСрезПоследних.СтоимостьРегл
			|		+ СтоимостьТоваровСрезПоследних.ДопРасходыРегл
			|		+ СтоимостьТоваровСрезПоследних.ТрудозатратыРегл
			|		+ СтоимостьТоваровСрезПоследних.ПостатейныеПостоянныеРегл
			|		+ СтоимостьТоваровСрезПоследних.ПостатейныеПеременныеРегл)
			|ИЗ
			|	РегистрСведений.СтоимостьТоваров.СрезПоследних(
			|			&ДатаОкончания,
			|			(АналитикаУчетаНоменклатуры.Номенклатура,
			|				АналитикаУчетаНоменклатуры.Характеристика,
			|				АналитикаУчетаНоменклатуры.Серия,
			|				АналитикаУчетаНоменклатуры.МестоХранения,
			|				АналитикаУчетаНоменклатуры.СтатьяКалькуляции,
			|				ВидЗапасов,
			|				РазделУчета,
			|				Организация) В
			|				(ВЫБРАТЬ
			|					Таблица.АналитикаУчетаНоменклатуры.Номенклатура,
			|					Таблица.АналитикаУчетаНоменклатуры.Характеристика,
			|					Таблица.АналитикаУчетаНоменклатуры.Серия,
			|					Таблица.АналитикаУчетаНоменклатуры.МестоХранения,
			|					Таблица.АналитикаУчетаНоменклатуры.СтатьяКалькуляции,
			|					Таблица.ВидЗапасов,
			|					ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
			|					Таблица.Организация
			|				ИЗ
			|					ТаблицаИнвентаризации КАК Таблица)) КАК СтоимостьТоваровСрезПоследних
			|
			|ГДЕ
			|	НЕ &СебестоимостьТоваровПоНазначениям
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ТаблицаИнвентаризации.ВидЗапасов КАК ВидЗапасов,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(СебестоимостьТоваровОстатки.Цена, 0) = 0
			|			ТОГДА ЕСТЬNULL(СтоимостьТоваровСрезПоследних.Цена, 0)
			|		ИНАЧЕ СебестоимостьТоваровОстатки.Цена
			|	КОНЕЦ КАК Цена
			|ПОМЕСТИТЬ ТаблицаЦен
			|ИЗ
			|	ТаблицаИнвентаризации КАК ТаблицаИнвентаризации
			|		ЛЕВОЕ СОЕДИНЕНИЕ СебестоимостьТоваровОстатки КАК СебестоимостьТоваровОстатки
			|		ПО ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры = СебестоимостьТоваровОстатки.АналитикаУчетаНоменклатуры
			|			И (ТаблицаИнвентаризации.ВидЗапасов = СебестоимостьТоваровОстатки.ВидЗапасов)
			|			И (СебестоимостьТоваровОстатки.Организация = &Организация)
			|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьТоваровСрезПоследних КАК СтоимостьТоваровСрезПоследних
			|		ПО ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры = СтоимостьТоваровСрезПоследних.АналитикаУчетаНоменклатуры
			|			И (ТаблицаИнвентаризации.ВидЗапасов = СтоимостьТоваровСрезПоследних.ВидЗапасов)
			|			И (СтоимостьТоваровСрезПоследних.Организация = &Организация)
			|ГДЕ
			|	&СебестоимостьТоваровПоНазначениям
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(СебестоимостьТоваровОстатки.Цена, 0) = 0
			|			ТОГДА ЕСТЬNULL(СтоимостьТоваровСрезПоследних.Цена, 0)
			|		ИНАЧЕ СебестоимостьТоваровОстатки.Цена
			|	КОНЕЦ,
			|	ТаблицаИнвентаризации.ВидЗапасов
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры,
			|	ТаблицаИнвентаризации.ВидЗапасов,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(СебестоимостьТоваровОстатки.Цена, 0) = 0
			|			ТОГДА ЕСТЬNULL(СтоимостьТоваровСрезПоследних.Цена, 0)
			|		ИНАЧЕ СебестоимостьТоваровОстатки.Цена
			|	КОНЕЦ КАК Цена
			|ИЗ
			|	ТаблицаИнвентаризации КАК ТаблицаИнвентаризации
			|		ЛЕВОЕ СОЕДИНЕНИЕ СебестоимостьТоваровОстатки КАК СебестоимостьТоваровОстатки
			|		ПО ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура = СебестоимостьТоваровОстатки.АналитикаУчетаНоменклатуры.Номенклатура
			|			И ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика = СебестоимостьТоваровОстатки.АналитикаУчетаНоменклатуры.Характеристика
			|			И ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Серия = СебестоимостьТоваровОстатки.АналитикаУчетаНоменклатуры.Серия
			|			И ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.МестоХранения = СебестоимостьТоваровОстатки.АналитикаУчетаНоменклатуры.МестоХранения
			|			И ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.СтатьяКалькуляции = СебестоимостьТоваровОстатки.АналитикаУчетаНоменклатуры.СтатьяКалькуляции
			|			И (ТаблицаИнвентаризации.ВидЗапасов = СебестоимостьТоваровОстатки.ВидЗапасов)
			|			И (СебестоимостьТоваровОстатки.Организация = &Организация)
			|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьТоваровСрезПоследних КАК СтоимостьТоваровСрезПоследних
			|		ПО ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура = СтоимостьТоваровСрезПоследних.АналитикаУчетаНоменклатуры.Номенклатура
			|			И ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика = СтоимостьТоваровСрезПоследних.АналитикаУчетаНоменклатуры.Характеристика
			|			И ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Серия = СтоимостьТоваровСрезПоследних.АналитикаУчетаНоменклатуры.Серия
			|			И ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.МестоХранения = СтоимостьТоваровСрезПоследних.АналитикаУчетаНоменклатуры.МестоХранения
			|			И ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.СтатьяКалькуляции = СтоимостьТоваровСрезПоследних.АналитикаУчетаНоменклатуры.СтатьяКалькуляции
			|			И (ТаблицаИнвентаризации.ВидЗапасов = СтоимостьТоваровСрезПоследних.ВидЗапасов)
			|		И (СтоимостьТоваровСрезПоследних.Организация = &Организация)
			|
			|ГДЕ
			|	НЕ &СебестоимостьТоваровПоНазначениям
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(СебестоимостьТоваровОстатки.Цена, 0) = 0
			|			ТОГДА ЕСТЬNULL(СтоимостьТоваровСрезПоследних.Цена, 0)
			|		ИНАЧЕ СебестоимостьТоваровОстатки.Цена
			|	КОНЕЦ,
			|	ТаблицаИнвентаризации.ВидЗапасов
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	&Руководитель КАК Руководитель,
			|	&ДолжностьРуководителя КАК ДолжностьРуководителя,
			|	&ГлавныйБухгалтер КАК ГлавныйБухгалтер,
			|	&Организация КАК Организация,
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).КодПоОКПО КАК ОрганизацияПоОКПО,
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).Префикс КАК Префикс,
			|	ИнвентаризационнаяОпись.Склад.ТекущийОтветственный КАК Кладовщик,
			|	ИнвентаризационнаяОпись.Склад.ТекущаяДолжностьОтветственного КАК ДолжностьКладовщика,
			|	ИнвентаризационнаяОпись.Склад.Наименование КАК ПодразделениеПредставление,
			|	&ДатаНачала КАК ДатаНачала,
			|	&ДатаОкончания КАК ДатаОкончания,
			|	ИнвентаризационнаяОпись.Склад КАК Склад,
			|	ИнвентаризационнаяОпись.Дата КАК ОснованиеДата,
			|	ИнвентаризационнаяОпись.Номер КАК ОснованиеНомер,
			|	ИнвентаризационнаяОпись.Номер КАК НомерДокумента,
			|	ИнвентаризационнаяОпись.Дата КАК ДатаДокумента,
			|	ИнвентаризационнаяОпись.Ссылка КАК Ссылка,
			|	&ДатаОкончания КАК ДатаСнятияОстатков
			|ИЗ
			|	Документ.ИнвентаризационнаяОпись КАК ИнвентаризационнаяОпись
			|ГДЕ
			|	ИнвентаризационнаяОпись.Ссылка = &ДокументОснование
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Серия КАК Серия,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Серия.Наименование КАК СерияНаименование,
			|	СУММА(ТаблицаИнвентаризации.КоличествоПоУчету) КАК КоличествоПоУчету,
			|	СУММА(ТаблицаИнвентаризации.КоличествоФактическое) КАК КоличествоФактическое,
			|	ТаблицаЦен.Цена КАК Цена,
			|	СУММА(ТаблицаИнвентаризации.КоличествоПоУчету * ТаблицаЦен.Цена) КАК СуммаПоУчету,
			|	СУММА(ТаблицаИнвентаризации.КоличествоФактическое * ТаблицаЦен.Цена) КАК СуммаФактическая,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.Код КАК ТоварКод,
			|	ЛОЖЬ КАК ЭтоВозвратнаяТара
			|ИЗ
			|	ТаблицаИнвентаризации КАК ТаблицаИнвентаризации
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЦен КАК ТаблицаЦен
			|		ПО ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры = ТаблицаЦен.АналитикаУчетаНоменклатуры
			|			И ТаблицаИнвентаризации.ВидЗапасов = ТаблицаЦен.ВидЗапасов
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика.НаименованиеПолное,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Серия.Наименование,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Код,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Наименование,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.НаименованиеПолное,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.Код,
			|	ТаблицаЦен.Цена,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Серия
			|
			|УПОРЯДОЧИТЬ ПО
			|	ТоварНаименование,
			|	Номенклатура";
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Номенклатура.Код", "Номенклатура." + КолонкаКодов);
			Запрос.Текст = ТекстЗапроса;
			
			Результаты = Запрос.ВыполнитьПакет();
			РезультатПоШапке = Результаты[8];
			РезультатПоТабличнойЧасти = Результаты[9];
			РезультатКурсВалюты = Неопределено;
			
		КонецЕсли;
		
	ИначеЕсли ИсточникИнформацииОЦенахДляПечати = Перечисления.ИсточникиИнформацииОЦенахДляПечати.ПоВидуЦен Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ВложенныйЗапрос.Организация КАК Организация
		|ПОМЕСТИТЬ ТаблицаНоменклатуры
		|ИЗ
		|	(ВЫБРАТЬ
		|		Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
		|		&Организация КАК Организация
		|	ИЗ
		|		Документ.ПересчетТоваров.Товары КАК ПересчетТоваровТовары
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
		|			ПО ПересчетТоваровТовары.Номенклатура = Аналитика.Номенклатура
		|				И ПересчетТоваровТовары.Характеристика = Аналитика.Характеристика
		|				И ПересчетТоваровТовары.Назначение = Аналитика.Назначение
		|				И (ПересчетТоваровТовары.Серия = Аналитика.Серия
		|					ИЛИ НЕ ПересчетТоваровТовары.СтатусУказанияСерий = 14)
		|				И ПересчетТоваровТовары.Ссылка.Склад = Аналитика.МестоХранения
		|	ГДЕ
		|		ПересчетТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|		И ПересчетТоваровТовары.Ссылка.Склад = &Склад
		|		И ПересчетТоваровТовары.Ссылка.Проведен
		|		И ПересчетТоваровТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПересчетовТоваров.Выполнено)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СписаниеНедостачТоваровТовары.АналитикаУчетаНоменклатуры,
		|		&Организация
		|	ИЗ
		|		Документ.СписаниеНедостачТоваров.Товары КАК СписаниеНедостачТоваровТовары
		|	ГДЕ
		|		СписаниеНедостачТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|		И СписаниеНедостачТоваровТовары.Ссылка.Склад = &Склад
		|		И СписаниеНедостачТоваровТовары.Ссылка.Проведен
		|		И СписаниеНедостачТоваровТовары.Ссылка.Организация = &Организация
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ОприходованиеИзлишковТоваровТовары.АналитикаУчетаНоменклатуры,
		|		&Организация
		|	ИЗ
		|		Документ.ОприходованиеИзлишковТоваров.Товары КАК ОприходованиеИзлишковТоваровТовары
		|	ГДЕ
		|		ОприходованиеИзлишковТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|		И ОприходованиеИзлишковТоваровТовары.Ссылка.Склад = &Склад
		|		И ОприходованиеИзлишковТоваровТовары.Ссылка.Проведен
		|		И ОприходованиеИзлишковТоваровТовары.Ссылка.Организация = &Организация
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ПересортицаТоваровТовары.АналитикаУчетаНоменклатуры,
		|		&Организация
		|	ИЗ
		|		Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
		|	ГДЕ
		|		ПересортицаТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|		И ПересортицаТоваровТовары.Ссылка.Склад = &Склад
		|		И ПересортицаТоваровТовары.Ссылка.Проведен
		|		И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ПересортицаТоваровТовары.АналитикаУчетаНоменклатурыОприходование,
		|		&Организация
		|	ИЗ
		|		Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
		|	ГДЕ
		|		ПересортицаТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|		И ПересортицаТоваровТовары.Ссылка.Склад = &Склад
		|		И ПересортицаТоваровТовары.Ссылка.Проведен
		|		И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ПорчаТоваровТовары.АналитикаУчетаНоменклатуры,
		|		&Организация
		|	ИЗ
		|		Документ.ПорчаТоваров.Товары КАК ПорчаТоваровТовары
		|	ГДЕ
		|		ПорчаТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|		И ПорчаТоваровТовары.Ссылка.Склад = &Склад
		|		И ПорчаТоваровТовары.Ссылка.Проведен
		|		И ПорчаТоваровТовары.Ссылка.Организация = &Организация
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Аналитика.КлючАналитики,
		|		&Организация
		|	ИЗ
		|		Документ.ПорчаТоваров.Товары КАК ПорчаТоваровТоварыОприходованные
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
		|			ПО ПорчаТоваровТоварыОприходованные.НоменклатураОприходование = Аналитика.Номенклатура
		|				И ПорчаТоваровТоварыОприходованные.ХарактеристикаОприходование = Аналитика.Характеристика
		|				И ПорчаТоваровТоварыОприходованные.Назначение = Аналитика.Назначение
		|				И (ПорчаТоваровТоварыОприходованные.Серия = Аналитика.Серия
		|					ИЛИ НЕ ПорчаТоваровТоварыОприходованные.СтатусУказанияСерий = 14)
		|				И ПорчаТоваровТоварыОприходованные.Ссылка.Склад = Аналитика.МестоХранения
		|	ГДЕ
		|		ПорчаТоваровТоварыОприходованные.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|		И ПорчаТоваровТоварыОприходованные.Ссылка.Склад = &Склад
		|		И ПорчаТоваровТоварыОприходованные.Ссылка.Проведен
		|		И ПорчаТоваровТоварыОприходованные.Ссылка.Организация = &Организация) КАК ВложенныйЗапрос
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ТоварыОрганизацийОстатки.КоличествоОстаток
		|ПОМЕСТИТЬ ТоварыОрганизацийОстатки
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций.Остатки(
		|			&ДатаОстатков,
		|			АналитикаУчетаНоменклатуры В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						ТаблицаНоменклатуры.АналитикаУчетаНоменклатуры
		|					ИЗ
		|						ТаблицаНоменклатуры)
		|				И Организация = &Организация
		|				И ВидЗапасов.ТипЗапасов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар))) КАК ТоварыОрганизацийОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	-ТоварыОрганизаций.Количество КАК КоличествоОтклонение,
		|	0 КАК КоличествоФактическое
		|ПОМЕСТИТЬ ТоварыИнвентаризации
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		|ГДЕ
		|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.СписаниеНедостачТоваров
		|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.СписаниеНедостачТоваров).Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.СписаниеНедостачТоваров).Склад = &Склад
		|	И ТоварыОрганизаций.Организация = &Организация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры,
		|	ТоварыОрганизаций.Количество,
		|	0
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		|ГДЕ
		|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.ОприходованиеИзлишковТоваров
		|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ОприходованиеИзлишковТоваров).Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ОприходованиеИзлишковТоваров).Склад = &Склад
		|	И ТоварыОрганизаций.Организация = &Организация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры,
		|	ВЫБОР
		|		КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ТоварыОрганизаций.Количество
		|		ИНАЧЕ -ТоварыОрганизаций.Количество
		|	КОНЕЦ,
		|	0
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		|ГДЕ
		|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.ПересортицаТоваров
		|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ПересортицаТоваров).Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ПересортицаТоваров).Склад = &Склад
		|	И ТоварыОрганизаций.Организация = &Организация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры,
		|	ВЫБОР
		|		КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ТоварыОрганизаций.Количество
		|		ИНАЧЕ -ТоварыОрганизаций.Количество
		|	КОНЕЦ,
		|	0
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		|ГДЕ
		|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.ПорчаТоваров
		|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ПорчаТоваров).Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ПорчаТоваров).Склад = &Склад
		|	И ТоварыОрганизаций.Организация = &Организация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаНоменклатуры.АналитикаУчетаНоменклатуры,
		|	0,
		|	ТоварыОрганизацийОстатки.КоличествоОстаток
		|ИЗ
		|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТоварыОрганизацийОстатки КАК ТоварыОрганизацийОстатки
		|		ПО ТаблицаНоменклатуры.АналитикаУчетаНоменклатуры = ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|	ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Серия КАК Серия,
		|	&ПоляДляЦенообразованияВыборка,
		|	СУММА(ТоварыИнвентаризации.КоличествоФактическое) КАК КоличествоФактическое,
		|	СУММА(ТоварыИнвентаризации.КоличествоФактическое - ТоварыИнвентаризации.КоличествоОтклонение) КАК КоличествоПоУчету
		|ПОМЕСТИТЬ ТаблицаИнвентаризации
		|ИЗ
		|	ТоварыИнвентаризации КАК ТоварыИнвентаризации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
		|		ПО ВидыНоменклатуры.Ссылка = ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры
		|
		|СГРУППИРОВАТЬ ПО
		|	ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура,
		|	ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика,
		|	&ПоляДляЦенообразованияГруппировка,
		|	ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Руководитель КАК Руководитель,
		|	&ДолжностьРуководителя КАК ДолжностьРуководителя,
		|	&ГлавныйБухгалтер КАК ГлавныйБухгалтер,
		|	&Организация КАК Организация,
		|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).КодПоОКПО КАК ОрганизацияПоОКПО,
		|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).Префикс КАК Префикс,
		|	ИнвентаризационнаяОпись.Склад.ТекущийОтветственный КАК Кладовщик,
		|	ИнвентаризационнаяОпись.Склад.ТекущаяДолжностьОтветственного КАК ДолжностьКладовщика,
		|	ИнвентаризационнаяОпись.Склад.Наименование КАК ПодразделениеПредставление,
		|	&ДатаНачала КАК ДатаНачала,
		|	&ДатаОкончания КАК ДатаОкончания,
		|	ИнвентаризационнаяОпись.Склад КАК Склад,
		|	ИнвентаризационнаяОпись.Дата КАК ОснованиеДата,
		|	ИнвентаризационнаяОпись.Номер КАК ОснованиеНомер,
		|	ИнвентаризационнаяОпись.Номер КАК НомерДокумента,
		|	ИнвентаризационнаяОпись.Дата КАК ДатаДокумента,
		|	ИнвентаризационнаяОпись.Ссылка КАК Ссылка,
		|	&ДатаОкончания КАК ДатаСнятияОстатков
		|ИЗ
		|	Документ.ИнвентаризационнаяОпись КАК ИнвентаризационнаяОпись
		|ГДЕ
		|	ИнвентаризационнаяОпись.Ссылка = &ДокументОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИнвентаризации.Номенклатура КАК Номенклатура,
		|	ТаблицаИнвентаризации.Характеристика КАК Характеристика,
		|	ТаблицаИнвентаризации.Серия КАК Серия,
		|	ТаблицаИнвентаризации.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
		|	ТаблицаИнвентаризации.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
		|	ТаблицаИнвентаризации.Серия.Наименование КАК СерияНаименование,
		|	ТаблицаИнвентаризации.КоличествоПоУчету,
		|	ТаблицаИнвентаризации.КоличествоФактическое,
		|	ВЫБОР
		|		КОГДА НЕ &ТекстЗапросаКоэффициентУпаковки ЕСТЬ NULL 
		|				И &ТекстЗапросаКоэффициентУпаковки <> 0
		|			ТОГДА ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) / &ТекстЗапросаКоэффициентУпаковки
		|		ИНАЧЕ ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
		|	КОНЕЦ КАК Цена,
		|	ТаблицаИнвентаризации.КоличествоПоУчету * ВЫБОР
		|		КОГДА НЕ &ТекстЗапросаКоэффициентУпаковки ЕСТЬ NULL 
		|				И &ТекстЗапросаКоэффициентУпаковки <> 0
		|			ТОГДА ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) / &ТекстЗапросаКоэффициентУпаковки
		|		ИНАЧЕ ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
		|	КОНЕЦ КАК СуммаПоУчету,
		|	ТаблицаИнвентаризации.КоличествоФактическое * ВЫБОР
		|		КОГДА НЕ &ТекстЗапросаКоэффициентУпаковки ЕСТЬ NULL 
		|				И &ТекстЗапросаКоэффициентУпаковки <> 0
		|			ТОГДА ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) / &ТекстЗапросаКоэффициентУпаковки
		|		ИНАЧЕ ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
		|	КОНЕЦ КАК СуммаФактическая,
		|	ТаблицаИнвентаризации.Номенклатура.Код КАК ТоварКод,
		|	ТаблицаИнвентаризации.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
		|	ТаблицаИнвентаризации.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
		|	ЛОЖЬ КАК ЭтоВозвратнаяТара
		|ИЗ
		|	ТаблицаИнвентаризации КАК ТаблицаИнвентаризации
		|		ЛЕВОЕ СОЕДИНЕНИЕ &ЦеныНоменклатурыСрезПоследних КАК ЦеныНоменклатуры
		|		ПО &УсловиеСоедиенияЦеныНоменклатурыСрезПоследних
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТоварНаименование,
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	КурсыВалютСрезПоследних.КурсЧислитель / КурсыВалютСрезПоследних.КурсЗнаменатель КАК КоэффициентПересчета
		|ИЗ
		|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&ДатаОкончания, Валюта = &ВидЦены И БазоваяВалюта = &БазоваяВалюта) КАК КурсыВалютСрезПоследних";

		ИспользуетсяЦенообразование25 = ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25(ДатаОкончания);
		
		НастройкаЦенообразования = ЦенообразованиеКлиентСервер.НастройкиДляВременнойТаблицыСДополнениемДляЦенообразования();
		НастройкаЦенообразования.ИсточникТоваров = "ТоварыИнвентаризации.АналитикаУчетаНоменклатуры";
		НастройкаЦенообразования.ПолеУпаковка = "";
		
		ТекстЗамены = ЦенообразованиеКлиентСервер.ТекстПолейДляЦенообразования(НастройкаЦенообразования,, ИспользуетсяЦенообразование25);
		Если ЗначениеЗаполнено(ТекстЗамены) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляЦенообразованияВыборка", ТекстЗамены);
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляЦенообразованияВыборка,", "");
		КонецЕсли;
		
		ТекстЗамены = ЦенообразованиеКлиентСервер.ТекстПолейДляЦенообразования(НастройкаЦенообразования, Истина, ИспользуетсяЦенообразование25);
		Если ЗначениеЗаполнено(ТекстЗамены) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляЦенообразованияГруппировка", ТекстЗамены);
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляЦенообразованияГруппировка,", "");
		КонецЕсли;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ЦеныНоменклатурыСрезПоследних",
			ЦенообразованиеКлиентСервер.ТекстЗапросаРегистрСведенийЦеныНоменклатуры(
				"ТаблицаИнвентаризации",
				"&ДатаОкончания",
				Новый Структура("ВТаблице", "&ВидЦены"),
				ИспользуетсяЦенообразование25)
		);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&УсловиеСоедиенияЦеныНоменклатурыСрезПоследних",
			ЦенообразованиеКлиентСервер.ТекстЗапросаРегистрСведенийЦеныНоменклатурыУсловиеСоединения(
				"ТаблицаИнвентаризации",
				"ЦеныНоменклатуры",,
				ИспользуетсяЦенообразование25)
		);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ТекстЗапросаКоэффициентУпаковки",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
				"ЦеныНоменклатуры.Упаковка",
				"ЦеныНоменклатуры.Номенклатура")
		);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Номенклатура.Код", "Номенклатура." + КолонкаКодов);
		
		Запрос.УстановитьПараметр("БазоваяВалюта", ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация));
		Запрос.Текст = ТекстЗапроса;
		
		Результаты = Запрос.ВыполнитьПакет();
		РезультатПоШапке = Результаты[4];
		РезультатПоТабличнойЧасти = Результаты[5];
		РезультатКурсВалюты = Результаты[6];
		
	КонецЕсли;
	
	СтруктураВозврата.РезультатПоШапке			= РезультатПоШапке;
	СтруктураВозврата.РезультатПоТабличнойЧасти	= РезультатПоТабличнойЧасти;
	СтруктураВозврата.РезультатКурсВалюты		= РезультатКурсВалюты;
	
	Возврат СтруктураВозврата;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
