
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ГрафикПлатежей,
		"ОбъектОплаты",
		Параметры.ДокументОснование,
		ВидСравненияКомпоновкиДанных.ВСписке,,
		Истина);
	
	Если Параметры.ФормаОплаты = "Безналичная" Тогда
		ТипМестаОплаты = Тип("СправочникСсылка.Кассы");
		Заголовок = НСтр("ru = 'Выберите банковский счет'")
	ИначеЕсли Параметры.ФормаОплаты = "Наличная" Тогда
		ТипМестаОплаты = Тип("СправочникСсылка.БанковскиеСчетаОрганизаций");
		Заголовок = НСтр("ru = 'Выберите кассу'")
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ГрафикПлатежей,
		"ТипМестаОплаты",
		ТипМестаОплаты,
		ВидСравненияКомпоновкиДанных.НеРавно,,
		Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ГрафикПлатежейВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ГрафикПлатежей.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура("БанковскийСчетКасса, Сумма",
		ТекущиеДанные.БанковскийСчетКасса, ТекущиеДанные.Сумма);
	
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

#КонецОбласти