
&НаСервере
Функция ПерезаполнитьОтчетыОРозничныхПродажахНаСервере(ПараметрКоманды)
	
	МассивДокументов = Новый Массив;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КассоваяСмена.Ссылка КАК КассоваяСмена,
	|	КассоваяСмена.КассаККМ КАК КассаККМ,
	|	КассоваяСмена.ФискальноеУстройство КАК ФискальноеУстройство	
	|ИЗ
	|	Документ.КассоваяСмена КАК КассоваяСмена
	|ГДЕ
	|	КассоваяСмена.Проведен
	|	И КассоваяСмена.Ссылка В (&КассоваяСмена)
	|	И КассоваяСмена.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыКассовойСмены.Открыта)
	|");
	
	Запрос.УстановитьПараметр("КассоваяСмена", ПараметрКоманды);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ОписаниеОшибки = "";
		// Получим состояние кассовой смены
		СостояниеКассовойСмены = РозничныеПродажи.ПолучитьСостояниеКассовойСмены(Выборка.КассаККМ, ПараметрКоманды);
		
		// Заполним по каждому типу налогобложения пару документов: Отчет о розничных продажах, Отчет о розничных возвратах
		ПарыДокументовСформированы = РозничныеПродажи.ЗаполнитьОтчетыОРозничныхПродажахИВозвратах(СостояниеКассовойСмены, ОписаниеОшибки);
		
		МассивДокументов.Добавить(СостояниеКассовойСмены.КассоваяСмена);
		
		Если Не ПарыДокументовСформированы Тогда
			МассивСтрокОписанияОшибки = Новый Массив;
			МассивСтрокОписанияОшибки.Добавить(СтрШаблон(НСтр("ru = 'Операция перезаполнения отчета о розничных продажах по кассовой смене %1 прервана по ошибке.'"), СостояниеКассовойСмены.КассоваяСмена));
			Если Не ПустаяСтрока(ОписаниеОшибки) Тогда
				МассивСтрокОписанияОшибки.Добавить(НСтр("ru = 'Дополнительное описание:'"));
				МассивСтрокОписанияОшибки.Добавить(ОписаниеОшибки);
			КонецЕсли;
			
			ОписаниеОшибки = СтрСоединить(МассивСтрокОписанияОшибки, Символы.ПС);
			ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат МассивДокументов;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ОчиститьСообщения();
	
	МассивДокументов = ПерезаполнитьОтчетыОРозничныхПродажахНаСервере(ПараметрКоманды);

	Для Каждого Документ Из МассивДокументов Цикл
		
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.КассоваяСмена") Тогда
			ОткрытьФорму("Документ.КассоваяСмена.ФормаОбъекта", Новый Структура("Ключ,ОткрытьВкладкуОтчетыЗаСмену", Документ, Истина));
		КонецЕсли;	
		
	КонецЦикла;
	
	Оповестить("Запись_ОтчетОРозничныхВозвратах", Новый Структура, ПараметрКоманды);
	Оповестить("Запись_ОтчетОРозничныхПродажах", Новый Структура, ПараметрКоманды);
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Заполнение отчетов о розничных продажах и возвратах выполнено'", ОбщегоНазначенияКлиент.КодОсновногоЯзыка()));
	
КонецПроцедуры
