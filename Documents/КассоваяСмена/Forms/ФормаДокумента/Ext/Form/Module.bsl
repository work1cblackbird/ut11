
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ТипОборудованияФискальногоУстройства = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ФискальноеУстройство, "ТипОборудования");
	ЭтоРазъезднаяКассаККМ = Ложь;
	Если ТипЗнч(Объект.КассаККМ) = Тип("СправочникСсылка.КассыККМ") Тогда
		ЭтоРазъезднаяКассаККМ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.КассаККМ, "РазъезднаяТорговля") = Истина;
	КонецЕсли;
	
	УправлениеВидимостьюЭлементов();
	УправлениеДоступностьюЭлементов(ЭтотОбъект);
	
	Если Параметры.Свойство("ОткрытьВкладкуОтчетыЗаСмену") Тогда
		Элементы.ГруппаДанные.ТекущаяСтраница = Элементы.ГруппаОтчетыЗаСмену;
	КонецЕсли;
	
	КассовыеСменыПереопределяемый.ФормаДокументаПриСозданииНаСервере(ЭтотОбъект);
	
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	XMLСтрока = ТекущийОбъект.ФДОЗакрытииСмены.Получить();
	Если XMLСтрока = Неопределено Или Не ТипЗнч(XMLСтрока) = Тип("Строка") Или XMLСтрока = "" Тогда
		ДоступныВсеФискальныеДанные = Ложь;
	Иначе
		ДоступныВсеФискальныеДанные = Истина;
	КонецЕсли;
	
	ОперацияПриЗакрытииКассовойСмены = Константы.ОперацияПриЗакрытииКассовойСмены.Получить();
	
	УстановитьОтборыДинамическихСписков();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	УстановитьОтборыДинамическихСписков();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОтчетыЗаСменуВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
		
	ОбщегоНазначенияУТКлиент.ИзменитьЭлемент(Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПолныеФискальныеДанные(Команда)
	
	ПараметрыОткрытияВсехДанных = Новый Структура();
	ПараметрыОткрытияВсехДанных.Вставить("КассоваяСмена", Объект.Ссылка);
	
	ОткрытьФорму("Документ.КассоваяСмена.Форма.ФормаФискальныхДанных", ПараметрыОткрытияВсехДанных,,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗакрытьСмену(Команда)
	
	Если Модифицированность Тогда
		
		ТекстВопроса = НСтр("ru = 'Перед закрытием смены документ будет записан. Продолжить?'");
		ОписаниеОтвета = Новый ОписаниеОповещения("ЗакрытьСменуПослеПодтвержденияЗаписи", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОтвета, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ЗакрытьСменуВыполнить();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьСменуПослеПодтвержденияЗаписи(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗакрытьСменуВыполнить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьСменуВыполнить()
	
	ОчиститьСообщения();
	
	РезультатВыбора = МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании(Истина, , ИдентификаторУстройства());
	ДополнительныеПараметры = Новый Структура("Команда", "ЗакрытьСмену");
	
	Если НЕ РезультатВыбора.Результат Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатВыбора.ОписаниеОшибки);
		Возврат;
	КонецЕсли;
	
	ОповещениеПриЗавершении = Новый ОписаниеОповещения("ЗакрытьСменуПослеЗакрытияСмены", ЭтотОбъект);
	ФискальноеУстройство = РезультатВыбора.ИдентификаторУстройства;
	Контекст = Новый Структура;
	Контекст.Вставить("ФискальноеУстройство",         ФискальноеУстройство);
	Контекст.Вставить("ОписаниеОповещенияЗавершение", ОповещениеПриЗавершении);
	
	ТекущийКассир = МенеджерОборудованияКлиентСервер.ТекущийКассирДляФискальныхОпераций();
	
	ПараметрыОперации = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОткрытияЗакрытияСмены();
	ПараметрыОперации.Кассир    = ТекущийКассир.Кассир;
	ПараметрыОперации.КассирИНН = ТекущийКассир.КассирИНН;
	
	ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьЗакрытиеСменыНаФискальномУстройстве(
				Новый ОписаниеОповещения("ПослеВыполненияКомандыЗакрытияСмены", РозничныеПродажиКлиент, Контекст),
					УникальныйИдентификатор, ФискальноеУстройство, ПараметрыОперации);

КонецПроцедуры

&НаСервере
Функция ИдентификаторУстройства()
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "ФискальноеУстройство");
КонецФункции

&НаКлиенте
Процедура ЗакрытьСменуПослеЗакрытияСмены(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия Тогда
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Кассовая смена закрыта'")
			,
			,
			,
			БиблиотекаКартинок.ВыполнитьЗадачу);
		
		Прочитать();
		
	Иначе
		
			ПоказатьОповещениеПользователя(
			НСтр("ru = 'Ошибка закрытия кассовой смены'")
			,
			,
			,
			БиблиотекаКартинок.Удалить);
		
	КонецЕсли;
	
	УправлениеДоступностьюЭлементов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
// Управляет достпуностью элементов формы
// Параметры:
//  Форма - ФормаКлиентсткогоПриложения
//
Процедура УправлениеДоступностьюЭлементов(Форма)
	
	СтатусСменыОткрыта = Форма.Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыКассовойСмены.Открыта");
	Форма.Элементы.АдресРасчетов.Доступность =  СтатусСменыОткрыта;
	Форма.Элементы.МестоРасчетов.Доступность =  СтатусСменыОткрыта;
	Форма.Элементы.ФормаЗакрытьСмену.Доступность = СтатусСменыОткрыта;
	
	ПерезаполнитьОРП = Форма.ОперацияПриЗакрытииКассовойСмены <> ПредопределенноеЗначение("Перечисление.ОперацииПриЗакрытииКассовойСмены.УдалениеЧековККМ");
	Форма.Элементы.ГлобальныеКомандыФормы.Доступность = ПерезаполнитьОРП;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостьюЭлементов()
	
	Элементы.ГруппаФискальныеДанные.Видимость = ТипОборудованияФискальногоУстройства = Перечисления.ТипыПодключаемогоОборудования.ККТ;
	Элементы.ПолныеФискальныеДанные.Видимость = Не Объект.Статус = Перечисления.СтатусыКассовойСмены.Открыта;
	Элементы.ПолныеФискальныеДанные.Видимость = ДоступныВсеФискальныеДанные;
	Элементы.АдресРасчетов.Видимость = ЭтоРазъезднаяКассаККМ;
	Элементы.МестоРасчетов.Видимость = ЭтоРазъезднаяКассаККМ;
	
КонецПроцедуры
#Область Отборы

&НаСервере
Процедура УстановитьОтборыДинамическихСписков()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		СписокОтчетыЗаСмену,
		"КассоваяСмена",
		Объект.Ссылка,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		Истина);
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти
