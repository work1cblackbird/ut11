#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("Рассылка") 
		ИЛИ НЕ ТипЗнч(Параметры.Рассылка) = Тип("ДокументСсылка.РассылкаКлиентам")
		ИЛИ Параметры.Рассылка.Пустая() Тогда
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	// Если нет права доступа к подсистеме "Взаимодействия" то показывать нечего.
	Если Не ПравоДоступа("Просмотр", Метаданные.ЖурналыДокументов.Взаимодействия) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'У вас нет прав на просмотр отчета по созданным документам рассылки.'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Рассылка = Параметры.Рассылка;

	СформироватьОтчет();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	СформироватьОтчет();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбработатьТекстЗапросаАдресатыРассылки(ДанныеРассылки, Запрос, ЕстьОтборПоКомпоновке)

	ТекстОбъединить =" 
	|
	|ОБЪЕДИНИТЬ
	|";
		
	ТекстПоместить = "ПОМЕСТИТЬ Адресаты";
		
	Если ДанныеРассылки.Принудительная Тогда
		
		ТекстЗапросаПартнеры = "";
		ТекстЗапросаКонтактныеЛица = "";
		
		Если ДанныеРассылки.ОтправлятьПартнеру Тогда
			
			ТекстЗапросаПартнеры = "
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Партнеры.Ссылка КАК Получатель,
			|	ЕСТЬNULL(ПартнерыКонтактнаяИнформация.Представление, """") КАК ПредставлениеКонтактнойИнформации,
			|	&ВидКонтактнойИнформацииПартнера КАК ВидКонтактнойИнформации,
			|	Партнеры.НаименованиеПолное КАК ПредставлениеПолучателя
			|	,&ТекстПоместить
			|ИЗ
			|	Справочник.Партнеры КАК Партнеры
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
			|		ПО (ПартнерыКонтактнаяИнформация.Ссылка = Партнеры.Ссылка)
			|			И (ПартнерыКонтактнаяИнформация.Вид = &ВидКонтактнойИнформацииПартнера)
			|		И &СоединениеПоОтбору
			|ГДЕ
			|	НЕ Партнеры.ПометкаУдаления";
			
			Если ЕстьОтборПоКомпоновке Тогда
				ТекстСоединениеПоОтбору ="ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборПартнеров КАК ОтборПартнеров
				                         |ПО Партнеры.Ссылка = ОтборПартнеров.Партнер";
			Иначе
				ТекстСоединениеПоОтбору = "";
			КонецЕсли;
			
			ТекстЗапросаПартнеры = СтрЗаменить(ТекстЗапросаПартнеры, "И &СоединениеПоОтбору", ТекстСоединениеПоОтбору);
			ТекстЗапросаПартнеры = СтрЗаменить(ТекстЗапросаПартнеры, ",&ТекстПоместить", ТекстПоместить);
			
			Запрос.УстановитьПараметр("ВидКонтактнойИнформацииПартнера",
			                           ?(ДанныеРассылки.ПредназначенаДляЭлектронныхПисем,
			                             ДанныеРассылки.ВидКонтактнойИнформацииПартнераДляПисем,
			                             ДанныеРассылки.ВидКонтактнойИнформацииПартнераДляSMS));
			
		КонецЕсли;
		
		Если ДанныеРассылки.ОтправлятьКонтактнымЛицамРоли Тогда
			
			ТекстЗапросаКонтактныеЛица ="
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	КонтактныеЛицаПартнеров.Ссылка КАК Получатель,
			|	ЕСТЬNULL(КонтактныеЛицаПартнеровКонтактнаяИнформация.Представление, """") КАК ПредставлениеКонтактнойИнформации,
			|	&ВидКонтактнойИнформацииКонтактногоЛица КАК ВидКонтактнойИнформации,
			|	КонтактныеЛицаПартнеров.Наименование КАК ПредставлениеПолучателя
			|	,&ТекстПоместить
			|ИЗ
			|	Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров.КонтактнаяИнформация КАК КонтактныеЛицаПартнеровКонтактнаяИнформация
			|		ПО (КонтактныеЛицаПартнеровКонтактнаяИнформация.Ссылка = КонтактныеЛицаПартнеров.Ссылка)
			|			И (КонтактныеЛицаПартнеровКонтактнаяИнформация.Вид = &ВидКонтактнойИнформацииКонтактногоЛица)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров.РолиКонтактногоЛица КАК КонтактныеЛицаПартнеровРолиКонтактногоЛица
			|		ПО КонтактныеЛицаПартнеров.Ссылка = КонтактныеЛицаПартнеровРолиКонтактногоЛица.Ссылка
			|			И (КонтактныеЛицаПартнеровРолиКонтактногоЛица.РольКонтактногоЛица = &РольКонтактногоЛица)
			|		И &СоединениеПоОтбору
			|ГДЕ
			|	НЕ КонтактныеЛицаПартнеров.ПометкаУдаления";
			
			Если ЕстьОтборПоКомпоновке Тогда
				ТекстСоединениеПоОтбору ="ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборПартнеров КАК ОтборПартнеров
				                         |ПО КонтактныеЛицаПартнеров.Владелец = ОтборПартнеров.Партнер";
			Иначе
				ТекстСоединениеПоОтбору = "";
			КонецЕсли;
			
			ТекстЗапросаКонтактныеЛица = СтрЗаменить(ТекстЗапросаКонтактныеЛица, "И &СоединениеПоОтбору", ТекстСоединениеПоОтбору);
			Если ДанныеРассылки.ОтправлятьПартнеру Тогда
				ТекстЗапросаКонтактныеЛица = СтрЗаменить(ТекстЗапросаКонтактныеЛица, ",&ТекстПоместить", "");
			Иначе
				ТекстЗапросаКонтактныеЛица = СтрЗаменить(ТекстЗапросаКонтактныеЛица, ",&ТекстПоместить", ТекстПоместить);
			КонецЕсли;
			
			Запрос.УстановитьПараметр("ВидКонтактнойИнформацииКонтактногоЛица",
			                          ?(ДанныеРассылки.ПредназначенаДляЭлектронныхПисем,
			                            ДанныеРассылки.ВидКонтактнойИнформацииКонтактногоЛицаДляПисем,
			                            ДанныеРассылки.ВидКонтактнойИнформацииКонтактногоЛицаДляSMS));
			Запрос.УстановитьПараметр("РольКонтактногоЛица", ДанныеРассылки.РольКонтактногоЛица);
			
		КонецЕсли;
		
		Если (НЕ ДанныеРассылки.ОтправлятьПартнеру) И (НЕ ДанныеРассылки.ОтправлятьКонтактнымЛицамРоли) Тогда
			Запрос.Текст = "";
		Иначе
			НеобходимТекстОбъединить = ДанныеРассылки.ОтправлятьПартнеру И ДанныеРассылки.ОтправлятьКонтактнымЛицамРоли;
			Запрос.Текст = Запрос.Текст + ТекстЗапросаПартнеры + ?(НеобходимТекстОбъединить, ТекстОбъединить, "") + ТекстЗапросаКонтактныеЛица;
		КонецЕсли;
		
	Иначе // По подпискам
		
		ТекстЗапросаПодписчики = "
		|ВЫБРАТЬ
		|	ПодпискиНаРассылкиИОповещенияКлиентам.Владелец КАК Получатель,
		|	ЕСТЬNULL(ПартнерыКонтактнаяИнформация.Представление, """") КАК ПредставлениеКонтактнойИнформации,
		|	ВЫБОР
		|		КОГДА &ПредназначенаДляПисем
		|			ТОГДА ПодпискиНаРассылкиИОповещенияКлиентам.ВидКИПартнераДляПисем
		|			ИНАЧЕ ПодпискиНаРассылкиИОповещенияКлиентам.ВидКИПартнераДляSMS
		|	КОНЕЦ КАК ВидКонтактнойИнформации,
		|	Партнеры.НаименованиеПолное КАК ПредставлениеПолучателя
		|ПОМЕСТИТЬ Адресаты
		|ИЗ
		|	Справочник.ПодпискиНаРассылкиИОповещенияКлиентам КАК ПодпискиНаРассылкиИОповещенияКлиентам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
		|		ПО ПодпискиНаРассылкиИОповещенияКлиентам.Владелец = Партнеры.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
		|		ПО ПодпискиНаРассылкиИОповещенияКлиентам.Владелец = ПартнерыКонтактнаяИнформация.Ссылка
		|			И (ВЫБОР
		|				КОГДА &ПредназначенаДляПисем
		|					ТОГДА ПодпискиНаРассылкиИОповещенияКлиентам.ВидКИПартнераДляПисем = ПартнерыКонтактнаяИнформация.Вид
		|				ИНАЧЕ ПодпискиНаРассылкиИОповещенияКлиентам.ВидКИПартнераДляSMS = ПартнерыКонтактнаяИнформация.Вид
		|			КОНЕЦ)
		|		И &СоединениеПоОтбору
		|ГДЕ
		|	ПодпискиНаРассылкиИОповещенияКлиентам.ПодпискаДействует
		|	И ПодпискиНаРассылкиИОповещенияКлиентам.ОтправлятьПартнеру
		|	И НЕ ПодпискиНаРассылкиИОповещенияКлиентам.ПометкаУдаления
		|	И ПодпискиНаРассылкиИОповещенияКлиентам.ГруппаРассылокИОповещений = &ГруппаРассылокИОповещений
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПодпискиНаРассылкиИОповещенияКлиентамКонтактныеЛица.КонтактноеЛицо,
		|	ЕСТЬNULL(КонтактныеЛицаПартнеровКонтактнаяИнформация.Представление, """"),
		|	ВЫБОР
		|		КОГДА &ПредназначенаДляПисем
		|		ТОГДА ПодпискиНаРассылкиИОповещенияКлиентамКонтактныеЛица.ВидКИДляПисем
		|			ИНАЧЕ ПодпискиНаРассылкиИОповещенияКлиентамКонтактныеЛица.ВидКИДляSMS
		|	КОНЕЦ КАК ВидКонтактнойИнформации,
		|	КонтактныеЛицаПартнеров.Наименование
		|ИЗ
		|	Справочник.ПодпискиНаРассылкиИОповещенияКлиентам.КонтактныеЛица КАК ПодпискиНаРассылкиИОповещенияКлиентамКонтактныеЛица
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПодпискиНаРассылкиИОповещенияКлиентам КАК ПодпискиНаРассылкиИОповещенияКлиентам
		|		ПО ПодпискиНаРассылкиИОповещенияКлиентамКонтактныеЛица.Ссылка = ПодпискиНаРассылкиИОповещенияКлиентам.Ссылка
		|		И &СоединениеПоОтбору
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров.КонтактнаяИнформация КАК КонтактныеЛицаПартнеровКонтактнаяИнформация
		|		ПО ПодпискиНаРассылкиИОповещенияКлиентамКонтактныеЛица.КонтактноеЛицо = КонтактныеЛицаПартнеровКонтактнаяИнформация.Ссылка
		|			И (ВЫБОР
		|				КОГДА &ПредназначенаДляПисем
		|					ТОГДА ПодпискиНаРассылкиИОповещенияКлиентамКонтактныеЛица.ВидКИДляПисем = КонтактныеЛицаПартнеровКонтактнаяИнформация.Вид
		|				ИНАЧЕ ПодпискиНаРассылкиИОповещенияКлиентамКонтактныеЛица.ВидКИДляSMS = КонтактныеЛицаПартнеровКонтактнаяИнформация.Вид
		|			КОНЕЦ)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
		|		ПО ПодпискиНаРассылкиИОповещенияКлиентамКонтактныеЛица.КонтактноеЛицо = КонтактныеЛицаПартнеров.Ссылка
		|ГДЕ
		|	НЕ ПодпискиНаРассылкиИОповещенияКлиентам.ПометкаУдаления
		|	И ПодпискиНаРассылкиИОповещенияКлиентам.ПодпискаДействует
		|	И ПодпискиНаРассылкиИОповещенияКлиентам.ГруппаРассылокИОповещений = &ГруппаРассылокИОповещений";
		
		Запрос.УстановитьПараметр("ГруппаРассылокИОповещений", ДанныеРассылки.ГруппаРассылокИОповещений);
		
		Если ЕстьОтборПоКомпоновке Тогда
			ТекстСоединениеПоОтбору ="ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборПартнеров КАК ОтборПартнеров
			                         |ПО ПодпискиНаРассылкиИОповещенияКлиентам.Владелец = ОтборПартнеров.Партнер";
		Иначе
			ТекстСоединениеПоОтбору = "";
		КонецЕсли;
		
		ТекстЗапросаПодписчики = СтрЗаменить(ТекстЗапросаПодписчики, "И &СоединениеПоОтбору", ТекстСоединениеПоОтбору);
		Запрос.Текст = Запрос.Текст + ТекстЗапросаПодписчики;
		
	КонецЕсли;
	
	ТекстЗапросаДополнительныеПолучатели = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РассылкаКлиентамДополнительныеПолучатели.Контакт КАК Получатель,
	|	РассылкаКлиентамДополнительныеПолучатели.КонтактнаяИнформация КАК ПредставлениеКонтактнойИнформации,
	|	Значение(Справочник.ВидыКонтактнойИнформации.ПустаяСсылка) КАК ВидКонтактнойИнформации,
	|	ВЫБОР
	|		КОГДА РассылкаКлиентамДополнительныеПолучатели.Контакт ССЫЛКА Справочник.Партнеры
	|			ТОГДА ЕСТЬNULL(Партнеры.НаименованиеПолное, """")
	|		ИНАЧЕ ЕСТЬNULL(КонтактныеЛицаПартнеров.Наименование, """")
	|	КОНЕЦ КАК ПредставлениеПолучателя
	|	,&ТекстПоместить
	|ИЗ
	|	Документ.РассылкаКлиентам.ДополнительныеПолучатели КАК РассылкаКлиентамДополнительныеПолучатели
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
	|		ПО РассылкаКлиентамДополнительныеПолучатели.Контакт = Партнеры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
	|		ПО РассылкаКлиентамДополнительныеПолучатели.Контакт = КонтактныеЛицаПартнеров.Ссылка
	|ГДЕ
	|	РассылкаКлиентамДополнительныеПолучатели.Ссылка = &РассылкаКлиентам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////";
	
	Если ПустаяСтрока(Запрос.Текст) Тогда
		ТекстЗапросаДополнительныеПолучатели = СтрЗаменить(ТекстЗапросаДополнительныеПолучатели, ",&ТекстПоместить", ТекстПоместить);
	Иначе
		ТекстЗапросаДополнительныеПолучатели = СтрЗаменить(ТекстЗапросаДополнительныеПолучатели, ",&ТекстПоместить", "");
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + ?(ПустаяСтрока(Запрос.Текст), "", ТекстОбъединить) + ТекстЗапросаДополнительныеПолучатели;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьТекстЗапросаАдресатыРассылкиНазначениеОпросов(Данные, ДанныеОснования, Запрос, ЕстьОтборПоКомпоновке)
	
	Если Данные.Принудительная Тогда
		
		Если ТипЗнч(ДанныеОснования.ТипРеспондентов) = Тип("СправочникСсылка.Партнеры") И Данные.ОтправлятьПартнеру Тогда
			
			Если ДанныеОснования.СвободныйОпрос Тогда
				
				ТекстЗапросаРеспонденты = "
				|ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
				|	Партнеры.Ссылка КАК Получатель,
				|	ЕСТЬNULL(ПартнерыКонтактнаяИнформация.Представление, """") КАК ПредставлениеКонтактнойИнформации,
				|	&ВидКонтактнойИнформацииПартнера КАК ВидКонтактнойИнформации,
				|	Партнеры.НаименованиеПолное КАК ПредставлениеПолучателя
				|ПОМЕСТИТЬ Адресаты
				|ИЗ
				|	Справочник.Партнеры КАК Партнеры 
				|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
				|		ПО ПартнерыКонтактнаяИнформация.Ссылка = Партнеры.Ссылка
				|			И (ПартнерыКонтактнаяИнформация.Вид = &ВидКонтактнойИнформацииПартнера)
				|			И (НЕ Партнеры.ПометкаУдаления)
				|		И &СоединениеПоОтбору";
				
			Иначе
				
				ТекстЗапросаРеспонденты = "
				|ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
				|	НазначениеОпросовРеспонденты.Респондент КАК Получатель,
				|	ЕСТЬNULL(ПартнерыКонтактнаяИнформация.Представление, """") КАК ПредставлениеКонтактнойИнформации,
				|	&ВидКонтактнойИнформацииПартнера КАК ВидКонтактнойИнформации,
				|	Партнеры.НаименованиеПолное КАК ПредставлениеПолучателя
				|ПОМЕСТИТЬ Адресаты
				|ИЗ
				|	Документ.НазначениеОпросов.Респонденты КАК НазначениеОпросовРеспонденты
				|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
				|		ПО НазначениеОпросовРеспонденты.Респондент = ПартнерыКонтактнаяИнформация.Ссылка
				|			И (ПартнерыКонтактнаяИнформация.Вид = &ВидКонтактнойИнформацииПартнера)
				|		И &СоединениеПоОтбору
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
				|		ПО НазначениеОпросовРеспонденты.Респондент = Партнеры.Ссылка
				|ГДЕ
				|	НазначениеОпросовРеспонденты.Ссылка = &НазначениеОпросов"
				
			КонецЕсли;
			
			РассылкиИОповещенияКлиентам.ОбработатьТекстЗапросаПоПартнерамРассылки(Запрос, ТекстЗапросаРеспонденты, Данные, ЕстьОтборПоКомпоновке);
			
		ИначеЕсли ТипЗнч(ДанныеОснования.ТипРеспондентов) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") И Данные.ОтправлятьКонтактномуЛицуОбъектаОповещения Тогда
			
			Если ДанныеОснования.СвободныйОпрос Тогда
				
				ТекстЗапросаРеспонденты ="
				|ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
				|	КонтактныеЛицаПартнеров.Ссылка КАК Получатель,
				|	ЕСТЬNULL(КонтактныеЛицаПартнеровКонтактнаяИнформация.Представление, """") КАК ПредставлениеКонтактнойИнформации,
				|	&ВидКонтактнойИнформацииКонтактногоЛица КАК ВидКонтактнойИнформации,
				|	КонтактныеЛицаПартнеров.Наименование КАК ПредставлениеПолучателя
				|ПОМЕСТИТЬ Адресаты
				|ИЗ
				|	Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
				|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров.КонтактнаяИнформация КАК КонтактныеЛицаПартнеровКонтактнаяИнформация
				|		ПО КонтактныеЛицаПартнеровКонтактнаяИнформация.Ссылка = КонтактныеЛицаПартнеров.Ссылка
				|			И (КонтактныеЛицаПартнеровКонтактнаяИнформация.Вид = &ВидКонтактнойИнформацииКонтактногоЛица)
				|			И (НЕ КонтактныеЛицаПартнеров.ПометкаУдаления)
				|		И &СоединениеПоОтбору
				|";
				
			Иначе
				
				ТекстЗапросаРеспонденты = "
				|ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
				|	НазначениеОпросовРеспонденты.Респондент КАК Получатель,
				|	ЕСТЬNULL(КонтактныеЛицаПартнеровКонтактнаяИнформация.Представление, """") КАК ПредставлениеКонтактнойИнформации,
				|	&ВидКонтактнойИнформацииКонтактногоЛица КАК ВидКонтактнойИнформации,
				|	КонтактныеЛицаПартнеров.Наименование КАК ПредставлениеПолучателя
				|ПОМЕСТИТЬ Адресаты
				|ИЗ
				|	Документ.НазначениеОпросов.Респонденты КАК НазначениеОпросовРеспонденты
				|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров.КонтактнаяИнформация КАК КонтактныеЛицаПартнеровКонтактнаяИнформация
				|		ПО НазначениеОпросовРеспонденты.Респондент = КонтактныеЛицаПартнеровКонтактнаяИнформация.Ссылка
				|			И (КонтактныеЛицаПартнеровКонтактнаяИнформация.Вид = &ВидКонтактнойИнформацииКонтактногоЛица)
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
				|		ПО НазначениеОпросовРеспонденты.Респондент = КонтактныеЛицаПартнеров.Ссылка
				|		И &СоединениеПоОтбору
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
				|		ПО КонтактныеЛицаПартнеров.Владелец = Партнеры.Ссылка
				|ГДЕ
				|	НазначениеОпросовРеспонденты.Ссылка = &НазначениеОпросов";
				
			КонецЕсли;
			
			РассылкиИОповещенияКлиентам.ОбработатьТекстЗапросаПоКонтактнымЛицамРассылки(Запрос, ТекстЗапросаРеспонденты, Данные, ЕстьОтборПоКомпоновке);
			
		КонецЕсли;
		
	Иначе
		
		Запрос.УстановитьПараметр("ГруппаРассылокИОповещений", Данные.ГруппаРассылокИОповещений);
		
		Если ТипЗнч(ДанныеОснования.ТипРеспондентов) = Тип("СправочникСсылка.Партнеры")  Тогда
			
			Если ДанныеОснования.СвободныйОпрос Тогда
				
				ТекстЗапросаРеспонденты = "
				|ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
				|	ПодпискиНаРассылкиИОповещенияКлиентам.Владелец КАК Получатель,
				|	ЕСТЬNULL(ПартнерыКонтактнаяИнформация.Представление, """") КАК ПредставлениеКонтактнойИнформации,
				|	ВЫБОР
				|		КОГДА &ПредназначенаДляПисем
				|			ТОГДА ПодпискиНаРассылкиИОповещенияКлиентам.ВидКИПартнераДляПисем 
				|			ИНАЧЕ ПодпискиНаРассылкиИОповещенияКлиентам.ВидКИПартнераДляSMS 
				|	КОНЕЦ КАК ВидКонтактнойИнформации,
				|	Партнеры.НаименованиеПолное КАК ПредставлениеПолучателя
				|ПОМЕСТИТЬ Адресаты
				|ИЗ
				|	Справочник.ПодпискиНаРассылкиИОповещенияКлиентам КАК ПодпискиНаРассылкиИОповещенияКлиентам
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
				|		ПО ПодпискиНаРассылкиИОповещенияКлиентам.Владелец = Партнеры.Ссылка
				|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
				|		ПО ПодпискиНаРассылкиИОповещенияКлиентам.Владелец = ПартнерыКонтактнаяИнформация.Ссылка
				|			И (ВЫБОР
				|				КОГДА &ПредназначенаДляПисем
				|					ТОГДА ПодпискиНаРассылкиИОповещенияКлиентам.ВидКИПартнераДляПисем = ПартнерыКонтактнаяИнформация.Вид
				|				ИНАЧЕ ПодпискиНаРассылкиИОповещенияКлиентам.ВидКИПартнераДляSMS = ПартнерыКонтактнаяИнформация.Вид
				|			КОНЕЦ)
				|		И &СоединениеПоОтбору
				|ГДЕ
				|	ПодпискиНаРассылкиИОповещенияКлиентам.ПодпискаДействует
				|	И ПодпискиНаРассылкиИОповещенияКлиентам.ОтправлятьПартнеру
				|	И НЕ ПодпискиНаРассылкиИОповещенияКлиентам.ПометкаУдаления
				|	И ПодпискиНаРассылкиИОповещенияКлиентам.ГруппаРассылокИОповещений = &ГруппаРассылокИОповещений";
				
			Иначе
				
				ТекстЗапросаРеспонденты = "
				|ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
				|	НазначениеОпросовРеспонденты.Респондент КАК Получатель,
				|	ЕСТЬNULL(ПартнерыКонтактнаяИнформация.Представление, """") КАК ПредставлениеКонтактнойИнформации,
				|	ВЫБОР
				|		КОГДА &ПредназначенаДляПисем
				|			ТОГДА ПодпискиНаРассылкиИОповещенияКлиентам.ВидКИПартнераДляПисем 
				|			ИНАЧЕ ПодпискиНаРассылкиИОповещенияКлиентам.ВидКИПартнераДляSMS 
				|	КОНЕЦ КАК ВидКонтактнойИнформации,
				|	Партнеры.НаименованиеПолное КАК ПредставлениеПолучателя
				|ПОМЕСТИТЬ Адресаты
				|ИЗ
				|	Документ.НазначениеОпросов.Респонденты КАК НазначениеОпросовРеспонденты
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПодпискиНаРассылкиИОповещенияКлиентам КАК ПодпискиНаРассылкиИОповещенияКлиентам
				|			ПО НазначениеОпросовРеспонденты.Респондент = ПодпискиНаРассылкиИОповещенияКлиентам.Владелец
				|				И ПодпискиНаРассылкиИОповещенияКлиентам.ПодпискаДействует
				|				И ПодпискиНаРассылкиИОповещенияКлиентам.ОтправлятьПартнеру
				|				И НЕ ПодпискиНаРассылкиИОповещенияКлиентам.ПометкаУдаления
				|				И ПодпискиНаРассылкиИОповещенияКлиентам.ГруппаРассылокИОповещений = &ГруппаРассылокИОповещений			
				|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
				|		ПО НазначениеОпросовРеспонденты.Респондент = ПартнерыКонтактнаяИнформация.Ссылка
				|			И (ВЫБОР
				|				КОГДА &ПредназначенаДляПисем
				|					ТОГДА ПодпискиНаРассылкиИОповещенияКлиентам.ВидКИПартнераДляПисем = ПартнерыКонтактнаяИнформация.Вид
				|				ИНАЧЕ ПодпискиНаРассылкиИОповещенияКлиентам.ВидКИПартнераДляSMS = ПартнерыКонтактнаяИнформация.Вид
				|			КОНЕЦ)
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
				|		ПО НазначениеОпросовРеспонденты.Респондент = Партнеры.Ссылка
				|		И &СоединениеПоОтбору
				|ГДЕ
				|	НазначениеОпросовРеспонденты.Ссылка = &НазначениеОпросов"
				
			КонецЕсли;
			
			РассылкиИОповещенияКлиентам.ОбработатьТекстЗапросаПоПартнерамРассылки(Запрос, ТекстЗапросаРеспонденты, Данные, ЕстьОтборПоКомпоновке);
			
		ИначеЕсли ТипЗнч(ДанныеОснования.ТипРеспондентов) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
			
			Если ДанныеОснования.СвободныйОпрос Тогда
				
				ТекстЗапросаРеспонденты = "
				|ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
				|	КонтактныеЛицаПартнеров.Ссылка КАК Получатель,
				|	ЕСТЬNULL(КонтактныеЛицаПартнеровКонтактнаяИнформация.Представление, """") КАК ПредставлениеКонтактнойИнформации,
				|	ВЫБОР
				|		КОГДА &ПредназначенаДляПисем
				|			ТОГДА ПодпискиНаРассылкиИОповещенияКлиентам.ВидКИКонтактногоЛицаОбъектаОповещенияДляПисем
				|			ИНАЧЕ ПодпискиНаРассылкиИОповещенияКлиентам.ВидКИКонтактногоЛицаОбъектаОповещенияДляSMS
				|	КОНЕЦ КАК ВидКонтактнойИнформации,
				|	КонтактныеЛицаПартнеров.Наименование КАК ПредставлениеПолучателя
				|ПОМЕСТИТЬ Адресаты
				|ИЗ
				|	Справочник.ПодпискиНаРассылкиИОповещенияКлиентам КАК ПодпискиНаРассылкиИОповещенияКлиентам
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК СправочникПартнеры
				|		ПО ПодпискиНаРассылкиИОповещенияКлиентам.Владелец = СправочникПартнеры.Ссылка
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
				|		ПО (СправочникПартнеры.Ссылка = КонтактныеЛицаПартнеров.Владелец)
				|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров.КонтактнаяИнформация КАК КонтактныеЛицаПартнеровКонтактнаяИнформация
				|		ПО (КонтактныеЛицаПартнеров.Ссылка = КонтактныеЛицаПартнеровКонтактнаяИнформация.Ссылка)
				|			И (ВЫБОР
				|				КОГДА &ПредназначенаДляПисем
				|					ТОГДА ПодпискиНаРассылкиИОповещенияКлиентам.ВидКИКонтактногоЛицаОбъектаОповещенияДляПисем = КонтактныеЛицаПартнеровКонтактнаяИнформация.Вид
				|				ИНАЧЕ ПодпискиНаРассылкиИОповещенияКлиентам.ВидКИКонтактногоЛицаОбъектаОповещенияДляSMS = КонтактныеЛицаПартнеровКонтактнаяИнформация.Вид
				|			КОНЕЦ)
				|		И &СоединениеПоОтбору
				|ГДЕ
				|	НЕ ПодпискиНаРассылкиИОповещенияКлиентам.ПометкаУдаления
				|	И ПодпискиНаРассылкиИОповещенияКлиентам.ПодпискаДействует
				|	И ПодпискиНаРассылкиИОповещенияКлиентам.ОтправлятьКонтактномуЛицуОбъектаОповещения
				|	И ПодпискиНаРассылкиИОповещенияКлиентам.ГруппаРассылокИОповещений = &ГруппаРассылокИОповещений";

			Иначе
				
				ТекстЗапросаРеспонденты = "
				|ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
				|	НазначениеОпросовРеспонденты.Респондент КАК Получатель,
				|	ЕСТЬNULL(КонтактныеЛицаПартнеровКонтактнаяИнформация.Представление, """") КАК ПредставлениеКонтактнойИнформации,
				|	ВЫБОР
				|		КОГДА &ПредназначенаДляПисем
				|			ТОГДА ПодпискиНаРассылкиИОповещенияКлиентам.ВидКИКонтактногоЛицаОбъектаОповещенияДляПисем
				|			ИНАЧЕ ПодпискиНаРассылкиИОповещенияКлиентам.ВидКИКонтактногоЛицаОбъектаОповещенияДляSMS
				|	КОНЕЦ КАК ВидКонтактнойИнформации,
				|	КонтактныеЛицаПартнеров.Наименование КАК ПредставлениеПолучателя
				|ПОМЕСТИТЬ Адресаты
				|ИЗ
				|	Документ.НазначениеОпросов.Респонденты КАК НазначениеОпросовРеспонденты
				|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
				|		ПО НазначениеОпросовРеспонденты.Респондент = КонтактныеЛицаПартнеров.Ссылка
				|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
				|		ПО КонтактныеЛицаПартнеров.Владелец = Партнеры.Ссылка
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПодпискиНаРассылкиИОповещенияКлиентам КАК ПодпискиНаРассылкиИОповещенияКлиентам
				|			ПО Партнеры.Ссылка = ПодпискиНаРассылкиИОповещенияКлиентам.Владелец
				|				И ПодпискиНаРассылкиИОповещенияКлиентам.ПодпискаДействует
				|				И ПодпискиНаРассылкиИОповещенияКлиентам.ОтправлятьКонтактномуЛицуОбъектаОповещения
				|				И НЕ ПодпискиНаРассылкиИОповещенияКлиентам.ПометкаУдаления
				|				И ПодпискиНаРассылкиИОповещенияКлиентам.ГруппаРассылокИОповещений = &ГруппаРассылокИОповещений
				|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров.КонтактнаяИнформация КАК КонтактныеЛицаПартнеровКонтактнаяИнформация
				|		ПО КонтактныеЛицаПартнеров.Ссылка = КонтактныеЛицаПартнеровКонтактнаяИнформация.Ссылка
				|			И (ВЫБОР
				|				КОГДА &ПредназначенаДляПисем
				|					ТОГДА ПодпискиНаРассылкиИОповещенияКлиентам.ВидКИКонтактногоЛицаОбъектаОповещенияДляПисем = КонтактныеЛицаПартнеровКонтактнаяИнформация.Вид
				|				ИНАЧЕ ПодпискиНаРассылкиИОповещенияКлиентам.ВидКИКонтактногоЛицаОбъектаОповещенияДляSMS = КонтактныеЛицаПартнеровКонтактнаяИнформация.Вид
				|			КОНЕЦ)
				|		И &СоединениеПоОтбору
				|ГДЕ
				|	НазначениеОпросовРеспонденты.Ссылка = &НазначениеОпросов"
				
			КонецЕсли;
			
			РассылкиИОповещенияКлиентам.ОбработатьТекстЗапросаПоКонтактнымЛицамРассылки(Запрос, ТекстЗапросаРеспонденты, Данные, ЕстьОтборПоКомпоновке);
		
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + ТекстЗапросаРеспонденты;
	Запрос.Текст = Запрос.Текст + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////";
	
	Запрос.УстановитьПараметр("НазначениеОпросов", ДанныеОснования.Ссылка); 
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчет()
	
	ДанныеРассылки = ДанныеРассылкиКлиентам(Рассылка);
	
	ТаблицаОтчета.Очистить();
	ТаблицаОтчета.АвтоМасштаб = Истина;
	Макет = Документы.РассылкаКлиентам.ПолучитьМакет("СостояниеРассылки");
	
	ОбластьПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	ТаблицаОтчета.Вывести(ОбластьПустаяСтрока);
	
	ТаблицаОтчета.НачатьГруппуСтрок("Легенда");
	
	// Вывод легенды
	ОбластьЛегенда = Макет.ПолучитьОбласть("СтрокаЛегенда");
	ОбластьЛегенда.Параметры.ТекстЛегенды = ТекстЛегенды(ДанныеРассылки);
	ТаблицаОтчета.Вывести(ОбластьЛегенда);
	
	ТаблицаОтчета.ЗакончитьГруппуСтрок();
	
	// Вывод шапки
	ОбластьШапка = Макет.ПолучитьОбласть("СтрокаШапка");
	Если ДанныеРассылки.ПредназначенаДляЭлектронныхПисем Тогда
		ОбластьШапка.Параметры.КонтактнаяИнформация = НСтр("ru = 'Адреса электронной почты'");
	Иначе
		ОбластьШапка.Параметры.КонтактнаяИнформация = НСтр("ru = 'Телефоны'");
	КонецЕсли;
	
	ТаблицаОтчета.Вывести(ОбластьШапка);
	
	Если ТипЗнч(ДанныеРассылки.Основание) = Тип("ДокументСсылка.НазначениеОпросов") И ЗначениеЗаполнено(ДанныеРассылки.Основание) Тогда
		ДанныеОснования = РассылкиИОповещенияКлиентам.ДанныеОснованияНазначениеОпросов(ДанныеРассылки.Основание);
		Если ДанныеОснования.ТипРеспондентов = Справочники.Пользователи.ПустаяСсылка() Тогда
			ВызватьИсключение НСтр("ru = 'В ""Назначении опросов"" указан некорректный тип респондентов. Для пользователей не может использоваться рассылка клиентам.'");
		КонецЕсли
	Иначе
		ДанныеОснования = Неопределено;
	КонецЕсли;
	
	// Формирование запроса
	УстановленОтборПоОснованию = Ложь;
	Запрос = РассылкиИОповещенияКлиентам.ЗапросОтборПоКомпоновке(ДанныеРассылки, УстановленОтборПоОснованию);
	ЕстьОтборПоКомпоновке = Не ПустаяСтрока(Запрос.Текст);
	
	Если ДанныеОснования <> Неопределено И УстановленОтборПоОснованию Тогда
		
		Если ТипЗнч(ДанныеОснования.Ссылка) = Тип("ДокументСсылка.НазначениеОпросов") Тогда
			
			ОбработатьТекстЗапросаАдресатыРассылкиНазначениеОпросов(ДанныеРассылки, ДанныеОснования, Запрос, ЕстьОтборПоКомпоновке);
			
		КонецЕсли;
		
	Иначе
		
		ОбработатьТекстЗапросаАдресатыРассылки(ДанныеРассылки, Запрос, ЕстьОтборПоКомпоновке);
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПредназначенаДляПисем", ДанныеРассылки.ПредназначенаДляЭлектронныхПисем);
	Запрос.УстановитьПараметр("РассылкаКлиентам", ДанныеРассылки.Рассылка);
	
	Если ДанныеРассылки.ПредназначенаДляЭлектронныхПисем Тогда
		
		ТекстЗапросаДокументыВзаимодействий = "
		|ВЫБРАТЬ
		|	Адресаты.Получатель КАК Получатель,
		|	Адресаты.ПредставлениеКонтактнойИнформации,
		|	Адресаты.ВидКонтактнойИнформации,
		|	Адресаты.ПредставлениеПолучателя,
		|	ЕСТЬNULL(КонтактыВзаимодействий.Взаимодействие, НЕОПРЕДЕЛЕНО) КАК Взаимодействие,
		|	ЕСТЬNULL(Взаимодействия.СтатусИсходящегоПисьма, НЕОПРЕДЕЛЕНО) КАК Состояние,
		|	ЕСТЬNULL(Взаимодействия.Дата, НЕОПРЕДЕЛЕНО) КАК ДатаДокумента,
		|	ЕСТЬNULL(Взаимодействия.Номер, НЕОПРЕДЕЛЕНО) КАК НомерДокумента
		|ИЗ
		|	Адресаты КАК Адресаты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактыВзаимодействий КАК КонтактыВзаимодействий
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЖурналДокументов.Взаимодействия КАК Взаимодействия
		|			ПО КонтактыВзаимодействий.Взаимодействие = Взаимодействия.Ссылка
		|				И (Взаимодействия.ВзаимодействиеОснование = &РассылкаКлиентам)
		|				И (НЕ Взаимодействия.ПометкаУдаления)
		|		ПО Адресаты.Получатель = КонтактыВзаимодействий.Контакт
		|ИТОГИ ПО
		|	Получатель";
		
	Иначе
		
		ТекстЗапросаДокументыВзаимодействий = "
		|ВЫБРАТЬ
		|	АдресатыРассылки.Получатель КАК Получатель,
		|	АдресатыРассылки.ПредставлениеКонтактнойИнформации,
		|	АдресатыРассылки.ВидКонтактнойИнформации,
		|	АдресатыРассылки.ПредставлениеПолучателя,
		|	ЕСТЬNULL(СообщениеSMSАдресаты.Ссылка, НЕОПРЕДЕЛЕНО) КАК Взаимодействие,
		|	ЕСТЬNULL(СообщениеSMSАдресаты.СостояниеСообщения, НЕОПРЕДЕЛЕНО) КАК Состояние,
		|	ЕСТЬNULL(СообщениеSMSАдресаты.Ссылка.Дата, НЕОПРЕДЕЛЕНО) КАК ДатаДокумента,
		|	ЕСТЬNULL(СообщениеSMSАдресаты.Ссылка.Номер, НЕОПРЕДЕЛЕНО) КАК НомерДокумента
		|ИЗ
		|	Адресаты КАК АдресатыРассылки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СообщениеSMS.Адресаты КАК СообщениеSMSАдресаты
		|		ПО АдресатыРассылки.Получатель = СообщениеSMSАдресаты.Контакт
		|			И АдресатыРассылки.ПредставлениеКонтактнойИнформации = СообщениеSMSАдресаты.КакСвязаться
		|			И СообщениеSMSАдресаты.Ссылка.ВзаимодействиеОснование = &РассылкаКлиентам
		|ИТОГИ ПО
		|	Получатель";
		
	КонецЕсли;
		
	Запрос.Текст = Запрос.Текст + ТекстЗапросаДокументыВзаимодействий;
		
	ВыборкаАдресат = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	НомерСтроки = 1;
	
	МассивКИ      = Новый Массив;
	МассивВидовКИ = Новый Массив;
	ОбластьПодписчик = Макет.ПолучитьОбласть("СтрокаПодписчик");

	Пока ВыборкаАдресат.Следующий() Цикл
		
		МассивКИ.Очистить();
		МассивВидовКИ.Очистить();
		
		ВыборкаДетали = ВыборкаАдресат.Выбрать();
		Пока ВыборкаДетали.Следующий() Цикл
			
			РассылкиИОповещенияКлиентам.ДобавитьВМассив(МассивКИ, ВыборкаДетали.ПредставлениеКонтактнойИнформации);
			РассылкиИОповещенияКлиентам.ДобавитьВМассив(МассивВидовКИ, ВыборкаДетали.ВидКонтактнойИнформации);
			Взаимодействие = ?(ЗначениеЗаполнено(Взаимодействие), Взаимодействие,ВыборкаДетали.Взаимодействие);
			ДатаДокумента  = ?(ЗначениеЗаполнено(ДатаДокумента), ДатаДокумента, ВыборкаДетали.ДатаДокумента);
			НомерДокумента = ?(ЗначениеЗаполнено(НомерДокумента), НомерДокумента, ВыборкаДетали.НомерДокумента);
			Состояние      = ВыборкаДетали.Состояние;
			
		КонецЦикла;
		
		ОбластьПодписчик.Параметры.НомерСтроки                 = НомерСтроки;
		ОбластьПодписчик.Параметры.Подписчик                   = ВыборкаАдресат.Получатель;
		ОбластьПодписчик.Параметры.ВидКонтактнойИнформации     = РассылкиИОповещенияКлиентам.СтрокаАдресовИзМассива(МассивВидовКИ, Символы.ПС);;
		ОбластьПодписчик.Параметры.ДокументВзаимодействий      = Взаимодействие;
		ОбластьПодписчик.Параметры.ПредставлениеВзаимодействия = ПредставлениеВзаимодействия(Взаимодействие, НомерДокумента, ДатаДокумента);
		ОбластьПодписчик.Параметры.Состояние                   = Состояние;
		
		КонтактнаяИнформация  = РассылкиИОповещенияКлиентам.СтрокаАдресовИзМассива(МассивКИ, Символы.ПС);
		Если ПустаяСтрока(КонтактнаяИнформация) Тогда
			ОбластьПодписчик.Области.ПодписчикКонтактнаяИнформация["ЦветТекста"] = ЦветаСтиля.ПоясняющийОшибкуТекст;
			КонтактнаяИнформация = НСтр("ru = 'не указан'");
		Иначе
			ОбластьПодписчик.Области.ПодписчикКонтактнаяИнформация["ЦветТекста"] = ОбластьПодписчик.Области.Подписчик["ЦветТекста"];
		КонецЕсли;
		
		ОбластьПодписчик.Параметры.КонтактнаяИнформация    = КонтактнаяИнформация;
		ТаблицаОтчета.Вывести(ОбластьПодписчик);
		
		НомерСтроки    = НомерСтроки + 1;
		Взаимодействие = Неопределено;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеРассылкиКлиентам(РассылкаКлиентам)

	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	РассылкаКлиентам.Ссылка КАК Рассылка,
	|	РассылкаКлиентам.ДатаРассылки,
	|	РассылкаКлиентам.ДатаАктуальности,
	|	РассылкаКлиентам.Основание,
	|	РассылкаКлиентам.ГруппаРассылокИОповещений,
	|	РассылкаКлиентам.ПредназначенаДляЭлектронныхПисем,
	|	РассылкаКлиентам.ПредназначенаДляSMS,
	|	РассылкаКлиентам.ОтборАдресатов,
	|	РассылкаКлиентам.Статус,
	|	РассылкаКлиентам.Ответственный,
	|	ГруппыРассылокИОповещений.ВидКонтактнойИнформацииПартнераДляПисем,
	|	ГруппыРассылокИОповещений.ВидКонтактнойИнформацииПартнераДляSMS,
	|	ГруппыРассылокИОповещений.ВидКонтактнойИнформацииКонтактногоЛицаДляПисем,
	|	ГруппыРассылокИОповещений.ВидКонтактнойИнформацииКонтактногоЛицаДляSMS,
	|	ГруппыРассылокИОповещений.ОтправлятьПартнеру,
	|	ГруппыРассылокИОповещений.ОтправлятьКонтактнымЛицамРоли,
	|	ГруппыРассылокИОповещений.РольКонтактногоЛица,
	|	ГруппыРассылокИОповещений.Принудительная,
	|	ГруппыРассылокИОповещений.ОтправлятьКонтактномуЛицуОбъектаОповещения
	|ИЗ
	|	Документ.РассылкаКлиентам КАК РассылкаКлиентам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыРассылокИОповещений КАК ГруппыРассылокИОповещений
	|		ПО РассылкаКлиентам.ГруппаРассылокИОповещений = ГруппыРассылокИОповещений.Ссылка
	|ГДЕ
	|	РассылкаКлиентам.Ссылка = &Рассылка";
	
	Запрос.УстановитьПараметр("Рассылка", РассылкаКлиентам);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Возврат Выборка;

КонецФункции

&НаСервереБезКонтекста
Функция ПредставлениеВзаимодействия(Взаимодействие, НомерДокумента, ДатаДокумента)
	
	Если Взаимодействие = Неопределено Тогда
		Возврат "";
	КонецЕсли;

	Представление = НСтр("ru = '%1 № %2 от %3.'");
	Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	                Представление,
	                ?(ТипЗнч(Взаимодействие) = Тип("ДокументСсылка.СообщениеSMS"), 
	                                                 НСтр("ru = 'Сообщение SMS'"),
	                                                 НСтр("ru = 'Исходящее письмо'")),
	                ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(НомерДокумента),
	                Формат(ДатаДокумента, "ДЛФ=DDT"));
	
	Возврат Представление;

КонецФункции

&НаСервереБезКонтекста
Функция ТекстЛегенды(ДанныеРассылки)

	ТекстЛегенды = "";
	
	Если ДанныеРассылки.Статус = Перечисления.СтатусыРассылокКлиентам.Черновик Тогда
		
		ТекстЛегенды = НСтр("ru = 'Рассылка находится в статусе ""Черновик"". В отчет выведены возможные адресаты рассылки и их контактная информация.
		                           |В случае если контактная информация не указана или указана в некорректном формате, рассылка данным адресатам 
		                           |выполнена не будет. Вы может изменить необходимую контактную информацию, выполнив ""двойной клик"" на получателе рассылки.'");
		
	ИначеЕсли ДанныеРассылки.Статус = Перечисления.СтатусыРассылокКлиентам.КОтправке Тогда
		
		ТекстЛегенды = НСтр("ru = 'Рассылка находится в статусе ""К отправке"" и запланирована %1
		                          |В отчет выведены возможные адресаты рассылки и их контактная информация.
		                          |В случае если контактная информация не указана или указана в некорректном формате, рассылка данным адресатам 
		                          |выполнена не будет. Вы еще можете изменить  контактную информацию необходимого вида, выполнив ""двойной клик"" на получателе рассылки.'");
		
		Если ДанныеРассылки.ДатаАктуальности = Дата(1, 1, 1) Тогда
			ТекстПериод = НСтр("ru = 'на'") + " " + Формат(ДанныеРассылки.ДатаРассылки,"ДЛФ=DD");
		Иначе
			
			Если ДанныеРассылки.ДатаАктуальности < ТекущаяДатаСеанса() Тогда
				ТекстЛегенды = НСтр("ru = 'Рассылка находится в статусе ""К отправке"" и была запланирована %1
				                  |Дата актуальности рассылки истекла и выполнена не будет.
				                  |В отчет выведены возможные адресаты рассылки и их контактная информация.'");
			КонецЕсли;
			
			ТекстПериод = НСтр("ru = 'на период с %1 по %2'");
			ТекстПериод = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПериод, 
		                                                                          Формат(ДанныеРассылки.ДатаРассылки,"ДЛФ=DD"),
		                                                                          Формат(ДанныеРассылки.ДатаАктуальности,"ДЛФ=DD"));
		КонецЕсли;
		
		ТекстЛегенды = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЛегенды, ТекстПериод);
		
	ИначеЕсли ДанныеРассылки.Статус = Перечисления.СтатусыРассылокКлиентам.Обрабатывается Тогда
		
		ТекстЛегенды = НСтр("ru = 'Рассылка находится в статусе ""Обрабатывается"". В данный момент создаются сообщения адресатам рассылки.
		                           |В отчет выведены возможные адресаты рассылки и их контактная информация. В случае если контактная информация не
		                           |указана или указана в некорректном формате, сообщения данным адресатам созданы не будут.'");
		
	ИначеЕсли ДанныеРассылки.Статус = Перечисления.СтатусыРассылокКлиентам.Выполнена Тогда
		
		ТекстЛегенды = НСтр("ru = 'Рассылка находится в статусе ""Выполнена"". Все возможные сообщения созданы.
		                           |Сообщения не создавались если контактная информация была не указана или указана в некорректном формате.
		                           |Созданные сообщения могут отсутствовать в данном отчете, если истек срок хранения данных сообщений.'");
		
	КонецЕсли;
	
	Возврат ТекстЛегенды;

КонецФункции 

#КонецОбласти


