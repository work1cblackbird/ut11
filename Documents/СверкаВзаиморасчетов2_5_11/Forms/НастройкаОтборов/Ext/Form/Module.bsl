
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ЗаголовокКнопкиПоУмолчанию") Тогда
		Элементы.ФормаУстановитьОтбор.Заголовок = Параметры.ЗаголовокКнопкиПоУмолчанию;
	КонецЕсли;
	
	Документы.СверкаВзаиморасчетов2_5_11.ИнициализироватьКомпоновщикНастроек(КомпоновщикОтбор, УникальныйИдентификатор, Параметры.НастройкиОтбора);
	
	Если Параметры.Свойство("СкрыватьСлужебныеОтборы") И Параметры.СкрыватьСлужебныеОтборы Тогда
		
		СлужебныеПоля = Новый Массив;
		СлужебныеПоля.Добавить(Новый ПолеКомпоновкиДанных("Организация"));
		СлужебныеПоля.Добавить(Новый ПолеКомпоновкиДанных("Контрагент"));
		Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
			СлужебныеПоля.Добавить(Новый ПолеКомпоновкиДанных("Партнер"));
		КонецЕсли;
		Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами")
			И НЕ ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСПоставщиками") Тогда
			СлужебныеПоля.Добавить(Новый ПолеКомпоновкиДанных("Договор"));
			СлужебныеПоля.Добавить(Новый ПолеКомпоновкиДанных("Договор.Статус"));
		КонецЕсли;
		
		Для каждого СтрокаОтбора Из КомпоновщикОтбор.Настройки.Отбор.Элементы Цикл
			Если ТипЗнч(СтрокаОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
				Продолжить;
			КонецЕсли;
			ЭтоСлужебнаяСтрокаОтбора = СлужебныеПоля.Найти(СтрокаОтбора.ЛевоеЗначение) <> Неопределено;
			Если ЭтоСлужебнаяСтрокаОтбора Тогда
				СтрокаОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьОтбор(Команда)
	
	Результат = Новый Структура;
	Результат.Вставить("НастройкиОтбора", ПолучитьНастройкиОтбора());
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменаУстановки(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьНастройкиОтбора()
	
	Возврат КомпоновщикОтбор.ПолучитьНастройки();
	
КонецФункции

#КонецОбласти