
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Печать") Тогда
		Элементы.Сохранить.Заголовок = НСтр("ru = 'Печать'");
	КонецЕсли;
	
	Если Параметры.Свойство("НастройкиПечати") Тогда
		НастройкиПечати = Параметры.НастройкиПечати;
		
	Иначе
		НастройкиПечати = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
								Документы.СверкаВзаиморасчетов2_5_11.КлючОбъектаПользовательскихНастроек(),
								"НастройкиПечати");
		Если НастройкиПечати = Неопределено Тогда
			НастройкиПечати = НастройкиПечатиПоУмолчанию();
		КонецЕсли;
		
	КонецЕсли;
	Если НЕ НастройкиПечати.Свойство("ОбъединитьКлиентовПоставщиков") Тогда
		НастройкиПечати.Вставить("ОбъединитьКлиентовПоставщиков", Истина);
	КонецЕсли;
	
	Если Параметры.Свойство("ОбъектыПечати") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СверкаВзаиморасчетов.РежимСверкиИтоговВзаиморасчетов КАК РежимСверкиИтогов
		|ИЗ
		|	Документ.СверкаВзаиморасчетов2_5_11 КАК СверкаВзаиморасчетов
		|ГДЕ
		|	СверкаВзаиморасчетов.Ссылка В(&ДокументыСверки)
		|;
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИтоговыеЗаписи.Договор КАК Договор
		|ИЗ
		|	Документ.СверкаВзаиморасчетов2_5_11.ИтоговыеЗаписи КАК ИтоговыеЗаписи
		|ГДЕ
		|	ИтоговыеЗаписи.Ссылка В(&ДокументыСверки)
		|	И ТИПЗНАЧЕНИЯ(ИтоговыеЗаписи.Договор) В (
		|		ТИП(Справочник.ДоговорыКредитовИДепозитов))";
		Запрос.УстановитьПараметр("ДокументыСверки", Параметры.ОбъектыПечати);
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		ПоследнийИндекс = РезультатЗапроса.ВГраница();
		ДокументыСверки = РезультатЗапроса[ПоследнийИндекс-1].Выбрать();
		ДокументыСверки.Следующий();
		РежимСверкиПоДоговорам = ДокументыСверки.РежимСверкиИтогов = Перечисления.РежимСверкиИтоговВзаиморасчетов.ПоДоговорам;
		ЕстьДоговораКредитовДепозитовЗаймовАренды = РезультатЗапроса[ПоследнийИндекс].Выбрать().Следующий();
		
		Элементы.ДебетКакДолг.ТолькоПросмотр = ЕстьДоговораКредитовДепозитовЗаймовАренды;
		Если РежимСверкиПоДоговорам Тогда
			СписокВыбора = Элементы.ДетализацияВзаиморасчетов.СписокВыбора;
			СписокВыбора.Удалить(СписокВыбора.Количество()-1);
		КонецЕсли;
	ИначеЕсли Параметры.Свойство("НастройкиСверки") Тогда
		Элементы.ДебетКакДолг.ТолькоПросмотр = Параметры.НастройкиСверки.ЕстьДоговораКредитовДепозитовЗаймовАренды;
		РежимСверкиПоДоговорам = Параметры.НастройкиСверки.РежимСверкиИтогов = Перечисления.РежимСверкиИтоговВзаиморасчетов.ПоДоговорам;
		Если РежимСверкиПоДоговорам Тогда
			СписокВыбора = Элементы.ДетализацияВзаиморасчетов.СписокВыбора;
			СписокВыбора.Удалить(СписокВыбора.Количество()-1);
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиПечати);
	Если НастройкиПечати.ОбъединитьКлиентовПоставщиков Тогда
		ДебетКакДолг = Ложь;
	КонецЕсли;
	Элементы.ДебетКакДолг.Доступность = НЕ НастройкиПечати.ОбъединитьКлиентовПоставщиков;
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Если РежимСверкиПоДоговорам И ДетализацияВзаиморасчетов = 2 Тогда
		ДетализацияВзаиморасчетов = 1;
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОбъединитьКлиентовПоставщиковПриИзменении(Элемент)
	
	Если ОбъединитьКлиентовПоставщиков Тогда
		ДебетКакДолг = Ложь;
	КонецЕсли;
	Элементы.ДебетКакДолг.Доступность = НЕ ОбъединитьКлиентовПоставщиков;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)

	НастройкиПечати = НастройкиПечатиПоУмолчанию();
	ЗаполнитьЗначенияСвойств(НастройкиПечати, ЭтотОбъект);
	СохранитьНастройкиНаСервере(НастройкиПечати); 
	Закрыть(НастройкиПечати);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьНастройкиНаСервере(НастройкиПечати)

	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		Документы.СверкаВзаиморасчетов2_5_11.КлючОбъектаПользовательскихНастроек(),
		"НастройкиПечати",
		НастройкиПечати);
	
КонецПроцедуры

&НаСервере
Функция НастройкиПечатиПоУмолчанию()
	
	Возврат Документы.СверкаВзаиморасчетов2_5_11.НастройкиПечатиПоУмолчанию();
	
КонецФункции

#КонецОбласти
