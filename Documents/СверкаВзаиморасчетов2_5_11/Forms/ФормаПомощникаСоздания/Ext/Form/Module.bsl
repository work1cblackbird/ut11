
#Область ОписаниеПеременных

&НаКлиенте
Перем ПредыдущиеЗначения;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	УстановитьВидимостьЭлементовНаСервере();
	
	Документы.СверкаВзаиморасчетов2_5_11.ИнициализироватьКомпоновщикНастроек(КомпоновщикОтбор, УникальныйИдентификатор);
	
	ПериодСверки.Вариант = ВариантСтандартногоПериода.ПрошлыйМесяц;
	РежимСверкиИтоговВзаиморасчетов = Перечисления.РежимСверкиИтоговВзаиморасчетов.ПоДоговорам;
	ДатаДокументов = ТекущаяДатаСеанса();
	НоваяАрхитектураВзаиморасчетов = ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов");
	ИспользоватьНесколькоОрганизаций = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
	ЭтоУправлениеТорговлей = ПолучитьФункциональнуюОпцию("УправлениеТорговлей");
	Элементы.Контрагент.ВыбиратьТип = ИспользоватьНесколькоОрганизаций;
	Элементы.ЭтоСверкаМеждуОрганизациями.Видимость = ИспользоватьНесколькоОрганизаций;
	Элементы.ДекорацияКонтрагент.Видимость = НЕ ИспользоватьНесколькоОрганизаций;
	Элементы.Контрагент.ОграничениеТипа  = Новый ОписаниеТипов("СправочникСсылка.Контрагенты");
	
	ДоступноСохранениеДанныхПользователя = ПравоДоступа("СохранениеДанныхПользователя", Метаданные);

	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	ПартнерыИКонтрагенты.ЗаголовокРеквизитаВЗависимостиОтФОИспользоватьПартнеровКакКонтрагентов(
		ЭтотОбъект, "РасчетыСПартнерами", НСтр("ru = 'Открыть отчет ""Расчеты с контрагентами""'"));

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами	
	Элементы.ТаблицаРасчетовСостояниеЭДО.Видимость = Истина;
	ПараметрыПриСозданииНаСервере = ОбменСКонтрагентами.ПараметрыПриСозданииНаСервере_ФормаСписка();
	ПараметрыПриСозданииНаСервере.Форма = ЭтотОбъект;
	ПараметрыПриСозданииНаСервере.МестоРазмещенияКоманд = Элементы.ПодменюЭДО;
	ПараметрыПриСозданииНаСервере.КолонкаСостоянияЭДО = Элементы.ПредставлениеСостояния;

	ОбменСКонтрагентами.ПриСозданииНаСервере_ФормаСписка(ПараметрыПриСозданииНаСервере);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановкаДоступностиЭлементовФормы();
	УправлениеКнопкамиПодвал();
	ПредыдущиеЗначения = Новый Структура;
	ПредыдущиеЗначения.Вставить("ВнешнийКонтрагент");
	ПредыдущиеЗначения.Вставить("КонтрагентНашаОрганизация");

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыОповещенияЭДО = ОбменСКонтрагентамиКлиент.ПараметрыОповещенияЭДО_ФормаСписка();
	ПараметрыОповещенияЭДО.Форма = ЭтотОбъект;
	ПараметрыОповещенияЭДО.ИмяДинамическогоСписка = "СписокДокументовСверки";
	ПараметрыОповещенияЭДО.ЕстьОбработчикОбновленияВидимостиСостоянияЭДО = Истина;
	
	ОбменСКонтрагентамиКлиент.ОбработкаОповещения_ФормаСписка(ИмяСобытия, Параметр, Источник, ПараметрыОповещенияЭДО);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		Документы.СверкаВзаиморасчетов2_5_11.КлючОбъектаПользовательскихНастроек(),
		"НастройкиОтбора",
		КомпоновщикОтбор.ПолучитьНастройки());
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	НастройкиОтбора = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
							Документы.СверкаВзаиморасчетов2_5_11.КлючОбъектаПользовательскихНастроек(),
							"НастройкиОтбора");
	
	Если НастройкиОтбора <> Неопределено Тогда
		КомпоновкаДанныхКлиентСервер.СкопироватьЭлементы(КомпоновщикОтбор.Настройки.Отбор, НастройкиОтбора.Отбор);
		Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
			Документы.СверкаВзаиморасчетов2_5_11.ЗаполнитьОтборПартнераПоКонтрагенту(КомпоновщикОтбор.Настройки.Отбор);
		КонецЕсли;
		ОбновитьОтборыНаФорме();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ЗадаватьВопросОЗакрытии Тогда
		
		ТекстВопроса = НСтр("ru = 'Закрыть форму помощника? Введенные данные будут потеряны.'");
		Отказ = Истина;
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ПередЗакрытиемВопросЗавершение", ЭтотОбъект),
			ТекстВопроса,
			РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодСверкиПриИзменении(Элемент)
	
	ОчиститьСообщения();
	
	Если НЕ ЗначениеЗаполнено(ПериодСверки.ДатаОкончания) Тогда
		ТекстСообщения = НСтр("ru = 'Дата окончания периода сверки должна быть заполнена.'");
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ПериодСверки");
		
		Если ЗначениеЗаполнено(ПериодСверки.ДатаНачала) Тогда
			ПериодСверки.ДатаОкончания = КонецДня(ПериодСверки.ДатаНачала);
		Иначе
			ПериодСверки.ДатаОкончания = КонецДня(ОбщегоНазначенияКлиент.ДатаСеанса());
		КонецЕсли;
	ИначеЕсли ПериодСверки.ДатаНачала > ПериодСверки.ДатаОкончания
		И ЗначениеЗаполнено(ПериодСверки.ДатаОкончания) Тогда
		
		ТекстСообщения = НСтр("ru = 'Дата начала периода не может быть больше даты окончания периода.'");
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ПериодСверки");
		
		ПериодСверки.ДатаОкончания = КонецДня(ПериодСверки.ДатаНачала);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	
	ПериодСверки.ДатаНачала = НачалоПериода;
	Если ПериодСверки.ДатаНачала > ПериодСверки.ДатаОкончания
		И ЗначениеЗаполнено(ПериодСверки.ДатаОкончания) Тогда
		ПериодСверки.ДатаОкончания = КонецДня(ПериодСверки.ДатаНачала);
		КонецПериода = ПериодСверки.ДатаОкончания;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	ПериодСверки.ДатаОкончания = КонецДня(КонецПериода);
	Если ПериодСверки.ДатаНачала > ПериодСверки.ДатаОкончания
		И ЗначениеЗаполнено(ПериодСверки.ДатаОкончания) Тогда
		ПериодСверки.ДатаОкончания = КонецДня(ПериодСверки.ДатаНачала);
		КонецПериода = ПериодСверки.ДатаОкончания;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЭтоСверкаМеждуОрганизациямиПриИзменении(Элемент)
	
	Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
		ПредыдущиеЗначения.ВнешнийКонтрагент = Контрагент;
	ИначеЕсли ТипЗнч(Контрагент) = Тип("СправочникСсылка.Организации") Тогда
		ПредыдущиеЗначения.КонтрагентНашаОрганизация = Контрагент;
	КонецЕсли;
	
	Если ЭтоСверкаМеждуОрганизациями Тогда
		Элементы.Контрагент.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Организации");
		Контрагент = ПредыдущиеЗначения.КонтрагентНашаОрганизация;
	Иначе
		Элементы.Контрагент.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Контрагенты");
		Контрагент = ПредыдущиеЗначения.ВнешнийКонтрагент;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьДокументыНаПечатьПриИзменении(Элемент)
	
	УстановкаДоступностиЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкиПечатиНажатие(Элемент)
	
	НастройкиСверки = Новый Структура;
	НастройкиСверки.Вставить("РежимСверкиИтогов", РежимСверкиИтоговВзаиморасчетов);
	НастройкиСверки.Вставить("ЕстьДоговораКредитовДепозитовЗаймовАренды", РасчетыСКредитором ИЛИ РасчетыСДебитором ИЛИ РасчетыСАрендодателем);
	ПараметрыФормы = Новый Структура("НастройкиСверки", НастройкиСверки);
	ОткрытьФорму(
		"Документ.СверкаВзаиморасчетов2_5_11.Форма.НастройкаПечати",
		ПараметрыФормы,
		ЭтотОбъект,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаРасчетов

&НаКлиенте
Процедура ТаблицаРасчетовСоздаватьДокументПриИзменении(Элемент)
	
	ПересчитатьКоличествоСтрокТаблицыРасчетов();
	ЗадаватьВопросОЗакрытии = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРасчетовОрганизацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРасчетовКонтрагентНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРасчетовОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРасчетовКонтрагентОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Далее(Команда)
	
	ВыполнитьПереходПоСтраницам(Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ВыполнитьПереходПоСтраницам(Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	ВыполнитьПереходПоСтраницам(Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетыСПартнерами(Команда)
	
	ОчиститьСообщения();
	
	ПараметрыФормыРасширенногоОтбора();
	
	ПараметрыФормы = Новый Структура("Отбор, ФиксированныеНастройки, КлючВарианта, СформироватьПриОткрытии");
	ПараметрыФормы.СформироватьПриОткрытии = Истина;
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", "ПомощникСозданияСверкиВзаиморасчетов");
	
	ПараметрыФормы.Отбор = Новый Структура("Период", ПериодСверки);
	
	Если Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаНастройкаФильтров Тогда
		
		ФиксированныеНастройки = Новый НастройкиКомпоновкиДанных();
		
		ПоляОтбораОтчета = "Организация, Партнер, Контрагент, СегментПартнеров";
		Для Каждого ЭлементОтбора Из КомпоновщикОтбор.Настройки.Отбор.Элементы Цикл
			
			Если СтрНайти(ПоляОтбораОтчета, Строка(ЭлементОтбора.ЛевоеЗначение)) <> 0
				И ЭлементОтбора.Использование Тогда
				
				Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
					ДобавитьВОтборЭлементОтбора(ФиксированныеНастройки.Отбор, ЭлементОтбора);
				Иначе
					ДобавитьВОтборГруппуЭлементовОтбора(ФиксированныеНастройки.Отбор, ЭлементОтбора);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ПараметрыФормы.ФиксированныеНастройки = ФиксированныеНастройки;
		
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаТаблицаРасчетов Тогда
		
		Если Элементы.ТаблицаРасчетов.ТекущиеДанные = Неопределено Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбрана ни одна строка'"),,"ТаблицаРасчетов");
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы.Отбор.Вставить("Организация", Элементы.ТаблицаРасчетов.ТекущиеДанные.Организация);
		ПараметрыФормы.Отбор.Вставить("Контрагент",  Элементы.ТаблицаРасчетов.ТекущиеДанные.Контрагент);
		
	КонецЕсли;
	
	ПараметрыФормы.КлючВарианта = "ПоСверкеВзаиморасчетовКонтекст";
	
	ИмяФормыОтчетаРасчетовСПартнерами = ?(НоваяАрхитектураВзаиморасчетов, "Отчет.ВедомостьРасчетовСПартнерами.Форма", "Отчет.РасчетыСПартнерами.Форма");
	
	ОткрытьФорму(ИмяФормыОтчетаРасчетовСПартнерами,
		ПараметрыФормы,
		ЭтотОбъект,
		Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетыМеждуОрганизациями(Команда)
	
	ОчиститьСообщения();
	
	ПараметрыФормы = Новый Структура("Отбор, ФиксированныеНастройки, КлючВарианта, СформироватьПриОткрытии");
	ПараметрыФормы.СформироватьПриОткрытии = Истина;
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", "ПомощникСозданияСверкиВзаиморасчетов");
	
	ПараметрыФормы.Отбор = Новый Структура("Период", ПериодСверки);
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		Если РасчетыСКлиентами Тогда
			ПараметрыФормы.КлючВарианта = "РасчетыПоПродажам";
			ПараметрыФормы.Отбор.Вставить("Продавец", Организация);
			Если ЗначениеЗаполнено(Контрагент) Тогда
				ПараметрыФормы.Отбор.Вставить("Покупатель", Контрагент);
			КонецЕсли;
		ИначеЕсли РасчетыСПоставщиками Тогда
			ПараметрыФормы.КлючВарианта = "РасчетыПоЗакупкам";
			ПараметрыФормы.Отбор.Вставить("Покупатель", Организация);
			Если ЗначениеЗаполнено(Контрагент) Тогда
				ПараметрыФормы.Отбор.Вставить("Продавец", Контрагент);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	ИмяФормыОтчетаРасчетовМеждуОрганизациями = ?(НоваяАрхитектураВзаиморасчетов, "Отчет.ВедомостьРасчетовМеждуОрганизациями.Форма", "Отчет.РасчетыМеждуОрганизациями.Форма");
	
	ОткрытьФорму(ИмяФормыОтчетаРасчетовМеждуОрганизациями,
		ПараметрыФормы,
		ЭтотОбъект,
		Истина); 
	
КонецПроцедуры

&НаКлиенте
Процедура КарточкаПоФинИнструментам(Команда)
	
	ПараметрыФормы = Новый Структура("Отбор, ФиксированныеНастройки, КлючВарианта, СформироватьПриОткрытии");
	ПараметрыФормы.СформироватьПриОткрытии = Истина;
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", "ПомощникСозданияСверкиВзаиморасчетов");
	
	ПараметрыФормы.Отбор = Новый Структура("ПериодОтчета", ПериодСверки);
	ФиксированныеНастройки = Новый НастройкиКомпоновкиДанных();
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ПараметрыФормы.Отбор.Вставить("Организация", Организация);
	КонецЕсли;
	Если ЗначениеЗаполнено(Контрагент) Тогда
		ПараметрыФормы.Отбор.Вставить("Контрагент", Контрагент);
	КонецЕсли;
	ПоляОтбораОтчета = "Организация, Партнер, Контрагент, СегментПартнеров";
	Для Каждого ЭлементОтбора Из КомпоновщикОтбор.Настройки.Отбор.Элементы Цикл
		
		Если СтрНайти(ПоляОтбораОтчета, Строка(ЭлементОтбора.ЛевоеЗначение)) <> 0
			И ЭлементОтбора.Использование Тогда
			
			Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
				ДобавитьВОтборЭлементОтбора(ФиксированныеНастройки.Отбор, ЭлементОтбора);
			Иначе
				ДобавитьВОтборГруппуЭлементовОтбора(ФиксированныеНастройки.Отбор, ЭлементОтбора);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЭтоУправлениеТорговлей Тогда
		ПараметрыФормы.КлючВарианта = "КарточкаРасчетовПоФинансовымИнструментамУТКонтекст";
	Иначе
		ПараметрыФормы.КлючВарианта = "КарточкаРасчетовПоФинансовымИнструментамКонтекст";
	КонецЕсли;
	
	ПараметрыФормы.ФиксированныеНастройки = ФиксированныеНастройки;
	
	ОткрытьФорму("Отчет.КарточкаРасчетовПоФинансовымИнструментам.Форма",
		ПараметрыФормы,
		ЭтотОбъект,
		Истина);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьНеИзменять(Команда)
	
	УстановитьДействиеССуществующимДокументом("НеИзменять");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНаУдаление(Команда)
	
	УстановитьДействиеССуществующимДокументом("УдалитьСоздатьНовый");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПерезаполнять(Команда)
	
	УстановитьДействиеССуществующимДокументом("Перезаполнять");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСоздавать(Команда)
	
	УстановитьДействиеСНовымДокументом("Создавать");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНеСоздавать(Команда)
	
	УстановитьДействиеСНовымДокументом("НеСоздавать");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНастройкиОтборов(Команда)
	
	ПараметрыОткрытия = ПараметрыФормыРасширенногоОтбора();
	
	ОбработчикЗакрытия = Новый ОписаниеОповещения("УстановитьНастройкиОтборовЗавершение", ЭтотОбъект);
	ОткрытьФорму("Документ.СверкаВзаиморасчетов2_5_11.Форма.НастройкаОтборов",
	            ПараметрыОткрытия,
	            ЭтотОбъект,
	            ,
	            ,
	            , 
	            ОбработчикЗакрытия,
                РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусНаСверке(Команда)
	
	ВыделенныеСтроки = ОбщегоНазначенияУТКлиент.ПроверитьПолучитьВыделенныеВСпискеСсылки(Элементы.СписокДокументовСверки);
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ПоказатьВопрос(Новый ОписаниеОповещения("УстановитьСтатусНаСверкеЗавершение", ЭтотОбъект, Новый Структура("ВыделенныеСтроки", ВыделенныеСтроки)), НСтр("ru='У выделенных в списке документов будет установлен статус ""На сверке"". Продолжить?'"), РежимДиалогаВопрос.ДаНет);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервалПериода(Команда)
	
	Оповещение = Новый ОписаниеОповещения(
		"УстановитьИнтервалПериодаЗавершение",
		ЭтотОбъект);
		
	ОбщегоНазначенияУТКлиент.РедактироватьПериод(
		ЭтотОбъект, 
		Новый Структура("ДатаНачала, ДатаОкончания", "НачалоПериода", "КонецПериода"),
		Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

// ЭлектронноеВзаимодействие.ОбменСКонтрагентами

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Элементы.СписокДокументовСверки);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьВидимостьСостоянияЭДО()
	
	ОбменСКонтрагентамиКлиент.ОбработчикОбновленияВидимостьСостоянияЭДО(ЭтотОбъект, Элементы.ПредставлениеСостояния);
	
КонецПроцедуры

// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)

	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.СписокДокументовСверки);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт

	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()

	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.СписокДокументовСверки);

КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиПервогоШагаПомощника

&НаСервере
Процедура ЗаполнитьТаблицуРасчетовСервер()
	
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "НачалоПериода", ПериодСверки.ДатаНачала);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "КонецПериода", КонецДня(ПериодСверки.ДатаОкончания));
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "Период", ПериодСверки.ДатаНачала);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "ПолучатьДокументыСверки", Истина);
	
	РежимСверкиПоДоговорам = РежимСверкиИтоговВзаиморасчетов = Перечисления.РежимСверкиИтоговВзаиморасчетов.ПоДоговорам;
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "РежимСверкиПоДоговорам", РежимСверкиПоДоговорам);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "РазбиватьПоТипамРасчетов", РазбиватьПоТипамРасчетов);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "РазбиватьПоПартнерам", РазбиватьПоПартнерам);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "РазбиватьПоДоговорам", РазбиватьПоДоговорам);
	
	Если НЕ ИспользуютсяФинИнструменты И НоваяАрхитектураВзаиморасчетов Тогда
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "СверкаВВалюте", СверкаВВалюте);
		УстановитьОсновныеОтборы();
		ТаблицаРезультатаСКД = Документы.СверкаВзаиморасчетов2_5_11.ПолучитьВзаиморасчеты(КомпоновщикОтбор);
	Иначе
		КомпоновщикОтборФинИнструменты = Новый КомпоновщикНастроекКомпоновкиДанных;
		Документы.СверкаВзаиморасчетов2_4.ИнициализироватьКомпоновщикНастроек(КомпоновщикОтборФинИнструменты, УникальныйИдентификатор);
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтборФинИнструменты.Настройки, "НачалоПериода", ПериодСверки.ДатаНачала);
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтборФинИнструменты.Настройки, "КонецПериода", КонецДня(ПериодСверки.ДатаОкончания));
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтборФинИнструменты.Настройки, "Период", ПериодСверки.ДатаНачала);
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтборФинИнструменты.Настройки, "ПолучатьДокументыСверки", Истина);
		
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтборФинИнструменты.Настройки, "РазбиватьПоТипамРасчетов", РазбиватьПоТипамРасчетов);
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтборФинИнструменты.Настройки, "РазбиватьПоПартнерам", РазбиватьПоПартнерам);
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтборФинИнструменты.Настройки, "РазбиватьПоДоговорам", РазбиватьПоДоговорам);
		
		УстановитьОсновныеОтборы(КомпоновщикОтборФинИнструменты);
		
		ТаблицаРезультатаСКД = Документы.СверкаВзаиморасчетов2_4.ПолучитьВзаиморасчеты(КомпоновщикОтборФинИнструменты);
	КонецЕсли;
	
	Если ТаблицаРезультатаСКД.Количество() > 0 Тогда
		Группировки = "Организация, Контрагент, СуществуютДокументы, ИзменятьДокумент, СоздаватьДокумент, ДокументСверки";
		//++ Локализация
		Если НЕ ИспользуютсяФинИнструменты И НоваяАрхитектураВзаиморасчетов Тогда
			Если ТаблицаРезультатаСКД.Колонки.Найти("СостояниеЭДО") <> Неопределено Тогда
				Группировки = Группировки + ", СостояниеЭДО";
			КонецЕсли;
		КонецЕсли;
		//-- Локализация
		Элементы.СписокДокументовСверкиТипРасчетов.Видимость = РазбиватьПоТипамРасчетов;
		Элементы.СписокДокументовСверкиПартнер.Видимость = РазбиватьПоПартнерам;
		Элементы.СписокДокументовСверкиДоговор.Видимость = РазбиватьПоДоговорам;
		Если РазбиватьПоДоговорам Тогда
			Группировки = "Договор, " + Группировки;
		КонецЕсли;
		Если РазбиватьПоПартнерам Тогда
			Группировки = "Партнер, " + Группировки;
		КонецЕсли;
		Если РазбиватьПоТипамРасчетов Тогда
			Группировки = "ПорядокТипаПлатежа, ТипРасчетов, " + Группировки;
		КонецЕсли;
		Если НЕ ИспользуютсяФинИнструменты И НоваяАрхитектураВзаиморасчетов Тогда
			Группировки = "ВалютаСверки, " + Группировки;
		КонецЕсли;

		ТаблицаРезультатаСКД.Свернуть(Группировки);
		Если РазбиватьПоТипамРасчетов Тогда
			Группировки = СтрЗаменить(Группировки, "ТипРасчетов, ", "");
		КонецЕсли;
		ТаблицаРезультатаСКД.Сортировать(Группировки);
		ТаблицаРасчетов.Загрузить(ТаблицаРезультатаСКД);
		
		Отбор = Новый Структура("СуществуютДокументы",Истина);
		СтрокиСуществуютДокументы = ТаблицаРасчетов.НайтиСтроки(Отбор);
		Для Каждого Строка Из СтрокиСуществуютДокументы Цикл
			Строка.ИзменятьДокумент = "НеИзменять";
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТаблицуРасчетовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаполнитьТаблицуРасчетовСервер();
	ПересчитатьКоличествоСтрокТаблицыРасчетов();
	Элементы.ТаблицаРасчетовТипРасчетов.Видимость = РазбиватьПоТипамРасчетов;
	Элементы.ТаблицаРасчетовПартнер.Видимость = РазбиватьПоПартнерам;
	Элементы.ТаблицаРасчетовДоговор.Видимость = РазбиватьПоДоговорам;
	Элементы.ТаблицаРасчетовВалютаСверки.Видимость = НоваяАрхитектураВзаиморасчетов И НЕ ИспользуютсяФинИнструменты;
	Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаТаблицаРасчетов;
	Элементы.Далее.Доступность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиВторогоШагаПомощника

&НаКлиенте
Процедура ПересчитатьКоличествоСтрокТаблицыРасчетов()
	
	КоличествоСозданных = 0;
	КоличествоУдаленных = 0;
	КоличествоПерезаполненных = 0;
	
	Для Каждого СтрокаТаблицыРасчетов Из ТаблицаРасчетов Цикл
		
		Если СтрокаТаблицыРасчетов.СоздаватьДокумент Тогда
			
			КоличествоСозданных = КоличествоСозданных + 1;
			
		ИначеЕсли СтрокаТаблицыРасчетов.ИзменятьДокумент = "УдалитьСоздатьНовый" Тогда
			КоличествоСозданных = КоличествоСозданных + 1;
			КоличествоУдаленных = КоличествоУдаленных + 1;
			
		ИначеЕсли СтрокаТаблицыРасчетов.ИзменятьДокумент = "Перезаполнять" Тогда
			КоличествоПерезаполненных = КоличествоПерезаполненных + 1;
			
		КонецЕсли;
		
	КонецЦикла;

	ВсегоСтрок = ТаблицаРасчетов.Количество();
	БудетСоздано = КоличествоСозданных;
	БудетУдалено = КоличествоУдаленных;
	БудетПерезаполнено = КоличествоПерезаполненных;
	КоличествоОтмеченных = КоличествоСозданных + КоличествоПерезаполненных;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиТретьегоШагаПомощника

&НаСервере
Процедура СоздатьВФоновомЗаданииНаСервере()
	
	ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Создание документов ""Сверка взаиморасчетов""'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("ТаблицаРасчетов", ТаблицаРасчетов.Выгрузить());
	ПараметрыМетода.Вставить("ДатаДокументов", ДатаДокументов);
	ПараметрыМетода.Вставить("ПериодСверки", ПериодСверки);
	ПараметрыМетода.Вставить("НастройкиОтбора", КомпоновщикОтбор.ПолучитьНастройки());
	ПараметрыМетода.Вставить("ДоговорыБезОборотов", ДоговорыБезОборотов);
	ПараметрыМетода.Вставить("РазбиватьПоТипамРасчетов", РазбиватьПоТипамРасчетов);
	ПараметрыМетода.Вставить("РазбиватьПоПартнерам", РазбиватьПоПартнерам);
	ПараметрыМетода.Вставить("РазбиватьПоДоговорам", РазбиватьПоДоговорам);
	ПараметрыМетода.Вставить("РежимСверкиИтоговВзаиморасчетов", РежимСверкиИтоговВзаиморасчетов);
	ПараметрыМетода.Вставить("СверкаВВалюте", СверкаВВалюте);
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
							"Документы.СверкаВзаиморасчетов2_5_11.СоздатьДокументы",
							ПараметрыМетода,
							ПараметрыВыполнения);
	
	ИдентификаторЗадания = ДлительнаяОперация.ИдентификаторЗадания;

КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Элементы.Далее.Доступность = Истина;
		Возврат;
	КонецЕсли;
	
	РезультатСоздания = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	ОтработанныеДокументы = РезультатСоздания.ОтработанныеДокументы;
	
	Если РезультатСоздания = Неопределено Тогда
		Элементы.Далее.Доступность = Истина;
		Возврат;
	КонецЕсли;
	
	ТаблицаОбработанныхДокументов.Очистить();
	КоличествоСозданных = 0;
	Для Каждого СтрокаПротокола Из ОтработанныеДокументы Цикл
		
		СтрокаТаблицы = ТаблицаОбработанныхДокументов.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаПротокола);
		ОбработатьСтрокуПротокола(СтрокаПротокола, КоличествоСозданных);
		
	КонецЦикла;

	ТекстРезультата = НСтр("ru='Создано %КоличествоСозданных% из %КоличествоВсего% документов'");
	ТекстРезультата = СтрЗаменить(ТекстРезультата, "%КоличествоСозданных%", КоличествоСозданных);
	ТекстРезультата = СтрЗаменить(ТекстРезультата, "%КоличествоВсего%",     КоличествоОтмеченных);
	Элементы.ДекорацияРезультат.Заголовок = ТекстРезультата;
	
	ПоказатьСтраницуРезультата(РезультатСоздания.МассивВсехДокументовСверки);
	
КонецПроцедуры

// Показать страницу результата.
// 
// Параметры:
//  ДокументыСверки - Массив Из ДокументСсылка.СверкаВзаиморасчетов2_5_11
// 
&НаКлиенте
Процедура ПоказатьСтраницуРезультата(ДокументыСверки)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		СписокДокументовСверки, "Ссылка", ДокументыСверки, ВидСравненияКомпоновкиДанных.ВСписке);
	
	ЗадаватьВопросОЗакрытии = Ложь;
		
	Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаРезультат;
	Элементы.Далее.Доступность = Истина;
	УстановкаДоступностиЭлементовФормы();
	УправлениеКнопкамиПодвал();
	
	Если ВладелецФормы <> Неопределено Тогда
		Список = ВладелецФормы.Элементы.Список; // ТаблицаФормы -
		Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

// Описание
// 
// Параметры:
// 	СтрокаПротокола - см. Документы.СверкаВзаиморасчетов2_5_11.СтрокаПротокола
// 	КоличествоСозданных - Число - 
// 	
&НаКлиенте
Процедура ОбработатьСтрокуПротокола(СтрокаПротокола, КоличествоСозданных)
	
	Если СтрокаПротокола.Действие = "Создание" Тогда 
		
		ОповеститьОДействии(СтрокаПротокола.ДокументСверки, НСтр("ru='Создание:'"));
		КоличествоСозданных = КоличествоСозданных + 1;
		
	ИначеЕсли СтрокаПротокола.Действие = "ПометкаНаУдаление" Тогда 
		
		ОповеститьОДействии(СтрокаПротокола.ДокументСверки, НСтр("ru='Пометка удаления установлена:'"));
		
	ИначеЕсли СтрокаПротокола.Действие = "Перезаполнение" Тогда 
		
		ОповеститьОДействии(СтрокаПротокола.ДокументСверки, НСтр("ru='Перезаполнение:'"));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОДействии(ДокументСверки, Текст)
	
	НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(ДокументСверки);

	ПоказатьОповещениеПользователя(
		Текст,
		НавигационнаяСсылка,
		Строка(ДокументСверки),
		БиблиотекаКартинок.Информация32);
	
	Если ДоступноСохранениеДанныхПользователя Тогда
		ИсторияРаботыПользователя.Добавить(НавигационнаяСсылка);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЧетвертогоШагаПомощника

&НаКлиенте
Процедура СписокДокументовСверкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	Если Поле = Элементы.ПредставлениеСостояния Тогда
		ОбменСКонтрагентамиКлиент.СостояниеЭДОНажатие_ФормаСписка(Элемент.ТекущиеДанные.Ссылка, СтандартнаяОбработка);
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

КонецПроцедуры

&НаКлиенте
Процедура СписокДокументовСверкиПриАктивизацииСтроки(Элемент)

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

&НаСервере
Функция ДокументыНаПечать()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СверкаВзаиморасчетов.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.СверкаВзаиморасчетов2_5_11 КАК СверкаВзаиморасчетов
	|ГДЕ
	|	СверкаВзаиморасчетов.Проведен = ИСТИНА
	|	И СверкаВзаиморасчетов.Ссылка В(&МассивСсылок)
	|
	|УПОРЯДОЧИТЬ ПО
	|	СверкаВзаиморасчетов.Дата,
	|	СверкаВзаиморасчетов.Номер");
	
	МассивСсылок = СписокДокументовСверки.Отбор.Элементы[0].ПравоеЗначение;
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура УстановитьПометкуНаУдаление(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаОтветаОПометкеУдаления", ЭтотОбъект);
	ТекстВопроса = НСтр("ru='Изменить пометку удаления для выделенных элементов?'");
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаОПометкеУдаления(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПометкуНаУдалениеНаСервере();
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПометкуНаУдалениеНаСервере()
	
	ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
	НаименованиеЗадания = НСтр("ru = 'Изменение пометки на удаление документов ""Сверка взаиморасчетов""'");
	ИмяПроцедуры        = "Документы.СверкаВзаиморасчетов2_5_11.УстановитьПометкуНаУдаление";
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("ВыделенныеДокументы", Элементы.СписокДокументовСверки.ВыделенныеСтроки);
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(ИмяПроцедуры, ПараметрыМетода, ПараметрыВыполнения);
	
	ИдентификаторЗадания = ДлительнаяОперация.ИдентификаторЗадания;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометкуНаУдалениеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.Далее.Доступность = Истина;
	Элементы.СписокДокументовСверки.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ВернутьсяВНачало(Команда)
	
	Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаНастройкаФильтров;
	УправлениеКнопкамиПодвал();
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	// ТаблицаРасчетовСоздаватьДокумент
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаРасчетовСоздаватьДокумент.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаРасчетов.СоздаватьДокумент");
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаРасчетов.СуществуютДокументы");
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

	// ТаблицаРасчетовИзменятьДокумент
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаРасчетовИзменятьДокумент.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаРасчетов.СуществуютДокументы");
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Тексты поля ТаблицаРасчетовИзменятьДокумент
	Для каждого ТекстПоля Из Элементы.ТаблицаРасчетовИзменятьДокумент.СписокВыбора Цикл
		
		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаРасчетовИзменятьДокумент.Имя);
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаРасчетов.ИзменятьДокумент");
		ОтборЭлемента.ПравоеЗначение = ТекстПоля.Значение;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", ТекстПоля.Представление);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовНаСервере()
	
	ДоступныРасчетыПоФинансовымИнструментам = ПравоДоступа("Просмотр", Метаданные.РегистрыНакопления.РасчетыПоФинансовымИнструментам);
	ДоступныДоговорыКредитовИДепозитов = ПравоДоступа("Просмотр", Метаданные.Справочники.ДоговорыКредитовИДепозитов);
	ЭтоБазовая = ПолучитьФункциональнуюОпцию("БазоваяВерсия");
	
	Элементы.ТаблицаРасчетовСостояниеЭДО.Видимость = Ложь;
	Элементы.РасчетыСДебитором.Видимость = Ложь;
	Элементы.РасчетыСКредитором.Видимость = Ложь;
	Элементы.РасчетыСАрендодателем.Видимость = Ложь;
	Элементы.КарточкаПоФинИнструментам.Видимость = Ложь;
	Если ДоступныРасчетыПоФинансовымИнструментам И НЕ ЭтоБазовая Тогда
		Элементы.КарточкаПоФинИнструментам.Видимость = ПравоДоступа("Просмотр", Метаданные.Отчеты.КарточкаРасчетовПоФинансовымИнструментам);
		Если ДоступныДоговорыКредитовИДепозитов Тогда
			Элементы.РасчетыСДебитором.Видимость = Истина;
			Элементы.РасчетыСКредитором.Видимость = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ПодменюЭДО.Видимость = НЕ ЭтоБазовая;
	Элементы.ПредставлениеСостояния.Видимость = НЕ ЭтоБазовая;
	Если ЭтоБазовая Тогда
		Элементы.ДекорацияТаблицаРезультата.Заголовок = НСтр("ru = 'Распечатайте акты сверки'");
	КонецЕсли;
	
	Элементы.РасчетыСПартнерами.Видимость = ?(
		НоваяАрхитектураВзаиморасчетов,
			ПравоДоступа("Просмотр", Метаданные.Отчеты.ВедомостьРасчетовСПартнерами),
			ПравоДоступа("Просмотр", Метаданные.Отчеты.РасчетыСПартнерами));
	Элементы.РасчетыМеждуОрганизациями.Видимость = НЕ ЭтоБазовая 
		И ?(НоваяАрхитектураВзаиморасчетов,
				ПравоДоступа("Просмотр", Метаданные.Отчеты.ВедомостьРасчетовМеждуОрганизациями),
				ПравоДоступа("Просмотр", Метаданные.Отчеты.РасчетыМеждуОрганизациями));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановкаДоступностиЭлементовФормы()

	Элементы.СтраницыПомощника.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	
	Элементы.КоличествоЭкземпляров.Доступность = ВыводитьДокументыНаПечать;
	Элементы.ДекорацияНастройкиПечати.Доступность = ВыводитьДокументыНаПечать;
	
	Если НЕ ВыводитьДокументыНаПечать Тогда
		КоличествоЭкземпляров = 0;
	ИначеЕсли КоличествоЭкземпляров = 0 Тогда
		КоличествоЭкземпляров = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПереходПоСтраницам(Команда)
	
	ОчиститьСообщения();
	Элементы.Далее.Доступность = Истина;
	Если Команда.Имя = "Далее"
		И Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаНастройкаФильтров Тогда
		
		Если НЕ (РасчетыСКлиентами ИЛИ РасчетыСПоставщиками ИЛИ РасчетыСКредитором ИЛИ РасчетыСДебитором ИЛИ РасчетыСАрендодателем) Тогда
			ТекстСообщения = НСтр("ru = 'Не выбран тип расчетов для сверки.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "РасчетыСКлиентами");
			Возврат;
		КонецЕсли;
		
		Отказ = Ложь;
		Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Организации")
			И ЗначениеЗаполнено(ОсновнойМенеджер) Тогда
			ТекстСообщения = НСтр("ru = 'Установлен отбор по основному менеджеру, сверка между организациями не будет создана.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ОсновнойМенеджер",, Отказ);
		КонецЕсли;

		Если НЕ ИспользуютсяФинИнструменты Тогда
			Если РежимСверкиИтоговВзаиморасчетов.Пустая() Тогда
				ТекстСообщения = НСтр("ru = 'Не заполнен режим сверки итогов взаиморасчетов.'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "РежимСверкиИтоговВзаиморасчетов",, Отказ);
			КонецЕсли;
		КонецЕсли;
		Если Отказ Тогда
			Возврат;
		КонецЕсли;

		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаНастроек;
		
	ИначеЕсли Команда.Имя = "Далее"
		И Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаНастроек Тогда
				
		Если ПараметрыЗаполнены() Тогда
			ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗаполнитьТаблицуРасчетовЗавершение", ЭтотОбъект);
			ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Истина);
		КонецЕсли;
		
	ИначеЕсли Команда.Имя = "Далее"
		И Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаТаблицаРасчетов Тогда
		
		ЗадаватьВопросОЗакрытии = Истина;
		Если ПараметрыЗаполнены() И ТаблицаРасчетов.Количество() > 0 Тогда
			Элементы.Далее.Доступность = Ложь;
			Если ТаблицаРасчетов.Количество() > 0 Тогда
				СоздатьВФоновомЗаданииНаСервере();
				ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 0.3, Истина);
			Иначе
				ПоказатьСтраницуРезультата(Новый Массив);
			КонецЕсли;
		КонецЕсли;

		// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
		ОбменСКонтрагентамиКлиент.ПриОткрытии(ЭтотОбъект);
		// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

	ИначеЕсли Команда.Имя = "Далее"
		И Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаРезультат Тогда

		Если ВыводитьДокументыНаПечать Тогда
			
			МассивДокументовДляПечати = ДокументыНаПечать();
			
			ИменаМакетов = "АктСверкиВзаимныхРасчетов";
			Для Индекс = 2 По КоличествоЭкземпляров Цикл
				ИменаМакетов = ИменаМакетов + ",АктСверкиВзаимныхРасчетов";
			КонецЦикла;
			
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
				"Документ.СверкаВзаиморасчетов2_5_11",
				ИменаМакетов,
				МассивДокументовДляПечати,
				Неопределено,
				Неопределено);
			
		КонецЕсли;
		Оповестить("СозданыСверкиПомощником2_5_11");
		Закрыть();
		
	ИначеЕсли Команда.Имя = "Назад"
		И Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаРезультат Тогда
		
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаТаблицаРасчетов;
		
	ИначеЕсли Команда.Имя = "Назад"
		И Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаТаблицаРасчетов Тогда
		
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаНастроек;
		ТаблицаРасчетов.Очистить();
		
	ИначеЕсли Команда.Имя = "Назад"
		И Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаНастроек Тогда
		
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаНастройкаФильтров;
			
	ИначеЕсли Команда.Имя = "Отмена" Тогда
		
		Закрыть();
		
	КонецЕсли;
	
	УправлениеКнопкамиПодвал();
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыЗаполнены()
	
	Отказ = Ложь;
	ОчиститьСообщения();
	
	Если Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаНастройкаФильтров Тогда
		Если НЕ ЗначениеЗаполнено(ПериодСверки.ДатаОкончания) Тогда
			ТекстСообщения = НСтр("ru = 'Не указано окончание периода сверки.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, ,"ПериодСверки", , Отказ);
		КонецЕсли;
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаТаблицаРасчетов Тогда
		ПересчитатьКоличествоСтрокТаблицыРасчетов();
		
		Если БудетСоздано = 0 И БудетПерезаполнено = 0 Тогда
			ТекстСообщения = НСтр("ru = 'Не отмечена ни одна строка для создания или перезаполнения.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, ,"ТаблицаРасчетов", , Отказ);
		КонецЕсли;
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаНастроек Тогда
		Если НЕ ЗначениеЗаполнено(ДатаДокументов) Тогда
			ТекстСообщения = НСтр("ru = 'Указана пустая дата документов.'");
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, ,"ДатаДокументов", , Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции

&НаКлиенте
Процедура УправлениеКнопкамиПодвал()
	
	Элементы.Назад.Видимость = Истина;
	Элементы.Отмена.Видимость = Истина;
	Элементы.ВернутьсяВНачало.Видимость = Ложь;
	ТекстДалее = НСтр("ru='Далее'");
	ТекстНазад = НСтр("ru='Назад'");
	Если Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаНастройкаФильтров Тогда
		
		Элементы.Далее.Заголовок = ТекстДалее + " >>";
		Элементы.Назад.Заголовок = ТекстНазад;
		Элементы.Назад.Видимость = Ложь;

	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаНастроек Тогда
		
		Элементы.Далее.Заголовок = ТекстДалее + " >>";
		Элементы.Назад.Заголовок = "<< " + ТекстНазад;

	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаТаблицаРасчетов Тогда
		
		Элементы.Далее.Заголовок = НСтр("ru='Выполнить'");
		Элементы.Назад.Заголовок = "<< " + ТекстНазад;

	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаРезультат Тогда
		
		Элементы.Далее.Заголовок = НСтр("ru='Завершить'");
		Элементы.Назад.Заголовок = "<< " + ТекстНазад;
		Элементы.Отмена.Видимость = Ложь;
		Элементы.ВернутьсяВНачало.Видимость = Истина;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДействиеССуществующимДокументом(Знач ИмяКоманды)
	
	Если Элементы.ТаблицаРасчетов.ВыделенныеСтроки.Количество() > 1 Тогда
		Для Каждого Ид Из Элементы.ТаблицаРасчетов.ВыделенныеСтроки Цикл
			стр = ТаблицаРасчетов.НайтиПоИдентификатору(Ид);
			Если стр <> Неопределено И стр.СуществуютДокументы Тогда
				стр.ИзменятьДокумент = ИмяКоманды;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Для Каждого стр Из ТаблицаРасчетов Цикл
			Если стр.СуществуютДокументы Тогда
				стр.ИзменятьДокумент = ИмяКоманды;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	ПересчитатьКоличествоСтрокТаблицыРасчетов();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДействиеСНовымДокументом(Знач ИмяКоманды)
	
	Если Элементы.ТаблицаРасчетов.ВыделенныеСтроки.Количество() > 1 Тогда
		Для Каждого Ид Из Элементы.ТаблицаРасчетов.ВыделенныеСтроки Цикл
			стр = ТаблицаРасчетов.НайтиПоИдентификатору(Ид);
			Если стр <> Неопределено И НЕ стр.СуществуютДокументы Тогда
				стр.СоздаватьДокумент = ИмяКоманды = "Создавать";
			КонецЕсли;
		КонецЦикла;
	Иначе
		Для Каждого стр Из ТаблицаРасчетов Цикл
			Если НЕ стр.СуществуютДокументы Тогда
				стр.СоздаватьДокумент = ИмяКоманды = "Создавать";
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	ПересчитатьКоличествоСтрокТаблицыРасчетов();
	
КонецПроцедуры

&НаСервере
Функция ПараметрыФормыРасширенногоОтбора()
	
	ОтборДоговорыБезОборотов = ФинансоваяОтчетностьСервер.НайтиЭлементОтбора(КомпоновщикОтбор.Настройки.Отбор, "ДоговорыБезОборотов");
	Если ОтборДоговорыБезОборотов = Неопределено Тогда
		ФинансоваяОтчетностьСервер.УстановитьОтбор(КомпоновщикОтбор.Настройки.Отбор, "ДоговорыБезОборотов", Истина);
		ОтборДоговорыБезОборотов = ФинансоваяОтчетностьСервер.НайтиЭлементОтбора(КомпоновщикОтбор.Настройки.Отбор, "ДоговорыБезОборотов");
		ОтборДоговорыБезОборотов.Представление = НСтр("ru = 'Включать договора без оборотов в периоде'");
	КонецЕсли;
	ОтборДоговорыБезОборотов.Использование = ДоговорыБезОборотов;
	
	УстановитьОсновныеОтборы();
	
	ПараметрыОткрытия = Новый Структура("НастройкиОтбора, СкрыватьСлужебныеОтборы", КомпоновщикОтбор.ПолучитьНастройки(), Ложь);
	
	Возврат ПараметрыОткрытия;
	
КонецФункции

&НаСервере
Процедура УстановитьОсновныеОтборы(КомпоновщикНастроек = Неопределено)
	
	Если КомпоновщикНастроек = Неопределено Тогда
		КомпоновщикНастроек = КомпоновщикОтбор;
	КонецЕсли;
	
	ОтборДоговорыБезОборотов = ФинансоваяОтчетностьСервер.НайтиЭлементОтбора(КомпоновщикНастроек.Настройки.Отбор, "ДоговорыБезОборотов");
	ОтборДоговорыБезОборотов.Использование = ДоговорыБезОборотов;
	
	УстановитьОтборКомпоновщика("Организация", Организация, КомпоновщикНастроек);
	УстановитьОтборКомпоновщика("Контрагент", Контрагент, КомпоновщикНастроек);
	УстановитьОтборКомпоновщика("ОсновнойМенеджер", ОсновнойМенеджер, КомпоновщикНастроек);
	
	#Область ОтборПоТипамРасчетов
	СписокРасчетов = Новый СписокЗначений();
	Если РасчетыСКлиентами Тогда
		СписокРасчетов.Добавить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом);
	КонецЕсли;
	
	Если РасчетыСПоставщиками Тогда
		СписокРасчетов.Добавить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком);
	КонецЕсли;
	
	Если РасчетыСКредитором Тогда
		СписокРасчетов.Добавить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСКредитором);
	КонецЕсли;
	
	Если РасчетыСДебитором Тогда
		СписокРасчетов.Добавить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСДебитором);
	КонецЕсли;
	
	Если РасчетыСАрендодателем Тогда
		СписокРасчетов.Добавить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСАрендодателем);
	КонецЕсли;
	
	ВидСравненияТипаРасчетов = ВидСравненияКомпоновкиДанных.ВСписке;
	Если СписокРасчетов.Количество() = 1 Тогда
		ВидСравненияТипаРасчетов = ВидСравненияКомпоновкиДанных.Равно;
		СписокРасчетов = СписокРасчетов[0].Значение;
	КонецЕсли;
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(
		КомпоновщикОтбор,
		"ТипРасчетов",
		СписокРасчетов,
		ВидСравненияТипаРасчетов,
		ЗначениеЗаполнено(СписокРасчетов));
	#КонецОбласти
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборКомпоновщика(ИмяОтбора, ЗначениеОтбора, КомпоновщикНастроек = Неопределено)
	
	Если КомпоновщикНастроек = Неопределено Тогда
		КомпоновщикНастроек = КомпоновщикОтбор;
	КонецЕсли;
	
	ЭлементОтбора = ФинансоваяОтчетностьСервер.НайтиЭлементОтбора(КомпоновщикНастроек.Настройки.Отбор, ИмяОтбора);
	
	Если ИмяОтбора = "Контрагент" И ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
		Если ТипЗнч(ЗначениеОтбора) = Тип("СправочникСсылка.Организации") Тогда
			ЭлементОтбора = ФинансоваяОтчетностьСервер.НайтиЭлементОтбора(КомпоновщикНастроек.Настройки.Отбор,
																			"ОрганизацияКонтрагент");
		Иначе
			ЭлементОтбора = ФинансоваяОтчетностьСервер.НайтиЭлементОтбора(КомпоновщикНастроек.Настройки.Отбор, "Партнер");
		КонецЕсли;
	КонецЕсли;
	
	Если ЭлементОтбора = Неопределено И НЕ ЗначениеЗаполнено(ЗначениеОтбора) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеОтбора)
		Или (Не ЗначениеЗаполнено(ЗначениеОтбора)
			И ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно) Тогда
		
		Если ИмяОтбора = "Контрагент"
			И ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
			
			Если ТипЗнч(ЗначениеОтбора) = Тип("СправочникСсылка.Контрагенты") Тогда
				Партнер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначениеОтбора, "Партнер");
				КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(КомпоновщикНастроек, "Партнер", Партнер, , ЗначениеЗаполнено(Партнер));
				КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(КомпоновщикНастроек, "ОрганизацияКонтрагент", , , Ложь);
			Иначе
				КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(КомпоновщикНастроек,
					"ОрганизацияКонтрагент",
					ЗначениеОтбора,
					,
					ЗначениеЗаполнено(ЗначениеОтбора));
				КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(КомпоновщикНастроек, "Партнер", , , Ложь);
			КонецЕсли;
			
		Иначе
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(КомпоновщикНастроек,
				ИмяОтбора,
				ЗначениеОтбора,
				,
				ЗначениеЗаполнено(ЗначениеОтбора));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтборыНаФорме()
	
	ОтборДоговорыБезОборотов = ФинансоваяОтчетностьСервер.НайтиЭлементОтбора(КомпоновщикОтбор.Настройки.Отбор, "ДоговорыБезОборотов");
	ДоговорыБезОборотов = ОтборДоговорыБезОборотов.Использование;
	
	УстановитьТипРасчетовНаФорме();
	УстановитьОтборНаФорме("Организация");
	УстановитьОтборНаФорме("Контрагент");
	УстановитьОтборНаФорме("ОсновнойМенеджер");
	
	ОбновитьГиперссылкуОтбора(Элементы.УстановитьНастройкиОтборов, КомпоновщикОтбор.Настройки.Отбор);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьГиперссылкуОтбора(КнопкаРасширенногоОтбора, Отбор)
	
	ВидимыеОтборыСтрокой = "ДоговорыБезОборотов,ТипРасчетов,Организация,Контрагент,ОсновнойМенеджер";
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
		ВидимыеОтборыСтрокой = СтрЗаменить(ВидимыеОтборыСтрокой, "Контрагент", "Партнер");
	КонецЕсли;
	ВидимыеОтборы = СтрРазделить(ВидимыеОтборыСтрокой, ",");
	УстановленРасширенныйОтбор = Ложь;
	КнопкаРасширенногоОтбора.Заголовок = НСтр("ru = 'Расширенный отбор'");
	КнопкаРасширенногоОтбора.ЦветТекста = Новый Цвет;
	
	УстановленРасширенныйОтбор = УстановленРасширенныйОтбор(ВидимыеОтборы, КомпоновщикОтбор.Настройки.Отбор.Элементы);
	Если УстановленРасширенныйОтбор Тогда
		КнопкаРасширенногоОтбора.Заголовок = НСтр("ru = 'Установлен расширенный отбор'");
		КнопкаРасширенногоОтбора.ЦветТекста = ЦветаСтиля.ЦветТекстаУспех;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция УстановленРасширенныйОтбор(ВидимыеОтборы, ЭлементыОтбора)
	
	Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			Если УстановленРасширенныйОтбор(ВидимыеОтборы, ЭлементОтбора.Элементы) Тогда
				Возврат Истина;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		ВидимыОтбор = ВидимыеОтборы.Найти(Строка(ЭлементОтбора.ЛевоеЗначение)) <> Неопределено;
		ВидимыйОтборИзменен = ВидимыОтбор И ЭлементОтбора.ВидСравнения <> ВидСравненияКомпоновкиДанных.Равно
								И Строка(ЭлементОтбора.ЛевоеЗначение) <> "ТипРасчетов";
								
		Если ЭлементОтбора.Использование И (ВидимыйОтборИзменен ИЛИ НЕ ВидимыОтбор) Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура УстановитьТипРасчетовНаФорме()
	
	ФлагиОтборов = Новый Соответствие;
	ФлагиОтборов.Вставить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом, "РасчетыСКлиентами");
	ФлагиОтборов.Вставить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком, "РасчетыСПоставщиками");
	ФлагиОтборов.Вставить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСКредитором, "РасчетыСКредитором");
	ФлагиОтборов.Вставить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСДебитором, "РасчетыСДебитором");
	ФлагиОтборов.Вставить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСАрендодателем, "РасчетыСАрендодателем");
	
	ЭлементОтбора = ФинансоваяОтчетностьСервер.НайтиЭлементОтбора(КомпоновщикОтбор.Настройки.Отбор, "ТипРасчетов");
	УстановленОтбор = ЭлементОтбора <> Неопределено И ЭлементОтбора.Использование;
	Для Каждого ФлагОтбора Из ФлагиОтборов Цикл
		
		ЭтотОбъект[ФлагОтбора.Значение] = Ложь;
		Если УстановленОтбор Тогда
			Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке Тогда
				ЕстьВСписке = ЭлементОтбора.ПравоеЗначение.НайтиПоЗначению(ФлагОтбора.Ключ) <> Неопределено;
				ЭтотОбъект[ФлагОтбора.Значение] = ЕстьВСписке;
			ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
				ЭтотОбъект[ФлагиОтборов[ЭлементОтбора.ПравоеЗначение]] = Истина;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборНаФорме(ИмяОтбора)
	
	ЭлементОтбора = ФинансоваяОтчетностьСервер.НайтиЭлементОтбора(КомпоновщикОтбор.Настройки.Отбор, ИмяОтбора);
	Если ИмяОтбора = "Контрагент" И ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
		ЭлементОтбора = ФинансоваяОтчетностьСервер.НайтиЭлементОтбора(КомпоновщикОтбор.Настройки.Отбор, "Партнер");
	КонецЕсли;
	ЭтотОбъект[ИмяОтбора] = Неопределено;
	Элемент = Элементы[ИмяОтбора]; // ПолеФормы - 
	Элемент.Доступность = Истина;
	Элемент.ПодсказкаВвода = НСтр("ru = 'Укажите значение отбора'");
	
	Если ЭлементОтбора <> Неопределено Тогда
		
		Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно И ЭлементОтбора.Родитель = Неопределено Тогда
			Если ЭлементОтбора.Использование Тогда
				ЭтотОбъект[ИмяОтбора] = ЭлементОтбора.ПравоеЗначение;
				Если ИмяОтбора = "Контрагент" И ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов")
					И ТипЗнч(ЭлементОтбора.ПравоеЗначение) = Тип("СправочникСсылка.Партнеры") Тогда
					Контрагент = ПартнерыИКонтрагенты.ПолучитьКонтрагентаПартнераПоУмолчанию(ЭлементОтбора.ПравоеЗначение);
					ЭтотОбъект[ИмяОтбора] = Контрагент;
				КонецЕсли;
			ИначеЕсли ИмяОтбора = "Контрагент" И ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
				ЭлементОтбора = ФинансоваяОтчетностьСервер.НайтиЭлементОтбора(КомпоновщикОтбор.Настройки.Отбор, "ОрганизацияКонтрагент");
				Если ЭлементОтбора <> Неопределено
					И ЭлементОтбора.Использование 
					И ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно 
					И ЭлементОтбора.Родитель = Неопределено Тогда
					ЭтотОбъект[ИмяОтбора] = ЭлементОтбора.ПравоеЗначение;
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			
			ЭлементВИспользуемойГруппеОтбора = ЭлементОтбора.Родитель <> Неопределено И ЭлементОтбора.Родитель.Использование;
			Если ЭлементОтбора.Использование ИЛИ ЭлементВИспользуемойГруппеОтбора Тогда
				Элемент = Элементы[ИмяОтбора]; // ПолеФормы - 
				Элемент.Доступность = Ложь;
				Элемент.ПодсказкаВвода = НСтр("ru = 'Установлен расширенный отбор'");
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)

	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.СписокДокументовСверки);

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемВопросЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗадаватьВопросОЗакрытии = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВОтборЭлементОтбора(ОтборПриемник, ЭлементОтбора)
	НовыйЭлемент = ОтборПриемник.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЗаполнитьЗначенияСвойств(НовыйЭлемент,ЭлементОтбора);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВОтборГруппуЭлементовОтбора(ОтборПриемник, ГруппаЭлементов)
	
	НоваяГруппа = ОтборПриемник.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ЗаполнитьЗначенияСвойств(НоваяГруппа,ГруппаЭлементов);
	
	Для Каждого ЭлементОтбора Из ГруппаЭлементов.Элементы Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			ДобавитьВОтборЭлементОтбора(НоваяГруппа, ЭлементОтбора);
		Иначе
			ДобавитьВОтборГруппуЭлементовОтбора(НоваяГруппа, ЭлементОтбора);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНастройкиОтборовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КомпоновщикОтбор.ЗагрузитьНастройки(Результат.НастройкиОтбора);
	ОбновитьОтборыНаФорме();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусНаСверкеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	ВыделенныеСтроки = ДополнительныеПараметры.ВыделенныеСтроки;

	Ответ = РезультатВопроса;
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;

	ОчиститьСообщения();
	КоличествоОбработанных = ОбщегоНазначенияУТВызовСервера.УстановитьСтатусДокументов(ВыделенныеСтроки, "НаСверке");
	ОбщегоНазначенияУТКлиент.ОповеститьПользователяОбУстановкеСтатуса(Элементы.СписокДокументовСверки, КоличествоОбработанных, ВыделенныеСтроки.Количество(), НСтр("ru='На сверке'"));
	Элементы.СписокДокументовСверки.Обновить();

КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервалПериодаЗавершение(Период, ДополнительныеПараметры) Экспорт 
	
	ПериодСверки.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод;
	ПериодСверки.ДатаНачала = НачалоПериода;
	ПериодСверки.ДатаОкончания = КонецДня(КонецПериода);
	
КонецПроцедуры

#Область ОбслуживаниеДлительныхОпераций

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	Если ЗначениеЗаполнено(ИдентификаторЗадания) И ЗаданиеВыполнено(ИдентификаторЗадания) Тогда 
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		Элементы.СписокДокументовСверки.Обновить();
	КонецЕсли;
	
	Если Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаНастроек Тогда
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗаполнитьТаблицуРасчетовЗавершение", ЭтотОбъект);
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаТаблицаРасчетов Тогда
		ОповещениеОЗавершении = Новый ОписаниеОповещения("СоздатьДокументыЗавершение", ЭтотОбъект);
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаРезультат Тогда
		ОповещениеОЗавершении = Новый ОписаниеОповещения("УстановитьПометкуНаУдалениеЗавершение", ЭтотОбъект);
	КонецЕсли;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаСервере
Процедура ОтменитьВыполнениеЗадания(ИдентификаторЗадания)
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
		ИдентификаторЗадания = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти
