#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
КонецПроцедуры

// Добавляет команду создания документа "Сверка взаиморасчетов".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
// 	Неопределено - Описание
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.СверкаВзаиморасчетов2_5_11) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.СверкаВзаиморасчетов2_5_11.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.СверкаВзаиморасчетов2_5_11);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Отчеты.КарточкаРасчетовПоФинансовымИнструментам.ДобавитьКомандуОтчета(КомандыОтчетов);
	ВзаиморасчетыСервер.ВедомостьРасчетовСПартнерами_ДобавитьКомандуОтчета(КомандыОтчетов);
	ВзаиморасчетыСервер.ВедомостьРасчетовМеждуОрганизациями_ДобавитьКомандуОтчета(КомандыОтчетов);
	
КонецПроцедуры

// Заполняет массивы реквизитов, зависимых от хозяйственной операции документа.
//
// Параметры:
//	СтруктураПараметров - Структура - Структура параметров заполнения:
//		* РасшифровкаПоЗаказам - Булево - Признак расшифровки по заказам.
//		* РасшифровкаПоПартнерам - Булево - Признак расшифровки по партнерам.
//		* РасшифровкаПоДоговорам - Булево - Признак расшифровки по договорам.
//		* Партнер - СправочникСсылка.Партнеры - Партнер документа.
//		* Договор - СправочникСсылка.ДоговорыКонтрагентов - Договор документа.
//	МассивВсехРеквизитов - Массив из Строка - Массив всех имен реквизитов, зависимых от хозяйственной операции.
//	МассивРеквизитовОперации - Массив из Строка - Массив имен реквизитов, используемых в выбранной хозяйственной операции.
//
Процедура ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(СтруктураПараметров, МассивВсехРеквизитов, МассивРеквизитовОперации) Экспорт
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("ИтоговыеЗаписи.ТипРасчетов");
	МассивВсехРеквизитов.Добавить("ИтоговыеЗаписи.Договор");
	
	МассивРеквизитовОперации = Новый Массив;
	Если СтруктураПараметров.ПоказатьТипРасчетов Тогда
		МассивРеквизитовОперации.Добавить("ИтоговыеЗаписи.ТипРасчетов");
	КонецЕсли;
	Если СтруктураПараметров.ПоказатьДоговор Тогда
		МассивРеквизитовОперации.Добавить("ИтоговыеЗаписи.Договор");
	КонецЕсли;
	
КонецПроцедуры

// Определяет реквизиты выбранного документа.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.СверкаВзаиморасчетов2_5_11 - Ссылка на документ.
//
// Возвращаемое значение:
//	Неопределено, ВыборкаИзРезультатаЗапроса - реквизиты выбранного документа.
//
Функция РеквизитыДокумента(ДокументСсылка) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.Контрагент,
	|	ДанныеДокумента.НачалоПериода,
	|	ДанныеДокумента.КонецПериода,
	|	МАКСИМУМ(СверкаВзаиморасчетовИтоговыеЗаписи.ТипРасчетов) КАК ТипРасчетов,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СверкаВзаиморасчетовИтоговыеЗаписи.ТипРасчетов) КАК ВсегоТиповРасчетов
	|ИЗ
	|	Документ.СверкаВзаиморасчетов2_5_11 КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СверкаВзаиморасчетов2_5_11.ИтоговыеЗаписи КАК СверкаВзаиморасчетовИтоговыеЗаписи
	|		ПО СверкаВзаиморасчетовИтоговыеЗаписи.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &ДокументСсылка
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.Контрагент,
	|	ДанныеДокумента.НачалоПериода,
	|	ДанныеДокумента.КонецПериода
	|");
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции

// Формирует запрос проверки при смене статуса списка документов
//
// Параметры:
//	МассивДокументов - Массив из ДокументСсылка.СверкаВзаиморасчетов2_5_11- Массив ссылок на документы, которые надо проверять.
//	НовыйСтатус - Строка - Имя нового статуса.
//	ДополнительныеПараметры - Структура - Структура дополнительных параметров.
//
// Возвращаемое значение:
//	Запрос - Запрос проверки перед сменой статуса.
//
Функция СформироватьЗапросПроверкиПриСменеСтатуса(МассивДокументов, НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	ЗначениеНовогоСтатуса = Перечисления.СтатусыСверокВзаиморасчетов2_5_11[НовыйСтатус];
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокументов.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаДокументов.Ссылка) КАК Представление,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаДокументов.Статус) КАК ПредставлениеТекущегоСтатуса,
	|	ПРЕДСТАВЛЕНИЕ(&Статус) КАК ПредставлениеНовогоСтатуса,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументов.Статус = &Статус
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СтатусСовпадает,
	|	ТаблицаДокументов.Проведен КАК Проведен,
	|	ТаблицаДокументов.ПометкаУдаления КАК ПометкаУдаления,
	|	ИСТИНА КАК ЗаписьПроведением
	|ИЗ
	|	Документ.СверкаВзаиморасчетов2_5_11 КАК ТаблицаДокументов
	|ГДЕ
	|	ТаблицаДокументов.Ссылка В(&МассивДокументов)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Статус", ЗначениеНовогоСтатуса);
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	Возврат Запрос;
	
КонецФункции

// Возвращает результат проверки при смене статуса документа
//
// Параметры:
//	ВыборкаПроверки - ВыборкаИзРезультатаЗапроса - Текущая строка выборки.
//	НовыйСтатус - ПеречислениеСсылка - Новый статус.
//	ДополнительныеПараметры - Структура - Структура дополнительных параметров.
//
// Возвращаемое значение:
//	Булево - Истина, в случае успешного завершения проверки.
//
Функция ПроверкаПередСменойСтатуса(ВыборкаПроверки, НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	Возврат Истина; // Для документа "СверкаВзаиморасчетов" отсутствуют дополнительные проверки
	
КонецФункции

// Возвращает КлючОбъекта для сохранения в хранилище общих настроек пользователя
// 
// Возвращаемое значение:
// 	Строка - Описание
Функция КлючОбъектаПользовательскихНастроек() Экспорт
	
	Возврат "Документ.СверкаВзаиморасчетов2_5_11";
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
//
// Параметры:
//	Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И (ЗначениеРазрешено(Контрагент ТОЛЬКО Справочник.Организации)
	|		ИЛИ ЗначениеРазрешено(Контрагент.Партнер) 
	|			И ДляВсехСтрок(ЗначениеРазрешено(ИтоговыеЗаписи.Партнер, ПустаяСсылка КАК ИСТИНА, Null КАК ИСТИНА))
	|	)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	// Акт сверки взаимных расчетов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "УправлениеПечатьюУТКлиент.ПечатьАктаСверкиВзаиморасчетов";
	КомандаПечати.Идентификатор = "АктСверкиВзаимныхРасчетов";
	КомандаПечати.Представление = НСтр("ru = 'Акт сверки взаимных расчетов'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

// Сформировать печатные формы объектов
//
// Параметры:
//   МассивОбъектов - см. УправлениеПечатьюПереопределяемый.ПриПечати.МассивОбъектов
//   ПараметрыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыПечати
//   КоллекцияПечатныхФорм - см. УправлениеПечатьюПереопределяемый.ПриПечати.КоллекцияПечатныхФорм
//   ОбъектыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ОбъектыПечати
//   ПараметрыВывода - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыВывода
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктСверкиВзаимныхРасчетов") Тогда
		
		ДокументыНаПечать = ДокументыНаПечать(МассивОбъектов);
		
		МакетНаПечать = Новый ТабличныйДокумент;
		Если ДокументыНаПечать.Количество() > 0 Тогда
			МакетНаПечать = СформироватьПечатнуюФормуАктСверкиВзаиморасчетов(ДокументыНаПечать, ОбъектыПечати);
		КонецЕсли;
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
			"АктСверкиВзаимныхРасчетов",
			НСтр("ru='Акт сверки взаимных расчетов'"),
			МакетНаПечать);
			
		ФормированиеПечатныхФорм.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, ДокументыНаПечать, КоллекцияПечатныхФорм);
		
	КонецЕсли;
	
КонецПроцедуры

// Сформировать печатную форму акта сверки взаиморасчетов
//
// Параметры:
//   МассивОбъектов  - Массив из ДокументСсылка.СверкаВзаиморасчетов2_5_11 - Массив ссылок на объекты которые нужно распечатать.
//   ОбъектыПечати - СписокЗначений -
//   ДляОтладки - Булево, Структура -
//
// Возвращаемое значение:
//  ТабличныйДокумент - Сформировать печатную форму акт сверки взаиморасчетов
Функция СформироватьПечатнуюФормуАктСверкиВзаиморасчетов(МассивОбъектов, ОбъектыПечати, ДляОтладки = Неопределено) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	НастройкиПечати = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
							КлючОбъектаПользовательскихНастроек(),
							"НастройкиПечати");
	Если НастройкиПечати = Неопределено Тогда
		НастройкиПечати = НастройкиПечатиПоУмолчанию();
	КонецЕсли;
	Если ТипЗнч(ДляОтладки) = Тип("Структура") Тогда
		НастройкиПечати.Вставить("ДляОтладки", ДляОтладки);
	КонецЕсли;
	
	ЗапросДанныеНаПечать = ЗапросДанныеНаПечать(МассивОбъектов, НастройкиПечати);
	РезультатЗапроса = ЗапросДанныеНаПечать.Выполнить();
	ОбщиеИтогиПоОбъектуГруппировки = ЗапросДанныеНаПечать.МенеджерВременныхТаблиц.Таблицы["втОбщиеИтогиПоОбъектуГруппировки"].ПолучитьДанные().Выгрузить();
	ОбщиеИтогиПоОбъектуГруппировки.Свернуть("ДокументСсылка,ОбъектГруппировки,ВалютаРегл,ВалютаСверки,ТипРасчетов,СверкаПоДоговорам",
		"НачальноеСальдоДт,НачальноеСальдоКт,ОборотДт,ОборотКт,КонечноеСальдоДт,КонечноеСальдоКт,"
		+"НачальноеСальдоДтКонтрагент,НачальноеСальдоКтКонтрагент,ОборотДтКонтрагент,ОборотКтКонтрагент,КонечноеСальдоДтКонтрагент,КонечноеСальдоКтКонтрагент,"
		+"НачальноеСальдоДолг,НачальноеСальдоАванс,КонечноеСальдоДолг,КонечноеСальдоАванс,"
		+"НачальноеСальдоДолгКонтрагент,НачальноеСальдоАвансКонтрагент,КонечноеСальдоДолгКонтрагент,КонечноеСальдоАвансКонтрагент");
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ПервыйДокумент = Истина;
	ДанныеСверки = РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	Для Каждого ДанныеДокумента Из ДанныеСверки.Строки Цикл
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.СверкаВзаиморасчетов2_5_11.ПФ_MXL_АктСверкиВзаимныхРасчетов"); // ТабличныйДокумент - 
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		#Область ШапкаДокумента
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьМакета, ДанныеДокумента.ДокументСсылка);
		
		ТекстЗаголовка = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(ДанныеДокумента, НСтр("ru='Акт сверки взаимных расчетов'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
		ЕстьОстаткиОборотыЗаПериод = ДанныеДокумента.ЕстьОбороты ИЛИ ДанныеДокумента.НачальноеСальдоДт <> 0 ИЛИ ДанныеДокумента.НачальноеСальдоКт <> 0
			ИЛИ ДанныеДокумента.КонечноеСальдоДт <> 0 ИЛИ ДанныеДокумента.КонечноеСальдоКт <> 0;
		
		Если ДанныеДокумента.НачалоПериода = '00010101' Тогда
			
			ПредставлениеПериода = СтрШаблон(НСтр("ru = 'по состоянию на %1'", ОбщегоНазначения.КодОсновногоЯзыка()), Формат(ДанныеДокумента.КонецПериода, "ДЛФ=ДД"),);
			
			ПредставлениеПериодаРасчетов = СтрШаблон(НСтр("ru = 'По состоянию на %1'", ОбщегоНазначения.КодОсновногоЯзыка()), Формат(ДанныеДокумента.КонецПериода, "ДЛФ=ДД"),);
			
		Иначе
			
			ПредставлениеПериода = ПредставлениеПериода(ДанныеДокумента.НачалоПериода, КонецДня(ДанныеДокумента.КонецПериода));

			ПервыйСимволПредставлениеПериода = Лев(ПредставлениеПериода, 1);
			Если  КодСимвола(ПервыйСимволПредставлениеПериода) >= 48
				И КодСимвола(ПервыйСимволПредставлениеПериода) <= 57 Тогда
				ПредставлениеПериода = СтрШаблон(НСтр("ru='за период: %1'", ОбщегоНазначения.КодОсновногоЯзыка()), ПредставлениеПериода);
				
			Иначе
				ПредставлениеПериода = СтрШаблон(НСтр("ru='за %1'", ОбщегоНазначения.КодОсновногоЯзыка()), ПредставлениеПериода);
				
			КонецЕсли;
			
			ШаблонПериодаРасчетов = НСтр("ru = 'В период с %1 по %2 были осуществлены следующие расчеты'", ОбщегоНазначения.КодОсновногоЯзыка())+":";
			Если НЕ ЕстьОстаткиОборотыЗаПериод Тогда
				ШаблонПериодаРасчетов = НСтр("ru = 'В период с %1 по %2 операции по расчетам не производились.'", ОбщегоНазначения.КодОсновногоЯзыка());
			КонецЕсли;
			
			ПредставлениеПериодаРасчетов = СтрШаблон(
				ШаблонПериодаРасчетов,
				Формат(ДанныеДокумента.НачалоПериода, "ДЛФ=ДД"),
				Формат(ДанныеДокумента.КонецПериода, "ДЛФ=ДД"),);
			
		КонецЕсли;

		СведенияОбОрганизации = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеДокумента.Организация, ДанныеДокумента.Дата);
		ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, 
									"ПолноеНаименование");
		СведенияОКонтрагенте  = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеДокумента.Контрагент,  ДанныеДокумента.Дата);
		КонтрагентНаименование = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОКонтрагенте, 
								  "ПолноеНаименование");
		
		СтруктураПредставленийУчастников = Новый Структура;
		СтруктураПредставленийУчастников.Вставить("ПредставлениеОрганизации", ПредставлениеОрганизации);
		СтруктураПредставленийУчастников.Вставить("КонтрагентНаименование", КонтрагентНаименование);
		
		ОбластьМакета.Параметры.Заполнить(ДанныеДокумента);
		ОбластьМакета.Параметры.Заполнить(СтруктураПредставленийУчастников);
		ОбластьМакета.Параметры.ТекстЗаголовкаМакета         = ТекстЗаголовка;
		ОбластьМакета.Параметры.ФИОРуководителяКонтрагента   = СокрЛП(ДанныеДокумента.ФИОРуководителяКонтрагента);
		ОбластьМакета.Параметры.ПредставлениеПериода         = ПредставлениеПериода;
		ОбластьМакета.Параметры.ПредставлениеПериодаРасчетов = ПредставлениеПериодаРасчетов;
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		#КонецОбласти
		
		#Область ДетальныеЗаписи
		ДанныеДокумента.Строки.Сортировать("ПредставлениеТипРасчетов");
		ТипЧисло = ОбщегоНазначенияУТ.ОписаниеТипаДенежногоПоля();
		ИтогоЗадолженность = Новый ТаблицаЗначений;
		ИтогоЗадолженность.Колонки.Добавить("Валюта");
		ИтогоЗадолженность.Колонки.Добавить("СуммаДт", ТипЧисло);
		ИтогоЗадолженность.Колонки.Добавить("СуммаКт", ТипЧисло);
		
		НастройкиПечати.Вставить("ПредставленияУчастников", СтруктураПредставленийУчастников);
		
		#Область ПараметрыВывода
		ДанныеВывода = Новый Структура();
		ДанныеВывода.Вставить("ДанныеДокумента", ДанныеДокумента);
		ДанныеВывода.Вставить("Макет", Макет);
		ДанныеВывода.Вставить("ИтогоЗадолженность", ИтогоЗадолженность);
		ДанныеВывода.Вставить("НастройкиПечати", НастройкиПечати);
		#КонецОбласти
		Для Каждого ДанныеПоТипуРасчетов Из ДанныеДокумента.Строки Цикл
			ДанныеПоТипуРасчетов.Строки.Сортировать("ПредставлениеОбъектГруппировки");
			Для Каждого ДанныеПоОбъектуГруппировки Из ДанныеПоТипуРасчетов.Строки Цикл
				Отбор = Новый Структура("ДокументСсылка,ТипРасчетов,ВалютаСверки,ОбъектГруппировки");
				ЗаполнитьЗначенияСвойств(Отбор, ДанныеПоОбъектуГруппировки);
				НайденныеИтоги = ОбщиеИтогиПоОбъектуГруппировки.НайтиСтроки(Отбор);
				Если НайденныеИтоги.Количество() > 0 Тогда
					ЗаполнитьЗначенияСвойств(ДанныеПоОбъектуГруппировки, НайденныеИтоги[0]);
				КонецЕсли;
				ЕстьДанныеДоговора = ДанныеПоОбъектуГруппировки.НачальноеСальдоДт <> 0
										ИЛИ ДанныеПоОбъектуГруппировки.НачальноеСальдоКт <> 0
										ИЛИ ДанныеПоОбъектуГруппировки.ЕстьОбороты;
				Если ЕстьДанныеДоговора Тогда
					ТабличныйДокумент.Вывести(ВывестиДетальныеЗаписи(ДанныеПоОбъектуГруппировки, ДанныеВывода));
				КонецЕсли;
			КонецЦикла; // по детальным записям
		КонецЦикла; // по видам расчетов Взаиморасчеты/ФинансовыеИнструменты
		#КонецОбласти
		
		#Область Задолженность
		Если ИтогоЗадолженность.Количество() > 0 Тогда
			
			ОбластьМакетаЗаголовок	= Макет.ПолучитьОбласть("ЗаголовокЗадолженность");
			ОбластьДолг				= Макет.ПолучитьОбласть("СтрокаЗадолженность");
			ОбластьПустаяСтрока		= Макет.ПолучитьОбласть("ПустаяСтрока");
			
			ОбластьМакетаЗаголовок.Параметры.КонецПериодаСверки = Формат(ДанныеДокумента.КонецПериода, "ДЛФ=ДД");
			ТабличныйДокумент.Вывести(ОбластьМакетаЗаголовок);
			ИспользоватьПартнеровИКонтрагентов = ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровИКонтрагентов");
			
			ЗадолженностьПоВалютам = ИтогоЗадолженность.Скопировать(); // ТаблицаЗначений
			ЗадолженностьПоВалютам.Свернуть("Валюта", "СуммаДт,СуммаКт");
			Для Каждого ДолгВВалюте Из ЗадолженностьПоВалютам Цикл
				
				ВывестиПустуюСтроку = Истина;
				ПредставлениеПартнера = ДанныеДокумента.ПредставлениеПартнера;
				Если НЕ ЗначениеЗаполнено(ДанныеДокумента.Партнер)
						ИЛИ НЕ ИспользоватьПартнеровИКонтрагентов Тогда
					ПредставлениеПартнера = СтруктураПредставленийУчастников.КонтрагентНаименование;
				КонецЕсли;
				
				Если ДолгВВалюте.СуммаДт <> 0 И ДолгВВалюте.СуммаКт <> 0 Тогда
					РассчитатьСальдо(ДолгВВалюте, "Сумма");
				КонецЕсли;
				
				ОбластьДолг.Параметры.Валюта = ДолгВВалюте.Валюта;
				
				// Долг партнера
				СуммаДолга = 0;
				Если ДолгВВалюте.СуммаДт > 0 Тогда
					СуммаДолга = ДолгВВалюте.СуммаДт;
					ОбластьДолг.Параметры.Сумма = Формат(ДолгВВалюте.СуммаДт, "ЧДЦ=2; ЧН=0; ЧГ=3,0");
					ОбластьДолг.Параметры.Дебитор = ПредставлениеПартнера;
				Иначе
					// Наш долг
					СуммаДолга = ДолгВВалюте.СуммаКт;
					ОбластьДолг.Параметры.Сумма = Формат(ДолгВВалюте.СуммаКт, "ЧДЦ=2; ЧН=0; ЧГ=3,0");
					ОбластьДолг.Параметры.Дебитор = СтрШаблон(
							НСтр("ru='%1 перед %2'"),
							СтруктураПредставленийУчастников.ПредставлениеОрганизации,
							ПредставлениеПартнера);
				КонецЕсли;
				ОбластьДолг.Параметры.СуммаПрописью = РаботаСКурсамиВалютУТ.СформироватьСуммуПрописью(СуммаДолга,ДолгВВалюте.Валюта);
				ТабличныйДокумент.Вывести(ОбластьДолг);
				
				Если ВывестиПустуюСтроку Тогда
					ТабличныйДокумент.Вывести(ОбластьПустаяСтрока);
				КонецЕсли;
			КонецЦикла; 
			
		Иначе
		
			ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокЗадолженностьОтсутствует");
			ОбластьМакета.Параметры.КонецПериодаСверки = Формат(ДанныеДокумента.КонецПериода, "ДЛФ=ДД");
			ТабличныйДокумент.Вывести(ОбластьМакета);
		
		КонецЕсли;
		#КонецОбласти 
		
		// РАСХОЖДЕНИЙ НЕ ВЫЯВЛЕНО
		ДанныеКонтрагентаЗаполнены = НЕ НастройкиПечати.НеЗаполнятьДанныеКонтрагента;
		Если НЕ ДанныеДокумента.ЕстьРасхождения И ДанныеКонтрагентаЗаполнены Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокРасхождения");
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЕсли;
		
		#Область Подписи
		ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
		ОбластьМакета.Параметры.Заполнить(ДанныеДокумента);
		ОбластьМакета.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, 
												  			  "ПолноеНаименование,ИНН,ЮридическийАдрес");
		ОбластьМакета.Параметры.КонтрагентНаименование   = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОКонтрагенте, 
												  			  "ПолноеНаименование,ИНН,ЮридическийАдрес");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		#КонецОбласти
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеДокумента.ДокументСсылка);
	
	КонецЦикла; // конец цикла по данным документа

	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Если ТипЗнч(ДляОтладки) = Тип("Структура") Тогда
		ДляОтладки.Вставить("ТабличныйДокумент", ТабличныйДокумент);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ВывестиДетальныеЗаписи(ДанныеПоОбъектуГруппировки, ДанныеВывода)
	
	Макет = ДанныеВывода.Макет;
	ИтогоЗадолженность = ДанныеВывода.ИтогоЗадолженность;
	НастройкиПечати = ДанныеВывода.НастройкиПечати;
	
	СтруктураПредставленийУчастников = НастройкиПечати.ПредставленияУчастников;
	
	ЭтоФинансовыеИнструменты = ДанныеПоОбъектуГруппировки.ФинансовыеИнструменты;
	ВыводитьДолгАванс = НЕ ЭтоФинансовыеИнструменты И НастройкиПечати.ДебетКакДолг;
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	#Область ОбластиПечати
	ОбластьМакетаГруппировка = Макет.ПолучитьОбласть("ГруппировкаОбъектДоговорВалюта");
	ОбластьМакетаШапка       = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьМакетаНачОстаток  = Макет.ПолучитьОбласть("СтрокаТаблицыНачОстаток");
	ОбластьМакетаСтрока      = Макет.ПолучитьОбласть("СтрокаТаблицы");
	ОбластьМакетаОбороты     = Макет.ПолучитьОбласть("ОборотыЗаПериод");
	ОбластьМакетаКонОстаток  = Макет.ПолучитьОбласть("СтрокаТаблицыКонОстаток");
	Если НастройкиПечати.НеЗаполнятьДанныеКонтрагента Тогда
		ОбластьМакетаНачОстаток.Область("СуммаКонтрагентНачОстаток").Формат = "ЧДЦ=2";
		ОбластьМакетаСтрока.Область("СуммаКонтрагент").Формат = "ЧДЦ=2";
		ОбластьМакетаОбороты.Область("СуммаКонтрагентОбороты").Формат = "ЧДЦ=2";
		ОбластьМакетаКонОстаток.Область("СуммаКонтрагентКонОстаток").Формат = "ЧДЦ=2";
	КонецЕсли;
	#КонецОбласти
	
	#Область ВыводНазванияГруппировки
	Валюта = СтрШаблон(НСтр("ru='в валюте'", ОбщегоНазначения.КодОсновногоЯзыка()) + " %1", ДанныеПоОбъектуГруппировки.ПредставлениеВалюта);
	Расчеты = НСтр("ru = 'Расчеты'", ОбщегоНазначения.КодОсновногоЯзыка());
	ШаблонЗаголовка = ?(НЕ ЗначениеЗаполнено(ДанныеПоОбъектуГруппировки.ПредставлениеТипРасчетов), Расчеты, ДанныеПоОбъектуГруппировки.ПредставлениеТипРасчетов) + " ";
	ТекстГруппировкиПоДоговору = НСтр("ru = 'по договору'", ОбщегоНазначения.КодОсновногоЯзыка()) + " %1 ";
	Если НастройкиПечати.ДетализацияВзаиморасчетов = "ОбъектРасчетов" Тогда
		Если ТипЗнч(ДанныеПоОбъектуГруппировки.ОбъектГруппировки) = Тип("СправочникСсылка.ОбъектыРасчетов") Тогда
			ШаблонЗаголовка = ШаблонЗаголовка + НСтр("ru = 'по объекту расчетов'", ОбщегоНазначения.КодОсновногоЯзыка()) + " %1 ";
		Иначе
			ШаблонЗаголовка = ШаблонЗаголовка + ТекстГруппировкиПоДоговору;
		КонецЕсли;
	ИначеЕсли НастройкиПечати.ДетализацияВзаиморасчетов = "Договор" Тогда
		ШаблонЗаголовка = ШаблонЗаголовка + ТекстГруппировкиПоДоговору;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеПоОбъектуГруппировки.ПредставлениеОбъектГруппировки) Тогда
		ТекстОбъектаГруппировки = ДанныеПоОбъектуГруппировки.ПредставлениеОбъектГруппировки;
	Иначе
		ТекстОбъектаГруппировки = "<" + НСтр("ru = 'не указан'", ОбщегоНазначения.КодОсновногоЯзыка()) + ">";
	КонецЕсли;
	
	ЗаголовокГруппировки = ШаблонЗаголовка + Валюта;
	Если НастройкиПечати.ДетализацияВзаиморасчетов <> "Валюта" Тогда
		ШаблонЗаголовка = ?(НЕ ЗначениеЗаполнено(ШаблонЗаголовка), "%1 ", ШаблонЗаголовка);
		ЗаголовокГруппировки = СтрШаблон(ШаблонЗаголовка, ТекстОбъектаГруппировки) + Валюта;
	КонецЕсли;
	ОбластьМакетаГруппировка.Параметры.ЗаголовокГруппировки = ЗаголовокГруппировки;
	ТабличныйДокумент.Вывести(ОбластьМакетаГруппировка);
	#КонецОбласти
	
	#Область ВыводШапкиТаблицы
	ОбластьМакетаШапка.Параметры.Заполнить(СтруктураПредставленийУчастников);
	ОбластьМакетаШапка.Параметры.Дебет = НСтр("ru = 'Дебет'", ОбщегоНазначения.КодОсновногоЯзыка());
	ОбластьМакетаШапка.Параметры.Кредит = НСтр("ru = 'Кредит'", ОбщегоНазначения.КодОсновногоЯзыка());
	ОбластьМакетаНачОстаток.Параметры.ИмяОстатка = НСтр("ru = 'Сальдо начальное'", ОбщегоНазначения.КодОсновногоЯзыка());
	ОбластьМакетаКонОстаток.Параметры.ИмяОстатка = НСтр("ru = 'Сальдо конечное'", ОбщегоНазначения.КодОсновногоЯзыка());
	Если НастройкиПечати.ДебетКакДолг Тогда
		ОбластьМакетаШапка.Параметры.Дебет = НСтр("ru = 'Долг'", ОбщегоНазначения.КодОсновногоЯзыка());
		ОбластьМакетаШапка.Параметры.Кредит = НСтр("ru = 'Аванс'", ОбщегоНазначения.КодОсновногоЯзыка());
		ОбластьМакетаНачОстаток.Параметры.ИмяОстатка = НСтр("ru = 'Начальный остаток'", ОбщегоНазначения.КодОсновногоЯзыка());
		ОбластьМакетаКонОстаток.Параметры.ИмяОстатка = НСтр("ru = 'Конечный остаток'", ОбщегоНазначения.КодОсновногоЯзыка());
	КонецЕсли;
	ТабличныйДокумент.Вывести(ОбластьМакетаШапка);
	#КонецОбласти
	
	#Область ВыводНачальногоОстатка
	СвернутьСальдо(ДанныеПоОбъектуГруппировки.НачальноеСальдоДт, ДанныеПоОбъектуГруппировки.НачальноеСальдоКт);
	ОбластьМакетаНачОстаток.Параметры.СуммаДебет = ДанныеПоОбъектуГруппировки.НачальноеСальдоДт;
	ОбластьМакетаНачОстаток.Параметры.СуммаКредит = ДанныеПоОбъектуГруппировки.НачальноеСальдоКт;
	
	СвернутьСальдо(ДанныеПоОбъектуГруппировки.НачальноеСальдоДтКонтрагент, ДанныеПоОбъектуГруппировки.НачальноеСальдоКтКонтрагент);
	ОбластьМакетаНачОстаток.Параметры.СуммаДебетКонтрагент = ДанныеПоОбъектуГруппировки.НачальноеСальдоДтКонтрагент;
	ОбластьМакетаНачОстаток.Параметры.СуммаКредитКонтрагент = ДанныеПоОбъектуГруппировки.НачальноеСальдоКтКонтрагент;
	Если ВыводитьДолгАванс Тогда
		ОчиститьДебетКредит(ОбластьМакетаНачОстаток);
		СвернутьСальдо(ДанныеПоОбъектуГруппировки.НачальноеСальдоДолг, ДанныеПоОбъектуГруппировки.НачальноеСальдоАванс);
		ОбластьМакетаНачОстаток.Параметры.СуммаДебет = ДанныеПоОбъектуГруппировки.НачальноеСальдоДолг;
		ОбластьМакетаНачОстаток.Параметры.СуммаКредит = ДанныеПоОбъектуГруппировки.НачальноеСальдоАванс;
		
		СвернутьСальдо(ДанныеПоОбъектуГруппировки.НачальноеСальдоДолгКонтрагент, ДанныеПоОбъектуГруппировки.НачальноеСальдоАвансКонтрагент);
		ОбластьМакетаНачОстаток.Параметры.СуммаДебетКонтрагент = ДанныеПоОбъектуГруппировки.НачальноеСальдоДолгКонтрагент;
		ОбластьМакетаНачОстаток.Параметры.СуммаКредитКонтрагент = ДанныеПоОбъектуГруппировки.НачальноеСальдоАвансКонтрагент;
	КонецЕсли;
	Если НастройкиПечати.НеЗаполнятьДанныеКонтрагента Тогда
		ОбластьМакетаНачОстаток.Параметры.СуммаДебетКонтрагент = 0;
		ОбластьМакетаНачОстаток.Параметры.СуммаКредитКонтрагент = 0;
	КонецЕсли;
	ТабличныйДокумент.Вывести(ОбластьМакетаНачОстаток);
	#КонецОбласти
	
	РасчетныеДокументыПериода = Новый Массив;
	ШаблонДокумента = НСтр("ru = '%ИмяДокумента% №%Номер% от %Дата%'");
	Если ДанныеПоОбъектуГруппировки.ЕстьОбороты Тогда
		Для Каждого ДетальныеЗаписи Из ДанныеПоОбъектуГруппировки.Строки Цикл
	
			Если ТипЗнч(ДетальныеЗаписи.РасчетныйДокумент) = Тип("ДокументСсылка.ВзаимозачетЗадолженности")
				И ДетальныеЗаписи.СуммаДебет - ДетальныеЗаписи.СуммаКредит = 0 Тогда
				Продолжить;
			КонецЕсли;
			ОписаниеДокумента = СтрЗаменить(ШаблонДокумента, "%ИмяДокумента%", ДетальныеЗаписи.НаименованиеДокумента);
			Если ЗначениеЗаполнено(ДетальныеЗаписи.НомерДокумента) Тогда
				ОписаниеДокумента = СтрЗаменить(ОписаниеДокумента, "%Номер%", ДетальныеЗаписи.НомерДокумента);
			Иначе
				ОписаниеДокумента = СтрЗаменить(ОписаниеДокумента, "%Номер%", "______");
			КонецЕсли;
			Если ЗначениеЗаполнено(ДетальныеЗаписи.ДатаДокумента) Тогда
				ОписаниеДокумента = СтрЗаменить(ОписаниеДокумента, "%Дата%", Формат(ДетальныеЗаписи.ДатаДокумента, "ДЛФ=D"));
			Иначе
				ОписаниеДокумента = СтрЗаменить(ОписаниеДокумента, "%Дата%", "  .  .  ");
			КонецЕсли;
			Если ЗначениеЗаполнено(ДетальныеЗаписи.СвязанныеДокументы) Тогда
				ОписаниеДокумента = ОписаниеДокумента + " (" + ДетальныеЗаписи.СвязанныеДокументы + ")";
			КонецЕсли;
			
			РасчетныеДокументыПериода.Добавить(ДетальныеЗаписи.РасчетныйДокумент);
			
			ОбластьМакетаСтрока = Макет.ПолучитьОбласть("СтрокаТаблицы");
			Если НастройкиПечати.НеЗаполнятьДанныеКонтрагента Тогда
				ОбластьМакетаСтрока.Область("СуммаКонтрагент").Формат = "ЧДЦ=2";
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(ОбластьМакетаСтрока.Параметры, ДетальныеЗаписи);
			ОбластьМакетаСтрока.Параметры.Дата = Формат(ДетальныеЗаписи.ДатаОперации, "ДЛФ=D");
			ОбластьМакетаСтрока.Параметры.ОписаниеДокумента = ОписаниеДокумента;
			Если ВыводитьДолгАванс Тогда
				ОчиститьДебетКредит(ОбластьМакетаСтрока);
				ОбластьМакетаСтрока.Параметры.СуммаДебет = ДетальныеЗаписи.СуммаДолг;
				ОбластьМакетаСтрока.Параметры.СуммаКредит = ДетальныеЗаписи.СуммаАванс;
				ОбластьМакетаСтрока.Параметры.СуммаДебетКонтрагент = ДетальныеЗаписи.СуммаДолгКонтрагент;
				ОбластьМакетаСтрока.Параметры.СуммаКредитКонтрагент = ДетальныеЗаписи.СуммаАвансКонтрагент;
			КонецЕсли;
			Если НастройкиПечати.НеЗаполнятьДанныеКонтрагента Тогда
				ОбластьМакетаСтрока.Параметры.СуммаДокументаКонтрагент = 0;
				ОбластьМакетаСтрока.Параметры.СуммаДебетКонтрагент = 0;
				ОбластьМакетаСтрока.Параметры.СуммаКредитКонтрагент = 0;
			КонецЕсли;
			ТабличныйДокумент.Вывести(ОбластьМакетаСтрока);
		КонецЦикла; 
	КонецЕсли;
	
	#Область ВыводОборотовЗаПериод
	ЗаполнитьЗначенияСвойств(ОбластьМакетаОбороты.Параметры, ДанныеПоОбъектуГруппировки);
	ОбластьМакетаОбороты.Параметры.СуммаДебет = ДанныеПоОбъектуГруппировки.СуммаДебет;
	ОбластьМакетаОбороты.Параметры.СуммаКредит = ДанныеПоОбъектуГруппировки.СуммаКредит;
	ОбластьМакетаОбороты.Параметры.СуммаДебетКонтрагент = ДанныеПоОбъектуГруппировки.СуммаДебетКонтрагент;
	ОбластьМакетаОбороты.Параметры.СуммаКредитКонтрагент = ДанныеПоОбъектуГруппировки.СуммаКредитКонтрагент;
	Если ВыводитьДолгАванс Тогда
		ОчиститьДебетКредит(ОбластьМакетаОбороты);
		ОбластьМакетаОбороты.Параметры.СуммаДебет = ДанныеПоОбъектуГруппировки.СуммаДолг;
		ОбластьМакетаОбороты.Параметры.СуммаКредит = ДанныеПоОбъектуГруппировки.СуммаАванс;
		ОбластьМакетаОбороты.Параметры.СуммаДебетКонтрагент = ДанныеПоОбъектуГруппировки.СуммаДолгКонтрагент;
		ОбластьМакетаОбороты.Параметры.СуммаКредитКонтрагент = ДанныеПоОбъектуГруппировки.СуммаАвансКонтрагент;
	КонецЕсли;
	
	Если НастройкиПечати.НеЗаполнятьДанныеКонтрагента Тогда
		ОбластьМакетаОбороты.Параметры.СуммаДебетКонтрагент = 0;
		ОбластьМакетаОбороты.Параметры.СуммаКредитКонтрагент = 0;
	КонецЕсли;
	Если РасчетныеДокументыПериода.Количество() > 0 Тогда
		ТабличныйДокумент.Вывести(ОбластьМакетаОбороты);
	КонецЕсли;
	#КонецОбласти
	
	#Область ВыводКонечногоОстатка
	СвернутьСальдо(ДанныеПоОбъектуГруппировки.КонечноеСальдоДт, ДанныеПоОбъектуГруппировки.КонечноеСальдоКт);
	ЗаполнитьЗначенияСвойств(ОбластьМакетаКонОстаток.Параметры, ДанныеПоОбъектуГруппировки);
	
	ОбластьМакетаКонОстаток.Параметры.СуммаДебет = ДанныеПоОбъектуГруппировки.КонечноеСальдоДт;
	ОбластьМакетаКонОстаток.Параметры.СуммаКредит = ДанныеПоОбъектуГруппировки.КонечноеСальдоКт;
	
	ОбластьМакетаКонОстаток.Параметры.СуммаДебетКонтрагент = 0;
	ОбластьМакетаКонОстаток.Параметры.СуммаКредитКонтрагент = 0;
	
	СвернутьСальдо(ДанныеПоОбъектуГруппировки.КонечноеСальдоДтКонтрагент, ДанныеПоОбъектуГруппировки.КонечноеСальдоКтКонтрагент);
	ОбластьМакетаКонОстаток.Параметры.СуммаДебетКонтрагент = ДанныеПоОбъектуГруппировки.КонечноеСальдоДтКонтрагент;
	ОбластьМакетаКонОстаток.Параметры.СуммаКредитКонтрагент = ДанныеПоОбъектуГруппировки.КонечноеСальдоКтКонтрагент;
	Если ВыводитьДолгАванс Тогда
		ОчиститьДебетКредит(ОбластьМакетаКонОстаток);
		
		СвернутьСальдо(ДанныеПоОбъектуГруппировки.КонечноеСальдоДолг, ДанныеПоОбъектуГруппировки.КонечноеСальдоАванс);
		ОбластьМакетаКонОстаток.Параметры.СуммаДебет = ДанныеПоОбъектуГруппировки.КонечноеСальдоДолг;
		ОбластьМакетаКонОстаток.Параметры.СуммаКредит = ДанныеПоОбъектуГруппировки.КонечноеСальдоАванс;
		
		СвернутьСальдо(ДанныеПоОбъектуГруппировки.КонечноеСальдоДолгКонтрагент, ДанныеПоОбъектуГруппировки.КонечноеСальдоАвансКонтрагент);
		ОбластьМакетаКонОстаток.Параметры.СуммаДебетКонтрагент = ДанныеПоОбъектуГруппировки.КонечноеСальдоДолгКонтрагент;
		ОбластьМакетаКонОстаток.Параметры.СуммаКредитКонтрагент = ДанныеПоОбъектуГруппировки.КонечноеСальдоАвансКонтрагент;
	КонецЕсли;
	Если НастройкиПечати.НеЗаполнятьДанныеКонтрагента Тогда
		ОбластьМакетаКонОстаток.Параметры.СуммаДебетКонтрагент = 0;
		ОбластьМакетаКонОстаток.Параметры.СуммаКредитКонтрагент = 0;
	КонецЕсли;
	ТабличныйДокумент.Вывести(ОбластьМакетаКонОстаток);
	#КонецОбласти

	Задолженность = ИтогоЗадолженность.Добавить();
	Задолженность.Валюта = ДанныеПоОбъектуГруппировки.Валюта;
	Задолженность.СуммаДт = ДанныеПоОбъектуГруппировки.КонечноеСальдоДт;
	Задолженность.СуммаКт = ДанныеПоОбъектуГруппировки.КонечноеСальдоКт;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура СвернутьСальдо(КонечноеСальдоДт, КонечноеСальдоКт)
	
	КонечноеСальдо = КонечноеСальдоДт - КонечноеСальдоКт;
	Если КонечноеСальдо > 0 Тогда
		КонечноеСальдоДт = КонечноеСальдо;
		КонечноеСальдоКт = 0;
	Иначе
		КонечноеСальдоДт = 0;
		КонечноеСальдоКт = -КонечноеСальдо;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет структуру данными о получателях печатных форм.
// Параметры:
// 	СтруктураДанныхОбъектаПечати - см. ФормированиеПечатныхФорм.ЗаполнитьДанныеСтруктурыПолучателяПоТипуОбъекта.СтруктураДанныхОбъектаПечати
// 
Процедура ЗаполнитьСтруктуруПолучателейПечатныхФорм(СтруктураДанныхОбъектаПечати) Экспорт
	
	СтруктураДанныхОбъектаПечати.ОсновнойПолучатель = "Контрагент";
	
	СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("КонтактноеЛицо");
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровИКонтрагентов") Тогда
		СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("Контрагент");
		СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("Партнер");
		СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("ДанныеКонтрагента.Партнер");
	Иначе
		СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("Контрагент.Партнер");
	КонецЕсли;
	
КонецПроцедуры

// Настройки печати по умолчанию.
// 
// Возвращаемое значение:
//  Структура - Настройки печати по умолчанию:
// * ДетализацияВзаиморасчетов - Число
// * ДетализацияФинансовыхИнструментов - Число
// * ДебетКакДолг - Булево
// * НеЗаполнятьДанныеКонтрагента - Булево
// * ОбъединитьКлиентовПоставщиков - Булево
Функция НастройкиПечатиПоУмолчанию() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ДетализацияВзаиморасчетов", 0);
	Результат.Вставить("ДетализацияФинансовыхИнструментов", 0);
	Результат.Вставить("ДебетКакДолг", Ложь);
	Результат.Вставить("НеЗаполнятьДанныеКонтрагента", Ложь);
	Результат.Вставить("ОбъединитьКлиентовПоставщиков", Истина);
	Возврат Результат;
	
КонецФункции

Процедура РассчитатьСальдо(ДолгВВалюте, Сумма)
	
	СуммаДт = ДолгВВалюте[Сумма + "Дт"];
	СуммаКт = ДолгВВалюте[Сумма + "Кт"];
	Сальдо = СуммаДт - СуммаКт;
	Если Сальдо > 0 Тогда
		ДолгВВалюте[Сумма + "Дт"] = Сальдо;
		ДолгВВалюте[Сумма + "Кт"] = 0;
	Иначе
		ДолгВВалюте[Сумма + "Дт"] = 0;
		ДолгВВалюте[Сумма + "Кт"] = -Сальдо;
	КонецЕсли;
	
КонецПроцедуры

#Область ПолучениеДанныхНаПечать

// Запрос данные на печать.
// 
// Параметры:
//  МассивОбъектов - Массив из ДокументСсылка.СверкаВзаиморасчетов2_5_11- Массив объектов
//  НастройкиПечати - Произвольный, Дата, Неопределено, Структура - Настройки печати:
// * ДебетКакДолг - Булево -
// * ДетализацияВзаиморасчетов - Число - 
// * ДетализацияФинансовыхИнструментов - Число -
// * НеЗаполнятьДанныеКонтрагента - Булево -
// * ОбъединитьКлиентовПоставщиков - Булево -
// 
// Возвращаемое значение:
//  Запрос - Запрос данные на печать
Функция ЗапросДанныеНаПечать(МассивОбъектов, НастройкиПечати) Экспорт
	
	РасшифроватьНастройкуДетализации(НастройкиПечати);
	
	ЗапросДанныеДокумента = Новый Запрос;
	МенеджерВременныхТаблиц = ИнициализироватьМенеджерТаблиц(МассивОбъектов);
	ЗапросДанныеДокумента.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ЗапросДанныеДокумента.Текст = ТекстЗапросаДанныеДокумента(НастройкиПечати);
	
	ЗапросДанныеДокумента.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	ЗапросДанныеДокумента.УстановитьПараметр("ДебетКакДолг", НастройкиПечати.ДебетКакДолг);
	ЗапросДанныеДокумента.УстановитьПараметр("ОбъединитьКлиентовПоставщиков", НастройкиПечати.ОбъединитьКлиентовПоставщиков);
	ЗапросДанныеДокумента.УстановитьПараметр("ДетализацияВзаиморасчетов", НастройкиПечати.ДетализацияВзаиморасчетов);

	Если НастройкиПечати.Свойство("ДляОтладки") Тогда
		Если НастройкиПечати.ДляОтладки.Свойство("ЗапросДанныеНаПечать") Тогда
			ЗапросДанныеДокумента = НастройкиПечати.ДляОтладки.ЗапросДанныеНаПечать;
			ЗапросДанныеДокумента.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			НастройкиПечати.ДляОтладки.Вставить("МенеджерВременныхТаблиц", ИнициализироватьМенеджерТаблиц(МассивОбъектов));
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЗапросДанныеДокумента;

КонецФункции

Функция ИнициализироватьМенеджерТаблиц(МассивОбъектов)
	
	ПараметрыОтветственныхЛиц = ОтветственныеЛицаСервер.ПараметрыОтветственныхЛицПоУмолчанию();
	ПараметрыОтветственныхЛиц.РеквизитыОтветственныеЛица = СтруктураРеквизитыОтветственныеЛица();
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Если ЗначениеЗаполнено(МассивОбъектов) Тогда
		ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц, ПараметрыОтветственныхЛиц);
	Иначе
		ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(Документы.СверкаВзаиморасчетов2_5_11.ПустаяСсылка(), МенеджерВременныхТаблиц, ПараметрыОтветственныхЛиц);
	КонецЕсли;
	
	СоответствиеПсевдонимовДанныхОтветственныхЛиц = Новый Соответствие();
	
	СтруктураИмен = ОтветственныеЛицаСервер.ПсевдонимыДанныхОтветственныхЛицПоУмолчанию();
	СтруктураИмен.Наименование = "РуководительОрганизацииКонтрагентаНаименование";
	СтруктураИмен.ФИО = "РуководительОрганизацииКонтрагентаФИО";
	СтруктураИмен.Должность = "РуководительОрганизацииКонтрагентаДолжность";
	СтруктураИмен.ОснованиеПраваПодписи = "РуководительОрганизацииКонтрагентаОснованиеПраваПодписи";
	СоответствиеПсевдонимовДанныхОтветственныхЛиц.Вставить("РуководительОрганизацииКонтрагента", СтруктураИмен);
	
	СтруктураИмен = ОтветственныеЛицаСервер.ПсевдонимыДанныхОтветственныхЛицПоУмолчанию();
	СтруктураИмен.Наименование = "ГлавныйБухгалтерОрганизацииКонтрагентаНаименование";
	СтруктураИмен.ФИО = "ГлавныйБухгалтерОрганизацииКонтрагентаФИО";
	СтруктураИмен.Должность = "ГлавныйБухгалтерОрганизацииКонтрагентаДолжность";
	СтруктураИмен.ОснованиеПраваПодписи = "ГлавныйБухгалтерОрганизацииКонтрагентаОснованиеПраваПодписи";
	СоответствиеПсевдонимовДанныхОтветственныхЛиц.Вставить("ГлавныйБухгалтерОрганизацииКонтрагента", СтруктураИмен);
	
	ПараметрыОтветственныхЛиц = ОтветственныеЛицаСервер.ПараметрыОтветственныхЛицПоУмолчанию();
	ПараметрыОтветственныхЛиц.ИмяРеквизитаОрганизация = "Контрагент";
	ПараметрыОтветственныхЛиц.РеквизитыОтветственныеЛица = СтруктураРеквизитыОтветственныеЛицаОрганизацииКонтрагента();
	ПараметрыОтветственныхЛиц.ИмяТаблицыОтветственныеЛица = "ТаблицаОтветственныеЛицаОрганизацииКонтрагента";
	ПараметрыОтветственныхЛиц.ПсевдонимыДанныхОтветственныхЛиц = СоответствиеПсевдонимовДанныхОтветственныхЛиц;
	
	Если ЗначениеЗаполнено(МассивОбъектов) Тогда
		ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(
			МассивОбъектов,
			МенеджерВременныхТаблиц,
			ПараметрыОтветственныхЛиц);
	Иначе
		ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(
			Документы.СверкаВзаиморасчетов2_5_11.ПустаяСсылка(),
			МенеджерВременныхТаблиц,
			ПараметрыОтветственныхЛиц);
	КонецЕсли;
	
	Возврат МенеджерВременныхТаблиц;
	
КонецФункции

Функция ТекстЗапросаДанныеДокумента(НастройкиПечати)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТЧИтогиРасчетов.Ссылка КАК Ссылка,
	|	ТЧИтогиРасчетов.Ссылка.Валюта КАК ВалютаСверки,
	|	ТЧИтогиРасчетов.Ссылка.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегл,
	|	ТЧИтогиРасчетов.Ссылка.Валюта = ТЧИтогиРасчетов.Ссылка.Организация.ВалютаРегламентированногоУчета КАК СверкаВВалютеРегл,
	|	ТЧИтогиРасчетов.Ссылка.ЕстьРасхождения КАК ЕстьРасхождения,
	|	ВЫБОР КОГДА &ОбъединитьКлиентовПоставщиков
	|			И ТЧИтогиРасчетов.ТипРасчетов В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком))
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТЧИтогиРасчетов.ТипРасчетов
	|	КОНЕЦ КАК ТипРасчетов,
	|	ТЧИтогиРасчетов.Ссылка.РежимСверкиИтоговВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.РежимСверкиИтоговВзаиморасчетов.ПоДоговорам) КАК СверкаПоДоговорам,
	|	ВЫБОР 
	|		КОГДА &ДетализацияВзаиморасчетов = ""Валюта""
	|			ТОГДА ТЧИтогиРасчетов.Ссылка.Валюта
	|		КОГДА &ДетализацияВзаиморасчетов = ""Договор""
	|			ТОГДА ТЧИтогиРасчетов.Договор
	|		КОГДА &ДетализацияВзаиморасчетов = ""ОбъектРасчетов""
	|			ТОГДА ТЧИтогиРасчетов.ОбъектРасчетов
	|	КОНЕЦ КАК ОбъектГруппировки,
	|
	|	ТЧИтогиРасчетов.НачальноеСальдоДт КАК НачальноеСальдоДт,
	|	ТЧИтогиРасчетов.НачальноеСальдоКт КАК НачальноеСальдоКт,
	|	ТЧИтогиРасчетов.ОборотДт КАК ОборотДт,
	|	ТЧИтогиРасчетов.ОборотКт КАК ОборотКт,
	|	ТЧИтогиРасчетов.КонечноеСальдоДт КАК КонечноеСальдоДт,
	|	ТЧИтогиРасчетов.КонечноеСальдоКт КАК КонечноеСальдоКт,
	|
	|	ВЫБОР КОГДА ТЧИтогиРасчетов.Ссылка.ЕстьРасхождения
	|		ТОГДА ТЧИтогиРасчетов.НачальноеСальдоКтКонтрагент
	|		ИНАЧЕ ТЧИтогиРасчетов.НачальноеСальдоКт
	|	КОНЕЦ КАК НачальноеСальдоДтКонтрагент,
	|	ВЫБОР КОГДА ТЧИтогиРасчетов.Ссылка.ЕстьРасхождения
	|		ТОГДА ТЧИтогиРасчетов.НачальноеСальдоДтКонтрагент
	|		ИНАЧЕ ТЧИтогиРасчетов.НачальноеСальдоДт
	|	КОНЕЦ КАК НачальноеСальдоКтКонтрагент,
	|
	|	ВЫБОР КОГДА ТЧИтогиРасчетов.Ссылка.ЕстьРасхождения
	|		ТОГДА ТЧИтогиРасчетов.ОборотКтКонтрагент
	|		ИНАЧЕ ТЧИтогиРасчетов.ОборотКт
	|	КОНЕЦ КАК ОборотДтКонтрагент,
	|	ВЫБОР КОГДА ТЧИтогиРасчетов.Ссылка.ЕстьРасхождения
	|		ТОГДА ТЧИтогиРасчетов.ОборотДтКонтрагент
	|		ИНАЧЕ ТЧИтогиРасчетов.ОборотДт
	|	КОНЕЦ КАК ОборотКтКонтрагент,
	|
	|	ВЫБОР КОГДА ТЧИтогиРасчетов.Ссылка.ЕстьРасхождения
	|		ТОГДА ТЧИтогиРасчетов.КонечноеСальдоКтКонтрагент
	|		ИНАЧЕ ТЧИтогиРасчетов.КонечноеСальдоКт
	|	КОНЕЦ КАК КонечноеСальдоДтКонтрагент,
	|	ВЫБОР КОГДА ТЧИтогиРасчетов.Ссылка.ЕстьРасхождения
	|		ТОГДА ТЧИтогиРасчетов.КонечноеСальдоДтКонтрагент
	|		ИНАЧЕ ТЧИтогиРасчетов.КонечноеСальдоДт
	|	КОНЕЦ КАК КонечноеСальдоКтКонтрагент,
	|
	|
	|	ВЫБОР КОГДА ТЧИтогиРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|		ТОГДА ТЧИтогиРасчетов.НачальноеСальдоДт
	|		ИНАЧЕ ТЧИтогиРасчетов.НачальноеСальдоКт
	|	КОНЕЦ КАК НачальноеСальдоДолг,
	|	ВЫБОР КОГДА ТЧИтогиРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|		ТОГДА ТЧИтогиРасчетов.НачальноеСальдоКт
	|		ИНАЧЕ ТЧИтогиРасчетов.НачальноеСальдоДт
	|	КОНЕЦ КАК НачальноеСальдоАванс,
	|
	|	ВЫБОР КОГДА ТЧИтогиРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|		ТОГДА ТЧИтогиРасчетов.КонечноеСальдоДт
	|		ИНАЧЕ ТЧИтогиРасчетов.КонечноеСальдоКт
	|	КОНЕЦ КАК КонечноеСальдоДолг,
	|	ВЫБОР КОГДА ТЧИтогиРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|		ТОГДА ТЧИтогиРасчетов.КонечноеСальдоКт
	|		ИНАЧЕ ТЧИтогиРасчетов.КонечноеСальдоДт
	|	КОНЕЦ КАК КонечноеСальдоАванс,
	|
	|
	|	ВЫБОР КОГДА ТЧИтогиРасчетов.Ссылка.ЕстьРасхождения
	|		ТОГДА ВЫБОР КОГДА ТЧИтогиРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|				ТОГДА ТЧИтогиРасчетов.НачальноеСальдоДтКонтрагент
	|				ИНАЧЕ ТЧИтогиРасчетов.НачальноеСальдоКтКонтрагент
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР КОГДА ТЧИтогиРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|				ТОГДА ТЧИтогиРасчетов.НачальноеСальдоДт
	|				ИНАЧЕ ТЧИтогиРасчетов.НачальноеСальдоКт
	|			КОНЕЦ
	|	КОНЕЦ КАК НачальноеСальдоДолгКонтрагент,
	|
	|	ВЫБОР КОГДА ТЧИтогиРасчетов.Ссылка.ЕстьРасхождения
	|		ТОГДА ВЫБОР КОГДА ТЧИтогиРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|				ТОГДА ТЧИтогиРасчетов.НачальноеСальдоКтКонтрагент
	|				ИНАЧЕ ТЧИтогиРасчетов.НачальноеСальдоДтКонтрагент
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР КОГДА ТЧИтогиРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|				ТОГДА ТЧИтогиРасчетов.НачальноеСальдоКт
	|				ИНАЧЕ ТЧИтогиРасчетов.НачальноеСальдоДт
	|			КОНЕЦ
	|	КОНЕЦ КАК НачальноеСальдоАвансКонтрагент,
	|
	|	ВЫБОР КОГДА ТЧИтогиРасчетов.Ссылка.ЕстьРасхождения
	|		ТОГДА ВЫБОР КОГДА ТЧИтогиРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|				ТОГДА ТЧИтогиРасчетов.КонечноеСальдоДтКонтрагент
	|				ИНАЧЕ ТЧИтогиРасчетов.КонечноеСальдоКтКонтрагент
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР КОГДА ТЧИтогиРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|				ТОГДА ТЧИтогиРасчетов.КонечноеСальдоДт
	|				ИНАЧЕ ТЧИтогиРасчетов.КонечноеСальдоКт
	|			КОНЕЦ
	|	КОНЕЦ КАК КонечноеСальдоДолгКонтрагент,
	|	ВЫБОР КОГДА ТЧИтогиРасчетов.Ссылка.ЕстьРасхождения
	|		ТОГДА ВЫБОР КОГДА ТЧИтогиРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|				ТОГДА ТЧИтогиРасчетов.КонечноеСальдоКтКонтрагент
	|				ИНАЧЕ ТЧИтогиРасчетов.КонечноеСальдоДтКонтрагент
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР КОГДА ТЧИтогиРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|				ТОГДА ТЧИтогиРасчетов.КонечноеСальдоКт
	|				ИНАЧЕ ТЧИтогиРасчетов.КонечноеСальдоДт
	|			КОНЕЦ
	|	КОНЕЦ КАК КонечноеСальдоАвансКонтрагент
	|
	|ПОМЕСТИТЬ втИтоговыеЗаписиРасчетов
	|ИЗ
	|	Документ.СверкаВзаиморасчетов2_5_11.ИтоговыеЗаписи КАК ТЧИтогиРасчетов
	|ГДЕ
	|	ТЧИтогиРасчетов.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИтогиРасчетов.Ссылка КАК Ссылка,
	|	ИтогиРасчетов.Ссылка.Валюта КАК ВалютаСверки,
	|	ИтогиРасчетов.Ссылка.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегл,
	|	ИтогиРасчетов.Ссылка.Валюта = ИтогиРасчетов.Ссылка.Организация.ВалютаРегламентированногоУчета КАК СверкаВВалютеРегл,
	|	ИтогиРасчетов.Ссылка.ЕстьРасхождения КАК ЕстьРасхождения,
	|	ИтогиРасчетов.ТипРасчетов КАК ТипРасчетов,
	|	ИтогиРасчетов.Ссылка.РежимСверкиИтоговВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.РежимСверкиИтоговВзаиморасчетов.ПоДоговорам) КАК СверкаПоДоговорам,
	|	ИтогиРасчетов.ОбъектГруппировки КАК ОбъектГруппировки,
	|
	|	СУММА(ИтогиРасчетов.НачальноеСальдоДт) КАК НачальноеСальдоДт,
	|	СУММА(ИтогиРасчетов.НачальноеСальдоКт) КАК НачальноеСальдоКт,
	|	СУММА(ИтогиРасчетов.ОборотДт) КАК ОборотДт,
	|	СУММА(ИтогиРасчетов.ОборотКт) КАК ОборотКт,
	|	СУММА(ИтогиРасчетов.КонечноеСальдоДт) КАК КонечноеСальдоДт,
	|	СУММА(ИтогиРасчетов.КонечноеСальдоКт) КАК КонечноеСальдоКт,
	|
	|	СУММА(ИтогиРасчетов.НачальноеСальдоДтКонтрагент) КАК НачальноеСальдоДтКонтрагент,
	|	СУММА(ИтогиРасчетов.НачальноеСальдоКтКонтрагент) КАК НачальноеСальдоКтКонтрагент,
	|
	|	СУММА(ИтогиРасчетов.ОборотДтКонтрагент) КАК ОборотДтКонтрагент,
	|	СУММА(ИтогиРасчетов.ОборотКтКонтрагент) КАК ОборотКтКонтрагент,
	|
	|	СУММА(ИтогиРасчетов.КонечноеСальдоДтКонтрагент) КАК КонечноеСальдоДтКонтрагент,
	|	СУММА(ИтогиРасчетов.КонечноеСальдоКтКонтрагент) КАК КонечноеСальдоКтКонтрагент,
	|
	|	СУММА(ИтогиРасчетов.НачальноеСальдоДолг) КАК НачальноеСальдоДолг,
	|	СУММА(ИтогиРасчетов.НачальноеСальдоАванс) КАК НачальноеСальдоАванс,
	|
	|	СУММА(ИтогиРасчетов.КонечноеСальдоДолг) КАК КонечноеСальдоДолг,
	|	СУММА(ИтогиРасчетов.КонечноеСальдоАванс) КАК КонечноеСальдоАванс,
	|
	|	СУММА(ИтогиРасчетов.НачальноеСальдоДолгКонтрагент) КАК НачальноеСальдоДолгКонтрагент,
	|	СУММА(ИтогиРасчетов.НачальноеСальдоАвансКонтрагент) КАК НачальноеСальдоАвансКонтрагент,
	|
	|	СУММА(ИтогиРасчетов.КонечноеСальдоДолгКонтрагент) КАК КонечноеСальдоДолгКонтрагент,
	|	СУММА(ИтогиРасчетов.КонечноеСальдоАвансКонтрагент) КАК КонечноеСальдоАвансКонтрагент
	|
	|ПОМЕСТИТЬ втИтогиРасчетов
	|ИЗ
	|	втИтоговыеЗаписиРасчетов КАК ИтогиРасчетов
	|
	|СГРУППИРОВАТЬ ПО
	|	ИтогиРасчетов.Ссылка,
	|	ИтогиРасчетов.ТипРасчетов,
	|	ИтогиРасчетов.ОбъектГруппировки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДетальныеЗаписи.Ссылка КАК Ссылка,
	|	ДетальныеЗаписи.ДатаОперации КАК ДатаОперации,
	|	ВЫБОР КОГДА &ОбъединитьКлиентовПоставщиков
	|			И ДетальныеЗаписи.ТипРасчетов В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком))
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ДетальныеЗаписи.ТипРасчетов
	|	КОНЕЦ КАК ТипРасчетов,
	|
	|	ВЫБОР 
	|		КОГДА &ДетализацияВзаиморасчетов = ""Валюта""
	|			ТОГДА ДетальныеЗаписи.Ссылка.Валюта
	|		КОГДА &ДетализацияВзаиморасчетов = ""Договор""
	|			ТОГДА ДетальныеЗаписи.Договор
	|		КОГДА &ДетализацияВзаиморасчетов = ""ОбъектРасчетов""
	|			ТОГДА ДетальныеЗаписи.ОбъектРасчетов
	|	КОНЕЦ КАК ОбъектГруппировки,
	|
	|	ДетальныеЗаписи.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ВЫБОР КОГДА ДетальныеЗаписи.НаименованиеДокумента = """"
	|		И ДетальныеЗаписи.СуммаДебет = 0 И ДетальныеЗаписи.СуммаКредит = 0
	|		ТОГДА ДетальныеЗаписи.НаименованиеДокументаКонтрагент
	|		ИНАЧЕ ДетальныеЗаписи.НаименованиеДокумента
	|	КОНЕЦ КАК НаименованиеДокумента,
	|	ВЫБОР КОГДА ДетальныеЗаписи.НаименованиеДокумента = """" И ДетальныеЗаписи.НомерДокумента = """"
	|		И ДетальныеЗаписи.СуммаДебет = 0 И ДетальныеЗаписи.СуммаКредит = 0
	|		ТОГДА ДетальныеЗаписи.НомерДокументаКонтрагент
	|		ИНАЧЕ ДетальныеЗаписи.НомерДокумента
	|	КОНЕЦ КАК НомерДокумента,
	|	ВЫБОР КОГДА ДетальныеЗаписи.НаименованиеДокумента = """" И ДетальныеЗаписи.ДатаДокумента = ДАТАВРЕМЯ(1,1,1)
	|		И ДетальныеЗаписи.СуммаДебет = 0 И ДетальныеЗаписи.СуммаКредит = 0
	|		ТОГДА ДетальныеЗаписи.ДатаДокументаКонтрагент
	|		ИНАЧЕ ДетальныеЗаписи.ДатаДокумента
	|	КОНЕЦ КАК ДатаДокумента,
	|	ДетальныеЗаписи.СвязанныеДокументы КАК СвязанныеДокументы,
	|
	|	ДетальныеЗаписи.ВалютаДокумента КАК ВалютаДокумента,
	|	ДетальныеЗаписи.СуммаДокумента КАК СуммаДокумента,
	|
	|	СУММА(ДетальныеЗаписи.СуммаДолг) КАК СуммаДолг,
	|	СУММА(ДетальныеЗаписи.СуммаАванс) КАК СуммаАванс,
	|	СУММА(ДетальныеЗаписи.СуммаДебет) КАК СуммаДебет,
	|	СУММА(ДетальныеЗаписи.СуммаКредит) КАК СуммаКредит,
	|
	|	СУММА(ДетальныеЗаписи.СуммаДебетКонтрагент) КАК СуммаДебетКонтрагент,
	|	СУММА(ДетальныеЗаписи.СуммаКредитКонтрагент) КАК СуммаКредитКонтрагент,
	|
	|	СУММА(ДетальныеЗаписи.СуммаДебетКонтрагент) КАК СуммаДолгКонтрагент,
	|	СУММА(ДетальныеЗаписи.СуммаКредитКонтрагент) КАК СуммаАвансКонтрагент
	|
	|ПОМЕСТИТЬ втДетальныеЗаписи
	|ИЗ
	|	Документ.СверкаВзаиморасчетов2_5_11.ДетальныеЗаписи КАК ДетальныеЗаписи
	|ГДЕ
	|	ДетальныеЗаписи.Ссылка В(&МассивОбъектов)
	|СГРУППИРОВАТЬ ПО
	|	ДетальныеЗаписи.Ссылка,
	|	ДетальныеЗаписи.ДатаОперации,
	|	ВЫБОР КОГДА &ОбъединитьКлиентовПоставщиков
	|			И ДетальныеЗаписи.ТипРасчетов В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком))
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ДетальныеЗаписи.ТипРасчетов
	|	КОНЕЦ,
	|
	|	ВЫБОР 
	|		КОГДА &ДетализацияВзаиморасчетов = ""Валюта""
	|			ТОГДА ДетальныеЗаписи.Ссылка.Валюта
	|		КОГДА &ДетализацияВзаиморасчетов = ""Договор""
	|			ТОГДА ДетальныеЗаписи.Договор
	|		КОГДА &ДетализацияВзаиморасчетов = ""ОбъектРасчетов""
	|			ТОГДА ДетальныеЗаписи.ОбъектРасчетов
	|	КОНЕЦ,
	|
	|	ДетальныеЗаписи.РасчетныйДокумент,
	|	ВЫБОР КОГДА ДетальныеЗаписи.НаименованиеДокумента = """"
	|		И ДетальныеЗаписи.СуммаДебет = 0 И ДетальныеЗаписи.СуммаКредит = 0
	|		ТОГДА ДетальныеЗаписи.НаименованиеДокументаКонтрагент
	|		ИНАЧЕ ДетальныеЗаписи.НаименованиеДокумента
	|	КОНЕЦ,
	|	ВЫБОР КОГДА ДетальныеЗаписи.НаименованиеДокумента = """" И ДетальныеЗаписи.НомерДокумента = """"
	|		И ДетальныеЗаписи.СуммаДебет = 0 И ДетальныеЗаписи.СуммаКредит = 0
	|		ТОГДА ДетальныеЗаписи.НомерДокументаКонтрагент
	|		ИНАЧЕ ДетальныеЗаписи.НомерДокумента
	|	КОНЕЦ,
	|	ВЫБОР КОГДА ДетальныеЗаписи.НаименованиеДокумента = """" И ДетальныеЗаписи.ДатаДокумента = ДАТАВРЕМЯ(1,1,1)
	|		И ДетальныеЗаписи.СуммаДебет = 0 И ДетальныеЗаписи.СуммаКредит = 0
	|		ТОГДА ДетальныеЗаписи.ДатаДокументаКонтрагент
	|		ИНАЧЕ ДетальныеЗаписи.ДатаДокумента
	|	КОНЕЦ,
	|	ДетальныеЗаписи.СвязанныеДокументы,
	|	ДетальныеЗаписи.ВалютаДокумента,
	|	ДетальныеЗаписи.СуммаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИтогиРасчетов.Ссылка КАК Ссылка,
	|	ИтогиРасчетов.СверкаВВалютеРегл КАК СверкаВВалютеРегл,
	|	ВЫБОР КОГДА ИтогиРасчетов.СверкаВВалютеРегл
	|		ТОГДА ИтогиРасчетов.ВалютаРегл
	|		ИНАЧЕ ИтогиРасчетов.ВалютаСверки
	|	КОНЕЦ КАК Валюта,
	|	ИтогиРасчетов.ВалютаСверки КАК ВалютаСверки,
	|	ИтогиРасчетов.ВалютаРегл КАК ВалютаРегл,
	|	ИтогиРасчетов.СверкаПоДоговорам КАК СверкаПоДоговорам,
	|	ИтогиРасчетов.ЕстьРасхождения КАК ЕстьРасхождения,
	|	ИтогиРасчетов.ТипРасчетов КАК ТипРасчетов,
	|
	|	ИтогиРасчетов.ОбъектГруппировки КАК ОбъектГруппировки,
	|
	|	ИтогиРасчетов.НачальноеСальдоДт КАК НачальноеСальдоДт,
	|	ИтогиРасчетов.НачальноеСальдоКт КАК НачальноеСальдоКт,
	|	ИтогиРасчетов.ОборотДт КАК ОборотДт,
	|	ИтогиРасчетов.ОборотКт КАК ОборотКт,
	|	ИтогиРасчетов.КонечноеСальдоДт КАК КонечноеСальдоДт,
	|	ИтогиРасчетов.КонечноеСальдоКт КАК КонечноеСальдоКт,
	|
	|	ИтогиРасчетов.НачальноеСальдоДтКонтрагент КАК НачальноеСальдоДтКонтрагент,
	|	ИтогиРасчетов.НачальноеСальдоКтКонтрагент КАК НачальноеСальдоКтКонтрагент,
	|	ИтогиРасчетов.ОборотДтКонтрагент КАК ОборотДтКонтрагент,
	|	ИтогиРасчетов.ОборотКтКонтрагент КАК ОборотКтКонтрагент,
	|	ИтогиРасчетов.КонечноеСальдоДтКонтрагент КАК КонечноеСальдоДтКонтрагент,
	|	ИтогиРасчетов.КонечноеСальдоКтКонтрагент КАК КонечноеСальдоКтКонтрагент,
	|
	|	ИтогиРасчетов.НачальноеСальдоДолг КАК НачальноеСальдоДолг,
	|	ИтогиРасчетов.НачальноеСальдоАванс КАК НачальноеСальдоАванс,
	|	ИтогиРасчетов.КонечноеСальдоДолг КАК КонечноеСальдоДолг,
	|	ИтогиРасчетов.КонечноеСальдоАванс КАК КонечноеСальдоАванс,
	|
	|	ИтогиРасчетов.НачальноеСальдоДолгКонтрагент КАК НачальноеСальдоДолгКонтрагент,
	|	ИтогиРасчетов.НачальноеСальдоАвансКонтрагент КАК НачальноеСальдоАвансКонтрагент,
	|	ИтогиРасчетов.КонечноеСальдоДолгКонтрагент КАК КонечноеСальдоДолгКонтрагент,
	|	ИтогиРасчетов.КонечноеСальдоАвансКонтрагент КАК КонечноеСальдоАвансКонтрагент,
	|
	|	НЕ ДетальныеЗаписи.РасчетныйДокумент ЕСТЬ NULL КАК ЕстьОбороты,
	|	ЕСТЬNULL(ДетальныеЗаписи.ДатаОперации, ДАТАВРЕМЯ(1,1,1)) КАК ДатаОперации,
	|	ЕСТЬNULL(ДетальныеЗаписи.РасчетныйДокумент, НЕОПРЕДЕЛЕНО) КАК РасчетныйДокумент,
	|	ЕСТЬNULL(ДетальныеЗаписи.НаименованиеДокумента, """") КАК НаименованиеДокумента,
	|	ЕСТЬNULL(ДетальныеЗаписи.НомерДокумента, """") КАК НомерДокумента,
	|	ЕСТЬNULL(ДетальныеЗаписи.ДатаДокумента, ДАТАВРЕМЯ(1,1,1)) КАК ДатаДокумента,
	|	ЕСТЬNULL(ДетальныеЗаписи.СвязанныеДокументы, """") КАК СвязанныеДокументы,
	|
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДетальныеЗаписи.РасчетныйДокумент) = ТИП(Документ.РасчетКурсовыхРазниц)
	|		ТОГДА ИтогиРасчетов.ВалютаРегл
	|		ИНАЧЕ ДетальныеЗаписи.ВалютаДокумента
	|	КОНЕЦ КАК ВалютаДокумента,
	|	
	|	ЕСТЬNULL(ДетальныеЗаписи.СуммаДокумента,0) КАК СуммаДокумента,
	|	ЕСТЬNULL(ДетальныеЗаписи.СуммаДолг,0) КАК СуммаДолг,
	|	ЕСТЬNULL(ДетальныеЗаписи.СуммаАванс,0) КАК СуммаАванс,
	|	ЕСТЬNULL(ДетальныеЗаписи.СуммаДебет,0) КАК СуммаДебет,
	|	ЕСТЬNULL(ДетальныеЗаписи.СуммаКредит,0) КАК СуммаКредит,
	|
	|	ВЫБОР КОГДА ИтогиРасчетов.ЕстьРасхождения
	|		ТОГДА ЕСТЬNULL(ДетальныеЗаписи.СуммаКредитКонтрагент,0)
	|		ИНАЧЕ ЕСТЬNULL(ДетальныеЗаписи.СуммаКредит,0)
	|	КОНЕЦ КАК СуммаДебетКонтрагент,
	|	ВЫБОР КОГДА ИтогиРасчетов.ЕстьРасхождения
	|		ТОГДА ЕСТЬNULL(ДетальныеЗаписи.СуммаДебетКонтрагент,0)
	|		ИНАЧЕ ЕСТЬNULL(ДетальныеЗаписи.СуммаДебет,0)
	|	КОНЕЦ КАК СуммаКредитКонтрагент,
	|
	|	ВЫБОР КОГДА ИтогиРасчетов.ЕстьРасхождения
	|		ТОГДА ЕСТЬNULL(ДетальныеЗаписи.СуммаДебетКонтрагент,0)
	|		ИНАЧЕ ЕСТЬNULL(ДетальныеЗаписи.СуммаДолг,0)
	|	КОНЕЦ КАК СуммаДолгКонтрагент,
	|	ВЫБОР КОГДА ИтогиРасчетов.ЕстьРасхождения
	|		ТОГДА ЕСТЬNULL(ДетальныеЗаписи.СуммаКредитКонтрагент,0)
	|		ИНАЧЕ ЕСТЬNULL(ДетальныеЗаписи.СуммаАванс,0)
	|	КОНЕЦ КАК СуммаАвансКонтрагент
	|
	|ПОМЕСТИТЬ втДетальныеЗаписиРасчетов
	|ИЗ
	|	втИтогиРасчетов КАК ИтогиРасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ втДетальныеЗаписи КАК ДетальныеЗаписи
	|		ПО ИтогиРасчетов.Ссылка = ДетальныеЗаписи.Ссылка
	|			И ИтогиРасчетов.ТипРасчетов = ДетальныеЗаписи.ТипРасчетов
	|			И ИтогиРасчетов.ОбъектГруппировки = ДетальныеЗаписи.ОбъектГруппировки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА ДеталиРасчетов.ТипРасчетов В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком))
	|		ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ФинансовыеИнструменты,
	|	ДеталиРасчетов.Ссылка КАК Ссылка,
	|	ДеталиРасчетов.Валюта КАК Валюта,
	|	ДеталиРасчетов.СверкаВВалютеРегл КАК СверкаВВалютеРегл,
	|	ДеталиРасчетов.ВалютаСверки КАК ВалютаСверки,
	|	ДеталиРасчетов.ВалютаРегл КАК ВалютаРегл,
	|	ДеталиРасчетов.СверкаПоДоговорам КАК СверкаПоДоговорам,
	|	ДеталиРасчетов.ЕстьРасхождения КАК ЕстьРасхождения,
	|	ДеталиРасчетов.ТипРасчетов КАК ТипРасчетов,
	|
	|	ДеталиРасчетов.ОбъектГруппировки КАК ОбъектГруппировки,
	|
	|	ДеталиРасчетов.НачальноеСальдоДт КАК НачальноеСальдоДт,
	|	ДеталиРасчетов.НачальноеСальдоКт КАК НачальноеСальдоКт,
	|	ДеталиРасчетов.ОборотДт КАК ОборотДт,
	|	ДеталиРасчетов.ОборотКт КАК ОборотКт,
	|	ДеталиРасчетов.КонечноеСальдоДт КАК КонечноеСальдоДт,
	|	ДеталиРасчетов.КонечноеСальдоКт КАК КонечноеСальдоКт,
	|
	|	ДеталиРасчетов.НачальноеСальдоДтКонтрагент КАК НачальноеСальдоДтКонтрагент,
	|	ДеталиРасчетов.НачальноеСальдоКтКонтрагент КАК НачальноеСальдоКтКонтрагент,
	|	ДеталиРасчетов.ОборотДтКонтрагент КАК ОборотДтКонтрагент,
	|	ДеталиРасчетов.ОборотКтКонтрагент КАК ОборотКтКонтрагент,
	|	ДеталиРасчетов.КонечноеСальдоДтКонтрагент КАК КонечноеСальдоДтКонтрагент,
	|	ДеталиРасчетов.КонечноеСальдоКтКонтрагент КАК КонечноеСальдоКтКонтрагент,
	|
	|	ДеталиРасчетов.НачальноеСальдоДолг КАК НачальноеСальдоДолг,
	|	ДеталиРасчетов.НачальноеСальдоАванс КАК НачальноеСальдоАванс,
	|	ДеталиРасчетов.КонечноеСальдоДолг КАК КонечноеСальдоДолг,
	|	ДеталиРасчетов.КонечноеСальдоАванс КАК КонечноеСальдоАванс,
	|
	|	ДеталиРасчетов.НачальноеСальдоДолгКонтрагент КАК НачальноеСальдоДолгКонтрагент,
	|	ДеталиРасчетов.НачальноеСальдоАвансКонтрагент КАК НачальноеСальдоАвансКонтрагент,
	|	ДеталиРасчетов.КонечноеСальдоДолгКонтрагент КАК КонечноеСальдоДолгКонтрагент,
	|	ДеталиРасчетов.КонечноеСальдоАвансКонтрагент КАК КонечноеСальдоАвансКонтрагент,
	|
	|	ДеталиРасчетов.ЕстьОбороты КАК ЕстьОбороты,
	|	ДеталиРасчетов.ДатаОперации КАК ДатаОперации,
	|	ДеталиРасчетов.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ДеталиРасчетов.НаименованиеДокумента КАК НаименованиеДокумента,
	|	ДеталиРасчетов.НомерДокумента КАК НомерДокумента,
	|	ДеталиРасчетов.ДатаДокумента КАК ДатаДокумента,
	|	ДеталиРасчетов.СвязанныеДокументы КАК СвязанныеДокументы,
	|
	|	ДеталиРасчетов.ВалютаДокумента КАК ВалютаДокумента,
	|	
	|	ДеталиРасчетов.СуммаДокумента КАК СуммаДокумента,
	|	СУММА(ДеталиРасчетов.СуммаДолг) КАК СуммаДолг,
	|	СУММА(ДеталиРасчетов.СуммаАванс) КАК СуммаАванс,
	|	СУММА(ДеталиРасчетов.СуммаДебет) КАК СуммаДебет,
	|	СУММА(ДеталиРасчетов.СуммаКредит) КАК СуммаКредит,
	|	СУММА(ДеталиРасчетов.СуммаДебетКонтрагент) КАК СуммаДебетКонтрагент,
	|	СУММА(ДеталиРасчетов.СуммаКредитКонтрагент) КАК СуммаКредитКонтрагент,
	|
	|	СУММА(ДеталиРасчетов.СуммаДолгКонтрагент) КАК СуммаДолгКонтрагент,
	|	СУММА(ДеталиРасчетов.СуммаАвансКонтрагент) КАК СуммаАвансКонтрагент,
	|	
	|	ВЫБОР 
	|		КОГДА ДеталиРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|			ТОГДА 10
	|		КОГДА ДеталиРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|			ТОГДА 20
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК ПорядокРасчетов,
	|	
	|	0 КАК ПорядокТипаСуммы
	|	
	|ПОМЕСТИТЬ ДетальныеЗаписиДокумента
	|ИЗ
	|	втДетальныеЗаписиРасчетов КАК ДеталиРасчетов
	|
	|СГРУППИРОВАТЬ ПО
	|	ДеталиРасчетов.Ссылка,
	|	ДеталиРасчетов.Валюта,
	|	ДеталиРасчетов.СверкаВВалютеРегл,
	|	ДеталиРасчетов.ВалютаСверки,
	|	ДеталиРасчетов.ВалютаРегл,
	|	ДеталиРасчетов.СверкаПоДоговорам,
	|	ДеталиРасчетов.ЕстьРасхождения,
	|	ДеталиРасчетов.ТипРасчетов,
	|
	|	ДеталиРасчетов.ОбъектГруппировки,
	|
	|	ДеталиРасчетов.НачальноеСальдоДт,
	|	ДеталиРасчетов.НачальноеСальдоКт,
	|	ДеталиРасчетов.ОборотДт,
	|	ДеталиРасчетов.ОборотКт,
	|	ДеталиРасчетов.КонечноеСальдоДт,
	|	ДеталиРасчетов.КонечноеСальдоКт,
	|
	|	ДеталиРасчетов.НачальноеСальдоДтКонтрагент,
	|	ДеталиРасчетов.НачальноеСальдоКтКонтрагент,
	|	ДеталиРасчетов.ОборотДтКонтрагент,
	|	ДеталиРасчетов.ОборотКтКонтрагент,
	|	ДеталиРасчетов.КонечноеСальдоДтКонтрагент,
	|	ДеталиРасчетов.КонечноеСальдоКтКонтрагент,
	|
	|	ДеталиРасчетов.НачальноеСальдоДолг,
	|	ДеталиРасчетов.НачальноеСальдоАванс,
	|	ДеталиРасчетов.КонечноеСальдоДолг,
	|	ДеталиРасчетов.КонечноеСальдоАванс,
	|
	|	ДеталиРасчетов.НачальноеСальдоДолгКонтрагент,
	|	ДеталиРасчетов.НачальноеСальдоАвансКонтрагент,
	|	ДеталиРасчетов.КонечноеСальдоДолгКонтрагент,
	|	ДеталиРасчетов.КонечноеСальдоАвансКонтрагент,
	|
	|	ДеталиРасчетов.ЕстьОбороты,
	|	ДеталиРасчетов.ДатаОперации,
	|	ДеталиРасчетов.РасчетныйДокумент,
	|	ДеталиРасчетов.НаименованиеДокумента,
	|	ДеталиРасчетов.НомерДокумента,
	|	ДеталиРасчетов.ДатаДокумента,
	|	ДеталиРасчетов.СвязанныеДокументы,
	|
	|	ДеталиРасчетов.ВалютаДокумента,
	|	ДеталиРасчетов.СуммаДокумента,
	|	
	|	ВЫБОР 
	|		КОГДА ДеталиРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|			ТОГДА 10
	|		КОГДА ДеталиРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|			ТОГДА 20
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	НЕ (ТИПЗНАЧЕНИЯ(ДеталиРасчетов.РасчетныйДокумент) В (ТИП(Документ.ВзаимозачетЗадолженности),ТИП(Документ.Сторно))
	|		И СУММА(ДеталиРасчетов.СуммаДебет - ДеталиРасчетов.СуммаКредит) = 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДеталиРасчетов.Ссылка КАК ДокументСсылка,
	|	ДеталиРасчетов.ОбъектГруппировки КАК ОбъектГруппировки,
	|	ДеталиРасчетов.ВалютаРегл КАК ВалютаРегл,
	|	ДеталиРасчетов.ВалютаСверки КАК ВалютаСверки,
	|	ДеталиРасчетов.ТипРасчетов КАК ТипРасчетов,
	|	ДеталиРасчетов.СверкаПоДоговорам КАК СверкаПоДоговорам,
	|
	|	ДеталиРасчетов.НачальноеСальдоДт КАК НачальноеСальдоДт,
	|	ДеталиРасчетов.НачальноеСальдоКт КАК НачальноеСальдоКт,
	|	СУММА(ДеталиРасчетов.СуммаДебет) КАК ОборотДт,
	|	СУММА(ДеталиРасчетов.СуммаКредит) КАК ОборотКт,
	|	ДеталиРасчетов.КонечноеСальдоДт КАК КонечноеСальдоДт,
	|	ДеталиРасчетов.КонечноеСальдоКт КАК КонечноеСальдоКт,
	|
	|	ДеталиРасчетов.НачальноеСальдоДтКонтрагент КАК НачальноеСальдоДтКонтрагент,
	|	ДеталиРасчетов.НачальноеСальдоКтКонтрагент КАК НачальноеСальдоКтКонтрагент,
	|	СУММА(ДеталиРасчетов.СуммаДебетКонтрагент) КАК ОборотДтКонтрагент,
	|	СУММА(ДеталиРасчетов.СуммаКредитКонтрагент) КАК ОборотКтКонтрагент,
	|	ДеталиРасчетов.КонечноеСальдоДтКонтрагент КАК КонечноеСальдоДтКонтрагент,
	|	ДеталиРасчетов.КонечноеСальдоКтКонтрагент КАК КонечноеСальдоКтКонтрагент,
	|
	|	ДеталиРасчетов.НачальноеСальдоДолг КАК НачальноеСальдоДолг,
	|	ДеталиРасчетов.НачальноеСальдоАванс КАК НачальноеСальдоАванс,
	|	СУММА(ДеталиРасчетов.СуммаДолг) КАК СуммаДолг,
	|	СУММА(ДеталиРасчетов.СуммаАванс) КАК СуммаАванс,
	|	ДеталиРасчетов.КонечноеСальдоДолг КАК КонечноеСальдоДолг,
	|	ДеталиРасчетов.КонечноеСальдоАванс КАК КонечноеСальдоАванс,
	|
	|	ДеталиРасчетов.НачальноеСальдоДолгКонтрагент КАК НачальноеСальдоДолгКонтрагент,
	|	ДеталиРасчетов.НачальноеСальдоАвансКонтрагент КАК НачальноеСальдоАвансКонтрагент,
	|	СУММА(ДеталиРасчетов.СуммаДолгКонтрагент) КАК СуммаДолгКонтрагент,
	|	СУММА(ДеталиРасчетов.СуммаАвансКонтрагент) КАК СуммаАвансКонтрагент,
	|	ДеталиРасчетов.КонечноеСальдоДолгКонтрагент КАК КонечноеСальдоДолгКонтрагент,
	|	ДеталиРасчетов.КонечноеСальдоАвансКонтрагент КАК КонечноеСальдоАвансКонтрагент
	|ПОМЕСТИТЬ втОбщиеИтогиПоОбъектуГруппировки
	|ИЗ
	|	ДетальныеЗаписиДокумента КАК ДеталиРасчетов
	|	
	|СГРУППИРОВАТЬ ПО
	|	ДеталиРасчетов.Ссылка,
	|	ДеталиРасчетов.ОбъектГруппировки,
	|	ДеталиРасчетов.ВалютаСверки,
	|	ДеталиРасчетов.ВалютаРегл,
	|	ДеталиРасчетов.ТипРасчетов,
	|	ДеталиРасчетов.СверкаПоДоговорам,
	|	
	|	ДеталиРасчетов.НачальноеСальдоДт,
	|	ДеталиРасчетов.НачальноеСальдоКт,
	|	ДеталиРасчетов.КонечноеСальдоДт,
	|	ДеталиРасчетов.КонечноеСальдоКт,
	|
	|	ДеталиРасчетов.НачальноеСальдоДолг,
	|	ДеталиРасчетов.НачальноеСальдоАванс,
	|	ДеталиРасчетов.КонечноеСальдоДолг,
	|	ДеталиРасчетов.КонечноеСальдоАванс,
	|
	|	ДеталиРасчетов.НачальноеСальдоДтКонтрагент,
	|	ДеталиРасчетов.НачальноеСальдоКтКонтрагент,
	|	ДеталиРасчетов.КонечноеСальдоДтКонтрагент,
	|	ДеталиРасчетов.КонечноеСальдоКтКонтрагент,
	|
	|	ДеталиРасчетов.НачальноеСальдоДолгКонтрагент,
	|	ДеталиРасчетов.НачальноеСальдоАвансКонтрагент,
	|	ДеталиРасчетов.КонечноеСальдоДолгКонтрагент,
	|	ДеталиРасчетов.КонечноеСальдоАвансКонтрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументСверки.Ссылка КАК ДокументСсылка,
	|	ЕСТЬNULL(ДеталиДокумента.ТипРасчетов, ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.ПустаяСсылка)) КАК ТипРасчетов,
	|	ПредставлениеСсылки(ЕСТЬNULL(ДеталиДокумента.ТипРасчетов, ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.ПустаяСсылка))) КАК ПредставлениеТипРасчетов,
	|	ДокументСверки.Номер КАК Номер,
	|	ДокументСверки.Дата КАК Дата,
	|
	|	ДокументСверки.Организация КАК Организация,
	|	ДокументСверки.Контрагент КАК Контрагент,
	|	ДокументСверки.Партнер КАК Партнер,
	|	ЕСТЬNULL(ДокументСверки.Партнер.НаименованиеПолное, """") КАК ПредставлениеПартнера,
	|
	|	ДокументСверки.ЕстьРасхождения КАК ЕстьРасхождения,
	|	ДокументСверки.НачалоПериода КАК НачалоПериода,
	|	ДокументСверки.КонецПериода КАК КонецПериода,
	|
	|	ВЫБОР КОГДА ДокументСверки.ОтветственноеЛицо = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер)
	|		ТОГДА ЕСТЬNULL(ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование, """")
	|		ИНАЧЕ ЕСТЬNULL(ТаблицаОтветственныеЛица.РуководительНаименование, """")
	|	КОНЕЦ КАК ФИООтветственногоЛица,
	|	ВЫБОР КОГДА ДокументСверки.ОтветственноеЛицо = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер)
	|		ТОГДА ЕСТЬNULL(ТаблицаОтветственныеЛица.ГлавныйБухгалтерДолжность, """")
	|		ИНАЧЕ ЕСТЬNULL(ТаблицаОтветственныеЛица.РуководительДолжность, """")
	|	КОНЕЦ КАК ДолжностьОтветственногоЛица,
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДокументСверки.Контрагент) = ТИП(Справочник.Организации)
	|		ТОГДА 
	|			ВЫБОР КОГДА ДокументСверки.ОтветственноеЛицоОрганизацииКонтрагента = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер)
	|				ТОГДА ТаблицаОтветственныеЛицаОрганизацииКонтрагента.ГлавныйБухгалтерОрганизацииКонтрагентаНаименование
	|				ИНАЧЕ ТаблицаОтветственныеЛицаОрганизацииКонтрагента.РуководительОрганизацииКонтрагентаНаименование
	|			КОНЕЦ
	|		ИНАЧЕ ДокументСверки.ФИОРуководителяКонтрагента
	|	КОНЕЦ КАК ФИОРуководителяКонтрагента,
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДокументСверки.Контрагент) = ТИП(Справочник.Организации)
	|		ТОГДА 
	|			ВЫБОР КОГДА ДокументСверки.ОтветственноеЛицоОрганизацииКонтрагента = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер)
	|				ТОГДА ТаблицаОтветственныеЛицаОрганизацииКонтрагента.ГлавныйБухгалтерОрганизацииКонтрагентаДолжность
	|				ИНАЧЕ ТаблицаОтветственныеЛицаОрганизацииКонтрагента.РуководительОрганизацииКонтрагентаДолжность
	|			КОНЕЦ
	|		ИНАЧЕ ДокументСверки.ДолжностьРуководителяКонтрагента
	|	КОНЕЦ КАК ДолжностьРуководителяКонтрагента,
	|	
	|	ЕСТЬNULL(ДеталиДокумента.ЕстьОбороты, ЛОЖЬ) КАК ЕстьОбороты,
	|	ЕСТЬNULL(ДеталиДокумента.ФинансовыеИнструменты, ЛОЖЬ) КАК ФинансовыеИнструменты,
	|
	|	ЕСТЬNULL(ДеталиДокумента.ОбъектГруппировки, Неопределено) КАК ОбъектГруппировки,
	|	ПредставлениеСсылки(ДеталиДокумента.ОбъектГруппировки) КАК ПредставлениеОбъектГруппировки,
	|
	|	ЕСТЬNULL(ДеталиДокумента.ДатаОперации, ДАТАВРЕМЯ(1,1,1)) КАК ДатаОперации,
	|	ЕСТЬNULL(ДеталиДокумента.РасчетныйДокумент, Неопределено) КАК РасчетныйДокумент,
	|	ЕСТЬNULL(ДеталиДокумента.НаименованиеДокумента, """") КАК НаименованиеДокумента,
	|	ЕСТЬNULL(ДеталиДокумента.НомерДокумента, """") КАК НомерДокумента,
	|	ЕСТЬNULL(ДеталиДокумента.ДатаДокумента, ДАТАВРЕМЯ(1,1,1)) КАК ДатаДокумента,
	|	ЕСТЬNULL(ДеталиДокумента.СвязанныеДокументы, """") КАК СвязанныеДокументы,
	|	
	|	ЕСТЬNULL(ДеталиДокумента.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК Валюта,
	|	ПредставлениеСсылки(ДеталиДокумента.Валюта) КАК ПредставлениеВалюта,
	|	ЕСТЬNULL(ДеталиДокумента.СверкаВВалютеРегл, ЛОЖЬ) КАК СверкаВВалютеРегл,
	|	ДокументСверки.Валюта КАК ВалютаСверки,
	|
	|	ЕСТЬNULL(ДеталиДокумента.ВалютаДокумента, ДокументСверки.Организация.ВалютаРегламентированногоУчета) КАК ВалютаДокумента,
	|	ПредставлениеСсылки(ЕСТЬNULL(ДеталиДокумента.ВалютаДокумента, ДокументСверки.Организация.ВалютаРегламентированногоУчета)) КАК ПредставлениеВалютаДокумента,
	|	
	|	ЕСТЬNULL(ДеталиДокумента.НачальноеСальдоДт, 0) КАК НачальноеСальдоДт,
	|	ЕСТЬNULL(ДеталиДокумента.НачальноеСальдоКт, 0) КАК НачальноеСальдоКт,
	|	ЕСТЬNULL(ДеталиДокумента.ОборотДт, 0) КАК ОборотДт,
	|	ЕСТЬNULL(ДеталиДокумента.ОборотДт, 0) КАК ОборотКт,
	|	ЕСТЬNULL(ДеталиДокумента.КонечноеСальдоДт, 0) КАК КонечноеСальдоДт,
	|	ЕСТЬNULL(ДеталиДокумента.КонечноеСальдоКт, 0) КАК КонечноеСальдоКт,
	|
	|	ЕСТЬNULL(ДеталиДокумента.НачальноеСальдоДтКонтрагент, 0) КАК НачальноеСальдоДтКонтрагент,
	|	ЕСТЬNULL(ДеталиДокумента.НачальноеСальдоКтКонтрагент, 0) КАК НачальноеСальдоКтКонтрагент,
	|	ЕСТЬNULL(ДеталиДокумента.ОборотДтКонтрагент, 0) КАК ОборотДтКонтрагент,
	|	ЕСТЬNULL(ДеталиДокумента.ОборотКтКонтрагент, 0) КАК ОборотКтКонтрагент,
	|	ЕСТЬNULL(ДеталиДокумента.КонечноеСальдоДтКонтрагент, 0) КАК КонечноеСальдоДтКонтрагент,
	|	ЕСТЬNULL(ДеталиДокумента.КонечноеСальдоКтКонтрагент, 0) КАК КонечноеСальдоКтКонтрагент,
	|	
	|	ЕСТЬNULL(ДеталиДокумента.СуммаДебет, 0) КАК СуммаДебет,
	|	ЕСТЬNULL(ДеталиДокумента.СуммаКредит, 0) КАК СуммаКредит,
	|	
	|	ЕСТЬNULL(ДеталиДокумента.СуммаДебетКонтрагент, 0) КАК СуммаДебетКонтрагент,
	|	ЕСТЬNULL(ДеталиДокумента.СуммаКредитКонтрагент, 0) КАК СуммаКредитКонтрагент,
	|	
	|	ЕСТЬNULL(ДеталиДокумента.СуммаДокумента, 0) КАК СуммаДокумента,
	|	ЕСТЬNULL(ДеталиДокумента.СуммаДокумента, 0) КАК СуммаДокументаКонтрагент,
	|
	|	ЕСТЬNULL(ДеталиДокумента.НачальноеСальдоДолг, 0) КАК НачальноеСальдоДолг,
	|	ЕСТЬNULL(ДеталиДокумента.НачальноеСальдоАванс, 0) КАК НачальноеСальдоАванс,
	|	ЕСТЬNULL(ДеталиДокумента.СуммаДолг, 0) КАК СуммаДолг,
	|	ЕСТЬNULL(ДеталиДокумента.СуммаАванс, 0) КАК СуммаАванс,
	|	ЕСТЬNULL(ДеталиДокумента.КонечноеСальдоДолг, 0) КАК КонечноеСальдоДолг,
	|	ЕСТЬNULL(ДеталиДокумента.КонечноеСальдоАванс, 0) КАК КонечноеСальдоАванс,
	|	
	|	ЕСТЬNULL(ДеталиДокумента.НачальноеСальдоДолгКонтрагент, 0) КАК НачальноеСальдоДолгКонтрагент,
	|	ЕСТЬNULL(ДеталиДокумента.НачальноеСальдоАвансКонтрагент, 0) КАК НачальноеСальдоАвансКонтрагент,
	|	ЕСТЬNULL(ДеталиДокумента.СуммаДолгКонтрагент, 0) КАК СуммаДолгКонтрагент,
	|	ЕСТЬNULL(ДеталиДокумента.СуммаАвансКонтрагент, 0) КАК СуммаАвансКонтрагент,
	|	ЕСТЬNULL(ДеталиДокумента.КонечноеСальдоДолгКонтрагент, 0) КАК КонечноеСальдоДолгКонтрагент,
	|	ЕСТЬNULL(ДеталиДокумента.КонечноеСальдоАвансКонтрагент, 0) КАК КонечноеСальдоАвансКонтрагент,
	|	
	|	ЕСТЬNULL(ДеталиДокумента.ПорядокРасчетов, 0) КАК ПорядокРасчетов
	|ИЗ
	|	Документ.СверкаВзаиморасчетов2_5_11 КАК ДокументСверки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ДокументСверки.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛицаОрганизацииКонтрагента КАК ТаблицаОтветственныеЛицаОрганизацииКонтрагента
	|		ПО ДокументСверки.Ссылка = ТаблицаОтветственныеЛицаОрганизацииКонтрагента.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДетальныеЗаписиДокумента КАК ДеталиДокумента
	|		ПО ДокументСверки.Ссылка = ДеталиДокумента.Ссылка
	|ГДЕ
	|	ДокументСверки.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаОперации,
	|	ДатаДокумента,
	|	НаименованиеДокумента,
	|	НомерДокумента,
	|	ПорядокТипаСуммы,
	|	ЕСТЬNULL(ДеталиДокумента.ОборотДт, 0) УБЫВ
	|
	|ИТОГИ
	|	МАКСИМУМ(НачальноеСальдоДт),
	|	МАКСИМУМ(НачальноеСальдоКт),
	|	МАКСИМУМ(ОборотДт),
	|	МАКСИМУМ(ОборотДт),
	|	МАКСИМУМ(КонечноеСальдоДт),
	|	МАКСИМУМ(КонечноеСальдоКт),
	|
	|	МАКСИМУМ(НачальноеСальдоДтКонтрагент),
	|	МАКСИМУМ(НачальноеСальдоКтКонтрагент),
	|	МАКСИМУМ(ОборотДтКонтрагент),
	|	МАКСИМУМ(ОборотДтКонтрагент),
	|	МАКСИМУМ(КонечноеСальдоДтКонтрагент),
	|	МАКСИМУМ(КонечноеСальдоКтКонтрагент),
	|	
	|	МАКСИМУМ(ФИООтветственногоЛица),
	|	МАКСИМУМ(ДолжностьОтветственногоЛица),
	|	МАКСИМУМ(ФИОРуководителяКонтрагента),
	|	МАКСИМУМ(ДолжностьРуководителяКонтрагента),
	|	МАКСИМУМ(Валюта),
	|	МАКСИМУМ(ПредставлениеВалюта),
	|	МАКСИМУМ(ВалютаДокумента),
	|	МАКСИМУМ(ПредставлениеВалютаДокумента),
	|	МАКСИМУМ(ПредставлениеОбъектГруппировки),
	|	МАКСИМУМ(ФинансовыеИнструменты),
	|	МАКСИМУМ(ЕстьОбороты),
	|	МАКСИМУМ(ЕстьРасхождения),
	|
	|	СУММА(СуммаДебет),
	|	СУММА(СуммаКредит),
	|	
	|	СУММА(СуммаДебетКонтрагент),
	|	СУММА(СуммаКредитКонтрагент),
	|	СУММА(СуммаДолгКонтрагент),
	|	СУММА(СуммаАвансКонтрагент),
	|	
	|	СУММА(СуммаДокумента),
	|	СУММА(СуммаДолг),
	|	СУММА(СуммаАванс)
	|ПО
	|	ДокументСсылка,
	|	ТипРасчетов,
	|	ОбъектГруппировки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура РасшифроватьНастройкуДетализации(НастройкиПечати)
	
	ГруппировкаРасчетов = Новый Массив;
	ГруппировкаРасчетов.Добавить("Валюта");
	ГруппировкаРасчетов.Добавить("Договор");
	ГруппировкаРасчетов.Добавить("ОбъектРасчетов");
	
	ГруппировкаФинИнструментов = Новый Массив;
	ГруппировкаФинИнструментов.Добавить("Валюта");
	ГруппировкаФинИнструментов.Добавить("ДоговорФинансовыхИнструментов");
	
	НастройкиПечати.ДетализацияВзаиморасчетов = ГруппировкаРасчетов[НастройкиПечати.ДетализацияВзаиморасчетов];
	НастройкиПечати.ДетализацияФинансовыхИнструментов = ГруппировкаФинИнструментов[НастройкиПечати.ДетализацияФинансовыхИнструментов];
	
КонецПроцедуры

Процедура ОчиститьДебетКредит(Область)
	
	ПараметрыОбласти = Новый Структура("СуммаДебет,СуммаКредит,СуммаДебетКонтрагент,СуммаКредитКонтрагент");
	ЗаполнитьЗначенияСвойств(ПараметрыОбласти, Область.Параметры);
	
	Область.Параметры.СуммаДебет = 0;
	Область.Параметры.СуммаКредит = 0;
	Если ПараметрыОбласти.СуммаДебетКонтрагент <> Неопределено Тогда 
		Область.Параметры.СуммаДебетКонтрагент = 0;
	КонецЕсли;
	Если ПараметрыОбласти.СуммаКредитКонтрагент <> Неопределено Тогда 
		Область.Параметры.СуммаКредитКонтрагент = 0;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет структуру данными о получателях печатных форм.
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ, описание которого готовится.
// 	Номер - Строка - Входящий номер документа.
// 	Дата - Дата - Входящая дата документа.
// 	ПредставлениеДокумента - Строка - представление входящего документа.
// 
// Возвращаемое значение:
// 	Строка - Описание документа
// 
Функция ОписаниеРасчетногоДокумента(ДокументСсылка, Номер, Дата, ПредставлениеДокумента) Экспорт
	
	ПредставленияДокументов = ПредставленияДокументов();
	ВходящиеДокументы = ВходящиеДокументы();
	
	ОписаниеДокумента = Новый Структура("НомерДокумента,ДатаДокумента,НаименованиеДокумента","","","");

	Если ТипЗнч(ДокументСсылка) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		
		ОписаниеДокумента.НаименованиеДокумента = Строка(ДокументСсылка);
		
	ИначеЕсли ДокументСсылка <> Неопределено И НЕ ДокументСсылка.Пустая() Тогда

		ИмяДокумента = ДокументСсылка.Метаданные().Имя;
		Если НЕ ЗначениеЗаполнено(ПредставлениеДокумента) Тогда
			ПредставлениеДокумента = "";
			ПредставленияДокументов.Свойство(ИмяДокумента, ПредставлениеДокумента);
		КонецЕсли;
		Если ИмяДокумента = "СписаниеБезналичныхДенежныхСредств" Тогда
			НомерНаПечать = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Номер,, Истина);
		ИначеЕсли ВходящиеДокументы.Свойство(ИмяДокумента) Тогда
			НомерНаПечать = УбратьЛидирующиеНули(Номер);
		Иначе
			НомерНаПечать = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Номер);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Номер) Тогда
			ОписаниеДокумента.НомерДокумента = НомерНаПечать;
		КонецЕсли;
		Если ЗначениеЗаполнено(Дата) Тогда
			ОписаниеДокумента.ДатаДокумента = Дата;
		КонецЕсли;
		Если ЗначениеЗаполнено(ПредставлениеДокумента) Тогда
			ОписаниеДокумента.НаименованиеДокумента = ПредставлениеДокумента;
		КонецЕсли;
		
	КонецЕсли;

	Возврат ОписаниеДокумента;

КонецФункции

// Представления документов.
// 
// Возвращаемое значение:
//  Структура - Представления документов:
//    * Ключ - Строка - Имя метаданных документа.
//    * Значение - Строка - Представление документа.
//  
Функция ПредставленияДокументов() Экспорт
	
	ВидыДокументовКонтрагента = Новый Структура;
	
	ВидыДокументовКонтрагента.Вставить("АвансовыйОтчет",                        НСтр("ru='Приходный кассовый ордер'"));
	ВидыДокументовКонтрагента.Вставить("АктВыполненныхРабот",                   НСтр("ru='Акт выполненных работ'"));
	ВидыДокументовКонтрагента.Вставить("ВводОстатков",                          НСтр("ru='Корректировка задолженности'"));
	ВидыДокументовКонтрагента.Вставить("ВводОстатковВзаиморасчетов",            НСтр("ru='Корректировка задолженности'"));
	ВидыДокументовКонтрагента.Вставить("ВводОстатковПоФинансовымИнструментам",  НСтр("ru='Корректировка задолженности'"));
	ВидыДокументовКонтрагента.Вставить("ВзаимозачетЗадолженности",              НСтр("ru='Взаимозачет задолженности'"));
	ВидыДокументовКонтрагента.Вставить("ВозвратТоваровМеждуОрганизациями",      НСтр("ru='Возврат товаров'"));
	ВидыДокументовКонтрагента.Вставить("ВозвратТоваровОтКлиента",               НСтр("ru='Возврат товаров'"));
	ВидыДокументовКонтрагента.Вставить("ВозвратТоваровПоставщику",              НСтр("ru='Возврат товаров'"));
	ВидыДокументовКонтрагента.Вставить("ВыкупВозвратнойТарыКлиентом",           НСтр("ru='Выкуп возвратной тары'"));
	ВидыДокументовКонтрагента.Вставить("ВыкупВозвратнойТарыУПоставщика",        НСтр("ru='Выкуп возвратной тары'"));
	ВидыДокументовКонтрагента.Вставить("ВыкупПринятыхНаХранениеТоваров",        НСтр("ru='Приобретение'"));
	ВидыДокументовКонтрагента.Вставить("ВыкупТоваровХранителем",                НСтр("ru='Реализация'"));
	ВидыДокументовКонтрагента.Вставить("КорректировкаЗадолженности",            НСтр("ru='Корректировка задолженности'"));
	ВидыДокументовКонтрагента.Вставить("КорректировкаЗадолженностиПоФинансовымИнструментам", НСтр("ru='Корректировка задолженности'"));
	ВидыДокументовКонтрагента.Вставить("КорректировкаПриобретения",             НСтр("ru='Корректировка задолженности'"));
	ВидыДокументовКонтрагента.Вставить("КорректировкаРеализации",               НСтр("ru='Корректировка задолженности'"));
	ВидыДокументовКонтрагента.Вставить("КорректировкаРегистров",                НСтр("ru='Корректировка задолженности'"));
	ВидыДокументовКонтрагента.Вставить("НачисленияКредитовИДепозитов",          НСтр("ru='Начисление'"));
	ВидыДокументовКонтрагента.Вставить("ОперацияПоПлатежнойКарте",              НСтр("ru='Эквайринговая операция'"));
	ВидыДокументовКонтрагента.Вставить("ОперацияПоЯндексКассе",                 НСтр("ru='Эквайринговая операция'"));
	ВидыДокументовКонтрагента.Вставить("ОтчетКомиссионера",                     НСтр("ru='Отчет комитенту'"));
	ВидыДокументовКонтрагента.Вставить("ОтчетКомиссионераОСписании",            НСтр("ru='Отчет комитенту'"));
	ВидыДокументовКонтрагента.Вставить("ОтчетКомитенту",                        НСтр("ru='Отчет комитенту'"));
	ВидыДокументовКонтрагента.Вставить("ОтчетКомитентуОЗакупках",               НСтр("ru='Отчет комитенту'"));
	ВидыДокументовКонтрагента.Вставить("ОтчетКомитентуОСписании",               НСтр("ru='Отчет комитенту'"));
	ВидыДокументовКонтрагента.Вставить("ОтчетПоКомиссииМеждуОрганизациями",     НСтр("ru='Отчет комитенту'"));
	ВидыДокументовКонтрагента.Вставить("ОтчетПоКомиссииМеждуОрганизациямиОСписании",НСтр("ru='Отчет комитенту'"));
	ВидыДокументовКонтрагента.Вставить("ПередачаТоваровМеждуОрганизациями",     НСтр("ru='Передача товаров'"));
	ВидыДокументовКонтрагента.Вставить("ПоступлениеБезналичныхДенежныхСредств", НСтр("ru='Платежное поручение'"));
	ВидыДокументовКонтрагента.Вставить("ПриобретениеТоваровУслуг",              НСтр("ru='Приобретение'"));
	ВидыДокументовКонтрагента.Вставить("ПриобретениеУслугПрочихАктивов",        НСтр("ru='Приобретение'"));
	ВидыДокументовКонтрагента.Вставить("ПриходныйКассовыйОрдер",                НСтр("ru='Приходный кассовый ордер'"));
	ВидыДокументовКонтрагента.Вставить("РасходныйКассовыйОрдер",                НСтр("ru='Расходный кассовый ордер'"));
	ВидыДокументовКонтрагента.Вставить("РасчетКурсовыхРазниц",                  НСтр("ru='Курсовые разницы'"));
	ВидыДокументовКонтрагента.Вставить("РеализацияТоваровУслуг",                НСтр("ru='Реализация'"));
	ВидыДокументовКонтрагента.Вставить("РеализацияУслугПрочихАктивов",          НСтр("ru='Реализация'"));
	ВидыДокументовКонтрагента.Вставить("СписаниеБезналичныхДенежныхСредств",    НСтр("ru='Платежное поручение'"));
	ВидыДокументовКонтрагента.Вставить("Сторно",                                НСтр("ru='Сторно'"));
	ВидыДокументовКонтрагента.Вставить("СчетФактураВыданный",                   НСтр("ru='Счет-фактура'"));
	ВидыДокументовКонтрагента.Вставить("СчетФактураПолученный",                 НСтр("ru='Счет-фактура'"));
	ВидыДокументовКонтрагента.Вставить("СчетФактураПолученныйНалоговыйАгент",   НСтр("ru='Счет-фактура'"));
	ВидыДокументовКонтрагента.Вставить("ТаможеннаяДекларацияИмпорт",            НСтр("ru='Таможенная декларация'"));

	
	Возврат ВидыДокументовКонтрагента;
	
КонецФункции

// Возвращаемое значение:
//  Структура - Представления документов:
//    * Ключ - Строка - Имя метаданных документа.
//    * Значение - Строка - Значение не заполняется.
//
Функция ВходящиеДокументы() Экспорт
	
	ВидыДокументовКонтрагента = Новый Структура;
	
	ВидыДокументовКонтрагента.Вставить("ВозвратТоваровМеждуОрганизациями");
	ВидыДокументовКонтрагента.Вставить("ВозвратТоваровОтКлиента");
	ВидыДокументовКонтрагента.Вставить("ВыкупВозвратнойТарыУПоставщика");
	ВидыДокументовКонтрагента.Вставить("КорректировкаПриобретения");
	ВидыДокументовКонтрагента.Вставить("ОтчетКомиссионера");
	ВидыДокументовКонтрагента.Вставить("ОтчетКомиссионераОСписании");
	ВидыДокументовКонтрагента.Вставить("ОтчетКомитенту");
	ВидыДокументовКонтрагента.Вставить("ОтчетКомитентуОСписании");
	ВидыДокументовКонтрагента.Вставить("ОтчетПоКомиссииМеждуОрганизациями");
	ВидыДокументовКонтрагента.Вставить("ОтчетПоКомиссииМеждуОрганизациямиОСписании");
	ВидыДокументовКонтрагента.Вставить("ПоступлениеБезналичныхДенежныхСредств");
	ВидыДокументовКонтрагента.Вставить("ПриобретениеТоваровУслуг");
	ВидыДокументовКонтрагента.Вставить("ПриобретениеУслугПрочихАктивов");
	ВидыДокументовКонтрагента.Вставить("ПриходныйКассовыйОрдер");
	ВидыДокументовКонтрагента.Вставить("СписаниеБезналичныхДенежныхСредств");
	ВидыДокументовКонтрагента.Вставить("СчетФактураПолученный");
	ВидыДокументовКонтрагента.Вставить("СчетФактураПолученныйНалоговыйАгент");
	
	Возврат ВидыДокументовКонтрагента;
	
КонецФункции

Функция УбратьЛидирующиеНули(Строка)
	
	Результат = СокрЛП(Строка);
	Пока Лев(Результат, 1) = "0" ИЛИ Лев(Результат, 1) = "-" Цикл 
		Результат = Сред(Результат, 2);
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

Функция ДокументыНаПечать(МассивОбъектов)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Ссылка КАК Ссылка,
	| 	ВложенныйЗапрос.МоментВремени КАК МоментВремени
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		РасчетыПоДокументу.Ссылка КАК Ссылка,
	|		РасчетыПоДокументу.Ссылка.МоментВремени КАК МоментВремени
	|	ИЗ
	|		Документ.СверкаВзаиморасчетов2_5_11.ИтоговыеЗаписи КАК РасчетыПоДокументу
	|	ГДЕ
	|		РасчетыПоДокументу.Ссылка В (&ДокументыСверки)) КАК ВложенныйЗапрос
	|
	|УПОРЯДОЧИТЬ ПО
	| 	ВложенныйЗапрос.МоментВремени";
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("ДокументыСверки", МассивОбъектов);
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

// Возвращает данные для формирования печатной формы УПД
//
// Параметры:
//	ПараметрыПечати - Структура - параметры.
//	МассивОбъектов - Массив из ДокументСсылка.СверкаВзаиморасчетов2_5_11 - ссылки на документы, по которым необходимо
//																		получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемая при печати УПД, содержащая следующие свойства:
//		* РезультатПоШапке - РезультатЗапроса - общие данные накладной.
//		* РезультатПоТабличнойЧасти - РезультатЗапроса - данные табличной части накладной.
//
Функция ПолучитьДанныеДляПечатнойФормыУПД(ПараметрыПечати, МассивОбъектов) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	///////////////////////////////////////////////////////////
	// Общие реквизиты.
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.Дата КАК ДатаДокумента,
	|	ДанныеДокументов.РежимСверкиИтоговВзаиморасчетов КАК РежимСверкиИтоговВзаиморасчетов,
	|	ДанныеДокументов.Статус КАК Статус,
	|
	///////////////////////////////////////////////////////////
	// Акт сверки взаимных расчетов, информация отправителя (Документ).
	//
	// Дата начала периода сверки взаимных расчетов. ДатаНачалаПериодаСверки = ДатаНачПер.
	|	ДанныеДокументов.НачалоПериода КАК ДатаНачалаПериодаСверки,
	// Дата окончания периода сверки взаимных расчетов. ДатаОкончанияПериодаСверки = ДатаОкПер.
	|	ДанныеДокументов.КонецПериода КАК ДатаОкончанияПериодаСверки,
	// Номер акта сверки. НомерДокумента = НомерАкт.
	|	ДанныеДокументов.Номер КАК НомерДокумента,
	///////////////////////////////////////////////////////////
	// Сведения документа, кроме сведений таблицы акта сверки взаимных расчетов (СвДокКрАкт).
	// Код валюты. КодВалюты = КодОКВ
	|	ДанныеДокументов.Валюта.Код КАК КодВалюты,
	// Сведения об отправителе. Отправитель = СвОтпр.
	|	ДанныеДокументов.Организация КАК Отправитель,
	// Сведения о получателе. Получатель = СвПол.
	|	ДанныеДокументов.Контрагент КАК Получатель,
	|
	///////////////////////////////////////////////////////////
	// Сведения таблицы акта сверки взаимных расчетов (ТаблАкт).
	// Сумма сальдо на начало периода сверки дебет. СальдоНаНачалоПериодаДебет = СальдоНачДеб.
	|	СУММА(ЕСТЬNULL(ИтоговыеЗаписиДокумента.НачальноеСальдоДт, 0)) КАК СальдоНаНачалоПериодаДебет,
	// Сумма сальдо на начало периода сверки кредит. СальдоНаНачалоПериодаКредит = СальдоНачКр.
	|	СУММА(ЕСТЬNULL(ИтоговыеЗаписиДокумента.НачальноеСальдоКт, 0)) КАК СальдоНаНачалоПериодаКредит,
	// Обороты по дебету. ОборотПоДебету = ОборотДеб.
	|	СУММА(ЕСТЬNULL(ИтоговыеЗаписиДокумента.ОборотДт, 0)) КАК ОборотПоДебету,
	// Обороты по кредиту. ОборотПоКредиту = ОборотКр.
	|	СУММА(ЕСТЬNULL(ИтоговыеЗаписиДокумента.ОборотКт, 0)) КАК ОборотПоКредиту,
	// Обороты по кредиту. СальдоНаКонецПериодаДебет = СальдоКонДеб.
	|	СУММА(ЕСТЬNULL(ИтоговыеЗаписиДокумента.КонечноеСальдоДт, 0)) КАК СальдоНаКонецПериодаДебет,
	// Сумма сальдо на конец периода сверки кредит. СальдоНаКонецПериодаКредит = СальдоКонКр.
	|	СУММА(ЕСТЬNULL(ИтоговыеЗаписиДокумента.КонечноеСальдоКт, 0)) КАК СальдоНаКонецПериодаКредит
	|ИЗ
	|	Документ.СверкаВзаиморасчетов2_5_11 КАК ДанныеДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СверкаВзаиморасчетов2_5_11.ИтоговыеЗаписи КАК ИтоговыеЗаписиДокумента
	|		ПО ДанныеДокументов.Ссылка = ИтоговыеЗаписиДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокументов.Ссылка,
	|	ДанныеДокументов.Валюта,
	|	ДанныеДокументов.Организация,
	|	ДанныеДокументов.Контрагент,
	|	ДанныеДокументов.НачалоПериода,
	|	ДанныеДокументов.КонецПериода,
	|	ДанныеДокументов.Номер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	///////////////////////////////////////////////////////////
	// Сведения таблицы акта сверки взаимных расчетов (ТаблАкт).
	|ВЫБРАТЬ
	// Сумма сальдо на начало периода сверки дебет. СальдоНаНачалоПериодаДебет = СальдоНачДеб.
	|	СУММА(ИтоговыеЗаписи.НачальноеСальдоДт) КАК СальдоНаНачалоПериодаДебет,
	// Сумма сальдо на начало периода сверки кредит. СальдоНаНачалоПериодаКредит = СальдоНачКр.
	|	СУММА(ИтоговыеЗаписи.НачальноеСальдоКт) КАК СальдоНаНачалоПериодаКредит,
	// Обороты по дебету. ОборотПоДебету = ОборотДеб.
	|	СУММА(ИтоговыеЗаписи.ОборотДт) КАК ОборотПоДебету,
	// Обороты по кредиту. ОборотПоКредиту = ОборотКр.
	|	СУММА(ИтоговыеЗаписи.ОборотКт) КАК ОборотПоКредиту,
	// Обороты по кредиту. СальдоНаКонецПериодаДебет = СальдоКонДеб.
	|	СУММА(ИтоговыеЗаписи.КонечноеСальдоДт) КАК СальдоНаКонецПериодаДебет,
	// Сумма сальдо на конец периода сверки кредит. СальдоНаКонецПериодаКредит = СальдоКонКр.
	|	СУММА(ИтоговыеЗаписи.КонечноеСальдоКт) КАК СальдоНаКонецПериодаКредит,
	|
	///////////////////////////////////////////////////////////
	// Сведения о договоре (ДогСв).
	// Идентификатор договора в учетной системе отправителя. ИдентификаторДоговора = ИдДог.
	|	ИтоговыеЗаписи.Договор КАК ИдентификаторДоговора,
	// Описание договора в учетной системе отправителя. ОписаниеДоговора = ОписТипДог.
	|	ИтоговыеЗаписи.НаименованиеДоговора КАК ОписаниеДоговора,
	// Номер договора. НомерДоговора = НомДог.
	|	ИтоговыеЗаписи.НомерДоговора КАК НомерДоговора,
	// Дата договора (дата принятия договора оферты). ДатаДоговора = ДатаДог.
	|	ИтоговыеЗаписи.ДатаДоговора КАК ДатаДоговора,
	|
	///////////////////////////////////////////////////////////
	// Сведения о документе.
	// Наименование документа.
	|	ВЫБОР
	|		КОГДА ДетальныеЗаписи.НаименованиеДокумента ЕСТЬ NULL
	|			ТОГДА """"
	|		КОГДА ДетальныеЗаписи.НаименованиеДокумента = """"
	|			ТОГДА ДетальныеЗаписи.НаименованиеДокументаКонтрагент
	|		ИНАЧЕ
	|			ДетальныеЗаписи.НаименованиеДокумента
	|	КОНЕЦ КАК НаименованиеДокумента,
	// Номер документа. НомерДокумента = НомДок.
	|	ВЫБОР
	|		КОГДА ДетальныеЗаписи.НомерДокумента ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ДетальныеЗаписи.НомерДокумента = """"
	|			ТОГДА ДетальныеЗаписи.НомерДокументаКонтрагент
	|		ИНАЧЕ
	|			ДетальныеЗаписи.НомерДокумента
	|	КОНЕЦ КАК НомерДокумента,
	// Дата документа. ДатаДокумента = ДатаДок.
	|	ВЫБОР
	|		КОГДА ДетальныеЗаписи.ДатаДокумента ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ДетальныеЗаписи.ДатаДокумента = ДАТАВРЕМЯ(1,1,1)
	|			ТОГДА ДетальныеЗаписи.ДатаДокументаКонтрагент
	|		ИНАЧЕ
	|			ДетальныеЗаписи.ДатаДокумента
	|	КОНЕЦ КАК ДатаДокумента,
	// Идентификатор документа в учетной системе отправителя. ИдентификаторДокумента = ИдДок.
	|	ЕСТЬNULL(ДетальныеЗаписи.РасчетныйДокумент, НЕОПРЕДЕЛЕНО) КАК ИдентификаторДокумента,
	// Дополнительная информация. ДопИнформация = ДопИнф.
	|	"""" КАК ДопИнформация,
	|
	///////////////////////////////////////////////////////////
	// Сведения об операции (СвОпер)
	// Номер строки таблицы "Сведения об операции". ПорядковыйНомер = НомСтр.
	|	ЕСТЬNULL(ДетальныеЗаписи.НомерСтроки, 0) КАК ПорядковыйНомер,
	// Дата операции. ДатаОперации = ДатаОпер.
	|	ВЫБОР
	|		КОГДА ДетальныеЗаписи.ДатаДокумента ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ДетальныеЗаписи.ДатаДокумента = ДАТАВРЕМЯ(1,1,1)
	|			ТОГДА ДетальныеЗаписи.ДатаДокументаКонтрагент
	|		ИНАЧЕ
	|			ДетальныеЗаписи.ДатаДокумента
	|	КОНЕЦ КАК ДатаОперации,
	// Наименование операции. НаименованиеОперации = НаимОпер.
	|	ВЫБОР
	|		КОГДА ДетальныеЗаписи.НаименованиеДокумента ЕСТЬ NULL
	|			ТОГДА """"
	|		КОГДА ДетальныеЗаписи.НаименованиеДокумента = """"
	|			ТОГДА ДетальныеЗаписи.НаименованиеДокументаКонтрагент
	|		ИНАЧЕ
	|			ДетальныеЗаписи.НаименованиеДокумента
	|	КОНЕЦ КАК НаименованиеОперации,
	// Сумма дебета операции. СуммаДебет = СумДебет.
	|	СУММА(ЕСТЬNULL(ДетальныеЗаписи.СуммаДебет, 0)) КАК СуммаДебет,
	// Сумма кредита операции. СуммаКредит = СумКредит.
	|	СУММА(ЕСТЬNULL(ДетальныеЗаписи.СуммаКредит, 0)) КАК СуммаКредит
	|ИЗ
	|	Документ.СверкаВзаиморасчетов2_5_11.ИтоговыеЗаписи КАК ИтоговыеЗаписи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СверкаВзаиморасчетов2_5_11.ДетальныеЗаписи КАК ДетальныеЗаписи
	|		ПО ИтоговыеЗаписи.Ссылка = ДетальныеЗаписи.Ссылка
	|		И ИтоговыеЗаписи.ТипРасчетов = ДетальныеЗаписи.ТипРасчетов
	|		И ИтоговыеЗаписи.Партнер = ДетальныеЗаписи.Партнер
	|		И ИтоговыеЗаписи.Договор = ДетальныеЗаписи.Договор
	|		И ИтоговыеЗаписи.ОбъектРасчетов = ДетальныеЗаписи.ОбъектРасчетов
	|ГДЕ
	|	ИтоговыеЗаписи.Ссылка В (&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИтоговыеЗаписи.Ссылка,
	|	ИтоговыеЗаписи.Договор,
	|	НомерДокумента,
	|	ДетальныеЗаписи.НомерСтроки,
	|	ВЫРАЗИТЬ(ИтоговыеЗаписи.Договор КАК Справочник.ДоговорыКонтрагентов),
	|	ИтоговыеЗаписи.НомерДоговора,
	|	ИтоговыеЗаписи.ДатаДоговора,
	|	ИтоговыеЗаписи.НаименованиеДоговора,
	|	ВЫБОР
	|		КОГДА ДетальныеЗаписи.НомерДокумента ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ДетальныеЗаписи.НомерДокумента = """"
	|			ТОГДА ДетальныеЗаписи.НомерДокументаКонтрагент
	|		ИНАЧЕ
	|			ДетальныеЗаписи.НомерДокумента
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ДетальныеЗаписи.ДатаДокумента ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ДетальныеЗаписи.ДатаДокумента = ДАТАВРЕМЯ(1,1,1)
	|			ТОГДА ДетальныеЗаписи.ДатаДокументаКонтрагент
	|		ИНАЧЕ
	|			ДетальныеЗаписи.ДатаДокумента
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ДетальныеЗаписи.НаименованиеДокумента ЕСТЬ NULL
	|			ТОГДА """"
	|		КОГДА ДетальныеЗаписи.НаименованиеДокумента = """"
	|			ТОГДА ДетальныеЗаписи.НаименованиеДокументаКонтрагент
	|		ИНАЧЕ
	|			ДетальныеЗаписи.НаименованиеДокумента
	|	КОНЕЦ,
	|	ДетальныеЗаписи.РасчетныйДокумент
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИтоговыеЗаписи.Договор,
	|	РасчетныйДокумент,
	|	ПорядковыйНомер
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Выполнить();
	
	МассивРезультатов			= Запрос.ВыполнитьПакет();
	РезультатПоШапке			= МассивРезультатов[0];
	РезультатПоТабличнойЧасти	= МассивРезультатов[1];
	
	ДанныеДляПечати = Новый Структура;
	ДанныеДляПечати.Вставить("РезультатПоШапке",			РезультатПоШапке);
	ДанныеДляПечати.Вставить("РезультатПоТабличнойЧасти",	РезультатПоТабличнойЧасти);
	
	Возврат ДанныеДляПечати;
	
КонецФункции

#КонецОбласти

#Область ШаблоныСообщений

// Вызывается при подготовке шаблонов сообщений и позволяет переопределить список реквизитов и вложений.
//
// Параметры:
//  Реквизиты               - ДеревоЗначений - список реквизитов шаблона:
//         * Имя            - Строка - Уникальное имя общего реквизита.
//         * Представление  - Строка - Представление общего реквизита.
//         * Тип            - Тип    - Тип реквизита. По умолчанию строка.
//         * Формат         - Строка - Формат вывода значения для чисел, дат, строк и булевых значений.
//  Вложения                - ТаблицаЗначений - печатные формы и вложения:
//         * Имя            - Строка - Уникальное имя вложения.
//         * Представление  - Строка - Представление варианта.
//         * ТипФайла       - Строка - Тип вложения, который соответствует расширению файла: "pdf", "png", "jpg", mxl"
//                                      и др.
//  ДополнительныеПараметры - Структура - дополнительные сведения о шаблоне сообщений.
//
Процедура ПриПодготовкеШаблонаСообщения(Реквизиты, Вложения, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

// Вызывается в момент создания сообщений по шаблону для заполнения значений реквизитов и вложений.
//
// Параметры:
//  Сообщение - Структура - структура с ключами:
//    * ЗначенияРеквизитов - Соответствие из КлючИЗначение- список используемых в шаблоне реквизитов:
//      * Ключ     - Строка - имя реквизита в шаблоне;
//      * Значение - Строка - значение заполнения в шаблоне.
//    * ЗначенияОбщихРеквизитов - Соответствие из КлючИЗначение - список используемых в шаблоне общих реквизитов:
//      * Ключ     - Строка - имя реквизита в шаблоне;
//      * Значение - Строка - значение заполнения в шаблоне.
//    * Вложения - Соответствие из КлючИЗначение - значения реквизитов
//      * Ключ     - Строка - имя вложения в шаблоне;
//      * Значение - ДвоичныеДанные, Строка - двоичные данные или адрес во временном хранилище вложения.
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//  ДополнительныеПараметры - Структура -  Дополнительная информация о шаблоне сообщения.
//
Процедура ПриФормированииСообщения(Сообщение, ПредметСообщения, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

// Заполняет список получателей SMS при отправке сообщения сформированного по шаблону.
//
// Параметры:
//   ПолучателиSMS - ТаблицаЗначений - список получается SMS:
//     * НомерТелефона - Строка - номер телефона, куда будет отправлено сообщение SMS.
//     * Представление - Строка - представление получателя сообщения SMS.
//     * Контакт       - СправочникСсылка - контакт, которому принадлежит номер телефона.
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//
Процедура ПриЗаполненииТелефоновПолучателейВСообщении(ПолучателиSMS, ПредметСообщения) Экспорт
	
КонецПроцедуры

// Заполняет список получателей письма при отправки сообщения сформированного по шаблону.
//
// Параметры:
//   ПолучателиПисьма - ТаблицаЗначений - список получается письма:
//     * Адрес           - Строка - адрес электронной почты получателя.
//     * Представление   - Строка - представление получается письма.
//     * ВариантОтправки - Строка - Варианты отправки письма: "Кому", "Копия", "СкрытаяКопия", "ОбратныйАдреса";
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//
Процедура ПриЗаполненииПочтыПолучателейВСообщении(ПолучателиПисьма, ПредметСообщения) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область ФормаПомощникаСоздания

#Область СозданиеДокументов

Процедура СоздатьДокументы(Параметры, АдресХранилища = "") Экспорт
	
	ТекущаяДата  = ТекущаяДатаСеанса();
	ТекущееВремя = Час(ТекущаяДата) * 3600 + Минута(ТекущаяДата) * 60 + Секунда(ТекущаяДата);
	
	ДатаДокументов = Параметры.ДатаДокументов;
	ТаблицаРасчетов = Параметры.ТаблицаРасчетов;
	ОтработанныеДокументы = Новый Массив;
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("ПериодСверки", Параметры.ПериодСверки);
	ДанныеЗаполнения.Вставить("ДоговорыБезОборотов", Параметры.ДоговорыБезОборотов);
	ДанныеЗаполнения.Вставить("РазбиватьПоТипамРасчетов", Параметры.РазбиватьПоТипамРасчетов);
	ДанныеЗаполнения.Вставить("РазбиватьПоПартнерам", Параметры.РазбиватьПоПартнерам);
	ДанныеЗаполнения.Вставить("РазбиватьПоДоговорам", Параметры.РазбиватьПоДоговорам);
	ДанныеЗаполнения.Вставить("РежимСверкиИтоговВзаиморасчетов", Параметры.РежимСверкиИтоговВзаиморасчетов);
	ДанныеЗаполнения.Вставить("СверкаВВалюте", Параметры.СверкаВВалюте);
	ДанныеЗаполнения.Вставить("УстанавливатьНовыйНомер", Ложь);
	
	КомпоновщикОтбор = Новый КомпоновщикНастроекКомпоновкиДанных;
	ИнициализироватьКомпоновщикНастроек(КомпоновщикОтбор,,Параметры.НастройкиОтбора);
	МассивВсехДокументовСверки = Новый Массив;
	Для Каждого СтрокаТаблицы Из ТаблицаРасчетов Цикл

		ДанныеЗаполнения.Вставить("ДанныеДокумента", СтрокаТаблицы);
		ДанныеЗаполнения.Вставить("НастройкиОтбора", КомпоновщикОтбор.ПолучитьНастройки());
		
		Если СтрокаТаблицы.СоздаватьДокумент ИЛИ СтрокаТаблицы.ИзменятьДокумент = "УдалитьСоздатьНовый" Тогда
			
			ДокументОбъект = СоздатьДокумент();
			
			ДокументОбъект.Дата = ДатаДокументов + ТекущееВремя;
			
			ДанныеЗаполнения.УстанавливатьНовыйНомер = Истина;
			ДанныеЗаполнения.Вставить("ДанныеДокумента", СтрокаТаблицы);
			ЗаполнитьПровестиДокументОбъект(ДокументОбъект, ДанныеЗаполнения);
			
			ДобавитьВПротоколВыполнения(ДокументОбъект.Ссылка, "Создание", ОтработанныеДокументы); 
			МассивВсехДокументовСверки.Добавить(ДокументОбъект.Ссылка);
			
		КонецЕсли;
		
		Если СтрокаТаблицы.СуществуютДокументы Тогда

			ДокументОбъект = СтрокаТаблицы.ДокументСверки.ПолучитьОбъект();
			
			Если СтрокаТаблицы.ИзменятьДокумент = "УдалитьСоздатьНовый" Тогда
				
				Попытка
					ДокументОбъект.УстановитьПометкуУдаления(Истина);
					ДобавитьВПротоколВыполнения(ДокументОбъект.Ссылка, "ПометкаНаУдаление", ОтработанныеДокументы);
				Исключение
					ТекстОшибки    = НСтр("ru='Не удалось пометить на удаление %Документ%. %ОписаниеОшибки%'");
					ТекстОшибки    = СтрЗаменить(ТекстОшибки, "%Документ%",	ДокументОбъект);
					ТекстОшибки    = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
					
					ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ДокументОбъект.Ссылка);
				КонецПопытки;
				
			ИначеЕсли СтрокаТаблицы.ИзменятьДокумент = "Перезаполнять" Тогда
				
				МассивВсехДокументовСверки.Добавить(СтрокаТаблицы.ДокументСверки);
				ДанныеЗаполнения.УстанавливатьНовыйНомер = Ложь;
				ЗаполнитьПровестиДокументОбъект(ДокументОбъект, ДанныеЗаполнения);
				
				ДобавитьВПротоколВыполнения(ДокументОбъект.Ссылка, "Перезаполнение", ОтработанныеДокументы);
				
			ИначеЕсли СтрокаТаблицы.ИзменятьДокумент = "НеИзменять" Тогда
				
				МассивВсехДокументовСверки.Добавить(СтрокаТаблицы.ДокументСверки);
				ДобавитьВПротоколВыполнения(ДокументОбъект.Ссылка, "НеИзменять", ОтработанныеДокументы);
				
			КонецЕсли;

		КонецЕсли;

	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("МассивВсехДокументовСверки", МассивВсехДокументовСверки);
	Результат.Вставить("ОтработанныеДокументы", ОтработанныеДокументы);
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

// Заполняет структуру данными о получателях печатных форм.
// Параметры:
// 	ДокументОбъект - ДокументОбъект.СверкаВзаиморасчетов2_5_11 - Объект сверки для заполнения.
// 	ДанныеЗаполнения - Структура - Структура ключевых реквизитов сверки:
//     * ДанныеДокумента - Структура - Данные шапки сверки:
//       * Организация - СправочникСсылка.Организации - Организация шапки.
//       * ТипРасчетов - ПеречислениеСсылка.ТипыРасчетовСПартнерами - Тип расчетов.
//       * Партнер - СправочникСсылка.Партнеры - Партнер шапки.
//       * Контрагент - СправочникСсылка.Контрагенты - Контрагент шапки.
//       * Договор - СправочникСсылка.ДоговорыКонтрагентов - Договор шапки.
//     * ПериодСверки - Структура - Структура периода сверки:
//       * ДатаНачала - Дата - Начало периода сверки.
//       * ДатаОкончания - Дата - Конец периода сверки.
//
Процедура ЗаполнитьПровестиДокументОбъект(ДокументОбъект, ДанныеЗаполнения)
	
	ДанныеДокумента = Новый Структура;
	ДанныеДокумента.Вставить("Организация",              ДанныеЗаполнения.ДанныеДокумента.Организация);
	ДанныеДокумента.Вставить("ТипРасчетов",              ДанныеЗаполнения.ДанныеДокумента.ТипРасчетов);
	ДанныеДокумента.Вставить("Партнер",                  ДанныеЗаполнения.ДанныеДокумента.Партнер);
	ДанныеДокумента.Вставить("Контрагент",               ДанныеЗаполнения.ДанныеДокумента.Контрагент);
	ДанныеДокумента.Вставить("Договор",                  ДанныеЗаполнения.ДанныеДокумента.Договор);
	ДанныеДокумента.Вставить("Валюта",                   ДанныеЗаполнения.ДанныеДокумента.ВалютаСверки);
	ДанныеДокумента.Вставить("НачалоПериода",            ДанныеЗаполнения.ПериодСверки.ДатаНачала);
	ДанныеДокумента.Вставить("КонецПериода",             ДанныеЗаполнения.ПериодСверки.ДатаОкончания);
	ДанныеДокумента.Вставить("Статус",                   Перечисления.СтатусыСверокВзаиморасчетов.Создана);
	ДанныеДокумента.Вставить("НастройкиОтбора",          ДанныеЗаполнения.НастройкиОтбора);
	ДанныеДокумента.Вставить("ДоговорыБезОборотов",      ДанныеЗаполнения.ДоговорыБезОборотов);
	ДанныеДокумента.Вставить("РазбиватьПоТипамРасчетов", ДанныеЗаполнения.РазбиватьПоТипамРасчетов);
	ДанныеДокумента.Вставить("РазбиватьПоПартнерам",     ДанныеЗаполнения.РазбиватьПоПартнерам);
	ДанныеДокумента.Вставить("РазбиватьПоДоговорам",     ДанныеЗаполнения.РазбиватьПоДоговорам);
	ДанныеДокумента.Вставить("СверкаВВалюте",            ДанныеЗаполнения.СверкаВВалюте);
	ДанныеДокумента.Вставить("РежимСверкиИтоговВзаиморасчетов", ДанныеЗаполнения.РежимСверкиИтоговВзаиморасчетов);
	
	УстановитьПривилегированныйРежим(Истина);
	ДокументОбъект.Заполнить(ДанныеДокумента);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ДанныеЗаполнения.УстанавливатьНовыйНомер Тогда
		ДокументОбъект.УстановитьНовыйНомер();
	КонецЕсли;
	
	Если ДокументОбъект.ПроверитьЗаполнение() Тогда
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			ТекстОшибки = НСтр("ru='Не удалось провести %Документ%. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%",       ДокументОбъект);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ДокументОбъект.Ссылка);
		КонецПопытки;
	Иначе
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		ТекстОшибки = НСтр("ru='Не удалось провести %Документ%.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДокументОбъект);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ДокументОбъект.Ссылка);
	КонецЕсли;

КонецПроцедуры

// Описание
// 
// Параметры:
// 	ДокументСверки - ДокументСсылка.СверкаВзаиморасчетов2_5_11 - Описание
// 	Действие - Строка - Описание
// 	ОтработанныеДокументы - Массив из см. СтрокаПротокола -
//
Процедура ДобавитьВПротоколВыполнения(ДокументСверки, Действие, ОтработанныеДокументы)
	
	СтрокаПротокола = СтрокаПротокола();
	СтрокаПротокола.ДокументСверки = ДокументСверки;
	СтрокаПротокола.Действие = Действие;
	
	ОтработанныеДокументы.Добавить(СтрокаПротокола);
	
КонецПроцедуры

// Строка протокола.
// 
// Возвращаемое значение:
//  Структура - Описание:
// * Действие - Строка -
// * ДокументСверки - ДокументСсылка.СверкаВзаиморасчетов2_5_11 -
Функция СтрокаПротокола() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("ДокументСверки", ПустаяСсылка());
	Результат.Вставить("Действие","");
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПометкаНаУдаление

Процедура УстановитьПометкуНаУдаление(Параметры, АдресХранилища = "") Экспорт
	
	Для Каждого Ссылка Из Параметры.ВыделенныеДокументы Цикл
		Объект = Ссылка.ПолучитьОбъект();
		Объект.УстановитьПометкуУдаления(НЕ Объект.ПометкаУдаления);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ФормаДокумента

Процедура РаспределениеРасчетовПоДокументам(Параметры, АдресХранилища = "") Экспорт
	
	РаспределениеВзаиморасчетовВызовСервера.РаспределитьРасчетыФоновымЗаданием(
		Параметры.КонецПериода,
		Параметры.АналитикиРасчета);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

#Область ЗаполнитьДанныеСверкиПоРасчетам

// Параметры:
// 	ДанныеДокумента - ДокументОбъект.СверкаВзаиморасчетов2_5_11 - 
Процедура ЗаполнитьДанныеПоРасчетам(ДанныеДокумента) Экспорт
	
	СведенияОбОрганизации = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеДокумента.Организация, ДанныеДокумента.Дата);
	ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование");
	СведенияОКонтрагенте  = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеДокумента.Контрагент, ДанныеДокумента.Дата);
	КонтрагентНаименование = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОКонтрагенте, "ПолноеНаименование");
	
	СтруктураПредставленийУчастников = Новый Структура;
	СтруктураПредставленийУчастников.Вставить("ПредставлениеОрганизации", ПредставлениеОрганизации);
	СтруктураПредставленийУчастников.Вставить("КонтрагентНаименование", КонтрагентНаименование);
	
	КомпоновщикОтбор = Новый КомпоновщикНастроекКомпоновкиДанных;
	ИнициализироватьКомпоновщикНастроек(КомпоновщикОтбор);
	Если ДанныеДокумента.НастройкиОтбора <> Неопределено Тогда
		НастройкиОтбора = ДанныеДокумента.НастройкиОтбора.Получить();
		Если НастройкиОтбора <> Неопределено Тогда
			КомпоновкаДанныхКлиентСервер.СкопироватьЭлементы(КомпоновщикОтбор.Настройки.Отбор, НастройкиОтбора.Отбор);
		КонецЕсли;
	КонецЕсли;
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "НачалоПериода", ДанныеДокумента.НачалоПериода);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "КонецПериода", КонецДня(ДанныеДокумента.КонецПериода));
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "Период", ДанныеДокумента.НачалоПериода);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "ПолучатьДокументыСверки", Ложь);
	
	РежимСверкиПоДоговорам = ДанныеДокумента.РежимСверкиИтоговВзаиморасчетов = Перечисления.РежимСверкиИтоговВзаиморасчетов.ПоДоговорам;
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "РежимСверкиПоДоговорам", РежимСверкиПоДоговорам);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "РазбиватьПоТипамРасчетов", ДанныеДокумента.РазбиватьПоТипамРасчетов);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "РазбиватьПоПартнерам", ДанныеДокумента.РазбиватьПоПартнерам);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "РазбиватьПоДоговорам", ДанныеДокумента.РазбиватьПоДоговорам);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "СверкаВВалюте", ДанныеДокумента.СверкаВВалюте);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(КомпоновщикОтбор.Настройки, "Организация", ДанныеДокумента.Организация);
	Если ДанныеДокумента.СверкаВВалюте Тогда
		КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(КомпоновщикОтбор.Настройки, "Валюта", ДанныеДокумента.Валюта);
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов")
		И ТипЗнч(ДанныеДокумента.Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
		Партнер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеДокумента.Контрагент, "Партнер");
		КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(КомпоновщикОтбор.Настройки, "Партнер", Партнер);
	Иначе
		КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(КомпоновщикОтбор.Настройки, "Контрагент", ДанныеДокумента.Контрагент);
	КонецЕсли;
	
	Если ДанныеДокумента.ДополнительныеСвойства.Свойство("ДляОтладки") Тогда
		ДляОтладки = ДанныеДокумента.ДополнительныеСвойства.ДляОтладки;
		КомпоновщикОтбор.Настройки.ДополнительныеСвойства.Вставить("ДляОтладки", ДляОтладки);
	КонецЕсли;
	
	ТаблицаРезультатаСКД = ПолучитьВзаиморасчеты(КомпоновщикОтбор);
	
	СвернутьРасчетыПоГруппировкам(ДанныеДокумента, ТаблицаРезультатаСКД);
	
	Если КомпоновщикОтбор.Настройки.ДополнительныеСвойства.Свойство("ДляОтладки") Тогда
		ДляОтладки = КомпоновщикОтбор.Настройки.ДополнительныеСвойства.ДляОтладки;
		ДанныеДокумента.ДополнительныеСвойства.Вставить("ДляОтладки", ДляОтладки);
		КомпоновщикОтбор.Настройки.ДополнительныеСвойства.Удалить("ДляОтладки");
	КонецЕсли;
	
КонецПроцедуры

Процедура СвернутьРасчетыПоГруппировкам(ДанныеДокумента, ТаблицаВзаиморасчетов)
	
	ВалютаРегл = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ДанныеДокумента.Организация);
	СверкаВВалютеРегл = ВалютаРегл = ДанныеДокумента.Валюта;
	СверкаПоДоговорам = ДанныеДокумента.РежимСверкиИтоговВзаиморасчетов = Перечисления.РежимСверкиИтоговВзаиморасчетов.ПоДоговорам;
	Группировки = "ТипРасчетов, Партнер, Договор, НомерДоговора, ДатаДоговора, НаименованиеДоговора";
	ПолеОбъектаРасчетов = ?(СверкаПоДоговорам,"",", ОбъектРасчетов");
	ПоляГруппировки = Группировки + ПолеОбъектаРасчетов;
	ГруппировкаДетальныхЗаписей = "ДатаОперации, ТипРасчетов" + ПолеОбъектаРасчетов + ", Партнер, Договор, РасчетныйДокумент, ВалютаДокумента, СуммаДокумента, НомерДокумента, ДатаДокумента";
	ПоляСуммирования = "НачальноеСальдоДт, НачальноеСальдоКт, ОборотДт, ОборотКт, КонечноеСальдоДт, КонечноеСальдоКт";
	ПоляСуммированияДетальныхЗаписей = "СуммаДолг, СуммаАванс, СуммаДебет, СуммаКредит";
	//++ Локализация
	СчетаФактуры = ПолучитьСчетаФактуры(ТаблицаВзаиморасчетов);
	//-- Локализация
	
	Ресурсы = Новый Соответствие;
	Ресурсы.Вставить("НачальноеСальдоДт", "НачальноеСальдоДт");
	Ресурсы.Вставить("НачальноеСальдоКт", "НачальноеСальдоКт");
	Ресурсы.Вставить("ОборотДт", "ОборотДт");
	Ресурсы.Вставить("ОборотКт", "ОборотКт");
	Ресурсы.Вставить("КонечноеСальдоДт", "КонечноеСальдоДт");
	Ресурсы.Вставить("КонечноеСальдоКт", "КонечноеСальдоКт");
	Ресурсы.Вставить("СуммаДолг", "СуммаДолг");
	Ресурсы.Вставить("СуммаАванс", "СуммаАванс");
	Ресурсы.Вставить("СуммаДебет", "СуммаДебет");
	Ресурсы.Вставить("СуммаКредит", "СуммаКредит");
	Ресурсы.Вставить("СуммаДокумента", "СуммаДокумента");
	
	РесурсыРегл = Новый Соответствие;
	РесурсыРегл.Вставить("НачальноеСальдоДт", "НачальноеСальдоДтРегл");
	РесурсыРегл.Вставить("НачальноеСальдоКт", "НачальноеСальдоКтРегл");
	РесурсыРегл.Вставить("ОборотДт", "ОборотДтРегл");
	РесурсыРегл.Вставить("ОборотКт", "ОборотКтРегл");
	РесурсыРегл.Вставить("КонечноеСальдоДт", "КонечноеСальдоДтРегл");
	РесурсыРегл.Вставить("КонечноеСальдоКт", "КонечноеСальдоКтРегл");
	РесурсыРегл.Вставить("СуммаДолг", "СуммаДолгРегл");
	РесурсыРегл.Вставить("СуммаАванс", "СуммаАвансРегл");
	РесурсыРегл.Вставить("СуммаДебет", "СуммаДебетРегл");
	РесурсыРегл.Вставить("СуммаКредит", "СуммаКредитРегл");
	РесурсыРегл.Вставить("СуммаДокумента", "СуммаДокумента");
	
	ДанныеДокумента.ИтоговыеЗаписи.Очистить();
	ДанныеДокумента.ДетальныеЗаписи.Очистить();
	
	Для Каждого СтрокаРасчетов Из ТаблицаВзаиморасчетов Цикл
		
		ТЧГруппировки = ДанныеДокумента.ИтоговыеЗаписи; // ТабличнаяЧасть
		ТЧДетальныеЗаписи = ДанныеДокумента.ДетальныеЗаписи; // ТабличнаяЧасть
		
		ДанныеСумм = ЗначенияРесурсов(СтрокаРасчетов, ?(СверкаВВалютеРегл,РесурсыРегл,Ресурсы));
		
		НоваяСтрока = ТЧГруппировки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасчетов, ПоляГруппировки + ", " + ПоляСуммирования);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСумм);
		Если НЕ ЗначениеЗаполнено(НоваяСтрока.Договор) Тогда
			НоваяСтрока.Договор = Неопределено;
		КонецЕсли;
		
		Если СтрокаРасчетов.НачальноеСальдо = 0 И ДанныеСумм.ЕстьЗаполненные Тогда
			Если ТипЗнч(СтрокаРасчетов.РасчетныйДокумент) = Тип("ДокументСсылка.РасчетКурсовыхРазниц") Тогда
				Если НЕ СверкаВВалютеРегл Тогда
					Продолжить;
				КонецЕсли;
				СтрокаРасчетов.ВалютаДокумента = ВалютаРегл;
			КонецЕсли;
			НоваяСтрока = ТЧДетальныеЗаписи.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасчетов, ГруппировкаДетальныхЗаписей + ", " + ПоляСуммированияДетальныхЗаписей);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСумм);
			Если НЕ ЗначениеЗаполнено(НоваяСтрока.Договор) Тогда
				НоваяСтрока.Договор = Неопределено;
			КонецЕсли;
			Если НоваяСтрока.СуммаДокумента < 0 Тогда
				НоваяСтрока.СуммаДокумента = -НоваяСтрока.СуммаДокумента;
			КонецЕсли;
			ОписаниеДокумента = ОписаниеРасчетногоДокумента(СтрокаРасчетов.РасчетныйДокумент, СтрокаРасчетов.НомерДокумента, СтрокаРасчетов.ДатаДокумента, СтрокаРасчетов.НаименованиеПервичногоДокумента);
			НоваяСтрока.НомерДокумента = ОписаниеДокумента.НомерДокумента;
			НоваяСтрока.ДатаДокумента = ОписаниеДокумента.ДатаДокумента;
			НоваяСтрока.НаименованиеДокумента = ОписаниеДокумента.НаименованиеДокумента;
			//++ Локализация
			Отбор = Новый Структура("РасчетныйДокумент", СтрокаРасчетов.РасчетныйДокумент);
			СФДокумента = СчетаФактуры.НайтиСтроки(Отбор);
			Если СФДокумента.Количество() > 0 Тогда
				Для Каждого СФ Из СФДокумента Цикл
					Если СФ.ЭтоВходящий Тогда
						НомерНаПечать = УбратьЛидирующиеНули(СФ.Номер);
					Иначе
						НомерНаПечать = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СФ.Номер);
					КонецЕсли;
				КонецЦикла;
				Если ЗначениеЗаполнено(СтрокаРасчетов.РасчетныйДокумент) Тогда
					Если НомерСФВУПД(СтрокаРасчетов.РасчетныйДокумент) Тогда
						НоваяСтрока.НаименованиеДокумента = НСтр("ru = 'УПД'");
						НоваяСтрока.НомерДокумента = СокрЛП(НомерНаПечать);
						НоваяСтрока.ДатаДокумента = СФ.Дата;
					КонецЕсли;
					НоваяСтрока.СвязанныеДокументы = СтрШаблон(НСтр("ru = '%1 №%2 от %3'"),
									ОписаниеДокумента.НаименованиеДокумента,
									ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СтрокаРасчетов.НомерДокументаИБ),
									Формат(СтрокаРасчетов.ДатаДокументаИБ, "ДЛФ=D"));
				КонецЕсли;
			КонецЕсли;
			//-- Локализация
		КонецЕсли;
		
	КонецЦикла;
	
	#Область ЗаполнениеТЧРасчетов
	ГрупповыеЗаписи = ДанныеДокумента.ИтоговыеЗаписи.Выгрузить();
	Если ГрупповыеЗаписи.Количество() > 0 Тогда
		ГрупповыеЗаписи.Свернуть(ПоляГруппировки, ПоляСуммирования);
	КонецЕсли;
	ДанныеДокумента.ИтоговыеЗаписи.Очистить();
	
	ДетальныеЗаписи = ДанныеДокумента.ДетальныеЗаписи.Выгрузить();
	Если ДетальныеЗаписи.Количество() > 0 Тогда
		ДетальныеЗаписи.Свернуть(ГруппировкаДетальныхЗаписей + ",СвязанныеДокументы,НаименованиеДокумента", ПоляСуммированияДетальныхЗаписей);
		ВзаимозачетАванса = Новый Массив;
		Для Каждого Запись Из ДетальныеЗаписи Цикл
			Если ТипЗнч(Запись.РасчетныйДокумент) = Тип("ДокументСсылка.ВзаимозачетЗадолженности")
				И Запись.СуммаДебет - Запись.СуммаКредит = 0 Тогда
				ВзаимозачетАванса.Добавить(Запись);
			КонецЕсли;
		КонецЦикла;
		Для Каждого Запись Из ВзаимозачетАванса Цикл
			ДетальныеЗаписи.Удалить(Запись);
		КонецЦикла;
	КонецЕсли;
	ДанныеДокумента.ДетальныеЗаписи.Загрузить(ДетальныеЗаписи);
	
	Для Каждого Группировка Из ГрупповыеЗаписи Цикл
		
		ВзаиморасчетыКлиентСервер.РассчитатьКонечноеСальдоДтКт(Группировка, ДетальныеЗаписи, СверкаПоДоговорам);
		
		Если Группировка.НачальноеСальдоДт = 0 И Группировка.НачальноеСальдоКт = 0
			И Группировка.ОборотДт = 0 И Группировка.ОборотКт = 0
			И Группировка.КонечноеСальдоДт = 0 И Группировка.КонечноеСальдоКт Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ДанныеДокумента.ИтоговыеЗаписи.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Группировка);
		
	КонецЦикла;
	
	ДанныеДокумента.ИтоговыеЗаписи.Сортировать("ТипРасчетов, Партнер, Договор, ОбъектРасчетов, Валюта");
	#КонецОбласти
	
КонецПроцедуры
//++ Локализация
Функция ПолучитьСчетаФактуры(ТаблицаВзаиморасчетов)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаРасчетов.РасчетныйДокумент КАК РасчетныйДокумент
	|ПОМЕСТИТЬ Обороты
	|ИЗ
	|	&Взаиморасчеты КАК ТаблицаРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
	|	Обороты.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ОснованиеСФ.Ссылка КАК СчетФактура,
	|	ОснованиеСФ.Ссылка.ПредставлениеНомера КАК Номер,
	|	ОснованиеСФ.Ссылка.Дата КАК Дата,
	|	ЛОЖЬ КАК ЭтоВходящий
	|ИЗ
	|	Обороты КАК Обороты
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК ОснованиеСФ
	|		ПО Обороты.РасчетныйДокумент = ОснованиеСФ.ДокументОснование
	|ГДЕ
	|	НЕ ОснованиеСФ.Ссылка ЕСТЬ NULL
	|	И ОснованиеСФ.Ссылка.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Обороты.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ОснованиеСФ.Ссылка КАК СчетФактура,
	|	ОснованиеСФ.Ссылка.Номер КАК Номер,
	|	ОснованиеСФ.Ссылка.ДатаСоставления КАК Дата,
	|	ИСТИНА КАК ЭтоВходящий
	|ИЗ
	|	Обороты КАК Обороты
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК ОснованиеСФ
	|		ПО Обороты.РасчетныйДокумент = ОснованиеСФ.ДокументОснование
	|ГДЕ
	|	НЕ ОснованиеСФ.Ссылка ЕСТЬ NULL
	|	И ОснованиеСФ.Ссылка.Проведен";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Взаиморасчеты", ТаблицаВзаиморасчетов);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

Функция НомерСФВУПД(РасчетныйДокумент)
	
	Возврат
		Метаданные.ОпределяемыеТипы.ОснованиеСчетФактураВыданный.Тип.СодержитТип(ТипЗнч(РасчетныйДокумент))
		ИЛИ Метаданные.ОпределяемыеТипы.ОснованиеСчетФактураПолученный.Тип.СодержитТип(ТипЗнч(РасчетныйДокумент));
	
КонецФункции
//-- Локализация

Функция ЗначенияРесурсов(ДанныеЗаполнения, ИменаРесурсов)
	
	ДанныеРесурсов = Новый Структура;
	
	ЕстьЗаполненные = Ложь;
	Для Каждого Ресурс Из ИменаРесурсов Цикл
		
		ДанныеРесурсов.Вставить(Ресурс.Ключ,ДанныеЗаполнения[Ресурс.Значение]);
		Если ДанныеРесурсов[Ресурс.Ключ] <> 0 И НЕ ЕстьЗаполненные Тогда
			ЕстьЗаполненные = Истина;
		КонецЕсли;
		
	КонецЦикла; 
	
	ДанныеРесурсов.Вставить("ЕстьЗаполненные", ЕстьЗаполненные);
	
	Возврат ДанныеРесурсов;
	
КонецФункции

#КонецОбласти

// Получить взаиморасчеты.
// 
// Параметры:
//  КомпоновщикОтбор - КомпоновщикНастроекКомпоновкиДанных - Компоновщик отбор
// 
// Возвращаемое значение:
//  ТаблицаЗначений, ДеревоЗначений - Получить взаиморасчеты
Функция ПолучитьВзаиморасчеты(КомпоновщикОтбор) Экспорт
	
	СхемаКомпоновкиДанных = ПолучитьМакет("ОтборРасчетов");
	
	ДобавитьДоступныеТаблицы(СхемаКомпоновкиДанных.НаборыДанных.ОстаткиИОбороты.Запрос);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	СегментыСервер.ВключитьОтборПоСегментуПартнеровВСКД(КомпоновщикОтбор);
	
	НастройкиКомпоновкиДанных = КомпоновщикОтбор.Настройки;
	ВключатьДоговорыБезОборотов = КомпоновкаДанныхКлиентСервер.ИспользуетсяОтбор(НастройкиКомпоновкиДанных.Отбор.Элементы, "ДоговорыБезОборотов", Истина);
	
	НачалоПериода = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикОтбор.Настройки, "НачалоПериода");
	Если НЕ ЗначениеЗаполнено(НачалоПериода.Значение) Тогда
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "НачалоПериода", Дата('19800101'));
	КонецЕсли;
	
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "ДоговорыБезОборотов", ВключатьДоговорыБезОборотов);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "НоваяАрхитектураВзаиморасчетов", ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов"));
	
	ТипыСуммАренды = Новый Массив;
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "ТипыСуммАренды", ТипыСуммАренды,,Истина);
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновкиДанных,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки,,,Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ТаблицаРезультатаСКД = ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
		ТаблицаРезультатаСКД.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
		ТаблицаРезультатаСКД.ЗаполнитьЗначения(Организация, "Организация");
	КонецЕсли;
	
	ЕстьВводОстатков(ТаблицаРезультатаСКД);
	
	СхемаОтбора = Новый Структура;
	СхемаОтбора.Вставить("СКД", СхемаКомпоновкиДанных);
	СхемаОтбора.Вставить("Настройки", НастройкиКомпоновкиДанных);
	СхемаОтбора.Вставить("Результат", ТаблицаРезультатаСКД);
	
	Если КомпоновщикОтбор.Настройки.ДополнительныеСвойства.Свойство("ДляОтладки") Тогда
		КомпоновщикОтбор.Настройки.ДополнительныеСвойства.ДляОтладки.Вставить("СхемаОтбора", СхемаОтбора);
	КонецЕсли;
	
	Возврат ТаблицаРезультатаСКД;
	
КонецФункции

// Проверяет наличие документов ввода остатков в выбоке СКД и .
// 
// Параметры:
//  ТаблицаРезультатаСКД - ТаблицаЗначений - таблица результата СКД отбора данных для сверки
// 
// Возвращаемое значение:
//  Булево -  Есть ввод остатков
//
Функция ЕстьВводОстатков(ТаблицаРезультатаСКД) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.РасчетныйДокумент КАК Ссылка,
	|	Т.ДатаДокумента КАК Период
	|ПОМЕСТИТЬ втДокументаВводаОстатков
	|ИЗ
	|	&Таблица КАК Т
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Т.РасчетныйДокумент) = ТИП(Документ.ВводОстатков)
	|	ИЛИ ТИПЗНАЧЕНИЯ(Т.РасчетныйДокумент) = ТИП(Документ.ВводОстатковВзаиморасчетов)
	|	ИЛИ ТИПЗНАЧЕНИЯ(Т.РасчетныйДокумент) = ТИП(Документ.ВводОстатковПоФинансовымИнструментам)
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВводОстатков.Период КАК Период,
	|	ВводОстатков.Ссылка КАК Ссылка
	|ИЗ
	|	втДокументаВводаОстатков КАК ВводОстатков
	|";
	
	Запрос.УстановитьПараметр("Таблица", ТаблицаРезультатаСКД);
	
	ВводОстатков = Запрос.Выполнить().Выгрузить();
	Если ВводОстатков.Количество() > 0 Тогда
		НовоеНачалоПериода = КонецДня(ВводОстатков[0].Период)+1;
		Шаблон = НСтр("ru = 'В заданном периоде сверки обнаружен документ ввода остатков.
		|Сверка за такой период некорректна. Начало периода сверки должно быть после ввода остатков. 
		|Документы были удалены из табличной части документа. Установите начало периода сверки на дату %1.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, Формат(НовоеНачалоПериода, "ДЛФ=D"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Для Каждого Документ Из ВводОстатков Цикл
		Отбор = Новый Структура("РасчетныйДокумент",Документ.Ссылка);
		СтрокиКУдалению = ТаблицаРезультатаСКД.НайтиСтроки(Отбор);
		Для Каждого СтрокаУдалить Из СтрокиКУдалению Цикл
			ТаблицаРезультатаСКД.Удалить(СтрокаУдалить);
		КонецЦикла;
	КонецЦикла;
	
	Возврат ВводОстатков.Количество() > 0;
	
КонецФункции

// Данные расчетного документа.
// 
// Параметры:
//  РасчетныеДокументы - Массив из ДокументСсылка
// 
// Возвращаемое значение:
//  Структура - Данные расчетного документа:
// * Ссылка - ДокументСсылка
// * Номер - Строка
// * Дата - Дата
// * НаименованиеПервичногоДокумента - Строка 
// * Валюта - СправочникСсылка.Валюты
// * Сумма - Число
Функция ДанныеРасчетногоДокумента(РасчетныеДокументы) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РеестрДокументов.Ссылка КАК Ссылка,
	|	РеестрДокументов.ДатаПервичногоДокумента КАК Дата,
	|	РеестрДокументов.НомерПервичногоДокумента КАК Номер,
	|	РеестрДокументов.НаименованиеПервичногоДокумента КАК НаименованиеПервичногоДокумента,
	|	РеестрДокументов.Валюта КАК Валюта,
	|	РеестрДокументов.Сумма КАК Сумма
	|ИЗ
	|	РегистрСведений.РеестрДокументов КАК РеестрДокументов
	|ГДЕ
	|	РеестрДокументов.Ссылка В (&МассивДокументов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СуммыДокументов.Ссылка КАК Ссылка,
	|	СуммыДокументов.Дата КАК Дата,
	|	СуммыДокументов.Номер КАК Номер,
	|	"""" КАК НаименованиеПервичногоДокумента,
	|	СуммыДокументов.Валюта КАК Валюта,
	|	СуммыДокументов.СуммаДокумента КАК Сумма
	|ИЗ
	|	Документ.ВыкупВозвратнойТарыКлиентом КАК СуммыДокументов
	|ГДЕ
	|	СуммыДокументов.Ссылка В (&МассивДокументов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СуммыДокументов.Ссылка КАК Ссылка,
	|	СуммыДокументов.ДатаВходящегоДокумента КАК Дата,
	|	СуммыДокументов.НомерВходящегоДокумента КАК Номер,
	|	СуммыДокументов.НаименованиеВходящегоДокумента КАК НаименованиеПервичногоДокумента,
	|	СуммыДокументов.Валюта КАК Валюта,
	|	СуммыДокументов.СуммаДокумента КАК Сумма
	|ИЗ
	|	Документ.ВыкупВозвратнойТарыУПоставщика КАК СуммыДокументов
	|ГДЕ
	|	СуммыДокументов.Ссылка В (&МассивДокументов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СуммыДокументов.Ссылка КАК Ссылка,
	|	СуммыДокументов.ДатаВходящегоДокумента КАК Дата,
	|	СуммыДокументов.НомерВходящегоДокумента КАК Номер,
	|	СуммыДокументов.НаименованиеВходящегоДокумента КАК НаименованиеПервичногоДокумента,
	|	СуммыДокументов.Валюта КАК Валюта,
	|	СуммыДокументов.СуммаДокумента КАК Сумма
	|ИЗ
	|	Документ.ОтчетКомиссионера КАК СуммыДокументов
	|ГДЕ
	|	СуммыДокументов.Ссылка В (&МассивДокументов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СуммыДокументов.Ссылка КАК Ссылка,
	|	СуммыДокументов.ДатаВходящегоДокумента КАК Дата,
	|	СуммыДокументов.НомерВходящегоДокумента КАК Номер,
	|	СуммыДокументов.НаименованиеВходящегоДокумента КАК НаименованиеПервичногоДокумента,
	|	СуммыДокументов.Валюта КАК Валюта,
	|	СуммыДокументов.СуммаДокумента КАК Сумма
	|ИЗ
	|	Документ.ОтчетКомиссионераОСписании КАК СуммыДокументов
	|ГДЕ
	|	СуммыДокументов.Ссылка В (&МассивДокументов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СуммыДокументов.Ссылка КАК Ссылка,
	|	СуммыДокументов.Дата КАК Дата,
	|	СуммыДокументов.Номер КАК Номер,
	|	"""" КАК НаименованиеПервичногоДокумента,
	|	СуммыДокументов.Валюта КАК Валюта,
	|	СуммыДокументов.СуммаДокумента КАК Сумма
	|ИЗ
	|	Документ.ОтчетКомитенту КАК СуммыДокументов
	|ГДЕ
	|	СуммыДокументов.Ссылка В (&МассивДокументов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СуммыДокументов.Ссылка КАК Ссылка,
	|	СуммыДокументов.Дата КАК Дата,
	|	СуммыДокументов.Номер КАК Номер,
	|	"""" КАК НаименованиеПервичногоДокумента,
	|	СуммыДокументов.Валюта КАК Валюта,
	|	СуммыДокументов.СуммаДокумента КАК Сумма
	|ИЗ
	|	Документ.ОтчетКомитентуОСписании КАК СуммыДокументов
	|ГДЕ
	|	СуммыДокументов.Ссылка В (&МассивДокументов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СуммыДокументов.Ссылка КАК Ссылка,
	|	СуммыДокументов.ДатаВходящегоДокумента КАК Дата,
	|	СуммыДокументов.НомерВходящегоДокумента КАК Номер,
	|	СуммыДокументов.НаименованиеВходящегоДокумента КАК НаименованиеПервичногоДокумента,
	|	СуммыДокументов.Валюта КАК Валюта,
	|	СуммыДокументов.СуммаДокумента КАК Сумма
	|ИЗ
	|	Документ.ПервичныйДокумент КАК СуммыДокументов
	|ГДЕ
	|	СуммыДокументов.Ссылка В (&МассивДокументов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СуммыДокументов.Ссылка КАК Ссылка,
	|	СуммыДокументов.Ссылка.Дата КАК Дата,
	|	СуммыДокументов.Ссылка.Номер КАК Номер,
	|	"""" КАК НаименованиеПервичногоДокумента,
	|	СуммыДокументов.ВалютаВзаиморасчетов КАК Валюта,
	|	СУММА(СуммыДокументов.Сумма) КАК Сумма
	|ИЗ
	|	Документ.КорректировкаЗадолженности.Задолженность КАК СуммыДокументов
	|ГДЕ
	|	СуммыДокументов.Ссылка В (&МассивДокументов)
	|		
	|СГРУППИРОВАТЬ ПО
	|	СуммыДокументов.Ссылка,
	|	СуммыДокументов.ВалютаВзаиморасчетов";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("МассивДокументов", РасчетныеДокументы);
	
	Выборка = Запрос.Выполнить().Выбрать();
	ДанныеДокумента = Новый Структура("Ссылка,Номер,Дата, НаименованиеПервичногоДокумента, Валюта,Сумма");
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ДанныеДокумента, Выборка);
	КонецЦикла;
	
	Возврат ДанныеДокумента;
	
КонецФункции

Процедура ИнициализироватьКомпоновщикНастроек(КомпоновщикОтбор, УникальныйИдентификаторФормы = Неопределено, НастройкиОтбора = Неопределено) Экспорт
	
	СхемаКомпоновкиДанных = ПолучитьМакет("ОтборРасчетов");
	
	ДобавитьДоступныеТаблицы(СхемаКомпоновкиДанных.НаборыДанных.ОстаткиИОбороты.Запрос);
	
	ИспользоватьПартнеровКакКонтрагентов = ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов");
	Если ИспользоватьПартнеровКакКонтрагентов Тогда
		Поле = СхемаКомпоновкиДанных.НаборыДанных.ОстаткиИОбороты.Поля.Найти("Контрагент"); // ПолеНабораДанныхСхемыКомпоновкиДанных 
		Если Поле <> Неопределено Тогда
			Поле.ОграничениеИспользования.Условие = Истина;
			Поле.ОграничениеИспользованияРеквизитов.Условие = Истина;
		КонецЕсли;
	КонецЕсли;
	
	ПартнерыИКонтрагенты.ЗаголовокПоляСКДВЗависимостиОтФОИспользоватьПартнеровКакКонтрагентов(
		СхемаКомпоновкиДанных.НаборыДанных.ОстаткиИОбороты, "Партнер", НСтр("ru = 'Контрагент'"));
	
	ДоступныеТипыРасчетов = Новый СписокЗначений;
	ДоступныеТипыРасчетов.Добавить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом);
	ДоступныеТипыРасчетов.Добавить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком);
	Если ПравоДоступа("Просмотр", Метаданные.Справочники.ДоговорыКредитовИДепозитов) Тогда
		ДоступныеТипыРасчетов.Добавить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСКредитором);
		ДоступныеТипыРасчетов.Добавить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСДебитором);
	КонецЕсли;

	ПолеТипРасчетов = СхемаКомпоновкиДанных.НаборыДанных.ОстаткиИОбороты.Поля.Найти("ТипРасчетов");
	ПолеТипРасчетов.УстановитьДоступныеЗначения(ДоступныеТипыРасчетов);
		
	Если УникальныйИдентификаторФормы = Неопределено Тогда
		АдресВХранилище = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор());
	КонецЕсли;
	АдресВХранилище = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификаторФормы);
	
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресВХранилище);
	
	КомпоновщикОтбор.Инициализировать(ИсточникНастроек);
	
	КомпоновщикОтбор.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	Если НастройкиОтбора <> Неопределено Тогда
		КомпоновкаДанныхКлиентСервер.СкопироватьЭлементы(КомпоновщикОтбор.Настройки.Отбор, НастройкиОтбора.Отбор);
	КонецЕсли;
	Если ИспользоватьПартнеровКакКонтрагентов Тогда
		КомпоновкаДанныхКлиентСервер.УдалитьОтбор(КомпоновщикОтбор.Настройки.Отбор, "Контрагент");
	КонецЕсли;
	КомпоновкаДанныхКлиентСервер.УдалитьОтбор(КомпоновщикОтбор.Настройки.Отбор, "ОрганизацияКонтрагент");
	КомпоновщикОтбор.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
	
	Если УникальныйИдентификаторФормы = Неопределено Тогда
		УдалитьИзВременногоХранилища(АдресВХранилище);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьОтборПартнераПоКонтрагенту(Отбор) Экспорт
	
	ЭлементОтбора = ФинансоваяОтчетностьСервер.НайтиЭлементОтбора(Отбор,"Контрагент");
	Если ЭлементОтбора <> Неопределено Тогда
		ПолеПартнер = Новый ПолеКомпоновкиДанных("Партнер");
		ЭлементОтбора.ЛевоеЗначение = ПолеПартнер;
		Если ЗначениеЗаполнено(ЭлементОтбора.ПравоеЗначение) Тогда
			Если ТипЗнч(ЭлементОтбора.ПравоеЗначение) = Тип("СправочникСсылка.Контрагенты") Тогда
				ЭлементОтбора.ПравоеЗначение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭлементОтбора.ПравоеЗначение, "Партнер");
			ИначеЕсли ТипЗнч(ЭлементОтбора.ПравоеЗначение) = Тип("Массив") Тогда
				Партнеры = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ЭлементОтбора.ПравоеЗначение, "Партнер");
				ЭлементОтбора.ПравоеЗначение.Очистить();
				Для Каждого Партнер Из Партнеры Цикл
					ЭлементОтбора.ПравоеЗначение.Добавить(Партнер);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьДоступныеТаблицы(ТекстЗапроса)
	
	ТекстыЗапросов = ТекстыЗапросовДоступныхТаблиц();
	Для Каждого Текст Из ТекстыЗапросов Цикл
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//"+Текст.Ключ, Текст.Значение);
	КонецЦикла;
	
КонецПроцедуры

// Запрещенные типы расчетов.
// 
// Возвращаемое значение:
//  Массив из ПеречислениеСписок.ТипыРасчетовСПартнерами- Запрещенные типы расчетов
Функция ЗапрещенныеТипыРасчетов() Экспорт
	
	Результат = Новый Массив;
	Если ПравоДоступа("Просмотр", Метаданные.РегистрыНакопления.РасчетыПоФинансовымИнструментам) Тогда
		Если НЕ ПравоДоступа("Просмотр", Метаданные.Справочники.ДоговорыКредитовИДепозитов) Тогда
			Результат.Добавить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСКредитором);
			Результат.Добавить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСДебитором);
		КонецЕсли;
		
	Иначе
		Результат.Добавить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСКредитором);
		Результат.Добавить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСДебитором);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ТекстыЗапросовДоступныхТаблиц()
	
	Результат = Новый Соответствие;
	Если ПравоДоступа("Просмотр", Метаданные.РегистрыНакопления.РасчетыПоФинансовымИнструментам) Тогда
		Результат.Вставить("#0", ТекстЗапросаРасчетыПоФинансовымИнструментамОстаткиНаКонецПериода()); //РегистрНакопленияРасчетыПоФинансовымИнструментам
		Результат.Вставить("#1", ТекстЗапросаРасчетыПоФинансовымИнструментам()); //РегистрНакопленияРасчетыПоФинансовымИнструментам//
	КонецЕсли;
	Если ПравоДоступа("Просмотр", Метаданные.Справочники.ДоговорыКредитовИДепозитов) Тогда
		Результат.Вставить("#2", ТекстЗапросаДоговоровКредитовДепозитовЗаймов()); //СправочникДоговорыКредитовИДепозитов
	КонецЕсли;
	Если ПравоДоступа("Просмотр", Метаданные.Документы.ПервичныйДокумент) Тогда
		Результат.Вставить("#4", ТекстЗапросаПервичныйДокумент()); //ДокументПервичныйДокумент
	КонецЕсли;
	Если ПравоДоступа("Просмотр", Метаданные.Документы.КорректировкаЗадолженности) Тогда
		Результат.Вставить("#5", ТекстЗапросаКорректировкаЗадолженности()); //ДокументКорректировкаЗадолженности
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ТекстЗапросаРасчетыПоФинансовымИнструментамОстаткиНаКонецПериода()
	
	Возврат 
	"///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&НачалоПериода КАК Период,
	|	ВложенныйЗапрос.ТипРасчетов КАК ТипРасчетов,
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.Партнер КАК Партнер,
	|	ВложенныйЗапрос.Контрагент КАК Контрагент,
	|	ВложенныйЗапрос.Договор КАК Договор,
	|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВложенныйЗапрос.НомерДоговора КАК НомерДоговора,
	|	ВложенныйЗапрос.ДатаДоговора КАК ДатаДоговора,
	|	ВложенныйЗапрос.НаименованиеДоговора КАК НаименованиеДоговора,
	|	ВложенныйЗапрос.Валюта КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК ТипСуммы,
	|	ИСТИНА КАК ДоговорыБезОборотов, //Договоры с оборотами включаем всегда
	|	
	|	СУММА(ВложенныйЗапрос.КонечноеСальдо) КАК КонечноеСальдо,
	|	СУММА(ВложенныйЗапрос.КонечноеСальдоРегл) КАК КонечноеСальдоРегл,
	|	СУММА(ВложенныйЗапрос.КонечноеСальдоДт) КАК КонечноеСальдоДт,
	|	СУММА(ВложенныйЗапрос.КонечноеСальдоКт) КАК КонечноеСальдоКт,
	|	СУММА(ВложенныйЗапрос.КонечноеСальдоДтРегл) КАК КонечноеСальдоДтРегл,
	|	СУММА(ВложенныйЗапрос.КонечноеСальдоКтРегл) КАК КонечноеСальдоКтРегл,
	|	
	|	ВложенныйЗапрос.ПорядокТипаПлатежа КАК ПорядокТипаПлатежа
	|ПОМЕСТИТЬ втОстаткиНаКонецПериодаФинИнструменты
	|ИЗ
	|	(ВЫБРАТЬ
	|	&КонецПериода КАК Период,
	|	ВЫБОР 
	|		КОГДА РасчетыОстатки.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКредитором)
	|		КОГДА РасчетыОстатки.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит)
	|				ИЛИ РасчетыОстатки.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСДебитором)
	|		КОГДА РасчетыОстатки.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Аренда)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСАрендодателем)
	|	КОНЕЦ КАК ТипРасчетов,
	|	РасчетыОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Аналитика.Организация КАК Организация,
	|	Аналитика.Партнер КАК Партнер,
	|	Аналитика.Контрагент КАК Контрагент,
	|	РасчетыОстатки.Договор КАК Договор,
	|	РасчетыОстатки.Договор.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	РасчетыОстатки.Договор.Номер КАК НомерДоговора,
	|	РасчетыОстатки.Договор.Дата КАК ДатаДоговора,
	|	РасчетыОстатки.Договор.НаименованиеДляПечати КАК НаименованиеДоговора,
	|	НЕОПРЕДЕЛЕНО КАК РасчетныйДокумент,
	|	РасчетыОстатки.Валюта КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК ТипСуммы,
	|	ИСТИНА КАК ДоговорыБезОборотов, //Договоры с оборотами включаем всегда
	|	
	|	РасчетыОстатки.СуммаОстаток КАК КонечноеСальдо,
	|	РасчетыОстатки.СуммаРеглОстаток КАК КонечноеСальдоРегл,
	|	
	|	ВЫБОР КОГДА РасчетыОстатки.СуммаОстаток > 0
	|		ТОГДА РасчетыОстатки.СуммаОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КонечноеСальдоДт,
	|	ВЫБОР КОГДА РасчетыОстатки.СуммаОстаток < 0
	|		ТОГДА -РасчетыОстатки.СуммаОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КонечноеСальдоКт,
	|	ВЫБОР КОГДА РасчетыОстатки.СуммаРеглОстаток > 0
	|		ТОГДА РасчетыОстатки.СуммаРеглОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КонечноеСальдоДтРегл,
	|	ВЫБОР КОГДА РасчетыОстатки.СуммаРеглОстаток < 0
	|		ТОГДА -РасчетыОстатки.СуммаРеглОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КонечноеСальдоКтРегл,
	|	
	|	ВЫБОР 
	|		КОГДА РасчетыОстатки.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|			ТОГДА 31
	|		КОГДА РасчетыОстатки.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит)
	|				ИЛИ РасчетыОстатки.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный)
	|			ТОГДА 32
	|		КОГДА РасчетыОстатки.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Аренда)
	|			ТОГДА 33
	|	КОНЕЦ КАК ПорядокТипаПлатежа
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам.Остатки(
	|		{&КонецПериода},
	|		{АналитикаУчетаПоПартнерам.Организация.* КАК Организация,
	|		АналитикаУчетаПоПартнерам.Партнер.* КАК Партнер,
	|		АналитикаУчетаПоПартнерам.Партнер.ОсновнойМенеджер.* КАК ОсновнойМенеджер,
	|		АналитикаУчетаПоПартнерам.Контрагент.* КАК Контрагент,
	|		АналитикаУчетаПоПартнерам.НаправлениеДеятельности.* КАК НаправлениеДеятельности}
	|	) КАК РасчетыОстатки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаПоПартнерам КАК Аналитика
	|	ПО РасчетыОстатки.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ТипРасчетов,
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.Партнер,
	|	ВложенныйЗапрос.Контрагент,
	|	ВложенныйЗапрос.Договор,
	|	ВложенныйЗапрос.НаправлениеДеятельности,
	|	ВложенныйЗапрос.НомерДоговора,
	|	ВложенныйЗапрос.ДатаДоговора,
	|	ВложенныйЗапрос.НаименованиеДоговора,
	|	ВложенныйЗапрос.Валюта,
	|	ВложенныйЗапрос.ПорядокТипаПлатежа
	|	
	|{ГДЕ
	|	ВложенныйЗапрос.ТипРасчетов КАК ТипРасчетов}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПоПартнерам,
	|	Договор,
	|	Валюта
	|;";
	
КонецФункции

Функция ТекстЗапросаРасчетыПоФинансовымИнструментам()
	
	Возврат "
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&НачалоПериода КАК Период,
	|	ВложенныйЗапрос.ТипРасчетов КАК ТипРасчетов,
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.Партнер КАК Партнер,
	|	ВложенныйЗапрос.Контрагент КАК Контрагент,
	|	ВложенныйЗапрос.Договор КАК Договор,
	|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВложенныйЗапрос.НомерДоговора КАК НомерДоговора,
	|	ВложенныйЗапрос.ДатаДоговора КАК ДатаДоговора,
	|	ВложенныйЗапрос.НаименованиеДоговора КАК НаименованиеДоговора,
	|	ВложенныйЗапрос.Договор КАК ОбъектРасчетов,
	|	ВложенныйЗапрос.Регистратор КАК РасчетныйДокумент,
	|	ВложенныйЗапрос.Валюта КАК Валюта,
	|	ВложенныйЗапрос.ТипСуммы КАК ТипСуммы,
	|	ИСТИНА КАК ДоговорыБезОборотов, //Договоры с оборотами включаем всегда
	|	
	|	СУММА(ВложенныйЗапрос.НачальноеСальдо) КАК НачальноеСальдо,
	|	СУММА(ВложенныйЗапрос.НачальноеСальдоРегл) КАК НачальноеСальдоРегл,
	|	СУММА(ВложенныйЗапрос.НачальноеСальдоДт) КАК НачальноеСальдоДт,
	|	СУММА(ВложенныйЗапрос.НачальноеСальдоДтРегл) КАК НачальноеСальдоДтРегл,
	|	СУММА(ВложенныйЗапрос.НачальноеСальдоКт) КАК НачальноеСальдоКт,
	|	СУММА(ВложенныйЗапрос.НачальноеСальдоКтРегл) КАК НачальноеСальдоКтРегл,
	|	
	|	0 КАК ОборотДт,
	|	0 КАК ОборотДтРегл,
	|	0 КАК ОборотКт,
	|	0 КАК ОборотКтРегл,
	|	
	|	0 КАК Долг,
	|	0 КАК ДолгРегл,
	|	0 КАК Предоплата,
	|	0 КАК ПредоплатаРегл,
	|	
	|	СУММА(ВложенныйЗапрос.КонечноеСальдо) КАК КонечноеСальдо,
	|	СУММА(ВложенныйЗапрос.КонечноеСальдоРегл) КАК КонечноеСальдоРегл,
	|	СУММА(ВложенныйЗапрос.КонечноеСальдоДт) КАК КонечноеСальдоДт,
	|	СУММА(ВложенныйЗапрос.КонечноеСальдоКт) КАК КонечноеСальдоКт,
	|	СУММА(ВложенныйЗапрос.КонечноеСальдоДтРегл) КАК КонечноеСальдоДтРегл,
	|	СУММА(ВложенныйЗапрос.КонечноеСальдоКтРегл) КАК КонечноеСальдоКтРегл,
	|	
	|	ВложенныйЗапрос.ПорядокТипаПлатежа КАК ПорядокТипаПлатежа
	|ИЗ
	|	(ВЫБРАТЬ
	|		&НачалоПериода КАК Период,
	|		ВЫБОР 
	|			КОГДА РасчетыОстатки.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКредитором)
	|			КОГДА РасчетыОстатки.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит)
	|					ИЛИ РасчетыОстатки.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСДебитором)
	|			КОГДА РасчетыОстатки.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Аренда)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСАрендодателем)
	|		КОНЕЦ КАК ТипРасчетов,
	|		РасчетыОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		Аналитика.Организация КАК Организация,
	|		Аналитика.Партнер КАК Партнер,
	|		Аналитика.Контрагент КАК Контрагент,
	|		РасчетыОстатки.Договор КАК Договор,
	|		РасчетыОстатки.Договор.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		РасчетыОстатки.Договор.Номер КАК НомерДоговора,
	|		РасчетыОстатки.Договор.Дата КАК ДатаДоговора,
	|		РасчетыОстатки.Договор.НаименованиеДляПечати КАК НаименованиеДоговора,
	|		НЕОПРЕДЕЛЕНО КАК Регистратор,
	|		РасчетыОстатки.Валюта КАК Валюта,
	|		НЕОПРЕДЕЛЕНО КАК ТипСуммы,
	|	
	|		РасчетыОстатки.СуммаОстаток КАК НачальноеСальдо,
	|		РасчетыОстатки.СуммаРеглОстаток КАК НачальноеСальдоРегл,
	|		ВЫБОР КОГДА РасчетыОстатки.СуммаОстаток > 0
	|			ТОГДА РасчетыОстатки.СуммаОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК НачальноеСальдоДт,
	|		ВЫБОР КОГДА РасчетыОстатки.СуммаОстаток < 0
	|			ТОГДА -РасчетыОстатки.СуммаОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК НачальноеСальдоКт,
	|		ВЫБОР КОГДА РасчетыОстатки.СуммаРеглОстаток > 0
	|			ТОГДА РасчетыОстатки.СуммаРеглОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК НачальноеСальдоДтРегл,
	|		ВЫБОР КОГДА РасчетыОстатки.СуммаРеглОстаток < 0
	|			ТОГДА -РасчетыОстатки.СуммаРеглОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК НачальноеСальдоКтРегл,
	|	
	|		ЕСТЬNULL(ОстаткиНаКонецПериода.КонечноеСальдо, 0) КАК КонечноеСальдо,
	|		ЕСТЬNULL(ОстаткиНаКонецПериода.КонечноеСальдоРегл, 0) КАК КонечноеСальдоРегл,
	|		ЕСТЬNULL(ОстаткиНаКонецПериода.КонечноеСальдоДт, 0) КАК КонечноеСальдоДт,
	|		ЕСТЬNULL(ОстаткиНаКонецПериода.КонечноеСальдоКт, 0) КАК КонечноеСальдоКт,
	|		ЕСТЬNULL(ОстаткиНаКонецПериода.КонечноеСальдоДтРегл, 0) КАК КонечноеСальдоДтРегл,
	|		ЕСТЬNULL(ОстаткиНаКонецПериода.КонечноеСальдоКтРегл, 0) КАК КонечноеСальдоКтРегл,
	|
	|		ВЫБОР 
	|			КОГДА РасчетыОстатки.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|				ТОГДА 31
	|			КОГДА РасчетыОстатки.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит)
	|					ИЛИ РасчетыОстатки.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный)
	|				ТОГДА 32
	|			КОГДА РасчетыОстатки.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Аренда)
	|				ТОГДА 33
	|		КОНЕЦ КАК ПорядокТипаПлатежа
	|	ИЗ
	|		РегистрНакопления.РасчетыПоФинансовымИнструментам.Остатки(
	|			{&НачалоПериода},
	|			ТипСуммы В (&ТипыСуммАренды) ИЛИ ТИПЗНАЧЕНИЯ(Договор) = ТИП(Справочник.ДоговорыКредитовИДепозитов)
	|			{АналитикаУчетаПоПартнерам.Партнер.* КАК Партнер,
	|			АналитикаУчетаПоПартнерам.Партнер.ОсновнойМенеджер.* КАК ОсновнойМенеджер,
	|			АналитикаУчетаПоПартнерам.Контрагент.* КАК Контрагент,
	|			АналитикаУчетаПоПартнерам.НаправлениеДеятельности.* КАК НаправлениеДеятельности}
	|		) КАК РасчетыОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаПоПартнерам КАК Аналитика
	|		ПО РасчетыОстатки.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиНаКонецПериодаФинИнструменты КАК ОстаткиНаКонецПериода
	|		ПО РасчетыОстатки.АналитикаУчетаПоПартнерам = ОстаткиНаКонецПериода.АналитикаУчетаПоПартнерам
	|			И РасчетыОстатки.Договор = ОстаткиНаКонецПериода.Договор
	|			И РасчетыОстатки.Валюта = ОстаткиНаКонецПериода.Валюта
	|	ГДЕ
	|		РасчетыОстатки.СуммаОстаток <> 0) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ТипРасчетов,
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.Партнер,
	|	ВложенныйЗапрос.Контрагент,
	|	ВложенныйЗапрос.Договор,
	|	ВложенныйЗапрос.НаправлениеДеятельности,
	|	ВложенныйЗапрос.НомерДоговора,
	|	ВложенныйЗапрос.ДатаДоговора,
	|	ВложенныйЗапрос.НаименованиеДоговора,
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.Валюта,
	|	ВложенныйЗапрос.ТипСуммы,
	|	ВложенныйЗапрос.ПорядокТипаПлатежа
	|	
	|{ГДЕ
	|	ВложенныйЗапрос.ТипРасчетов КАК ТипРасчетов}
	|
	|ОБЪЕДИНИТЬ ВСЕ 
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Период КАК Период,
	|	ВложенныйЗапрос.ТипРасчетов КАК ТипРасчетов,
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.Партнер КАК Партнер,
	|	ВложенныйЗапрос.Контрагент КАК Контрагент,
	|	ВложенныйЗапрос.Договор КАК Договор,
	|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВложенныйЗапрос.НомерДоговора КАК НомерДоговора,
	|	ВложенныйЗапрос.ДатаДоговора КАК ДатаДоговора,
	|	ВложенныйЗапрос.НаименованиеДоговора КАК НаименованиеДоговора,
	|	ВложенныйЗапрос.Договор КАК ОбъектРасчетов,
	|	ВложенныйЗапрос.Регистратор КАК РасчетныйДокумент,
	|	ВложенныйЗапрос.Валюта КАК Валюта,
	|	ВложенныйЗапрос.ТипСуммы КАК ТипСуммы,
	|	ИСТИНА КАК ДоговорыБезОборотов, //Договоры с оборотами включаем всегда
	|	
	|	0 КАК НачальноеСальдо,
	|	0 КАК НачальноеСальдоРегл,
	|	0 КАК НачальноеСальдоДт,
	|	0 КАК НачальноеСальдоДтРегл,
	|	0 КАК НачальноеСальдоКт,
	|	0 КАК НачальноеСальдоКтРегл,
	|	
	|	СУММА(ВложенныйЗапрос.ОборотДт) КАК ОборотДт,
	|	СУММА(ВложенныйЗапрос.ОборотДтРегл) КАК ОборотДтРегл,
	|	СУММА(ВложенныйЗапрос.ОборотКт) КАК ОборотКт,
	|	СУММА(ВложенныйЗапрос.ОборотКтРегл) КАК ОборотКтРегл,
	|	
	|	0 КАК Долг,
	|	0 КАК ДолгРегл,
	|	0 КАК Предоплата,
	|	0 КАК ПредоплатаРегл,
	|	
	|	СУММА(ВложенныйЗапрос.КонечноеСальдо) КАК КонечноеСальдо,
	|	СУММА(ВложенныйЗапрос.КонечноеСальдоРегл) КАК КонечноеСальдоРегл,
	|	СУММА(ВложенныйЗапрос.КонечноеСальдоДт) КАК КонечноеСальдоДт,
	|	СУММА(ВложенныйЗапрос.КонечноеСальдоКт) КАК КонечноеСальдоКт,
	|	СУММА(ВложенныйЗапрос.КонечноеСальдоДтРегл) КАК КонечноеСальдоДтРегл,
	|	СУММА(ВложенныйЗапрос.КонечноеСальдоКтРегл) КАК КонечноеСальдоКтРегл,
	|	
	|	ВложенныйЗапрос.ПорядокТипаПлатежа КАК ПорядокТипаПлатежа
	|ИЗ
	|	(ВЫБРАТЬ
	|		РасчетыОбороты.Период КАК Период,
	|		ВЫБОР 
	|			КОГДА РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКредитором)
	|			КОГДА РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит)
	|					ИЛИ РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСДебитором)
	|			КОГДА РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Аренда)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСАрендодателем)
	|		КОНЕЦ КАК ТипРасчетов,
	|		РасчетыОбороты.АналитикаУчетаПоПартнерам   КАК АналитикаУчетаПоПартнерам,
	|		Аналитика.Организация КАК Организация,
	|		Аналитика.Партнер КАК Партнер,
	|		Аналитика.Контрагент КАК Контрагент,
	|		РасчетыОбороты.Договор КАК Договор,
	|		РасчетыОбороты.Договор.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		РасчетыОбороты.Договор.Номер КАК НомерДоговора,
	|		РасчетыОбороты.Договор.Дата КАК ДатаДоговора,
	|		РасчетыОбороты.Договор.НаименованиеДляПечати КАК НаименованиеДоговора,
	|		РасчетыОбороты.Регистратор КАК Регистратор,
	|		РасчетыОбороты.Валюта КАК Валюта,
	|		НЕОПРЕДЕЛЕНО КАК ТипСуммы,
	|	
	|		0 КАК НачальноеСальдо,
	|		0 КАК НачальноеСальдоРегл,
	|		0 КАК НачальноеСальдоДт,
	|		0 КАК НачальноеСальдоДтРегл,
	|		0 КАК НачальноеСальдоКт,
	|		0 КАК НачальноеСальдоКтРегл,
	|	
	|		РасчетыОбороты.СуммаПриход КАК ОборотДт,
	|		РасчетыОбороты.СуммаРеглПриход КАК ОборотДтРегл,
	|		РасчетыОбороты.СуммаРасход КАК ОборотКт,
	|		РасчетыОбороты.СуммаРеглРасход КАК ОборотКтРегл,
	|	
	|		0 КАК Долг,
	|		0 КАК ДолгРегл,
	|		0 КАК Предоплата,
	|		0 КАК ПредоплатаРегл,
	|	
	|		ЕСТЬNULL(ОстаткиНаКонецПериода.КонечноеСальдо, 0) КАК КонечноеСальдо,
	|		ЕСТЬNULL(ОстаткиНаКонецПериода.КонечноеСальдоРегл, 0) КАК КонечноеСальдоРегл,
	|		ЕСТЬNULL(ОстаткиНаКонецПериода.КонечноеСальдоДт, 0) КАК КонечноеСальдоДт,
	|		ЕСТЬNULL(ОстаткиНаКонецПериода.КонечноеСальдоКт, 0) КАК КонечноеСальдоКт,
	|		ЕСТЬNULL(ОстаткиНаКонецПериода.КонечноеСальдоДтРегл, 0) КАК КонечноеСальдоДтРегл,
	|		ЕСТЬNULL(ОстаткиНаКонецПериода.КонечноеСальдоКтРегл, 0) КАК КонечноеСальдоКтРегл,
	|	
	|		ВЫБОР 
	|			КОГДА РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|				ТОГДА 31
	|			КОГДА РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит)
	|					ИЛИ РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный)
	|				ТОГДА 32
	|			КОГДА РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Аренда)
	|				ТОГДА 33
	|		КОНЕЦ КАК ПорядокТипаПлатежа
	|	ИЗ
	|		РегистрНакопления.РасчетыПоФинансовымИнструментам.Обороты(
	|			{&НачалоПериода}, {&КонецПериода}, Регистратор,
	|			ТипСуммы В (&ТипыСуммАренды) ИЛИ ТИПЗНАЧЕНИЯ(Договор) = ТИП(Справочник.ДоговорыКредитовИДепозитов)
	|			{АналитикаУчетаПоПартнерам.Партнер.* КАК Партнер,
	|			АналитикаУчетаПоПартнерам.Контрагент.* КАК Контрагент,
	|			АналитикаУчетаПоПартнерам.НаправлениеДеятельности.* КАК НаправлениеДеятельности}
	|		) КАК РасчетыОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаПоПартнерам КАК Аналитика
	|		ПО РасчетыОбороты.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиНаКонецПериодаФинИнструменты КАК ОстаткиНаКонецПериода
	|		ПО РасчетыОбороты.АналитикаУчетаПоПартнерам = ОстаткиНаКонецПериода.АналитикаУчетаПоПартнерам
	|			И РасчетыОбороты.Договор = ОстаткиНаКонецПериода.Договор
	|			И РасчетыОбороты.Валюта = ОстаткиНаКонецПериода.Валюта
	|	ГДЕ
	|		РасчетыОбороты.СуммаОборот <> 0) КАК ВложенныйЗапрос
	|	
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.ТипРасчетов,
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.Партнер,
	|	ВложенныйЗапрос.Контрагент,
	|	ВложенныйЗапрос.Договор,
	|	ВложенныйЗапрос.НаправлениеДеятельности,
	|	ВложенныйЗапрос.НомерДоговора,
	|	ВложенныйЗапрос.ДатаДоговора,
	|	ВложенныйЗапрос.НаименованиеДоговора,
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.Валюта,
	|	ВложенныйЗапрос.ТипСуммы,
	|	ВложенныйЗапрос.ПорядокТипаПлатежа
	|	
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.ОборотДт) <> 0 ИЛИ СУММА(ВложенныйЗапрос.ОборотДтРегл) <> 0
	|	ИЛИ СУММА(ВложенныйЗапрос.ОборотКт) <> 0 ИЛИ СУММА(ВложенныйЗапрос.ОборотКтРегл) <> 0
	|	
	|{ГДЕ
	|	ВложенныйЗапрос.ТипРасчетов КАК ТипРасчетов}";
	
КонецФункции

Функция ТекстЗапросаДоговоровКредитовДепозитовЗаймов()
	
	Возврат "
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&НачалоПериода КАК Период,
	|	ВЫБОР 
	|		КОГДА Договоры.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКредитором)
	|		КОГДА Договоры.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит)
	|				ИЛИ Договоры.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСДебитором)
	|	КОНЕЦ КАК ТипРасчетов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПоПартнерам,
	|	Договоры.Организация КАК Организация,
	|	Договоры.Партнер КАК Партнер,
	|	Договоры.Контрагент КАК Контрагент,
	|	Договоры.Ссылка КАК Договор,
	|	Договоры.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Договоры.Номер КАК НомерДоговора,
	|	Договоры.Дата КАК ДатаДоговора,
	|	Договоры.НаименованиеДляПечати КАК НаименованиеДоговора,
	|	НЕОПРЕДЕЛЕНО КАК ОбъектРасчетов,
	|	НЕОПРЕДЕЛЕНО КАК РасчетныйДокумент,
	|	Договоры.ВалютаВзаиморасчетов КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК ТипСуммы,
	|	ИСТИНА КАК ДоговорыБезОборотов,
	|		
	|	0 КАК НачальноеСальдо,
	|	0 КАК НачальноеСальдоРегл,
	|	0 КАК НачальноеСальдоДт,
	|	0 КАК НачальноеСальдоДтРегл,
	|	0 КАК НачальноеСальдоКт,
	|	0 КАК НачальноеСальдоКтРегл,
	|	
	|	0 КАК ОборотДт,
	|	0 КАК ОборотДтРегл,
	|	0 КАК ОборотКт,
	|	0 КАК ОборотКтРегл,
	|	
	|	0 КАК Долг,
	|	0 КАК ДолгРегл,
	|	0 КАК Предоплата,
	|	0 КАК ПредоплатаРегл,
	|	
	|	0 КАК КонечноеСальдо,
	|	0 КАК КонечноеСальдоРегл,
	|	0 КАК КонечноеСальдоДт,
	|	0 КАК КонечноеСальдоКт,
	|	0 КАК КонечноеСальдоДтРегл,
	|	0 КАК КонечноеСальдоКтРегл,
	|	
	|	ВЫБОР 
	|		КОГДА Договоры.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|			ТОГДА 31
	|		КОГДА Договоры.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит)
	|				ИЛИ Договоры.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный)
	|			ТОГДА 32
	|	КОНЕЦ КАК ПорядокТипаПлатежа
	|ИЗ
	|	Справочник.ДоговорыКредитовИДепозитов КАК Договоры
	|ГДЕ
	|	НЕ Договоры.ПометкаУдаления
	|	И &ДоговорыБезОборотов
	|{ГДЕ
	|	ВЫБОР 
	|		КОГДА Договоры.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКредитором)
	|		КОГДА Договоры.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит)
	|				ИЛИ Договоры.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСДебитором)
	|	КОНЕЦ КАК ТипРасчетов,
	|	ИСТИНА КАК ДоговорыБезОборотов}";
	
КонецФункции

Функция ТекстЗапросаПервичныйДокумент()
	
	Возврат "
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СуммыДокументов.Ссылка КАК Регистратор,
	|	Обороты.Организация КАК Организация,
	|	Обороты.ТипРасчетов КАК ТипРасчетов,
	|	СуммыДокументов.ДатаВходящегоДокумента КАК ДатаДокумента,
	|	СуммыДокументов.НомерВходящегоДокумента КАК НомерДокумента,
	|	СуммыДокументов.Дата КАК ДатаДокументаИБ,
	|	СуммыДокументов.Номер КАК НомерДокументаИБ,
	|	СуммыДокументов.НаименованиеВходящегоДокумента КАК НаименованиеПервичногоДокумента,
	|	СуммыДокументов.Валюта КАК Валюта,
	|	СуммыДокументов.СуммаДокумента КАК Сумма
	|ИЗ
	|	Обороты КАК Обороты
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПервичныйДокумент КАК СуммыДокументов
	|	ПО Обороты.РасчетныйДокумент = СуммыДокументов.Ссылка
	|		И Обороты.РасчетныйДокумент <> НЕОПРЕДЕЛЕНО";
	
КонецФункции

Функция ТекстЗапросаКорректировкаЗадолженности()
	
	Возврат "
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СуммыДокументов.Ссылка КАК Регистратор,
	|	Обороты.Организация КАК Организация,
	|	Обороты.ТипРасчетов КАК ТипРасчетов,
	|	СуммыДокументов.Ссылка.Дата КАК ДатаДокумента,
	|	СуммыДокументов.Ссылка.Номер КАК НомерДокумента,
	|	СуммыДокументов.Ссылка.Дата КАК ДатаДокументаИБ,
	|	СуммыДокументов.Ссылка.Номер КАК НомерДокументаИБ,
	|	"""" КАК НаименованиеПервичногоДокумента,
	|	СуммыДокументов.ВалютаВзаиморасчетов КАК Валюта,
	|	СУММА(СуммыДокументов.Сумма) КАК Сумма
	|ИЗ
	|	Обороты КАК Обороты
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаЗадолженности.Задолженность КАК СуммыДокументов
	|	ПО Обороты.РасчетныйДокумент = СуммыДокументов.Ссылка
	|		И Обороты.РасчетныйДокумент <> НЕОПРЕДЕЛЕНО
	|		И Обороты.ТипРасчетов = СуммыДокументов.ТипРасчетов
	|		И Обороты.ОбъектРасчетов = СуммыДокументов.ОбъектРасчетов
	|		И Обороты.Партнер = СуммыДокументов.Партнер
	|		
	|СГРУППИРОВАТЬ ПО
	|	СуммыДокументов.Ссылка,
	|	Обороты.ТипРасчетов,
	|	Обороты.Организация,
	|	СуммыДокументов.ВалютаВзаиморасчетов";
	
КонецФункции

// Реквизиты последнего документа.
// 
// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты
// 
// Возвращаемое значение:
//  Структура - Реквизиты последнего документа:
// * Партнер - СправочникСсылка.Партнеры
// * Договор - СправочникСсылка.ДоговорыКонтрагентов
// * РасшифровкаПоЗаказам - Булево
// * РасшифровкаПоПартнерам - Булево
// * РасшифровкаПоДоговорам - Булево
// * ФИОРуководителяКонтрагента - Строка
// * ДолжностьРуководителяКонтрагента - Строка
// * КонтактноеЛицо - Строка
Функция РеквизитыПоследнегоДокумента(Контрагент) Экспорт

	СтруктураРеквизитов = Новый Структура("Партнер,
										  |Договор,
										  |РасшифровкаПоЗаказам,
										  |РасшифровкаПоПартнерам,
										  |РасшифровкаПоДоговорам,
										  |ФИОРуководителяКонтрагента,
										  |ДолжностьРуководителяКонтрагента,
										  |КонтактноеЛицо");
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	СверкаВзаиморасчетов.Партнер КАК Партнер,
		|	СверкаВзаиморасчетов.Договор КАК Договор,
		|	ИСТИНА КАК РасшифровкаПоЗаказам,
		|	СверкаВзаиморасчетов.РазбиватьПоПартнерам КАК РасшифровкаПоПартнерам,
		|	СверкаВзаиморасчетов.РазбиватьПоДоговорам КАК РасшифровкаПоДоговорам,
		|	СверкаВзаиморасчетов.ФИОРуководителяКонтрагента КАК ФИОРуководителяКонтрагента,
		|	СверкаВзаиморасчетов.ДолжностьРуководителяКонтрагента КАК ДолжностьРуководителяКонтрагента,
		|	СверкаВзаиморасчетов.КонтактноеЛицо КАК КонтактноеЛицо
		|ИЗ
		|	Документ.СверкаВзаиморасчетов2_5_11 КАК СверкаВзаиморасчетов
		|ГДЕ
		|	СверкаВзаиморасчетов.Контрагент = &Контрагент
		|	И НЕ СверкаВзаиморасчетов.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	СверкаВзаиморасчетов.МоментВремени УБЫВ";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтруктураРеквизитов, Выборка);
	КонецЕсли;
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

Функция СтруктураРеквизитыОтветственныеЛица() Экспорт
	
	РеквизитыДокумента = Метаданные.Документы.СверкаВзаиморасчетов2_5_11.Реквизиты;
	Результат = Новый Структура();
	Результат.Вставить(
		РеквизитыДокумента.Руководитель.Имя, 
		Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
	Результат.Вставить(
		РеквизитыДокумента.ГлавныйБухгалтер.Имя, 
		Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер);
	Возврат Результат;
	
КонецФункции

Функция СтруктураРеквизитыОтветственныеЛицаОрганизацииКонтрагента() Экспорт
	
	РеквизитыДокумента = Метаданные.Документы.СверкаВзаиморасчетов2_5_11.Реквизиты;
	Результат = Новый Структура();
	Результат.Вставить(
		РеквизитыДокумента.РуководительОрганизацииКонтрагента.Имя, 
		Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
	Результат.Вставить(
		РеквизитыДокумента.ГлавныйБухгалтерОрганизацииКонтрагента.Имя, 
		Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер);
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.СверкаВзаиморасчетов2_5_11.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.20.47";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("7ff47b4e-d9e2-403e-8194-2684e42a7751");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.СверкаВзаиморасчетов2_5_11.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Некритичный;
	Обработчик.Комментарий = НСтр("ru = 'Заполняет новый реквизит ""Дата операции""'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.СверкаВзаиморасчетов2_5_11.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Документы.СверкаВзаиморасчетов2_5_11.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.СверкаВзаиморасчетов2_5_11.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = ПустаяСсылка().Метаданные().ПолноеИмя();
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СверкаВзаиморасчетов2_5_11.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.СверкаВзаиморасчетов2_5_11.ДетальныеЗаписи КАК СверкаВзаиморасчетов2_5_11
	|ГДЕ
	|	СверкаВзаиморасчетов2_5_11.ДатаОперации = ДАТАВРЕМЯ(1, 1, 1)
	|";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

// Обработчик обновления.
// 
// Параметры:
//  Параметры - Структура - параметры обработчика:
//   * ВерсияПодсистемыНаНачалоОбновления - Строка - версия подсистемы.
//   * ИмяОбработчика - Строка - имя обработчика.
//   * ОбновляемыеДанные - Структура.
//   * ОбработкаЗавершена - Булево, Неопределено - признак завершения обработки.
//   * Очередь - Число - очередь.
//   * ПрогрессВыполнения - Структура:
//     ** ВсегоОбъектов - Число - всего обработано объектов.
//     ** ОбработаноОбъектов - Число - обработано объектов.
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	ПолноеИмяОбъекта = ПустаяСсылка().Метаданные().ПолноеИмя();
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена =
			ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	ИсключенияПриОбновлении = Новый Массив;
	
	ТекстОписания = СтрЗаменить(НСтр("ru = 'Не удалось обработать документы ""%1"" по обработчику:'"),"%1",ПолноеИмяОбъекта);
	СписокОписаний = Новый Массив;
	СписокОписаний.Добавить(ТекстОписания);
	СписокОписаний.Добавить(НСтр("ru = '- заполнение реквизита ""Дата операции"" в табличной части ""Детальные записи"";'"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДляОбновления.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ТаблицаДокументов
	|ИЗ
	|	&ДанныеДляОбновления КАК ДанныеДляОбновления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокумента.ВерсияДанных КАК ВерсияДанных
	|ИЗ
	|	ТаблицаДокументов КАК ТаблицаДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СверкаВзаиморасчетов2_5_11 КАК ДанныеДокумента
	|		ПО ТаблицаДокументов.Ссылка = ДанныеДокумента.Ссылка
	|;";
	
	Запрос.УстановитьПараметр("ДанныеДляОбновления", ОбновляемыеДанные);
	
	ЗапросДатыОпераций = ЗапросДатыОпераций();
	
	Документ = Запрос.Выполнить().Выбрать();
	Пока Документ.Следующий() Цикл
		
		ПроблемныйДокумент = Документ.Ссылка;
		ПричинаИсключения = 0;
		Рекомендация = "";
		
		НачатьТранзакцию();
		
		Попытка
			
			ПричинаИсключения = 1; // Блокировка
			
			#Область Блокировка
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ПроблемныйДокумент);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			#КонецОбласти
			
			ДокументОбъект = ОбновлениеИнформационнойБазыУТ.ПроверитьПолучитьОбъект(
				ПроблемныйДокумент, Документ.ВерсияДанных, Параметры.Очередь); // ДокументОбъект
			
			ПричинаИсключения = 2; // ПлохиеДанные
			Рекомендация = НСтр("ru = 'Перезаполните документ вручную.'");
			
			ОбъектИзменен = Ложь;
			
			Если ДокументОбъект <> Неопределено Тогда
				
				#Область ЗаполнитьДатуОперации
				ЗапросДатыОпераций.УстановитьПараметр("Ссылка", ПроблемныйДокумент);
				ДатыОпераций = ЗапросДатыОпераций.Выполнить().Выгрузить();
				ДатыОпераций.Индексы.Добавить("РасчетныйДокумент");
				Для Каждого Запись Из ДокументОбъект.ДетальныеЗаписи Цикл
					ДанныеДокумента = ДатыОпераций.Найти(Запись.РасчетныйДокумент,"РасчетныйДокумент");
					Если НЕ ЗначениеЗаполнено(Запись.ДатаОперации) И ДанныеДокумента <> Неопределено Тогда
						Запись.ДатаОперации = ДанныеДокумента.ДатаОперации;
						ОбъектИзменен = Истина;
					КонецЕсли;
				КонецЦикла;
				#КонецОбласти
				
			КонецЕсли;
			
			ПричинаИсключения = 3; // Запись
			
			Если ОбъектИзменен Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ПроблемныйДокумент);
			КонецЕсли;
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), ПроблемныйДокумент);
			
			Если ПричинаИсключения = 2 Тогда
				
				ОписаниеПроблемы = ОбновлениеИнформационнойБазыУТ.ПроблемаСДанными(
					ПроблемныйДокумент, Рекомендация, ИнформацияОбОшибке());
				ИсключенияПриОбновлении.Добавить(ОписаниеПроблемы);
				
			ИначеЕсли ПричинаИсключения = 3 Тогда
				
				ОбновлениеИнформационнойБазыУТ.ЗаписатьПлохиеДанные(
					ИсключенияПриОбновлении, ОбъектовОбработано, Параметры);
				ВызватьИсключение СтрСоединить(СписокОписаний, Символы.ПС);
				
			КонецЕсли;
			
		КонецПопытки;
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена =
		ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов <> 0 Тогда
		
		СписокОписаний.Добавить(НСтр("ru = 'Всего пропущено: %1'"));
		ТекстСообщения = СтрШаблон(СтрСоединить(СписокОписаний, Символы.ПС), ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
		
	Иначе
		
		ШаблонСообщения = НСтр("ru = 'Обработана порция документов ""%1"": %2'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ПолноеИмяОбъекта, ОбъектовОбработано);
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация, , ,
			ТекстСообщения);
		
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыУТ.ЗаписатьПлохиеДанные(ИсключенияПриОбновлении, ОбъектовОбработано, Параметры);
	
КонецПроцедуры

Функция ЗапросДатыОпераций()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДетальныеЗаписи.РасчетныйДокумент КАК Ссылка
	|ПОМЕСТИТЬ втРасчетныеДокументы
	|ИЗ
	|	Документ.СверкаВзаиморасчетов2_5_11.ДетальныеЗаписи КАК ДетальныеЗаписи
	|ГДЕ
	|	ДетальныеЗаписи.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(РасчетыПоСрокам.Период) КАК ДатаОперации,
	|	РасчетыПоСрокам.ДокументРегистратор КАК РасчетныйДокумент
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыПоСрокам
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРасчетныеДокументы КАК РасчетныеДокументы
	|	ПО РасчетыПоСрокам.ДокументРегистратор = РасчетныеДокументы.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоСрокам.ДокументРегистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МИНИМУМ(РасчетыПоСрокам.Период) КАК ДатаОперации,
	|	РасчетыПоСрокам.ДокументРегистратор КАК РасчетныйДокумент
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыПоСрокам
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРасчетныеДокументы КАК РасчетныеДокументы
	|	ПО РасчетыПоСрокам.ДокументРегистратор = РасчетныеДокументы.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоСрокам.ДокументРегистратор
	|";
	
	Возврат Запрос;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
