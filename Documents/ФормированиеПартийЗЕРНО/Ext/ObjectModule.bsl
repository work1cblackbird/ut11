#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВнесениеСведенийОСобранномУрожаеЗЕРНО") Тогда
		ЗаполнитьПоВнесениюСведенийОСобранномУрожаеЗЕРНО(ДанныеЗаполнения);
	ИначеЕсли ЭтоГосмониторинг(ДанныеЗаполнения) Тогда
		ЗаполнитьПоДаннымГосмониторинга(ДанныеЗаполнения);
	КонецЕсли;
	
	ИнтеграцияЗЕРНОПереопределяемый.ОбработкаЗаполненияДокумента(ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	
	ЗаполнитьОбъектПоСтатистике();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПродуктыПереработкиЗерна") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ГодУрожая");
		МассивНепроверяемыхРеквизитов.Добавить("ВладелецПартии");
		Если ИнтеграцияЗЕРНО.ОпределитьТипОрганизацияКонтрагент(Товаропроизводитель) = 0 Тогда
			МассивНепроверяемыхРеквизитов.Добавить("Товаропроизводитель");
		КонецЕсли;
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("Товаропроизводитель");
		Если Не ПоставитьПартиюНаХранение
			Или ИнтеграцияЗЕРНО.ОпределитьТипОрганизацияКонтрагент(ВладелецПартии) = 0 Тогда
			МассивНепроверяемыхРеквизитов.Добавить("ВладелецПартии");
		КонецЕсли;
		МассивНепроверяемыхРеквизитов.Добавить("Товары.ДатаИзготовления");
	КонецЕсли;
	
	Если Операция <> Перечисления.ВидыОперацийЗЕРНО.ФормированиеПартииПриСбореУрожая Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДокументОснование");
	КонецЕсли;
	
	Если Операция = Перечисления.ВидыОперацийЗЕРНО.ФормированиеПартииИмпорт
		Или Операция = Перечисления.ВидыОперацийЗЕРНО.ФормированиеПартииИзОстатков
		Или Операция = Перечисления.ВидыОперацийЗЕРНО.ФормированиеПартииПоРезультатамГосмониторинга Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.ИдентификаторСведенийОСобранномУрожае");
	КонецЕсли;
	
	Если Не ИнтеграцияЗЕРНОКлиентСервер.ТребуетсяЗаполнениеКодаТНВЭД(НазначениеПартии) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.КодТНВЭД");
	КонецЕсли;
	
	Если НазначениеПартии <> ПредопределенноеЗначение("Справочник.КлассификаторНСИЗЕРНО.НазначениеПартииВывозСТерриторииРФ") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СтранаНазначения");
	КонецЕсли;
	
	Если Операция <> Перечисления.ВидыОперацийЗЕРНО.ФормированиеПартииИмпорт Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.ПроисхождениеСтрокой");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Происхождение");
	КонецЕсли;
	
	ВыполнятьПроверкуПотребительскихСвойств = Истина;
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ТекущееСостояние = РегистрыСведений.СтатусыОбъектовСинхронизацииЗЕРНО.ТекущееСостояние(Ссылка);
		КонечныеСтатусы = Документы.ФормированиеПартийЗЕРНО.КонечныеСтатусы(Ложь);
		Если КонечныеСтатусы.Найти(ТекущееСостояние.Статус) <> Неопределено Тогда
			ВыполнятьПроверкуПотребительскихСвойств = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ВыполнятьПроверкуПотребительскихСвойств Тогда
		
		ТаблицаОшибок = ПустыеПотребительскиеСвойстваПродукцииПоДокументу();
		
		Для Каждого СтрокаТаблицы Из ТаблицаОшибок Цикл
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'В строке %1 для с/х продукции %2 значение потребительского свойства %3 вне диапазона.'"),
				СтрокаТаблицы.НомерСтроки,
				СтрокаТаблицы.ТоварыОКПД2,
				СтрокаТаблицы.ПотребительскоеСвойство);
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект, 
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					"Товары", СтрокаТаблицы.НомерСтроки, "ОКПД2"),, Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("Товары.РезультатИсследования");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.АктОтбораПроб");
	Если Операция = Перечисления.ВидыОперацийЗЕРНО.ФормированиеПартииПоРезультатамГосмониторинга Тогда
		
		ТаблицаОшибок = ТаблицаОшибокПриГосмониторинге();
		
		Для Каждого СтрокаТаблицы Из ТаблицаОшибок Цикл
			
			Если СтрокаТаблицы.КатегорияОшибки = 0 Тогда
				ТекстСообщения = СтрШаблон(НСтр("ru = 'В строке %1 для с/х продукции %2 недоступна операция ""По результатам госмониторинга"".'"),
					СтрокаТаблицы.НомерСтроки,
					СтрокаТаблицы.ТоварыОКПД2);
			Иначе
				ТекстСообщения = СтрШаблон(НСтр("ru = 'В строке %1 для с/х продукции %2 должны быть заполнены ""Результат исследования"" или ""Акт отбора проб"".'"),
					СтрокаТаблицы.НомерСтроки,
					СтрокаТаблицы.ТоварыОКПД2);
			КонецЕсли;
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект, 
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					"Товары", СтрокаТаблицы.НомерСтроки, "ОКПД2"),, Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ИнтеграцияЗЕРНОПереопределяемый.ПриОпределенииОбработкиПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	Если ИнтеграцияЗЕРНО.ОпределитьТипОрганизацияКонтрагент(ВладелецПартии) = 1 Тогда
		ПодразделениеВладельцаПартии = ОбщегоНазначенияИС.ПустоеЗначениеОпределяемогоТипа("Подразделение");
	КонецЕсли;
	
	Если ИнтеграцияЗЕРНО.ОпределитьТипОрганизацияКонтрагент(Товаропроизводитель) = 1 Тогда
		ПодразделениеТоваропроизводителя = ОбщегоНазначенияИС.ПустоеЗначениеОпределяемогоТипа("Подразделение");
	КонецЕсли;
	
	ИнтеграцияЗЕРНО.ЗаписатьСоответствиеНоменклатуры(ЭтотОбъект);
	
	ИнтеграцияИСПереопределяемый.ПередЗаписьюОбъекта(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	ИнтеграцияЗЕРНО.ЗаписатьСтатусДокументаЗЕРНОПоУмолчанию(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Для Каждого СтрокаТоваров Из Товары Цикл
		СтрокаТоваров.Партия = Неопределено;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа
	ИнтеграцияИС.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	// Инициализация данных документа
	Документы.ФормированиеПартийЗЕРНО.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ИнтеграцияИС.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	РегистрыНакопления.ОстаткиПартийЗЕРНО.ОтразитьДвижения(ДополнительныеСвойства, Движения, Отказ);
	ИнтеграцияИСПереопределяемый.ОтразитьДвижения(ДополнительныеСвойства, Движения, Отказ);
	
	ИнтеграцияИС.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ИнтеграцияИСПереопределяемый.ОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения);
	
	ИнтеграцияИС.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаЗаполнения

Процедура ЗаполнитьПоВнесениюСведенийОСобранномУрожаеЗЕРНО(ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("ПодлежитГосмониторингу", ИнтеграцияЗЕРНО.ОКПД2ПодлежащиеГосмониторингу());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНО.ДатаСбораУрожая КАК ДатаСбораУрожая,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНО.Организация КАК Организация,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНО.Подразделение КАК Подразделение,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНО.СкладКонтрагент КАК СкладКонтрагент,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНО.МестоХранения КАК Местоположение,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНО.МестоХраненияСтрокой КАК МестоположениеСтрокой
	|ИЗ
	|	Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО КАК ВнесениеСведенийОСобранномУрожаеЗЕРНО
	|ГДЕ
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНО.Ссылка = &ДокументОснование";
	Шапка = Запрос.Выполнить().Выбрать();
	
	ВидПродукции      = Перечисления.ВидыПродукцииИС.Зерно;
	ДокументОснование = ДанныеЗаполнения;
	Операция = Перечисления.ВидыОперацийЗЕРНО.ФормированиеПартииПриСбореУрожая;
	Если Шапка.Следующий() Тогда
		Если Не ЗначениеЗаполнено(НазначениеПартии) 
			Или НазначениеПартии = Справочники.КлассификаторНСИЗЕРНО.НазначениеПартииВвозНаТерриториюРФ Тогда
			НазначениеПартии = Справочники.КлассификаторНСИЗЕРНО.НазначениеПартииХранениеОбработка;
		КонецЕсли;
		ГодУрожая         = Год(Шапка.ДатаСбораУрожая);
		Организация       = Шапка.Организация;
		Подразделение     = Шапка.Подразделение;
	КонецЕсли;
	
	Если Операция = Перечисления.ВидыОперацийЗЕРНО.ФормированиеПартииПоРезультатамГосмониторинга Тогда
		ЗаполнитьПоВнесениюСведенийОСобранномУрожаеВключаяРезультатыГосмониторинга(Шапка, Запрос, ДанныеЗаполнения);
	Иначе
		ЗаполнитьПоВнесениюСведенийОСобранномУрожаеБезГосмониторинга(Шапка, Запрос, ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоВнесениюСведенийОСобранномУрожаеВключаяРезультатыГосмониторинга(Шапка, Запрос, ДанныеЗаполнения)
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.ОКПД2,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Номенклатура,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Характеристика,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Серия,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.СтатусУказанияСерий,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Количество,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.КоличествоЗЕРНО,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Ссылка.СкладКонтрагент КАК СкладКонтрагент,
	|	АктыОтбораПробЗЕРНО.Ссылка КАК АктОтбораПроб,
	|	РезультатыИсследованийЗЕРНО.Ссылка КАК РезультатИсследования
	|ПОМЕСТИТЬ ОжидаемыеПартии
	|ИЗ
	|	Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО.Товары КАК ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.АктыОтбораПробЗЕРНО КАК АктыОтбораПробЗЕРНО
	|		ПО АктыОтбораПробЗЕРНО.МестоФормированияПартии = ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.МестоФормированияПартии
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РезультатыИсследованийЗЕРНО КАК РезультатыИсследованийЗЕРНО
	|		ПО РезультатыИсследованийЗЕРНО.МестоФормированияПартии = ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.МестоФормированияПартии
	|ГДЕ
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Ссылка = &ДокументОснование
	|	И ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.ОКПД2 В (&ПодлежитГосмониторингу)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФормированиеПартийЗЕРНО.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ФормированиеПартийЗЕРНО КАК ФормированиеПартийЗЕРНО
	|ГДЕ
	|	ФормированиеПартийЗЕРНО.Ссылка <> &Ссылка
	|	И ФормированиеПартийЗЕРНО.Проведен
	|	И ФормированиеПартийЗЕРНО.ДокументОснование = &ДокументОснование
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ФормированиеПартийЗЕРНОТовары.Количество) КАК Количество,
	|	СУММА(ФормированиеПартийЗЕРНОТовары.КоличествоЗЕРНО) КАК КоличествоЗЕРНО,
	|	ВЫБОР
	|		КОГДА ФормированиеПартийЗЕРНОТовары.АктОтбораПроб <> ЗНАЧЕНИЕ(Справочник.АктыОтбораПробЗЕРНО.ПустаяСсылка)
	|			ТОГДА ФормированиеПартийЗЕРНОТовары.АктОтбораПроб
	|		ИНАЧЕ ФормированиеПартийЗЕРНОТовары.РезультатИсследования.АктОтбораПроб
	|	КОНЕЦ КАК АктОтбораПроб
	|ПОМЕСТИТЬ ОформленныеТовары
	|ИЗ
	|	Документ.ФормированиеПартийЗЕРНО.Товары КАК ФормированиеПартийЗЕРНОТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОформленныеДокументы КАК ОформленныеДокументы
	|		ПО ФормированиеПартийЗЕРНОТовары.Ссылка = ОформленныеДокументы.Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ФормированиеПартийЗЕРНОТовары.АктОтбораПроб <> ЗНАЧЕНИЕ(Справочник.АктыОтбораПробЗЕРНО.ПустаяСсылка)
	|			ТОГДА ФормированиеПартийЗЕРНОТовары.АктОтбораПроб
	|		ИНАЧЕ ФормированиеПартийЗЕРНОТовары.РезультатИсследования.АктОтбораПроб
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОжидаемыеПартии.ОКПД2 КАК ОКПД2
	|ИЗ
	|	ОжидаемыеПартии КАК ОжидаемыеПартии
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОформленныеТовары КАК ОформленныеТовары
	|		ПО ОжидаемыеПартии.АктОтбораПроб = ОформленныеТовары.АктОтбораПроб
	|ГДЕ
	|	ОжидаемыеПартии.КоличествоЗЕРНО > ЕСТЬNULL(ОформленныеТовары.КоличествоЗЕРНО, 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОжидаемыеПартии.ОКПД2 КАК ОКПД2,
	|	ОжидаемыеПартии.Номенклатура КАК Номенклатура,
	|	ОжидаемыеПартии.Характеристика КАК Характеристика,
	|	ОжидаемыеПартии.Серия КАК Серия,
	|	ОжидаемыеПартии.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ОжидаемыеПартии.СкладКонтрагент КАК СкладКонтрагент,
	|	ОжидаемыеПартии.АктОтбораПроб КАК АктОтбораПроб,
	|	ОжидаемыеПартии.РезультатИсследования КАК РезультатИсследования,
	|	ОжидаемыеПартии.Количество - ЕСТЬNULL(ОформленныеТовары.Количество, 0) КАК Количество,
	|	ОжидаемыеПартии.КоличествоЗЕРНО - ЕСТЬNULL(ОформленныеТовары.КоличествоЗЕРНО, 0) КАК КоличествоЗЕРНО
	|ИЗ
	|	ОжидаемыеПартии КАК ОжидаемыеПартии
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОформленныеТовары КАК ОформленныеТовары
	|		ПО ОжидаемыеПартии.АктОтбораПроб = ОформленныеТовары.АктОтбораПроб
	|ГДЕ
	|	ОжидаемыеПартии.КоличествоЗЕРНО > ЕСТЬNULL(ОформленныеТовары.КоличествоЗЕРНО, 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПотребительскиеСвойства.Ссылка   КАК РезультатИсследования,
	|	ПотребительскиеСвойства.ПотребительскоеСвойство КАК ПотребительскоеСвойство,
	|	ПотребительскиеСвойства.Значение КАК Значение
	|ИЗ
	|	ОжидаемыеПартии КАК ОжидаемыеПартии
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РезультатыИсследованийЗЕРНО.ПотребительскиеСвойства КАК ПотребительскиеСвойства
	|		ПО ПотребительскиеСвойства.Ссылка = ОжидаемыеПартии.РезультатИсследования";
	
	Пакет = Запрос.ВыполнитьПакет();
	ПотребительскиеСвойстваПоРезультатамГМ = Пакет[Пакет.ВГраница()].Выгрузить();
	ПотребительскиеСвойстваПоРезультатамГМ.Индексы.Добавить("РезультатИсследования, ПотребительскоеСвойство");
	Выборка = Пакет[Пакет.ВГраница()-1].Выбрать();
	
	Если Выборка.Количество() = 0 Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru='В %1 отсутствует продукция для заполнения.'"), ДанныеЗаполнения);
	КонецЕсли;
	
	КодыОКПД2 = Пакет[Пакет.ВГраница()-2].Выгрузить().ВыгрузитьКолонку("ОКПД2");
	ПотребительскиеСвойстваПоОКПД2 = ИнтеграцияЗЕРНО.ПотребительскиеСвойстваПродукцииПоДаннымОКПД2(
		КодыОКПД2, НазначениеПартии);
	
	ЗаполнитьПотребительскиеСвойства = ПотребительскиеСвойстваПоОКПД2 <> Неопределено;
	
	ЗаполнитьТНВЭД = Ложь;
	Если НазначениеПартии = Справочники.КлассификаторНСИЗЕРНО.НазначениеПартииВывозСТерриторииРФ Тогда
		ТаблицаОКПД2ТНВЭД = РегистрыСведений.ВидыСельскохозяйственныхКультурЗЕРНО.ДанныеТНВЭДПоОКПД2(КодыОКПД2);
		ЗаполнитьТНВЭД = Истина;
	КонецЕсли;
	
	Товары.Очистить();
	ПотребительскиеСвойства.Очистить();
	
	Отбор                        = Новый Структура("РезультатИсследования, ПотребительскоеСвойство"); 
	ОтборПотребительскиеСвойства = Новый Структура("ОКПД2");
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Шапка);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.Идентификатор = Новый УникальныйИдентификатор;
		
		Если ЗаполнитьПотребительскиеСвойства И ЗначениеЗаполнено(НоваяСтрока.РезультатИсследования) Тогда
			
			Отбор.РезультатИсследования = НоваяСтрока.РезультатИсследования;
			
			ОтборПотребительскиеСвойства.ОКПД2 = НоваяСтрока.ОКПД2;
			СвойстваПродукцииПоОКПД2 = ПотребительскиеСвойстваПоОКПД2.НайтиСтроки(ОтборПотребительскиеСвойства);
			Для Каждого СвойствоПродукции Из СвойстваПродукцииПоОКПД2 Цикл
				
				НовоеСвойство = ПотребительскиеСвойства.Добавить();
				НовоеСвойство.ИдентификаторСтрокиТоваров = НоваяСтрока.Идентификатор;
				ЗаполнитьЗначенияСвойств(НовоеСвойство, СвойствоПродукции);
				
				Отбор.ПотребительскоеСвойство = НовоеСвойство.ПотребительскоеСвойство;
				СвойстваПродукцииПоРезультатамГМ = ПотребительскиеСвойстваПоРезультатамГМ.НайтиСтроки(Отбор);
				
				Для Каждого СвойствоПродукцииГМ Из СвойстваПродукцииПоРезультатамГМ Цикл
					НовоеСвойство.Значение = СвойствоПродукцииГМ.Значение;
				КонецЦикла;
				
			КонецЦикла;
			
		ИначеЕсли ЗаполнитьПотребительскиеСвойства Тогда
			
			ОтборПотребительскиеСвойства.ОКПД2 = НоваяСтрока.ОКПД2;
			СвойстваПродукцииПоОКПД2 = ПотребительскиеСвойстваПоОКПД2.НайтиСтроки(ОтборПотребительскиеСвойства);
			Для Каждого СвойствоПродукции Из СвойстваПродукцииПоОКПД2 Цикл
				
				НовоеСвойство = ПотребительскиеСвойства.Добавить();
				НовоеСвойство.ИдентификаторСтрокиТоваров = НоваяСтрока.Идентификатор;
				ЗаполнитьЗначенияСвойств(НовоеСвойство, СвойствоПродукции);
				
			КонецЦикла;
				
		КонецЕсли;
		
		Если ЗаполнитьТНВЭД Тогда
			ОтборПотребительскиеСвойства.ОКПД2 = НоваяСтрока.ОКПД2;
			ДанныеОКПД2ТНВЭД = ТаблицаОКПД2ТНВЭД.НайтиСтроки(ОтборПотребительскиеСвойства);
			Если ДанныеОКПД2ТНВЭД.Количество() Тогда
				НоваяСтрока.КодТНВЭД = ДанныеОКПД2ТНВЭД[0].КодТНВЭД;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПоВнесениюСведенийОСобранномУрожаеБезГосмониторинга(Шапка, Запрос, ДанныеЗаполнения)
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.ОКПД2,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Номенклатура,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Характеристика,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Серия,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.СтатусУказанияСерий,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Количество,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.КоличествоЗЕРНО,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Ссылка.СкладКонтрагент КАК СкладКонтрагент,
	|	СтатусыОбъектовСинхронизацииЗЕРНО.ИдентификаторЗаявки КАК ИдентификаторСведенийОСобранномУрожае
	|ПОМЕСТИТЬ ОжидаемыеПартии
	|ИЗ
	|	Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО.Товары КАК ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОбъектовСинхронизацииЗЕРНО КАК СтатусыОбъектовСинхронизацииЗЕРНО
	|		ПО ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Ссылка = СтатусыОбъектовСинхронизацииЗЕРНО.ОбъектСинхронизации
	|			И ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Идентификатор = СтатусыОбъектовСинхронизацииЗЕРНО.ИдентификаторСтроки
	|ГДЕ
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Ссылка = &ДокументОснование
	|	И СтатусыОбъектовСинхронизацииЗЕРНО.ИдентификаторСтроки <> """"
	|	И Не ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.ОКПД2 В (&ПодлежитГосмониторингу)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФормированиеПартийЗЕРНО.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ФормированиеПартийЗЕРНО КАК ФормированиеПартийЗЕРНО
	|ГДЕ
	|	ФормированиеПартийЗЕРНО.Ссылка <> &Ссылка
	|	И ФормированиеПартийЗЕРНО.Проведен
	|	И ФормированиеПартийЗЕРНО.ДокументОснование = &ДокументОснование
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ФормированиеПартийЗЕРНОТовары.Количество) КАК Количество,
	|	СУММА(ФормированиеПартийЗЕРНОТовары.КоличествоЗЕРНО) КАК КоличествоЗЕРНО,
	|	ФормированиеПартийЗЕРНОТовары.ИдентификаторСведенийОСобранномУрожае КАК ИдентификаторСведенийОСобранномУрожае
	|ПОМЕСТИТЬ ОформленныеТовары
	|ИЗ
	|	Документ.ФормированиеПартийЗЕРНО.Товары КАК ФормированиеПартийЗЕРНОТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОформленныеДокументы КАК ОформленныеДокументы
	|		ПО ФормированиеПартийЗЕРНОТовары.Ссылка = ОформленныеДокументы.Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ФормированиеПартийЗЕРНОТовары.ИдентификаторСведенийОСобранномУрожае
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОжидаемыеПартии.ОКПД2 КАК ОКПД2
	|ИЗ
	|	ОжидаемыеПартии КАК ОжидаемыеПартии
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОформленныеТовары КАК ОформленныеТовары
	|		ПО ОжидаемыеПартии.ИдентификаторСведенийОСобранномУрожае = ОформленныеТовары.ИдентификаторСведенийОСобранномУрожае
	|ГДЕ
	|	ОжидаемыеПартии.КоличествоЗЕРНО > ЕСТЬNULL(ОформленныеТовары.КоличествоЗЕРНО, 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОжидаемыеПартии.ОКПД2 КАК ОКПД2,
	|	ОжидаемыеПартии.Номенклатура КАК Номенклатура,
	|	ОжидаемыеПартии.Характеристика КАК Характеристика,
	|	ОжидаемыеПартии.Серия КАК Серия,
	|	ОжидаемыеПартии.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ОжидаемыеПартии.СкладКонтрагент КАК СкладКонтрагент,
	|	ОжидаемыеПартии.ИдентификаторСведенийОСобранномУрожае КАК ИдентификаторСведенийОСобранномУрожае,
	|	ОжидаемыеПартии.Количество - ЕСТЬNULL(ОформленныеТовары.Количество, 0) КАК Количество,
	|	ОжидаемыеПартии.КоличествоЗЕРНО - ЕСТЬNULL(ОформленныеТовары.КоличествоЗЕРНО, 0) КАК КоличествоЗЕРНО
	|ИЗ
	|	ОжидаемыеПартии КАК ОжидаемыеПартии
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОформленныеТовары КАК ОформленныеТовары
	|		ПО ОжидаемыеПартии.ИдентификаторСведенийОСобранномУрожае = ОформленныеТовары.ИдентификаторСведенийОСобранномУрожае
	|ГДЕ
	|	ОжидаемыеПартии.КоличествоЗЕРНО > ЕСТЬNULL(ОформленныеТовары.КоличествоЗЕРНО, 0)
	|";
	
	Пакет = Запрос.ВыполнитьПакет();
	Выборка = Пакет[Пакет.ВГраница()].Выбрать();
	
	Если Выборка.Количество() = 0 Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru='В %1 отсутствует продукция для заполнения.'"), ДанныеЗаполнения);
	КонецЕсли;
	
	КодыОКПД2 = Пакет[Пакет.ВГраница()-1].Выгрузить().ВыгрузитьКолонку("ОКПД2");
	ПотребительскиеСвойстваПоОКПД2 = ИнтеграцияЗЕРНО.ПотребительскиеСвойстваПродукцииПоДаннымОКПД2(
		КодыОКПД2, НазначениеПартии);
	
	ЗаполнитьПотребительскиеСвойства = ПотребительскиеСвойстваПоОКПД2 <> Неопределено;
	
	ЗаполнитьТНВЭД = Ложь;
	Если НазначениеПартии = Справочники.КлассификаторНСИЗЕРНО.НазначениеПартииВывозСТерриторииРФ Тогда
		ТаблицаОКПД2ТНВЭД = РегистрыСведений.ВидыСельскохозяйственныхКультурЗЕРНО.ДанныеТНВЭДПоОКПД2(КодыОКПД2);
		ЗаполнитьТНВЭД = Истина;
	КонецЕсли;
	
	Товары.Очистить();
	ПотребительскиеСвойства.Очистить();
	
	ОтборПотребительскиеСвойства = Новый Структура("ОКПД2");
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Шапка);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.Идентификатор = Новый УникальныйИдентификатор;
		
		Если ЗаполнитьТНВЭД Тогда
			ОтборПотребительскиеСвойства.ОКПД2 = НоваяСтрока.ОКПД2;
			ДанныеОКПД2ТНВЭД = ТаблицаОКПД2ТНВЭД.НайтиСтроки(ОтборПотребительскиеСвойства);
			Если ДанныеОКПД2ТНВЭД.Количество() Тогда
				НоваяСтрока.КодТНВЭД = ДанныеОКПД2ТНВЭД[0].КодТНВЭД;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗаполнитьПотребительскиеСвойства Тогда
			
			ОтборПотребительскиеСвойства.ОКПД2 = НоваяСтрока.ОКПД2;
			СвойстваПродукцииПоОКПД2 = ПотребительскиеСвойстваПоОКПД2.НайтиСтроки(ОтборПотребительскиеСвойства);
			Для Каждого СвойствоПродукции Из СвойстваПродукцииПоОКПД2 Цикл
				
				НовоеСвойство = ПотребительскиеСвойства.Добавить();
				НовоеСвойство.ИдентификаторСтрокиТоваров = НоваяСтрока.Идентификатор;
				ЗаполнитьЗначенияСвойств(НовоеСвойство, СвойствоПродукции);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьОбъектПоСтатистике()
	
	ДанныеСтатистики = ЗаполнениеОбъектовПоСтатистикеЗЕРНО.ДанныеЗаполненияФормированиеПартийЗЕРНО(Организация);
	
	Для Каждого КлючИЗначение Из ДанныеСтатистики Цикл
		ЗаполнениеОбъектовПоСтатистикеЗЕРНО.ЗаполнитьПустойРеквизит(ЭтотОбъект, ДанныеСтатистики, КлючИЗначение.Ключ);
	КонецЦикла;
	
	ПроверитьАктуальностьВладельцаПартии();
	ПроверитьАктуальностьОперации();
	
КонецПроцедуры 

Процедура ПроверитьАктуальностьВладельцаПартии()
	
	Если ВидПродукции = Перечисления.ВидыПродукцииИС.Зерно Тогда
		Если Не ПоставитьПартиюНаХранение Тогда
			ВладелецПартии = Неопределено;
			ПодразделениеВладельцаПартии = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьАктуальностьОперации()
	
	ВидыОперацийПоВидуПродукцииИС = Документы.ФормированиеПартийЗЕРНО.ДоступныеВидыОперацийПоВидуПродукцииИС();
	ВидыОперацийПоВидуПродукции   = ВидыОперацийПоВидуПродукцииИС.Получить(ВидПродукции);
	
	Если ВидыОперацийПоВидуПродукции <> Неопределено
		И ВидыОперацийПоВидуПродукции.Найти(Операция) = Неопределено Тогда
		Операция = ВидыОперацийПоВидуПродукции[0];
	КонецЕсли;
	
КонецПроцедуры

Функция ЭтоГосмониторинг(ДанныеЗаполнения)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.АктыОтбораПробЗЕРНО") Тогда
		Возврат Истина;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.РезультатыИсследованийЗЕРНО") Тогда
		Возврат Истина;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Массив") И ДанныеЗаполнения.Количество() Тогда
		Возврат ЭтоГосмониторинг(ДанныеЗаполнения[0]);
	КонецЕсли;
	Возврат Ложь;
КонецФункции

Процедура ЗаполнитьПоДаннымГосмониторинга(ДанныеЗаполнения)
	
	Если ТипЗнч(ДанныеЗаполнения) <> Тип("Массив") Тогда
		ДанныеЗаполнения = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеЗаполнения);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("КонечныеСтатусы", Документы.ФормированиеПартийЗЕРНО.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ДокументыГосмониторинга", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("МестоФормированияПартии", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения[0], "МестоФормированияПартии"));
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНО.ДатаСбораУрожая КАК ДатаСбораУрожая,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНО.Организация КАК Организация,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНО.Подразделение КАК Подразделение,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНО.СкладКонтрагент КАК СкладКонтрагент,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНО.МестоХранения КАК Местоположение,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНО.МестоХраненияСтрокой КАК МестоположениеСтрокой
	|ИЗ Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО.Товары КАК ТаблицаТовары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО КАК ВнесениеСведенийОСобранномУрожаеЗЕРНО
	|		ПО ВнесениеСведенийОСобранномУрожаеЗЕРНО.Ссылка = ТаблицаТовары.Ссылка
	|ГДЕ
	|	ТаблицаТовары.МестоФормированияПартии = &МестоФормированияПартии";
	Шапка = Запрос.Выполнить().Выбрать();
	
	ВидПродукции     = Перечисления.ВидыПродукцииИС.Зерно;
	Операция         = Перечисления.ВидыОперацийЗЕРНО.ФормированиеПартииПоРезультатамГосмониторинга;
	НазначениеПартии = Справочники.КлассификаторНСИЗЕРНО.НазначениеПартииХранениеОбработка;
	Если Шапка.Следующий() Тогда
		ГодУрожая         = Год(Шапка.ДатаСбораУрожая);
		Организация       = Шапка.Организация;
		Подразделение     = Шапка.Подразделение;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РезультатыИсследованийЗЕРНО.Ссылка КАК Ссылка,
	|	РезультатыИсследованийЗЕРНО.Количество КАК Количество,
	|	РезультатыИсследованийЗЕРНО.ОКПД2 КАК ОКПД2,
	|	РезультатыИсследованийЗЕРНО.МестоФормированияПартии КАК МестоФормированияПартии
	|ПОМЕСТИТЬ ДанныеИсследований
	|ИЗ
	|	Справочник.РезультатыИсследованийЗЕРНО КАК РезультатыИсследованийЗЕРНО
	|ГДЕ
	|	РезультатыИсследованийЗЕРНО.Ссылка В(&ДокументыГосмониторинга)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АктыОтбораПробЗЕРНО.Ссылка,
	|	ВЫБОР
	|		КОГДА АктыОтбораПробЗЕРНО.МестоФормированияПартии.МассаНакопленная = 0
	|			ТОГДА СУММА(ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Количество)
	|		ИНАЧЕ АктыОтбораПробЗЕРНО.МестоФормированияПартии.МассаНакопленная
	|	КОНЕЦ,
	|	АктыОтбораПробЗЕРНО.ОКПД2,
	|	АктыОтбораПробЗЕРНО.МестоФормированияПартии
	|ИЗ
	|	Справочник.АктыОтбораПробЗЕРНО КАК АктыОтбораПробЗЕРНО
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО.Товары КАК ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары
	|		ПО АктыОтбораПробЗЕРНО.МестоФормированияПартии.МассаНакопленная = 0
	|		И ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.МестоФормированияПартии = АктыОтбораПробЗЕРНО.МестоФормированияПартии
	|		И ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Ссылка.Проведен
	|ГДЕ
	|	АктыОтбораПробЗЕРНО.Ссылка В(&ДокументыГосмониторинга)
	|СГРУППИРОВАТЬ ПО
	|	АктыОтбораПробЗЕРНО.Ссылка,
	|	АктыОтбораПробЗЕРНО.МестоФормированияПартии.МассаНакопленная,
	|	АктыОтбораПробЗЕРНО.ОКПД2,
	|	АктыОтбораПробЗЕРНО.МестоФормированияПартии
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	МестоФормированияПартии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФормированиеПартийЗЕРНОТовары.ОКПД2 КАК ОКПД2,
	|	ЕСТЬNULL(ФормированиеПартийЗЕРНОТовары.РезультатИсследования.МестоФормированияПартии, ФормированиеПартийЗЕРНОТовары.АктОтбораПроб.МестоФормированияПартии) КАК МестоФормированияПартии,
	|	СУММА(ФормированиеПартийЗЕРНОТовары.КоличествоЗЕРНО) КАК Количество
	|ПОМЕСТИТЬ ОформленныеПартии
	|ИЗ
	|	Документ.ФормированиеПартийЗЕРНО.Товары КАК ФормированиеПартийЗЕРНОТовары
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОбъектовСинхронизацииЗЕРНО КАК Статусы
	|		ПО Статусы.ОбъектСинхронизации = ФормированиеПартийЗЕРНОТовары.Ссылка
	|		И Статусы.ИдентификаторСтроки = """"
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОбъектовСинхронизацииЗЕРНО КАК СтатусыСтрок
	|		ПО СтатусыСтрок.ОбъектСинхронизации = ФормированиеПартийЗЕРНОТовары.Ссылка
	|		И СтатусыСтрок.ИдентификаторСтроки = ФормированиеПартийЗЕРНОТовары.Идентификатор
	|ГДЕ
	|	ЕСТЬNULL(ФормированиеПартийЗЕРНОТовары.РезультатИсследования.МестоФормированияПартии, ФормированиеПартийЗЕРНОТовары.АктОтбораПроб.МестоФормированияПартии) В
	|			(ВЫБРАТЬ
	|				ДанныеИсследований.МестоФормированияПартии
	|			ИЗ
	|				ДанныеИсследований)
	|	И ФормированиеПартийЗЕРНОТовары.Ссылка <> &Ссылка
	|   И ФормированиеПартийЗЕРНОТовары.Ссылка.Проведен
	|	И (НЕ Статусы.Статус В (&КонечныеСтатусы))
	|	И (НЕ ЕСТЬNULL(СтатусыСтрок.Статус, """") В (&КонечныеСтатусы))
	|СГРУППИРОВАТЬ ПО
	|	ФормированиеПартийЗЕРНОТовары.ОКПД2,
	|	ЕСТЬNULL(ФормированиеПартийЗЕРНОТовары.РезультатИсследования.МестоФормированияПартии, ФормированиеПартийЗЕРНОТовары.АктОтбораПроб.МестоФормированияПартии)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.МестоФормированияПартии КАК МестоФормированияПартии,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.ОКПД2 КАК ОКПД2,
	|	МАКСИМУМ(ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Ссылка) КАК ВнесениеСведенийОСобранномУрожае
	|ПОМЕСТИТЬ ДокументыСбораУрожая
	|ИЗ
	|	Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО.Товары КАК ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары
	|ГДЕ
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Ссылка.Проведен
	|	И ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.МестоФормированияПартии В
	|			(ВЫБРАТЬ
	|				ДанныеИсследований.МестоФормированияПартии
	|			ИЗ
	|				ДанныеИсследований)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.МестоФормированияПартии,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.ОКПД2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыСбораУрожая.МестоФормированияПартии КАК МестоФормированияПартии,
	|	ДокументыСбораУрожая.ОКПД2 КАК ОКПД2,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Номенклатура КАК Номенклатура,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Характеристика КАК Характеристика,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Серия КАК Серия,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Количество / ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.КоличествоЗЕРНО КАК Коэффициент,
	|	ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Ссылка.СкладКонтрагент КАК СкладКонтрагент
	|ПОМЕСТИТЬ СведенияОНоменклатуре
	|ИЗ
	|	ДокументыСбораУрожая КАК ДокументыСбораУрожая
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО.Товары КАК ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары
	|		ПО ДокументыСбораУрожая.ВнесениеСведенийОСобранномУрожае = ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.Ссылка
	|			И ДокументыСбораУрожая.МестоФормированияПартии = ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.МестоФормированияПартии
	|			И ДокументыСбораУрожая.ОКПД2 = ВнесениеСведенийОСобранномУрожаеЗЕРНОТовары.ОКПД2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОжидаемыеПартии.ОКПД2 КАК ОКПД2
	|ИЗ
	|	ДанныеИсследований КАК ОжидаемыеПартии
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОформленныеПартии КАК ОформленныеТовары
	|		ПО ОжидаемыеПартии.МестоФормированияПартии = ОформленныеТовары.МестоФормированияПартии
	|ГДЕ
	|	ОжидаемыеПартии.Количество > ЕСТЬNULL(ОформленныеТовары.Количество, 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеИсследований.ОКПД2 КАК ОКПД2,
	|	ДанныеИсследований.Ссылка КАК РезультатИсследования,
	|	ВЫБОР КОГДА ДанныеИсследований.Ссылка ССЫЛКА Справочник.АктыОтбораПробЗЕРНО ТОГДА
	|		ДанныеИсследований.Ссылка
	|	ИНАЧЕ
	|		ДанныеИсследований.Ссылка.АктОтбораПроб
	|	КОНЕЦ КАК АктОтбораПроб,
	|	ДанныеИсследований.Количество - ЕСТЬNULL(ОформленныеПартии.Количество, 0) КАК КоличествоЗЕРНО,
	|	(ДанныеИсследований.Количество - ЕСТЬNULL(ОформленныеПартии.Количество, 0)) * СведенияОНоменклатуре.Коэффициент КАК Количество,
	|	СведенияОНоменклатуре.Номенклатура КАК Номенклатура,
	|	СведенияОНоменклатуре.Характеристика КАК Характеристика,
	|	СведенияОНоменклатуре.Серия КАК Серия,
	|	СведенияОНоменклатуре.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	СведенияОНоменклатуре.СкладКонтрагент КАК СкладКонтрагент
	|ИЗ
	|	ДанныеИсследований КАК ДанныеИсследований
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СведенияОНоменклатуре КАК СведенияОНоменклатуре
	|		ПО (СведенияОНоменклатуре.ОКПД2 = ДанныеИсследований.ОКПД2)
	|			И (СведенияОНоменклатуре.МестоФормированияПартии = ДанныеИсследований.МестоФормированияПартии)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОформленныеПартии КАК ОформленныеПартии
	|		ПО (ОформленныеПартии.ОКПД2 = ДанныеИсследований.ОКПД2)
	|			И (ОформленныеПартии.МестоФормированияПартии = ДанныеИсследований.МестоФормированияПартии)
	|ГДЕ
	|	ДанныеИсследований.Количество > ЕСТЬNULL(ОформленныеПартии.Количество, 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПотребительскиеСвойства.Ссылка   КАК РезультатИсследования,
	|	ПотребительскиеСвойства.ПотребительскоеСвойство КАК ПотребительскоеСвойство,
	|	ПотребительскиеСвойства.Значение КАК Значение
	|ИЗ
	|	ДанныеИсследований КАК ОжидаемыеПартии
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РезультатыИсследованийЗЕРНО.ПотребительскиеСвойства КАК ПотребительскиеСвойства
	|		ПО ПотребительскиеСвойства.Ссылка = ОжидаемыеПартии.Ссылка";
	
	Пакет = Запрос.ВыполнитьПакет();
	ПотребительскиеСвойстваПоРезультатамГМ = Пакет[Пакет.ВГраница()].Выгрузить();
	ПотребительскиеСвойстваПоРезультатамГМ.Индексы.Добавить("РезультатИсследования, ПотребительскоеСвойство");
	Выборка = Пакет[Пакет.ВГраница()-1].Выбрать();
	
	Если Выборка.Количество() = 0 Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru='В %1 отсутствует продукция для заполнения.'"), ДанныеЗаполнения);
	КонецЕсли;
	
	КодыОКПД2 = Пакет[Пакет.ВГраница()-2].Выгрузить().ВыгрузитьКолонку("ОКПД2");
	ПотребительскиеСвойстваПоОКПД2 = ИнтеграцияЗЕРНО.ПотребительскиеСвойстваПродукцииПоДаннымОКПД2(
		КодыОКПД2, НазначениеПартии);
	
	ЗаполнитьПотребительскиеСвойства = ПотребительскиеСвойстваПоОКПД2 <> Неопределено;
	
	ЗаполнитьТНВЭД = Ложь;
	Если НазначениеПартии = Справочники.КлассификаторНСИЗЕРНО.НазначениеПартииВывозСТерриторииРФ Тогда
		ТаблицаОКПД2ТНВЭД = РегистрыСведений.ВидыСельскохозяйственныхКультурЗЕРНО.ДанныеТНВЭДПоОКПД2(КодыОКПД2);
		ЗаполнитьТНВЭД = Истина;
	КонецЕсли;
	
	Товары.Очистить();
	ПотребительскиеСвойства.Очистить();
	
	Отбор                        = Новый Структура("РезультатИсследования, ПотребительскоеСвойство"); 
	ОтборПотребительскиеСвойства = Новый Структура("ОКПД2");
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Шапка);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.Идентификатор = Новый УникальныйИдентификатор;
		
		Если ЗаполнитьПотребительскиеСвойства И ЗначениеЗаполнено(НоваяСтрока.РезультатИсследования) Тогда
			
			Отбор.РезультатИсследования = НоваяСтрока.РезультатИсследования;
			
			ОтборПотребительскиеСвойства.ОКПД2 = НоваяСтрока.ОКПД2;
			СвойстваПродукцииПоОКПД2 = ПотребительскиеСвойстваПоОКПД2.НайтиСтроки(ОтборПотребительскиеСвойства);
			Для Каждого СвойствоПродукции Из СвойстваПродукцииПоОКПД2 Цикл
				
				НовоеСвойство = ПотребительскиеСвойства.Добавить();
				НовоеСвойство.ИдентификаторСтрокиТоваров = НоваяСтрока.Идентификатор;
				ЗаполнитьЗначенияСвойств(НовоеСвойство, СвойствоПродукции);
				
				Отбор.ПотребительскоеСвойство = НовоеСвойство.ПотребительскоеСвойство;
				СвойстваПродукцииПоРезультатамГМ = ПотребительскиеСвойстваПоРезультатамГМ.НайтиСтроки(Отбор);
				
				Для Каждого СвойствоПродукцииГМ Из СвойстваПродукцииПоРезультатамГМ Цикл
					НовоеСвойство.Значение = СвойствоПродукцииГМ.Значение;
				КонецЦикла;
				
			КонецЦикла;
			
		ИначеЕсли ЗаполнитьПотребительскиеСвойства Тогда
			
			ОтборПотребительскиеСвойства.ОКПД2 = НоваяСтрока.ОКПД2;
			СвойстваПродукцииПоОКПД2 = ПотребительскиеСвойстваПоОКПД2.НайтиСтроки(ОтборПотребительскиеСвойства);
			Для Каждого СвойствоПродукции Из СвойстваПродукцииПоОКПД2 Цикл
				
				НовоеСвойство = ПотребительскиеСвойства.Добавить();
				НовоеСвойство.ИдентификаторСтрокиТоваров = НоваяСтрока.Идентификатор;
				ЗаполнитьЗначенияСвойств(НовоеСвойство, СвойствоПродукции);
				
			КонецЦикла;
				
		КонецЕсли;
		
		Если ЗаполнитьТНВЭД Тогда
			ОтборПотребительскиеСвойства.ОКПД2 = НоваяСтрока.ОКПД2;
			ДанныеОКПД2ТНВЭД = ТаблицаОКПД2ТНВЭД.НайтиСтроки(ОтборПотребительскиеСвойства);
			Если ДанныеОКПД2ТНВЭД.Количество() Тогда
				НоваяСтрока.КодТНВЭД = ДанныеОКПД2ТНВЭД[0].КодТНВЭД;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

Функция ПустыеПотребительскиеСвойстваПродукцииПоДокументу()
	
	ТаблицаТовары = Товары.Выгрузить(, "НомерСтроки, Идентификатор, ОКПД2");
	ТаблицаПотребительскиеСвойства = ПотребительскиеСвойства.Выгрузить(, "ПотребительскоеСвойство, Значение, ИдентификаторСтрокиТоваров");
	
	НазначениеПотребительскогоСвойства = ИнтеграцияЗЕРНО.НазначениеПотребительскогоСвойстваДаннымПартии(НазначениеПартии);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Идентификатор КАК Идентификатор,
	|	Товары.ОКПД2 КАК ОКПД2
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|/////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПотребительскиеСвойства.ПотребительскоеСвойство КАК ПотребительскоеСвойство,
	|	ПотребительскиеСвойства.Значение КАК Значение,
	|	ПотребительскиеСвойства.ИдентификаторСтрокиТоваров КАК ИдентификаторСтрокиТоваров
	|ПОМЕСТИТЬ ВТ_ПотребительскиеСвойства
	|ИЗ
	|	&ПотребительскиеСвойства КАК ПотребительскиеСвойства
	|;
	|/////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	ПРЕДСТАВЛЕНИЕ(Товары.ОКПД2) КАК ТоварыОКПД2,
	|	ПРЕДСТАВЛЕНИЕ(ПотребительскиеСвойства.ПотребительскоеСвойство) КАК ПотребительскоеСвойство
	|ИЗ
	|	ВТ_Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПотребительскиеСвойства КАК ПотребительскиеСвойства
	|		ПО Товары.Идентификатор = ПотребительскиеСвойства.ИдентификаторСтрокиТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДопустимыеЗначенияПотребительскихСвойствЗЕРНО КАК
	|			ДопустимыеЗначенияПотребительскихСвойствЗЕРНО
	|		ПО (ПотребительскиеСвойства.ПотребительскоеСвойство = ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.ПотребительскоеСвойство)
	|		И Товары.ОКПД2 = ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.ОКПД2
	|		И (ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.Назначение = &Назначение)
	|		И (ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.ДействуетПо >= &Дата
	|		ИЛИ ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.ДействуетПо = ДАТАВРЕМЯ(1, 1, 1))
	|		И (ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.КодСтраны = &КодСтраны)
	|		И (ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.ТипЗначения = ЗНАЧЕНИЕ(Перечисление.ТипыЗначенияПотребительскогоСвойстваЗЕРНО.Число)
	|			И (ПотребительскиеСвойства.Значение < ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.ДиапазонС
	|				ИЛИ ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.ДиапазонПо > 0
	|				И ПотребительскиеСвойства.Значение > ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.ДиапазонПо)
	|			ИЛИ ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.ТипЗначения = ЗНАЧЕНИЕ(Перечисление.ТипыЗначенияПотребительскогоСвойстваЗЕРНО.Перечисление)
	|				И ПотребительскиеСвойства.Значение = """"
	|				И ВЫРАЗИТЬ(ДопустимыеЗначенияПотребительскихСвойствЗЕРНО.ДопустимыеЗначения КАК СТРОКА(1)) <> """")";
	
	Запрос.УстановитьПараметр("Товары",                  ТаблицаТовары);
	Запрос.УстановитьПараметр("ПотребительскиеСвойства", ТаблицаПотребительскиеСвойства);
	
	Запрос.УстановитьПараметр("Назначение", НазначениеПотребительскогоСвойства);
	Запрос.УстановитьПараметр("Дата",       ТекущаяУниверсальнаяДата());
	Запрос.УстановитьПараметр("КодСтраны", "RU");
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ТаблицаОшибокПриГосмониторинге()
	
	ТаблицаТовары = Товары.Выгрузить(, "НомерСтроки, ОКПД2, РезультатИсследования, АктОтбораПроб");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.НомерСтроки           КАК НомерСтроки,
	|	Товары.ОКПД2                 КАК ОКПД2,
	|	Товары.РезультатИсследования КАК РезультатИсследования,
	|	Товары.АктОтбораПроб         КАК АктОтбораПроб
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	&Товары КАК Товары
	|ИНДЕКСИРОВАТЬ ПО
	|	Товары.ОКПД2
	|;
	|/////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки          КАК НомерСтроки,
	|	ПРЕДСТАВЛЕНИЕ(Товары.ОКПД2) КАК ТоварыОКПД2,
	|	0                           КАК КатегорияОшибки
	|ИЗ
	|	ВТ_Товары КАК Товары
	|ГДЕ
	|	НЕ Товары.ОКПД2 В (&ОКПД2ПодлежащиеГосмониторингу)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	ПРЕДСТАВЛЕНИЕ(Товары.ОКПД2) КАК ТоварыОКПД2,
	|	1 КАК КатегорияОшибки
	|ИЗ
	|	ВТ_Товары КАК Товары
	|ГДЕ
	|	Товары.ОКПД2 В (&ОКПД2ПодлежащиеГосмониторингу)
	|	И Товары.РезультатИсследования = &ПустойРезультатИсследования
	|	И Товары.АктОтбораПроб = &ПустойАктОтбораПроб";
	
	Запрос.УстановитьПараметр("Товары",                        ТаблицаТовары);
	Запрос.УстановитьПараметр("ОКПД2ПодлежащиеГосмониторингу", ИнтеграцияЗЕРНО.ОКПД2ПодлежащиеГосмониторингу());
	Запрос.УстановитьПараметр("ПустойРезультатИсследования",   Справочники.РезультатыИсследованийЗЕРНО.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустойАктОтбораПроб",           Справочники.АктыОтбораПробЗЕРНО.ПустаяСсылка());
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#КонецЕсли
