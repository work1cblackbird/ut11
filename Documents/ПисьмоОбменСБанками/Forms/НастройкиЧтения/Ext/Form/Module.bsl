
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВосстановитьНастройкиЧтения();
	УстановитьДоступностьЭлементов();    
	
КонецПроцедуры  

#КонецОбласти     

#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура ОтменаЗакрытьФорму(Команда)   
	
	Если Модифицированность Тогда		
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект, Параметры);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Сохранить изменения?'"), Режим, 0);
	Иначе	
		Закрыть();  
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройки(Команда)
	СохранитьНастройкиЧтения(); 
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура РучноеПрочтениеПриИзменении(Элемент)

	Модифицированность = Истина;  
	УстановитьДоступностьЭлементов();

КонецПроцедуры

&НаКлиенте
Процедура ЗадержкаЧтенияПриИзменении(Элемент) 
	
	Модифицированность = Истина; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьПредпросмотрПриИзменении(Элемент) 
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		СохранитьНастройкиЧтения();   	
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементов()
	
	Если РучноеПрочтение Тогда
		ЗадержкаЧтения = Ложь;
		Элементы.ГруппаЗадержка.Доступность = Ложь;
	Иначе
		Элементы.ГруппаЗадержка.Доступность = Истина;
	КонецЕсли;    	
	
КонецПроцедуры 

&НаСервере
Процедура ВосстановитьНастройкиЧтения()   
	
	НастройкиЧтения = ОбменСБанкамиСлужебный.НастройкиЧтенияФормыСписка();   
	ЗаполнитьЗначенияСвойств(ЭтотОбъект,НастройкиЧтения);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиЧтения()
	
	НастройкиЧтенияФормыСписка = ОбменСБанкамиСлужебный.НастройкиЧтенияФормыСпискаПоУмолчанию(); 
	ЗаполнитьЗначенияСвойств(НастройкиЧтенияФормыСписка,ЭтотОбъект);  
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("СохраненныеНастройкиДиректБанк",
		"НастройкиЧтенияПисемДиректБанк",НастройкиЧтенияФормыСписка);    
	
КонецПроцедуры     
    
#КонецОбласти



