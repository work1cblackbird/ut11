#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ИнтеграцияИСМП.ЗапрещеноИспользованиеОбъектаВИСМП(Объект.Ссылка) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ИнтеграцияИС.НастроитьВидимостьДокументаОснования(ЭтотОбъект);
	Элементы.ДокументОснование.ДоступныеТипы = Метаданные.ОпределяемыеТипы.ОснованиеПеремаркировкиТоваровИСМП.Тип;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыИС.ПриСозданииНаСервере(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВерсионированиеОбъектов") Тогда
		МодульВерсионированиеОбъектов = ОбщегоНазначения.ОбщийМодуль("ВерсионированиеОбъектов");
		МодульВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ДоступныеВидыПродукцииИС = ОбщегоНазначенияИСКлиентСервер.ВидыПродукцииИСМП(Ложь, Ложь, Ложь);
	ОткорректироватьДоступныеВидыПродукции(ДоступныеВидыПродукцииИС);
	СобытияФормИСМП.НастроитьВидПродукцииПриСозданииНаСервере(ЭтотОбъект, ДоступныеВидыПродукцииИС);
	
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСНоменклатурой(
		ЭтотОбъект, "ТоварыХарактеристика");
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСНоменклатурой(
		ЭтотОбъект, "ТоварыНоваяХарактеристика", "Элементы.Товары.ТекущиеДанные.НоваяНоменклатура");
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСНоменклатурой(
		ЭтотОбъект, "ТоварыМарка", "Элементы.Товары.ТекущиеДанные.Номенклатура");
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСНоменклатурой(
		ЭтотОбъект, "ТоварыНоваяМарка", "Элементы.Товары.ТекущиеДанные.НоваяНоменклатура");
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСХарактеристикой(
		ЭтотОбъект, "ТоварыМарка", "Элементы.Товары.ТекущиеДанные.Характеристика");
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСХарактеристикой(
		ЭтотОбъект, "ТоварыНоваяМарка", "Элементы.Товары.ТекущиеДанные.НоваяХарактеристика");
	
	Если Объект.Ссылка.Пустая() Тогда
		ПриСозданииЧтенииНаСервере();
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
	ПоддерживаемыеТипыПодключаемогоОборудования = "СканерШтрихкода";
	ИнтеграцияИС.НастроитьПодключаемоеОборудование(ЭтотОбъект, ПоддерживаемыеТипыПодключаемогоОборудования);
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	Если НовыйОбъект = Элементы.ДокументОснование.ДоступныеТипы.ПривестиЗначение(НовыйОбъект) Тогда
		Объект.ДокументОснование = НовыйОбъект;
		Модифицированность = Истина;
		Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриСозданииЧтенииНаСервере();
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыИС.ПриЧтенииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОповещениеПриПодключении = Новый ОписаниеОповещения("ПодключитьОборудованиеЗавершение", ЭтотОбъект);
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(
		ОповещениеПриПодключении,
		ЭтотОбъект,
		ПоддерживаемыеТипыПодключаемогоОборудования);
		
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ОповещениеПриОтключении = Новый ОписаниеОповещения("ОтключитьОборудованиеЗавершение", ЭтотОбъект);
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(ОповещениеПриОтключении, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = ИнтеграцияИСКлиентСервер.ИмяСобытияИзмененоСостояние(
		ИнтеграцияИСМПСлужебныйКлиент.ИмяПодсистемы())
		И Параметр.Ссылка = Объект.Ссылка Тогда
		
		Если Параметр.Свойство("ОбъектИзменен")
			И Параметр.ОбъектИзменен Тогда
			ОбновитьПредставленияНаФорме(Истина);
		Иначе
			ОбновитьПредставленияНаФорме(Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИмяСобытия = ИнтеграцияИСКлиентСервер.ИмяСобытияВыполненОбмен(ИнтеграцияИСМПСлужебныйКлиент.ИмяПодсистемы())
	 И (Параметр = Неопределено
		Или (ТипЗнч(Параметр) = Тип("Структура") И Параметр.ОбновлятьСтатусВФормахДокументов)) Тогда
		
		ОбновитьПредставленияНаФорме(Истина);
		
	КонецЕсли;
	
	Если ИмяСобытия = "Закрытие_ПерейтиКСтрокеОшибки"
		И Источник = "Справочник.ИСМППрисоединенныеФайлы.Форма.ФормаОшибки" Тогда
		ТекущийЭлемент = Элементы.Товары;
		Элементы.Товары.ТекущаяСтрока = Параметр;
	КонецЕсли;
	
	Если ИмяСобытия = ИнтеграцияИСКлиентСервер.ИмяСобытияОбновитьКэшМаркируемойПродукции(
		ИнтеграцияИСМПСлужебныйКлиент.ИмяПодсистемы())
		И Параметр = ЭтотОбъект Тогда
		
		ОбновитьКэшМаркируемойПродукции();
		
	КонецЕсли;
	
	ОбщегоНазначенияСобытияФормИСКлиентПереопределяемый.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	СобытияФормИСМПКлиентПереопределяемый.ОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, ИсточникВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Если Не ВидПродукцииУказан()
		Или РедактированиеФормыНедоступно
		Или Не ПравоИзменения Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСоСканераСтруктура = СобытияФормИСКлиент.ВнешнееСобытиеПреобразоватьДанныеСоСканераВСтруктуру(ЭтотОбъект, Источник, Событие, Данные);
	Если ДанныеСоСканераСтруктура = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьКодМаркировки(ДанныеСоСканераСтруктура);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ШтрихкодированиеОбщегоНазначенияИС.СохранитьНастройкиВыбораМаркируемойПродукции(ЭтотОбъект, Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ОбновитьПредставленияНаФорме();
	
	ИнтеграцияИСПереопределяемый.ЗаполнитьСлужебныеРеквизитыВКоллекции(ЭтотОбъект, Объект.Товары);
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	ЗаполнитьПредставлениеСертификацииИПервичногоДокумента();
	
	ИнтеграцияИС.ПослеЗаписиНаСервереВФормеОбъектаДокументаИС(
		ЭтотОбъект,
		ТекущийОбъект,
		ИнтеграцияИСМПКлиентСервер.ИмяПодсистемы(),
		ПараметрыЗаписи);
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ИнтеграцияИСКлиент.ПослеЗаписиВФормеОбъектаДокументаИС(
		ЭтотОбъект,
		Объект,
		ИнтеграцияИСМПКлиентСервер.ИмяПодсистемы(),
		ПараметрыЗаписи);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ОбщегоНазначенияСобытияФормИСКлиентПереопределяемый.ПередЗаписью(ЭтотОбъект, Отказ, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СобытияФормИСМПКлиент.ОбработкаНавигационнойСсылки(
		ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтатусПредставлениеОбработкаНавигационнойСсылки(
	Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	ОчиститьСообщения();

	Если (Не ЗначениеЗаполнено(Объект.Ссылка)) Или (Не Объект.Проведен) Тогда

		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения(
			"СтатусПредставлениеОбработкаНавигационнойСсылкиЗавершение",
				ЭтотОбъект, Новый Структура("НавигационнаяСсылкаФорматированнойСтроки",
				НавигационнаяСсылкаФорматированнойСтроки));
		ТекстВопроса = НСтр("ru = 'Документ ""Перемаркировка товаров ИС МП"" не проведен. Провести?'");
		ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет);

	ИначеЕсли Модифицированность Тогда

		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения(
			"СтатусПредставлениеОбработкаНавигационнойСсылкиЗавершение",
		ЭтотОбъект,
		Новый Структура("НавигационнаяСсылкаФорматированнойСтроки", НавигационнаяСсылкаФорматированнойСтроки));
		ТекстВопроса = НСтр("ru = 'Документ ""Перемаркировка товаров ИС МП"" был изменен. Провести?'");
		ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
	ИначеЕсли ПроверитьЗаполнениеПоВидуДействия(НавигационнаяСсылкаФорматированнойСтроки) Тогда
		
		ОбработатьНажатиеНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки);

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВидПродукцииПриИзменении(Элемент, ПерезаполнитьПоОснованию = Ложь)
	
	Если ПерезаполнитьПоОснованию Тогда
		ПерезаполнитьПоОснованиюСервер();
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ВидПродукции");
	
	СобытияФормИСМПКлиент.ВидПродукцииПриИзменении(ЭтотОбъект, Элемент);
	СобытияФормИСКлиентСерверПереопределяемый.УстановитьПараметрыВыбораНоменклатуры(ЭтотОбъект, Объект.ВидПродукции);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПродукцииОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение <> Объект.ВидПродукции
		И Объект.Товары.Количество() > 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		ТекстВопроса = НСтр("ru = 'При изменении вида продукции табличная часть Товары будет очищена. Продолжить?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПриИзмененииВидаПродукцииПриЗавершении", ЭтотОбъект, ВыбранноеЗначение);
		
		РежимДиалога = РежимДиалогаВопрос.ДаНет;
		
		Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
			РежимДиалога = Новый СписокЗначений;
			РежимДиалога.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Очистить'"));
			РежимДиалога.Добавить(Истина, НСтр("ru = 'Перезаполнить'"));
			РежимДиалога.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Отмена'"));
		КонецЕсли;
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалога);
		
	ИначеЕсли ВыбранноеЗначение <> Объект.ВидПродукции
		И Объект.Товары.Количество() = 0
		И ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Объект.ВидПродукции = ВыбранноеЗначение;
		ВидПродукцииПриИзменении(Элементы.ВидПродукции, Истина);
		
		Модифицированность = Истина;
		
	ИначеЕсли ВыбранноеЗначение = Объект.ВидПродукции Тогда
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Объект.Товары.Количество() > 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		ТекстВопроса = НСтр("ru = 'При изменении организации табличная часть Товары будет очищена. Продолжить?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПриИзмененииОрганизацияПриЗавершении", ЭтотОбъект, ВыбранноеЗначение);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ВидПродукцииОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТребуетсяИндивидуализацияКИЗПриИзменении(Элемент)
	
	ОбновитьСтатусИСМП();
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ДокументОснование");
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеСохраненногоВыбораОбработкаНавигационнойСсылки(
	Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнтеграцияИСКлиент.ПредставлениеСохраненногоВыбораОбработкаНавигационнойСсылки(
		ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыСертификацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ОткрытьФормуРедактированияСертификации(Элементы.Товары.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСертификацияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ПустаяСтрока(ТекущиеДанные.Сертификация) Тогда

		ТекущиеДанные.ВидДокументаСертификации   = ПредопределенноеЗначение(
			"Перечисление.ВидыДокументовОбязательнойСертификацииИС.ПустаяСсылка");
		ТекущиеДанные.НомерДокументаСертификации = "";
		ТекущиеДанные.ДатаДокументаСертификации  = '00010101';

		Возврат;
	КонецЕсли;

	ПараметрыПоиска = Новый Структура("Представление", ТекущиеДанные.Сертификация);
	НайденныеСтроки = КэшСертификации.НайтиСтроки(ПараметрыПоиска);
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		ТекущиеДанные.ВидДокументаСертификации   = НайденныеСтроки[0].ВидСертификации;
		ТекущиеДанные.НомерДокументаСертификации = НайденныеСтроки[0].НомерСертификации;
		ТекущиеДанные.ДатаДокументаСертификации  = НайденныеСтроки[0].ДатаСертификации;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПредставлениеПервичногоДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИдентификаторыСтрок = Новый Массив;
	ИдентификаторыСтрок.Добавить(Элементы.Товары.ТекущиеДанные.ПолучитьИдентификатор());
	ОткрытьФормуРедактированияДанныхПервичногоДокумента(ИдентификаторыСтрок);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыПредставлениеПервичногоДокументаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ПустаяСтрока(ТекущиеДанные.ПредставлениеПервичногоДокумента) Тогда
	
		ТекущиеДанные.ВидПервичногоДокумента          = ПредопределенноеЗначение("Перечисление.ВидыПервичныхДокументовИСМП.ПустаяСсылка");
		ТекущиеДанные.НомерПервичногоДокумента        = "";
		ТекущиеДанные.НаименованиеПервичногоДокумента = "";
		ТекущиеДанные.ДатаПервичногоДокумента         = Дата(1,1,1);
		ТекущиеДанные.ПервичныйДокумент               = Неопределено;
		Возврат;
	КонецЕсли;
	
	СтрокаКэша = КэшПервичныхДокументов[Число(ТекущиеДанные.ПредставлениеПервичногоДокумента)];
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, СтрокаКэша);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПричинаПеремаркировкиПриИзменении(Элемент)
	
	ИдентификаторСтроки = Элементы.Товары.ТекущаяСтрока;
	
	Если ИдентификаторСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	ПараметрыСтроки = Новый Структура();
	ПараметрыСтроки.Вставить("ВидСертификации",   ТекущиеДанные.ВидДокументаСертификации);
	ПараметрыСтроки.Вставить("ДатаСертификации",  ТекущиеДанные.ДатаДокументаСертификации);
	ПараметрыСтроки.Вставить("НомерСертификации", ТекущиеДанные.НомерДокументаСертификации);
		
	ДополнительныеПараметры = Новый Структура();
	ИдентификаторСтрокиМассив = Новый Массив();
	ИдентификаторСтрокиМассив.Добавить(ИдентификаторСтроки);
	ДополнительныеПараметры.Вставить("ИдентификаторыСтрок", ИдентификаторСтрокиМассив);
	
	ЗаполнениеСертификацииЗавершение(ПараметрыСтроки, ДополнительныеПараметры);
	
	ОбновитьКэшМаркируемойПродукции();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.Товары.ТекущаяСтрока = Неопределено
		Или (Не ДоступноРедактированиеНовогоКодаМаркировки
			И (РедактированиеФормыНеДоступно Или Не ПравоИзменения)) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоРедактируемоеПолеФормы(Поле) Тогда
		ОткрытьФормуВводаКодовМаркировки(Элементы.Товары.ТекущаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоваяХарактеристикаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	СобытияФормИСМПКлиентПереопределяемый.ПриНачалеВыбораХарактеристики(
		Элемент, ТекущиеДанные, СтандартнаяОбработка, "НоваяНоменклатура");
		
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СобытияФормИСКлиентПереопределяемый.ПриНачалеВыбораНоменклатуры(Элемент, Объект.ВидПродукции, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	СобытияФормИСМПКлиентПереопределяемый.ПриИзмененииНоменклатуры(
		ЭтотОбъект, ТекущиеДанные, КэшированныеЗначения, Неопределено);
	
	ОбновитьКэшМаркируемойПродукции();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоваяНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	СобытияФормИСМПКлиентПереопределяемый.ПриИзмененииНоменклатуры(
		ЭтотОбъект, ТекущиеДанные, КэшированныеЗначения, Неопределено);
	
	ОбновитьКэшМаркируемойПродукции();
	
	СписокНоменклатуры = Новый Массив;
	СписокНоменклатуры.Добавить(ТекущиеДанные.НоваяНоменклатура);
	
	ДобавитьСертификатыНоменклатурыВКэш(ЭтотОбъект, СписокНоменклатуры);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПослеУдаления(Элемент)
	
	ТоварыПослеУдаленияСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	СобытияФормИСМПКлиентПереопределяемый.ПриНачалеВыбораХарактеристики(
		Элемент, ТекущиеДанные, СтандартнаяОбработка, "Номенклатура");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	СобытияФормИСМПКлиентПереопределяемый.ПриИзмененииХарактеристики(ЭтотОбъект, ТекущиеДанные, КэшированныеЗначения);
	
	ОбновитьКэшМаркируемойПродукции();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	ОткрытьФормуВводаКодовМаркировки(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриАктивизацииЯчейки(Элемент)
	
	Если Элемент.ТекущийЭлемент = Элементы.ТоварыСертификация Тогда
		ИнтеграцияИСМПКлиент.ОбновитьСписокВыбораСертификации(ЭтотОбъект);
	ИначеЕсли Элемент.ТекущийЭлемент = Элементы.ТоварыПредставлениеПервичногоДокумента Тогда
		ИнтеграцияИСМПКлиент.ОбновитьСписокВыбораПервичногоДокумента(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКодТНВЭДНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		ПараметрыФормы = Новый Структура;
		
		ПараметрыФормы.Вставить("ТекущаяСтрока",                           ТекущиеДанные.КодТНВЭД);
		ПараметрыФормы.Вставить("ВидПродукции",                            Объект.ВидПродукции);
		ПараметрыФормы.Вставить("Организация",                             Объект.Организация);
		ПараметрыФормы.Вставить("РежимВыбора",                             Истина);
		ПараметрыФормы.Вставить("ВозвращатьСсылкуНаЭлементКлассификатора", Ложь);
		
		ОткрытьФорму(
			"РегистрСведений.КодыТНВЭДИСМП.Форма.ФормаСписка", ПараметрыФормы, Элемент,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПровестиДокумент(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ПеремаркировкаТоваровИСМП.Форма.ФормаДокумента.Провести",,Истина);
	
	ОчиститьСообщения();
	ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДокумент(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ПеремаркировкаТоваровИСМП.Форма.ФормаДокумента.Записать",,Истина);
	
	ОчиститьСообщения();
	Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени(
		"Документ.ПеремаркировкаТоваровИСМП.Форма.ФормаДокумента.ПровестиИЗакрыть",,Истина);
	
	ОчиститьСообщения();
	ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение);
	
	Если Записать(ПараметрыЗаписи) Тогда
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкоду(Команда)
	
	ОчиститьСообщения();
	
	Если Не ВидПродукцииУказан() Тогда
		Возврат;
	КонецЕсли;
	
	ШтрихкодированиеИСКлиент.ПоказатьВводШтрихкода(ОписаниеОповещенияОбработкиКодаМаркировки());
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодбор(Команда)
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ПеремаркировкаТоваровИСМП.ФормаДокумента.Команда.ОткрытьПодбор");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("Подключаемый_ОбработкаРезультатаПодбораНоменклатуры", ЭтотОбъект);
	СобытияФормИСМПКлиентПереопределяемый.ОткрытьФормуПодбораНоменклатуры(ЭтотОбъект, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДокументыСертификации(Команда)
	
	ОткрытьФормуРедактированияСертификации(Элементы.Товары.ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПервичныеДокументы(Команда)
	
	ОткрытьФормуРедактированияДанныхПервичногоДокумента(Элементы.Товары.ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьПоОснованию(Команда)
	ОбработчикПерезаполненияПоОснованию();
КонецПроцедуры

#Область ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда) Экспорт
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды() Экспорт
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюКомандПодключенныхКОбъекту(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда) Экспорт
	
	СобытияФормИСКлиентПереопределяемый.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ПриЗавершенииОперации(Результат, ДополнительныеПараметры) Экспорт

	Прочитать();

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКэшМаркируемойПродукции()
	
	ОбновитьКэшМаркируемойПродукцииСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКэшМаркируемойПродукцииСервер()
	
	ШтрихкодированиеИС.ОбновитьКэшМаркируемойПродукции(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	СобытияФормИСПереопределяемый.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтотОбъект);
	СобытияФормИСПереопределяемый.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(
		ЭтотОбъект, "ТоварыНоваяХарактеристика", "Объект.Товары.НоваяХарактеристикаИспользуется");
	
	// Новая номенклатура
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыНоваяНоменклатура.Имя);
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра(
		"ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		ЭлементУсловногоОформления.Отбор,
		Элементы.ТоварыПричинаПеремаркировки.ПутьКДанным,
		ПредопределенноеЗначение("Перечисление.ПричиныПеремаркировкиТоваровИСМП.ОшибкиОписанияТовара"),
		ВидСравненияКомпоновкиДанных.НеРавно);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		ЭлементУсловногоОформления.Отбор,
		Элементы.ТоварыМарка.ПутьКДанным,
		,
		ВидСравненияКомпоновкиДанных.Заполнено);
	
	// Новая характеристика
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыНоваяХарактеристика.Имя);
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра(
		"ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		ЭлементУсловногоОформления.Отбор,
		"Объект.Товары.НоваяХарактеристикаИспользуется", Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		ЭлементУсловногоОформления.Отбор,
		Элементы.ТоварыПричинаПеремаркировки.ПутьКДанным,
		ПредопределенноеЗначение("Перечисление.ПричиныПеремаркировкиТоваровИСМП.ОшибкиОписанияТовара"),
		ВидСравненияКомпоновкиДанных.НеРавно);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		ЭлементУсловногоОформления.Отбор,
		Элементы.ТоварыМарка.ПутьКДанным,
		,
		ВидСравненияКомпоновкиДанных.Заполнено);
	
	// Старая номенклатура
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыНоменклатура.Имя);
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		ЭлементУсловногоОформления.Отбор,
		Элементы.ТоварыПричинаПеремаркировки.ПутьКДанным,
		ПредопределенноеЗначение("Перечисление.ПричиныПеремаркировкиТоваровИСМП.ОшибкиОписанияТовара"),
		ВидСравненияКомпоновкиДанных.НеРавно);
	
	// Старая характеристика
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыХарактеристика.Имя);
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		ЭлементУсловногоОформления.Отбор,
		Элементы.ТоварыПричинаПеремаркировки.ПутьКДанным,
		ПредопределенноеЗначение("Перечисление.ПричиныПеремаркировкиТоваровИСМП.ОшибкиОписанияТовара"),
		ВидСравненияКомпоновкиДанных.НеРавно);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		ЭлементУсловногоОформления.Отбор,
		"Объект.Товары.ХарактеристикиИспользуются", Истина);
	
	// Старый код маркировки
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыМарка.Имя);
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		ЭлементУсловногоОформления.Отбор,
		Элементы.ТоварыПричинаПеремаркировки.ПутьКДанным,
		ПредопределенноеЗначение("Перечисление.ПричиныПеремаркировкиТоваровИСМП.ОшибкиОписанияТовара"),
		ВидСравненияКомпоновкиДанных.НеРавно);
	
	// Документ продажи
	СписокСравнения = Новый СписокЗначений();
	СписокСравнения.ЗагрузитьЗначения(ИнтеграцияИСМПКлиентСервер.ПолучитьНеДоступныеПричиныПеремаркировкиДляПервичногоДокумента());
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПредставлениеПервичногоДокумента.Имя);
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра(
		"Текст", НСтр("ru = '<не требуется>'"));
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра(
		"ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		ЭлементУсловногоОформления.Отбор,
		Элементы.ТоварыПричинаПеремаркировки.ПутьКДанным,
		СписокСравнения,
		ВидСравненияКомпоновкиДанных.ВСписке);
	
КонецПроцедуры

&НаСервере
Процедура ТоварыПослеУдаленияСервер()

	ОбновитьКэшМаркируемойПродукцииСервер();
	ОбновитьСтатусИСМП();

КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьОбменОбработкаОжидания()
	
	ИнтеграцияИСМПСлужебныйКлиент.ПродолжитьВыполнениеОбмена(ЭтотОбъект, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаРезультатаПодбораНоменклатуры(Результат, ДополнительныеПараметры) Экспорт
	
	ОбработкаРезультатаПодбораНоменклатуры(Результат, КэшированныеЗначения);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСтатусИСМП()
	
	МенеджерОбъекта = ОбщегоНазначенияИС.МенеджерОбъектаПоСсылке(Объект.Ссылка);
	
	СтатусИСМП = МенеджерОбъекта.СтатусПоУмолчанию();
	
	СтатусПроверкиИПодбора = ПроверкаИПодборПродукцииИС.СтатусПроверкиИПодбораДокумента(
		Объект.Ссылка, Объект.ВидПродукции);
	
	ДопустимыеДействия = МенеджерОбъекта.ДопустимыеДействия(Объект);
	ДальнейшееДействие = Новый Массив;
	
	Если СтатусПроверкиИПодбора <> Перечисления.СтатусыПроверкиИПодбораИС.Выполняется Тогда
		
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			
			Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Статусы.Статус КАК Статус,
			|	ВЫБОР
			|		КОГДА Статусы.ДальнейшееДействие1 В (&МассивДальнейшиеДействия)
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.НеТребуется)
			|		ИНАЧЕ Статусы.ДальнейшееДействие1
			|	КОНЕЦ КАК ДальнейшееДействие1,
			|	ВЫБОР
			|		КОГДА Статусы.ДальнейшееДействие2 В (&МассивДальнейшиеДействия)
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.НеТребуется)
			|		ИНАЧЕ Статусы.ДальнейшееДействие2
			|	КОНЕЦ КАК ДальнейшееДействие2,
			|	ВЫБОР
			|		КОГДА Статусы.ДальнейшееДействие3 В (&МассивДальнейшиеДействия)
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.НеТребуется)
			|		ИНАЧЕ Статусы.ДальнейшееДействие3
			|	КОНЕЦ КАК ДальнейшееДействие3
			|ИЗ
			|	РегистрСведений.СтатусыДокументовИСМП КАК Статусы
			|ГДЕ
			|	Статусы.Документ = &Документ");
			
			Запрос.УстановитьПараметр("Документ", Объект.Ссылка);
			Запрос.УстановитьПараметр(
				"МассивДальнейшиеДействия", ИнтеграцияИСМП.НеотображаемыеВДокументахДальнейшиеДействия());
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Выборка.Следующий() Тогда
				
				СтатусИСМП = Выборка.Статус;
				
				Если СтатусИСМП = Перечисления.СтатусыОбработкиПеремаркировкиТоваровИСМП.Черновик Тогда
					
					СтруктураПараметров = Новый Структура;
					СтруктураПараметров.Вставить("ОбъектРасчета", Объект);
					ДальнейшееДействие = МенеджерОбъекта.ДальнейшееДействиеПоУмолчанию(СтруктураПараметров);
					
				Иначе
					
					Если ДопустимыеДействия.Найти(Выборка.ДальнейшееДействие1) <> Неопределено Тогда
						ДальнейшееДействие.Добавить(Выборка.ДальнейшееДействие1);
					КонецЕсли;
					Если ДопустимыеДействия.Найти(Выборка.ДальнейшееДействие2) <> Неопределено Тогда
						ДальнейшееДействие.Добавить(Выборка.ДальнейшееДействие2);
					КонецЕсли;
					Если ДопустимыеДействия.Найти(Выборка.ДальнейшееДействие3) <> Неопределено Тогда
						ДальнейшееДействие.Добавить(Выборка.ДальнейшееДействие3);
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			СтруктураПараметров = Новый Структура;
			СтруктураПараметров.Вставить("ОбъектРасчета", Объект);
			ДальнейшееДействие = МенеджерОбъекта.ДальнейшееДействиеПоУмолчанию(СтруктураПараметров);
			
		КонецЕсли;
	
	КонецЕсли;
	
	СтатусПредставление = ИнтеграцияИСМП.ПредставлениеСтатуса(СтатусИСМП, ДальнейшееДействие, ДопустимыеДействия);
	
	РедактированиеФормыНеДоступно = СтатусИСМП <> Перечисления.СтатусыОбработкиПеремаркировкиТоваровИСМП.Черновик
		И СтатусИСМП <> Перечисления.СтатусыОбработкиПеремаркировкиТоваровИСМП.БлокировкаКодовМаркировкиОшибкаПередачи
		И СтатусИСМП <> Перечисления.СтатусыОбработкиПеремаркировкиТоваровИСМП.ПеремаркировкаОшибкаПередачи;
	
	ДоступноРедактированиеНовогоКодаМаркировки = (СтатусИСМП = Перечисления.СтатусыОбработкиПеремаркировкиТоваровИСМП.КодыМаркировкиЗаблокированыДляПродажи);
	
	ЗависимыеОтСтатусаОбработкиЗаказа = Новый Массив;
	ЗависимыеОтСтатусаОбработкиЗаказа.Добавить("ГруппаНередактируемыеПослеОтправкиРеквизитыОсновное");
	ЗависимыеОтСтатусаОбработкиЗаказа.Добавить("Товары");
	ЗависимыеОтСтатусаОбработкиЗаказа.Добавить("ТоварыПодменюЗаполнить");
	ЗависимыеОтСтатусаОбработкиЗаказа.Добавить("ПоискПоШтрихкоду");
	ЗависимыеОтСтатусаОбработкиЗаказа.Добавить("Ответственный");
	
	ИнтеграцияИСКлиентСервер.УстановитьДоступностьЭлементовФормы(ЭтотОбъект,
		ЗависимыеОтСтатусаОбработкиЗаказа, Не РедактированиеФормыНеДоступно);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПредставлениеОбработкаНавигационнойСсылкиЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если Не РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Не ПроверитьЗаполнение();
	Отказ = Не ПроверитьЗаполнениеПоВидуДействия(ДополнительныеПараметры.НавигационнаяСсылкаФорматированнойСтроки)
		Или Отказ;
	
	Если Отказ Тогда
		Возврат;
	Иначе
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
		РазблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;
	
	Если Не Модифицированность И Объект.Проведен Тогда
		ОбработатьНажатиеНавигационнойСсылки(ДополнительныеПараметры.НавигационнаяСсылкаФорматированнойСтроки);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНажатиеНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ЗаблокироватьКодыМаркировки" Тогда
		
		ПараметрыОбработкиДокументов = ИнтеграцияИСМПСлужебныйКлиентСервер.ПараметрыОбработкиДокументов();
		ПараметрыОбработкиДокументов.Ссылка             = Объект.Ссылка;
		ПараметрыОбработкиДокументов.Организация        = Объект.Организация;
		ПараметрыОбработкиДокументов.ДальнейшееДействие = ПредопределенноеЗначение(
			"Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗаблокируйтеКодыМаркировки");
		
		ОписаниеПриЗавершении = Новый ОписаниеОповещения(
			"Подключаемый_ПриЗавершенииОперации", ЭтотОбъект, ПараметрыОбработкиДокументов);
			
		ИнтеграцияИСМПКлиент.ПодготовитьКПередаче(
			ЭтотОбъект,
			ПараметрыОбработкиДокументов,
			ОписаниеПриЗавершении);
	
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ВыполнитьИндивидуализациюКИЗ" Тогда
		
		ПараметрыОбработкиДокументов = ИнтеграцияИСМПСлужебныйКлиентСервер.ПараметрыОбработкиДокументов();
		ПараметрыОбработкиДокументов.Ссылка             = Объект.Ссылка;
		ПараметрыОбработкиДокументов.Организация        = Объект.Организация;
		ПараметрыОбработкиДокументов.ДальнейшееДействие = ПредопределенноеЗначение(
			"Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.ВыполнитеИндивидуализациюКИЗ");
		
		ОписаниеПриЗавершении = Новый ОписаниеОповещения(
			"Подключаемый_ПриЗавершенииОперации", ЭтотОбъект, ПараметрыОбработкиДокументов);
			
		ИнтеграцияИСМПКлиент.ПодготовитьКПередаче(
			ЭтотОбъект,
			ПараметрыОбработкиДокументов,
			ОписаниеПриЗавершении);
		
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ВыполнитеПеремаркировку" Тогда
		
		ПараметрыОбработкиДокументов = ИнтеграцияИСМПСлужебныйКлиентСервер.ПараметрыОбработкиДокументов();
		ПараметрыОбработкиДокументов.Ссылка             = Объект.Ссылка;
		ПараметрыОбработкиДокументов.Организация        = Объект.Организация;
		ПараметрыОбработкиДокументов.ДальнейшееДействие = ПредопределенноеЗначение(
			"Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.ВыполнитеПеремаркировку");
		
		ОписаниеПриЗавершении = Новый ОписаниеОповещения(
			"Подключаемый_ПриЗавершенииОперации", ЭтотОбъект, ПараметрыОбработкиДокументов);
			
		ИнтеграцияИСМПКлиент.ПодготовитьКПередаче(
			ЭтотОбъект,
			ПараметрыОбработкиДокументов,
			ОписаниеПриЗавершении);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОтменитьОперацию" Тогда
		
		ИнтеграцияИСМПКлиент.ОтменитьПоследнююОперацию(Объект.Ссылка);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОтменитьПередачуДанных" Тогда
	
		ИнтеграцияИСМПКлиент.ОтменитьПередачу(Объект.Ссылка);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ПоказатьПричинуОшибки" Тогда
		
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("Документ", Объект.Ссылка);
		
		ОткрытьФорму(
			"Справочник.ИСМППрисоединенныеФайлы.Форма.ФормаОшибки",
			ПараметрыОткрытияФормы,
			ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаРезультатаПодбораНоменклатуры(ВыбранноеЗначение, КэшированныеЗначения)
	
	ПараметрыЗаполнения = ИнтеграцияИС.ПараметрыЗаполненияТабличнойЧастиТовары(Объект.ВидПродукции);
	
	СобытияФормИСМППереопределяемый.ОбработкаРезультатаПодбораНоменклатуры(
		ЭтотОбъект, ВыбранноеЗначение, ПараметрыЗаполнения, КэшированныеЗначения);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииЧтенииНаСервере()
	
	ЦветГиперссылки = ЦветаСтиля.ЦветГиперссылкиГосИС;
	ЦветТекстаПоля  = ЦветаСтиля.ЦветТекстаПоля;
	
	ШтрихкодированиеОбщегоНазначенияИС.ВосстановитьНастройкиВыбораМаркируемойПродукции(ЭтотОбъект, Объект.Ссылка);
	ШтрихкодированиеИСКлиентСервер.ОтобразитьСохраненныйВыборПоМаркируемойПродукции(ЭтотОбъект);
	
	ПравоИзменения = ПравоДоступа("Изменение", Метаданные.Документы.ПеремаркировкаТоваровИСМП);
	
	ИнтеграцияИСПереопределяемый.ЗаполнитьСлужебныеРеквизитыВКоллекции(ЭтотОбъект, Объект.Товары);
	
	СобытияФормИСКлиентСерверПереопределяемый.УстановитьПараметрыВыбораНоменклатуры(ЭтотОбъект, Объект.ВидПродукции);
	СобытияФормИСКлиентСерверПереопределяемый.УстановитьПараметрыВыбораНоменклатуры(
		ЭтотОбъект, Объект.ВидПродукции, "ТоварыНоваяНоменклатура");
	
	ОбновитьПредставленияНаФорме();
	
	ЗаполнитьПредставлениеСертификацииИПервичногоДокумента();
	СоздатьКэшСертификацииСервер();
	
	Если Не ПодключенаОбработкаКодовМаркировки Тогда
		ПроверкаИПодборПродукцииИС.ПодключитьОбработкуКодовМаркировки(ЭтотОбъект, Ложь);
		ПодключенаОбработкаКодовМаркировки = Истина;
	Иначе
		ПерезаполнитьДанныеФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОткорректироватьДоступныеВидыПродукции(ДоступныеВидыПродукцииИС)
	
	ВГраница = ДоступныеВидыПродукцииИС.ВГраница();
	Для Счетчик = 0 По ВГраница Цикл
		Индекс = ВГраница - Счетчик;
		Если Не ИнтеграцияИСКлиентСервер.ВидПродукцииПодлежитПеремаркировке(ДоступныеВидыПродукцииИС[Индекс]) Тогда
			ДоступныеВидыПродукцииИС.Удалить(Индекс);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#Область Оборудование

&НаКлиенте
Процедура ПодключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт

	Если Не РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр( "ru = 'При подключении оборудования произошла ошибка:""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт

	Если Не РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр( "ru = 'При отключении оборудования произошла ошибка: ""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область Сертификация

&НаКлиенте
Процедура ОткрытьФормуРедактированияСертификации(ИдентификаторыСтрок)
	
	Если ТипЗнч(ИдентификаторыСтрок) = Тип("Массив") Тогда
		МассивСтрок = ИдентификаторыСтрок;
	Иначе
		МассивСтрок = Новый Массив();
		МассивСтрок.Добавить(ИдентификаторыСтрок);
	КонецЕсли;
	
	Если МассивСтрок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ВидСертификации",
		ПредопределенноеЗначение("Перечисление.ВидыДокументовОбязательнойСертификацииИС.ПустаяСсылка"));
	ПараметрыОткрытия.Вставить("ДатаСертификации",  Дата('00010101'));
	ПараметрыОткрытия.Вставить("НомерСертификации", "");
	
	Если МассивСтрок.Количество() = 1 Тогда
		
		ВыбраннаяСтрока = Объект.Товары.НайтиПоИдентификатору(МассивСтрок[0]);
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("ВидСертификации",   ВыбраннаяСтрока.ВидДокументаСертификации);
		ПараметрыОткрытия.Вставить("ДатаСертификации",  ВыбраннаяСтрока.ДатаДокументаСертификации);
		ПараметрыОткрытия.Вставить("НомерСертификации", ВыбраннаяСтрока.НомерДокументаСертификации);
		
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("ИдентификаторыСтрок", МассивСтрок);
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ЗаполнениеСертификацииЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму(
		"ОбщаяФорма.УточнениеСертификацииИС",
		ПараметрыОткрытия, ЭтотОбъект,,,,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеСертификации(ВидСертификации, НомерСертификации, ДатаСертификации)
	
	ШаблонПредставлениеСертификата = "%1 №%2 от %3";
	
	Если ЗначениеЗаполнено(ВидСертификации)
		И ЗначениеЗаполнено(НомерСертификации)
		И ЗначениеЗаполнено(ДатаСертификации) Тогда
		
		ПредставлениеСертификата = СтрШаблон(ШаблонПредставлениеСертификата,
			ВидСертификации, НомерСертификации, Формат(ДатаСертификации, "ДФ=dd.MM.yyyy"));
		
	КонецЕсли;
	
	Возврат ПредставлениеСертификата;
	
КонецФункции

&НаСервере
Процедура СоздатьКэшСертификацииСервер()
	
	СписокНоменклатуры = Новый Массив;
	Для Каждого СтрокаТовары Из Объект.Товары Цикл
		
		ОбновитьКэшСертификацииКлиентСервер(ЭтотОбъект, СтрокаТовары.ВидДокументаСертификации,
			СтрокаТовары.НомерДокументаСертификации, СтрокаТовары.ДатаДокументаСертификации);
		
		СтрокаТовары.Сертификация = ПредставлениеСертификации(СтрокаТовары.ВидДокументаСертификации,
			СтрокаТовары.НомерДокументаСертификации, СтрокаТовары.ДатаДокументаСертификации);
		
		СписокНоменклатуры.Добавить(СтрокаТовары.Номенклатура);
		
	КонецЦикла;
	
	ДобавитьСертификатыНоменклатурыВКэш(ЭтотОбъект, СписокНоменклатуры);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьКэшСертификацииКлиентСервер(Форма, ВидСертификации, НомерСертификации, ДатаСертификации, Номенклатура = Неопределено)
	
	Если Не ЗначениеЗаполнено(ВидСертификации)
		И Не ЗначениеЗаполнено(НомерСертификации)
		И Не ЗначениеЗаполнено(ДатаСертификации) Тогда
		
		Возврат;
	КонецЕсли;
	
	ПараметрыПоиска = Новый Структура("ВидСертификации, НомерСертификации, ДатаСертификации",
		ВидСертификации, НомерСертификации, ДатаСертификации);
		
	НайденныеСтроки = Форма.КэшСертификации.НайтиСтроки(ПараметрыПоиска);
	Если НайденныеСтроки.Количество() = 0 Тогда
		
		НоваяСтрока = Форма.КэшСертификации.Добавить();
		НоваяСтрока.ВидСертификации   = ВидСертификации;
		НоваяСтрока.НомерСертификации = НомерСертификации;
		НоваяСтрока.ДатаСертификации  = ДатаСертификации;
		НоваяСтрока.Представление     = ПредставлениеСертификации(ВидСертификации, НомерСертификации, ДатаСертификации);
		НоваяСтрока.Номенклатура      = Номенклатура;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПредставлениеСертификацииИПервичногоДокумента()

	ПервичныеДокументы = Новый Соответствие;
	
	КэшПервичныхДокументов.Очистить();
	Для Каждого Строка Из Объект.Товары Цикл
		
		Строка.Сертификация = ПредставлениеСертификации(
			Строка.ВидДокументаСертификации,
			Строка.НомерДокументаСертификации,
			Строка.ДатаДокументаСертификации);
		
		Строка.ПредставлениеПервичногоДокумента = ИнтеграцияИСМПКлиентСервер.ПредставлениеПервичногоДокумента(Строка);
		Если ПервичныеДокументы.Получить(Строка.ПредставлениеПервичногоДокумента) = Неопределено
			И ЗначениеЗаполнено(Строка.ПредставлениеПервичногоДокумента) Тогда
			ПервичныеДокументы.Вставить(Строка.ПредставлениеПервичногоДокумента, Истина);
			ЗаполнитьЗначенияСвойств(КэшПервичныхДокументов.Добавить(), Строка);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнениеСертификацииЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт

	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВидСертификации   = РезультатВыбора.ВидСертификации;
	НомерСертификации = РезультатВыбора.НомерСертификации;
	ДатаСертификации  = РезультатВыбора.ДатаСертификации;
	
	Для Каждого ИдентификаторСтроки Из ДополнительныеПараметры.ИдентификаторыСтрок Цикл
		ДанныеСтроки = Объект.Товары.НайтиПоИдентификатору(ИдентификаторСтроки);
		ДанныеСтроки.ВидДокументаСертификации   = ВидСертификации;
		ДанныеСтроки.НомерДокументаСертификации = НомерСертификации;
		ДанныеСтроки.ДатаДокументаСертификации  = ДатаСертификации;
		ДанныеСтроки.Сертификация = ПредставлениеСертификации(
			ВидСертификации, НомерСертификации, ДатаСертификации);
	КонецЦикла;
	
	ОбновитьКэшСертификацииКлиентСервер(
		ЭтотОбъект, ВидСертификации, НомерСертификации, ДатаСертификации);
	
	ИнтеграцияИСМПКлиент.ОбновитьСписокВыбораСертификации(ЭтотОбъект);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьСертификатыНоменклатурыВКэш(Форма, СписокНоменклатуры)
	
	СертификатыНоменклатуры = ИнформацияСертификатовНоменклатурыСервер(СписокНоменклатуры);
	
	Для Каждого КлючЗначение Из СертификатыНоменклатуры Цикл
		
		Номенклатура = КлючЗначение.Ключ;
		Для Каждого Сертификат Из КлючЗначение.Значение Цикл
			
			ОбновитьКэшСертификацииКлиентСервер(
				Форма, Сертификат.ВидСертификации, Сертификат.НомерСертификации, Сертификат.ДатаСертификации, Номенклатура);
				
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИнформацияСертификатовНоменклатурыСервер(СписокНоменклатуры)
	
	СертификатыНоменклатуры = Новый Соответствие;
	ИнтеграцияИСПереопределяемый.ПриЗаполненииСертификатовНоменклатуры(СписокНоменклатуры, СертификатыНоменклатуры);
	
	Возврат СертификатыНоменклатуры;
	
КонецФункции

#КонецОбласти

#Область ПервичныйДокумент

&НаКлиенте
Процедура ОткрытьФормуРедактированияДанныхПервичногоДокумента(ИдентификаторыСтрок)
	
	Если ИдентификаторыСтрок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НеДоступныеПричиныПеремаркировкиДляПервичногоДокумента = ИнтеграцияИСМПКлиентСервер.
		ПолучитьНеДоступныеПричиныПеремаркировкиДляПервичногоДокумента();
	
	МассивСтрок = Новый Массив;
	ПричинаПеремаркировки = Неопределено;
	
	Для Каждого ИдентификаторСтроки Из ИдентификаторыСтрок Цикл
		
		ДанныеСтроки = Объект.Товары.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если НеДоступныеПричиныПеремаркировкиДляПервичногоДокумента.Найти(ДанныеСтроки.ПричинаПеремаркировки) = Неопределено Тогда
			
			Если ПричинаПеремаркировки = Неопределено Тогда
				
				ПричинаПеремаркировки = ДанныеСтроки.ПричинаПеремаркировки;
				
			ИначеЕсли Не ИнтеграцияИСМПКлиентСервер.СоответствиеПричинПеремаркировкиПервичногоДокумента(
				ДанныеСтроки.ПричинаПеремаркировки,
				ПричинаПеремаркировки) Тогда
					
				ОбщегоНазначенияКлиент.СообщитьПользователю(
					НСтр("ru = 'Для причин перемаркировки выделенных строк, указание одного документа продажи невозможно.'"));
				Возврат;
				
			КонецЕсли;
			
			МассивСтрок.Добавить(ИдентификаторСтроки);
			
		КонецЕсли;
		
	КонецЦикла;

	Если МассивСтрок.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'В выбранных строках указание документа продажи не требуется.'"));
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Элементы.Товары.ТекущаяСтрока;
	Если МассивСтрок.Найти(ТекущаяСтрока) <> Неопределено Тогда
		ВыбраннаяСтрока = Объект.Товары.НайтиПоИдентификатору(ТекущаяСтрока);
	Иначе
		ВыбраннаяСтрока = Объект.Товары.НайтиПоИдентификатору(МассивСтрок[0]);
	КонецЕсли;
	
	ДоступныеВидыПервичныхДокументов = ИнтеграцияИСМПКлиентСервер.
		ПолучитьДоступныеВидыПервичныхДокументовДляПричиныПеремаркировки(ВыбраннаяСтрока.ПричинаПеремаркировки);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ВидПервичногоДокумента",           ПредопределенноеЗначение("Перечисление.ВидыПервичныхДокументовИСМП.ПустаяСсылка"));
	ПараметрыОткрытия.Вставить("НаименованиеПервичногоДокумента",  "");
	ПараметрыОткрытия.Вставить("НомерПервичногоДокумента",         "");
	ПараметрыОткрытия.Вставить("ДатаПервичногоДокумента",          Дата(1,1,1));
	ПараметрыОткрытия.Вставить("ДоступныеВидыПервичныхДокументов", ДоступныеВидыПервичныхДокументов);
	ПараметрыОткрытия.Вставить("Организация",                      Объект.Организация);
	ПараметрыОткрытия.Вставить("ПервичныйДокумент",                Неопределено);
	
	ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, ВыбраннаяСтрока);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИдентификаторыСтрок", МассивСтрок);
	ДополнительныеПараметры.Вставить("ПричинаПеремаркировки", ВыбраннаяСтрока.ПричинаПеремаркировки);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ЗаполнениеПервичногоДокументаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму(
		"ОбщаяФорма.ИнформацияОДокументеПродажи",
		ПараметрыОткрытия, ЭтотОбъект,,,,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнениеПервичногоДокументаЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ИдентификаторСтроки Из ДополнительныеПараметры.ИдентификаторыСтрок Цикл
		ДанныеСтроки = Объект.Товары.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если ИнтеграцияИСМПКлиентСервер.СоответствиеПричинПеремаркировкиПервичногоДокумента(
			ДанныеСтроки.ПричинаПеремаркировки,
			ДополнительныеПараметры.ПричинаПеремаркировки) Тогда
			ЗаполнитьЗначенияСвойств(ДанныеСтроки, РезультатВыбора);
		КонецЕсли;
	КонецЦикла;
	
	РезультатВыбора.Вставить("ПричинаПеремаркировки", ДополнительныеПараметры.ПричинаПеремаркировки);
	ОбновитьКэшПервичныхДокументов(ЭтотОбъект, РезультатВыбора);
	ИнтеграцияИСМПКлиент.ОбновитьСписокВыбораПервичногоДокумента(ЭтотОбъект);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьКэшПервичныхДокументов(Форма, ДанныеПервичногоДокумента)
	
	Если ПустаяСтрока(ДанныеПервичногоДокумента.ПредставлениеПервичногоДокумента) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПоиска = Новый Структура("ПредставлениеПервичногоДокумента", ДанныеПервичногоДокумента.ПредставлениеПервичногоДокумента);
	НайденныеСтроки = Форма.КэшПервичныхДокументов.НайтиСтроки(ПараметрыПоиска);
	
	Если НайденныеСтроки.Количество() = 0 Тогда
		ЭлементКэша = Форма.КэшПервичныхДокументов.Добавить();
		ЗаполнитьЗначенияСвойств(ЭлементКэша, ДанныеПервичногоДокумента);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Штрихкодирование

&НаКлиенте
Процедура ОбработатьКодМаркировки(ИсходныеДанные, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ИсходныеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.ДекодироватьШтрихкодДанныхBase64(ИсходныеДанные);
	
	ДанныеРазбора = РазборКодаМаркировкиИССлужебныйКлиент.РазобратьКодМаркировки(ИсходныеДанные.Штрихкод, Объект.ВидПродукции);
	
	Если ДанныеРазбора <> Неопределено
		И ДанныеРазбора.ВидыУпаковокПоВидамПродукции[Объект.ВидПродукции].Найти(
			ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.Потребительская")) = Неопределено Тогда
		ПараметрыОткрытия = ШтрихкодированиеИСКлиент.ПараметрыОткрытияФормыНевозможностиДобавленияОтсканированного();
		ПараметрыОткрытия.Штрихкод = ДанныеРазбора.НормализованныйКодМаркировки;
		ПараметрыОткрытия.ТекстОшибки = НСтр(
			"ru = 'Штрихкод не является штрихкодом потребительской упаковки'");
		ОткрытьФорму("ОбщаяФорма.ИнформацияОНевозможностиДобавленияОтсканированного", ПараметрыОткрытия, ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	ПараметрыСканирования = ШтрихкодированиеОбщегоНазначенияИСКлиент.ПараметрыСканирования(ЭтотОбъект);
	
	ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.ЗакодироватьШтрихкодДанныхBase64(ИсходныеДанные);
	
	РезультатОбработки = ОбработатьВводШтрихкода(ИсходныеДанные, Неопределено, ПараметрыСканирования);
	
	ПараметрыЗавершенияВводаШтрихкода = ПараметрыЗавершенияВводаШтрихкода(ИсходныеДанные, РезультатОбработки, ПараметрыСканирования);
	ШтрихкодированиеОбщегоНазначенияИСКлиент.ЗавершитьОбработкуШтрихкода(ПараметрыЗавершенияВводаШтрихкода);
	
КонецПроцедуры

&НаКлиенте
Функция ОписаниеОповещенияОбработкиКодаМаркировки()
	
	Возврат Новый ОписаниеОповещения("ОбработатьКодМаркировки", ЭтотОбъект);
	
КонецФункции

&НаКлиенте
Функция ПараметрыЗавершенияВводаШтрихкода(ИсходныеДанные, РезультатОбработки, ПараметрыСканирования)
	
	ПараметрыЗавершенияВводаШтрихкода = ШтрихкодированиеОбщегоНазначенияИСКлиент.ПараметрыЗавершенияОбработкиШтрихкода();
	ПараметрыЗавершенияВводаШтрихкода.РезультатОбработкиШтрихкода   = РезультатОбработки;
	ПараметрыЗавершенияВводаШтрихкода.Форма                         = ЭтотОбъект;
	ПараметрыЗавершенияВводаШтрихкода.ПараметрыСканирования         = ПараметрыСканирования;
	ПараметрыЗавершенияВводаШтрихкода.ДанныеШтрихкода               = ИсходныеДанные;
	
	Возврат ПараметрыЗавершенияВводаШтрихкода;
	
КонецФункции

&НаСервере
Функция ОбработатьВводШтрихкода(ШтрихкодКоличество, КэшированныеЗначения, ПараметрыСканирования = Неопределено)
	
	РезультатОбработкиШтрихкода = ШтрихкодированиеОбщегоНазначенияИС.ОбработатьВводШтрихкода(
		ЭтотОбъект, ШтрихкодКоличество, КэшированныеЗначения, ПараметрыСканирования);
	
	Если РезультатОбработкиШтрихкода.ДополнительныеПараметры <> Неопределено
		И РезультатОбработкиШтрихкода.ТипУпаковки <> ПредопределенноеЗначение(
		"Перечисление.ТипыУпаковок.МаркированныйТовар") Тогда
			
		РезультатОбработкиШтрихкода.Вставить("Номенклатура",             Неопределено);
		РезультатОбработкиШтрихкода.Вставить("Характеристика",           Неопределено);
		РезультатОбработкиШтрихкода.Вставить("ТребуетсяАвторизацияИСМП", Ложь);
		РезультатОбработкиШтрихкода.ИспользоватьОбработкуНаКлиенте       = Истина;
		РезультатОбработкиШтрихкода.ОткрытьФормуВводаКодаМаркировки      = Ложь;
		РезультатОбработкиШтрихкода.ОбработкаШтрихкодаБезМарки           = Истина;
		РезультатОбработкиШтрихкода.ДополнительныеПараметры.Свойство("Номенклатура",
			РезультатОбработкиШтрихкода.Номенклатура);
		РезультатОбработкиШтрихкода.ДополнительныеПараметры.Свойство("Характеристика",
			РезультатОбработкиШтрихкода.Характеристика);
		Для Каждого ВидПродукции Из РезультатОбработкиШтрихкода.ВидыПродукции Цикл
			РезультатОбработкиШтрихкода.Вставить("ВидПродукции", ВидПродукции);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат РезультатОбработкиШтрихкода;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаКодаМаркировкиВыполнитьДействие(ДанныеДляВыполненияДействия, ДополнительныеПараметры) Экспорт
	
	РезультатВыбора             = ДанныеДляВыполненияДействия.РезультатВыбора;
	РезультатОбработкиШтрихкода = ДанныеДляВыполненияДействия.РезультатОбработкиШтрихкода;
	КэшированныеЗначения        = ДанныеДляВыполненияДействия.КэшированныеЗначения;
	ПараметрыСканирования       = ДанныеДляВыполненияДействия.ПараметрыСканирования;
	
	Действие = ДанныеДляВыполненияДействия.Действие;
	РезультатОбработкиШтрихкода = ОбработкаКодаМаркировкиВыполнитьДействиеСервер(Действие, РезультатВыбора, РезультатОбработкиШтрихкода, ПараметрыСканирования, КэшированныеЗначения);
	
	ПараметрыЗавершенияВводаШтрихкода = ПараметрыЗавершенияВводаШтрихкода(
		ДанныеДляВыполненияДействия.ИсходныеДанные, РезультатОбработкиШтрихкода, ДанныеДляВыполненияДействия.ПараметрыСканирования);
	ШтрихкодированиеОбщегоНазначенияИСКлиент.ЗавершитьОбработкуШтрихкода(ПараметрыЗавершенияВводаШтрихкода);
	
КонецПроцедуры

&НаСервере
Функция ОбработкаКодаМаркировкиВыполнитьДействиеСервер(Действие, РезультатВыбора, РезультатОбработкиШтрихкода, ПараметрыСканирования, КэшированныеЗначения)
	
	ПараметрыОбработкиВыбора    = ШтрихкодированиеОбщегоНазначенияИС.ИнициализироватьПараметрыОбработкиВыбора(РезультатВыбора, РезультатОбработкиШтрихкода, КэшированныеЗначения);
	РезультатОбработкиШтрихкода = ШтрихкодированиеИС.ВыполнитьДействие(ЭтотОбъект, Действие, ПараметрыОбработкиВыбора);
	
	РезультатОбработкиШтрихкода.ИзмененныеСтроки  = Новый Массив;
	РезультатОбработкиШтрихкода.ДобавленныеСтроки = Новый Массив;
	
	Возврат РезультатОбработкиШтрихкода;
	
КонецФункции

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОткрытьФормуУточненияДанных()
	
	ШтрихкодированиеИСКлиент.Подключаемый_ОткрытьФормуУточненияДанных(ЭтотОбъект, ОписаниеОповещенияОбработкиКодаМаркировки());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаШтрихкодов

&НаКлиенте
Процедура ОткрытьФормуВводаКодовМаркировки(ИсходныеДанныеСтроки)
	
	//&ЗамерПроизводительности
	ОценкаПроизводительностиКлиент.ЗамерВремени(
		"Документ.ПеремаркировкаТоваровИСМП.ФормаДокумента.Команда.ОткрытьФормуВводаКодовМаркировки");
	
	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("Организация", Объект.Организация);
	ПараметрыОткрытия.Вставить("ВидПродукции", Объект.ВидПродукции);
	ПараметрыОткрытия.Вставить("Номенклатура");
	ПараметрыОткрытия.Вставить("Характеристика");
	ПараметрыОткрытия.Вставить("НоваяНоменклатура");
	ПараметрыОткрытия.Вставить("НоваяХарактеристика");
	ПараметрыОткрытия.Вставить("КодМаркировки");
	ПараметрыОткрытия.Вставить("НовыйКодМаркировки");
	ПараметрыОткрытия.Вставить("ПричинаПеремаркировки");
	ПараметрыОткрытия.Вставить("СпособВводаВОборот");
	ПараметрыОткрытия.Вставить("ВидДокументаСертификации");
	ПараметрыОткрытия.Вставить("НомерДокументаСертификации");
	ПараметрыОткрытия.Вставить("ДатаДокументаСертификации");
	ПараметрыОткрытия.Вставить("Сертификация");
	ПараметрыОткрытия.Вставить("ВидПервичногоДокумента");
	ПараметрыОткрытия.Вставить("НаименованиеПервичногоДокумента");
	ПараметрыОткрытия.Вставить("НомерПервичногоДокумента");
	ПараметрыОткрытия.Вставить("ДатаПервичногоДокумента");
	ПараметрыОткрытия.Вставить("ПервичныйДокумент");
	ПараметрыОткрытия.Вставить("ПредставлениеПервичногоДокумента");
	ПараметрыОткрытия.Вставить("КодТНВЭД");
	ПараметрыОткрытия.Вставить("СтранаПроизводства");
	ПараметрыОткрытия.Вставить("Цвет");
	ПараметрыОткрытия.Вставить("Размер");
	ПараметрыОткрытия.Вставить("БлокировкаРедактированияСтарогоКода", Ложь);
	ПараметрыОткрытия.Вставить("БлокировкаРедактированияНовогоКода",  Ложь);
	ПараметрыОткрытия.Вставить("ТребуетсяИндивидуализацияКИЗ",        Объект.ТребуетсяИндивидуализацияКИЗ);
	
	Если РедактированиеФормыНеДоступно Тогда
		ПараметрыОткрытия.БлокировкаРедактированияСтарогоКода = РедактированиеФормыНеДоступно;
		ПараметрыОткрытия.БлокировкаРедактированияНовогоКода  = Не ДоступноРедактированиеНовогоКодаМаркировки;
	КонецЕсли;
	
	Если СохраненВыборПоМаркируемойПродукции Тогда
		ПараметрыОткрытия.Вставить("ДанныеВыбораПоМаркируемойПродукции", ДанныеВыбораПоМаркируемойПродукции);
	КонецЕсли;
	
	Если ТипЗнч(ИсходныеДанныеСтроки) = Тип("Структура") Тогда
		
		ПараметрыОткрытия.Вставить("АдресДанныеШтрихкода",
			ПоместитьВоВременноеХранилище(ИсходныеДанныеСтроки.ДанныеШтрихкода, УникальныйИдентификатор));
		
	ИначеЕсли ИсходныеДанныеСтроки <> Неопределено Тогда
		
		ДанныеСтроки = Объект.Товары.НайтиПоИдентификатору(ИсходныеДанныеСтроки);
		ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, ДанныеСтроки);
		
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("ИдентификаторСтроки", ИсходныеДанныеСтроки);
	
	КонецЕсли;
	
	ОписаниеОповещенияЗакрытиеФормыВводаКодаМаркировки = Новый ОписаниеОповещения(
		"ОповещениеЗакрытиеФормыВводаКода", ЭтотОбъект, ДополнительныеПараметры);
		
	Форма = ОткрытьФорму(
		"Обработка.РаботаСКМПриПеремаркировкеТоваровИСМП.Форма.ФормаВводаКодовМаркировки",
		ПараметрыОткрытия,
		ЭтотОбъект,,,, ОписаниеОповещенияЗакрытиеФормыВводаКодаМаркировки);
	
	Если Не Форма.Открыта()
		И ПараметрыОткрытия.Свойство("АдресДанныеШтрихкода")
		И ЭтоАдресВременногоХранилища(ПараметрыОткрытия.АдресДанныеШтрихкода) Тогда
		ДанныеШтрихкода = ПолучитьИзВременногоХранилища(ПараметрыОткрытия.АдресДанныеШтрихкода);
		УдалитьИзВременногоХранилища(ПараметрыОткрытия.АдресДанныеШтрихкода);
		Если ДанныеШтрихкода.ОбработанСОшибками Тогда
			ПараметрыОткрытия = ШтрихкодированиеИСКлиент.ПараметрыОткрытияФормыНевозможностиДобавленияОтсканированного();
			ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, ДанныеШтрихкода);
			ОткрытьФорму("ОбщаяФорма.ИнформацияОНевозможностиДобавленияОтсканированного", ПараметрыОткрытия, ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеЗакрытиеФормыВводаКода(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры = Неопределено Или ДополнительныеПараметры.ИдентификаторСтроки = Неопределено Тогда
		ТекущиеДанные = Объект.Товары.Добавить();
	Иначе
		ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(ДополнительныеПараметры.ИдентификаторСтроки);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, Результат);

	ОбновитьКэшМаркируемойПродукции();
	
	СписокНоменклатуры = Новый Массив;
	СписокНоменклатуры.Добавить(ТекущиеДанные.НоваяНоменклатура);
	ДобавитьСертификатыНоменклатурыВКэш(ЭтотОбъект, СписокНоменклатуры);
	
	ОбновитьКэшСертификацииКлиентСервер(ЭтотОбъект,
		ТекущиеДанные.ВидДокументаСертификации,
		ТекущиеДанные.НомерДокументаСертификации,
		ТекущиеДанные.ДатаДокументаСертификации);
	
	ИнтеграцияИСМПКлиент.ОбновитьСписокВыбораСертификации(ЭтотОбъект);
	
	ОбновитьКэшПервичныхДокументов(ЭтотОбъект, Результат);
	ИнтеграцияИСМПКлиент.ОбновитьСписокВыбораПервичногоДокумента(ЭтотОбъект);
	
	ОбновитьСтатусИСМП();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОбработкиШтрихкодов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено
		Или Не (Результат.ИспользоватьОбработкуНаКлиенте)
		Или РедактированиеФормыНеДоступно Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуВводаКодовМаркировки(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеРезультатПотоковойПечати(ДанныеОтветаРезервированияИПечати, ДополнительныеПараметры) Экспорт
	
	Если ДанныеОтветаРезервированияИПечати = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеОтветаРезервированияИПечати.РезультатРезервирования.Количество() Тогда
		
		СтрокаРезультата = ДанныеОтветаРезервированияИПечати.РезультатРезервирования.Получить(0);
		
		НоваяСтрокаТовары = Объект.Товары.Добавить();
		ДанныеВыбора                         = ДополнительныеПараметры.ДанныеВыбораПоМаркируемойПродукции;
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары, ДанныеВыбора);
		НоваяСтрокаТовары.КодМаркировки      = ДополнительныеПараметры.КодМаркировки;
		НоваяСтрокаТовары.НовыйКодМаркировки = СтрокаРезультата.ШтрихкодУпаковки;
			
		СобытияФормИСМПКлиент.ОпределитьИспользованиеХарактеристик(
			ЭтотОбъект,
			НоваяСтрокаТовары,
			"Номенклатура", "ХарактеристикиИспользуются");
		СобытияФормИСМПКлиент.ОпределитьИспользованиеХарактеристик(
			ЭтотОбъект,
			НоваяСтрокаТовары,
			"НоваяНоменклатура", "НоваяХарактеристикаИспользуется");
		
		СписокНоменклатуры = Новый Массив;
		СписокНоменклатуры.Добавить(НоваяСтрокаТовары.НоваяНоменклатура);
		ДобавитьСертификатыНоменклатурыВКэш(ЭтотОбъект, СписокНоменклатуры);
		
		НоваяСтрокаТовары.Сертификация = ПредставлениеСертификации(
			НоваяСтрокаТовары.ВидДокументаСертификации,
			НоваяСтрокаТовары.НомерДокументаСертификации,
			НоваяСтрокаТовары.ДатаДокументаСертификации);
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Функция ПроверитьЗаполнениеПоВидуДействия(ВыполняемоеДействие)
	
	Отказ = Ложь;
	
	Если ВыполняемоеДействие = "ВыполнитьИндивидуализациюКИЗ"
		Или ВыполняемоеДействие = "ВыполнитеПеремаркировку" Тогда
		Для Каждого СтрокаТовар Из Объект.Товары Цикл
			НомерСтроки = Объект.Товары.Индекс(СтрокаТовар) + 1;
			Если Не ЗначениеЗаполнено(СтрокаТовар.НовыйКодМаркировки) Тогда
				ТекстОшибки = СтрШаблон(
					НСтр("ru = 'В строке %1 табличной части ""Товары "" не заполнен новый код маркировки.'"),
					НомерСтроки);
				ОбщегоНазначенияКлиент.СообщитьПользователю(
					ТекстОшибки,,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
						"Объект.Товары", НомерСтроки, "НовыйКодМаркировки"),,
					Отказ);
			КонецЕсли;
			Если Не ЗначениеЗаполнено(СтрокаТовар.КодТНВЭД) Тогда
				ТекстОшибки = СтрШаблон(
					НСтр("ru = 'В строке %1 табличной части ""Товары "" не заполнено поле ""Код ТНВЭД"".'"),
					НомерСтроки);
				ОбщегоНазначенияКлиент.СообщитьПользователю(
					ТекстОшибки,,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
						"Объект.Товары", НомерСтроки, "КодТНВЭД"),,
					Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Не Отказ;
	
КонецФункции

&НаСервере
Процедура ОбновитьПредставленияНаФорме(Прочитать = Ложь)

	Если Прочитать Тогда
		Прочитать();
	Иначе
		ОбновитьСтатусИСМП();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПослеВыбораОснования(ДанныеВыбора, ДополнительныеПараметры) Экспорт
	
	Если Не (ДанныеВыбора = Элементы.ДокументОснование.ДоступныеТипы.ПривестиЗначение(ДанныеВыбора)) Тогда
		Возврат
	КонецЕсли;
	
	Объект.ДокументОснование = ДанныеВыбора;
	Модифицированность       = Истина;
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("ОбработатьПерезаполнение") Тогда
		ОбработчикПерезаполненияПоОснованию(Ложь);
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ДокументОснование");
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьДанныеФормы()
	
	ЗаполнитьПредставлениеСертификацииИПервичногоДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПерезаполненияПоОснованию(ЗадаватьВопрос = Истина)
	
	ОчиститьСообщения();
	
	Если Объект.Товары.Количество() > 0 И ЗадаватьВопрос Тогда
		
		ТекстВопроса = НСтр("ru = 'Данные документа будут перезаполнены. Продолжить?'");
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения(
			"ВопросОПерезаполнениииПоОснованиюПриЗавершении", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ПерезаполнитьПоОснованиюСервер();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОПерезаполнениииПоОснованиюПриЗавершении(Результат, Параметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ПерезаполнитьПоОснованиюСервер();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьПоОснованиюСервер()
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	ТекущийОбъект.Заполнить(Объект.ДокументОснование);
	
	ЗначениеВРеквизитФормы(ТекущийОбъект, "Объект");
	
	ПриСозданииЧтенииНаСервере();
	
	ОбновитьКэшМаркируемойПродукцииСервер();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПриИзмененииВидаПродукцииПриЗавершении(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да
		Или РезультатВопроса = Истина Тогда
		
		Объект.ВидПродукции = ДополнительныеПараметры;
		Объект.Товары.Очистить();
		
		ВидПродукцииПриИзменении(Элементы.ВидПродукции, РезультатВопроса = Истина);
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПриИзмененииОрганизацияПриЗавершении(РезультатВопроса, НоваяОрганизация) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		Объект.Организация = НоваяОрганизация;
		Объект.Товары.Очистить();
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СписокРеквизитов = "")
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Инициализация = ПустаяСтрока(СписокРеквизитов);
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);
	
	Если Инициализация Или СтруктураРеквизитов.Свойство("ДокументОснование") Тогда
		ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюКомандПодключенныхКОбъекту(Форма);
		Элементы.ТоварыПерезаполнитьПоОснованию.Доступность = ЗначениеЗаполнено(Объект.ДокументОснование);
	КонецЕсли;
	
	ЭтоОбувь = Объект.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Обувь");
	ЭтоШины  = Объект.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Шины");
	ЭтоМех   = Объект.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха");
	
	Элементы.ТребуетсяИндивидуализацияКИЗ.Видимость = ЭтоМех;
	Элементы.ТоварыСтранаПроизводства.Видимость     = Не ЭтоШины;
	Элементы.ТоварыЦвет.Видимость                   = ЭтоОбувь;
	Элементы.ТоварыРазмер.Видимость                 = ЭтоОбувь;
	
КонецПроцедуры

&НаКлиенте
Функция ВидПродукцииУказан()
	
	Если Не ЗначениеЗаполнено(Объект.ВидПродукции) Тогда
		
		ТекстСообщения = НСтр("ru = 'Не указан вид продукции'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Функция ЭтоРедактируемоеПолеФормы(Поле)
	
	Возврат РедактируемыеПоляФормы(ЭтотОбъект).Найти(Поле) <> Неопределено;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РедактируемыеПоляФормы(Форма)
	
	Поля = Новый Массив;
	Поля.Добавить(Форма.Элементы.ТоварыСертификация);
	Поля.Добавить(Форма.Элементы.ТоварыПредставлениеПервичногоДокумента);
	Поля.Добавить(Форма.Элементы.ТоварыКодТНВЭД);
	Поля.Добавить(Форма.Элементы.ТоварыСтранаПроизводства);
	Поля.Добавить(Форма.Элементы.ТоварыЦвет);
	Поля.Добавить(Форма.Элементы.ТоварыРазмер);
	
	Возврат Поля;
	
КонецФункции

#КонецОбласти