
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЕстьПравоДобавлениеПартнеры = ПравоДоступа("Добавление", Метаданные.НайтиПоПолномуИмени("Справочник.Партнеры"));
	ЕстьПравоДобавлениеСоглашения = ПравоДоступа("Добавление", Метаданные.НайтиПоПолномуИмени("Справочник.СоглашенияСКлиентами"));
	ЕстьПравоДобавлениеУсловияОбслуживания = ПравоДоступа("Добавление", Метаданные.НайтиПоПолномуИмени("Справочник.УсловияОбслуживанияПартнеровТорговымиПредставителями"));
	
	УстановитьПараметрыСпискаЗаданий();

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Куратор = Настройки.Получить("Куратор");
	ТорговыйПредставитель = Настройки.Получить("ТорговыйПредставитель");
	ПериодПодготовкиЗаданий = Настройки.Получить("ПериодПодготовкиЗаданий");
	УстановитьПараметрыСпискаЗаданий();
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокЗаданий, "ТорговыйПредставитель", ТорговыйПредставитель, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(ТорговыйПредставитель));
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокЗаданий, "Куратор", Куратор, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(Куратор));

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ЗаданиеТорговомуПредставителю" Тогда
		Элементы.СписокЗаданий.Обновить();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПодготовкиЗаданийПриИзменении(Элемент)
	
	СписокЗаданий.Параметры.УстановитьЗначениеПараметра("ДатаНачалаРаботы",ПериодПодготовкиЗаданий.ДатаНачала);
	СписокЗаданий.Параметры.УстановитьЗначениеПараметра("НачальнаяДатаПланирования", Макс(НачалоДня(ТекущаяДатаСервера()), ПериодПодготовкиЗаданий.ДатаНачала));
	СписокЗаданий.Параметры.УстановитьЗначениеПараметра("КонечнаяДатаПланирования", ПериодПодготовкиЗаданий.ДатаОкончания);
	
	Элементы.СписокЗаданий.Обновить();

КонецПроцедуры

&НаКлиенте
Процедура ТорговыйПредставительПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокЗаданий, "ТорговыйПредставитель", ТорговыйПредставитель, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(ТорговыйПредставитель));

КонецПроцедуры

&НаКлиенте
Процедура КураторПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокЗаданий, "Куратор", Куратор, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(Куратор));

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

&НаКлиенте
Процедура СписокЗаданийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущийЭлемент = Элементы.СписокЗаданийЗаданиеТорговомуПредставителю Тогда
		
		ПараметрыФормы = Новый Структура();
		
		Если Элементы.СписокЗаданий.ТекущиеДанные.ЗаданиеТорговомуПредставителю.Пустая() Тогда
			ЗначенияЗаполнения = Новый Структура();
			ЗначенияЗаполнения.Вставить("Партнер", Элементы.СписокЗаданий.ТекущиеДанные.Партнер);
			ЗначенияЗаполнения.Вставить("УсловияОбслуживания", Элементы.СписокЗаданий.ТекущиеДанные.УсловияОбслуживания);
			
			ПараметрыФормы.Вставить("ЗначенияЗаполнения",ЗначенияЗаполнения);
		Иначе	
			ПараметрыФормы.Вставить("Ключ",Элементы.СписокЗаданий.ТекущиеДанные.ЗаданиеТорговомуПредставителю);
		КонецЕсли;
		
		ОткрытьФормуЗаданияТорговомуПредставителю(ПараметрыФормы);
		
	ИначеЕсли Элемент.ТекущийЭлемент = Элементы.СписокЗаданийПартнер Тогда
		
		ПараметрыФормы = Новый Структура();
		Если ЗначениеЗаполнено(Элементы.СписокЗаданий.ТекущиеДанные.Партнер) Тогда
			ПараметрыФормы.Вставить("Ключ",Элементы.СписокЗаданий.ТекущиеДанные.Партнер);
		КонецЕсли;
			
		Если ЕстьПравоДобавлениеПартнеры Или ЗначениеЗаполнено(Элементы.СписокЗаданий.ТекущиеДанные.Партнер) Тогда
			ОткрытьФорму("Справочник.Партнеры.ФормаОбъекта", ПараметрыФормы, Элементы.СписокЗаданийПартнер);
		КонецЕсли;
		
	ИначеЕсли Элемент.ТекущийЭлемент = Элементы.СписокЗаданийСоглашение Тогда
		
		ПараметрыФормы = Новый Структура();
		Если ЗначениеЗаполнено(Элементы.СписокЗаданий.ТекущиеДанные.Соглашение) Тогда
			ПараметрыФормы.Вставить("Ключ",Элементы.СписокЗаданий.ТекущиеДанные.Соглашение);
		КонецЕсли;
			
		Если ЕстьПравоДобавлениеСоглашения Или ЗначениеЗаполнено(Элементы.СписокЗаданий.ТекущиеДанные.Соглашение) Тогда
			ОткрытьФорму("Справочник.СоглашенияСКлиентами.ФормаОбъекта", ПараметрыФормы, Элементы.СписокЗаданийСоглашение);
		КонецЕсли;
		
	ИначеЕсли Элемент.ТекущийЭлемент = Элементы.СписокЗаданийУсловияОбслуживания Тогда
		
		ПараметрыФормы = Новый Структура();
		Если ЗначениеЗаполнено(Элементы.СписокЗаданий.ТекущиеДанные.УсловияОбслуживания) Тогда
			ПараметрыФормы.Вставить("Ключ",Элементы.СписокЗаданий.ТекущиеДанные.УсловияОбслуживания);
		КонецЕсли;
			
		Если ЕстьПравоДобавлениеУсловияОбслуживания 
				Или ЗначениеЗаполнено(Элементы.СписокЗаданий.ТекущиеДанные.УсловияОбслуживания) Тогда
			ОткрытьФорму("Справочник.УсловияОбслуживанияПартнеровТорговымиПредставителями.ФормаОбъекта", ПараметрыФормы, Элементы.СписокЗаданийУсловияОбслуживания);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьЗадание(Команда)

	ПараметрыФормы = Новый Структура();
	ОткрытьФормуЗаданияТорговомуПредставителю(ПараметрыФормы);

КонецПроцедуры

&НаКлиенте
Процедура НастройкаПечатиБланковЗадания(Команда)

	ОткрытьФорму("Документ.ЗаданиеТорговомуПредставителю.Форма.НастройкаПечатиБланковЗадания");

КонецПроцедуры

&НаКлиенте
Процедура ПечатьБланкаЗадания(Команда)
	
	МассивОбъектов = ЗаданияДляПечати();
	
	Если МассивОбъектов.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru='Не выбраны задания для печати'");
		ПоказатьПредупреждение(Неопределено, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ПараметрыПечати = Новый Структура();
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.ЗаданиеТорговомуПредставителю",
													"БланкЗадания,БланкЗаказа",
													МассивОбъектов,
													Неопределено,
													ПараметрыПечати);

КонецПроцедуры

&НаКлиенте
Процедура ПечатьСводногоЗадания(Команда)
	
	МассивОбъектов = ЗаданияДляПечати();
	
	Если МассивОбъектов.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru='Не выбраны задания для печати'");
		ПоказатьПредупреждение(Неопределено, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ПараметрыПечати = Новый Структура();
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.ЗаданиеТорговомуПредставителю",
													"СводноеЗадание",
													МассивОбъектов,
													Неопределено,
													ПараметрыПечати);

КонецПроцедуры
												
&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокЗаданийЗаданиеТорговомуПредставителю.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокЗаданий.ЗаданиеТорговомуПредставителю");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПросроченныйДокумент);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Создать задание>'"));

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуЗаданияТорговомуПредставителю(ПараметрыФормы = Неопределено)

	ОткрытьФорму("Документ.ЗаданиеТорговомуПредставителю.ФормаОбъекта", ПараметрыФормы, Элементы.СписокЗаданий);

КонецПроцедуры

&НаКлиенте
Функция ЗаданияДляПечати()
	
	МассивОбъектов = Новый Массив();
	
	Для Каждого ИдентификаторСтроки Из Элементы.СписокЗаданий.ВыделенныеСтроки Цикл
		Задание = Элементы.СписокЗаданий.ДанныеСтроки(ИдентификаторСтроки).ЗаданиеТорговомуПредставителю;
		Если ЗначениеЗаполнено(Задание) Тогда
			МассивОбъектов.Добавить(Задание);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивОбъектов;

КонецФункции

&НаСервере
Процедура УстановитьПараметрыСпискаЗаданий()
	
	СписокЗаданий.Параметры.УстановитьЗначениеПараметра("ДатаНачалаРаботы",ПериодПодготовкиЗаданий.ДатаНачала);
	СписокЗаданий.Параметры.УстановитьЗначениеПараметра("НачальнаяДатаПланирования", Макс(НачалоДня(ТекущаяДатаСеанса()), ПериодПодготовкиЗаданий.ДатаНачала));
	СписокЗаданий.Параметры.УстановитьЗначениеПараметра("КонечнаяДатаПланирования", ПериодПодготовкиЗаданий.ДатаОкончания);
	СписокЗаданий.Параметры.УстановитьЗначениеПараметра("ПустаяДата",ПустаяДата);
	
	СписокЗаданий.Параметры.УстановитьЗначениеПараметра("Понедельник",Перечисления.ДниНедели.Понедельник);
	СписокЗаданий.Параметры.УстановитьЗначениеПараметра("Вторник",Перечисления.ДниНедели.Вторник);
	СписокЗаданий.Параметры.УстановитьЗначениеПараметра("Среда",Перечисления.ДниНедели.Среда);
	СписокЗаданий.Параметры.УстановитьЗначениеПараметра("Четверг",Перечисления.ДниНедели.Четверг);
	СписокЗаданий.Параметры.УстановитьЗначениеПараметра("Пятница",Перечисления.ДниНедели.Пятница);
	СписокЗаданий.Параметры.УстановитьЗначениеПараметра("Суббота",Перечисления.ДниНедели.Суббота);
	СписокЗаданий.Параметры.УстановитьЗначениеПараметра("Воскресенье",Перечисления.ДниНедели.Воскресенье);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекущаяДатаСервера()
	
	Возврат ТекущаяДатаСеанса();
	
КонецФункции

#КонецОбласти
