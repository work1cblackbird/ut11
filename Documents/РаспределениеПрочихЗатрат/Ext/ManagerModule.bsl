#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область РаспределениеНаФинансовыйРезультат
// Распределяет расходы на финансовый результат.
// Параметры:
//	ПараметрыОбработчика - Структура -
//	ДокументРаспределения - ДокументСсылка.РаспределениеПрочихЗатрат - отбор по документу, для формирования отчета.
// Возвращаемое значение:
//	Массив Из СправочникСсылка.Организации, Неопределено - организации, по которым выполнилось распределение.
Функция РаспределитьРасходыНаФинансовыйРезультат(ПараметрыОбработчика, ДокументРаспределения = Неопределено) Экспорт
	
	Период = ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации;
	Организации = ПараметрыОбработчика.ПараметрыРасчета.МассивОрганизаций;
	
	ДлительнаяОперация = "Документ.РаспределениеПрочихЗатрат.МодульМенеджера.РаспределитьРасходыНаФинансовыйРезультат";
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(ДлительнаяОперация);
	
	СтруктураПараметровЗапроса = ИнициализироватьПараметрыРаспределенияРасходовНаФинРез(Период, Организации, ДокументРаспределения);
	ВременныеТаблицы = ВременныеТаблицыРаспределенияРасходовНаФинансовыйРезультат(СтруктураПараметровЗапроса);
	
	КоличествоФЗ = РасчетСебестоимостиПовтИсп.ЗначенияТехнологическихПараметров(Перечисления.ОперацииЗакрытияМесяца.ПустаяСсылка()).МаксимальноеКоличествоФЗФормированияДокументов;
	КоличествоФЗ = ?(КоличествоФЗ < 2 ИЛИ ЗначениеЗаполнено(ДокументРаспределения), 1, КоличествоФЗ);
	ВсегоОбработано = РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ВременныеТаблицы, "РезультатРаспределения");
	
	Если КоличествоФЗ = 1 Тогда
		
		РассчитанныеОрганизации = РаспределитьОчереднуюПорциюРасходовНаФинансовыйРезультат(ПараметрыОбработчика, ВременныеТаблицы, Период);
		
	Иначе
		
		//Многопоточное распределение
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = ВременныеТаблицы;
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Регистратор
		|ИЗ
		|	РезультатРаспределения КАК Т
		|";
		
		РегистраторыРезультата = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
		
		КоличествоСтрок = РегистраторыРезультата.Количество();
		СтрокВФЗ = Цел(КоличествоСтрок / ?(КоличествоФЗ = 0, 1, КоличествоФЗ));
		
		Если СтрокВФЗ < 1 Тогда
			
			// Однопоточное распределение
			РассчитанныеОрганизации = РаспределитьОчереднуюПорциюРасходовНаФинансовыйРезультат(ПараметрыОбработчика, ВременныеТаблицы, Период);
			
		Иначе

			//Многопоточное распределение
			БылиОшибки 			= Ложь;
			ОбработаноСтрок     = 0;
			МассивЗаданий       = Новый Массив;
			МассивРегистраторов = Новый Массив;
			
			Пока ОбработаноСтрок <= КоличествоСтрок Цикл
				
				Если ОбработаноСтрок = 0 ИЛИ ОбработаноСтрок = КоличествоСтрок
				 // Задание запускается при наполнении ТаблицаРезультата количеством регистраторов, равным СтрокВФЗ.
				 // В последнее задание помещаются все оставшиеся регистраторы, даже если их больше, чем СтрокВФЗ.
				 ИЛИ (Цел(ОбработаноСтрок/СтрокВФЗ) = ОбработаноСтрок/СтрокВФЗ И МассивЗаданий.Количество() < КоличествоФЗ-1) Тогда
					
					Если ОбработаноСтрок > 0 Тогда
						
						// Запускаем новое ФЗ
						Запрос.УстановитьПараметр("МассивРегистраторов", МассивРегистраторов);
						
						Запрос.Текст =
						"ВЫБРАТЬ
						|	*
						|ИЗ
						|	РезультатРаспределения КАК Т
						|ГДЕ
						|	Т.Регистратор В (&МассивРегистраторов)
						|";
						
						ТаблицаРезультата = Новый ХранилищеЗначения(
							Запрос.Выполнить().Выгрузить(),
							Новый СжатиеДанных(9));
						
						Запрос.Текст =
						"ВЫБРАТЬ
						|	Т.Регистратор
						|ИЗ
						|	ДокументыСНовымиДвижениями КАК Т
						|ГДЕ
						|	Т.Регистратор В (&МассивРегистраторов)
						|";
						
						ДокументыСНовымиДвижениями = Новый ХранилищеЗначения(
							Запрос.Выполнить().Выгрузить(),
							Новый СжатиеДанных(9));
						
						МассивПараметров = Новый Массив;                                                                      
						МассивПараметров.Добавить(ПараметрыОбработчика);
						МассивПараметров.Добавить(ТаблицаРезультата);
						МассивПараметров.Добавить(ДокументыСНовымиДвижениями);
						
						КлючЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'ФЗ №%1 - %2 (%3)'", ОбщегоНазначения.КодОсновногоЯзыка()),
							СокрЛП(МассивЗаданий.Количество() + 1),
							СокрЛП(ПараметрыОбработчика.ДанныеЭтапа.Код),
							СокрЛП(ПараметрыОбработчика.ИдентификаторРасчета));
						
						Задание = ЗакрытиеМесяцаСервер.ЗапуститьПодчиненноеФоновоеЗаданиеЭтапаРасчета(
							ПараметрыОбработчика.ИдентификаторРасчета,
							"РаспределениеПрочихЗатратЛокализация.РаспределениеРасходовНаФинансовыйРезультат",
							МассивПараметров,
							КлючЗадания,
							НСтр("ru='Пакетное формирование документов'"));
							
				        МассивЗаданий.Добавить(Задание);
						
					КонецЕсли;
					
					Если ОбработаноСтрок = КоличествоСтрок Тогда
						// Все задания запущены
						Прервать;
					КонецЕсли;
					
					МассивРегистраторов = Новый Массив;
					
				КонецЕсли;
				
				МассивРегистраторов.Добавить(РегистраторыРезультата[ОбработаноСтрок]);
				
				ОбработаноСтрок = ОбработаноСтрок + 1;
				
			КонецЦикла;
			
			ЗаданияЗавершены = Ложь;;
			
			Пока НЕ ЗаданияЗавершены Цикл
				
				ВыполненныеЗадания = ФоновыеЗадания.ОжидатьЗавершенияВыполнения(МассивЗаданий);
				
				МассивЗаданий = Новый Массив;
				
				Для Каждого Задание Из ВыполненныеЗадания Цикл
					
					Если Задание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
						
						МассивЗаданий.Добавить(Задание);
						
					ИначеЕсли Задание.Состояние <> СостояниеФоновогоЗадания.Завершено Тогда

						БылиОшибки = Истина;
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Фоновое задание формирования документов %1
							|%2'", ОбщегоНазначения.КодОсновногоЯзыка()),
							?(Задание.Состояние = СостояниеФоновогоЗадания.Отменено,
								НСтр("ru = 'отменено'", ОбщегоНазначения.КодОсновногоЯзыка()),
								НСтр("ru = 'завершено аварийно'", ОбщегоНазначения.КодОсновногоЯзыка())),
							?(Задание.ИнформацияОбОшибке = Неопределено,
								НСтр("ru = '<Нет описания ошибки>'", ОбщегоНазначения.КодОсновногоЯзыка()),
								ПодробноеПредставлениеОшибки(Задание.ИнформацияОбОшибке)));
								
						ЗакрытиеМесяцаСервер.ЗафиксироватьНаличиеПроблемыПриВыполненииРасчета(
							ПараметрыОбработчика,
							ТекстОшибки);
						
					КонецЕсли;
					
				КонецЦикла;
				
				ЗаданияЗавершены = (МассивЗаданий.Количество() = 0);
				
			КонецЦикла;
			
			Если БылиОшибки Тогда
				
				РассчитанныеОрганизации = Новый Массив;
				
			Иначе
				
				Запрос.Текст =
				"ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	Т.Организация
				|ИЗ
				|	РезультатРаспределения КАК Т
				|";
				
				РассчитанныеОрганизации = Запрос.Выполнить().Выгрузить();
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ВсегоОбработано);
	
	УдалитьДвиженияРаспределенныхРасходовВСтаромДокументе(СтруктураПараметровЗапроса, ВременныеТаблицы);
	
	Возврат РассчитанныеОрганизации;
	
КонецФункции

// Распределяет расходы на финансовый результат.
// 
// Параметры:
//  ПараметрыОбработчика - Структура -
//  ВременныеТаблицы - МенеджерВременныхТаблиц -
//  Период - Дата -
// 
// Возвращаемое значение:
//  Массив из СправочникСсылка.Организации - обработанные организации
Функция РаспределитьОчереднуюПорциюРасходовНаФинансовыйРезультат(ПараметрыОбработчика, ВременныеТаблицы, Период) Экспорт
	
	РассчитанныеОрганизации = Новый Массив;
	
	Результат = ВыборкаРаспределенияРасходовНаФинансовыйРезультат(ВременныеТаблицы);
	ВыборкаПоОрганизациям = Результат[Результат.Количество() - 1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ФормироватьУправленческийБаланс = ПолучитьФункциональнуюОпцию("ФормироватьУправленческийБаланс");
	
	
	Пока ВыборкаПоОрганизациям.Следующий() Цикл
		
		БылиОшибки = Ложь;
		
		ВыборкаРегистраторов = ВыборкаПоОрганизациям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаРегистраторов.Следующий() Цикл 
			
			ФинансовыеРезультаты = РегистрыНакопления.ФинансовыеРезультаты.СоздатьНаборЗаписей();
			ФинансовыеРезультаты.Отбор.Регистратор.Установить(ВыборкаРегистраторов.Регистратор);
			
			ПрочиеРасходы = РегистрыНакопления.ПрочиеРасходы.СоздатьНаборЗаписей();
			ПрочиеРасходы.Отбор.Регистратор.Установить(ВыборкаРегистраторов.Регистратор);
			
			ПрочиеАктивыПассив = РегистрыНакопления.ПрочиеАктивыПассивы.СоздатьНаборЗаписей();
			ПрочиеАктивыПассив.Отбор.Регистратор.Установить(ВыборкаРегистраторов.Регистратор);
			
			ДвиженияДоходыРасходыПрочиеАктивыПассивы = РегистрыНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы.СоздатьНаборЗаписей();
			ДвиженияДоходыРасходыПрочиеАктивыПассивы.Отбор.Регистратор.Установить(ВыборкаРегистраторов.Регистратор);
			
			ДвиженияПоПрочимАктивамПассивам = РегистрыНакопления.ДвиженияПоПрочимАктивамПассивам.СоздатьНаборЗаписей();
			ДвиженияПоПрочимАктивамПассивам.Отбор.Регистратор.Установить(ВыборкаРегистраторов.Регистратор);
			
			ВыборкаДетальная = ВыборкаРегистраторов.Выбрать();
			Пока ВыборкаДетальная.Следующий() Цикл
				
				ИдентификаторФинЗаписи = Строка(Новый УникальныйИдентификатор());
				
				Если Не (ВыборкаДетальная.Сумма = 0
					И ВыборкаДетальная.СуммаБезНДС = 0
					И ВыборкаДетальная.СуммаУпр = 0
					И ВыборкаДетальная.СуммаРегл = 0
					И ВыборкаДетальная.ПостояннаяРазница = 0
					И ВыборкаДетальная.ВременнаяРазница = 0) Тогда
					
					НоваяЗапись = ПрочиеРасходы.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаДетальная);
					НоваяЗапись.КорНаправлениеДеятельности = ВыборкаДетальная.НаправлениеДеятельностиПриемник;
					НоваяЗапись.НастройкаХозяйственнойОперации = Справочники.НастройкиХозяйственныхОпераций.РаспределениеРасходовПоНаправлениямДеятельности;
					НоваяЗапись.ИдентификаторФинЗаписи = ИдентификаторФинЗаписи;
					НоваяЗапись.РасчетСебестоимости = Истина;
					
				КонецЕсли;
				
				Если Не ВыборкаДетальная.Сумма = 0 Тогда
					
					НоваяЗапись = ПрочиеАктивыПассив.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаДетальная, "Период, Регистратор, Организация, Подразделение, НаправлениеДеятельности, Сумма");
					НоваяЗапись.Статья = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки;
					НоваяЗапись.Аналитика = Неопределено;
					НоваяЗапись.РасчетСебестоимости = Истина;
					
				КонецЕсли;
				
				Если Не (ВыборкаДетальная.Сумма = 0
					И ВыборкаДетальная.СуммаБезНДС = 0
					И ВыборкаДетальная.СуммаУпр = 0
					И ВыборкаДетальная.СуммаРегл = 0) Тогда
					
					НоваяЗапись = ДвиженияДоходыРасходыПрочиеАктивыПассивы.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаДетальная, 
						"Период, Регистратор, Организация, Подразделение, НаправлениеДеятельности, 
						|АналитикаРасходов, ХозяйственнаяОперация, Сумма, СуммаУпр, СуммаРегл");
					НоваяЗапись.Статья = ВыборкаДетальная.СтатьяРасходов;
					НоваяЗапись.КорСтатья = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки;
					НоваяЗапись.КорНаправлениеДеятельности = ВыборкаДетальная.НаправлениеДеятельностиПриемник;
					
				КонецЕсли;
				
				Если Не ВыборкаДетальная.Расходы = 0 Тогда
					
					НоваяЗапись = ФинансовыеРезультаты.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаДетальная);
					НоваяЗапись.НаправлениеДеятельности = ВыборкаДетальная.НаправлениеДеятельностиПриемник;
					НоваяЗапись.РасчетСебестоимости = Истина;
					
				КонецЕсли;
				
				Если Не (ВыборкаДетальная.Сумма = 0
					И ВыборкаДетальная.СуммаУпр = 0
					И ВыборкаДетальная.СуммаРегл = 0
					И ВыборкаДетальная.ПостояннаяРазница = 0
					И ВыборкаДетальная.ВременнаяРазница = 0) Тогда
					
					НоваяЗапись = ДвиженияПоПрочимАктивамПассивам.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаДетальная);
					Если ВыборкаДетальная.ВидДвижения = ВидДвиженияНакопления.Расход Тогда
						НоваяЗапись.ДебетКредит = Перечисления.ВидыДвиженийПрочихАктивовПассивов.Дебет;
					Иначе
						НоваяЗапись.ДебетКредит = Перечисления.ВидыДвиженийПрочихАктивовПассивов.Кредит;
					КонецЕсли;
					НоваяЗапись.НаправлениеДеятельности = ВыборкаДетальная.НаправлениеДеятельностиПриемник;
					НоваяЗапись.Статья = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки;
					НоваяЗапись.Аналитика = Неопределено;
					НоваяЗапись.СуммаСНДС = ВыборкаДетальная.Сумма;
					НоваяЗапись.НастройкаХозяйственнойОперации = Справочники.НастройкиХозяйственныхОпераций.РаспределениеРасходовПоНаправлениямДеятельности;
					НоваяЗапись.ИдентификаторФинЗаписи = ИдентификаторФинЗаписи;
					НоваяЗапись.РасчетСебестоимости = Истина;
					
				КонецЕсли;
				
			КонецЦикла;
			
			
			НачатьТранзакцию();
			Попытка
			
				Если ФормироватьУправленческийБаланс Тогда
					ДвиженияУпрБаланса = Новый Структура;
					ДвиженияУпрБаланса.Вставить("ПрочиеРасходы", ПрочиеРасходы.Выгрузить());
					ДвиженияУпрБаланса.Вставить("ПрочиеАктивыПассивы", ПрочиеАктивыПассив.Выгрузить());
					
					УправленческийУчетПроведениеСервер.ОбновитьДвиженияАктивовПассивов(ВыборкаРегистраторов.Регистратор, ДвиженияУпрБаланса);
					
					ПрочиеАктивыПассив.Загрузить(ДвиженияУпрБаланса.ПрочиеАктивыПассивы);
				КонецЕсли;
				
				ФинансовыеРезультаты.Записать();
				ПрочиеРасходы.Записать();
				ПрочиеАктивыПассив.Записать();
				ДвиженияДоходыРасходыПрочиеАктивыПассивы.Записать();
				ДвиженияПоПрочимАктивамПассивам.Записать();

				
				ЗафиксироватьТранзакцию();
				
			Исключение
				
				ОтменитьТранзакцию();
				БылиОшибки = Истина;

				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось обработать документ ""%1"" из-за ошибки:
						|%2'", ОбщегоНазначения.КодОсновногоЯзыка()),
					ВыборкаРегистраторов.Регистратор,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ЗакрытиеМесяцаСервер.ЗафиксироватьНаличиеПроблемыПриВыполненииРасчета(
					ПараметрыОбработчика,
					ТекстОшибки,
					ВыборкаРегистраторов.Организация,
					,
					,
					ВыборкаРегистраторов.Регистратор);
					
			КонецПопытки;
			
		КонецЦикла;
		
		Если Не БылиОшибки Тогда
			РассчитанныеОрганизации.Добавить(ВыборкаПоОрганизациям.Организация);
		КонецЕсли;
		
	КонецЦикла;

	
	Обработки.ОперацииЗакрытияМесяца.ЗарегистрироватьПроблемыРасчетаЭтапа(ПараметрыОбработчика);
	
	Возврат РассчитанныеОрганизации;
	
КонецФункции

	
// Добавляет служебные таблицы в менеджер временных таблиц, необходимые для расчета
// базы прямых производственных затрат.
// Параметры:
//	ПараметрыРасчета - см. РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьОбщиеПараметрыРасчета
//	Запрос - Запрос - инициализированный ранее запрос.
Процедура ПодготовитьТаблицыКРасчету(ПараметрыРасчета, Запрос = Неопределено) Экспорт
	
	Если Запрос = Неопределено Тогда
		
		Запрос = Новый Запрос;
		РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(
			Запрос,
			ПараметрыРасчета);
			
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыРасчета) = Тип("Структура") И ПараметрыРасчета.Свойство("Регистратор") Тогда
		
		Запрос.УстановитьПараметр("Регистратор", ПараметрыРасчета.Регистратор);
		Запрос.УстановитьПараметр("ПоВсемДокументам", ПараметрыРасчета.Регистратор = Неопределено);
		
	Иначе
		
		Запрос.УстановитьПараметр("Регистратор", Неопределено);
		Запрос.УстановитьПараметр("ПоВсемДокументам", Истина);
		
	КонецЕсли;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Регистратор,
		|	Реквизиты.Дата КАК Дата,
		|	Реквизиты.НазначениеНастройкиРаспределения,
		|	Реквизиты.НаправлениеРаспределения,
		|	ВЫБОР
		|		КОГДА Реквизиты.НаправлениеРаспределения = ЗНАЧЕНИЕ(Перечисление.НаправлениеРаспределенияПоПодразделениям.ПоКоэффициентам)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПустаяСсылка)
		|		ИНАЧЕ	
		|			Реквизиты.БазаРаспределенияПоПартиям
		|	КОНЕЦ КАК БазаРаспределенияПоПартиям,
		|	Реквизиты.Организация КАК Организация,
		|	Реквизиты.Подразделение КАК Подразделение,
		|	Реквизиты.СтатьяРасходов КАК СтатьяРасходов,
		|	Реквизиты.АналитикаРасходов КАК АналитикаРасходов,
		|	Реквизиты.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	(НЕ Реквизиты.НаправлениеРаспределения = ЗНАЧЕНИЕ(Перечисление.НаправлениеРаспределенияПоПодразделениям.Текущее)) КАК ТребуетсяРаспределение,
		|	Реквизиты.УправленческийУчет КАК УправленческийУчет,
		|	Реквизиты.РегламентированныйУчет КАК РегламентированныйУчет,
		|	Реквизиты.НалоговыйУчет КАК НалоговыйУчет,
		|	ВЫБОР // Источник данных актуален только для настроек распределения ""НаФинРез""
		|		КОГДА Реквизиты.НазначениеНастройкиРаспределения = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаФинансовыйРезультат)
		|		ТОГДА Реквизиты.ИсточникДанных
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРаспределенияРасходов.ПустаяСсылка)
		|	КОНЕЦ КАК ИсточникДанных
		|ПОМЕСТИТЬ Регистраторы
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат КАК Реквизиты
		|ГДЕ
		|	Реквизиты.Проведен
		|	И (
		|		Реквизиты.НазначениеНастройкиРаспределения = 
		|			ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаФинансовыйРезультат)
		|		ИЛИ (
		|			Реквизиты.НазначениеНастройкиРаспределения В (
		|				ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаСебестоимостьПроизводства), 
		|				ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеСтатейРасходовПоЭтапамПроизводства)
		|				) 
		|			И Реквизиты.НалоговыйУчет
		|			И Реквизиты.СтатьяРасходов.ВариантРаспределенияРасходовНУ = 
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|		)
		|	)
		|	И Реквизиты.Организация В (&МассивОрганизаций)
		|	И Реквизиты.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И (Реквизиты.Ссылка = &Регистратор
		|		ИЛИ &ПоВсемДокументам)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СтатьяРасходов,
		|	АналитикаРасходов,
		|	НаправлениеДеятельности,
		|	Подразделение,
		|	Организация
		|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Инициализирует параметры запроса распределения расходов на финансовый результат.
// Параметры:
// 	Период - Дата - период распределения расходов.
//	Организации - Массив Из СправочникСсылка.Организации - список организаций, по которым необходимо выполнить распределение.
//	ДокументРаспределения - ДокументСсылка.РаспределениеПрочихЗатрат - отбор по документу, для формирования отчета.
// Возвращаемое значение:
//	Структура:
//		* Период - Дата - период формирования данных;
//		* НачалоПериода - Дата - начало месяца периода формирования данных;
//		* КонецПериода - Дата - конец месяца периода формирования данных;
//		* МассивОрганизаций - Массив Из СправочникСсылка.Организации - массив организаций (СправочникСсылка.Организации);
//		* ДокументРаспределения - ДокументСсылка.РаспределениеПрочихЗатрат,Неопределено - отбор по документу, для формирования отчета.
Функция ИнициализироватьПараметрыРаспределенияРасходовНаФинРез(Период, Организации, ДокументРаспределения = Неопределено) Экспорт
	
	СтруктураПараметровЗапроса = Новый Структура;
	СтруктураПараметровЗапроса.Вставить("Период", Период);
	СтруктураПараметровЗапроса.Вставить("НачалоПериода", НачалоМесяца(Период));
	СтруктураПараметровЗапроса.Вставить("КонецПериода", КонецМесяца(Период));
	СтруктураПараметровЗапроса.Вставить("МассивОрганизаций", Организации);
	СтруктураПараметровЗапроса.Вставить("ДокументРаспределения", ДокументРаспределения);
	
	Возврат СтруктураПараметровЗапроса;
	
КонецФункции

// Формирует временные таблицы для распределения расходов на финансовый результат.
// Параметры:
//	Параметры - Структура - см. ИнициализироватьПараметрыРаспределенияРасходовНаФинРез()
// Возвращаемое значение:
//	МенеджерВременныхТаблиц - временные таблицы для формирования выборки, по которой будет производиться дальнейшее
//		распределение расходов.
Функция ВременныеТаблицыРаспределенияРасходовНаФинансовыйРезультат(Параметры) Экспорт
	
	ПараметрыРасчета = Неопределено;
	
	ВременныеТаблицы = ?(ПараметрыРасчета = Неопределено, Новый МенеджерВременныхТаблиц, ПараметрыРасчета.МенеджерВременныхТаблиц);
	
	РаспределениеРасходовНаФинансовыйРезультатДополнитьПараметрамиПоУмолчанию(Параметры);
	
	Запрос = РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьЗапросПоПараметрам(Параметры, ВременныеТаблицы);
	
	ПодготовитьСлужебныеТаблицы(Параметры.Период, Параметры.МассивОрганизаций, Запрос);
	
	РежимОтладкиПоИсточникам = РасчетСебестоимостиПовтИсп.ЗначенияТехнологическихПараметров().ФормироватьВтИсточникиРасходов;
	
	Запрос.Текст = ТекстЗапросаВременныхТаблицРаспределенияНаФинансовыйРезультат(РежимОтладкиПоИсточникам);
	
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

// Возвращает текст запроса для получения временных таблиц для распределения на финансовый результат.
// Параметры:
// РежимОтладкиПоИсточникам - Булево - признак того, что запрос будет анализироваться в режиме отладки. Если параметр
//		установлен в "Истина", все вложенные таблицы будут выводиться отдельно во временные таблицы (которые можно
//		будет отдельно смотреть), а все подзапросы объединения будут содержать поле "Источник" с кратким описанием
//		текущей выборки.
// Возвращаемое значение:
//	Строка - текст запроса получения временных таблиц.
Функция ТекстЗапросаВременныхТаблицРаспределенияНаФинансовыйРезультат(РежимОтладкиПоИсточникам = Ложь) Экспорт
	
	ЗапросыВременныхТаблиц = Новый Массив;
	
	ЗапросыВременныхТаблиц.Добавить(ТекстПредварительныеОстаткиРасходов());
	ЗапросыВременныхТаблиц.Добавить(ТекстОстаткиРасходов(РежимОтладкиПоИсточникам));

	
	ЗапросыВременныхТаблиц.Добавить(ТекстРасходыКРаспределению(РежимОтладкиПоИсточникам));
	ЗапросыВременныхТаблиц.Добавить(ТекстВтАктуальныеНаправленияДеятельности());
	ЗапросыВременныхТаблиц.Добавить(ТекстФинансовыеРезультаты());
	ЗапросыВременныхТаблиц.Добавить(ТекстВыручкаИСебестоимостьПродаж());
	ЗапросыВременныхТаблиц.Добавить(ТекстБазаРаспределения());
	ЗапросыВременныхТаблиц.Добавить(ТекстБазыРаспределенияСводно());
	ЗапросыВременныхТаблиц.Добавить(ТекстРаспределенныеРасходы());
	ЗапросыВременныхТаблиц.Добавить(ТекстДокументыРаспределения());
	ЗапросыВременныхТаблиц.Добавить(ТекстМаксимальныеКоэффициентыПоРегистраторам());
	ЗапросыВременныхТаблиц.Добавить(ТекстНаправленияДеятельностиДляПогрешности());
	ЗапросыВременныхТаблиц.Добавить(ТекстПогрешностиОкругления());
	ЗапросыВременныхТаблиц.Добавить(ТекстРегистраторыКОчисткеДвижений(РежимОтладкиПоИсточникам));
	ЗапросыВременныхТаблиц.Добавить(ТекстРаспределенныеАналитикиРасходов());
	ЗапросыВременныхТаблиц.Добавить(ТекстДокументыСНовымиДвижениями());
	ЗапросыВременныхТаблиц.Добавить(ТекстРезультатРаспределения(РежимОтладкиПоИсточникам));
	
	ТекстЗапроса = СтрСоединить(ЗапросыВременныхТаблиц, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Если РежимОтладкиПоИсточникам Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//РежимОтладки", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса выборки данных для распределения на финансовый результат.
// Возвращаемое значение:
//	Строка - текст запроса выборки данных.
Функция ТекстЗапросаВыборкиРаспределенияНаФинансовыйРезультат() Экспорт
	
	ЗапросыВыборки = Новый Массив;
	
	
	ЗапросыВыборки.Добавить(ТекстВыборкаРезультатРаспределения());
	
	Возврат СтрСоединить(ЗапросыВыборки, ОбщегоНазначения.РазделительПакетаЗапросов());
	
КонецФункции

#КонецОбласти

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("УчетНДС");
	
	РаспределениеПрочихЗатратЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * ТаблицаИмяРегистра - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.РаспределениеПрочихЗатрат") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		
		РаспределениеПрочихЗатратЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

#Область Описание_СтатьиКРаспределению

// Заполнение данных рабочего места распределения расходов:
// Основная функция, запускающая получения списка: см. СтатьиКРаспределению.
// Выходные данные формируются в ВТ СостояниеРаспределенияРасходов. см. ТекстЗапросаДанныеДляРаспределения.
// 
// Порядок выполнения:
// 1. Подготовка данных:
// 1.1 Инициализация запроса: см. ИнициализироватьЗапросПолученияДанныхДляРаспределения.
// 
// 1.2. Формирование служебных таблиц: см. ПодготовитьСлужебныеТаблицы.
//	- ДолиТранспортныхРасходов - см. НалоговыйУчет.ДоляТранспортныхРасходовТекущегоМесяца
//	Будет пустая, если ИсключитьТранспортныеРасходы = Истина (последний параметр процедуры).
//	
//	- ВтДолиСписанияКосвенныхРасходов - см. НалоговыйУчет.СоздатьВременнуюТаблицуДолиСписанияКосвенныхРасходов.
//	Доли списания косвенных расходов в разрезе статей расходов.
//	
//	- ВтОВЗ - см. РасчетСебестоимостиПостатейныеЗатраты.ТекстВТпоОВЗ
//	Таблица с настройками ОВЗ (варианты и правила распределения).
//	
//	- ЗначенияПоказателейОВЗ - см. РасчетСебестоимостиПостатейныеЗатраты.ТекстЗначенияПоказателейРаспределенияНаОВЗ.
//	На базе данных РС РегистрСведений.ЗначенияПоказателейДляРаспределенияРасходовНаОВЗ и правил распределения собирает показатели распределения на ОВЗ.
//	
//	- Регистраторы - см. ПодготовитьТаблицыКРасчету
//	Проведенные документы распределения прочих затрат с отборами по настройкам распределения.
//	
//	- РаспределитьПоПодразделениям - см. ПодготовитьТаблицыКРасчету.
//	На этом этапе пустая таблица.
//	
//	- РаспределитьПоЭтапам - см. ПодготовитьТаблицыКРасчету
//	Выборка регистраторов из ВТ Регистраторы с отбором по БазаРаспределенияПоПартиям.
//	
//	
// 2. Формирование текста запроса расходов к распределению: см. ТекстЗапросаДанныеДляРаспределения.
//	
// 2.1 Сначала формируется текст запроса по поступившим расходам: см. ТекстЗапросаПоступилоПрочихРасходов.
//	Формируются таблицы отборов:
//	- СтатьиРасходовКРаспределению (по видам учета и общая).
// Список статей расходов, у которых вариант распределения в одном из видов учета входит в &ВариантыРаспределенияРасходов.
// 
//	- ОВЗКРаспределению (по видам учета и общая).
// Список ОВЗ, у которых вариант распределения в одном из видов учета входит в &ВариантыРаспределенияРасходов.
// 
//	
// 2.2 Формируется текст запроса по источникам поступивших расходов
//	Данные источников разбиты на области:
//	- ТекстОписаниеДанных
//	
//	- ТекстОстаткиПрочихРасходов (Упр, Регл, НУ)
//	Остатки по РН ПрочиеРасходы.
//	
//	- ТекстДвиженияПрочихРасходов (Упр, Регл, НУ)
//	Движения по РН ПрочиеРасходы.
//	
//	- ТекстДвиженияПартийПрочихРасходов (Упр, Регл, НУ)
//	Приходные движения по РН ПарииПрочихРасходов по документам НачислениеРеверсивногоНДС.
//	
//	- ТекстДвиженияПрочихРасходовВторичногоКонтура (Упр, Регл, НУ)
//	Приходные движения вторичного контура по РН ПарииПрочихРасходов.
//	
//	- ТекстДвиженияНаСебестоимостьПроизводства (Упр, Регл, НУ)
//	Расходные движения по РН ПрочиеРасходы по документам выпуска продукции.
//	
//	- ТекстСписаниеРаспределениеТМЦ
//	Движения по РН СебестоимостьТоваров по списанию ТМЦ в производство.
//	
//	- ТекстСписаниеРаспределениеТМЦвДругуюОрганизацию
//	Движения по РН СебестоимостьТоваров по списанию ТМЦ в производство с заполненным полем КорОрганизация.
//	
//	- ТекстРаспределениеМатериалыИРаботы
//	Движения по РН МатериалыИРаботыВПроизводстве по документам РаспределениеПроизводственныхЗатрат.
//	
//	- ТекстОстаткиПрочихРасходовОВЗ (Упр, Регл, НУ, НДД)
//	Остатки по РН ПрочиеРасходы с отбором по ОВЗ.
//	
//	- ТекстДвиженияПрочихРасходовИзОВЗ (Упр, Регл, НУ, НДД)
//	Расходные движения по РН ПрочиеРасходы с отбором по ОВЗ.
//	
//	- ТекстСписаниеРаспределениеТМЦНаОВЗ
//	Движения по РН СебестоимостьТоваров по списанию ТМЦ в производство с отбором по ОВЗ.
//	
//	- ТекстРаспределениеМатериалыИРаботыНаОВЗ
//	Движения по РН МатериалыИРаботыВПроизводстве по документам РаспределениеПроизводственныхЗатрат с отбором по ОВЗ.
//	
//	- ТекстДвиженияПрочихРасходовМеждуОВЗРасход
//	Расходные движения по РН ПрочиеРасходы по документам РаспределениеПрочихЗатрат с отбором по ОВЗ и операции ТранзитРасходовМеждуОВЗ.
//	
//	- ТекстПрочихДвиженияРасходовМеждуОВЗПриход
//	Приходные движения по РН ПрочиеРасходы по документам РаспределениеПрочихЗатрат с отбором по ОВЗ и операции ТранзитРасходовМеждуОВЗ.
//	
//	- ТекстТранзитРасходовМеждуОВЗпоГрафу (общий, НДД)
//	Выборка из ВТ ВтТранзитРасходовМеждуОВЗ с отбором по ОВЗ.
//	
//	Разделение на виды учета для Упр, Регл и НУ идет через отдельные функции: см. ШаблонЗамен и см. ЗаменитьПоШаблону. 
//	Для НДД подбираются отдельными запросами.
//	
//	
// 2.3 По источникам формируются таблицы РасходыКРаспределению и РасходыКРаспределениюСводно.
//	По данным РасходыКРаспределению, формируются настройки распределения по НДД.
//	
// 2.4 Дальнейшее формирование таблиц:
//	- ПравилаРаспределенияПоНастройкам
//	Правила распределения по аналитикам прочих расходов.
//	
//	- ПравилаРаспределенияСРучнымВводом
//	Правила распределения с ручным вводом.
//	
//	- СтатьиРасходовБезСтатьиКалькуляции
//	Статьи расходов, у которых не указаны статьи калькуляции и вариант распределения НаПроизводственныеЗатраты или НаСебестоимостьПроизводства в одном из видов учета.
//	
//	- ДвиженияСтатейРасходов
//	Таблица с признаками наличия движений по аналитикам прочих расходов по видам учета:
//	1) ЕстьДвиженияМеждуОВЗ (Упр, Регл, НУ, НДД).
//	2) ЕстьДвиженияНаФинансовыйРезультат (Упр, Регл, НУ, НДД).
//	3) ЕстьДвиженияНаПроизводство (Упр, Регл, НУ, НДД).
//	4) ЕстьДвиженияНаДругиеСтатьи (Упр, Регл, НУ, НДД).
//	5) ЕстьСписаниеНУ (только НУ).
//	
//	- ДокументыРаспределения
//	Проведенные документы РаспределениеПрочихЗатрат в выбранном периоде.
//	
//	- НормированиеНаСчетахУчетах
//	Движения по РН ПрочиеРасходы по операции РаспределениеНормируемыхРасходовПоНУ.
//	
// 2.5 Собираются данные по нераспределенным расходам через вызов отдельной функции: см. ТекстЗапросаРасходыПослеРаспределенияИсточники.
//	- АктуальныеОстаткиРасходовНаКонецМесяца
//	Остатки по РН ПрочиеРасходы по аналитикам из ВТ РасходыКРаспределениюСводно.
//	Флаги ЕстьПогрешностьРасчета = Ложь
//	
//	- КорректировкаПриЗакрытииГода
//	Движения по РН ПрочиеРасходы по аналитикам из ВТ РасходыКРаспределению по документам РегламентнаяОперация и операции СписаниеКосвенныхРасходов.
//	Из сумм заполняются только СуммаНУОстаток и ПостояннаяРазницаОстаток
//	Флаги ЕстьПогрешностьРасчета = Ложь
//	
//	- КорректировкаНаФиксируемыеОстаткиНЗП
//	Выборка из ВТ РасходыКРаспределениюСводно с отбором по наличию документов распределения
//	Флаги ЕстьПогрешностьРасчета = Истина, когда существует документ распределения
//	
//	- КорректировкаНаНормированиеРасходов
//	Выборка из ВТ РасходыКРаспределению с отбором по аналитикам (статья расходов, организация) из ВтДолиСписанияКосвенныхРасходов
//	Флаги ЕстьПогрешностьРасчета = Ложь, когда сумма или доля списания = 0 или когда нет совпадающих аналитик по ВТ ДвиженияСтатейРасходов. 
//	ЕстьПогрешностьРасчета = Истина, когда есть совпадающие аналитики по ВТ ДвиженияСтатейРасходов.
//	
//	- АктуальныеОстаткиРасходовНаКонецМесяцаПоОВЗ
//	Остатки по РН ПрочиеРасходы по аналитикам из ВТ РасходыКРаспределениюСводно. АналитикаРасходов = ОВЗ. СтатьяРасходов пустая.
//	Флаги ЕстьПогрешностьРасчета = Ложь
//	
// 2.6 Дальнейшее формирование таблиц:
//	- РасходыПослеРаспределения
//	Данные по нераспределенным расходам из прошлого пункта, дополненные флагом РаспределеноВ.. по видам учета.
//	РаспределеноВ.. = Истина, когда вариант распределение входит в список, нет погрешности и сумма = 0 и когда сумма в пределах &ЗначениеПогрешностиРегл.
//	
//	- Задания
//	Задания из РС ЗаданияКРасчетуСебестоимости. По организациям подбирается минимальный месяц, в котором есть задания к расчету.
//	
//	- НераспределяемыеРасходыНаСебестоимостьТоваров
//	Выборка из РасходыКРаспределениюСводно по варианту распределения НаСебестоимостьТоваров.
//	Устанавливается флаг НеРаспределяетсяВ.. по видам учета, если по виду учета вариант = НаСебестоимостьТоваров.
//	
//	2.7 Формирование таблицы ВТДанныеДляРаспределения
//	Основная таблица с данными по распределению расходов.
//	Заполняются состояния по видам учета. 
//	Делится на 2 части в объединении - основной и по ОВЗ.
//	
//	Принцип определения состояния распределения расходов для 1-й части запроса следующий:
//	- НеТребуется, если вариант распределения не входит в &ВариантыРаспределенияРасходов.
//	- НеТребуется, если аналитика попадает в ВТ НераспределяемыеРасходыНаСебестоимостьТоваров.
//	Скорее всего некорректная аналитика расходов или расходы относятся к будущим периодам.
//	
//	- НеТребуется, если статья расходов заполнена и вариант распределения - на ОВЗ.
//	Статьи с вариантом распределения ""На ОВЗ"" обрабатываются сводно, документ не требуется.
//	
//	- НеПоступилиРасходы, если месяц закрыт, остатков и движений нет.
//	- Распределено, если месяц закрыт и расходов нет.
//	- ТребуетсяРучнойВводДокумента, если не настроено распределение долей, есть остаток к распределению и правило с ручным заполнением, 
//	то требуется настройка вручную. Состояние - ТребуетсяРучнойВводДокумента.
//	
//	- НеРаспределено, если не настроено распределение долей, но месяц закрыт.
//	- ТребуетсяСформироватьДокумент, если не настроено распределение долей, и есть остаток к распределению.
//	- ОшибкаРаспределения, если распределение долей есть, документ распределения сделал движения на производство, но есть остаток к распределению, то есть ошибка.
//	- ОшибкаРаспределения, если распределение между ОВЗ есть, документ распределения сделал движения, но есть остаток к распределению.
//	- ОшибкаРаспределения, если распределение долей есть, документ распределения сделал движения на фин. рез., но есть остаток к распределению.
//	- Распределено, если распределение долей есть, документ распределения сделал движения и статья полностью распределена.
//	- НеТребуется, если месяц не закрыт, нет остатка к распределению и сумма не расчетная.
//	- ГотовоКРаспределениюПоБазе, если ни одно из условий не выполнилось.
//	
//	По второй части (ОВЗ) принцип следующий:
//	- НеТребуется, если вариант распределения не входит в &ВариантыРаспределенияРасходов.
//	- НеПоступилиРасходы, если месяц закрыт и вариант распределения в списке: 
//		НаПроизводственныеЗатраты, 
//		НаСебестоимостьПроизводства, 
//		НаНаправленияДеятельности, 
//		НаОбъектыВозникновенияЗатрат.
//	- ТребуетсяРучнойВводДокумента, если документ распределения не создан, вариант распределения ОВЗ = НаПроизводственныеЗатраты и подобрано правило по виду учета.
//	- ТребуетсяСформироватьДокумент, если документ распределения не создан и не выполнилось предыдущее условие.
//	- ГотовоКРаспределениюПоБазе, если ни одно из условий не выполнилось.
//	
// 2.8 Формирование таблицы СостояниеРаспределенияРасходов.
//	Выборка данных из ВТДанныеДляРаспределения с дополнительными отборами:
//	- отсеиваются строки, у которых во всех видах учета состояние = НеТребуется.
//	- для всех видов учета накладывается фильтр &ФильтрПоСостоянию на состояния и дополнительная проверка вхождения варианта распределения в &ВариантыРаспределенияРасходов.
//	

#КонецОбласти 

// Возвращает список статей, по которым необходимо выполнить распределение.
//
// Параметры:
//		Период - Дата - Период выборки.
//		Организации - Массив - Список организаций по которым анализируются статьи.
//		Подразделения - Массив - Список подразделений по которым анализируются статьи.
//		Состояние - ПеречислениеСсылка.СостоянияРаспределенияРасходов - Фильтр по состоянию.
//
//	Возвращаемое значение:
//		ТаблицаЗначений - Таблица статей к распределению.
//
Функция СтатьиКРаспределению(Период, Организации, Подразделения, Состояние, ВариантРаспределения = Неопределено) Экспорт
	
	ПараметрыЗапроса = ОписаниеПараметровЗапросаПолученияДанныхДляРаспределения();
	ПараметрыЗапроса.Период = Период;
	ПараметрыЗапроса.Организации = Организации;
	ПараметрыЗапроса.Подразделения = Подразделения;
	ПараметрыЗапроса.Состояние = Состояние;
	ПараметрыЗапроса.ВариантРаспределения = ВариантРаспределения;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ИнициализироватьЗапросПолученияДанныхДляРаспределения(Запрос, ПараметрыЗапроса);
	
	Запрос.Текст = ТекстЗапросаДанныеДляРаспределения();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить();
	
КонецФункции

// Получает и помещает таблицу статей расходов для настройки распределения во временное хранилище.
// Параметры:
//	Параметры - Структура - параметры получения статей расходов:
//		* Период - Дата - месяц распределения.
//		* Организации - СправочникСсылка.Организации - организация расходов.
//		* Подразделения - Массив из СправочникСсылка.СтруктураПредприятия - подразделения расходов.
//		* Состояние - ПеречислениеСсылка.СостоянияРаспределенияРасходов - состояние распределения расхода.
//	АдресХранилища - Строка - адрес во временном хранилище, куда следует поместить результат работы.
//
Процедура СтатьиКРаспределениюВФоне(Параметры, АдресХранилища) Экспорт
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"Документ.РаспределениеПрочихЗатрат.МодульМенеджера.СтатьиКРаспределениюВФоне");
	
	Подразделения = Новый Массив;
	Если ЗначениеЗаполнено(Параметры.Подразделение) Тогда
		Подразделения.Добавить(Параметры.Подразделение);
	КонецЕсли;
	
	Организации = Новый Массив;
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		Организации.Добавить(Параметры.Организация);
	КонецЕсли;
	
	Статьи = СтатьиКРаспределению(Параметры.Период, Организации, 
		Подразделения, Параметры.Состояние);
		
	ПоместитьВоВременноеХранилище(Статьи, АдресХранилища);
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, Статьи.Количество() / 100);
	
КонецПроцедуры

// Формирует документ "Распределение прочих затрат" по заданным условиям.
// Параметры:
//	ПараметрыЗаполнения - Структура - Параметры заполнения документа.
// Возвращаемое значение:
//	Структура - Содержит сведения о документе и состоянии распределения:
//		* СостояниеУпр - ПеречислениеСсылка.СостоянияРаспределенияРасходов - состояние распределения расхода в управленческом учете.
//		* СостояниеРегл - ПеречислениеСсылка.СостоянияРаспределенияРасходов - состояние распределения расхода в регламентированном учете.
//		* ДокументУпр - ДокументСсылка.РаспределениеПрочихЗатрат - созданный документ распределения для управленческого учета.
//		* ДокументРегл - ДокументСсылка.РаспределениеПрочихЗатрат - созданный документ распределения для регламентированном учета.
//		* ОписаниеОшибкиУпр - Строка - текст ошибки возникший в результате создания документа для управленческого учета.
//		* ОписаниеОшибкиРегл - Строка - текст ошибки возникший в результате создания документа для регламентированного учета.
//
Функция СформироватьДокумент(ПараметрыЗаполнения) Экспорт
	
	ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(НСтр("ru = 'Распределение прочих затрат'"));
	
	ПараметрыВидовУчета = Новый Структура;
	ПараметрыВидовУчета.Вставить("УправленческийУчет", 
		Новый Структура("Состояние, Документ, ОписаниеОшибки", "СостояниеУпр", "ДокументУпр", "ОписаниеОшибкиУпр"));
	ПараметрыВидовУчета.Вставить("РегламентированныйУчет", 
		Новый Структура("Состояние, Документ, ОписаниеОшибки", "СостояниеРегл", "ДокументРегл", "ОписаниеОшибкиРегл"));
	
	Если РасчетСебестоимостиЛокализация.ПолучитьФункциональнуюОпциюУчетПоНДД() Тогда
		ПараметрыВидовУчета.Вставить("НДД", 
			Новый Структура("Состояние, Документ, ОписаниеОшибки", "СостояниеНДД", "ДокументНДД", "ОписаниеОшибкиНДД"));
	КонецЕсли;
	
	ДокументыКПроведению = Новый Массив; // Массив из ДокументОбъект.РаспределениеПрочихЗатрат
	ПараметрыПередачи = Новый Структура;
	
	Для Каждого ВидУчета Из ПараметрыВидовУчета Цикл
		
		РеквизитыВидаУчета = ВидУчета.Значение;
		
		Если ЗначениеЗаполнено(ПараметрыЗаполнения[РеквизитыВидаУчета.Документ])
			Или ПараметрыЗаполнения[ПараметрыВидовУчета[ВидУчета.Ключ].Состояние] = Перечисления.СостоянияРаспределенияРасходов.ТребуетсяРучнойВводДокумента
			Или ПараметрыЗаполнения[ПараметрыВидовУчета[ВидУчета.Ключ].Состояние] = Перечисления.СостоянияРаспределенияРасходов.НеТребуется Тогда
			Продолжить;
		КонецЕсли;
		
		Док = ПодобратьПодходящийДокумент(ДокументыКПроведению, ВидУчета.Ключ, ПараметрыЗаполнения);
		
		Если Не Док = Неопределено Тогда
			
			Если ЗначениеЗаполнено(ПараметрыЗаполнения[РеквизитыВидаУчета.Документ]) 
				И Не ПараметрыЗаполнения[РеквизитыВидаУчета.Документ] = Док.Ссылка Тогда
						
				Попытка
					
					ПредыдущийДокумент = ПараметрыЗаполнения[РеквизитыВидаУчета.Документ].ПолучитьОбъект(); // ДокументОбъект
					ПредыдущийДокумент.Записать(РежимЗаписиДокумента.ОтменаПроведения);
					
				Исключение
					
					ЗаписьЖурналаРегистрации(Док.Метаданные().Имя, 
						УровеньЖурналаРегистрации.Ошибка,,,
						ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					
					ШаблонОшибки = НСтр("ru = 'Не удалось обновить настройки распределения по виду учета ""%1"" по причине: %2.'", ОбщегоНазначения.КодОсновногоЯзыка());
					
					ПараметрыПередачи.Вставить(РеквизитыВидаУчета["Состояние"], 
						Перечисления.СостоянияРаспределенияРасходов.ГотовоКРаспределениюПоБазе);
					ПараметрыПередачи.Вставить(РеквизитыВидаУчета["Документ"], 
						ПараметрыЗаполнения[РеквизитыВидаУчета.Документ]);
					ПараметрыПередачи.Вставить(РеквизитыВидаУчета["ОписаниеОшибки"], 
						СтрШаблон(ШаблонОшибки, ВидУчета.Ключ, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
					
					Продолжить;
					
				КонецПопытки;
				
			КонецЕсли;
			
			Док[ВидУчета.Ключ] = Истина;
			
			Продолжить;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ПараметрыЗаполнения[РеквизитыВидаУчета.Документ])
			Или (Не Док = Неопределено 
				И Док.Ссылка = ПараметрыЗаполнения[РеквизитыВидаУчета.Документ]) Тогда
			Док = Документы.РаспределениеПрочихЗатрат.СоздатьДокумент();
		Иначе
			
			Док = ПараметрыЗаполнения[РеквизитыВидаУчета.Документ].ПолучитьОбъект();
			Док.УправленческийУчет = Ложь;
			Док.РегламентированныйУчет = Ложь;
			Док.НДД = Ложь;
			
		КонецЕсли;
			
		Док[ВидУчета.Ключ] = Истина;
		
		Док.Заполнить(ПараметрыЗаполнения);
		
		ДокументыКПроведению.Вставить(0, Док);
				
	КонецЦикла;
	
	Для Каждого ДокументКПроведению Из ДокументыКПроведению Цикл
		
		Если ДокументКПроведению.ПроверитьЗаполнение() Тогда
			Попытка
				ДокументКПроведению.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				ИнформацияОбОшибке = ИнформацияОбОшибке();
				ЗаписьЖурналаРегистрации(ДокументКПроведению.Метаданные().Имя, 
					УровеньЖурналаРегистрации.Ошибка,,, 
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
				Сообщение.УстановитьДанные(ДокументКПроведению);
				Сообщение.Сообщить();
			КонецПопытки;
		КонецЕсли;
		
		ТекстОшибки = "";
		ТекстыОшибок = Новый Массив;
		Ошибки = ПолучитьСообщенияПользователю(Истина);
		Для Индекс = 0 По Ошибки.Количество() - 1 Цикл
			ТекстыОшибок.Добавить(Ошибки[Индекс].Текст);
		КонецЦикла;
		
		ТекстОшибки = Символы.Таб + СтрСоединить(ТекстыОшибок, Символы.ПС + Символы.Таб);
			
		Для Каждого ВидУчета Из ПараметрыВидовУчета Цикл
			
			Если Не ДокументКПроведению[ВидУчета.Ключ] Тогда
				Продолжить;
			КонецЕсли;
			
			РеквизитыВидаУчета = ВидУчета.Значение;
			
			Если Не ПустаяСтрока(ТекстОшибки) Тогда
				
				ПараметрыПередачи.Вставить(РеквизитыВидаУчета["Состояние"], 
					Перечисления.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент);
				ПараметрыПередачи.Вставить(РеквизитыВидаУчета["ОписаниеОшибки"], ТекстОшибки);
				
			Иначе
				
				ПараметрыПередачи.Вставить(РеквизитыВидаУчета["Состояние"], Перечисления.СостоянияРаспределенияРасходов.ГотовоКРаспределениюПоБазе);
				ПараметрыПередачи.Вставить(РеквизитыВидаУчета["Документ"], ДокументКПроведению.Ссылка);
				
			КонецЕсли;
			
		КонецЦикла;
		
		
	КонецЦикла;
	
	Возврат ПараметрыПередачи;
	
КонецФункции

// Возвращает текст запроса для получения данных о прочих расходах.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаПоступилоПрочихРасходов(ВариантРасчета = "ПредварительныйРасчет", ИнициализацияОВЗ = Истина) Экспорт
	
	ТекстСтатьиРасходовКРаспределениюПоВидамУчета = 
		"ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Ссылка,
		|	Реквизиты.Предопределенный КАК Предопределенный,
		|	Реквизиты.ВариантРаспределенияРасходовУпр КАК ВариантРаспределенияРасходов
		|ПОМЕСТИТЬ СтатьиРасходовКРаспределениюУпр
		|ИЗ
		|	ПланВидовХарактеристик.СтатьиРасходов КАК Реквизиты
		|ГДЕ
		|	Реквизиты.ВариантРаспределенияРасходовУпр В (&ВариантыРаспределенияРасходов)
		|	И Реквизиты.ВариантРаспределенияРасходовУпр <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Ссылка,
		|	Реквизиты.Предопределенный КАК Предопределенный,
		|	Реквизиты.ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияРасходов
		|ПОМЕСТИТЬ СтатьиРасходовКРаспределениюРегл
		|ИЗ
		|	ПланВидовХарактеристик.СтатьиРасходов КАК Реквизиты
		|ГДЕ
		|	Реквизиты.ВариантРаспределенияРасходовРегл В (&ВариантыРаспределенияРасходов)
		|	И Реквизиты.ВариантРаспределенияРасходовРегл <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|
		|";
		
		ТекстСтатьиОВЗПоВидамУчета = 
		"ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Ссылка,
		|	Реквизиты.Предопределенный КАК Предопределенный,
		|	Реквизиты.ВариантРаспределенияРасходовУпр КАК ВариантРаспределенияРасходов
		|ПОМЕСТИТЬ СтатьиОВЗУпр
		|ИЗ
		|	ПланВидовХарактеристик.СтатьиРасходов КАК Реквизиты
		|ГДЕ
		|	Реквизиты.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Ссылка,
		|	Реквизиты.Предопределенный КАК Предопределенный,
		|	Реквизиты.ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияРасходов
		|ПОМЕСТИТЬ СтатьиОВЗРегл
		|ИЗ
		|	ПланВидовХарактеристик.СтатьиРасходов КАК Реквизиты
		|ГДЕ
		|	Реквизиты.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|
		|";
		
	ТекстСтатьиРасходовКРаспределениюНДД = РасчетСебестоимостиЛокализация.ТекстЗапросаСтатьиКРаспределениюНДД();
	
	ТекстСтатьиРасходовКРаспределению = 
		"ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Ссылка,
		|	Реквизиты.Предопределенный КАК Предопределенный,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка) КАК ВариантРаспределенияРасходов
		|ПОМЕСТИТЬ СтатьиРасходовКРаспределению
		|ИЗ
		|	СтатьиРасходовКРаспределениюУпр КАК Реквизиты
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Ссылка,
		|	Реквизиты.Предопределенный КАК Предопределенный,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка)
		|ИЗ
		|	СтатьиРасходовКРаспределениюРегл КАК Реквизиты
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|";
		
	
	ТекстРасходыКРаспределению = 
		"////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Организация КАК Организация,
		|	Т.Подразделение КАК Подразделение,
		|	Т.СтатьяРасходов КАК СтатьяРасходов,
		|	Т.АналитикаРасходов КАК АналитикаРасходов,
		|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Т.СуммаОстаток КАК СуммаОстаток,
		|	Т.СуммаБезНДСОстаток КАК СуммаБезНДСОстаток,
		|	Т.СуммаУпрОстаток КАК СуммаУпрОстаток,
		|	Т.СуммаРеглОстаток КАК СуммаРеглОстаток,
		|	Т.ПостояннаяРазницаОстаток КАК ПостояннаяРазницаОстаток,
		|	Т.ВременнаяРазницаОстаток КАК ВременнаяРазницаОстаток,
		|	Т.СуммаНДДОстаток КАК СуммаНДДОстаток
		|ПОМЕСТИТЬ ВтПрочиеРасходыОстатки
		|ИЗ
		|	РегистрНакопления.ПрочиеРасходы.Остатки(
		|			&ГраницаКонецПериода,
		|				(&ПоВсемОрганизациям
		|					ИЛИ Организация В (&МассивОрганизаций))
		|					И (&ПоВсемПодразделениям
		|						ИЛИ Подразделение В (&СписокПодразделений))
		|) КАК Т
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СтатьяРасходов,
		|	Организация,
		|	Подразделение,
		|	НаправлениеДеятельности,
		|	АналитикаРасходов
		|;
		|
		|// Для целей отладки предусмотрен вывод источников расходов в исходном виде
		|// ВтИсточникиРасходов
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Расходы.Организация 					КАК Организация,
		|	Расходы.Подразделение 					КАК Подразделение,
		|	Расходы.НаправлениеДеятельности 		КАК НаправлениеДеятельности,
		|	Расходы.СтатьяРасходов 					КАК СтатьяРасходов,
		|	Расходы.АналитикаРасходов 				КАК АналитикаРасходов,
		|	Расходы.НаправлениеДеятельностиДляСвязи	КАК НаправлениеДеятельностиДляСвязи,
		|	Расходы.СтатьяРасходовДляСвязи			КАК СтатьяРасходовДляСвязи,
		|	НЕ Расходы.СтатьяРасходовДляСвязи = Расходы.СтатьяРасходов КАК ЭтоРасходОВЗ,
		|	СУММА(Расходы.РасчетноеКоличество) <> 0 ИЛИ
		|		МАКСИМУМ(Расходы.РасчетнаяСумма)	КАК РасчетнаяСумма,
		|	СУММА(Расходы.Сумма) 					КАК Сумма,
		|	СУММА(Расходы.СуммаБезНДС) 				КАК СуммаБезНДС,
		|	СУММА(Расходы.СуммаРегл) 				КАК СуммаРегл,
		|	СУММА(Расходы.ПостояннаяРазница) 		КАК ПостояннаяРазница,
		|	СУММА(Расходы.ВременнаяРазница) 		КАК ВременнаяРазница,
		|	СУММА(Расходы.СуммаНДД) 				КАК СуммаНДД,
		|	СУММА(Расходы.СуммаУпр) 				КАК СуммаУпр,
		|	СУММА(Расходы.СуммаРеглРасход) 			КАК СуммаРеглРасход,
		|	СУММА(Расходы.ПостояннаяРазницаРасход) 	КАК ПостояннаяРазницаРасход,
		|	СУММА(Расходы.ВременнаяРазницаРасход) 	КАК ВременнаяРазницаРасход,
		|	СУММА(Расходы.СуммаНДДРасход) 			КАК СуммаНДДРасход
		|ПОМЕСТИТЬ РасходыКРаспределению
		|ИЗ
		|	&ИсточникиРасходов КАК Расходы
		|
		|СГРУППИРОВАТЬ ПО
		|	Расходы.Организация,
		|	Расходы.Подразделение,
		|	Расходы.НаправлениеДеятельности,
		|	Расходы.СтатьяРасходов,
		|	Расходы.АналитикаРасходов,
		|	Расходы.НаправлениеДеятельностиДляСвязи,
		|	Расходы.СтатьяРасходовДляСвязи,
		|	НЕ Расходы.СтатьяРасходовДляСвязи = Расходы.СтатьяРасходов
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СтатьяРасходов,
		|	АналитикаРасходов,
		|	Подразделение,
		|	НаправлениеДеятельности,
		|	Организация
		|";
		
	ТекстНастройкиРаспределенияСтатейНДД = РасчетСебестоимостиЛокализация.ТекстЗапросаНастроекНДД("РасходыКРаспределению", 
																	"РаспределениеПрочихЗатрат_ТекстЗапросаПоступилоПрочихРасходов");
	
	ТекстРасходыКРаспределениюСводно = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Расходы.Организация 					КАК Организация,
		|	Расходы.Подразделение 					КАК Подразделение,
		|	Расходы.НаправлениеДеятельностиДляСвязи	КАК НаправлениеДеятельности,
		|	Расходы.СтатьяРасходовДляСвязи			КАК СтатьяРасходов,
		|	Расходы.АналитикаРасходов 				КАК АналитикаРасходов,
		|	&ВариантРаспределенияРасходовУпр КАК ВариантРаспределенияРасходовУпр,
		|	&ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияРасходовРегл,
		|	&ВариантРаспределенияРасходовНУ КАК ВариантРаспределенияРасходовНУ,
		|	&ВариантРаспределенияРасходовНДД КАК ВариантРаспределенияРасходовНДД,
		|	МАКСИМУМ(Расходы.РасчетнаяСумма)		КАК РасчетнаяСумма,
		|	СУММА(Расходы.Сумма) 					КАК Сумма,
		|	СУММА(Расходы.СуммаБезНДС) 				КАК СуммаБезНДС,
		|	СУММА(Расходы.СуммаРегл) 				КАК СуммаРегл,
		|	СУММА(Расходы.ПостояннаяРазница) 		КАК ПостояннаяРазница,
		|	СУММА(Расходы.ВременнаяРазница) 		КАК ВременнаяРазница,
		|	СУММА(Расходы.СуммаНДД) 				КАК СуммаНДД,
		|	СУММА(Расходы.СуммаУпр) 				КАК СуммаУпр,
		|	СУММА(Расходы.СуммаРеглРасход) 			КАК СуммаРеглРасход,
		|	СУММА(Расходы.ПостояннаяРазницаРасход) 	КАК ПостояннаяРазницаРасход,
		|	СУММА(Расходы.ВременнаяРазницаРасход) 	КАК ВременнаяРазницаРасход,
		|	СУММА(Расходы.СуммаНДДРасход) 			КАК СуммаНДДРасход
		|ПОМЕСТИТЬ РасходыКРаспределениюСводно
		|ИЗ
		|	РасходыКРаспределению КАК Расходы
		|		ЛЕВОЕ СОЕДИНЕНИЕ НастройкиРаспределенияСтатейНДД КАК НастройкиНДД
		|			ПО Расходы.СтатьяРасходов = НастройкиНДД.СтатьяРасходов
		|			И &Расходы_ОбъектРаздельногоУчетаНДД = НастройкиНДД.ОбъектРаздельногоУчетаНДД
		|			И Расходы.Организация = НастройкиНДД.Организация
		|			И НастройкиНДД.РасходыВключаютсяВСтоимость
		|		ЛЕВОЕ СОЕДИНЕНИЕ НастройкиРаспределенияСтатейНДДПоОВЗ КАК НастройкиРаспределенияСтатейНДДПоОВЗ
		|			ПО Расходы.АналитикаРасходов = НастройкиРаспределенияСтатейНДДПоОВЗ.ОВЗ
		|			И НастройкиРаспределенияСтатейНДДПоОВЗ.РасходыВключаютсяВСтоимость
		|
		|СГРУППИРОВАТЬ ПО
		|	Расходы.Организация,
		|	Расходы.Подразделение,
		|	Расходы.АналитикаРасходов,
		|	Расходы.НаправлениеДеятельностиДляСвязи,
		|	Расходы.СтатьяРасходовДляСвязи,
		|	&ВариантРаспределенияРасходовУпр,
		|	&ВариантРаспределенияРасходовРегл,
		|	&ВариантРаспределенияРасходовНУ,
		|	&ВариантРаспределенияРасходовНДД
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СтатьяРасходов,
		|	АналитикаРасходов,
		|	Подразделение
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расходы.Организация 			КАК Организация,
		|	Расходы.Подразделение 			КАК Подразделение,
		|	Расходы.НаправлениеДеятельности	КАК НаправлениеДеятельности,
		|	Расходы.СтатьяРасходов			КАК СтатьяРасходов,
		|	Расходы.АналитикаРасходов 		КАК АналитикаРасходов
		|ПОМЕСТИТЬ РасходыКРаспределениюСводноСтатьяЗаполнена
		|ИЗ
		|	РасходыКРаспределениюСводно КАК Расходы
		|ГДЕ
		|	Расходы.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СтатьяРасходов,
		|	АналитикаРасходов,
		|	НаправлениеДеятельности,
		|	Подразделение, 
		|	Организация
		|;
		|";
		
		ТекстыЗапроса = Новый Массив;
		
		ТекстыЗапроса.Добавить(ТекстСтатьиРасходовКРаспределениюПоВидамУчета);
		
		ТекстыЗапроса.Добавить(ТекстСтатьиРасходовКРаспределениюНДД);
		ТекстыЗапроса.Добавить(ТекстСтатьиРасходовКРаспределению);
		ТекстыЗапроса.Добавить(ТекстРасходыКРаспределению);
		ТекстыЗапроса.Добавить(ТекстНастройкиРаспределенияСтатейНДД);
		ТекстыЗапроса.Добавить(ТекстРасходыКРаспределениюСводно);
		
		ТекстЗапроса = СтрСоединить(ТекстыЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
		
		ВариантУпр = "Расходы.СтатьяРасходов.ВариантРаспределенияРасходовУпр";
		ВариантРегл = "Расходы.СтатьяРасходов.ВариантРаспределенияРасходовРегл";
		ВариантНУ = "Расходы.СтатьяРасходов.ВариантРаспределенияРасходовНУ";
		ВариантНДД = "ЕСТЬNULL(НастройкиНДД.ВариантРаспределенияРасходовНДД, ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка))";
		
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВариантРаспределенияРасходовУпр", ВариантУпр);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВариантРаспределенияРасходовРегл", ВариантРегл);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВариантРаспределенияРасходовНУ", 
			?(ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет"), ВариантНУ, ВариантРегл));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВариантРаспределенияРасходовНДД", ВариантНДД);
		
		РасчетСебестоимостиЛокализация.ПодставитьВЗапросеТипОбъектаРаздельногоУчетаНДД(ТекстЗапроса, "Расходы_ОбъектРаздельногоУчетаНДД");
		
		РежимОтладкиПоИсточникам = РасчетСебестоимостиПовтИсп.ЗначенияТехнологическихПараметров().ФормироватьВтИсточникиРасходов;
		Если РежимОтладкиПоИсточникам Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
				"// ВтИсточникиРасходов", //@Query-part
				ТекстЗапросаПоступилоПрочихРасходовИсточники(ВариантРасчета)+";");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
				"&ИсточникиРасходов", //@Query-part
				"ВтИсточникиРасходов" //@Query-part
				);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
				"//РежимОтладки", //@Query-part
				""
				); 
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
				"&ИсточникиРасходов", //@Query-part
				"("+ТекстЗапросаПоступилоПрочихРасходовИсточники(ВариантРасчета)+")");
		КонецЕсли;
		
	Возврат 
		ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса для получения данных для рабочего места по распределению расходов.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаДанныеДляРаспределения(ВыбиратьПервые = 0, ПоместитьСостояниеРасходовВВТ = Ложь, ВызовДляСКД = Ложь) Экспорт
	
	// Распределяемые документы.
	ТекстЗапроса = ТекстЗапросаПоступилоПрочихРасходов("РабочееМесто", ВызовДляСКД) +
		"ВЫБРАТЬ 
		|	ДанныеПоРасходам.Организация 				КАК Организация,
		|	ДанныеПоРасходам.Подразделение 				КАК Подразделение,
		|	ДанныеПоРасходам.НаправлениеДеятельности	КАК НаправлениеДеятельности,
		|	ДанныеПоРасходам.СтатьяРасходов				КАК СтатьяРасходов,
		|	ДанныеПоРасходам.АналитикаРасходов 			КАК АналитикаРасходов,
		|					РеквизитыСтатьи.ПравилоРаспределенияРасходовРегл
		|		КАК ПравилоРаспределенияРегл,

		|					РеквизитыСтатьи.ПравилоРаспределенияРасходовУпр
		|		КАК ПравилоРаспределенияУпр,
		|	НастройкиНДД.ПравилоРаспределенияРасходовНДД КАК ПравилоРаспределенияНДД
		|ПОМЕСТИТЬ ПравилаРаспределенияПоНастройкам
		|ИЗ
		|	РасходыКРаспределениюСводно КАК ДанныеПоРасходам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК РеквизитыСтатьи
		|		ПО ДанныеПоРасходам.СтатьяРасходов = РеквизитыСтатьи.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ НастройкиРаспределенияСтатейНДД КАК НастройкиНДД
		|			ПО ДанныеПоРасходам.СтатьяРасходов = НастройкиНДД.СтатьяРасходов
		|			И &ДанныеПоРасходам_ОбъектРаздельногоУчетаНДД = НастройкиНДД.ОбъектРаздельногоУчетаНДД
		|			И ДанныеПоРасходам.Организация = НастройкиНДД.Организация
		|			И НастройкиНДД.РасходыВключаютсяВСтоимость
		|			
		|ИНДЕКСИРОВАТЬ ПО
		|	СтатьяРасходов,
		|	АналитикаРасходов,
		|	Подразделение,
		|	НаправлениеДеятельности,
		|	Организация;
		|
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДД.Организация			КАК Организация,
		|	ДД.Подразделение		КАК Подразделение,
		|	ЕСТЬNULL(РаспределениеРасходов.НаправлениеДеятельности, ДД.НаправлениеДеятельности) КАК НаправлениеДеятельности,
		|	ЕСТЬNULL(РаспределениеРасходов.СтатьяРасходов, ДД.СтатьяРасходов) КАК СтатьяРасходов,
		|	ДД.АналитикаРасходов	КАК АналитикаРасходов,
		|	МАКСИМУМ((ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		|				И (ДД.Сумма <> 0 ИЛИ ДД.СуммаБезНДС <> 0 ИЛИ ДД.СуммаУпр <> 0))) КАК ЕстьДвиженияНаФинансовыйРезультатУпр,
		|	МАКСИМУМ(НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		|				И НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
		|				И НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНормируемыхРасходовПоНУ)
		|				И (ДД.Сумма <> 0 ИЛИ ДД.СуммаБезНДС <> 0 ИЛИ ДД.СуммаУпр <> 0)) КАК ЕстьДвиженияНаПроизводствоУпр,
		|	МАКСИМУМ((ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		|				И ДД.СуммаРегл <> 0)) КАК ЕстьДвиженияНаФинансовыйРезультатРегл,
		|	МАКСИМУМ((ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		|				И (ДД.СуммаРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница <> 0))) КАК ЕстьДвиженияНаФинансовыйРезультатНал,
		|	МАКСИМУМ((ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		|				И (ДД.СуммаНДД <> 0))) КАК ЕстьДвиженияНаФинансовыйРезультатНДД,
		|	МАКСИМУМ(НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		|				И НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
		|				И НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНормируемыхРасходовПоНУ)
		|				И ДД.СуммаРегл <> 0) ЕстьДвиженияНаПроизводствоРегл,
		|	МАКСИМУМ(НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		|				И НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
		|				И НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНормируемыхРасходовПоНУ)
		|				И (ДД.СуммаРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница <> 0)) ЕстьДвиженияНаПроизводствоНал,
		|	МАКСИМУМ(НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		|				И НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
		|				И НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНормируемыхРасходовПоНУ)
		|				И НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ТранзитРасходовМеждуОВЗ)
		|				И (ДД.СуммаНДД <> 0)) ЕстьДвиженияНаПроизводствоНДД,
		|	МАКСИМУМ(ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
		|				И (ДД.Сумма <> 0 ИЛИ ДД.СуммаБезНДС <> 0 ИЛИ ДД.СуммаУпр <> 0)) ЕстьДвиженияНаДругиеСтатьиУпр,
		|	МАКСИМУМ(ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
		|				И ДД.СуммаРегл <> 0) ЕстьДвиженияНаДругиеСтатьиРегл,
		|	МАКСИМУМ(ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
		|				И (ДД.СуммаРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница <> 0)) ЕстьДвиженияНаДругиеСтатьиНал,
		|	МАКСИМУМ(ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
		|				И (ДД.СуммаНДД <> 0)) ЕстьДвиженияНаДругиеСтатьиНДД,
		|	МАКСИМУМ(ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНормируемыхРасходовПоНУ)
		|				И (ДД.ПостояннаяРазница <> 0 ИЛИ ДД.ВременнаяРазница <> 0)) КАК ЕстьСписаниеНУ,
		|	МАКСИМУМ(ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеСуммНДД)
		|				И ДД.СуммаНДД <> 0) КАК ЕстьСписаниеНДД
		|ПОМЕСТИТЬ ДвиженияСтатейРасходов
		|ИЗ
		|	РегистрНакопления.ПрочиеРасходы КАК ДД
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК РаспределениеРасходов
		|		ПО ДД.Регистратор = РаспределениеРасходов.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеДоходовПоНаправлениямДеятельности КАК РаспределениеДоходов
		|		ПО ДД.Регистратор = РаспределениеДоходов.Ссылка
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И (&ПоВсемОрганизациям ИЛИ ДД.Организация В (&МассивОрганизаций))
		|	И (&ПоВсемПодразделениям ИЛИ ДД.Подразделение В (&СписокПодразделений))
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И НЕ (РаспределениеРасходов.Ссылка ЕСТЬ NULL
		|		И РаспределениеДоходов.Ссылка ЕСТЬ NULL)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ЕСТЬNULL(РаспределениеРасходов.НаправлениеДеятельности, ДД.НаправлениеДеятельности),
		|	ЕСТЬNULL(РаспределениеРасходов.СтатьяРасходов, ДД.СтатьяРасходов),
		|	ДД.АналитикаРасходов
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СтатьяРасходов,
		|	АналитикаРасходов,
		|	Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Реквизиты.Ссылка КАК Документ,
		|	Реквизиты.УправленческийУчет,
		|	Реквизиты.РегламентированныйУчет,
		|	Реквизиты.НалоговыйУчет,
		|	Реквизиты.НДД,
		|	Реквизиты.НазначениеНастройкиРаспределения КАК НазначениеНастройкиРаспределения,
		|	Реквизиты.Организация КАК Организация,
		|	Реквизиты.Дата КАК Дата,
		|	Реквизиты.Подразделение КАК Подразделение,
		|	Реквизиты.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Реквизиты.СтатьяРасходов КАК СтатьяРасходов,
		|	Реквизиты.АналитикаРасходов КАК АналитикаРасходов
		|ПОМЕСТИТЬ ДокументыРаспределения
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат КАК Реквизиты
		|ГДЕ
		|	Реквизиты.Дата МЕЖДУ &НачалоПериода И КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)
		|	И (&ПоВсемОрганизациям ИЛИ Реквизиты.Организация В (&МассивОрганизаций))
		|	И (&ПоВсемПодразделениям ИЛИ Реквизиты.Подразделение В (&СписокПодразделений))
		|	И Реквизиты.Проведен
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СтатьяРасходов,
		|	АналитикаРасходов,
		|	Подразделение,
		|	Организация,
		|	НаправлениеДеятельности
		|;
		|
		|// ВтИсточникиНераспределенныхРасходов
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НеРаспределенные.Организация КАК Организация,
		|	НеРаспределенные.Подразделение КАК Подразделение,
		|	НеРаспределенные.СтатьяРасходов КАК СтатьяРасходов,
		|	НеРаспределенные.АналитикаРасходов КАК АналитикаРасходов,
		|	НеРаспределенные.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ВЫБОР
		|		КОГДА НЕ НеРаспределенные.ВариантРаспределенияРасходовУпр В (
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров),
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности))
		|			ТОГДА ИСТИНА
		|		КОГДА НЕ МАКСИМУМ(НеРаспределенные.ЕстьПогрешностьРасчетаУУ)
		|			ТОГДА (СУММА(НеРаспределенные.СуммаОстаток) = 0
		|					И СУММА(НеРаспределенные.СуммаБезНДСОстаток) = 0
		|					И СУММА(НеРаспределенные.СуммаУпрОстаток) = 0)
		|		ИНАЧЕ (СУММА(НеРаспределенные.СуммаОстаток) МЕЖДУ -&ЗначениеПогрешностиУпр И &ЗначениеПогрешностиУпр
		|			И СУММА(НеРаспределенные.СуммаБезНДСОстаток) МЕЖДУ -&ЗначениеПогрешностиУпр И &ЗначениеПогрешностиУпр
		|			И СУММА(НеРаспределенные.СуммаУпрОстаток) МЕЖДУ -&ЗначениеПогрешностиУпр И &ЗначениеПогрешностиУпр)
		|	КОНЕЦ КАК РаспределеноВУУ,
		|	ВЫБОР
		|		КОГДА НЕ НеРаспределенные.ВариантРаспределенияРасходовРегл В (
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров),
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности))
		|			ТОГДА ИСТИНА
		|		КОГДА НЕ МАКСИМУМ(НеРаспределенные.ЕстьПогрешностьРасчетаБУ)
		|			ТОГДА СУММА(НеРаспределенные.СуммаРеглОстаток) = 0
		|		ИНАЧЕ СУММА(НеРаспределенные.СуммаРеглОстаток) МЕЖДУ -&ЗначениеПогрешностиРегл И &ЗначениеПогрешностиРегл
		|	КОНЕЦ КАК РаспределеноВБУ,
		|	ВЫБОР
		|		КОГДА НЕ НеРаспределенные.ВариантРаспределенияРасходовНУ В (
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров),
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности))
		|			ТОГДА ИСТИНА
		|		КОГДА НЕ МАКСИМУМ(НеРаспределенные.ЕстьПогрешностьРасчетаНУ)
		|			ТОГДА (СУММА(НеРаспределенные.СуммаНУОстаток) = 0)
		|		ИНАЧЕ (СУММА(НеРаспределенные.СуммаНУОстаток) МЕЖДУ -&ЗначениеПогрешностиРегл И &ЗначениеПогрешностиРегл)
		|	КОНЕЦ КАК  РаспределеноВНУ,
		|	ВЫБОР
		|		КОГДА НЕ НеРаспределенные.ВариантРаспределенияРасходовНДД В (
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства),
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат),
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров),
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности))
		|			ТОГДА ИСТИНА
		|		КОГДА НЕ МАКСИМУМ(НеРаспределенные.ЕстьПогрешностьРасчетаНДД)
		|			ТОГДА (СУММА(НеРаспределенные.СуммаНДДОстаток) = 0)
		|		ИНАЧЕ (СУММА(НеРаспределенные.СуммаНДДОстаток) МЕЖДУ -&ЗначениеПогрешностиРегл И &ЗначениеПогрешностиРегл)
		|	КОНЕЦ КАК  РаспределеноВНДД
		|ПОМЕСТИТЬ РасходыПослеРаспределения
		|ИЗ
		|	&ИсточникиНераспределенныхРасходов КАК НеРаспределенные
		|
		|СГРУППИРОВАТЬ ПО
		|	НеРаспределенные.Организация,
		|	НеРаспределенные.Подразделение,
		|	НеРаспределенные.НаправлениеДеятельности,
		|	НеРаспределенные.СтатьяРасходов,
		|	НеРаспределенные.АналитикаРасходов,
		|	НеРаспределенные.ВариантРаспределенияРасходовУпр,
		|	НеРаспределенные.ВариантРаспределенияРасходовРегл,
		|	НеРаспределенные.ВариантРаспределенияРасходовНУ,
		|	НеРаспределенные.ВариантРаспределенияРасходовНДД
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НеРаспределенные.СтатьяРасходов,
		|	НеРаспределенные.АналитикаРасходов,
		|	НеРаспределенные.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Т.Организация КАК Организация,
		|	МИНИМУМ(Т.Месяц) КАК Месяц
		|ПОМЕСТИТЬ Задания
		|ИЗ
		|	(ВЫБРАТЬ
		|		Задания.Организация КАК Организация,
		|		НАЧАЛОПЕРИОДА(Задания.Месяц, МЕСЯЦ) КАК Месяц
		|	ИЗ
		|		РегистрСведений.ЗаданияКРасчетуСебестоимости КАК Задания
		|	ГДЕ
		|		(&ПоВсемОрганизациям
		|				ИЛИ Задания.Организация В (&МассивОрганизаций))
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		// Текст запроса должен быть аналогичен функции ЕстьДокументыПредварительногоРасчетаСебестоимости()
		// общего модуля РасчетСебестоимостиПрикладныеАлгоритмы.
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		РасчетСебестоимостиТоваров.Организация КАК Организация,
		|		НАЧАЛОПЕРИОДА(РасчетСебестоимостиТоваров.Дата, МЕСЯЦ) КАК Месяц
		|	ИЗ
		|		Документ.РасчетСебестоимостиТоваров КАК РасчетСебестоимостиТоваров
		|	ГДЕ
		|		РасчетСебестоимостиТоваров.Организация.Ссылка В (&МассивОрганизаций)
		|		И РасчетСебестоимостиТоваров.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И РасчетСебестоимостиТоваров.Проведен
		|		И РасчетСебестоимостиТоваров.РежимЗакрытияМесяца
		|			= ЗНАЧЕНИЕ(Перечисление.РежимыЗакрытияМесяца.ПредварительноеЗакрытие)
		|	) КАК Т
		|
		|СГРУППИРОВАТЬ ПО
		|	Т.Организация
		|;
		|
		|ВЫБРАТЬ
		|	Т.Организация КАК Организация,
		|	Т.Подразделение КАК Подразделение,
		|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Т.СтатьяРасходов КАК СтатьяРасходов,
		|	Т.АналитикаРасходов КАК АналитикаРасходов,
		|	СтатьяРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров) КАК НеРаспределяетсяВУпр,
		|	СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров) КАК НеРаспределяетсяВРегл,
		|	СтатьяРасходов.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров) КАК НеРаспределяетсяВНУ,
		|	Т.ВариантРаспределенияРасходовНДД = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров) КАК НеРаспределяетсяВНДД
		|ПОМЕСТИТЬ НераспределяемыеРасходыНаСебестоимостьТоваров
		|ИЗ
		|	РасходыКРаспределениюСводно КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
		|		ПО Т.СтатьяРасходов = СтатьиРасходов.Ссылка
		|ГДЕ
		|	(СтатьяРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|		ИЛИ СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|		ИЛИ СтатьяРасходов.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|		ИЛИ Т.ВариантРаспределенияРасходовНДД = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|	)
		|	И ИСТИНА В (ВЫБРАТЬ ПЕРВЫЕ 1 ИСТИНА
		|		ИЗ РегистрСведений.РеестрДокументов КАК ДД
		|		ГДЕ ДД.Ссылка = Т.АналитикаРасходов
		|		И (ДД.ДатаДокументаИБ > &Конецпериода 
		|			ИЛИ НЕ ДД.Проведен)
		|	)
		|ИНДЕКСИРОВАТЬ ПО
		|	СтатьяРасходов,
		|	АналитикаРасходов,
		|	Подразделение,
		|	Организация,
		|	НаправлениеДеятельности
		|;
		|
		|ВЫБРАТЬ
		|	Источник.Организация КАК Организация,
		|	Источник.Подразделение КАК Подразделение,
		|	Источник.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Источник.СтатьяРасходов КАК СтатьяРасходов,
		|	Источник.АналитикаРасходов КАК АналитикаРасходов,
		|	ЕСТЬNULL(ДокументыУпр.Документ, ЗНАЧЕНИЕ(Документ.РаспределениеПрочихЗатрат.ПустаяСсылка)) КАК ДокументУпр,
		|	ЕСТЬNULL(ДокументыРегл.Документ, ЗНАЧЕНИЕ(Документ.РаспределениеПрочихЗатрат.ПустаяСсылка)) КАК ДокументРегл,
		|	ЕСТЬNULL(ДокументыНДД.Документ, ЗНАЧЕНИЕ(Документ.РаспределениеПрочихЗатрат.ПустаяСсылка)) КАК ДокументНДД,
		|	Источник.ВариантРаспределенияРасходовУпр КАК ВариантРаспределенияРасходовУпр,
		|	Источник.ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияРасходовРегл,
		|	Источник.ВариантРаспределенияРасходовНУ КАК ВариантРаспределенияРасходовНУ,
		|	Источник.ВариантРаспределенияРасходовНДД КАК ВариантРаспределенияРасходовНДД,
		|	Источник.Сумма КАК Сумма,
		|	Источник.СуммаБезНДС КАК СуммаБезНДС,
		|	Источник.СуммаРегл КАК СуммаРегл,
		|	Источник.СуммаНДД КАК СуммаНДД,
		|	Источник.СуммаУпр КАК СуммаУпр,
		|	Источник.ПостояннаяРазница КАК ПостояннаяРазница,
		|	Источник.ВременнаяРазница КАК ВременнаяРазница,
		|	ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА) КАК РаспределеноВУУ,
		|	ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА) КАК РаспределеноВБУ,
		|	ЕСТЬNULL(Остатки.РаспределеноВНУ, ИСТИНА) КАК РаспределеноВНУ,
		|	ЕСТЬNULL(Остатки.РаспределеноВНДД, ИСТИНА) КАК РаспределеноВНДД,
		
		//////////////////////////////////////////////////////////////////
		// Состояние УУ
		//////////////////////////////////////////////////////////////////
		|	ВЫБОР
		// отличные варианты распределения от перечисленных, рабочее место не обслуживает.
		|		КОГДА НЕ Источник.ВариантРаспределенияРасходовУпр В (&ВариантыРаспределенияРасходов)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		// аналитика распределения некорректна либо относится к будущим периодам закрытия
		|		КОГДА ЕСТЬNULL(НераспределяемыеРасходы.НеРаспределяетсяВУпр, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)

		// если месяц закрыт, остатков и движений нет
		|		КОГДА (Задания.Месяц > &КонецПериода ИЛИ Задания.Месяц ЕСТЬ NULL)
		|			И ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА)
		|			И НЕ (
		|				ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатУпр, ЛОЖЬ) 
		|				ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоУпр, ЛОЖЬ)
		|				ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаДругиеСтатьиУпр, ЛОЖЬ)
		|			)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеПоступилиРасходы)
		// если месяц закрыт и расходов нет, то состояние Распределено
		|		КОГДА (Задания.Месяц > &КонецПериода ИЛИ Задания.Месяц ЕСТЬ NULL) 
		|			И ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.Распределено)

		// Если не настроено распределение долей, но месяц закрыт, то не распределено.
		|		КОГДА Задания.Месяц > &КонецПериода И ДокументыУпр.Документ ЕСТЬ NULL
		|		 И Источник.ВариантРаспределенияРасходовУпр <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеРаспределено)
		// Если не настроено распределение долей, и есть остаток к распределению, то требуется настройка.
		|		КОГДА ДокументыУпр.Документ ЕСТЬ NULL И (НЕ ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА) ИЛИ Источник.РасчетнаяСумма)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент)
		// Если распределение долей есть, документ распределения сделал движения на производство, но есть остаток к распределению, то есть ошибка.
		|		КОГДА Задания.Месяц > &КонецПериода И НЕ ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА)
		|			И Источник.ВариантРаспределенияРасходовУпр 
		|				В (ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
		|					ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства))
		|			И ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоУпр, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)

		// Если распределение долей есть, документ распределения сделал движения на фин. рез., но есть остаток к распределению, то есть ошибка.
		|		КОГДА Задания.Месяц > &КонецПериода И НЕ ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА)
		|			И Источник.ВариантРаспределенияРасходовУпр
		|				= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			И ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатУпр, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
		// Если распределение долей есть, документ распределения сделал движения и статья полностью распределена то расходы распределены.
		|		КОГДА ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА)
		|			И (ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатУпр, ЛОЖЬ) 
		|				ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоУпр, ЛОЖЬ) 
		|				ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаДругиеСтатьиУпр, ЛОЖЬ)
		|			)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.Распределено)
		
		// Если месяц не закрыт, нет остатка к распределению и сумма не расчетная
		|		КОГДА Задания.Месяц < &КонецПериода И ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА) И НЕ Источник.РасчетнаяСумма
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ГотовоКРаспределениюПоБазе)
		|	КОНЕЦ КАК СостояниеУУ,
		//////////////////////////////////////////////////////////////////
		// Состояние БУ
		//////////////////////////////////////////////////////////////////
		|	ВЫБОР
		|		КОГДА Не Источник.ВариантРаспределенияРасходовРегл В (&ВариантыРаспределенияРасходов)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|		КОГДА ЕСТЬNULL(НераспределяемыеРасходы.НеРаспределяетсяВРегл, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|		КОГДА (Задания.Месяц > &КонецПериода ИЛИ Задания.Месяц ЕСТЬ NULL) 
		|			И ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА)
		|			И НЕ (ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатРегл, ЛОЖЬ) ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоРегл, ЛОЖЬ)
		|					ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаДругиеСтатьиРегл, ЛОЖЬ)
		|				)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеПоступилиРасходы)
		|		КОГДА (Задания.Месяц > &КонецПериода ИЛИ Задания.Месяц ЕСТЬ NULL) 
		|			И ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.Распределено)
		|		КОГДА Задания.Месяц > &КонецПериода И ДокументыРегл.Документ ЕСТЬ NULL
		|		 И Источник.ВариантРаспределенияРасходовРегл <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеРаспределено)
		|		КОГДА ДокументыРегл.Документ ЕСТЬ NULL И (НЕ ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА) ИЛИ Источник.РасчетнаяСумма)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент)
		|		КОГДА Задания.Месяц > &КонецПериода И НЕ ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА)
		|			И Источник.ВариантРаспределенияРасходовРегл 
		|				В (ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
		|					ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства))
		|			И ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоРегл, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
		|		КОГДА Задания.Месяц > &КонецПериода И НЕ ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА)
		|			И Источник.ВариантРаспределенияРасходовРегл 
		|				= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			И ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатРегл, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
		|		КОГДА ЕСТЬNULL(НераспределяемыеРасходы.НеРаспределяетсяВНУ, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|		КОГДА ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА)
		|			И (ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатРегл, ЛОЖЬ) 
		|				ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоРегл, ЛОЖЬ) 
		|			)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.Распределено)
		|		КОГДА Задания.Месяц < &КонецПериода И ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА) И НЕ Источник.РасчетнаяСумма
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ГотовоКРаспределениюПоБазе)
		|	КОНЕЦ КАК СостояниеБУ

		//////////////////////////////////////////////////////////////////
		// Состояние НДД
		//////////////////////////////////////////////////////////////////
		|	,ВЫБОР
		|		КОГДА ИСТИНА
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|	КОНЕЦ КАК СостояниеНДД
		|
		|ПОМЕСТИТЬ ВТДанныеДляРаспределения
		|ИЗ
		|	РасходыКРаспределениюСводно КАК Источник
		|		ЛЕВОЕ СОЕДИНЕНИЕ Задания КАК Задания
		|		ПО Источник.Организация = Задания.Организация
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РасходыПослеРаспределения КАК Остатки
		|		ПО	Источник.СтатьяРасходов 			= Остатки.СтатьяРасходов
		|			И Источник.АналитикаРасходов 		= Остатки.АналитикаРасходов
		|			И Источник.Подразделение			= Остатки.Подразделение
		|			И Источник.Организация				= Остатки.Организация
		|			И Источник.НаправлениеДеятельности	= Остатки.НаправлениеДеятельности
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументыРаспределения КАК ДокументыУпр
		|		ПО	Источник.СтатьяРасходов				= ДокументыУпр.СтатьяРасходов
		|			И Источник.АналитикаРасходов 		= ДокументыУпр.АналитикаРасходов
		|			И Источник.Подразделение			= ДокументыУпр.Подразделение
		|			И Источник.НаправлениеДеятельности	= ДокументыУпр.НаправлениеДеятельности
		|			И Источник.Организация				= ДокументыУпр.Организация
		|			И ДокументыУпр.УправленческийУчет
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументыРаспределения КАК ДокументыРегл
		|		ПО	Источник.СтатьяРасходов				= ДокументыРегл.СтатьяРасходов
		|			И Источник.АналитикаРасходов 		= ДокументыРегл.АналитикаРасходов
		|			И Источник.Подразделение			= ДокументыРегл.Подразделение
		|			И Источник.НаправлениеДеятельности	= ДокументыРегл.НаправлениеДеятельности
		|			И Источник.Организация				= ДокументыРегл.Организация
		|			И ДокументыРегл.РегламентированныйУчет
		|
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументыРаспределения КАК ДокументыНДД
		|		ПО	Источник.СтатьяРасходов				= ДокументыНДД.СтатьяРасходов
		|			И Источник.АналитикаРасходов 		= ДокументыНДД.АналитикаРасходов
		|			И Источник.Подразделение			= ДокументыНДД.Подразделение
		|			И Источник.НаправлениеДеятельности	= ДокументыНДД.НаправлениеДеятельности
		|			И Источник.Организация				= ДокументыНДД.Организация
		|			И ДокументыНДД.НДД
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияСтатейРасходов КАК ДвиженияСтатейРасходов
		|		ПО	Источник.СтатьяРасходов				= ДвиженияСтатейРасходов.СтатьяРасходов
		|			И Источник.АналитикаРасходов 		= ДвиженияСтатейРасходов.АналитикаРасходов
		|			И Источник.Подразделение			= ДвиженияСтатейРасходов.Подразделение
		|			И Источник.НаправлениеДеятельности	= ДвиженияСтатейРасходов.НаправлениеДеятельности
		|			И Источник.Организация				= ДвиженияСтатейРасходов.Организация
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ НераспределяемыеРасходыНаСебестоимостьТоваров КАК НераспределяемыеРасходы
		|		ПО	Источник.СтатьяРасходов				= НераспределяемыеРасходы.СтатьяРасходов
		|			И Источник.АналитикаРасходов 		= НераспределяемыеРасходы.АналитикаРасходов
		|			И Источник.Подразделение			= НераспределяемыеРасходы.Подразделение
		|			И Источник.НаправлениеДеятельности	= НераспределяемыеРасходы.НаправлениеДеятельности
		|			И Источник.Организация				= НераспределяемыеРасходы.Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 100
		|	ВТДанныеДляЗагрузки.Организация КАК Организация,
		|	ВТДанныеДляЗагрузки.Подразделение КАК Подразделение,
		|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	&СтатьяРасходов КАК СтатьяРасходов,
		|	ЕСТЬNULL(СтатьиРасходов.Родитель, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) КАК ГруппаСтатей,
		|	ВТДанныеДляЗагрузки.АналитикаРасходов КАК АналитикаРасходов,
		|	ВТДанныеДляЗагрузки.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК ЭтоОВЗ,
		|	ВТДанныеДляЗагрузки.ДокументУпр КАК ДокументУпр,
		|	ВТДанныеДляЗагрузки.ДокументРегл КАК ДокументРегл,
		|	ВТДанныеДляЗагрузки.ДокументНДД КАК ДокументНДД,
		|	ВТДанныеДляЗагрузки.СостояниеУУ КАК СостояниеУпр,
		|	ВТДанныеДляЗагрузки.СостояниеБУ КАК СостояниеРегл
		|	,
		|	ВТДанныеДляЗагрузки.СостояниеНДД КАК СостояниеНДД
		|ПОМЕСТИТЬ СостояниеРаспределенияРасходов
		|ИЗ
		|	ВТДанныеДляРаспределения КАК ВТДанныеДляЗагрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
		|		ПО ВТДанныеДляЗагрузки.СтатьяРасходов = СтатьиРасходов.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НаправленияДеятельности КАК СпрНаправленияДеятельности
		|		ПО ВТДанныеДляЗагрузки.НаправлениеДеятельности = СпрНаправленияДеятельности.Ссылка
		|ГДЕ
		|	ВЫБОР
		|		КОГДА &ФильтрПоСостоянию = ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ 
		|			(ВТДанныеДляЗагрузки.ВариантРаспределенияРасходовУпр В (&ВариантыРаспределенияРасходов)
		|				 И ВТДанныеДляЗагрузки.СостояниеУУ = &ФильтрПоСостоянию)
		|			ИЛИ
		|			(ВТДанныеДляЗагрузки.ВариантРаспределенияРасходовРегл В (&ВариантыРаспределенияРасходов)
		|				 И ВТДанныеДляЗагрузки.СостояниеБУ = &ФильтрПоСостоянию)
		|			ИЛИ
		|			(ВТДанныеДляЗагрузки.ВариантРаспределенияРасходовНДД В (&ВариантыРаспределенияРасходов)
		|				 И ВТДанныеДляЗагрузки.СостояниеНДД = &ФильтрПоСостоянию)
		|	КОНЕЦ
		|	И НЕ (
		|		ВТДанныеДляЗагрузки.СостояниеУУ В (
		|			ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется), 
		|			ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеПоступилиРасходы))
		|		И ВТДанныеДляЗагрузки.СостояниеБУ В (
		|			ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется), 
		|			ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеПоступилиРасходы))
		|		И ВТДанныеДляЗагрузки.СостояниеНДД = ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|		)
		|;
		|";
	
	РежимОтладкиПоИсточникам = РасчетСебестоимостиПовтИсп.ЗначенияТехнологическихПараметров().ФормироватьВтИсточникиРасходов;
	Если РежимОтладкиПоИсточникам Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
			"// ВтИсточникиНераспределенныхРасходов", //@Query-part
			ТекстЗапросаРасходыПослеРаспределенияИсточники()+";");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
			"&ИсточникиНераспределенныхРасходов", //@Query-part
			"ВтИсточникиНераспределенныхРасходов" //@Query-part
			); 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
			"//РежимОтладки", //@Query-part
			""
			); 
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
			"&ИсточникиНераспределенныхРасходов", //@Query-part 
			"("+ТекстЗапросаРасходыПослеРаспределенияИсточники()+")");
	КонецЕсли;
	
	ЗаменяемыйТекст = 
		"ПЕРВЫЕ 100"; //@Query-part
	Если ВыбиратьПервые > 0 Тогда
		ЗаменяющийТекст = 
			"ПЕРВЫЕ " //@Query-part
			+ Строка(ВыбиратьПервые); 
	Иначе
		ЗаменяющийТекст = ""; 
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменяемыйТекст, ЗаменяющийТекст);
	
	// Статья расходов и направление деятельности могут быть пустыми ссылками. 
	// Для проверок нужно заменить на NULL, чтобы не попали в отборы по регистрам.
	Если Не ПоместитьСостояниеРасходовВВТ Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
			"ПОМЕСТИТЬ СостояниеРаспределенияРасходов", //@Query-part
			"");
			
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
			"&СтатьяРасходов КАК СтатьяРасходов", //@Query-part
			"ВТДанныеДляЗагрузки.СтатьяРасходов КАК СтатьяРасходов" //@Query-part
			);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
			"&НаправлениеДеятельности КАК НаправлениеДеятельности", //@Query-part
			"ВТДанныеДляЗагрузки.НаправлениеДеятельности КАК НаправлениеДеятельности" //@Query-part
			);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
			"&СтатьяРасходов КАК СтатьяРасходов", //@Query-part
			"СтатьиРасходов.Ссылка КАК СтатьяРасходов" //@Query-part
			);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
			"&НаправлениеДеятельности КАК НаправлениеДеятельности", //@Query-part
			"СпрНаправленияДеятельности.Ссылка КАК НаправлениеДеятельности" //@Query-part
			);
	КонецЕсли;
	
	// Список имен параметров НДД в соединениях для замены. 
	ПараметрыОбъектаРаздельногоУчетаНДД = Новый Массив; //Массив из Строка
	ПараметрыОбъектаРаздельногоУчетаНДД.Добавить("Источник_ОбъектРаздельногоУчетаНДД");
	ПараметрыОбъектаРаздельногоУчетаНДД.Добавить("НеРаспределенные_ОбъектРаздельногоУчетаНДД");
	ПараметрыОбъектаРаздельногоУчетаНДД.Добавить("ДанныеПоРасходам_ОбъектРаздельногоУчетаНДД");
	ПараметрыОбъектаРаздельногоУчетаНДД.Добавить("РегламентныеДвижения_ОбъектРаздельногоУчетаНДД");
	ПараметрыОбъектаРаздельногоУчетаНДД.Добавить("Расходы_ОбъектРаздельногоУчетаНДД");
	
	СписокПараметровОбъектаРаздельногоУчетаНДД = СтрСоединить(ПараметрыОбъектаРаздельногоУчетаНДД, ",");
	
	РасчетСебестоимостиЛокализация.ПодставитьВЗапросеТипОбъектаРаздельногоУчетаНДД(ТекстЗапроса, СписокПараметровОбъектаРаздельногоУчетаНДД);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Устанавливает необходимые параметры запроса ТекстЗапросаДанныеДляРаспределения().
// Параметры:
//	Запрос - Запрос - запрос с текстом см. ТекстЗапросаДанныеДляРаспределения.
//	ПараметрыЗапроса - см. ОписаниеПараметровЗапросаПолученияДанныхДляРаспределения.
//
Процедура ИнициализироватьЗапросПолученияДанныхДляРаспределения(Запрос, ПараметрыЗапроса) Экспорт
	
	ПараметрыЗакрытия = РасчетСебестоимостиПовтИсп.ЗначенияТехнологическихПараметров();
	
	ПоддерживаемыеВариантыРаспределения = Новый Массив;
	Если ПараметрыЗапроса.ВариантРаспределения = Неопределено Тогда
		
		ПоддерживаемыеВариантыРаспределения.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
		Если РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии22(НачалоМесяца(ПараметрыЗапроса.Период)) Тогда
			ПоддерживаемыеВариантыРаспределения.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ПараметрыЗапроса.ВариантРаспределения) = Тип("Массив") Тогда
		
		Для каждого ЗначениеМассива Из ПараметрыЗапроса.ВариантРаспределения Цикл
			ПоддерживаемыеВариантыРаспределения.Добавить(ЗначениеМассива);
		КонецЦикла;
		
	Иначе
		ПоддерживаемыеВариантыРаспределения.Добавить(ПараметрыЗапроса.ВариантРаспределения);
	КонецЕсли;
	
	Если Не Запрос.Параметры.Свойство("НачалоПериода")
		Или Не ЗначениеЗаполнено(Запрос.Параметры.НачалоПериода) Тогда
		
		Если ЗначениеЗаполнено(ПараметрыЗапроса.НачалоПериода) Тогда
			Запрос.УстановитьПараметр("НачалоПериода", ПараметрыЗапроса.НачалоПериода);
		Иначе
			Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(ПараметрыЗапроса.Период));
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Запрос.Параметры.Свойство("КонецПериода") 
		Или Не ЗначениеЗаполнено(Запрос.Параметры.КонецПериода) Тогда
		
		Если ЗначениеЗаполнено(ПараметрыЗапроса.КонецПериода) Тогда
			Запрос.УстановитьПараметр("КонецПериода", ПараметрыЗапроса.КонецПериода);
		Иначе
			Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(ПараметрыЗапроса.Период));
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Запрос.Параметры.Свойство("МассивОрганизаций") 
		Или Запрос.Параметры.МассивОрганизаций.Количество() = 0	Тогда
		
		Если ПараметрыЗапроса.Организации.Количество() = 0 Тогда
			Запрос.УстановитьПараметр("МассивОрганизаций", Справочники.Организации.ДоступныеОрганизации(Истина));
		Иначе
			Запрос.УстановитьПараметр("МассивОрганизаций", ПараметрыЗапроса.Организации);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ГраницаКонецПериода",  
		Новый Граница(Запрос.Параметры.КонецПериода, ВидГраницы.Включая));		
	Запрос.УстановитьПараметр("СписокПодразделений",  ПараметрыЗапроса.Подразделения);
	Запрос.УстановитьПараметр("ПоВсемПодразделениям", Запрос.Параметры.СписокПодразделений.Количество() = 0);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям",   Запрос.Параметры.МассивОрганизаций.Количество() = 0);
	Запрос.УстановитьПараметр("ФильтрПоСостоянию",    ПараметрыЗапроса.Состояние);
	Запрос.УстановитьПараметр("ИсключитьИзКонтроляРазницы", ПараметрыЗапроса.ИсключитьИзКонтроляРазницы);
	Запрос.УстановитьПараметр("ЗначениеПогрешностиУпр",  ПараметрыЗакрытия.ЗначениеПогрешностиРасходыУпр);
	Запрос.УстановитьПараметр("ЗначениеПогрешностиРегл",  ПараметрыЗакрытия.ЗначениеПогрешностиРасходыРегл);
	Запрос.УстановитьПараметр("ВариантыРаспределенияРасходов", ПоддерживаемыеВариантыРаспределения);
	Запрос.УстановитьПараметр("УчетПоНаправлениямДеятельности", ПолучитьФункциональнуюОпцию("ФормироватьФинансовыйРезультат"));
	Запрос.УстановитьПараметр("ИспользоватьУчетЗатратПоНаправлениямДеятельности", ПолучитьФункциональнуюОпцию("ИспользоватьУчетЗатратПоНаправлениямДеятельности"));
	Запрос.УстановитьПараметр("ИспользоватьРеглУчет", ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет"));

	
КонецПроцедуры

// Возвращает структуру параметров запроса ТекстЗапросаДанныеДляРаспределения() необходимых для выполнения.
// Возвращаемое значение:
//	Структура - параметры выполнения запроса получения данных распределения:
//		* Период - Дата - дата на которую требуются остатки расходов.
//		* НачалоПериода - Дата - начало периода анализа расходов.
//		* КонецПериода - Дата - конец периода анализа расходов.
//		* Организации - Массив из СправочникСсылка.Организации - организации расходов.
//		* Подразделения - Массив из СправочникСсылка.СтруктураПредприятия - подразделения расходов.
//		* Состояние - ПеречислениеСсылка.СостоянияРаспределенияРасходов - отбор по состоянию распределения.
//		* ИсключитьИзКонтроляРазницы - Булево - исключать в остатках постоянные и переменные разницы.
//		* ИсключитьТранспортныеРасходы - Булево - исключать транспортные расходы.
//		* ВариантРаспределения - Массив из ПеречислениеСсылка.ВариантыРаспределенияРасходов - отбор по варианту распределения.
Функция ОписаниеПараметровЗапросаПолученияДанныхДляРаспределения() Экспорт
	
	ОписаниеПараметров = Новый Структура;
	ОписаниеПараметров.Вставить("Период", Дата(1, 1, 1));
	ОписаниеПараметров.Вставить("НачалоПериода", Дата(1, 1, 1));
	ОписаниеПараметров.Вставить("КонецПериода", Дата(1, 1, 1));
	ОписаниеПараметров.Вставить("Организации", Новый Массив); // Тип элементов СправочникСсылка.Организации.
	ОписаниеПараметров.Вставить("Подразделения", Новый Массив); // Тип элементов СправочникСсылка.СтруктураПредприятия.
	ОписаниеПараметров.Вставить("Состояние", Перечисления.СостоянияРаспределенияРасходов.ПустаяСсылка());
	ОписаниеПараметров.Вставить("ИсключитьИзКонтроляРазницы", Ложь);
	ОписаниеПараметров.Вставить("ИсключитьТранспортныеРасходы", Ложь);
	// Можно переопределить на Массив с элементами Перечисления.ВариантыРаспределенияРасходов.
	ОписаниеПараметров.Вставить("ВариантРаспределения", Неопределено);
	
	Возврат ОписаниеПараметров;
	
КонецФункции

// Возвращает настройки распределения для набора параметров.
// Параметры:
//	ПараметрыРасходов - Массив из Структура, ТаблицаЗначений - содержит структуру со свойствами или таблицу с колонками:
//		* Дата - Дата - дата документа
//		* ИДСтроки - Число - идентификатор строки в ТЧ
//		* Документ - ДокументСсылка.РаспределениеПрочихЗатрат - созданный документ
//		* СтатьяРасходов - ПланВидовХарактеристикСсылка.СтатьиРасходов - статья расходов
//		* АналитикаРасходов - Характеристика.СтатьиРасходов - аналитика расходов статьи
//		* Организация - СправочникСсылка.Организации - организация, в которой возник расход
//		* Подразделение - СправочникСсылка.СтруктураПредприятия - подразделение, в котором возник расход
//		* НаправлениеДеятельности - СправочникСсылка.НаправленияДеятельности - направление деятельности
//	МассивОрганизаций - Массив из СправочникСсылка.Организации - массив организаций
//	ПериодРегистрации - Дата - период регистрации
//
// Возвращаемое значение:
//	Массив из Структура - Массив из параметров распределения (состав выходных параметров зависит от конфигурации):
//		* Дата - Дата - дата документа
//		* ИДСтроки - Число - идентификатор строки в ТЧ
//		* ДокументУпр - ДокументСсылка.РаспределениеПрочихЗатрат - созданный документ по упр. учету
//		* ДокументРегл - ДокументСсылка.РаспределениеПрочихЗатрат - созданный документ по регл. учету
//		* ДокументНал - ДокументСсылка.РаспределениеПрочихЗатрат - созданный документ по нал. учету
//		* ДокументНДД - ДокументСсылка.РаспределениеПрочихЗатрат - созданный документ по НДД
//		* СостояниеУпр - Строка - Состояние распределения по упр. учету
//		* СостояниеРегл - Строка - Состояние распределения по регл. учету
//		* СостояниеНал - Строка - Состояние распределения по нал. учету
//		* СостояниеНДД - Строка - Состояние распределения по НДД
//		* СтатьяРасходов - ПланВидовХарактеристикСсылка.СтатьиРасходов - статья расходов
//		* АналитикаРасходов - Характеристика.СтатьиРасходов - аналитика расходов статьи
//		* СтатьяКалькуляции - СправочникСсылка.СтатьиКалькуляции - статья расходов
//		* ВариантРаспределенияРегл - ПеречислениеСсылка.ВариантыРаспределенияРасходов - вариант распределения по регл. учету
//		* ВариантРаспределенияНал - ПеречислениеСсылка.ВариантыРаспределенияРасходов - вариант распределения по нал. учету
//		* ВариантРаспределенияУпр - ПеречислениеСсылка.ВариантыРаспределенияРасходов - вариант распределения по упр. учету
//		* ВариантРаспределенияНДД - ПеречислениеСсылка.ВариантыРаспределенияРасходов - вариант распределения по НДД
//		* Организация - СправочникСсылка.Организации - организация, в которой возник расход
//		* Подразделение - СправочникСсылка.СтруктураПредприятия - подразделение, в котором возник расход
//		* НаправлениеДеятельности - СправочникСсылка.НаправленияДеятельности - направление деятельности
//		* ПравилоРаспределенияУпр - СправочникСсылка.ПравилаРаспределенияРасходов - правило распределения по упр. учету
//		* ПравилоРаспределенияРегл - СправочникСсылка.ПравилаРаспределенияРасходов - правило распределения по регл. учету
//		* ПравилоРаспределенияНал - СправочникСсылка.ПравилаРаспределенияРасходов - правило распределения по нал. учету
//		* ПравилоРаспределенияНДД - СправочникСсылка.ПравилаРаспределенияРасходов - правило распределения по НДД.
Функция ПолучитьНастройкиРаспределенияСтатейРасходов(ПараметрыРасходов, МассивОрганизаций, ПериодРегистрации) Экспорт

	Если ТипЗнч(ПараметрыРасходов) = Тип("Массив") Тогда
		ТаблицаДанных = КонвертироватьДанные(ПараметрыРасходов);
	Иначе
		ТаблицаДанных = ПараметрыРасходов;
	КонецЕсли;
	
	ТекстЗапросаДанныеПоРасходам = "
		|ВЫБРАТЬ
		|	ДанныеПоРасходам.Дата КАК Дата,
		|	ДанныеПоРасходам.ИДСтроки КАК ИДСтроки,
		|	ДанныеПоРасходам.ДокументУпр КАК ДокументУпр,
		|	ДанныеПоРасходам.ДокументРегл КАК ДокументРегл,
		|	ДанныеПоРасходам.ДокументНДД КАК ДокументНДД,
		|	ДанныеПоРасходам.СтатьяРасходов КАК СтатьяРасходов,
		|	ДанныеПоРасходам.АналитикаРасходов КАК АналитикаРасходов,
		|	ДанныеПоРасходам.Организация КАК Организация,
		|	ДанныеПоРасходам.Подразделение КАК Подразделение,
		|	ДанныеПоРасходам.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ДанныеПоРасходам.СостояниеУпр КАК СостояниеУпр,
		|	ДанныеПоРасходам.СостояниеРегл КАК СостояниеРегл,
		|	ДанныеПоРасходам.СостояниеНДД КАК СостояниеНДД
		|ПОМЕСТИТЬ ДанныеПоРасходам
		|ИЗ
		|	&ДанныеПоРасходам КАК ДанныеПоРасходам
		|";
	
	ТекстЗапросаПредварительныеНастройки = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеПоРасходам.Дата КАК Дата,
		|	ДанныеПоРасходам.ИДСтроки КАК ИДСтроки,
		|	ДанныеПоРасходам.ДокументУпр КАК ДокументУпр,
		|	ДанныеПоРасходам.ДокументРегл КАК ДокументРегл,
		|	ДанныеПоРасходам.ДокументНДД КАК ДокументНДД,
		|	ДанныеПоРасходам.СтатьяРасходов КАК СтатьяРасходов,
		|	ДанныеПоРасходам.АналитикаРасходов КАК АналитикаРасходов,
		|	ДанныеПоРасходам.СостояниеУпр КАК СостояниеУпр,
		|	ДанныеПоРасходам.СостояниеРегл КАК СостояниеРегл,
		|	ДанныеПоРасходам.СостояниеНДД КАК СостояниеНДД,
		|	РеквизитыСтатьи.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	РеквизитыСтатьи.ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияРегл,
		|	РеквизитыСтатьи.ВариантРаспределенияРасходовУпр КАК ВариантРаспределенияУпр,
		|	ДанныеПоРасходам.Организация КАК Организация,
		|	ДанныеПоРасходам.Подразделение КАК Подразделение,
		|	ДанныеПоРасходам.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|					РеквизитыСтатьи.ПравилоРаспределенияРасходовРегл
		|		КАК ПравилоРаспределенияРегл,

		|					РеквизитыСтатьи.ПравилоРаспределенияРасходовУпр
		|		КАК ПравилоРаспределенияУпр		
		|ПОМЕСТИТЬ ПредварительныеНастройки
		|ИЗ
		|	ДанныеПоРасходам КАК ДанныеПоРасходам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК РеквизитыСтатьи
		|		ПО ДанныеПоРасходам.СтатьяРасходов = РеквизитыСтатьи.Ссылка
		|";
		
	ТекстЗапросаНастройкиРаспределенияСтатейНДД = РасчетСебестоимостиЛокализация.ТекстЗапросаНастроекНДД("ДанныеПоРасходам", "РаспределениеПрочихЗатрат_ПолучитьНастройкиРаспределенияСтатейРасходов");
	
	ТекстЗапросаНастройки = "
		|ВЫБРАТЬ
		|	Настройки.Дата КАК Дата,
		|	Настройки.ИДСтроки КАК ИДСтроки,
		|	Настройки.ДокументУпр КАК ДокументУпр,
		|	Настройки.ДокументРегл КАК ДокументРегл,
		|	Настройки.ДокументНДД КАК ДокументНДД,
		|	Настройки.СтатьяРасходов КАК СтатьяРасходов,
		|	Настройки.АналитикаРасходов КАК АналитикаРасходов,
		|	Настройки.СостояниеУпр КАК СостояниеУпр,
		|	Настройки.СостояниеРегл КАК СостояниеРегл,
		|	Настройки.СостояниеНДД КАК СостояниеНДД,
		|	Настройки.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	Настройки.ВариантРаспределенияРегл,
		|	Настройки.ВариантРаспределенияУпр,
		|	ЕСТЬNULL(НастройкиРаспределенияСтатейНДД.ВариантРаспределенияРасходовНДД, 
		|		ЕСТЬNULL(НастройкиРаспределенияСтатейНДДПоОВЗ.ВариантРаспределенияРасходовНДД, 
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка)))  КАК ВариантРаспределенияНДД,
		|	Настройки.Организация КАК Организация,
		|	Настройки.Подразделение КАК Подразделение,
		|	Настройки.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Настройки.ПравилоРаспределенияРегл,
		|	ЕСТЬNULL(НастройкиРаспределенияСтатейНДД.ПравилоРаспределенияРасходовНДД, 
		|		ЕСТЬNULL(НастройкиРаспределенияСтатейНДДПоОВЗ.ПравилоРаспределенияРасходовНДД, 
		|			ЗНАЧЕНИЕ(Справочник.ПравилаРаспределенияРасходов.ПустаяСсылка)))  КАК ПравилоРаспределенияНДД,
		|	Настройки.ПравилоРаспределенияУпр
		|ИЗ ПредварительныеНастройки КАК Настройки
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ НастройкиРаспределенияСтатейНДД КАК НастройкиРаспределенияСтатейНДД
		|		ПО Настройки.СтатьяРасходов = НастройкиРаспределенияСтатейНДД.СтатьяРасходов
		|		И &Настройки_ОбъектРаздельногоУчетаНДД = НастройкиРаспределенияСтатейНДД.ОбъектРаздельногоУчетаНДД
		|		И Настройки.Организация = НастройкиРаспределенияСтатейНДД.Организация
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ НастройкиРаспределенияСтатейНДДПоОВЗ КАК НастройкиРаспределенияСтатейНДДПоОВЗ
		|		ПО Настройки.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|		И Настройки.АналитикаРасходов = НастройкиРаспределенияСтатейНДДПоОВЗ.ОВЗ
		|";
	
	ТекстыЗапроса = Новый Массив; // Массив из Строка
	
	ТекстыЗапроса.Добавить(ТекстЗапросаДанныеПоРасходам);
	ТекстыЗапроса.Добавить(ТекстЗапросаПредварительныеНастройки);
	ТекстыЗапроса.Добавить(ТекстЗапросаНастройкиРаспределенияСтатейНДД);
	ТекстыЗапроса.Добавить(ТекстЗапросаНастройки);
		
	ТекстЗапроса = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	РасчетСебестоимостиЛокализация.ПодставитьВЗапросеТипОбъектаРаздельногоУчетаНДД(ТекстЗапроса, "Настройки_ОбъектРаздельногоУчетаНДД");
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ДанныеПоРасходам", ТаблицаДанных);
	Запрос.УстановитьПараметр("Период",           ПериодРегистрации);
	Запрос.УстановитьПараметр("Организация",      МассивОрганизаций);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	МассивНастроек = Новый Массив;
	Пока Выборка.Следующий() Цикл
		
		ФорматДанныхРаспределенияСтатьиРасходов = ФорматДанныхРаспределенияСтатьиРасходов();
		ЗаполнитьЗначенияСвойств(ФорматДанныхРаспределенияСтатьиРасходов, Выборка);
		
		МассивНастроек.Добавить(ФорматДанныхРаспределенияСтатьиРасходов);
		
	КонецЦикла;
		
	Возврат МассивНастроек;//
	
КонецФункции

// Формирует документы распределения расходов по переданным параметрам
// и помещает результат создания документов во временное хранилище.
// Параметры:
//	Параметры - Структура - параметры получения статей расходов:
//		* ПараметрыРасходов - ТаблицаЗначений - Таблица статей к распределению.
//	АдресХранилища - Строка - адрес во временном хранилище, куда следует поместить результат создания документов.
// Возвращаемое значение:
//	Соответствие - в качестве ключа документа, а в качестве значения структура.
Функция СформироватьДокументы(Параметры, АдресХранилища) Экспорт
	
	Перем ПараметрыРасходов, МассивОрганизаций, ПериодРегистрации;
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"Документ.РаспределениеПрочихЗатрат.МодульМенеджера.СформироватьДокументы");
	
	Параметры.Свойство("ПараметрыРасходов", ПараметрыРасходов);
	Параметры.Свойство("МассивОрганизаций", МассивОрганизаций);
	Параметры.Свойство("ПериодРегистрации", ПериодРегистрации);
	
	РезультатыФормированияДокументов = Новый Соответствие();
	
	ШаблонСообщения = НСтр("ru = '%6 (%7): не удалось создать документ распределения по %1 учету для статьи %2 с аналитикой %3 по причине: %4 %5.'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	НастройкиРаспределенияСтатьиРасходов = 
		ПолучитьНастройкиРаспределенияСтатейРасходов(ПараметрыРасходов, МассивОрганизаций, ПериодРегистрации);

	Для Каждого НастройкаРаспределения Из НастройкиРаспределенияСтатьиРасходов Цикл
		
		ДетализацияРезультатовФормирования = РезультатыФормированияДокументов.Получить(НастройкаРаспределения.Организация);
		Если ДетализацияРезультатовФормирования = Неопределено Тогда
			
			ДетализацияРезультатов = Новый Структура;
			ДетализацияРезультатов.Вставить("СформированоДокументов", 0);
			ДетализацияРезультатов.Вставить("ОбновленныеДанные", Новый Массив);
			ДетализацияРезультатов.Вставить("ОписаниеОшибок", Новый Массив);
			
			РезультатыФормированияДокументов.Вставить(НастройкаРаспределения.Организация,
				ДетализацияРезультатов);
				
		КонецЕсли;
		
		Результат = СформироватьДокумент(НастройкаРаспределения);
		
		МесяцНастройки = ТРег(Формат(НастройкаРаспределения.Дата, "ДФ='MMMM yyyy';"));
		
		Если Результат.Свойство("ОписаниеОшибкиРегл") Тогда
			
			ТекстСообщения = СтрШаблон(
				ШаблонСообщения,
				НСтр("ru = 'бухгалтерскому'", ОбщегоНазначения.КодОсновногоЯзыка()),
				НастройкаРаспределения.СтатьяРасходов,
				НастройкаРаспределения.АналитикаРасходов,
				Символы.ПС,
				Результат.ОписаниеОшибкиРегл,
				НастройкаРаспределения.Организация,
				МесяцНастройки);

			ОписаниеОшибки = Новый Структура;
			ОписаниеОшибки.Вставить("СтатьяРасходов", НастройкаРаспределения.СтатьяРасходов);
			ОписаниеОшибки.Вставить("ТекстОшибки", ТекстСообщения);
			
			ДетализацияРезультатов.ОписаниеОшибок.Добавить(ОписаниеОшибки);
			
		Иначе
			ДетализацияРезультатов.СформированоДокументов = ДетализацияРезультатов.СформированоДокументов + 1;
		КонецЕсли;
		
		Если Результат.Свойство("ОписаниеОшибкиНал") Тогда
			
			ТекстСообщения = СтрШаблон(
				ШаблонСообщения,
				НСтр("ru = 'налоговому'", ОбщегоНазначения.КодОсновногоЯзыка()),
				НастройкаРаспределения.СтатьяРасходов,
				НастройкаРаспределения.АналитикаРасходов,
				Символы.ПС,
				Результат.ОписаниеОшибкиНал,
				НастройкаРаспределения.Организация,
				МесяцНастройки);

			ОписаниеОшибки = Новый Структура;
			ОписаниеОшибки.Вставить("СтатьяРасходов", НастройкаРаспределения.СтатьяРасходов);
			ОписаниеОшибки.Вставить("ТекстОшибки", ТекстСообщения);
			
			ДетализацияРезультатов.ОписаниеОшибок.Добавить(ОписаниеОшибки);
			
		Иначе
			ДетализацияРезультатов.СформированоДокументов = ДетализацияРезультатов.СформированоДокументов + 1;
		КонецЕсли;
		
		Если Результат.Свойство("ОписаниеОшибкиУпр") Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСообщения,
				НСтр("ru = 'управленческому'", ОбщегоНазначения.КодОсновногоЯзыка()),
				НастройкаРаспределения.СтатьяРасходов,
				НастройкаРаспределения.АналитикаРасходов,
				Символы.ПС,
				Результат.ОписаниеОшибкиУпр,
				НастройкаРаспределения.Организация,
				МесяцНастройки);

			ОписаниеОшибки = Новый Структура;
			ОписаниеОшибки.Вставить("СтатьяРасходов", НастройкаРаспределения.СтатьяРасходов);
			ОписаниеОшибки.Вставить("ТекстОшибки", ТекстСообщения);
			
			ДетализацияРезультатов.ОписаниеОшибок.Добавить(ОписаниеОшибки);
			
		Иначе
			ДетализацияРезультатов.СформированоДокументов = ДетализацияРезультатов.СформированоДокументов + 1;
		КонецЕсли;
		
		Если Результат.Свойство("ОписаниеОшибкиНДД") Тогда
			
			ТекстСообщения = СтрШаблон(
				ШаблонСообщения,
				НСтр("ru = 'НДД'", ОбщегоНазначения.КодОсновногоЯзыка()),
				НастройкаРаспределения.СтатьяРасходов,
				НастройкаРаспределения.АналитикаРасходов,
				Символы.ПС,
				Результат.ОписаниеОшибкиНДД,
				НастройкаРаспределения.Организация,
				МесяцНастройки);

			ОписаниеОшибки = Новый Структура;
			ОписаниеОшибки.Вставить("СтатьяРасходов", НастройкаРаспределения.СтатьяРасходов);
			ОписаниеОшибки.Вставить("ТекстОшибки", ТекстСообщения);
			
			ДетализацияРезультатов.ОписаниеОшибок.Добавить(ОписаниеОшибки);
			
		Иначе
			ДетализацияРезультатов.СформированоДокументов = ДетализацияРезультатов.СформированоДокументов + 1;
		КонецЕсли;
		
		НовыеЗначения = Новый Структура("СтатьяРасходов, АналитикаРасходов, Организация, Подразделение, НаправлениеДеятельности, ИдСтроки");
		ЗаполнитьЗначенияСвойств(НовыеЗначения, НастройкаРаспределения);
		
		Для Каждого КлючИЗначение Из Результат Цикл
			НовыеЗначения.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
		
		ДетализацияРезультатов.ОбновленныеДанные.Добавить(НовыеЗначения);
		
	КонецЦикла;
		
	Если Не АдресХранилища = Неопределено Тогда
		ПоместитьВоВременноеХранилище(РезультатыФормированияДокументов, АдресХранилища);
	КонецЕсли;
	
	СформированоДокументов = 0;
	Для Каждого РезультатПоОрганизации Из РезультатыФормированияДокументов Цикл
		СформированоДокументов = СформированоДокументов + РезультатПоОрганизации.Значение.СформированоДокументов;
	КонецЦикла;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, СформированоДокументов);
	
	Возврат РезультатыФормированияДокументов;
	
КонецФункции


// Возвращает параметры выбора статей и аналитик.
// 
// Возвращаемое значение:
//	См. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики
//
Функция ПараметрыВыбораСтатейИАналитик() Экспорт 
	
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект.Списание";
	ПараметрыВыбора.Статья      = "СтатьяРасходов";
	ПараметрыВыбора.ТипСтатьи   = "ТипСтатьи";
	
	ПараметрыВыбора.АналитикаРасходов 		 = "АналитикаРасходов";
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивов";
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	
	МассивВариантов = Новый Массив;
	МассивВариантов.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
	МассивВариантов.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов);
	МассивВариантов.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров);
	МассивВариантов.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаСебестоимостьПродаж);
	МассивВариантов.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаПрочиеАктивы);
	МассивВариантов.Добавить(Перечисления.ВариантыРаспределенияРасходов.НеРаспределять);
	
	ПараметрыВыбора.ОтборСтатейРасходов.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеТоваровПоТребованию;
	ПараметрыВыбора.ОтборСтатейРасходов.ВариантРаспределенияРасходов = МассивВариантов;
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СписаниеСтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("СписаниеАналитикаРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("СписаниеАналитикаАктивовПассивов");
	
	Возврат ПараметрыВыбора;
	
КонецФункции

// Устанавливает доступность флагов использования документа для видов учета в зависимости от настроек статьи расходов
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта документа "Распределение расходов"
//
Процедура УстановитьДоступностьВидовУчета(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	УчетПоНДД = РасчетСебестоимостиЛокализация.ПолучитьФункциональнуюОпциюУчетПоНДД();
	
	Элементы.УправленческийУчет.Доступность = Истина;
	Элементы.РегламентированныйУчет.Доступность = Истина;
	НастройкиРаспределения = ПолучитьНастройкиРаспределенияПоДокументу(Объект);
	
	Если Объект.УправленческийУчет Тогда
		Элементы.РегламентированныйУчет.Доступность = Объект.РегламентированныйУчет
			ИЛИ НастройкиРаспределения.ВариантРаспределенияРасходовУпр = НастройкиРаспределения.ВариантРаспределенияРасходовРегл;
		
		
		Элементы.НДД.Доступность = УчетПоНДД И (Объект.НДД
			ИЛИ НастройкиРаспределения.ВариантРаспределенияРасходовНДД = НастройкиРаспределения.ВариантРаспределенияРасходовУпр);
	ИначеЕсли Объект.РегламентированныйУчет Тогда
		Элементы.УправленческийУчет.Доступность =
			НастройкиРаспределения.ВариантРаспределенияРасходовУпр = НастройкиРаспределения.ВариантРаспределенияРасходовРегл;
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает или снимает признак отражения в БУ в зависимости от признака НУ и настроек статьи расходов и наоборот.// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта документа "Распределение расходов"
//  ВидУчета - Строка - имя вида учета, для которого пользователь установил или снял признак отражения в документе.
//
Процедура СинхронизироватьВидыУчетаБУиНУ(Форма, ВидУчета) Экспорт
	
	Если ВидУчета = "УправленческийУчет" Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	УстановитьПризнакиБУиНУ(Объект, ВидУчета);
	
КонецПроцедуры

#Область СтандартныеПодсистемы

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	РаспределениеПрочихЗатратЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Распределение расходов на себестоимость продукции".
//
// Параметры:
//  КомандыСозданияНаОсновании - ТаблицаЗначений - описание в СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
// Возвращаемое значение:
// 	СтрокаТаблицыЗначений - описание в СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании 
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РаспределениеПрочихЗатрат) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РаспределениеПрочихЗатрат.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.РаспределениеПрочихЗатрат);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьПроизводство,ИспользоватьУчетПрочихДоходовРасходов";
	
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	РаспределениеПрочихЗатратЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	РаспределениеПрочихЗатратЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

#КонецОбласти


#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// см. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Подразделение)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если Не ВидФормы = "ФормаОбъекта" Тогда
		Возврат;
	КонецЕсли;
	
	НаФР = ПредопределенноеЗначение("Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаФинансовыйРезультат");
	НаСебестоимость = ПредопределенноеЗначение("Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаСебестоимостьТоваров");
	
	ФормыПоНазначению = Новый Соответствие;
	ФормыПоНазначению.Вставить(НаФР, "ФормаНастроекНаФР");
	ФормыПоНазначению.Вставить(НаСебестоимость, "ФормаНастроекНаСебестоимостьТоваров");
	
	Если Параметры.Свойство("Ключ") Тогда
		
		ВыбраннаяФорма = ФормыПоНазначению.Получить(
			ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(
				Параметры.Ключ, 
				"НазначениеНастройкиРаспределения"));
				
	ИначеЕсли Параметры.Свойство("Основание") Тогда
		
		НазначенияПоВарианту = Новый Соответствие;
		НазначенияПоВарианту.Вставить(
			ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности"),
			НаФР);
		НазначенияПоВарианту.Вставить(
			ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров"),
			НаСебестоимость);
			
		
		Если Параметры.Основание.УправленческийУчет Тогда
			ВыбраннаяФорма = ФормыПоНазначению.Получить(НазначенияПоВарианту.Получить(Параметры.Основание.ВариантРаспределенияУпр));
		КонецЕсли;
		
		Если Параметры.Основание.РегламентированныйУчет Тогда
			ВыбраннаяФорма = ФормыПоНазначению.Получить(НазначенияПоВарианту.Получить(Параметры.Основание.ВариантРаспределенияРегл));
		КонецЕсли;
		
		Если Параметры.Основание.НДД Тогда
			ВыбраннаяФорма = ФормыПоНазначению.Получить(НазначенияПоВарианту.Получить(Параметры.Основание.ВариантРаспределенияНДД));
		КонецЕсли;
		
	ИначеЕсли Параметры.Свойство("ЗначенияЗаполнения")
		И Параметры.ЗначенияЗаполнения.Свойство("НазначениеНастройкиРаспределения") Тогда
		ВыбраннаяФорма = ФормыПоНазначению.Получить(Параметры.ЗначенияЗаполнения.НазначениеНастройкиРаспределения);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыбраннаяФорма) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиЭтаповЗакрытияМесяца

#Область ОформлениеДокументовРаспределенияРасходов

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
//
// Параметры:
//	ТаблицаЭтапов - см. Обработки.ОперацииЗакрытияМесяца.ЗаполнитьОписаниеЭтаповЗакрытияМесяца
//	ТекущийРодитель - Строка - идентификатор группы.
Процедура ДобавитьЭтап_ОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности(ТаблицаЭтапов, ТекущийРодитель) Экспорт
	
	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(ТаблицаЭтапов, ТекущийРодитель,
		Перечисления.ОперацииЗакрытияМесяца.ОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности);
	НоваяСтрока.ТекстВыполнить = НСтр("ru = 'Оформить'");
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"Документы.РаспределениеПрочихЗатрат.Использование_ОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности");
	НоваяСтрока.ДействиеВыполнить  = ЗакрытиеМесяцаСервер.ОписаниеДействия_ВыполнитьРасчет(
		"Документы.РаспределениеПрочихЗатрат.Выполнить_ОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности");
	НоваяСтрока.ДействиеПодробнее = ЗакрытиеМесяцаСервер.ОписаниеДействия_ОткрытьФорму(
		Метаданные.Документы.РаспределениеПрочихЗатрат.Формы.ФормаРабочееМесто.ПолноеИмя());
	НоваяСтрока.ДействиеПодробнее.ПараметрыФормы.Вставить("Подразделение", Справочники.СтруктураПредприятия.ПустаяСсылка());
	НоваяСтрока.ДействиеПодробнее.ПараметрыФормы.Вставить("Состояние", Перечисления.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент);
		
КонецПроцедуры

// Обработчики этапа.

Процедура Использование_ОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности(ПараметрыОбработчика) Экспорт
	
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	
	ПараметрыЗапроса = ОписаниеПараметровЗапросаПолученияДанныхДляРаспределения();
	ПараметрыЗапроса.Состояние = Перечисления.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент;
	ПараметрыЗапроса.ВариантРаспределения =
		ОбщегоНазначенияУТКлиентСервер.Массив(Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
	Документы.РаспределениеПрочихЗатрат.ИнициализироватьЗапросПолученияДанныхДляРаспределения(Запрос, ПараметрыЗапроса);
	
	Запрос.Текст = ТекстЗапросаДанныеДляРаспределения(, Истина);
	Запрос.Выполнить();	
	
	РазмерыВременныхТаблиц = ЗакрытиеМесяцаСервер.РазмерыВременныхТаблиц(Запрос, ПараметрыОбработчика);
	
	// Для этапа уже установлено состояние "Не требуется", поэтому проверку состояния не выполняем.
	Если ПараметрыОбработчика.ДанныеЭтапа.Состояние = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется Тогда
		Возврат;
	КонецЕсли;
	
	Если РазмерыВременныхТаблиц.ВТДанныеДляРаспределения = 0 Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
			ПараметрыОбработчика,
			НСтр("ru = 'Нет данных для распределения расходов.'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
		Возврат;
		
	КонецЕсли;
	
	Если РазмерыВременныхТаблиц.СостояниеРаспределенияРасходов = 0 Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
			ПараметрыОбработчика,
			НСтр("ru = 'По всем расходам заданы настройки распределения.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	Иначе
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеВыполнен(
			ПараметрыОбработчика,
			НСтр("ru = 'Требуется оформление документов распределения расходов.'"));	
	КонецЕсли;
	
КонецПроцедуры

Процедура Выполнить_ОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности(ПараметрыОбработчика) Экспорт
	
	ПараметрыРасчета = ПараметрыОбработчика.ПараметрыРасчета;
	
	СтатьиКРаспределению = СтатьиКРаспределению(
		ПараметрыРасчета.ПериодРегистрации,
		ПараметрыРасчета.МассивОрганизаций,
		Новый Массив,
		Перечисления.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент,
		Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
		
	КоличествоСтрок = СтатьиКРаспределению.Количество();
	Если КоличествоСтрок = 0 Тогда // нет данных для распределения
		Возврат;
	КонецЕсли;
	
	СтатьиКРаспределению.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	СтатьиКРаспределению.Колонки.Добавить("ИДСтроки", Новый ОписаниеТипов("Число"));
	СтатьиКРаспределению.ЗаполнитьЗначения(КонецМесяца(ПараметрыРасчета.ПериодРегистрации), "Дата");
	
	КоличествоФЗ = РасчетСебестоимостиПовтИсп.ЗначенияТехнологическихПараметров(Перечисления.ОперацииЗакрытияМесяца.ПустаяСсылка()).МаксимальноеКоличествоФЗФормированияДокументов;
	СтрокВФЗ = Цел(КоличествоСтрок / ?(КоличествоФЗ = 0, 1, КоличествоФЗ));
	
	Если КоличествоФЗ < 2 ИЛИ СтрокВФЗ < 1 Тогда
		
		// Однопоточное формирование документов, без фоновых заданий.
		РаспределениеПрочихЗатратЛокализация.ФормированиеДокументовРаспределениеПрочихЗатрат(ПараметрыОбработчика, СтатьиКРаспределению);
		
		Возврат;
		
	КонецЕсли;
	
	ОбработаноСтрок = 0;
	МассивЗаданий   = Новый Массив;
	ТаблицаСтатей   = Неопределено;
	
	// Многопоточное формирование документов.
	Пока ОбработаноСтрок <= КоличествоСтрок Цикл
		
		Если ОбработаноСтрок = 0 ИЛИ ОбработаноСтрок = КоличествоСтрок
		 // Задание запускается при наполнении ТаблицаСтатей количеством строк, равным СтрокВФЗ.
		 // В последнее задание помещаются все оставшиеся строки, даже если их больше, чем СтрокВФЗ.
		 ИЛИ (Цел(ОбработаноСтрок/СтрокВФЗ) = ОбработаноСтрок/СтрокВФЗ И МассивЗаданий.Количество() < КоличествоФЗ-1) Тогда
			
			Если ОбработаноСтрок > 0 Тогда
				
				// Запускаем новое ФЗ
				МассивПараметров = Новый Массив;                                                                      
				МассивПараметров.Добавить(ПараметрыОбработчика);
				МассивПараметров.Добавить(ТаблицаСтатей);
				
				КлючЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'ФЗ №%1 - %2 (%3)'", ОбщегоНазначения.КодОсновногоЯзыка()),
					СокрЛП(МассивЗаданий.Количество() + 1),
					СокрЛП(ПараметрыОбработчика.ДанныеЭтапа.Код),
					СокрЛП(ПараметрыОбработчика.ИдентификаторРасчета));
				
				Задание = ЗакрытиеМесяцаСервер.ЗапуститьПодчиненноеФоновоеЗаданиеЭтапаРасчета(
					ПараметрыОбработчика.ИдентификаторРасчета,
					"РаспределениеПрочихЗатратЛокализация.ФормированиеДокументовРаспределениеПрочихЗатрат",
					МассивПараметров,
					КлючЗадания,
					НСтр("ru='Пакетное формирование документов'"));
					
		        МассивЗаданий.Добавить(Задание);
				
			КонецЕсли;
			
			Если ОбработаноСтрок = КоличествоСтрок Тогда
				// Все задания запущены
				Прервать;
			КонецЕсли;
			
			ТаблицаСтатей = СтатьиКРаспределению.СкопироватьКолонки();
			
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ТаблицаСтатей.Добавить(), СтатьиКРаспределению[ОбработаноСтрок]);
		
		ОбработаноСтрок = ОбработаноСтрок + 1;
		
	КонецЦикла;
	
	ЗаданияЗавершены = Ложь;;
	
	Пока НЕ ЗаданияЗавершены Цикл
		
		ВыполненныеЗадания = ФоновыеЗадания.ОжидатьЗавершенияВыполнения(МассивЗаданий);
		
		МассивЗаданий = Новый Массив;
		
		Для Каждого Задание Из ВыполненныеЗадания Цикл
			
			Если Задание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
				
				МассивЗаданий.Добавить(Задание);
				
			ИначеЕсли Задание.Состояние <> СостояниеФоновогоЗадания.Завершено Тогда

				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Фоновое задание формирования документов %1
					|%2'", ОбщегоНазначения.КодОсновногоЯзыка()),
					?(Задание.Состояние = СостояниеФоновогоЗадания.Отменено,
						НСтр("ru = 'отменено'", ОбщегоНазначения.КодОсновногоЯзыка()),
						НСтр("ru = 'завершено аварийно'", ОбщегоНазначения.КодОсновногоЯзыка())),
					?(Задание.ИнформацияОбОшибке = Неопределено,
						НСтр("ru = '<Нет описания ошибки>'", ОбщегоНазначения.КодОсновногоЯзыка()),
						ПодробноеПредставлениеОшибки(Задание.ИнформацияОбОшибке)));
						
				ЗакрытиеМесяцаСервер.ЗафиксироватьНаличиеПроблемыПриВыполненииРасчета(
					ПараметрыОбработчика,
					ТекстОшибки);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ЗаданияЗавершены = (МассивЗаданий.Количество() = 0);
		
	КонецЦикла;
	
КонецПроцедуры

// Проверки состояния системы, относящиеся к этапу.

Процедура ОписаниеПроверок_ОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности(ТаблицаПроверок) Экспорт
	
	// Настройка распределения расходов.
	ОписаниеПроверки = ЗакрытиеМесяцаСервер.ДобавитьОписаниеНовойПроверки(ТаблицаПроверок,
		"НеВыполненаОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности",
		Перечисления.ОперацииЗакрытияМесяца.ОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности,
		Перечисления.МоментЗапускаПроверкиОперацииЗакрытияМесяца.ПослеРасчета,
		"Документы.РаспределениеПрочихЗатрат.ПроверкаНеобходимостиОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности");
	
	ЗакрытиеМесяцаСервер.ЗаполнитьПредставлениеНовойПроверки(ОписаниеПроверки,
		НСтр("ru = 'Есть неоформленные документы распределения расходов'", ОбщегоНазначения.КодОсновногоЯзыка()),
		НСтр("ru = 'Все производственные расходы должны быть распределены по направлениям деятельности.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
КонецПроцедуры

Процедура ПроверкаНеобходимостиОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности(ПараметрыПроверки) Экспорт
	
	Если НЕ ЗакрытиеМесяцаСервер.ПроверкаВыполняетсяМеханизмомЗакрытияМесяца(ПараметрыПроверки) Тогда
		Возврат;
	КонецЕсли;
	
	СписокПолей = Новый СписокЗначений;
	СписокПолей.Добавить("Организация", 			НСтр("ru = 'Организация'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("Подразделение", 			НСтр("ru = 'Подразделение'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("НаправлениеДеятельности", НСтр("ru = 'Направление деятельности'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("СтатьяРасходов", 			НСтр("ru = 'Статья расходов'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("АналитикаРасходов", 		НСтр("ru = 'Аналитика расходов'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	ПараметрыРегистрации = ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыРегистрацииПроблемПроверки(
		"СостояниеРаспределенияРасходов",
		НСтр("ru = 'Есть неоформленные документы распределения расходов по организации ""%1"" на конец периода %2'", ОбщегоНазначения.КодОсновногоЯзыка()),
		СписокПолей);
	
	ЗакрытиеМесяцаСервер.ЗарегистрироватьПроблемыВыполненияПроверки(
	 	ПараметрыПроверки,
		ПараметрыРегистрации);
		
КонецПроцедуры

#КонецОбласти

#Область РаспределениеРасходовПоНаправлениямДеятельности

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
//
// Параметры:
// 	ТаблицаЭтапов - см. Обработки.ОперацииЗакрытияМесяца.ЗаполнитьОписаниеЭтаповЗакрытияМесяца
// 	ТекущийРодитель - Строка - идентификатор группы.
Процедура ДобавитьЭтап_РаспределениеРасходовПоНаправлениямДеятельности(ТаблицаЭтапов,ТекущийРодитель) Экспорт
	
	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(ТаблицаЭтапов, ТекущийРодитель,
		Перечисления.ОперацииЗакрытияМесяца.РаспределениеРасходовПоНаправлениямДеятельности);
	НоваяСтрока.ПредшествующиеЭтапы.Добавить(Перечисления.ОперацииЗакрытияМесяца.РасчетПартийИСебестоимости);
	НоваяСтрока.ПредшествующиеЭтапы.Добавить(Перечисления.ОперацииЗакрытияМесяца.РаспределениеНДС);
	НоваяСтрока.ПредшествующиеЭтапы.Добавить(Перечисления.ОперацииЗакрытияМесяца.ОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности);
	НоваяСтрока.ТекстВыполнить = НСтр("ru = 'Распределить'");
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"Документы.РаспределениеПрочихЗатрат.Использование_РаспределениеРасходовПоНаправлениямДеятельности");
	НоваяСтрока.ДействиеВыполнить  = ЗакрытиеМесяцаСервер.ОписаниеДействия_ВыполнитьРасчет(
		"Документы.РаспределениеПрочихЗатрат.Выполнить_РаспределениеРасходовПоНаправлениямДеятельности");
	НоваяСтрока.ДействиеПодробнее = ЗакрытиеМесяцаСервер.ОписаниеДействия_ОткрытьФорму(
		Метаданные.Документы.РаспределениеПрочихЗатрат.Формы.ФормаРабочееМесто.ПолноеИмя());
		
КонецПроцедуры

// Обработчики этапа.

Процедура Использование_РаспределениеРасходовПоНаправлениямДеятельности(ПараметрыОбработчика) Экспорт
	
	Если ПараметрыОбработчика.ДанныеЭтапа.Состояние = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется Тогда
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	
	ПараметрыЗапроса = ОписаниеПараметровЗапросаПолученияДанныхДляРаспределения();
	ПараметрыЗапроса.ВариантРаспределения =
		ОбщегоНазначенияУТКлиентСервер.Массив(Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
	
	ИнициализироватьЗапросПолученияДанныхДляРаспределения(Запрос, ПараметрыЗапроса);
	
	Запрос.Текст = ТекстЗапросаДанныеДляРаспределения();
	Результат = Запрос.Выполнить();	
	
	РазмерыВременныхТаблиц = ЗакрытиеМесяцаСервер.РазмерыВременныхТаблиц(Запрос, ПараметрыОбработчика);
	
	Если РазмерыВременныхТаблиц.ВТДанныеДляРаспределения = 0 Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
			ПараметрыОбработчика,
			НСтр("ru = 'Нет данных для распределения расходов.'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
		Возврат;
		
	КонецЕсли;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	*
		|ПОМЕСТИТЬ ВТОстаткиПрочихРасходовПредварительная
		|ИЗ
		|	ВТДанныеДляРаспределения КАК Т
		|ГДЕ
		|	НЕ Т.РаспределеноВУУ
		|		И Т.ДокументУпр.НазначениеНастройкиРаспределения = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаФинансовыйРезультат)
		|	ИЛИ НЕ Т.РаспределеноВБУ
		|		И Т.ДокументРегл.НазначениеНастройкиРаспределения = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаФинансовыйРезультат)
		//		Доп. проверка состояния распределения для нормируемых расходов, в т.ч. транспортные
		|		И НЕ Т.СостояниеБУ В (
		|			ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.Распределено), 
		|			ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеРаспределено), 
		|			ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|		)
		|;	
		|ВЫБРАТЬ
		|	*
		|ПОМЕСТИТЬ ВТОстаткиПрочихРасходов
		|ИЗ
		|	ВТОстаткиПрочихРасходовПредварительная КАК Т
		|ГДЕ
		|	Т.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|;	
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	*
		|ИЗ
		|	ВТОстаткиПрочихРасходовПредварительная";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеВыполнен(
			ПараметрыОбработчика,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Требуется пересчет за период %1.'", ОбщегоНазначения.КодОсновногоЯзыка()),
				РасчетСебестоимостиПротоколРасчета.ПредставлениеПериодаРасчета(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации)));
			
	КонецЕсли;
			
КонецПроцедуры

Процедура Выполнить_РаспределениеРасходовПоНаправлениямДеятельности(ПараметрыОбработчика) Экспорт
	
	ПараметрыРасчета = ПараметрыОбработчика.ПараметрыРасчета;
	
	НачалоПериода = НачалоМесяца(ПараметрыРасчета.ПериодРегистрации);
	КонецПериода  = КонецМесяца(ПараметрыРасчета.ПериодРегистрации);
	
	НомерЗаданияДоРасчета = ЗакрытиеМесяцаСервер.УвеличитьНомерЗадания();
	
	Попытка
		РассчитанныеОрганизации = РаспределитьРасходыНаФинансовыйРезультат(ПараметрыОбработчика);
	Исключение
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Распределение расходов по направлениям деятельности за период %1 завершилось с ошибкой:
					|%2'", ОбщегоНазначения.КодОсновногоЯзыка()),
				РасчетСебестоимостиПротоколРасчета.ПредставлениеПериодаРасчета(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации),
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗакрытиеМесяцаСервер.ЗафиксироватьНаличиеПроблемыПриВыполненииРасчета(
			ПараметрыОбработчика,
			ТекстОшибки);
				
	КонецПопытки;
	
	
	
КонецПроцедуры

// Проверки состояния системы, относящиеся к этапу.

Процедура ОписаниеПроверок_РаспределениеРасходовПоНаправлениямДеятельности(ТаблицаПроверок) Экспорт
	
	// Настройка распределения расходов.
	ОписаниеПроверки = ЗакрытиеМесяцаСервер.ДобавитьОписаниеНовойПроверки(ТаблицаПроверок,
		"ПроверкаОстатковПрочихРасходов",
		Перечисления.ОперацииЗакрытияМесяца.РаспределениеРасходовПоНаправлениямДеятельности,
		Перечисления.МоментЗапускаПроверкиОперацииЗакрытияМесяца.ПослеРасчета,
		"Документы.РаспределениеПрочихЗатрат.ПроверкаОстатковПрочихРасходов");
	
	ЗакрытиеМесяцаСервер.ЗаполнитьПредставлениеНовойПроверки(ОписаниеПроверки,
		НСтр("ru = 'Не распределены прочие расходы на финансовый результат'", ОбщегоНазначения.КодОсновногоЯзыка()),
		НСтр("ru = 'Все прочие расходы должны быть распределены на финансовый результат.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
КонецПроцедуры

Процедура ПроверкаОстатковПрочихРасходов(ПараметрыПроверки) Экспорт
	
	Если НЕ ЗакрытиеМесяцаСервер.ПроверкаВыполняетсяМеханизмомЗакрытияМесяца(ПараметрыПроверки) Тогда
		Возврат;
	КонецЕсли;
	
	СписокПолей = Новый СписокЗначений;
	СписокПолей.Добавить("Организация", 			НСтр("ru = 'Организация'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("Подразделение", 			НСтр("ru = 'Подразделение'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("НаправлениеДеятельности", НСтр("ru = 'Направление деятельности'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("СтатьяРасходов", 			НСтр("ru = 'Статья расходов'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("АналитикаРасходов", 		НСтр("ru = 'Аналитика расходов'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	ПараметрыРегистрации = ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыРегистрацииПроблемПроверки(
		"ВТОстаткиПрочихРасходов",
		НСтр("ru = 'По организации ""%1"" на конец периода %2 есть остатки в регистре прочих расходов по статьям.'", ОбщегоНазначения.КодОсновногоЯзыка()),
		СписокПолей,
		,
		Метаданные.РегистрыНакопления.ПрочиеРасходы.ПолноеИмя());
	
	ЗакрытиеМесяцаСервер.ЗарегистрироватьПроблемыВыполненияПроверки(
	 	ПараметрыПроверки,
		ПараметрыРегистрации);
		
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти

Процедура РаспределениеРасходовНаФинансовыйРезультатДополнитьПараметрамиПоУмолчанию(Параметры) Экспорт
	
	Параметры.Вставить("ГраницаПериода", Новый Граница(Параметры.КонецПериода, ВидГраницы.Включая));
	Если ТипЗнч(Параметры.МассивОрганизаций) = Тип("Массив") Тогда
		Параметры.Вставить("ПоВсемОрганизациям", Параметры.МассивОрганизаций.Количество() = 0);
	ИначеЕсли ТипЗнч(Параметры.МассивОрганизаций) = Тип("СправочникСсылка.Организации") Тогда
		Параметры.Вставить("МассивОрганизаций", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Параметры.МассивОрганизаций));
		Параметры.Вставить("ПоВсемОрганизациям", Ложь);
	Иначе
		Параметры.Вставить("ПоВсемОрганизациям", Истина);
	КонецЕсли;
	Параметры.Вставить("ФормироватьФинансовыйРезультат", ПолучитьФункциональнуюОпцию("ФормироватьФинансовыйРезультат"));
	Параметры.Вставить("ПредварительныйРасчет", Не Параметры.ДокументРаспределения = Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область Проведение

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	Документ.РаспределениеПрочихЗатрат КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ИдентификаторМетаданных",
								ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Тип("ДокументСсылка.РаспределениеПрочихЗатрат")));
								
	Запрос.УстановитьПараметр("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка						КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО				КАК РазделительЗаписи,
	|	&Период						КАК ДатаДокументаИБ,
	|	&Номер						КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных	КАК ТипСсылки,
	|	&Организация				КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства) КАК ХозяйственнаяОперация,
	|	&НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	&Подразделение				КАК Подразделение,
	|	&Ответственный				КАК Ответственный,
	|	&Комментарий				КАК Комментарий,
	|	0							КАК Сумма,
	|	&Проведен					КАК Проведен,
	|	&ПометкаУдаления			КАК ПометкаУдаления,
	|	ЛОЖЬ						КАК ДополнительнаяЗапись,
	|	""""						КАК Дополнительно,
	|	&Период						КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать				КАК НомерПервичногоДокумента,
	|	&Подразделение				КАК МестоХранения,
	|	&Период						КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО                КАК Приоритет
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента      = "Документ.РаспределениеПрочихЗатрат";
	СинонимТаблицыДокумента = "";
	ВЗапросеЕстьИсточник    = Истина;
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("ИдентификаторМетаданных",
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяДокумента));
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		ВЗапросеЕстьИсточник = Ложь;
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ВЗапросеЕстьИсточник,
			ПереопределениеРасчетаПараметров);
		
	Иначе
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ПереопределениеРасчетаПараметров);
		
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ФормированиеГиперссылкиВЖурналеПроизводства

Процедура СформироватьГиперссылкуКОформлениюФоновоеЗадание(Параметры, АдресХранилища) Экспорт
	
	КОформлению = ОбщегоНазначенияУТ.СформироватьГиперссылкуКОформлению(Параметры[0], Параметры[1]);
	ПоместитьВоВременноеХранилище(КОформлению, АдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#Область Заполнение

Функция КонвертироватьДанные(ДанныеПоРасходам)
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("ИДСтроки", Новый ОписаниеТипов("Число"));
	ТаблицаДанных.Колонки.Добавить("ДокументУпр", Новый ОписаниеТипов("ДокументСсылка.РаспределениеПрочихЗатрат"));
	ТаблицаДанных.Колонки.Добавить("ДокументРегл", Новый ОписаниеТипов("ДокументСсылка.РаспределениеПрочихЗатрат"));
	ТаблицаДанных.Колонки.Добавить("ДокументНДД", Новый ОписаниеТипов("ДокументСсылка.РаспределениеПрочихЗатрат"));
	ТаблицаДанных.Колонки.Добавить("СостояниеУпр", Новый ОписаниеТипов("ПеречислениеСсылка.СостоянияРаспределенияРасходов"));
	ТаблицаДанных.Колонки.Добавить("СостояниеРегл", Новый ОписаниеТипов("ПеречислениеСсылка.СостоянияРаспределенияРасходов"));
	ТаблицаДанных.Колонки.Добавить("СостояниеНДД", Новый ОписаниеТипов("ПеречислениеСсылка.СостоянияРаспределенияРасходов"));
	ТаблицаДанных.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.СтатьиРасходов"));
	ТаблицаДанных.Колонки.Добавить("АналитикаРасходов", Метаданные.ПланыВидовХарактеристик.СтатьиРасходов.Тип);
	ТаблицаДанных.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДанных.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));
	ТаблицаДанных.Колонки.Добавить("НаправлениеДеятельности", Новый ОписаниеТипов("СправочникСсылка.НаправленияДеятельности"));

	Для Каждого ДанныеПоРасходу Из ДанныеПоРасходам Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаДанных.Добавить(), ДанныеПоРасходу);
	КонецЦикла;
	
	Возврат ТаблицаДанных;
	
КонецФункции

Функция ФорматДанныхРаспределенияСтатьиРасходов()
	
	СтруктураЗаполнения = Новый Структура;
	СтруктураЗаполнения.Вставить("Дата");
	СтруктураЗаполнения.Вставить("ИДСтроки");
	СтруктураЗаполнения.Вставить("ДокументУпр");
	СтруктураЗаполнения.Вставить("ДокументРегл");
	СтруктураЗаполнения.Вставить("ДокументНДД");
	СтруктураЗаполнения.Вставить("СостояниеУпр");
	СтруктураЗаполнения.Вставить("СостояниеРегл");
	СтруктураЗаполнения.Вставить("СостояниеНДД");
	СтруктураЗаполнения.Вставить("СтатьяРасходов");
	СтруктураЗаполнения.Вставить("АналитикаРасходов");
	СтруктураЗаполнения.Вставить("СтатьяКалькуляции");
	СтруктураЗаполнения.Вставить("ВариантРаспределенияРегл");
	СтруктураЗаполнения.Вставить("ВариантРаспределенияУпр");
	СтруктураЗаполнения.Вставить("ВариантРаспределенияНДД");
	СтруктураЗаполнения.Вставить("Организация");
	СтруктураЗаполнения.Вставить("Подразделение");
	СтруктураЗаполнения.Вставить("НаправлениеДеятельности");
	СтруктураЗаполнения.Вставить("ПравилоРаспределенияУпр");
	СтруктураЗаполнения.Вставить("ПравилоРаспределенияРегл");
	СтруктураЗаполнения.Вставить("ПравилоРаспределенияНДД");
	
	Возврат СтруктураЗаполнения;
	
КонецФункции

Процедура УстановитьПризнакиБУиНУ(Объект, ВидУчета)
	
	
	Если ВидУчета = "РегламентированныйУчет" Тогда
		Объект.НалоговыйУчет = Объект.РегламентированныйУчет;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область БлокировкаПриОбновленииИБ

Процедура ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ПредставлениеОперации)
	
	ВходящиеДанные = Новый Соответствие;
	
	ВходящиеДанные.Вставить(Метаданные.Документы.РаспределениеПрочихЗатрат);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.МатериалыИРаботыВПроизводстве);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПрочиеРасходы);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.СебестоимостьТоваров);
	
	ЗакрытиеМесяцаСервер.ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ВходящиеДанные, ПредставлениеОперации);
	
КонецПроцедуры

#КонецОбласти

#Область РаспределениеНаФинансовыйРезультат

Процедура ПодготовитьСлужебныеТаблицы(Период, Знач Организации, Знач Запрос, ИсключитьТранспортныеРасходы = Ложь) Экспорт
	
	
	Если Не РасчетСебестоимостиПрикладныеАлгоритмы.ВременнаяТаблицаСуществует(Запрос.МенеджерВременныхТаблиц, "ВтДолиСписанияКосвенныхРасходов") Тогда
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
			|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
			|	ЛОЖЬ КАК НовыйАлгоритмСписания,
			|	ЛОЖЬ КАК Нормируемые,
			|	ЛОЖЬ КАК Транспортные,
			|	1 КАК ДоляСписания
			|ПОМЕСТИТЬ ВтДолиСписанияКосвенныхРасходов";
		Запрос.Выполнить();
	КонецЕсли;
	
	
	Если Не РасчетСебестоимостиПрикладныеАлгоритмы.ВременнаяТаблицаСуществует(Запрос.МенеджерВременныхТаблиц, "Регистраторы") Тогда
		ПодготовитьТаблицыКРасчету(Неопределено, Запрос);
	КонецЕсли;
	
КонецПроцедуры

Функция ВыборкаРаспределенияРасходовНаФинансовыйРезультат(ВременныеТаблицы)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВременныеТаблицы;
	
	Запрос.Текст = ТекстЗапросаВыборкиРаспределенияНаФинансовыйРезультат();
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Возврат РезультатЗапроса;
	
КонецФункции

#КонецОбласти

#Область КорректировкаУстаревшихДокументов

Функция ЕстьУстаревшиеДокументыСРасходами(Запрос)
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РаспределениеДоходовПоНаправлениямДеятельностиРасходы.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.РаспределениеДоходовПоНаправлениямДеятельности.Расходы КАК РаспределениеДоходовПоНаправлениямДеятельностиРасходы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеДоходовПоНаправлениямДеятельности КАК РаспределениеДоходовПоНаправлениямДеятельности
		|		ПО РаспределениеДоходовПоНаправлениямДеятельностиРасходы.Ссылка = РаспределениеДоходовПоНаправлениямДеятельности.Ссылка
		|ГДЕ
		|	РаспределениеДоходовПоНаправлениямДеятельности.Организация В(&МассивОрганизаций)
		|	И РаспределениеДоходовПоНаправлениямДеятельности.Проведен
		|	И РаспределениеДоходовПоНаправлениямДеятельности.Дата МЕЖДУ &НачалоПериода И &КонецПериода";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Процедура УдалитьДвиженияРаспределенныхРасходовВСтаромДокументе(Параметры, ВременнаяТаблица)
	
	Запрос = РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьЗапросПоПараметрам(Параметры, ВременнаяТаблица);
	
	Если Не ЕстьУстаревшиеДокументыСРасходами(Запрос) Тогда
		Возврат;
	КонецЕсли;
	
	// Удаление движений распределенных расходов в старом документе "Распределение доходов по направлениям деятельности".
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Движения.Регистратор
		|ПОМЕСТИТЬ РегистраторыКПерезаписиДвижений
		|ИЗ
		|	РегистрНакопления.ПрочиеРасходы КАК Движения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РаспределенныеАналитикиРасходов КАК Аналитики
		|		ПО Движения.Организация = Аналитики.Организация
		|			И Движения.Подразделение = Аналитики.Подразделение
		|			И Движения.НаправлениеДеятельности = Аналитики.НаправлениеДеятельности
		|			И Движения.СтатьяРасходов = Аналитики.СтатьяРасходов
		|			И Движения.АналитикаРасходов = Аналитики.АналитикаРасходов
		|ГДЕ
		|	Движения.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Движения.Активность
		|	И ТИПЗНАЧЕНИЯ(Движения.Регистратор) = ТИП(Документ.РаспределениеДоходовПоНаправлениямДеятельности)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Движения.*,
		|	(НЕ Аналитики.Организация ЕСТЬ NULL) КАК ПропуститьЗапись
		|ИЗ
		|	РегистрНакопления.ПрочиеРасходы КАК Движения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистраторыКПерезаписиДвижений КАК Регистраторы
		|		ПО Движения.Регистратор = Регистраторы.Регистратор
		|		ЛЕВОЕ СОЕДИНЕНИЕ РаспределенныеАналитикиРасходов КАК Аналитики
		|		ПО Движения.Организация = Аналитики.Организация
		|			И Движения.Подразделение = Аналитики.Подразделение
		|			И Движения.НаправлениеДеятельности = Аналитики.НаправлениеДеятельности
		|			И Движения.СтатьяРасходов = Аналитики.СтатьяРасходов
		|			И Движения.АналитикаРасходов = Аналитики.АналитикаРасходов
		|
		|ИТОГИ ПО
		|	Движения.Регистратор";
		
	Результат = Запрос.Выполнить();
	
	ОрганизацииКПересчету = Новый СписокЗначений;
	ВыборкаРегистраторовНовыхДвижений = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаРегистраторовНовыхДвижений.Следующий() Цикл
		
		ОрганизацииКПересчету.Очистить();
		
		ПрочиеРасходы = РегистрыНакопления.ПрочиеРасходы.СоздатьНаборЗаписей();
		ПрочиеРасходы.Отбор.Регистратор.Установить(ВыборкаРегистраторовНовыхДвижений.Регистратор);
		
		ВыборкаДетальная = ВыборкаРегистраторовНовыхДвижений.Выбрать();
		Пока ВыборкаДетальная.Следующий() Цикл
			
			Если ОрганизацииКПересчету.НайтиПоЗначению(ВыборкаДетальная.Организация) = Неопределено Тогда
				ОрганизацииКПересчету.Добавить(ВыборкаДетальная.Организация);
			КонецЕсли;
			
			Если ВыборкаДетальная.ПропуститьЗапись Тогда
				Продолжить;
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(ПрочиеРасходы.Добавить(), ВыборкаДетальная);
			
		КонецЦикла;
		
		ПрочиеРасходы.Записать();
		
		Для Каждого ПересчитываемаяОрганизация Из ОрганизацииКПересчету Цикл
			РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьЗаписьРегистра(Параметры.НачалоПериода, 
				ВыборкаРегистраторовНовыхДвижений.Регистратор,
				ПересчитываемаяОрганизация.Значение, 
				Перечисления.ОперацииЗакрытияМесяца.РаспределениеДоходовПоНаправлениямДеятельности);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПоступилоПрочихРасходов

// Возвращает текст источника данных с заменой по шаблону.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ЗаменитьПоШаблону(ТекстЗапроса, Шаблон, Настройка) Экспорт

	ТекстЗапросаРезультат = ТекстЗапроса;
	Замены = Неопределено;
	Шаблон.Свойство(Настройка, Замены);
	Для Каждого Замена Из Замены Цикл
		ТекстЗапросаРезультат = СтрЗаменить(ТекстЗапросаРезультат, Замена.Ключ, Замена.Значение);
	КонецЦикла;
	Возврат ТекстЗапросаРезультат;
	
КонецФункции

// Возвращает текст подзапроса для получения источников данных о прочих расходах.
// 
// Параметры:
//  ВариантРасчета - Строка - Вариант расчета
// 
// Возвращаемое значение:
//  Строка -- Текст запроса.
Функция ТекстЗапросаПоступилоПрочихРасходовИсточники(ВариантРасчета) Экспорт
	
	ШаблонЗамен = ШаблонЗамен();

	#Область ТекстОписаниеДанных
	ТекстОписаниеДанных = "
	|	ВЫБРАТЬ ПЕРВЫЕ 0
	|		//РежимОтладки """" КАК ЗапросИсточник,
	|		ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|		ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)  КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
	|		НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|		0 КАК Сумма,
	|		0 КАК СуммаБезНДС,
	|		0 КАК СуммаРегл,
	|		0 КАК ПостояннаяРазница,
	|		0 КАК ВременнаяРазница,
	|		0 КАК СуммаНДД,
	|		0 КАК СуммаУпр,
	|		0 КАК СуммаРеглРасход,
	|		0 КАК ПостояннаяРазницаРасход,
	|		0 КАК ВременнаяРазницаРасход,
	|		0 КАК СуммаНДДРасход,
	|		0 КАК РасчетноеКоличество,
	|		ЛОЖЬ КАК РасчетнаяСумма,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДляСвязи,
	// СтатьяРасходовДляСвязи = ПустаяСсылка только в описании и для блоков ОВЗ. 
	// Участвует в проверке статей на распределение на ОВЗ. Например, см. ТекстЗапросаПоступилоПрочихРасходов
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовДляСвязи
	|	//РежимОтладки ПОМЕСТИТЬ ВтИсточникиРасходов
	|";
	#КонецОбласти

	#Область ТекстОстаткиПрочихРасходов
	ТекстОстаткиПрочихРасходов = "
	|	ВЫБРАТЬ
	|		//РежимОтладки ""ТекстОстаткиПрочихРасходов"",
	|		Расходы.Организация,
	|		Расходы.Подразделение,
	|		Расходы.НаправлениеДеятельности,
	|		Расходы.СтатьяРасходов,
	|		Расходы.АналитикаРасходов,
	|		Расходы.СуммаОстаток,
	|		Расходы.СуммаБезНДСОстаток,
	|		Расходы.СуммаРеглОстаток,
	|		Расходы.ПостояннаяРазницаОстаток,
	|		Расходы.ВременнаяРазницаОстаток,
	|		Расходы.СуммаНДДОстаток,
	|		Расходы.СуммаУпрОстаток,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0 КАК РасчетноеКоличество,
	|		ЛОЖЬ КАК РасчетнаяСумма,
	|		Расходы.НаправлениеДеятельности,
	|		Расходы.СтатьяРасходов
	|	ИЗ
	|		СтатьиРасходовКРаспределению КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПрочиеРасходыОстатки КАК Расходы
	|		ПО Расходы.СтатьяРасходов = Т.Ссылка
	|";
	ТекстОстаткиПрочихРасходовУпр =		ЗаменитьПоШаблону(ТекстОстаткиПрочихРасходов, ШаблонЗамен, "ТекстОстаткиПрочихРасходовУпр");
	ТекстОстаткиПрочихРасходовРегл =	ЗаменитьПоШаблону(ТекстОстаткиПрочихРасходов, ШаблонЗамен, "ТекстОстаткиПрочихРасходовРегл");
	ТекстОстаткиПрочихРасходовНУ =		ЗаменитьПоШаблону(ТекстОстаткиПрочихРасходов, ШаблонЗамен, "ТекстОстаткиПрочихРасходовНУ");
	ТекстОстаткиПрочихРасходовНДД =		РасчетСебестоимостиЛокализация.ТекстОстаткиПрочихРасходовНДД();
	
	#КонецОбласти
	
	#Область ТекстДвиженияПрочихРасходов
	ТекстДвиженияПрочихРасходов = "
	|	ВЫБРАТЬ
	|		//РежимОтладки ""ТекстДвиженияПрочихРасходов"",
	|		Движения.Организация,
	|		Движения.Подразделение,
	|		Движения.НаправлениеДеятельности,
	|		Движения.СтатьяРасходов,
	|		Движения.АналитикаРасходов,
	|		Движения.Сумма,
	|		Движения.СуммаБезНДС,
	|		Движения.СуммаРегл,
	|		Движения.ПостояннаяРазница,
	|		Движения.ВременнаяРазница,
	|		Движения.СуммаНДД,
	|		Движения.СуммаУпр,
	|		Движения.СуммаРегл,
	|		Движения.ПостояннаяРазница,
	|		Движения.ВременнаяРазница,
	|		Движения.СуммаНДД,
	|		0 КАК РасчетноеКоличество,
	|		ЛОЖЬ КАК РасчетнаяСумма,
	|		ЕСТЬNULL(ДД.НаправлениеДеятельности, Движения.НаправлениеДеятельности),
	|		ЕСТЬNULL(ДД.СтатьяРасходов, Движения.СтатьяРасходов)
	|	ИЗ
	|		РегистрНакопления.ПрочиеРасходы КАК Движения
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтатьиРасходовКРаспределению КАК СтатьиКРаспределению
	|			ПО Движения.СтатьяРасходов = СтатьиКРаспределению.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК ДД
	|			ПО Движения.Регистратор = ДД.Ссылка 
	|				И ДД.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	ГДЕ
	|		Движения.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Движения.Активность
	|		И (ТИПЗНАЧЕНИЯ(Движения.Регистратор) В (
	|			ТИП(Документ.РасчетСебестоимостиТоваров), 
	|			ТИП(Документ.РаспределениеПрочихЗатрат), 
	|			ТИП(Документ.РаспределениеДоходовПоНаправлениямДеятельности))
	|		ИЛИ СтатьиКРаспределению.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|		)
	|		И Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Движения.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеОшибокОкругления)
	|		И (&ПоВсемОрганизациям
	|			ИЛИ Движения.Организация В (&МассивОрганизаций))
	|		И (&ПоВсемПодразделениям
	|			ИЛИ Движения.Подразделение В (&СписокПодразделений))
	|";
	ТекстДвиженияПрочихРасходовУпр = ЗаменитьПоШаблону(ТекстДвиженияПрочихРасходов, ШаблонЗамен, "ТекстДвиженияПрочихРасходовУпр");
	ТекстДвиженияПрочихРасходовРегл = ЗаменитьПоШаблону(ТекстДвиженияПрочихРасходов, ШаблонЗамен, "ТекстДвиженияПрочихРасходовРегл");
	ТекстДвиженияПрочихРасходовНУ = ЗаменитьПоШаблону(ТекстДвиженияПрочихРасходов, ШаблонЗамен, "ТекстДвиженияПрочихРасходовНУ");
	ТекстДвиженияПрочихРасходовНДД = ЗаменитьПоШаблону(ТекстДвиженияПрочихРасходов, ШаблонЗамен, "ТекстДвиженияПрочихРасходовНДД");
	#КонецОбласти
	
	#Область ТекстДвиженияПартийПрочихРасходов
	ТекстДвиженияПартийПрочихРасходов = "
	|	ВЫБРАТЬ
	|		//РежимОтладки ""ТекстДвиженияПартийПрочихРасходов"",
	|		Движения.Организация,
	|		Движения.Подразделение,
	|		Движения.НаправлениеДеятельности,
	|		Движения.СтатьяРасходов,
	|		Движения.АналитикаРасходов,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0 КАК РасчетноеКоличество,
	|		ИСТИНА КАК РасчетнаяСумма,
	|		Движения.НаправлениеДеятельности,
	|		Движения.СтатьяРасходов
	|	ИЗ
	|		РегистрНакопления.ПартииПрочихРасходов КАК Движения
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтатьиРасходовКРаспределению КАК СтатьиКРаспределению
	|			ПО Движения.СтатьяРасходов = СтатьиКРаспределению.Ссылка
	|	ГДЕ
	|		Движения.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Движения.Активность
	|		И Движения.Регистратор ССЫЛКА Документ.НачислениеРеверсивногоНДС
	|		И Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И (&ПоВсемОрганизациям
	|			ИЛИ Движения.Организация В (&МассивОрганизаций))
	|		И (&ПоВсемПодразделениям
	|			ИЛИ Движения.Подразделение В (&СписокПодразделений))
	|";
	ТекстДвиженияПартийПрочихРасходовУпр = ЗаменитьПоШаблону(ТекстДвиженияПартийПрочихРасходов, ШаблонЗамен, "ТекстДвиженияПрочихРасходовУпр");
	ТекстДвиженияПартийПрочихРасходовРегл = ЗаменитьПоШаблону(ТекстДвиженияПартийПрочихРасходов, ШаблонЗамен, "ТекстДвиженияПрочихРасходовРегл");
	ТекстДвиженияПартийПрочихРасходовНУ = ЗаменитьПоШаблону(ТекстДвиженияПартийПрочихРасходов, ШаблонЗамен, "ТекстДвиженияПрочихРасходовНУ");
	ТекстДвиженияПартийПрочихРасходовНДД = ЗаменитьПоШаблону(ТекстДвиженияПартийПрочихРасходов, ШаблонЗамен, "ТекстДвиженияПрочихРасходовНДД");
	#КонецОбласти
	
	
	#Область ТекстСписаниеРаспределениеТМЦ
	ТекстСписаниеРаспределениеТМЦ = "
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		//РежимОтладки ""ТекстСписаниеРаспределениеТМЦ"",
	|		СебестоимостьТоваров.Организация,
	|		СебестоимостьТоваров.Подразделение,
	|		СебестоимостьТоваров.КорНаправлениеДеятельности,
	|		СебестоимостьТоваров.СтатьяРасходовСписания,
	|		СебестоимостьТоваров.АналитикаРасходов,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		СебестоимостьТоваров.Количество КАК РасчетноеКоличество,
	|		ЛОЖЬ КАК РасчетнаяСумма,
	|		СебестоимостьТоваров.КорНаправлениеДеятельности,
	|		СебестоимостьТоваров.СтатьяРасходовСписания
	|	ИЗ
	|		РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтатьиРасходовКРаспределению КАК Статьи
	|			ПО (Статьи.Ссылка = СебестоимостьТоваров.СтатьяРасходовСписания)
	|	ГДЕ
	|		СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И СебестоимостьТоваров.Активность
	|		И СебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ТИПЗНАЧЕНИЯ(СебестоимостьТоваров.Регистратор) В (
	|			ТИП(Документ.Сторно),
	|			ТИП(Документ.ВнутреннееПотребление),
	|			ТИП(Документ.ОтчетОСписанииТоваровСХранения),


	|			ТИП(Документ.ПрочееОприходованиеТоваров),
	|			ТИП(Документ.СписаниеНедостачТоваров))
	|		И НЕ СебестоимостьТоваров.РасчетСебестоимости
	|		И (&ПоВсемОрганизациям
	|				ИЛИ СебестоимостьТоваров.Организация В (&МассивОрганизаций))
	|		И (&ПоВсемПодразделениям
	|				ИЛИ СебестоимостьТоваров.Подразделение В (&СписокПодразделений))
	|";
	#КонецОбласти
	
	#Область ТекстСписаниеРаспределениеТМЦвДругуюОрганизацию
	ТекстСписаниеРаспределениеТМЦвДругуюОрганизацию = "
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		//РежимОтладки ""ТекстСписаниеРаспределениеТМЦвДругуюОрганизацию"",
	|		СебестоимостьТоваров.КорОрганизация,
	|		СебестоимостьТоваров.Подразделение,
	|		СебестоимостьТоваров.КорНаправлениеДеятельности,
	|		СебестоимостьТоваров.СтатьяРасходовСписания,
	|		СебестоимостьТоваров.АналитикаРасходов,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		СебестоимостьТоваров.Количество КАК РасчетноеКоличество,
	|		ИСТИНА КАК РасчетнаяСумма,
	|		СебестоимостьТоваров.КорНаправлениеДеятельности,
	|		СебестоимостьТоваров.СтатьяРасходовСписания
	|	ИЗ
	|		РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтатьиРасходовКРаспределению КАК Статьи
	|			ПО (Статьи.Ссылка = СебестоимостьТоваров.СтатьяРасходовСписания)
	|	ГДЕ
	|		СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И СебестоимостьТоваров.Активность
	|		И СебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ТИПЗНАЧЕНИЯ(СебестоимостьТоваров.Регистратор) В (
	|			ТИП(Документ.Сторно),
	|			ТИП(Документ.ВнутреннееПотребление),

	|			ТИП(Документ.ПрочееОприходованиеТоваров),
	|			ТИП(Документ.СписаниеНедостачТоваров))
	|		И НЕ СебестоимостьТоваров.РасчетСебестоимости
	|		И НЕ СебестоимостьТоваров.КорОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		И НЕ СебестоимостьТоваров.КорОрганизация = СебестоимостьТоваров.Организация
	|		И (&ПоВсемОрганизациям
	|				ИЛИ СебестоимостьТоваров.КорОрганизация В (&МассивОрганизаций))
	|		И (&ПоВсемПодразделениям
	|				ИЛИ СебестоимостьТоваров.Подразделение В (&СписокПодразделений))
	|";
	#КонецОбласти
	

	ТекстыЗапроса = Новый Массив;
	ТекстыЗапроса.Добавить(ТекстОписаниеДанных);
	
	ТекстыЗапроса.Добавить(ТекстОстаткиПрочихРасходовУпр);
	ТекстыЗапроса.Добавить(ТекстОстаткиПрочихРасходовРегл);
	ТекстыЗапроса.Добавить(ТекстОстаткиПрочихРасходовНДД);
	
	ТекстыЗапроса.Добавить(ТекстДвиженияПрочихРасходовУпр);
	ТекстыЗапроса.Добавить(ТекстДвиженияПрочихРасходовРегл);
	ТекстыЗапроса.Добавить(ТекстДвиженияПрочихРасходовНДД);
	
	ТекстыЗапроса.Добавить(ТекстДвиженияПартийПрочихРасходовУпр);
	ТекстыЗапроса.Добавить(ТекстДвиженияПартийПрочихРасходовРегл);
	ТекстыЗапроса.Добавить(ТекстДвиженияПартийПрочихРасходовНДД);
	
	ТекстыЗапроса.Добавить(ТекстСписаниеРаспределениеТМЦ);
	ТекстыЗапроса.Добавить(ТекстСписаниеРаспределениеТМЦвДругуюОрганизацию);

	
	Возврат СтрСоединить(ТекстыЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении())
	
КонецФункции

Функция ШаблонЗамен()
	
	СтруктураВозврата = Новый Структура;
	
	СтатьиРасходовКРаспределениюУпр = Новый Соответствие;
	СтатьиРасходовКРаспределениюУпр.Вставить("СтатьиРасходовКРаспределению", "СтатьиРасходовКРаспределениюУпр");
	СтруктураВозврата.Вставить("СтатьиРасходовКРаспределениюУпр", СтатьиРасходовКРаспределениюУпр);
	
	СтатьиРасходовКРаспределениюРегл = Новый Соответствие;
	СтатьиРасходовКРаспределениюРегл.Вставить("СтатьиРасходовКРаспределению", "СтатьиРасходовКРаспределениюРегл");
	СтруктураВозврата.Вставить("СтатьиРасходовКРаспределениюРегл", СтатьиРасходовКРаспределениюРегл);
	
	СтатьиРасходовКРаспределениюНУ = Новый Соответствие;
	СтатьиРасходовКРаспределениюНУ.Вставить("СтатьиРасходовКРаспределению", "СтатьиРасходовКРаспределениюРегл");
	СтруктураВозврата.Вставить("СтатьиРасходовКРаспределениюРегл", СтатьиРасходовКРаспределениюРегл);
	
	ТекстОстаткиПрочихРасходовУпр = Новый Соответствие;
	ТекстОстаткиПрочихРасходовУпр.Вставить("Расходы.СуммаРеглОстаток,", "0,");
	ТекстОстаткиПрочихРасходовУпр.Вставить("Расходы.ПостояннаяРазницаОстаток,", "0,");
	ТекстОстаткиПрочихРасходовУпр.Вставить("Расходы.ВременнаяРазницаОстаток,", "0,");
	ТекстОстаткиПрочихРасходовУпр.Вставить("Расходы.СуммаНДДОстаток,", "0,");
	ТекстОстаткиПрочихРасходовУпр.Вставить("Расходы.СуммаНДД,", "0,");
	ТекстОстаткиПрочихРасходовУпр.Вставить("СтатьиРасходовКРаспределению", "СтатьиРасходовКРаспределениюУпр");
	ТекстОстаткиПрочихРасходовУпр.Вставить("СтатьиОВЗ", "СтатьиОВЗУпр");
	ТекстОстаткиПрочихРасходовУпр.Вставить("ОВЗКРаспределению", "ОВЗКРаспределениюУпр");
	СтруктураВозврата.Вставить("ТекстОстаткиПрочихРасходовУпр", ТекстОстаткиПрочихРасходовУпр);
	
	ТекстОстаткиПрочихРасходовРегл = Новый Соответствие;
	ТекстОстаткиПрочихРасходовРегл.Вставить("Расходы.СуммаОстаток,", "0,");
	ТекстОстаткиПрочихРасходовРегл.Вставить("Расходы.СуммаБезНДСОстаток,", "0,");
	ТекстОстаткиПрочихРасходовРегл.Вставить("Расходы.СуммаУпрОстаток,", "0,");
	ТекстОстаткиПрочихРасходовРегл.Вставить("Расходы.ВременнаяРазницаОстаток,", "Расходы.СуммаРеглОстаток - Расходы.ПостояннаяРазницаОстаток ,"); // пробел перед запятой для исключения замены
	ТекстОстаткиПрочихРасходовРегл.Вставить("Расходы.СуммаНДДОстаток,", "0,");
	ТекстОстаткиПрочихРасходовРегл.Вставить("Расходы.СуммаНДД,", "0,"); 
	ТекстОстаткиПрочихРасходовРегл.Вставить("СтатьиРасходовКРаспределению", "СтатьиРасходовКРаспределениюРегл");
	ТекстОстаткиПрочихРасходовРегл.Вставить("СтатьиОВЗ", "СтатьиОВЗРегл");
	ТекстОстаткиПрочихРасходовРегл.Вставить("ОВЗКРаспределению", "ОВЗКРаспределениюРегл");
	СтруктураВозврата.Вставить("ТекстОстаткиПрочихРасходовРегл", ТекстОстаткиПрочихРасходовРегл);
	
	ТекстОстаткиПрочихРасходовНУ = Новый Соответствие;
	ТекстОстаткиПрочихРасходовНУ.Вставить("Расходы.СуммаОстаток,", "0,");
	ТекстОстаткиПрочихРасходовНУ.Вставить("Расходы.СуммаБезНДСОстаток,", "0,");
	ТекстОстаткиПрочихРасходовНУ.Вставить("Расходы.СуммаУпрОстаток,", "0,");
	ТекстОстаткиПрочихРасходовНУ.Вставить("Расходы.СуммаРеглОстаток,", "0,");
	ТекстОстаткиПрочихРасходовНУ.Вставить("Расходы.ПостояннаяРазницаОстаток,", "0,");
	ТекстОстаткиПрочихРасходовНУ.Вставить("Расходы.ВременнаяРазницаОстаток,", "Расходы.ПостояннаяРазницаОстаток + Расходы.ВременнаяРазницаОстаток - Расходы.СуммаРеглОстаток ,"); // пробел перед запятой для исключения замены
	ТекстОстаткиПрочихРасходовНУ.Вставить("Расходы.СуммаНДДОстаток,", "0,");
	ТекстОстаткиПрочихРасходовНУ.Вставить("Расходы.СуммаНДД,", "0,"); 
	ТекстОстаткиПрочихРасходовНУ.Вставить("СтатьиРасходовКРаспределению", "СтатьиРасходовКРаспределениюНУ");
	ТекстОстаткиПрочихРасходовНУ.Вставить("СтатьиОВЗ", "СтатьиОВЗНУ");
	ТекстОстаткиПрочихРасходовНУ.Вставить("ОВЗКРаспределению", "ОВЗКРаспределениюНУ");
	СтруктураВозврата.Вставить("ТекстОстаткиПрочихРасходовНУ", ТекстОстаткиПрочихРасходовНУ);
	
	ТекстОстаткиПрочихРасходовНДД = Новый Соответствие;
	ТекстОстаткиПрочихРасходовНДД.Вставить("Расходы.СуммаОстаток,", "0,");
	ТекстОстаткиПрочихРасходовНДД.Вставить("Расходы.СуммаБезНДСОстаток,", "0,");
	ТекстОстаткиПрочихРасходовНДД.Вставить("Расходы.СуммаУпрОстаток,", "0,");
	ТекстОстаткиПрочихРасходовНДД.Вставить("Расходы.СуммаРеглОстаток,", "0,");
	ТекстОстаткиПрочихРасходовНДД.Вставить("Расходы.ПостояннаяРазницаОстаток,", "0,");
	ТекстОстаткиПрочихРасходовНДД.Вставить("Расходы.ВременнаяРазницаОстаток,", "0,");
	ТекстОстаткиПрочихРасходовНДД.Вставить("СтатьиРасходовКРаспределению", "СтатьиРасходовКРаспределениюНДД");
	ТекстОстаткиПрочихРасходовНДД.Вставить("ОВЗКРаспределению", "ОВЗКРаспределениюНДД");
	СтруктураВозврата.Вставить("ТекстОстаткиПрочихРасходовНДД", ТекстОстаткиПрочихРасходовНДД);
	
	ТекстДвиженияПрочихРасходовУпр = Новый Соответствие;
	ТекстДвиженияПрочихРасходовУпр.Вставить("Движения.СуммаРегл,", "0,");
	ТекстДвиженияПрочихРасходовУпр.Вставить("Движения.ПостояннаяРазница,", "0,");
	ТекстДвиженияПрочихРасходовУпр.Вставить("Движения.ВременнаяРазница,", "0,");
	ТекстДвиженияПрочихРасходовУпр.Вставить("Движения.СуммаНДД,", "0,");
	ТекстДвиженияПрочихРасходовУпр.Вставить("СтатьиРасходовКРаспределению", "СтатьиРасходовКРаспределениюУпр");
	ТекстДвиженияПрочихРасходовУпр.Вставить("ОВЗКРаспределению", "ОВЗКРаспределениюУпр");
	СтруктураВозврата.Вставить("ТекстДвиженияПрочихРасходовУпр", ТекстДвиженияПрочихРасходовУпр);
	
	ТекстДвиженияПрочихРасходовРегл = Новый Соответствие;
	ТекстДвиженияПрочихРасходовРегл.Вставить("Движения.Сумма,", "0,");
	ТекстДвиженияПрочихРасходовРегл.Вставить("Движения.СуммаБезНДС,", "0,");
	ТекстДвиженияПрочихРасходовРегл.Вставить("Движения.СуммаУпр,", "0,");
	ТекстДвиженияПрочихРасходовРегл.Вставить("Движения.ВременнаяРазница,", "Движения.СуммаРегл - Движения.ПостояннаяРазница ,"); // пробел перед запятой для исключения замены
	ТекстДвиженияПрочихРасходовРегл.Вставить("Движения.СуммаНДД,", "0,");
	ТекстДвиженияПрочихРасходовРегл.Вставить("СтатьиРасходовКРаспределению", "СтатьиРасходовКРаспределениюРегл");
	ТекстДвиженияПрочихРасходовРегл.Вставить("ОВЗКРаспределению", "ОВЗКРаспределениюРегл");
	СтруктураВозврата.Вставить("ТекстДвиженияПрочихРасходовРегл", ТекстДвиженияПрочихРасходовРегл);
	
	ТекстДвиженияПрочихРасходовНУ = Новый Соответствие;
	ТекстДвиженияПрочихРасходовНУ.Вставить("Движения.Сумма,", "0,");
	ТекстДвиженияПрочихРасходовНУ.Вставить("Движения.СуммаБезНДС,", "0,");
	ТекстДвиженияПрочихРасходовНУ.Вставить("Движения.СуммаУпр,", "0,");
	ТекстДвиженияПрочихРасходовНУ.Вставить("Движения.СуммаРегл,", "0,");
	ТекстДвиженияПрочихРасходовНУ.Вставить("Движения.ПостояннаяРазница,", "0,");
	ТекстДвиженияПрочихРасходовНУ.Вставить("Движения.ВременнаяРазница,", "Движения.ПостояннаяРазница + Движения.ВременнаяРазница - Движения.СуммаРегл ,"); // пробел перед запятой для исключения замены 
	ТекстДвиженияПрочихРасходовНУ.Вставить("Движения.СуммаНДД,", "0,");
	ТекстДвиженияПрочихРасходовНУ.Вставить("СтатьиРасходовКРаспределению", "СтатьиРасходовКРаспределениюНУ");
	ТекстДвиженияПрочихРасходовНУ.Вставить("ОВЗКРаспределению", "ОВЗКРаспределениюНУ");
	СтруктураВозврата.Вставить("ТекстДвиженияПрочихРасходовНУ", ТекстДвиженияПрочихРасходовНУ);
	
	ТекстДвиженияПрочихРасходовНДД = Новый Соответствие;
	ТекстДвиженияПрочихРасходовНДД.Вставить("Движения.Сумма,", "0,");
	ТекстДвиженияПрочихРасходовНДД.Вставить("Движения.СуммаБезНДС,", "0,");
	ТекстДвиженияПрочихРасходовНДД.Вставить("Движения.СуммаУпр,", "0,");
	ТекстДвиженияПрочихРасходовНДД.Вставить("Движения.СуммаРегл,", "0,");
	ТекстДвиженияПрочихРасходовНДД.Вставить("Движения.ПостояннаяРазница,", "0,");
	ТекстДвиженияПрочихРасходовНДД.Вставить("Движения.ВременнаяРазница,", "0,"); 
	ТекстДвиженияПрочихРасходовНДД.Вставить("СтатьиРасходовКРаспределению", "СтатьиРасходовКРаспределениюНДД");
	ТекстДвиженияПрочихРасходовНДД.Вставить("ОВЗКРаспределению", "ОВЗКРаспределениюНДД");
	СтруктураВозврата.Вставить("ТекстДвиженияПрочихРасходовНДД", ТекстДвиженияПрочихРасходовНДД);
	
	Возврат СтруктураВозврата
	
КонецФункции

#КонецОбласти

#Область РасходыПослеРаспределения
// Возвращает текст подзапроса для получения источников данных о прочих расходах.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаРасходыПослеРаспределенияИсточники() Экспорт
	
	Возврат "
		|ВЫБРАТЬ
		|//РежимОтладки ""АктуальныеОстаткиРасходовНаКонецМесяца"" КАК ЗапросИсточник,
		|		НеРаспределенные.Организация КАК Организация,
		|		НеРаспределенные.Подразделение КАК Подразделение,
		|		НеРаспределенные.СтатьяРасходов КАК СтатьяРасходов,
		|		НеРаспределенные.АналитикаРасходов КАК АналитикаРасходов,
		|		НеРаспределенные.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		Статьи.ВариантРаспределенияРасходовУпр,
		|		Статьи.ВариантРаспределенияРасходовРегл,
		|		Статьи.ВариантРаспределенияРасходовНУ,
		|		НастройкиРаспределенияСтатейНДД.ВариантРаспределенияРасходовНДД,
		|		ВЫБОР
		|			КОГДА НЕ Статьи.ВариантРаспределенияРасходовУпр В (&ВариантыРаспределенияРасходов)
		|				ТОГДА 0
		|			ИНАЧЕ
		|				НеРаспределенные.СуммаОстаток
		|		КОНЕЦ КАК СуммаОстаток,
		|		ВЫБОР
		|			КОГДА НЕ Статьи.ВариантРаспределенияРасходовУпр В (&ВариантыРаспределенияРасходов)
		|				ТОГДА 0
		|			ИНАЧЕ
		|				НеРаспределенные.СуммаБезНДСОстаток
		|		КОНЕЦ КАК СуммаБезНДСОстаток,
		|		ВЫБОР
		|			КОГДА НЕ Статьи.ВариантРаспределенияРасходовУпр В (&ВариантыРаспределенияРасходов)
		|				ТОГДА 0
		|			ИНАЧЕ
		|				НеРаспределенные.СуммаУпрОстаток
		|		КОНЕЦ КАК СуммаУпрОстаток,
		|		0 КАК СуммаНУОстаток,
		|		ВЫБОР
		|			КОГДА НастройкиРаспределенияСтатейНДД.ВариантРаспределенияРасходовНДД В (&ВариантыРаспределенияРасходов)
		|				ТОГДА НеРаспределенные.СуммаНДДОстаток
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК СуммаНДДОстаток,
		|		ВЫБОР
		|			КОГДА НЕ Статьи.ВариантРаспределенияРасходовРегл В (&ВариантыРаспределенияРасходов)
		|				ТОГДА 0
		|			ИНАЧЕ
		|				НеРаспределенные.СуммаРеглОстаток
		|		КОНЕЦ КАК СуммаРеглОстаток,
		|		0 КАК ПостояннаяРазницаОстаток,
		|		ЛОЖЬ КАК ЕстьПогрешностьРасчетаУУ,
		|		ЛОЖЬ КАК ЕстьПогрешностьРасчетаБУ,
		|		ЛОЖЬ КАК ЕстьПогрешностьРасчетаНУ,
		|		ЛОЖЬ КАК ЕстьПогрешностьРасчетаНДД
		|//РежимОтладки ПОМЕСТИТЬ ВтИсточникиНераспределенныхРасходов
		|	ИЗ
		|		РасходыКРаспределениюСводноСтатьяЗаполнена КАК РасходыКРаспределениюСводно
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПрочиеРасходыОстатки КАК НеРаспределенные
		|		ПО РасходыКРаспределениюСводно.Организация = НеРаспределенные.Организация
		|			И РасходыКРаспределениюСводно.Подразделение = НеРаспределенные.Подразделение
		|			И РасходыКРаспределениюСводно.НаправлениеДеятельности = НеРаспределенные.НаправлениеДеятельности
		|			И РасходыКРаспределениюСводно.СтатьяРасходов = НеРаспределенные.СтатьяРасходов
		|			И РасходыКРаспределениюСводно.АналитикаРасходов = НеРаспределенные.АналитикаРасходов
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
		|		ПО РасходыКРаспределениюСводно.СтатьяРасходов = Статьи.Ссылка
		|
		|			ЛЕВОЕ СОЕДИНЕНИЕ НастройкиРаспределенияСтатейНДД КАК НастройкиРаспределенияСтатейНДД
		|			ПО НеРаспределенные.СтатьяРасходов = НастройкиРаспределенияСтатейНДД.СтатьяРасходов
		|				И &НеРаспределенные_ОбъектРаздельногоУчетаНДД = НастройкиРаспределенияСтатейНДД.ОбъектРаздельногоУчетаНДД
		|				И НеРаспределенные.Организация = НастройкиРаспределенияСтатейНДД.Организация
		|";
		
КонецФункции
#КонецОбласти

#Область ФормированиеДокументов

Функция ПодобратьПодходящийДокумент(ДокументыКПроведению, ВидУчета, ПараметрыЗаполнения)
	
	ПодходящийДокумент = Неопределено;
	
	Для Каждого ДокументРаспределения Из ДокументыКПроведению Цикл
		
		ДокументНайден = Ложь;
		
		Если ВидУчета = "УправленческийУчет" Тогда
			Если ДокументРаспределения.РегламентированныйУчет Тогда
				ДокументНайден = ПроверитьСоответствиеНастроекУпрРегл(ПараметрыЗаполнения);
			КонецЕсли;
		ИначеЕсли ВидУчета = "РегламентированныйУчет" Тогда
			Если ДокументРаспределения.УправленческийУчет Тогда
				ДокументНайден = ПроверитьСоответствиеНастроекУпрРегл(ПараметрыЗаполнения);
			КонецЕсли;
		
		//++ Локализация
		

		//-- Локализация
		
		ИначеЕсли ВидУчета = "НДД" Тогда
			
			ДокументНайден = РаспределениеПрочихЗатратЛокализация.ПодобратьПодходящийДокумент(
				ДокументРаспределения,
				ВидУчета,
				ПараметрыЗаполнения
				);
			
		КонецЕсли;
		
		Если ДокументНайден Тогда
			ПодходящийДокумент = ДокументРаспределения;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПодходящийДокумент;
	
КонецФункции

Функция ПроверитьСоответствиеНастроекУпрРегл(ПараметрыЗаполнения)
	
	НастройкиСоответствуют = 
		Не РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии22(ПараметрыЗаполнения.Дата)
		И ПараметрыЗаполнения.ВариантРаспределенияРегл = ПараметрыЗаполнения.ВариантРаспределенияУпр
		И ПараметрыЗаполнения.ВариантРаспределенияРегл = Перечисления.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты
		ИЛИ ПараметрыЗаполнения.ВариантРаспределенияРегл = ПараметрыЗаполнения.ВариантРаспределенияУпр
		И ПараметрыЗаполнения.ПравилоРаспределенияРегл = ПараметрыЗаполнения.ПравилоРаспределенияУпр
		И ПараметрыЗаполнения.ДокументУпр = ПараметрыЗаполнения.ДокументРегл;
		
	Возврат НастройкиСоответствуют;
	
КонецФункции

//++ Локализация


//-- Локализация

Функция ПолучитьНастройкиРаспределенияПоДокументу(ДокументРаспределения)
	
	ИсточникВариантовРаспределения = ДокументРаспределения.СтатьяРасходов;
	
	РаспределениеИзОВЗ = Ложь;
	
	
	НастройкиРаспределения = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(ИсточникВариантовРаспределения, 
		"ВариантРаспределенияРасходовУпр,ВариантРаспределенияРасходовРегл,ПравилоРаспределенияРасходовУпр,ПравилоРаспределенияРасходовРегл
		|");	
		
	
	Если РасчетСебестоимостиЛокализация.ПолучитьФункциональнуюОпциюУчетПоНДД() Тогда
		НастройкиРаспределенияНДД = РасчетСебестоимостиЛокализация.ПолучитьНастройкиРаспределенияПоДокументу(ДокументРаспределения, РаспределениеИзОВЗ);
		Если ЗначениеЗаполнено(НастройкиРаспределенияНДД) Тогда
			НастройкиРаспределения.Вставить("ВариантРаспределенияРасходовНДД", НастройкиРаспределенияНДД.ВариантРаспределенияРасходовНДД);
			НастройкиРаспределения.Вставить("ПравилоРаспределенияРасходовНДД", НастройкиРаспределенияНДД.ПравилоРаспределенияРасходовНДД);
		Иначе
			НастройкиРаспределения.Вставить("ВариантРаспределенияРасходовНДД", Перечисления.ВариантыРаспределенияРасходов.ПустаяСсылка());
			НастройкиРаспределения.Вставить("ПравилоРаспределенияРасходовНДД", Справочники.ПравилаРаспределенияРасходов.ПустаяСсылка());
		КонецЕсли;
	КонецЕсли;
	
	Возврат НастройкиРаспределения;
	
КонецФункции
	
#КонецОбласти

#Область ТекстыЗапросов

#Область РаспределениеРасходовНаФинансовыйРезультат

Функция ТекстПредварительныеОстаткиРасходов()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Расходы.Организация КАК Организация,
	|	Расходы.Подразделение КАК Подразделение,
	|	Расходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Расходы.СтатьяРасходов КАК СтатьяРасходов,
	|	Расходы.АналитикаРасходов КАК АналитикаРасходов,
	|	Расходы.СуммаОстаток КАК Сумма,
	|	Расходы.СуммаБезНДСОстаток КАК СуммаБезНДС,
	|	Расходы.СуммаРеглОстаток КАК СуммаРегл,
	//++ Локализация
	|	Расходы.ПостояннаяРазницаОстаток +
	//-- Локализация
	|	0 КАК ПостояннаяРазница,
	//++ Локализация
	|	Расходы.ВременнаяРазницаОстаток	+
	//-- Локализация
	|	0 КАК ВременнаяРазница,
	|	Расходы.СуммаРеглОстаток - Расходы.ПостояннаяРазницаОстаток - Расходы.ВременнаяРазницаОстаток КАК СуммаНУ,
	|	Расходы.СуммаУпрОстаток КАК СуммаУпр
	|ПОМЕСТИТЬ ПредварительныеОстаткиРасходов
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы.Остатки(
	|			&ГраницаПериода,
	|			(Организация, Подразделение, НаправлениеДеятельности, СтатьяРасходов, АналитикаРасходов) В
	|				(ВЫБРАТЬ
	|					НастроенныеРасходы.Организация,
	|					НастроенныеРасходы.Подразделение,
	|					НастроенныеРасходы.НаправлениеДеятельности,
	|					НастроенныеРасходы.СтатьяРасходов,
	|					НастроенныеРасходы.АналитикаРасходов
	|				ИЗ
	|					Регистраторы КАК НастроенныеРасходы
	|				)
	|) КАК Расходы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СтатьяРасходов,
	|	АналитикаРасходов,
	|	НаправлениеДеятельности,
	|	Подразделение,
	|	Организация";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстОстаткиРасходов(РежимОтладкиПоИсточникам)
	
	ТекстыВложенногоЗапроса = Новый Массив;
	
	#Область ИнициализацияВременнойТаблицыОстаткиРасходовПредварительная
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 0
	|	//РежимОтладки""Инициализация"" КАК ИсточникДанных,
	|	НЕОПРЕДЕЛЕНО КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	0 КАК Сумма,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	0 КАК СуммаНУ,
	|	0 КАК СуммаУпр
	|ПОМЕСТИТЬ ОстаткиРасходовПредварительная";
	Если РежимОтладкиПоИсточникам Тогда
		ТекстыВложенногоЗапроса.Добавить(ТекстЗапроса);
	КонецЕсли;
	#КонецОбласти
	
	#Область ПредварительныеОстаткиРасходов
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	//РежимОтладки""ПредварительныеОстаткиРасходов"" КАК ИсточникДанных,
	|	Расходы.Организация КАК Организация,
	|	Расходы.Подразделение КАК Подразделение,
	|	Расходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Расходы.СтатьяРасходов КАК СтатьяРасходов,
	|	Расходы.АналитикаРасходов КАК АналитикаРасходов,
	|	Расходы.Сумма КАК Сумма,
	|	Расходы.СуммаБезНДС КАК СуммаБезНДС,
	|	Расходы.СуммаРегл КАК СуммаРегл,
	|	Расходы.ПостояннаяРазница КАК ПостояннаяРазница,
	|	Расходы.ВременнаяРазница КАК ВременнаяРазница,
	|	Расходы.СуммаНУ КАК СуммаНУ,
	|	Расходы.СуммаУпр КАК СуммаУпр
	|ИЗ ПредварительныеОстаткиРасходов КАК Расходы";
	ТекстыВложенногоЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ТекущиеДвижения
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	//РежимОтладки""ТекущиеДвижения"" КАК ИсточникДанных,
	|	ТекущиеДвижения.Организация,
	|	ТекущиеДвижения.Подразделение,
	|	ТекущиеДвижения.НаправлениеДеятельности,
	|	ТекущиеДвижения.СтатьяРасходов,
	|	ТекущиеДвижения.АналитикаРасходов,
	|	ТекущиеДвижения.Сумма КАК Сумма,
	|	ТекущиеДвижения.СуммаБезНДС КАК СуммаБезНДС,
	|	ТекущиеДвижения.СуммаРегл КАК СуммаРегл,
	|	ТекущиеДвижения.ПостояннаяРазница КАК ПостояннаяРазница,
	|	ТекущиеДвижения.ВременнаяРазница КАК ВременнаяРазница,
	|	ТекущиеДвижения.СуммаРегл - ТекущиеДвижения.ПостояннаяРазница - ТекущиеДвижения.ВременнаяРазница КАК СуммаНУ,
	|	ТекущиеДвижения.СуммаУпр КАК СуммаУпр
	|ИЗ
	|	ПредварительныеОстаткиРасходов КАК Остатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК ТекущиеДвижения
	|		ПО ТекущиеДвижения.Организация = Остатки.Организация
	|			И ТекущиеДвижения.Подразделение = Остатки.Подразделение
	|			И ТекущиеДвижения.НаправлениеДеятельности = Остатки.НаправлениеДеятельности
	|			И ТекущиеДвижения.СтатьяРасходов = Остатки.СтатьяРасходов
	|			И ТекущиеДвижения.АналитикаРасходов = Остатки.АналитикаРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК РеквизитыДокументаРаспределения
	|		ПО РеквизитыДокументаРаспределения.Ссылка = ТекущиеДвижения.Регистратор
	|ГДЕ
	|	НЕ &ПредварительныйРасчет И
	|	ТекущиеДвижения.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ТекущиеДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ТекущиеДвижения.Активность
	|	И (
	//			Регистратором является документ РаспределениеПрочихЗатрат
	|			ТекущиеДвижения.Регистратор ССЫЛКА Документ.РаспределениеПрочихЗатрат
	|				И ТекущиеДвижения.ХозяйственнаяОперация В (
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНормируемыхРасходовПоНУ))

	//			Регистратором является документ РаспределениеДоходовПоНаправлениямДеятельности
	|			ИЛИ ТекущиеДвижения.Регистратор ССЫЛКА Документ.РаспределениеДоходовПоНаправлениямДеятельности
	|				И ТекущиеДвижения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
	|		)";
	ТекстыВложенногоЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ТекущиеДвиженияУказанногоДокумента
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	//РежимОтладки""ТекущиеДвиженияУказанногоДокумента"" КАК ИсточникДанных,
	|	ТекущиеДвижения.Организация,
	|	ТекущиеДвижения.Подразделение,
	|	ТекущиеДвижения.НаправлениеДеятельности,
	|	ТекущиеДвижения.СтатьяРасходов,
	|	ТекущиеДвижения.АналитикаРасходов,
	|	ТекущиеДвижения.Сумма,
	|	ТекущиеДвижения.СуммаБезНДС,
	|	ТекущиеДвижения.СуммаРегл,
	|	ТекущиеДвижения.ПостояннаяРазница,
	|	ТекущиеДвижения.ВременнаяРазница,
	|	ТекущиеДвижения.СуммаРегл
	|	- ТекущиеДвижения.ПостояннаяРазница
	|	- ТекущиеДвижения.ВременнаяРазница,
	|	ТекущиеДвижения.СуммаУпр
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК ТекущиеДвижения
	|ГДЕ
	|	&ПредварительныйРасчет И
	|	ТекущиеДвижения.Регистратор = &ДокументРаспределения";
	ТекстыВложенногоЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	СтрокаВложенногоЗапроса = СтрСоединить(ТекстыВложенногоЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Расходы.Организация КАК Организация,
	|	Расходы.Подразделение КАК Подразделение,
	|	Расходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Расходы.СтатьяРасходов КАК СтатьяРасходов,
	|	Расходы.АналитикаРасходов КАК АналитикаРасходов,
	|	СУММА(Расходы.Сумма) КАК Сумма,
	|	СУММА(Расходы.СуммаБезНДС) КАК СуммаБезНДС,
	|	СУММА(Расходы.СуммаРегл) КАК СуммаРегл,
	|	СУММА(Расходы.ПостояннаяРазница) КАК ПостояннаяРазница,
	|	СУММА(Расходы.ВременнаяРазница) КАК ВременнаяРазница,
	|	СУММА(Расходы.СуммаНУ) КАК СуммаНУ,
	|	СУММА(Расходы.СуммаУпр) КАК СуммаУпр
	|ПОМЕСТИТЬ ОстаткиРасходов
	|ИЗ
	|	ОстаткиРасходовПредварительная КАК Расходы
	|
	|СГРУППИРОВАТЬ ПО
	|	Расходы.СтатьяРасходов,
	|	Расходы.АналитикаРасходов,
	|	Расходы.НаправлениеДеятельности,
	|	Расходы.Подразделение,
	|	Расходы.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СтатьяРасходов,
	|	АналитикаРасходов,
	|	НаправлениеДеятельности,
	|	Подразделение,
	|	Организация";
	
	Если РежимОтладкиПоИсточникам Тогда
		
		ТекстыЗапроса = Новый Массив;
		ТекстыЗапроса.Добавить(СтрокаВложенногоЗапроса);
		ТекстыЗапроса.Добавить(ТекстЗапроса);
		ТекстЗапроса = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
		
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ОстаткиРасходовПредварительная", "(" + СтрокаВложенногоЗапроса + ")");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции


// ВТ РасходыКРаспределению: формирование остатков к распределению по НД
Функция ТекстРасходыКРаспределению(РежимОтладкиПоИсточникам)
	
	ТекстыВложенногоЗапроса = Новый Массив;
	
	#Область ИнициализацияВременнойТаблицыРасходыКРаспределениюПредварительная
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 0
	|	//РежимОтладки""Инициализация"" КАК ИсточникДанных,
	|	НЕОПРЕДЕЛЕНО КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	0 КАК Сумма,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаУпр,
	|	0 КАК СуммаРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	НЕОПРЕДЕЛЕНО КАК Регистратор,
	|	НЕОПРЕДЕЛЕНО КАК Дата,
	|	НЕОПРЕДЕЛЕНО КАК ТипБазыРаспределения,
	|	ЛОЖЬ КАК ТребуетсяРаспределение
	|ПОМЕСТИТЬ РасходыКРаспределениюПредварительная";
	Если РежимОтладкиПоИсточникам Тогда
		ТекстыВложенногоЗапроса.Добавить(ТекстЗапроса);
	КонецЕсли;
	#КонецОбласти
	
	// К распределению УУ и БУ
	#Область РаспределениеУУиБУ
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	//РежимОтладки""РаспределениеУУиБУ"" КАК ИсточникДанных,
	|	ОстаткиРасходов.Организация КАК Организация,
	|	ОстаткиРасходов.Подразделение КАК Подразделение,
	|	ОстаткиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ОстаткиРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	ОстаткиРасходов.АналитикаРасходов КАК АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА НЕ Регистраторы.УправленческийУчет
	|			ИЛИ НЕ ИсточникНастроек.ВариантРаспределенияРасходовУпр = 
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
	|			ТОГДА 0
	|		ИНАЧЕ ОстаткиРасходов.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА НЕ Регистраторы.УправленческийУчет
	|			ИЛИ НЕ ИсточникНастроек.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
	|			ТОГДА 0
	|		ИНАЧЕ ОстаткиРасходов.СуммаБезНДС
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ВЫБОР
	|		КОГДА НЕ Регистраторы.УправленческийУчет
	|			ИЛИ НЕ ИсточникНастроек.ВариантРаспределенияРасходовУпр = 
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
	|			ТОГДА 0
	|		ИНАЧЕ ОстаткиРасходов.СуммаУпр
	|	КОНЕЦ КАК СуммаУпр,
	|	ВЫБОР
	|		КОГДА НЕ Регистраторы.РегламентированныйУчет
	|			ИЛИ НЕ ИсточникНастроек.ВариантРаспределенияРасходовРегл = 
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
	|			ТОГДА 0
	|		КОГДА ЕСТЬNULL(ДолиСписания.Транспортные, ЛОЖЬ)
	|			ТОГДА ВЫРАЗИТЬ(ОстаткиРасходов.СуммаРегл * ДолиСписания.ДоляСписания КАК ЧИСЛО(31, 2))
	|		ИНАЧЕ ОстаткиРасходов.СуммаРегл
	|	КОНЕЦ КАК СуммаРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	Регистраторы.Регистратор КАК Регистратор,
	|	Регистраторы.Дата КАК Дата,
	|	Регистраторы.БазаРаспределенияПоПартиям КАК ТипБазыРаспределения,
	|	Регистраторы.ТребуетсяРаспределение
	|ИЗ
	|	ОстаткиРасходов КАК ОстаткиРасходов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК Регистраторы
	|		ПО ОстаткиРасходов.СтатьяРасходов = Регистраторы.СтатьяРасходов
	|			И ОстаткиРасходов.АналитикаРасходов = Регистраторы.АналитикаРасходов
	|			И ОстаткиРасходов.Организация = Регистраторы.Организация
	|			И ОстаткиРасходов.Подразделение = Регистраторы.Подразделение
	|			И ОстаткиРасходов.НаправлениеДеятельности = Регистраторы.НаправлениеДеятельности
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК ИсточникНастроек
	|		ПО ОстаткиРасходов.СтатьяРасходов = ИсточникНастроек.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДолиСписанияКосвенныхРасходов КАК ДолиСписания
	|		ПО ОстаткиРасходов.СтатьяРасходов = ДолиСписания.СтатьяРасходов
	|			И ОстаткиРасходов.Организация = ДолиСписания.Организация
	|ГДЕ
	|	(НЕ(ОстаткиРасходов.Сумма = 0 И ОстаткиРасходов.СуммаБезНДС = 0
	|		И ОстаткиРасходов.СуммаУпр = 0) И Регистраторы.УправленческийУчет 
	|		И ИсточникНастроек.ВариантРаспределенияРасходовУпр = 
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности))
	|	ИЛИ
	|	(НЕ ОстаткиРасходов.СуммаРегл = 0 И Регистраторы.РегламентированныйУчет 
	|		И ИсточникНастроек.ВариантРаспределенияРасходовРегл = 
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности))";
	ТекстыВложенногоЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	// К распределению суммы ПР и ВР, при выключенном использовании РУ, применяется простая логика трансляции разниц из остатков:
	// все образованные разницы распределяются документом с флажком РегламентированныйУчет без разделения на БУ и НУ
	#Область РаспределениеПРиВРБезРеглУчета
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	//РежимОтладки""РаспределениеПРиВРБезРеглУчета"" КАК ИсточникДанных,
	|	ОстаткиРасходов.Организация КАК Организация,
	|	ОстаткиРасходов.Подразделение КАК Подразделение,
	|	ОстаткиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ОстаткиРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	ОстаткиРасходов.АналитикаРасходов КАК АналитикаРасходов,
	|	0 КАК Сумма,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаУпр,
	|	0 КАК СуммаРегл,
	|	ВЫРАЗИТЬ (ОстаткиРасходов.ПостояннаяРазница КАК ЧИСЛО(31, 2))	КАК ПостояннаяРазница,
	|	ВЫРАЗИТЬ (ОстаткиРасходов.ВременнаяРазница КАК ЧИСЛО(31, 2))	КАК ВременнаяРазница,
	|	Регистраторы.Регистратор КАК Регистратор,
	|	Регистраторы.Дата КАК Дата,
	|	Регистраторы.БазаРаспределенияПоПартиям КАК ТипБазыРаспределения,
	|	Регистраторы.ТребуетсяРаспределение
	|ИЗ
	|	ОстаткиРасходов КАК ОстаткиРасходов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК Регистраторы
	|		ПО ОстаткиРасходов.СтатьяРасходов = Регистраторы.СтатьяРасходов
	|			И ОстаткиРасходов.АналитикаРасходов = Регистраторы.АналитикаРасходов
	|			И ОстаткиРасходов.Организация = Регистраторы.Организация
	|			И ОстаткиРасходов.Подразделение = Регистраторы.Подразделение
	|			И ОстаткиРасходов.НаправлениеДеятельности = Регистраторы.НаправлениеДеятельности
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК ИсточникНастроек
	|		ПО ОстаткиРасходов.СтатьяРасходов = ИсточникНастроек.Ссылка
	|ГДЕ
	|	Регистраторы.РегламентированныйУчет";
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
		ТекстыВложенногоЗапроса.Добавить(ТекстЗапроса);
	КонецЕсли;
	#КонецОбласти
	
	
	СтрокаВложенногоЗапроса = СтрСоединить(ТекстыВложенногоЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении(Истина));
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.Подразделение КАК Подразделение,
	|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВложенныйЗапрос.СтатьяРасходов КАК СтатьяРасходов,
	|	ВложенныйЗапрос.АналитикаРасходов КАК АналитикаРасходов,
	|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
	|	СУММА(ВложенныйЗапрос.СуммаБезНДС) КАК СуммаБезНДС,
	|	СУММА(ВложенныйЗапрос.СуммаУпр) КАК СуммаУпр,
	|	СУММА(ВложенныйЗапрос.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ВложенныйЗапрос.ПостояннаяРазница) КАК ПостояннаяРазница,
	|	СУММА(ВложенныйЗапрос.ВременнаяРазница) КАК ВременнаяРазница,
	|	ВложенныйЗапрос.Регистратор КАК Регистратор,
	|	ВложенныйЗапрос.Дата КАК Дата,
	|	ВложенныйЗапрос.ТипБазыРаспределения КАК ТипБазыРаспределения,
	|	ВложенныйЗапрос.ТребуетсяРаспределение КАК ТребуетсяРаспределение
	|ПОМЕСТИТЬ РасходыКРаспределению
	|ИЗ
	|	РасходыКРаспределениюПредварительная КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|		ВложенныйЗапрос.Организация,
	|		ВложенныйЗапрос.Подразделение,
	|		ВложенныйЗапрос.НаправлениеДеятельности,
	|		ВложенныйЗапрос.СтатьяРасходов,
	|		ВложенныйЗапрос.АналитикаРасходов,
	|		ВложенныйЗапрос.Регистратор,
	|		ВложенныйЗапрос.Дата,
	|		ВложенныйЗапрос.ТипБазыРаспределения,
	|		ВложенныйЗапрос.ТребуетсяРаспределение
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СтатьяРасходов,
	|	АналитикаРасходов,
	|	НаправлениеДеятельности,
	|	Подразделение,
	|	Организация";
	
	Если РежимОтладкиПоИсточникам Тогда
		
		ТекстыЗапроса = Новый Массив;
		ТекстыЗапроса.Добавить(СтрокаВложенногоЗапроса);
		ТекстыЗапроса.Добавить(ТекстЗапроса);
		ТекстЗапроса = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
		
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РасходыКРаспределениюПредварительная", "(" + СтрокаВложенногоЗапроса + ")");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстВтАктуальныеНаправленияДеятельности()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НД.Ссылка КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ ВтАктуальныеНаправленияДеятельности
	|ИЗ Справочник.НаправленияДеятельности КАК НД 
	|ГДЕ
	|	(НД.ДатаНачалаДеятельности <= &КонецПериода ИЛИ НД.ДатаНачалаДеятельности = ДАТАВРЕМЯ(1,1,1))
	|	И (НД.ДатаОкончанияДеятельности >= &НачалоПериода ИЛИ НД.ДатаОкончанияДеятельности = ДАТАВРЕМЯ(1,1,1))
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|ИНДЕКСИРОВАТЬ ПО
	|	НаправлениеДеятельности";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстФинансовыеРезультаты()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ФинансовыеРезультаты.Организация КАК Организация,
	|	ФинансовыеРезультаты.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СУММА(ВЫБОР
	|		КОГДА ФинансовыеРезультаты.СтатьяДоходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ВыручкаОтПродаж)
	|		ТОГДА ФинансовыеРезультаты.Доходы
	|		ИНАЧЕ 0 КОНЕЦ) КАК Доходы,
	|	СУММА(ВЫБОР
	|		КОГДА ФинансовыеРезультаты.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.СебестоимостьПродаж)
	|			ТОГДА ФинансовыеРезультаты.Расходы
	|		ИНАЧЕ 0 КОНЕЦ) КАК Расходы
	|ПОМЕСТИТЬ ФинансовыеРезультаты
	|ИЗ
	|	РегистрНакопления.ФинансовыеРезультаты КАК ФинансовыеРезультаты
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАктуальныеНаправленияДеятельности КАК НД
	|		ПО ФинансовыеРезультаты.НаправлениеДеятельности = НД.НаправлениеДеятельности
	|ГДЕ
	|	ФинансовыеРезультаты.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ФинансовыеРезультаты.Организация В(&МассивОрганизаций)
	|	И ФинансовыеРезультаты.Активность
	|	И (ТИПЗНАЧЕНИЯ(ФинансовыеРезультаты.Регистратор) = ТИП(Документ.РасчетСебестоимостиТоваров)
	|			ИЛИ ФинансовыеРезультаты.РасчетСебестоимости)
	|
	|СГРУППИРОВАТЬ ПО
	|	ФинансовыеРезультаты.Организация,
	|	ФинансовыеРезультаты.НаправлениеДеятельности
	|ИНДЕКСИРОВАТЬ ПО
	|	НаправлениеДеятельности,
	|	Организация";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстВыручкаИСебестоимостьПродаж()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Данные.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	Данные.НаправлениеДеятельностиКонтрагента КАК НаправлениеДеятельности,
	|	СУММА(Данные.СуммаВыручкиБезНДС) КАК Доходы,
	|	СУММА(Данные.СтоимостьУпр + Данные.ДопРасходыУпр + Данные.РезервПодОбесценениеУпр
	|		+ Данные.ПостатейныеПостоянныеУпр + Данные.ПостатейныеПеременныеУпр + Данные.ТрудозатратыУпр
	|		+ Данные.РасходыНаПродажуУпр) КАК Расходы,
	|	СУММА(Данные.СуммаВыручкиРегл) КАК ДоходыРегл,
	|	СУММА(Данные.СтоимостьРегл + Данные.ДопРасходыРегл + Данные.РезервПодОбесценениеРегл
	|		+ Данные.ПостатейныеПостоянныеРегл + Данные.ПостатейныеПеременныеРегл + Данные.ТрудозатратыРегл
	|		+ Данные.РасходыНаПродажуРегл) КАК РасходыРегл
	|ПОМЕСТИТЬ ВыручкаИСебестоимостьПродаж
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Данные
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАктуальныеНаправленияДеятельности КАК НД
	|		ПО НД.НаправлениеДеятельности = Данные.НаправлениеДеятельностиКонтрагента
	|ГДЕ
	|	Данные.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Данные.АналитикаУчетаПоПартнерам.Организация В(&МассивОрганизаций)
	|	И Данные.Активность
	|	И ИСТИНА В (ВЫБРАТЬ РАЗЛИЧНЫЕ ИСТИНА ИЗ Регистраторы КАК Т ГДЕ Т.ТребуетсяРаспределение
	|		И Т.ИсточникДанных = ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРаспределенияРасходов.ВыручкаИСебестоимостьПродаж)
	|		И Т.БазаРаспределенияПоПартиям В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВыручкаОтПродаж),
	|										ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СебестоимостьПродаж),
	|										ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВаловаяПрибыль)))
	|	И Данные.СуммаВыручкиРегл + Данные.СтоимостьРегл + Данные.ДопРасходыРегл + Данные.РезервПодОбесценениеРегл
	|	+ Данные.ПостатейныеПостоянныеРегл + Данные.ПостатейныеПеременныеРегл + Данные.ТрудозатратыРегл
	|	+ Данные.СуммаВыручкиБезНДС + Данные.СтоимостьУпр + Данные.ДопРасходыУпр + Данные.РезервПодОбесценениеУпр
	|	+ Данные.РасходыНаПродажуУпр + Данные.РасходыНаПродажуРегл
	|	+ Данные.ПостатейныеПостоянныеУпр + Данные.ПостатейныеПеременныеУпр + Данные.ТрудозатратыУпр <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	Данные.АналитикаУчетаПоПартнерам.Организация,
	|	Данные.НаправлениеДеятельностиКонтрагента
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НаправлениеДеятельности,
	|	Организация";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстБазаРаспределения()
	
	ТекстыЗапросаОбъединения = Новый Массив;
	
	#Область НулеваяВыборкаПомещениеВоВременнуюТаблицу
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 0
	|	//РежимОтладки""Инициализация"" КАК ИсточникДанных,
	|	НЕОПРЕДЕЛЕНО КАК Регистратор,
	|	НЕОПРЕДЕЛЕНО КАК ТипБазыРаспределения,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	0 КАК База,
	|	0 КАК БазаРегл
	|ПОМЕСТИТЬ БазаРаспределения";
	ТекстыЗапросаОбъединения.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область Распределение_ФинансовыеРезультаты
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	//РежимОтладки""Распределение_ФинансовыеРезультаты"" КАК ИсточникДанных,
	|	Регистраторы.Регистратор КАК Регистратор,
	|	Регистраторы.БазаРаспределенияПоПартиям КАК ТипБазыРаспределения,
	|	ЕСТЬNULL(ФинансовыеРезультаты.НаправлениеДеятельности, 
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА Регистраторы.БазаРаспределенияПоПартиям = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВыручкаОтПродаж)
	|		ТОГДА ЕСТЬNULL(ФинансовыеРезультаты.Доходы, 0)
	|		КОГДА Регистраторы.БазаРаспределенияПоПартиям = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СебестоимостьПродаж)
	|		ТОГДА ЕСТЬNULL(ФинансовыеРезультаты.Расходы, 0)
	|		ИНАЧЕ ЕСТЬNULL(ФинансовыеРезультаты.Доходы, 0) - ЕСТЬNULL(ФинансовыеРезультаты.Расходы, 0)
	|	КОНЕЦ КАК База,
	|	ВЫБОР
	|		КОГДА Регистраторы.БазаРаспределенияПоПартиям = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВыручкаОтПродаж)
	|		ТОГДА ЕСТЬNULL(ФинансовыеРезультаты.Доходы, 0)
	|		КОГДА Регистраторы.БазаРаспределенияПоПартиям = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СебестоимостьПродаж)
	|		ТОГДА ЕСТЬNULL(ФинансовыеРезультаты.Расходы, 0)
	|		ИНАЧЕ ЕСТЬNULL(ФинансовыеРезультаты.Доходы, 0) - ЕСТЬNULL(ФинансовыеРезультаты.Расходы, 0)
	|	КОНЕЦ КАК БазаРегл
	|ИЗ
	|	Регистраторы КАК Регистраторы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.ОтборПоНаправлениямДеятельности КАК НаправленияДеятельности
	|		ПО Регистраторы.Регистратор = НаправленияДеятельности.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ФинансовыеРезультаты КАК ФинансовыеРезультаты
	|		ПО Регистраторы.Организация = ФинансовыеРезультаты.Организация
	|			И (НаправленияДеятельности.НаправлениеДеятельности = ФинансовыеРезультаты.НаправлениеДеятельности
	|				ИЛИ НаправленияДеятельности.НаправлениеДеятельности ЕСТЬ NULL)
	|ГДЕ
	|	Регистраторы.ТребуетсяРаспределение
	|	И Регистраторы.БазаРаспределенияПоПартиям В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВыручкаОтПродаж),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СебестоимостьПродаж),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВаловаяПрибыль))
	|	И Регистраторы.ИсточникДанных <> ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРаспределенияРасходов.ВыручкаИСебестоимостьПродаж)";
	ТекстыЗапросаОбъединения.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область Распределение_ВыручкаИСебестоимостьПродаж
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	//РежимОтладки""Распределение_ВыручкаИСебестоимостьПродаж"" КАК ИсточникДанных,
	|	Регистраторы.Регистратор КАК Регистратор,
	|	Регистраторы.БазаРаспределенияПоПартиям КАК ТипБазыРаспределения,
	|	ЕСТЬNULL(ВыручкаИСебестоимостьПродаж.НаправлениеДеятельности, 
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА Регистраторы.БазаРаспределенияПоПартиям = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВыручкаОтПродаж)
	|		ТОГДА ЕСТЬNULL(ВыручкаИСебестоимостьПродаж.Доходы, 0)
	|		КОГДА Регистраторы.БазаРаспределенияПоПартиям = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СебестоимостьПродаж)
	|		ТОГДА ЕСТЬNULL(ВыручкаИСебестоимостьПродаж.Расходы, 0)
	|		ИНАЧЕ ЕСТЬNULL(ВыручкаИСебестоимостьПродаж.Доходы, 0) - ЕСТЬNULL(ВыручкаИСебестоимостьПродаж.Расходы, 0)
	|	КОНЕЦ КАК База,
	|	ВЫБОР
	|		КОГДА Регистраторы.БазаРаспределенияПоПартиям = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВыручкаОтПродаж)
	|		ТОГДА ЕСТЬNULL(ВыручкаИСебестоимостьПродаж.ДоходыРегл, 0)
	|		КОГДА Регистраторы.БазаРаспределенияПоПартиям = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СебестоимостьПродаж)
	|		ТОГДА ЕСТЬNULL(ВыручкаИСебестоимостьПродаж.РасходыРегл, 0)
	|		ИНАЧЕ ЕСТЬNULL(ВыручкаИСебестоимостьПродаж.ДоходыРегл, 0) - ЕСТЬNULL(ВыручкаИСебестоимостьПродаж.РасходыРегл, 0)
	|	КОНЕЦ КАК БазаРегл
	|ИЗ
	|	Регистраторы КАК Регистраторы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.ОтборПоНаправлениямДеятельности КАК НаправленияДеятельности
	|		ПО Регистраторы.Регистратор = НаправленияДеятельности.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
	|		ПО Регистраторы.Организация = ВыручкаИСебестоимостьПродаж.Организация
	|			И (НаправленияДеятельности.НаправлениеДеятельности = ВыручкаИСебестоимостьПродаж.НаправлениеДеятельности
	|				ИЛИ НаправленияДеятельности.НаправлениеДеятельности ЕСТЬ NULL)
	|ГДЕ
	|	Регистраторы.ТребуетсяРаспределение
	|	И Регистраторы.БазаРаспределенияПоПартиям В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВыручкаОтПродаж),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СебестоимостьПродаж),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВаловаяПрибыль))
	|	И Регистраторы.ИсточникДанных = ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРаспределенияРасходов.ВыручкаИСебестоимостьПродаж)";
	ТекстыЗапросаОбъединения.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область Распределение_ПоУказаннымНаправлениям
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	//РежимОтладки""Распределение_ПоУказаннымНаправлениям"" КАК ИсточникДанных,
	|	НаправленияДеятельности.Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПустаяСсылка),
	|	НаправленияДеятельности.НаправлениеДеятельности,
	|	НаправленияДеятельности.ДоляСтоимости,
	|	НаправленияДеятельности.ДоляСтоимости
	|ИЗ
	|	Регистраторы КАК Регистраторы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.НаправленияДеятельности КАК НаправленияДеятельности
	|		ПО Регистраторы.Регистратор = НаправленияДеятельности.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАктуальныеНаправленияДеятельности КАК НД
	|			ПО НаправленияДеятельности.НаправлениеДеятельности = НД.НаправлениеДеятельности
	|ГДЕ
	|	Регистраторы.ТребуетсяРаспределение";
	ТекстыЗапросаОбъединения.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапросаОбъединения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстБазыРаспределенияСводно()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	БазаРаспределения.Регистратор КАК Регистратор,
	|	БазаРаспределения.ТипБазыРаспределения КАК ТипБазыРаспределения,
	|	СУММА(БазаРаспределения.База) КАК База,
	|	СУММА(БазаРаспределения.БазаРегл) КАК БазаРегл
	|ПОМЕСТИТЬ БазыРаспределенияСводно
	|ИЗ
	|	БазаРаспределения КАК БазаРаспределения
	|
	|СГРУППИРОВАТЬ ПО
	|	БазаРаспределения.Регистратор,
	|	БазаРаспределения.ТипБазыРаспределения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	ТипБазыРаспределения";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстРаспределенныеРасходы()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА НЕ РасходыКРаспределению.ТребуетсяРаспределение
	|				ИЛИ НЕ &ФормироватьФинансовыйРезультат
	|			ТОГДА РасходыКРаспределению.Сумма
	|		КОГДА ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РасходыКРаспределению.Сумма * (БазаРаспределения.База / БазыРаспределенияСводно.База) КАК ЧИСЛО(31, 2))
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА НЕ РасходыКРаспределению.ТребуетсяРаспределение
	|				ИЛИ НЕ &ФормироватьФинансовыйРезультат
	|			ТОГДА РасходыКРаспределению.СуммаБезНДС
	|		КОГДА ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РасходыКРаспределению.СуммаБезНДС * (БазаРаспределения.База / БазыРаспределенияСводно.База) КАК ЧИСЛО(31, 2))
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ВЫБОР
	|		КОГДА НЕ РасходыКРаспределению.ТребуетсяРаспределение
	|				ИЛИ НЕ &ФормироватьФинансовыйРезультат
	|			ТОГДА РасходыКРаспределению.СуммаУпр
	|		КОГДА ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РасходыКРаспределению.СуммаУпр * (БазаРаспределения.База / БазыРаспределенияСводно.База) КАК ЧИСЛО(31, 2))
	|	КОНЕЦ КАК СуммаУпр,
	|	ВЫБОР
	|		КОГДА НЕ РасходыКРаспределению.ТребуетсяРаспределение
	|				ИЛИ НЕ &ФормироватьФинансовыйРезультат
	|			ТОГДА РасходыКРаспределению.СуммаРегл
	|		КОГДА ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РасходыКРаспределению.СуммаРегл * (БазаРаспределения.БазаРегл / БазыРаспределенияСводно.БазаРегл) КАК ЧИСЛО(31, 2))
	|	КОНЕЦ КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА НЕ РасходыКРаспределению.ТребуетсяРаспределение
	|				ИЛИ НЕ &ФормироватьФинансовыйРезультат
	|			ТОГДА РасходыКРаспределению.ПостояннаяРазница
	|		КОГДА ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РасходыКРаспределению.ПостояннаяРазница * (БазаРаспределения.БазаРегл / БазыРаспределенияСводно.БазаРегл) КАК ЧИСЛО(31, 2))
	|	КОНЕЦ КАК ПостояннаяРазница,
	|	ВЫБОР
	|		КОГДА НЕ РасходыКРаспределению.ТребуетсяРаспределение
	|				ИЛИ НЕ &ФормироватьФинансовыйРезультат
	|			ТОГДА РасходыКРаспределению.ВременнаяРазница
	|		КОГДА ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РасходыКРаспределению.ВременнаяРазница * (БазаРаспределения.БазаРегл / БазыРаспределенияСводно.БазаРегл) КАК ЧИСЛО(31, 2))
	|	КОНЕЦ КАК ВременнаяРазница,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ БазаРаспределения.База / БазыРаспределенияСводно.База
	|	КОНЕЦ КАК КоэффициентРаспределения,
	|	ЕСТЬNULL(БазаРаспределения.База, 0) КАК База,
	|	ЕСТЬNULL(БазаРаспределения.БазаРегл, 0) КАК БазаРегл,
	|	РасходыКРаспределению.Регистратор КАК Регистратор,
	|	РасходыКРаспределению.Организация КАК Организация,
	|	РасходыКРаспределению.Дата КАК Дата,
	|	РасходыКРаспределению.Подразделение КАК Подразделение,
	|	РасходыКРаспределению.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	РасходыКРаспределению.СтатьяРасходов КАК СтатьяРасходов,
	|	РасходыКРаспределению.АналитикаРасходов КАК АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА НЕ РасходыКРаспределению.ТребуетсяРаспределение
	|			ТОГДА 
	|				ВЫБОР 
	|					КОГДА НЕ РасходыКРаспределению.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|						ТОГДА РасходыКРаспределению.НаправлениеДеятельности
	|					КОГДА ТИПЗНАЧЕНИЯ(РасходыКРаспределению.АналитикаРасходов) = ТИП(Справочник.НаправленияДеятельности)
	|						И НЕ РасходыКРаспределению.АналитикаРасходов = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|						ТОГДА РасходыКРаспределению.АналитикаРасходов
	|					ИНАЧЕ
	|						ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|				КОНЕЦ
	|		ИНАЧЕ
	|			БазаРаспределения.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельностиПриемник,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности) КАК ХозяйственнаяОперация
	|ПОМЕСТИТЬ РаспределенныеРасходы
	|ИЗ
	|	РасходыКРаспределению КАК РасходыКРаспределению
	|		ЛЕВОЕ СОЕДИНЕНИЕ БазаРаспределения КАК БазаРаспределения
	|		ПО РасходыКРаспределению.Регистратор = БазаРаспределения.Регистратор
	|			И РасходыКРаспределению.ТипБазыРаспределения = БазаРаспределения.ТипБазыРаспределения
	|			И (БазаРаспределения.База <> 0 ИЛИ БазаРаспределения.БазаРегл <> 0)
	|		ЛЕВОЕ СОЕДИНЕНИЕ БазыРаспределенияСводно КАК БазыРаспределенияСводно
	|		ПО РасходыКРаспределению.Регистратор = БазыРаспределенияСводно.Регистратор
	|			И РасходыКРаспределению.ТипБазыРаспределения = БазыРаспределенияСводно.ТипБазыРаспределения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	КоэффициентРаспределения";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстДокументыРаспределения()
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РаспределенныеРасходы.Регистратор КАК Регистратор,
	|	РаспределенныеРасходы.Организация КАК Организация,
	|	РаспределенныеРасходы.Дата КАК Дата
	|ПОМЕСТИТЬ ДокументыРаспределения
	|ИЗ
	|	РаспределенныеРасходы КАК РаспределенныеРасходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ БазыРаспределенияСводно КАК БазаРаспределения
	|		ПО РаспределенныеРасходы.Регистратор = БазаРаспределения.Регистратор
	|ГДЕ
	|	НЕ (РаспределенныеРасходы.Сумма = 0
	|	И РаспределенныеРасходы.СуммаБезНДС = 0
	|	И РаспределенныеРасходы.СуммаУпр = 0
	|	И РаспределенныеРасходы.СуммаРегл = 0
	|	И РаспределенныеРасходы.ПостояннаяРазница = 0
	|	И РаспределенныеРасходы.ВременнаяРазница = 0)
	|		ИЛИ НЕ БазаРаспределения.Регистратор ЕСТЬ NULL
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстМаксимальныеКоэффициентыПоРегистраторам()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РаспределенныеРасходы.Регистратор КАК Регистратор,
	|	МАКСИМУМ(РаспределенныеРасходы.КоэффициентРаспределения) КАК КоэффициентРаспределения
	|ПОМЕСТИТЬ МаксимальныеКоэффициентыПоРегистраторам
	|ИЗ
	|	РаспределенныеРасходы КАК РаспределенныеРасходы
	|
	|СГРУППИРОВАТЬ ПО
	|	РаспределенныеРасходы.Регистратор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстНаправленияДеятельностиДляПогрешности()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	МаксимальныеКоэффициентыПоРегистраторам.Регистратор КАК Регистратор,
	|	МАКСИМУМ(РаспределенныеРасходы.НаправлениеДеятельностиПриемник) КАК НаправлениеДеятельностиПриемник
	|ПОМЕСТИТЬ НаправленияДеятельностиДляПогрешности
	|ИЗ
	|	МаксимальныеКоэффициентыПоРегистраторам КАК МаксимальныеКоэффициентыПоРегистраторам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РаспределенныеРасходы КАК РаспределенныеРасходы
	|		ПО МаксимальныеКоэффициентыПоРегистраторам.Регистратор = РаспределенныеРасходы.Регистратор
	|			И МаксимальныеКоэффициентыПоРегистраторам.КоэффициентРаспределения = РаспределенныеРасходы.КоэффициентРаспределения
	|
	|СГРУППИРОВАТЬ ПО
	|	МаксимальныеКоэффициентыПоРегистраторам.Регистратор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстПогрешностиОкругления()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
	|	СУММА(ВложенныйЗапрос.СуммаБезНДС) КАК СуммаБезНДС,
	|	СУММА(ВложенныйЗапрос.СуммаУпр) КАК СуммаУпр,
	|	СУММА(ВложенныйЗапрос.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ВложенныйЗапрос.ПостояннаяРазница) КАК ПостояннаяРазница,
	|	СУММА(ВложенныйЗапрос.ВременнаяРазница) КАК ВременнаяРазница,
	|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВложенныйЗапрос.СтатьяРасходов КАК СтатьяРасходов,
	|	ВложенныйЗапрос.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ПогрешностиОкругления
	|ИЗ
	|	(ВЫБРАТЬ
	|		РасходыКРаспределению.Сумма,
	|		РасходыКРаспределению.СуммаБезНДС,
	|		РасходыКРаспределению.СуммаУпр,
	|		РасходыКРаспределению.СуммаРегл,
	//++ Локализация
	|		РасходыКРаспределению.ПостояннаяРазница +
	//-- Локализация
	|		0 КАК ПостояннаяРазница,
	//++ Локализация
	|		РасходыКРаспределению.ВременнаяРазница +
	//-- Локализация
	|		0 КАК ВременнаяРазница,
	|		РасходыКРаспределению.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		РасходыКРаспределению.СтатьяРасходов КАК СтатьяРасходов,
	|		РасходыКРаспределению.Регистратор КАК Регистратор
	|	ИЗ
	|		РасходыКРаспределению КАК РасходыКРаспределению
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыРаспределения КАК ДокументыРаспределения
	|			ПО РасходыКРаспределению.Регистратор = ДокументыРаспределения.Регистратор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		-РаспределенныеРасходы.Сумма,
	|		-РаспределенныеРасходы.СуммаБезНДС,
	|		-РаспределенныеРасходы.СуммаУпр,
	|		-РаспределенныеРасходы.СуммаРегл,
	//++ Локализация
	|		-РаспределенныеРасходы.ПостояннаяРазница +
	//-- Локализация
	|		0,
	//++ Локализация
	|		-РаспределенныеРасходы.ВременнаяРазница +
	//-- Локализация
	|		0,
	|		РаспределенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		РаспределенныеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|		РаспределенныеРасходы.Регистратор
	|	ИЗ
	|		РаспределенныеРасходы КАК РаспределенныеРасходы) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.НаправлениеДеятельности,
	|	ВложенныйЗапрос.СтатьяРасходов,
	|	ВложенныйЗапрос.Регистратор
	|
	|ИМЕЮЩИЕ
	|	НЕ (СУММА(ВложенныйЗапрос.Сумма) = 0
	|		И СУММА(ВложенныйЗапрос.СуммаБезНДС) = 0
	|		И СУММА(ВложенныйЗапрос.СуммаУпр) = 0
	|		И СУММА(ВложенныйЗапрос.СуммаРегл) = 0
	|		И СУММА(ВложенныйЗапрос.ПостояннаяРазница) = 0
	|		И СУММА(ВложенныйЗапрос.ВременнаяРазница) = 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	НаправлениеДеятельности,
	|	СтатьяРасходов";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстРегистраторыКОчисткеДвижений(РежимОтладкиПоИсточникам)
	
	ТекстыВложенногоЗапроса = Новый Массив;
	
	#Область ИнициализацияВременнойТаблицыРегистраторыКОчисткеДвиженийПредварительная
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 0
	|	//РежимОтладки""Инициализация"" КАК ИсточникДанных,
	|	НЕОПРЕДЕЛЕНО КАК Регистратор,
	|	НЕОПРЕДЕЛЕНО КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	0 КАК СуммаРегл,
	|	0 КАК СуммаНУ,
	|	0 КАК Сумма,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаУпр
	|ПОМЕСТИТЬ РегистраторыКОчисткеДвиженийПредварительная";
	Если РежимОтладкиПоИсточникам Тогда
		ТекстыВложенногоЗапроса.Добавить(ТекстЗапроса);
	КонецЕсли;
	#КонецОбласти
	
	#Область ОстаткиРасходов
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	//РежимОтладки""ОстаткиРасходов"" КАК ИсточникДанных,
	|	Регистраторы.Регистратор 				КАК Регистратор,
	|	ОстаткиРасходов.Организация 			КАК Организация,
	|	ОстаткиРасходов.Подразделение 			КАК Подразделение,
	|	ОстаткиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ОстаткиРасходов.СтатьяРасходов			КАК СтатьяРасходов,
	|	ОстаткиРасходов.АналитикаРасходов		КАК АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА Реквизиты.РегламентированныйУчет
	|			ТОГДА ОстаткиРасходов.СуммаРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА Реквизиты.НалоговыйУчет
	|			ТОГДА ОстаткиРасходов.СуммаНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНУ,
	|	ВЫБОР
	|		КОГДА Реквизиты.УправленческийУчет
	|			ТОГДА ОстаткиРасходов.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА Реквизиты.УправленческийУчет
	|			ТОГДА ОстаткиРасходов.СуммаБезНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ВЫБОР
	|		КОГДА Реквизиты.УправленческийУчет
	|			ТОГДА ОстаткиРасходов.СуммаУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУпр
	|ИЗ
	|	ПредварительныеОстаткиРасходов КАК ОстаткиРасходов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК Регистраторы
	|		ПО ОстаткиРасходов.Организация = Регистраторы.Организация
	|			И ОстаткиРасходов.Подразделение = Регистраторы.Подразделение
	|			И ОстаткиРасходов.НаправлениеДеятельности = Регистраторы.НаправлениеДеятельности
	|			И ОстаткиРасходов.СтатьяРасходов = Регистраторы.СтатьяРасходов
	|			И ОстаткиРасходов.АналитикаРасходов = Регистраторы.АналитикаРасходов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК Реквизиты
	|		ПО Регистраторы.Регистратор = Реквизиты.Ссылка";
	ТекстыВложенногоЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ТекущиеДвижения
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	//РежимОтладки""ТекущиеДвижения"" КАК ИсточникДанных,
	|	Регистраторы.Регистратор 				КАК Регистратор,
	|	ОстаткиРасходов.Организация 			КАК Организация,
	|	ОстаткиРасходов.Подразделение 			КАК Подразделение,
	|	ОстаткиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ОстаткиРасходов.СтатьяРасходов			КАК СтатьяРасходов,
	|	ОстаткиРасходов.АналитикаРасходов		КАК АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА Реквизиты.РегламентированныйУчет
	|			ТОГДА ТекущиеДвижения.СуммаРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА Реквизиты.РегламентированныйУчет
	|			ТОГДА ТекущиеДвижения.СуммаРегл
	//++ Локализация
	|				- ТекущиеДвижения.ПостояннаяРазница - ТекущиеДвижения.ВременнаяРазница
	//-- Локализация
	|		ИНАЧЕ 0
	|	КОНЕЦ 
	//++ Локализация
	|	-ВЫБОР
	|		КОГДА Реквизиты.НалоговыйУчет
	|			ТОГДА ТекущиеДвижения.СуммаРегл - ТекущиеДвижения.ПостояннаяРазница - ТекущиеДвижения.ВременнаяРазница
	|		ИНАЧЕ 0
	|	КОНЕЦ
	//-- Локализация
	|	КАК СуммаНУ,
	|	ВЫБОР
	|		КОГДА Реквизиты.УправленческийУчет
	|			ТОГДА ТекущиеДвижения.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА Реквизиты.УправленческийУчет
	|			ТОГДА ТекущиеДвижения.СуммаБезНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ВЫБОР
	|		КОГДА Реквизиты.УправленческийУчет
	|			ТОГДА ТекущиеДвижения.СуммаУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУпр
	|ИЗ
	|	ПредварительныеОстаткиРасходов КАК ОстаткиРасходов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК Регистраторы
	|		ПО ОстаткиРасходов.Организация = Регистраторы.Организация
	|			И ОстаткиРасходов.Подразделение = Регистраторы.Подразделение
	|			И ОстаткиРасходов.НаправлениеДеятельности = Регистраторы.НаправлениеДеятельности
	|			И ОстаткиРасходов.СтатьяРасходов = Регистраторы.СтатьяРасходов
	|			И ОстаткиРасходов.АналитикаРасходов = Регистраторы.АналитикаРасходов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК ТекущиеДвижения
	|		ПО Регистраторы.Регистратор = ТекущиеДвижения.Регистратор
	|			И (ТекущиеДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|			И (ТекущиеДвижения.Организация = ОстаткиРасходов.Организация)
	|			И (ТекущиеДвижения.Подразделение = ОстаткиРасходов.Подразделение)
	|			И (ТекущиеДвижения.НаправлениеДеятельности = ОстаткиРасходов.НаправлениеДеятельности)
	|			И (ТекущиеДвижения.СтатьяРасходов = ОстаткиРасходов.СтатьяРасходов)
	|			И (ТекущиеДвижения.АналитикаРасходов = ОстаткиРасходов.АналитикаРасходов)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК Реквизиты
	|		ПО Реквизиты.Ссылка = ТекущиеДвижения.Регистратор
	|ГДЕ
	|	ТекущиеДвижения.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ТекущиеДвижения.Активность";
	ТекстыВложенногоЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	СтрокаВложенногоЗапроса = СтрСоединить(ТекстыВложенногоЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РасчетныеОстатки.Регистратор 				КАК Регистратор,
	|	РасчетныеОстатки.Организация 				КАК Организация,
	|	РасчетныеОстатки.Подразделение 				КАК Подразделение,
	|	РасчетныеОстатки.НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	РасчетныеОстатки.СтатьяРасходов				КАК СтатьяРасходов,
	|	РасчетныеОстатки.АналитикаРасходов			КАК АналитикаРасходов
	|ПОМЕСТИТЬ РегистраторыКОчисткеДвижений
	|ИЗ
	|	РегистраторыКОчисткеДвиженийПредварительная КАК РасчетныеОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетныеОстатки.Регистратор,
	|	РасчетныеОстатки.Организация,
	|	РасчетныеОстатки.Подразделение,
	|	РасчетныеОстатки.НаправлениеДеятельности,
	|	РасчетныеОстатки.СтатьяРасходов,
	|	РасчетныеОстатки.АналитикаРасходов
	|
	|ИМЕЮЩИЕ
	|	СУММА(РасчетныеОстатки.Сумма) = 0
	|	И СУММА(РасчетныеОстатки.СуммаБезНДС) = 0
	|	И СУММА(РасчетныеОстатки.СуммаУпр) = 0
	|	И СУММА(РасчетныеОстатки.СуммаРегл) = 0
	|	И СУММА(РасчетныеОстатки.СуммаНУ) = 0";
	
	Если РежимОтладкиПоИсточникам Тогда
		
		ТекстыЗапроса = Новый Массив;
		ТекстыЗапроса.Добавить(СтрокаВложенногоЗапроса);
		ТекстыЗапроса.Добавить(ТекстЗапроса);
		ТекстЗапроса = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
		
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РегистраторыКОчисткеДвиженийПредварительная", "(" + СтрокаВложенногоЗапроса + ")");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстРаспределенныеАналитикиРасходов()
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АналитикиРасходов.Организация КАК Организация,
	|	АналитикиРасходов.Подразделение КАК Подразделение,
	|	АналитикиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	АналитикиРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	АналитикиРасходов.АналитикаРасходов КАК АналитикаРасходов
	|ПОМЕСТИТЬ РаспределенныеАналитикиРасходов
	|ИЗ
	|	(ВЫБРАТЬ
	|		РаспределенныеРасходы.Организация КАК Организация,
	|		РаспределенныеРасходы.Подразделение КАК Подразделение,
	|		РаспределенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		РаспределенныеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|		РаспределенныеРасходы.АналитикаРасходов КАК АналитикаРасходов
	|	ИЗ
	|		РаспределенныеРасходы КАК РаспределенныеРасходы
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыРаспределения КАК Документы
	|			ПО РаспределенныеРасходы.Регистратор = Документы.Регистратор
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		АналитикиРасходов.Организация КАК Организация,
	|		АналитикиРасходов.Подразделение КАК Подразделение,
	|		АналитикиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		АналитикиРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|		АналитикиРасходов.АналитикаРасходов КАК АналитикаРасходов
	|	ИЗ
	|		РегистраторыКОчисткеДвижений КАК АналитикиРасходов
	|	) КАК АналитикиРасходов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Подразделение,
	|	НаправлениеДеятельности,
	|	СтатьяРасходов,
	|	АналитикаРасходов";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстДокументыСНовымиДвижениями()
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор,
	|	Т.Организация,
	|	Т.Дата
	|ПОМЕСТИТЬ ДокументыСНовымиДвижениями
	|ИЗ
	|	(ВЫБРАТЬ
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.Дата
	|	ИЗ
	|		ДокументыРаспределения КАК Т
	|) КАК Т";
	
	Возврат ТекстЗапроса;
	
КонецФункции


Функция ТекстРезультатРаспределения(РежимОтладкиПоИсточникам)
	
	ТекстыВложенногоЗапроса = Новый Массив;
	
	#Область ИнициализацияВременнойТаблицыРезультатРаспределенияПредварительная
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 0
	|	//РежимОтладки""Инициализация"" КАК ИсточникДанных,
	|	НЕОПРЕДЕЛЕНО КАК Период,
	|	НЕОПРЕДЕЛЕНО КАК ВидДвижения,
	|	НЕОПРЕДЕЛЕНО КАК Регистратор,
	|	НЕОПРЕДЕЛЕНО КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиПриемник,
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	0 КАК Сумма,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаУпр,
	|	0 КАК СуммаРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	0 КАК Расходы,
	|	0 КАК БазаРаспределения
	|ПОМЕСТИТЬ РезультатРаспределенияПредварительная";
	Если РежимОтладкиПоИсточникам Тогда
		ТекстыВложенногоЗапроса.Добавить(ТекстЗапроса);
	КонецЕсли;
	#КонецОбласти
	
	#Область РаспределенныеРасходы
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	//РежимОтладки""РаспределенныеРасходы"" КАК ИсточникДанных,
	|	&КонецПериода КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	РаспределенныеРасходы.Регистратор КАК Регистратор,
	|	РаспределенныеРасходы.Организация КАК Организация,
	|	РаспределенныеРасходы.Подразделение КАК Подразделение,
	|	РаспределенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	РаспределенныеРасходы.НаправлениеДеятельностиПриемник КАК НаправлениеДеятельностиПриемник,
	|	РаспределенныеРасходы.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	РаспределенныеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	РаспределенныеРасходы.АналитикаРасходов КАК АналитикаРасходов,
	|	РаспределенныеРасходы.Сумма КАК Сумма,
	|	РаспределенныеРасходы.СуммаБезНДС КАК СуммаБезНДС,
	|	РаспределенныеРасходы.СуммаУпр КАК СуммаУпр,
	|	РаспределенныеРасходы.СуммаРегл КАК СуммаРегл,
	// Нормируемые расходы в НУ распределяются отдельно и сохраняются во временной таблице СписаниеНУ.
	// Поэтому в строке распределения расходов сумма НУ должна быть равна 0. Для этого на всю сумму БУ должна
	// быть сформирована временная разница. 
	// Постоянная разница у нормируемых расходов уже распределена и отражена во временной таблице СписаниеНУ,
	// поэтому в движении по списанию нормируемых расходов постонная разница должна быть равна 0.  
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(ДолиСписания.Нормируемые, ЛОЖЬ) ТОГДА 0 
	|		ИНАЧЕ РаспределенныеРасходы.ПостояннаяРазница 
	|	КОНЕЦ КАК ПостояннаяРазница,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(ДолиСписания.Нормируемые, ЛОЖЬ) 
	|			ТОГДА РаспределенныеРасходы.СуммаРегл 
	|		ИНАЧЕ РаспределенныеРасходы.ВременнаяРазница 
	|	КОНЕЦ КАК ВременнаяРазница,
	|	РаспределенныеРасходы.Сумма КАК Расходы,
	|	РаспределенныеРасходы.База КАК БазаРаспределения
	|ИЗ
	|	РаспределенныеРасходы КАК РаспределенныеРасходы
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтДолиСписанияКосвенныхРасходов КАК ДолиСписания
	|	ПО РаспределенныеРасходы.СтатьяРасходов = ДолиСписания.СтатьяРасходов
	|		И РаспределенныеРасходы.Организация = ДолиСписания.Организация";
	ТекстыВложенногоЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	
	#Область РасходыКРаспределению
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	//РежимОтладки""РасходыКРаспределению"" КАК ИсточникДанных,
	|	&КонецПериода,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	РасходыКРаспределению.Регистратор,
	|	РасходыКРаспределению.Организация,
	|	РасходыКРаспределению.Подразделение,
	|	РасходыКРаспределению.НаправлениеДеятельности,
	|	НаправленияДеятельностиДляПогрешности.НаправлениеДеятельностиПриемник,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности),
	|	РасходыКРаспределению.СтатьяРасходов,
	|	РасходыКРаспределению.АналитикаРасходов,
	|	ПогрешностиОкругления.Сумма,
	|	ПогрешностиОкругления.СуммаБезНДС,
	|	ПогрешностиОкругления.СуммаУпр,
	|	ПогрешностиОкругления.СуммаРегл,
	// Нормируемые расходы в НУ распределяются отдельно и сохраняются во временной таблице СписаниеНУ.
	// Поэтому в строке распределения расходов сумма НУ должна быть равна 0. Для этого на всю сумму БУ должна
	// быть сформирована временная разница. 
	// Постоянная разница у нормируемых расходов уже распределена и отражена во временной таблице СписаниеНУ,
	// поэтому в движении по списанию нормируемых расходов постонная разница должна быть равна 0.  
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(ДолиСписания.Нормируемые, ЛОЖЬ) ТОГДА 0 
	|		ИНАЧЕ ПогрешностиОкругления.ПостояннаяРазница 
	|	КОНЕЦ КАК ПостояннаяРазница,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(ДолиСписания.Нормируемые, ЛОЖЬ) 
	|			ТОГДА ПогрешностиОкругления.СуммаРегл 
	|		ИНАЧЕ ПогрешностиОкругления.ВременнаяРазница 
	|	КОНЕЦ КАК ВременнаяРазница,
	|	ПогрешностиОкругления.Сумма КАК Расходы,
	|	0 КАК БазаРаспределения
	|ИЗ
	|	РасходыКРаспределению КАК РасходыКРаспределению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПогрешностиОкругления КАК ПогрешностиОкругления
	|		ПО РасходыКРаспределению.Регистратор = ПогрешностиОкругления.Регистратор
	|			И РасходыКРаспределению.СтатьяРасходов = ПогрешностиОкругления.СтатьяРасходов
	|			И РасходыКРаспределению.НаправлениеДеятельности = ПогрешностиОкругления.НаправлениеДеятельности
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ НаправленияДеятельностиДляПогрешности КАК НаправленияДеятельностиДляПогрешности
	|		ПО РасходыКРаспределению.Регистратор = НаправленияДеятельностиДляПогрешности.Регистратор
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтДолиСписанияКосвенныхРасходов КАК ДолиСписания
	|	ПО РасходыКРаспределению.СтатьяРасходов = ДолиСписания.СтатьяРасходов
	|		И РасходыКРаспределению.Организация = ДолиСписания.Организация";
	ТекстыВложенногоЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область РегистраторыКОчисткеДвижений
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	//РежимОтладки""РегистраторыКОчисткеДвижений"" КАК ИсточникДанных,
	|	&КонецПериода,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	РегистраторыИАналитики.Регистратор,
	|	РегистраторыИАналитики.Организация,
	|	РегистраторыИАналитики.Подразделение,
	|	РегистраторыИАналитики.НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка),
	|	РегистраторыИАналитики.СтатьяРасходов,
	|	РегистраторыИАналитики.АналитикаРасходов,
	|	0 КАК Сумма,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаУпр,
	|	0 КАК СуммаРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	0 КАК Расходы,
	|	0 КАК БазаРаспределения
	|ИЗ
	|	РегистраторыКОчисткеДвижений КАК РегистраторыИАналитики";
	ТекстыВложенногоЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	СтрокаВложенногоЗапроса = СтрСоединить(ТекстыВложенногоЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Период КАК Период,
	|	ВложенныйЗапрос.ВидДвижения КАК ВидДвижения,
	|	ВложенныйЗапрос.Регистратор КАК Регистратор,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.Подразделение КАК Подразделение,
	|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВложенныйЗапрос.НаправлениеДеятельностиПриемник КАК НаправлениеДеятельностиПриемник,
	|	ВложенныйЗапрос.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ВложенныйЗапрос.СтатьяРасходов КАК СтатьяРасходов,
	|	ВложенныйЗапрос.АналитикаРасходов КАК АналитикаРасходов,
	|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
	|	СУММА(ВложенныйЗапрос.СуммаБезНДС) КАК СуммаБезНДС,
	|	СУММА(ВложенныйЗапрос.СуммаУпр) КАК СуммаУпр,
	|	СУММА(ВложенныйЗапрос.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ВложенныйЗапрос.ПостояннаяРазница) КАК ПостояннаяРазница,
	|	СУММА(ВложенныйЗапрос.ВременнаяРазница) КАК ВременнаяРазница,
	|	СУММА(ВложенныйЗапрос.Расходы) КАК Расходы,
	|	СУММА(ВложенныйЗапрос.БазаРаспределения) КАК БазаРаспределения
	|ПОМЕСТИТЬ РезультатРаспределения
	|ИЗ
	|	РезультатРаспределенияПредварительная КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.ВидДвижения,
	|	ВложенныйЗапрос.Подразделение,
	|	ВложенныйЗапрос.НаправлениеДеятельности,
	|	ВложенныйЗапрос.НаправлениеДеятельностиПриемник,
	|	ВложенныйЗапрос.ХозяйственнаяОперация,
	|	ВложенныйЗапрос.СтатьяРасходов,
	|	ВложенныйЗапрос.АналитикаРасходов";
	
	Если РежимОтладкиПоИсточникам Тогда
		
		ТекстыЗапроса = Новый Массив;
		ТекстыЗапроса.Добавить(СтрокаВложенногоЗапроса);
		ТекстыЗапроса.Добавить(ТекстЗапроса);
		ТекстЗапроса = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
		
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РезультатРаспределенияПредварительная", "(" + СтрокаВложенногоЗапроса + ")");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстВыборкаРезультатРаспределения()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РезультатРаспределения.Период КАК Период,
	|	РезультатРаспределения.ВидДвижения КАК ВидДвижения,
	|	РезультатРаспределения.Регистратор КАК Регистратор,
	|	РезультатРаспределения.Организация КАК Организация,
	|	РезультатРаспределения.Подразделение КАК Подразделение,
	|	РезультатРаспределения.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	РезультатРаспределения.НаправлениеДеятельностиПриемник КАК НаправлениеДеятельностиПриемник,
	|	РезультатРаспределения.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	РезультатРаспределения.СтатьяРасходов КАК СтатьяРасходов,
	|	РезультатРаспределения.АналитикаРасходов КАК АналитикаРасходов,
	|	(РезультатРаспределения.Сумма) КАК Сумма,
	|	(РезультатРаспределения.СуммаБезНДС) КАК СуммаБезНДС,
	|	(РезультатРаспределения.СуммаУпр) КАК СуммаУпр,
	|	(РезультатРаспределения.СуммаРегл) КАК СуммаРегл,
	|	(РезультатРаспределения.ПостояннаяРазница) КАК ПостояннаяРазница,
	|	(РезультатРаспределения.ВременнаяРазница) КАК ВременнаяРазница,
	|	(РезультатРаспределения.Расходы) КАК Расходы,
	|	(РезультатРаспределения.БазаРаспределения) КАК БазаРаспределения
	|ИЗ
	|	РезультатРаспределения КАК РезультатРаспределения
	|
	|ИТОГИ ПО
	|	Организация,
	|	Регистратор";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ОбновлениеИнформационнойБазы



#КонецОбласти

#КонецОбласти

#КонецЕсли
