
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ДоступныеТипыДокументаОснования   = Метаданные.ОпределяемыеТипы.ОснованиеОформлениеСДИЗЗЕРНО.Тип;
	ДоступныеТипыПрикладныхДокументов = Новый Массив;
	
	Для Каждого ТипДокумента Из ДоступныеТипыДокументаОснования.Типы() Цикл
		
		МетаданныеДокумента = Метаданные.НайтиПоТипу(ТипДокумента);
		
		Если МетаданныеДокумента = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не Метаданные.Подсистемы.ГосИС.Подсистемы.ЗЕРНО.Состав.Содержит(МетаданныеДокумента) Тогда
			ДоступныеТипыПрикладныхДокументов.Добавить(ТипДокумента);
		КонецЕсли;
		
	КонецЦикла;
	
	ДоступныеТипыДокументаОснования = Новый ОписаниеТипов(ДоступныеТипыПрикладныхДокументов);
	
	Элементы.ДокументОснование.ОграничениеТипа = ДоступныеТипыДокументаОснования;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Элементы.ДокументОснование.ОграничениеТипа.Типы().Количество() = 0 Тогда
		
		ВызватьИсключение 
			НСтр("ru = 'Невозможно создать Корректировку оформления СДИЗ из списка, поскольку отсутствуют доступные прикладные документы для основания.
				|
				|Создайте Корректировку оформления на основании нужного Оформления СДИЗ или обратитесь к Администратору.
				|Возможны ошибки встраивания.'");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		Закрыть(ДокументОснование);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти