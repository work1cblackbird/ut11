
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ПолноеПогашение",                          ПолноеПогашение);
	Параметры.Свойство("ДляПроизводстваНеПрослеживаемойПродукции", ДляПроизводстваНеПрослеживаемойПродукции);
	Параметры.Свойство("ПричинаРасхождения",                       ПричинаРасхождения);
	Параметры.Свойство("ОписаниеПричиныРасхождения",               Примечание);
	Параметры.Свойство("ПросмотрПогашения",                        ТолькоПросмотр);
	Параметры.Свойство("ДоступностьПричины",                       ДоступностьПричины);
	
	Если ТолькоПросмотр Тогда
		Элементы.ФормаСохранить.Заголовок = НСтр("ru = 'Закрыть'");
		Элементы.ГруппаПогашение.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	ДоступностьЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Не ДоступностьПричины Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ПричинаРасхождения");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолноеПогашениеПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	ДоступностьЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДляПроизводстваНеПрослеживаемойПродукцииПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПричинаРасхожденияПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПримечаниеПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если ТолькоПросмотр Тогда
		Закрыть();
		Возврат;
	КонецЕсли;
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ПолноеПогашение",                          ПолноеПогашение);
	Результат.Вставить("ДляПроизводстваНеПрослеживаемойПродукции", ДляПроизводстваНеПрослеживаемойПродукции);
	Результат.Вставить("ПричинаРасхождения",                       ПричинаРасхождения);
	Результат.Вставить("ОписаниеПричиныРасхождения",               Примечание);
	Результат.Вставить("Модифицированность",                       Модифицированность);
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ДоступностьЭлементовФормы(Форма)
	
	ДоступностьПричины = Форма.ДоступностьПричины Или ЗначениеЗаполнено(Форма.ПричинаРасхождения);
	
	Форма.Элементы.ПричинаРасхождения.Доступность = ДоступностьПричины;
	Форма.Элементы.Примечание.Доступность         = ДоступностьПричины;
	
КонецПроцедуры

#КонецОбласти