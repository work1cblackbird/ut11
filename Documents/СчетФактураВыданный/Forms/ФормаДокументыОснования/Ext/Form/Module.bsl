
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	СписокОснований = Параметры.СписокПодобранныхОснований;
	Организация     = Параметры.Организация;
	Контрагент      = Параметры.Контрагент;
	Валюта          = Параметры.Валюта;
	СчетФактура     = Параметры.СчетФактура;
	СчетФактураИсправление = Параметры.СчетФактураИсправление;
	РеализацияЧерезКомиссионера = Параметры.РеализацияЧерезКомиссионера;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокОснованийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = СписокОснований.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ПоказатьЗначение(, ТекущиеДанные.Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОснованийПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Закрыть(СписокОснований);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСостав(Команда)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("СписокПодобранныхОснований", СписокОснований);
	СтруктураПараметров.Вставить("СчетФактура",     СчетФактура);
	СтруктураПараметров.Вставить("Организация",     Организация);
	СтруктураПараметров.Вставить("Контрагент",      Контрагент);
	СтруктураПараметров.Вставить("Валюта",          Валюта);
	СтруктураПараметров.Вставить("СчетФактураИсправление", СчетФактураИсправление);
	СтруктураПараметров.Вставить("РеализацияЧерезКомиссионера", РеализацияЧерезКомиссионера);
	
	Оповещение = Новый ОписаниеОповещения("ИзменитьСоставЗавершение", ЭтаФорма);
	
	ОткрытьФорму("ОбщаяФорма.ПодборОснованийСчетаФактуры",
		СтруктураПараметров,
		ЭтаФорма, , , ,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИзменитьСоставЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СписокОснований = Результат;
	
КонецПроцедуры

#КонецОбласти
