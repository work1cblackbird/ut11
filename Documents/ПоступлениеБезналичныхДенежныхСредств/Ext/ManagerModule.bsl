#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует описание реквизитов объекта, заполняемых по статистике их использования.
//
// Параметры:
//  ОписаниеРеквизитов - Структура - описание реквизитов, для которых необходимо получить значения по статистике
//
//
Процедура ЗадатьОписаниеЗаполняемыхРеквизитовПоСтатистике(ОписаниеРеквизитов) Экспорт
	
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьВсегда = "Автор";
	Параметры.ЗаполнятьПриУсловии.ПоляОбъектаЗаполнены = "Автор";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов,
		"Подразделение", Параметры);
	
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьВсегда = "Контрагент";
	Параметры.РазрезыСбораСтатистики.ИспользоватьТолькоЗаполненные = "Валюта";
	Параметры.ЗаполнятьПриУсловии.ПоляОбъектаЗаполнены = "Контрагент";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов,
		"БанковскийСчетКонтрагента", Параметры);
		
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьТолькоЗаполненные = "Подразделение, Контрагент";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(
		ОписаниеРеквизитов, "Ответственный", Параметры);
	
КонецПроцедуры

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("Взаиморасчеты");
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	МеханизмыДокумента.Добавить("ПланированиеКонтрольДенежныхСредств");
	МеханизмыДокумента.Добавить("РасчетыСПодотчетниками");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("СуммыДокументовВВалютахУчета");
	МеханизмыДокумента.Добавить("УчетДенежныхСредств");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("ФинансовыеИнструменты");
	МеханизмыДокумента.Добавить("ИсправлениеДокументов");
	
	ПоступлениеБезналичныхДенежныхСредствЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов - таблиц значений - данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.ПоступлениеБезналичныхДенежныхСредств") Тогда
		ДокументОбъект = Документ;
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументОбъект = Документ.ПолучитьОбъект();
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	ПолучитьДанныеДокумента(Запрос, ДокументСсылка);
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
		
		ТекстЗапросаТаблицаДенежныеСредстваБезналичные(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
		ТекстЗапросаТаблицаДенежныеСредстваВПути(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
		
		ТекстЗапросаТаблицаДенежныеСредстваУПодотчетныхЛиц(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
		ТекстЗапросаТаблицаРасчетыПоФинансовымИнструментам(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
		ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
		ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
		ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
		
		ТекстЗапросаТаблицаДенежныеСредстваКВыплате(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
		ТекстЗапросаТаблицаЛимитыРасходаДенежныхСредств(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
		
		ТекстЗапросаТаблицаДвиженияДенежныхСредств(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
		ТекстЗапросаТаблицаДвиженияДенежныеСредстваКонтрагент(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
		ТекстЗапросаТаблицаДвиженияДенежныеСредстваДоходыРасходы(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
		ТекстЗапросаТаблицаДвиженияКонтрагентКонтрагент(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
		ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
		
		ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
		
		// Выполение запроса и выгрузка полученных таблиц для формирования движений
		ПоступлениеБезналичныхДенежныхСредствЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	ДобавитьТекстыОтраженияВзаиморасчетов(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
	
	ПроведениеДокументов.ДобавитьЗапросыСторноДвижений(Запрос, ТекстыЗапроса, Регистры, ПустаяСсылка().Метаданные());
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// Возвращает структуру параметров для заполнения налогообложения НДС продажи.
//
// Параметры:
//  Объект - ДокументОбъект.ПоступлениеБезналичныхДенежныхСредств - документ, по которому необходимо сформировать параметры.
//
// Возвращаемое значение:
//  См. УчетНДСУПКлиентСервер.ПараметрыЗаполненияНалогообложенияНДСПродажи
//
Функция ПараметрыЗаполненияНалогообложенияНДСПродажи(Объект) Экспорт
	
	ПараметрыЗаполнения = УчетНДСУПКлиентСервер.ПараметрыЗаполненияНалогообложенияНДСПродажи();
	
	ПараметрыЗаполнения.Организация = Объект.Организация;
	ПараметрыЗаполнения.Дата = Объект.Дата;
	ПараметрыЗаполнения.Договор = Объект.Договор;
	ПараметрыЗаполнения.НаправлениеДеятельности = Объект.НаправлениеДеятельности;
	ПараметрыЗаполнения.Подразделение = Объект.Подразделение;
	
	ПараметрыЗаполнения.РеализацияТоваров = Истина;
	ПараметрыЗаполнения.РеализацияРаботУслуг = Истина;
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

// Возвращает структуру параметров для заполнения налогообложения НДС закупки.
//
// Параметры:
//  Объект - ДокументОбъект.ПоступлениеБезналичныхДенежныхСредств - документ, по которому необходимо сформировать параметры.
//
// Возвращаемое значение:
//  См. УчетНДСУПКлиентСервер.ПараметрыЗаполненияНалогообложенияНДСЗакупки
//
Функция ПараметрыЗаполненияНалогообложенияНДСЗакупки(Объект) Экспорт
	
	ПараметрыЗаполнения = УчетНДСУПКлиентСервер.ПараметрыЗаполненияНалогообложенияНДСЗакупки();
	
	ПараметрыЗаполнения.Контрагент = Объект.Контрагент;
	ПараметрыЗаполнения.Договор = Объект.Договор;
	ПараметрыЗаполнения.Период  = Объект.Дата;
	ПараметрыЗаполнения.ПриобретениеТоваров = Истина;
	ПараметрыЗаполнения.ПриобретениеРабот = Истина;
	ПараметрыЗаполнения.ПриобретениеНаСтатьи = Истина;
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетНДС") Тогда
		Документы.СчетФактураВыданныйАванс.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	КонецЕсли;

	СозданиеНаОснованииЛокализация.ДобавитьКомандуСоздатьНаОснованииПисьмоОбменСБанками(КомандыСозданияНаОсновании);
	
	ХозяйственныеОперацииДляИсправления = Новый Массив;
	ХозяйственныеОперацииДляИсправления.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента);
	ХозяйственныеОперацииДляИсправления.Добавить(Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика);
	ХозяйственныеОперацииДляИсправления.Добавить(Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств);
	ХозяйственныеОперацииДляИсправления.Добавить(Перечисления.ХозяйственныеОперации.КонвертацияВалюты);
	ХозяйственныеОперацииДляИсправления.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам);
	ХозяйственныеОперацииДляИсправления.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам);
	ХозяйственныеОперацииДляИсправления.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным);
	ХозяйственныеОперацииДляИсправления.Добавить(Перечисления.ХозяйственныеОперации.ПогашениеЗаймаСотрудником);
	ХозяйственныеОперацииДляИсправления.Добавить(Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника);
	ХозяйственныеОперацииДляИсправления.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствСДругогоСчета);
	ХозяйственныеОперацииДляИсправления.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет);
	ХозяйственныеОперацииДляИсправления.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации);
	ХозяйственныеОперацииДляИсправления.Добавить(Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации);
	ХозяйственныеОперацииДляИсправления.Добавить(Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств);
	ХозяйственныеОперацииДляИсправления.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте);
	
	КомандаИсправление = ИсправлениеДокументов.ДобавитьКомандуИсправление(КомандыСозданияНаОсновании, ПустаяСсылка().Метаданные());
	
	Если КомандаИсправление <> Неопределено Тогда
		
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(
			КомандаИсправление,
			"ХозяйственнаяОперация",
			ХозяйственныеОперацииДляИсправления,
			ВидСравненияКомпоновкиДанных.ВСписке);
		
	КонецЕсли;
	
	КомандаСторно = ИсправлениеДокументов.ДобавитьКомандуСторно(
		КомандыСозданияНаОсновании,
		ПустаяСсылка().Метаданные());
	
	Если КомандаСторно <> Неопределено Тогда
		
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(
			КомандаСторно,
			"ХозяйственнаяОперация",
			ХозяйственныеОперацииДляИсправления,
			ВидСравненияКомпоновкиДанных.ВСписке);
		
	КонецЕсли;
	
	КомандаСозданияНаОснованииЗаявка = Документы.ЗаявкаНаРасходованиеДенежныхСредств.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	ХозяйственныеОперацииДляВводаНаОсновании = Новый Массив;
	ХозяйственныеОперацииДляВводаНаОсновании.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента);
	ХозяйственныеОперацииДляВводаНаОсновании.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте);
	ХозяйственныеОперацииДляВводаНаОсновании.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации);
	
	Если КомандаСозданияНаОснованииЗаявка <> Неопределено Тогда
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаСозданияНаОснованииЗаявка,
															"ХозяйственнаяОперация",
															ХозяйственныеОперацииДляВводаНаОсновании,
															ВидСравненияКомпоновкиДанных.ВСписке);
	КонецЕсли;
	
	КомандаСоздатьНаОснованииСписаниеБДС = Документы.СписаниеБезналичныхДенежныхСредств.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	Если КомандаСоздатьНаОснованииСписаниеБДС <> Неопределено Тогда
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаСоздатьНаОснованииСписаниеБДС,
															"ХозяйственнаяОперация",
															ХозяйственныеОперацииДляВводаНаОсновании,
															ВидСравненияКомпоновкиДанных.ВСписке);
	КонецЕсли;
		
	ПоступлениеБезналичныхДенежныхСредствЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Поступление безналичных ДС".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//	ТаблицаЗначений, Неопределено - сформированные команды для вывода в подменю.
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.ПоступлениеБезналичныхДенежныхСредств) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ПоступлениеБезналичныхДенежныхСредств.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ПоступлениеБезналичныхДенежныхСредств);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ПоступлениеБезналичныхДенежныхСредствЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);
	
КонецПроцедуры

// Формирует запрос проверки при смене статуса списка документов
//
// Параметры:
//	МассивДокументов - Массив - Массив ссылок на документы, которые надо проверять
//	НовыйСтатус - Строка - Имя нового статуса
//	ДополнительныеПараметры - Структура - Структура дополнительных параметров.
//
// Возвращаемое значение:
//	Запрос - Запрос проверки перед сменой статуса.
//
Функция СформироватьЗапросПроверкиПриСменеСтатуса(МассивДокументов, НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокументов.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаДокументов.Ссылка) КАК Представление,
	|	ВЫБОР КОГДА ТаблицаДокументов.ПроведеноБанком ТОГДА
	|		&СтрокаПроведеноБанком
	|	ИНАЧЕ
	|		&СтрокаНеПроведеноБанком
	|	КОНЕЦ КАК ПредставлениеТекущегоСтатуса,
	|	&СтрокаПроведеноБанком КАК ПредставлениеНовогоСтатуса,
	|	ТаблицаДокументов.ПроведеноБанком КАК СтатусСовпадает,
	|	ТаблицаДокументов.Проведен КАК Проведен,
	|	ТаблицаДокументов.ПометкаУдаления КАК ПометкаУдаления,
	|	ИСТИНА КАК ЗаписьПроведением
	|ИЗ
	|	Документ.ПоступлениеБезналичныхДенежныхСредств КАК ТаблицаДокументов
	|ГДЕ
	|	ТаблицаДокументов.Ссылка В(&МассивДокументов)
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Запрос.УстановитьПараметр("СтрокаПроведеноБанком", НСтр("ru = 'Проведено банком'"));
	Запрос.УстановитьПараметр("СтрокаНеПроведеноБанком", НСтр("ru = 'Не проведено банком'"));
	
	Возврат Запрос;
	
КонецФункции

// Возвращает результат проверки при смене статуса документа
//
// Параметры:
//	ВыборкаПроверки - ВыборкаИзРезультатаЗапроса - Текущая строка выборки
//	НовыйСтатус - Строка - Новый статус
//	ДополнительныеПараметры - Структура - Структура дополнительных параметров.
//
// Возвращаемое значение:
//	Булево - Истина, в случае успешного завершения проверки.
//
Функция ПроверкаПередСменойСтатуса(ВыборкаПроверки, НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	Возврат Истина; // Дополнительные проверки отсутствуют
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Возвращает параметры механизма взаиморасчетов.
//
// Параметры:
// 	ДанныеЗаполнения - ДокументОбъект, СправочникОбъект, ДокументСсылка, СправочникСсылка, Структура, ДанныеФормыСтруктура - Объект или коллекция для
//              расчета параметров взаиморасчетов.
//
// Возвращаемое значение:
// 	Массив из См. ВзаиморасчетыСервер.ПараметрыМеханизма - Массив параметров функций механизма взаиморасчетов.
//
Функция ПараметрыВзаиморасчеты(ДанныеЗаполнения = Неопределено) Экспорт
	
	Если ОбщегоНазначения.ЗначениеСсылочногоТипа(ДанныеЗаполнения) Тогда
		СтруктураДанныеЗаполнения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ДанныеЗаполнения, 
			"ХозяйственнаяОперация,
			|ПроведеноБанком");
		ХозяйственнаяОперация = СтруктураДанныеЗаполнения.ХозяйственнаяОперация;
		ПроведеноБанком       = СтруктураДанныеЗаполнения.ПроведеноБанком;
	ИначеЕсли ДанныеЗаполнения = Неопределено Тогда
		ХозяйственнаяОперация = Неопределено;
		ПроведеноБанком       = Ложь;
	Иначе
		ХозяйственнаяОперация = ДанныеЗаполнения.ХозяйственнаяОперация;
		ПроведеноБанком       = ДанныеЗаполнения.ПроведеноБанком;
	КонецЕсли;
	
	МассивСтруктур = Новый Массив();
	
	#Область ВсеОстальныеФункции
	
	СтруктураПараметров = ВзаиморасчетыСервер.ПараметрыМеханизма();
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента
		ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации Тогда
		СтруктураПараметров.ТипРасчетов                      = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика
		ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации Тогда
		СтруктураПараметров.ТипРасчетов                      = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
	КонецЕсли;
	
	СтруктураПараметров.ИзменяетПланОплаты                  = ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента
									ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации
									ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика
									ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации;
	
	СтруктураПараметров.ИзменяетРасчетыСтрокой           =
	"ИсточникДанных.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента)
	|	ИЛИ ИсточникДанных.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации)
	|	ИЛИ
	|//Условие возврата начало
	|	ИсточникДанных.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика)
	|	ИЛИ ИсточникДанных.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации)
	|//Условие возврата окончание
	|";
	
	СтруктураПараметров.ИзменяетПланОтгрузкиПоставки = Ложь;
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации
		ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации Тогда
		СтруктураПараметров.Контрагент                             = "Объект.БанковскийСчетОтправитель.Владелец";
		СтруктураПараметров.Партнер                                = Справочники.Партнеры.НашеПредприятие;
		
	Иначе
		СтруктураПараметров.Контрагент                             = "Объект.Контрагент";
		СтруктураПараметров.Партнер                                = "Объект.РасшифровкаПлатежа.Партнер";
	КонецЕсли;
	
	СтруктураПараметров.ВалютаВзаиморасчетов             = "Объект.РасшифровкаПлатежа.ВалютаВзаиморасчетов";
	СтруктураПараметров.КурсЧислитель                    = "";
	СтруктураПараметров.КурсЗнаменатель                  = "";
	СтруктураПараметров.ПорядокРасчетов                  = "";
	
	СтруктураПараметров.ФормаОплаты                      = "";
	СтруктураПараметров.ОплатаВВалюте                    = "";
	 
	СтруктураПараметров.ПутьКДаннымТЧРасшифровкаПлатежа  = "Объект.РасшифровкаПлатежа";
	СтруктураПараметров.Организация                      = "Объект.Организация";
	СтруктураПараметров.Соглашение                       = "";
	СтруктураПараметров.Договор                          = "Объект.Договор";
	СтруктураПараметров.БанковскийСчетОрганизации        = "Объект.БанковскийСчет";
	СтруктураПараметров.Касса                            = "";
	СтруктураПараметров.Дата                             = "Объект.Дата";
	СтруктураПараметров.ИдентификаторПлатежа             = "";
	СтруктураПараметров.Менеджер                         = "";
	СтруктураПараметров.НомерВходящегоДокумента          = "Объект.НомерВходящегоДокумента";
	СтруктураПараметров.ДатаВходящегоДокумента           = "Объект.ДатаВходящегоДокумента";
	СтруктураПараметров.НалогообложениеНДС               = "Объект.НалогообложениеНДС";
	
	//Имена элементов форм для текущего набора параметров
	СтруктураПараметров.ЭлементыФормы.РасшифровкаПлатежа               = "РасшифровкаПлатежа";
	СтруктураПараметров.ЭлементыФормы.ПодборВРасшифровкуПлатежа        = "РасшифровкаПлатежаПодборПоОстаткам";
	
	СтруктураПараметров.ПодборДебиторскойЗадолженности    = Истина;
	СтруктураПараметров.ПодборТолькоБезусловнойЗадолженности = ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика
									ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации;
	
	МассивСтруктур.Добавить(СтруктураПараметров);
	
	#КонецОбласти
	
	#Область ОбъектРасчетовВозврат
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика
		ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации Тогда
		
		СтруктураПараметров = ВзаиморасчетыСервер.ПараметрыМеханизма();
		
		СтруктураПараметров.ТипРасчетов                      = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
		СтруктураПараметров.ОбъектРасчетов                   = "Объект.ОбъектРасчетов";
		СтруктураПараметров.ИзменяетПланОплаты               = Истина;
		СтруктураПараметров.ИзменяетПланОтгрузкиПоставки     = Ложь;
		
		Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации Тогда
			СтруктураПараметров.Контрагент                             = "Объект.БанковскийСчетОтправитель.Владелец";
			СтруктураПараметров.Партнер                                = Справочники.Партнеры.НашеПредприятие;
		Иначе
			СтруктураПараметров.Контрагент                             = "Объект.Контрагент";
			СтруктураПараметров.Партнер                                = "Объект.РасшифровкаПлатежа.Партнер";
		КонецЕсли;
		
		СтруктураПараметров.ПутьКДаннымТЧРасшифровкаПлатежа  = "Объект.РасшифровкаПлатежа";
		СтруктураПараметров.ВалютаВзаиморасчетов             = "Объект.РасшифровкаПлатежа.ВалютаВзаиморасчетов";
		СтруктураПараметров.КурсЧислитель                             = "";
		СтруктураПараметров.КурсЗнаменатель                        = "";
		СтруктураПараметров.ПорядокРасчетов                  = "";
		СтруктураПараметров.ФормаОплаты                      = "";
		СтруктураПараметров.ОплатаВВалюте                    = "";
		СтруктураПараметров.Соглашение                       = "";
		СтруктураПараметров.БанковскийСчетОрганизации        = "Объект.БанковскийСчет";
		СтруктураПараметров.Касса                            = "";
		
		Если ПроведеноБанком Тогда
			СтруктураПараметров.Дата                             = "Объект.ДатаПроведенияБанком";
		Иначе
			СтруктураПараметров.Дата                             = "Объект.Дата";
		КонецЕсли;
		
		СтруктураПараметров.ИдентификаторПлатежа             = "";
		СтруктураПараметров.Менеджер                         = "";
		СтруктураПараметров.НомерВходящегоДокумента          = "Объект.НомерВходящегоДокумента";
		СтруктураПараметров.ДатаВходящегоДокумента           = "Объект.ДатаВходящегоДокумента";
		СтруктураПараметров.НалогообложениеНДС               = "Объект.НалогообложениеНДС";
		
		МассивСтруктур.Добавить(СтруктураПараметров);
		
	КонецЕсли;
	
	#КонецОбласти
	
	Возврат МассивСтруктур;
КонецФункции

#Область Прочее

// Процедура заполняет массивы реквизитов, зависимых от хозяйственной операции документа.
//
// Параметры:
//	Объект - ДокументОбъект.ПоступлениеБезналичныхДенежныхСредств, ДанныеФормыСтруктура - Объект документа.
//	МассивВсехРеквизитов - Массив из Строка - Массив всех имен реквизитов, зависимых от хозяйственной операции
//	МассивРеквизитовОперации - Массив из Строка - Массив имен реквизитов, используемых в выбранной хозяйственной операции.
//
Процедура ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(Объект, МассивВсехРеквизитов, МассивРеквизитовОперации) Экспорт
	
	ХозяйственнаяОперация = Объект.ХозяйственнаяОперация;
	
	Если МассивВсехРеквизитов = Неопределено Тогда
		МассивВсехРеквизитов = Новый Массив;
	КонецЕсли;
	Если МассивРеквизитовОперации = Неопределено Тогда
		МассивРеквизитовОперации = Новый Массив;
	КонецЕсли;
	
	МассивВсехРеквизитов.Добавить("БанковскийСчетКонтрагента");
	МассивВсехРеквизитов.Добавить("БанковскийСчетОтправитель");
	МассивВсехРеквизитов.Добавить("ВалютаКонвертации");
	МассивВсехРеквизитов.Добавить("КассаОтправитель");
	МассивВсехРеквизитов.Добавить("Контрагент");
	МассивВсехРеквизитов.Добавить("КурсКонвертации");
	МассивВсехРеквизитов.Добавить("КратностьКурсаКонвертации");
	МассивВсехРеквизитов.Добавить("СуммаКонвертации");
	МассивВсехРеквизитов.Добавить("ПодотчетноеЛицо");
	МассивВсехРеквизитов.Добавить("Подразделение");
	МассивВсехРеквизитов.Добавить("СтатьяДвиженияДенежныхСредств");
	МассивВсехРеквизитов.Добавить("ДоговорЭквайринга");
	МассивВсехРеквизитов.Добавить("СтатьяРасходов");
	МассивВсехРеквизитов.Добавить("АналитикаРасходов");
	
	МассивВсехРеквизитов.Добавить("ТипНалога");
	
	МассивВсехРеквизитов.Добавить("ГруппаФинансовогоУчета");
	МассивВсехРеквизитов.Добавить("НаправлениеДеятельности");
	МассивВсехРеквизитов.Добавить("Договор");
	МассивВсехРеквизитов.Добавить("НалогообложениеНДС");
	
	МассивВсехРеквизитов.Добавить("ПодтверждениеЗачисленияЗарплаты");
	МассивВсехРеквизитов.Добавить("ДокументВыдачи");
	
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.ВалютаВзаиморасчетов");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.ДоговорКредитаДепозита");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.НаправлениеДеятельности");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.ОбъектРасчетов");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.ОснованиеПлатежа");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.Партнер");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.Подразделение");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.СуммаВзаиморасчетов");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.ТипСуммыКредитаДепозита");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.СтавкаНДС");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.СуммаНДС");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.Организация");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.ДоговорАренды");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.ТипПлатежаПоАренде");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.РасчетныйДокументПоАренде");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.ДатаПогашения");
	
	МассивРеквизитовОперации = Новый Массив;
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента Тогда
		МассивРеквизитовОперации.Добавить("Контрагент");
		МассивРеквизитовОперации.Добавить("БанковскийСчетКонтрагента");
		МассивРеквизитовОперации.Добавить("Подразделение");
		МассивРеквизитовОперации.Добавить("ГруппаФинансовогоУчета");
		МассивРеквизитовОперации.Добавить("НаправлениеДеятельности");
		МассивРеквизитовОперации.Добавить("Договор");
		МассивРеквизитовОперации.Добавить("НалогообложениеНДС");
		
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.Партнер");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ОбъектРасчетов");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ОснованиеПлатежа");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СуммаВзаиморасчетов");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтавкаНДС");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СуммаНДС");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ВалютаВзаиморасчетов");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.Организация");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ДатаПогашения");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика Тогда
		МассивРеквизитовОперации.Добавить("Контрагент");
		МассивРеквизитовОперации.Добавить("БанковскийСчетКонтрагента");
		МассивРеквизитовОперации.Добавить("Подразделение");
		МассивРеквизитовОперации.Добавить("ГруппаФинансовогоУчета");
		МассивРеквизитовОперации.Добавить("НаправлениеДеятельности");
		МассивРеквизитовОперации.Добавить("Договор");
		МассивРеквизитовОперации.Добавить("НалогообложениеНДС");
		
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.Партнер");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ОбъектРасчетов");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ОснованиеПлатежа");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СуммаВзаиморасчетов");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтавкаНДС");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СуммаНДС");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ВалютаВзаиморасчетов");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.Организация");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств Тогда
		МассивРеквизитовОперации.Добавить("Контрагент");
		МассивРеквизитовОперации.Добавить("БанковскийСчетКонтрагента");
		
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.Подразделение");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.НаправлениеДеятельности");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты Тогда
		МассивРеквизитовОперации.Добавить("Подразделение");
		МассивРеквизитовОперации.Добавить("Контрагент");
		МассивРеквизитовОперации.Добавить("СтатьяДвиженияДенежныхСредств");
		МассивРеквизитовОперации.Добавить("ВалютаКонвертации");
		МассивРеквизитовОперации.Добавить("КурсКонвертации");
		МассивРеквизитовОперации.Добавить("КратностьКурсаКонвертации");
		МассивРеквизитовОперации.Добавить("СуммаКонвертации");
		МассивРеквизитовОперации.Добавить("БанковскийСчетКонтрагента");
		МассивРеквизитовОперации.Добавить("ДокументОснование");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам
			ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам
			ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным Тогда
		
		МассивРеквизитовОперации.Добавить("Контрагент");
		МассивРеквизитовОперации.Добавить("БанковскийСчетКонтрагента");
		
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.Партнер");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ДоговорКредитаДепозита");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СуммаВзаиморасчетов");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ВалютаВзаиморасчетов");
		Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам
			ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным Тогда
			МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ТипСуммыКредитаДепозита");
		КонецЕсли;
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПогашениеЗаймаСотрудником Тогда
		
		МассивРеквизитовОперации.Добавить("ПодотчетноеЛицо");
		МассивРеквизитовОперации.Добавить("БанковскийСчетКонтрагента");
		МассивРеквизитовОперации.Добавить("Подразделение");
		
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ТипСуммыКредитаДепозита");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника Тогда
		
		МассивРеквизитовОперации.Добавить("ПодотчетноеЛицо");
		МассивРеквизитовОперации.Добавить("Подразделение");
		МассивРеквизитовОперации.Добавить("БанковскийСчетКонтрагента");
		
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленныхДС Тогда
		
		МассивРеквизитовОперации.Добавить("ДокументВыдачи");
		МассивРеквизитовОперации.Добавить("ПодотчетноеЛицо");
		МассивРеквизитовОперации.Добавить("БанковскийСчетКонтрагента");
		МассивРеквизитовОперации.Добавить("СтатьяДвиженияДенежныхСредств");
		МассивРеквизитовОперации.Добавить("Подразделение");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленнойЗарплатыПоЗарплатномуПроекту Тогда
		
		МассивРеквизитовОперации.Добавить("ДокументВыдачи");
		МассивРеквизитовОперации.Добавить("ПодтверждениеЗачисленияЗарплаты");
		МассивРеквизитовОперации.Добавить("Контрагент");
		МассивРеквизитовОперации.Добавить("БанковскийСчетКонтрагента");
		МассивРеквизитовОперации.Добавить("СтатьяДвиженияДенежныхСредств");
		МассивРеквизитовОперации.Добавить("Подразделение");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствСДругогоСчета Тогда
		МассивРеквизитовОперации.Добавить("БанковскийСчетОтправитель");
		МассивРеквизитовОперации.Добавить("ДокументОснование");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет Тогда
		МассивРеквизитовОперации.Добавить("КассаОтправитель");
		МассивРеквизитовОперации.Добавить("ДокументОснование");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации Тогда
		МассивРеквизитовОперации.Добавить("БанковскийСчетОтправитель");
		МассивРеквизитовОперации.Добавить("Подразделение");
		МассивРеквизитовОперации.Добавить("ГруппаФинансовогоУчета");
		МассивРеквизитовОперации.Добавить("НаправлениеДеятельности");
		МассивРеквизитовОперации.Добавить("Договор");
		МассивРеквизитовОперации.Добавить("НалогообложениеНДС");
		
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ОбъектРасчетов");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ОснованиеПлатежа");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СуммаВзаиморасчетов");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтавкаНДС");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СуммаНДС");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ВалютаВзаиморасчетов");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.Комментарий");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.Организация");
		Если ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации Тогда
			МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ДатаПогашения");
		КонецЕсли;
		МассивРеквизитовОперации.Добавить("ДокументОснование");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств Тогда
		МассивРеквизитовОперации.Добавить("БанковскийСчетОтправитель");
		МассивРеквизитовОперации.Добавить("СтатьяДвиженияДенежныхСредств");
		МассивРеквизитовОперации.Добавить("ДокументОснование");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте Тогда
		МассивРеквизитовОперации.Добавить("Контрагент");
		МассивРеквизитовОперации.Добавить("ДоговорЭквайринга");
		МассивРеквизитовОперации.Добавить("Подразделение");
		МассивРеквизитовОперации.Добавить("НаправлениеДеятельности");
		МассивРеквизитовОперации.Добавить("ОтражатьКомиссию");
		МассивРеквизитовОперации.Добавить("БанковскийСчетКонтрагента");
		МассивРеквизитовОперации.Добавить("СтатьяДвиженияДенежныхСредств");
		МассивРеквизитовОперации.Добавить("СтатьяРасходов");
		МассивРеквизитовОперации.Добавить("АналитикаРасходов");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтАрендодателя Тогда
		МассивРеквизитовОперации.Добавить("Контрагент");
		МассивРеквизитовОперации.Добавить("БанковскийСчетКонтрагента");
		
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.Партнер");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ДоговорАренды");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СуммаВзаиморасчетов");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ВалютаВзаиморасчетов");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.ТипПлатежаПоАренде");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.РасчетныйДокументПоАренде");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНалогов Тогда
		МассивРеквизитовОперации.Добавить("БанковскийСчетКонтрагента");
		МассивРеквизитовОперации.Добавить("Контрагент");
		МассивРеквизитовОперации.Добавить("ТипНалога");
		МассивРеквизитовОперации.Добавить("Подразделение");
		
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.Комментарий");
	КонецЕсли;
	
КонецПроцедуры

// Определяет состав хоз. операций документа, для которых не используется расшифровка платежа.
//
// Возвращаемое значение:
//    Массив - хозяйственные операции без использования расшифровки платежа.
//
Функция МассивОперацийБезРасшифровкиПлатежа() Экспорт
	
	МассивОпераций = Новый Массив;
	
	МассивОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте);
	МассивОпераций.Добавить(Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств);
	МассивОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствСДругогоСчета);
	МассивОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет);
	МассивОпераций.Добавить(Перечисления.ХозяйственныеОперации.КонвертацияВалюты);
	МассивОпераций.Добавить(Перечисления.ХозяйственныеОперации.ВозвратНеперечисленныхДС);
	
	Возврат МассивОпераций;
	
КонецФункции

// Определяет свойства полей формы в зависимости от данных
//
// Параметры:
//	Настройки - ТаблицаЗначений - таблица с колонками:
//		* Поля - Массив из Строка- поля, для которых определяются настройки отображения
//		* Условие - ОтборКомпоновкиДанных - условия применения настройки
//		* Свойства - Структура - имена и значения свойств
//
Процедура ЗаполнитьНастройкиПолейФормы(Настройки) Экспорт
	
	Финансы = ФинансоваяОтчетностьСервер;
	
	Операции = Перечисления.ХозяйственныеОперации;
	
	ОтборОперацийСИденификаторомПлатежа = Новый ОтборКомпоновкиДанных();
	ОтборОпераций = Финансы.НовыйОтбор(ОтборОперацийСИденификаторомПлатежа, , , Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОтборОпераций.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ОтборОпераций, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыОтКлиента);
	Финансы.НовыйОтбор(ОтборОпераций, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствИзДругойОрганизации);
	Финансы.НовыйОтбор(ОтборОпераций, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоКредитам);
	Финансы.НовыйОтбор(ОтборОпераций, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоДепозитам);
	Финансы.НовыйОтбор(ОтборОпераций, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоЗаймамВыданным);
	Финансы.НовыйОтбор(ОтборОпераций, "ХозяйственнаяОперация", Операции.ПогашениеЗаймаСотрудником);
	Финансы.НовыйОтбор(ОтборОпераций, "ХозяйственнаяОперация", Операции.ПрочееПоступлениеДенежныхСредств);
	Финансы.НовыйОтбор(ОтборОпераций, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПоставщика);
	Финансы.НовыйОтбор(ОтборОпераций, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПодотчетника);
	Финансы.НовыйОтбор(ОтборОпераций, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтДругойОрганизации);
	Финансы.НовыйОтбор(ОтборОпераций, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтАрендодателя);
	Финансы.НовыйОтбор(ОтборОпераций, "ХозяйственнаяОперация", Операции.ВозвратНалогов);
	Финансы.НовыйОтбор(ОтборОпераций, "ХозяйственнаяОперация", Операции.КонвертацияВалюты);
	
	#Область Шапка
	// ПроведеноБанком
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ПроведеноБанком");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ТипПлатежногоДокумента", Перечисления.ТипыПлатежныхДокументов.ИнкассовоеПоручение);
	Финансы.НовыйОтбор(ГруппаИли, "ТипПлатежногоДокумента", Перечисления.ТипыПлатежныхДокументов.ПлатежныйОрдер);
	Элемент.Свойства.Вставить("ТолькоПросмотр");
	
	// ДатаПроведенияБанком
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ДатаПроведенияБанком");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ПроведеноБанком", Ложь);
	Финансы.НовыйОтбор(ГруппаИли, "ТипПлатежногоДокумента", Перечисления.ТипыПлатежныхДокументов.ПлатежныйОрдер);
	Элемент.Свойства.Вставить("ТолькоПросмотрБезОтметкиНезаполненного");
	
	// ДатаВходящегоДокумента, НомерВходящегоДокумента
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ДатаВходящегоДокумента");
	Элемент.Поля.Добавить("НомерВходящегоДокумента");
	Финансы.НовыйОтбор(Элемент.Условие, "ТипПлатежногоДокумента", Перечисления.ТипыПлатежныхДокументов.ПлатежноеТребование);
	Элемент.Свойства.Вставить("АвтоОтметкаНезаполненного", Ложь);
	Элемент.Свойства.Вставить("ОтметкаНезаполненного", Ложь);
	
	// Контрагент
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("Контрагент");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыОтКлиента);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыПоПлатежнойКарте);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПоставщика);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПрочееПоступлениеДенежныхСредств);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.КонвертацияВалюты);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоКредитам);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоДепозитам);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоЗаймамВыданным);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратНеперечисленнойЗарплатыПоЗарплатномуПроекту);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтАрендодателя);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратНалогов);
	Элемент.Свойства.Вставить("Видимость");
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("Контрагент");
	ГруппаНе = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаНе.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаНе;
	Финансы.НовыйОтбор(ГруппаНе, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыПоПлатежнойКарте);
	Элемент.Свойства.Вставить("Заголовок", НСтр("ru = 'Плательщик'"));
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("Контрагент");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыПоПлатежнойКарте);
	Элемент.Свойства.Вставить("Заголовок", НСтр("ru = 'Эквайер'"));
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("Контрагент");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Операции.КонвертацияВалюты);
	Элемент.Свойства.Вставить("Заголовок", НСтр("ru = 'Брокер'"));
	
	// Договор подключения к платежной системе
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ДоговорЭквайринга");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыПоПлатежнойКарте);
	Элемент.Свойства.Вставить("Видимость");
	
	// БанковскийСчетКонтрагента
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("БанковскийСчетКонтрагента");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыОтКлиента);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыПоПлатежнойКарте);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПоставщика);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПрочееПоступлениеДенежныхСредств);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.КонвертацияВалюты);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоКредитам);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоДепозитам);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоЗаймамВыданным);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПогашениеЗаймаСотрудником);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПодотчетника);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратНеперечисленныхДС);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратНеперечисленнойЗарплатыПоЗарплатномуПроекту);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтАрендодателя);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратНалогов);
	Элемент.Свойства.Вставить("Видимость");
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("БанковскийСчетКонтрагента");
	ГруппаНе = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаНе.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаНе;
	Финансы.НовыйОтбор(ГруппаНе, "ХозяйственнаяОперация", Операции.ПогашениеЗаймаСотрудником);
	Элемент.Свойства.Вставить("Заголовок", НСтр("ru = 'Счет плательщика'"));
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("БанковскийСчетКонтрагента");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПогашениеЗаймаСотрудником);
	Элемент.Свойства.Вставить("Заголовок", НСтр("ru = 'Лицевой счет'"));
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("БанковскийСчетКонтрагента");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Операции.КонвертацияВалюты);
	Элемент.Свойства.Вставить("Заголовок", НСтр("ru = 'Счет брокера'"));
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("БанковскийСчетКонтрагента");
	
	ОперацииСФизЛицами = Новый Массив;
	ОперацииСФизЛицами.Добавить(Операции.ПогашениеЗаймаСотрудником);
	ОперацииСФизЛицами.Добавить(Операции.ВозвратДенежныхСредствОтПодотчетника);
	ОперацииСФизЛицами.Добавить(Операции.ВозвратНеперечисленныхДС);
	
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие, , , Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	КонтрагентНеЗаполнен = Финансы.НовыйОтбор(ГруппаИли, , , Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	КонтрагентНеЗаполнен.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	Финансы.НовыйОтбор(КонтрагентНеЗаполнен, "Контрагент", , , ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ОстальныеОперации = Финансы.НовыйОтбор(КонтрагентНеЗаполнен, , , Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОстальныеОперации.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаНе;
	
	Финансы.НовыйОтбор(ОстальныеОперации, "ХозяйственнаяОперация", ОперацииСФизЛицами, , ВидСравненияКомпоновкиДанных.ВСписке);
	
	ФизЛицоНеЗаполнено = Финансы.НовыйОтбор(ГруппаИли, , , Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ФизЛицоНеЗаполнено.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	Финансы.НовыйОтбор(ФизЛицоНеЗаполнено, "ПодотчетноеЛицо", , , ВидСравненияКомпоновкиДанных.НеЗаполнено);
	Финансы.НовыйОтбор(ФизЛицоНеЗаполнено, "ХозяйственнаяОперация", ОперацииСФизЛицами, , ВидСравненияКомпоновкиДанных.ВСписке);
	
	Элемент.Свойства.Вставить("ТолькоПросмотр", Истина);
	
	// Подразделение
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("Подразделение");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПогашениеЗаймаСотрудником);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПодотчетника);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.КонвертацияВалюты);
	ГруппаИ = Финансы.НовыйОтбор(ГруппаИли,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	ГруппаИли1 = Финансы.НовыйОтбор(ГруппаИ,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли1.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли1, "ХозяйственнаяОперация", Операции.ВозвратНеперечисленнойЗарплатыПоЗарплатномуПроекту);
	Финансы.НовыйОтбор(ГруппаИли1, "ХозяйственнаяОперация", Операции.ВозвратНеперечисленныхДС);
	Финансы.НовыйОтбор(ГруппаИ, "Дополнительно.ВключеныПодразделенияНаСчетахУчетаЗарплаты", Истина);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.КонвертацияВалюты);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратНалогов);
	Элемент.Свойства.Вставить("Видимость");
	
	// БанковскийСчетОтправитель
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("БанковскийСчетОтправитель");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствСДругогоСчета);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствИзДругойОрганизации);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтДругойОрганизации);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВнутренняяПередачаДенежныхСредств);
	Элемент.Свойства.Вставить("Видимость");
	
	// КассаОтправитель
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("КассаОтправитель");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет);
	Элемент.Свойства.Вставить("Видимость");
	
	// ПодотчетноеЛицо
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ПодотчетноеЛицо");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПогашениеЗаймаСотрудником);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПодотчетника);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратНеперечисленныхДС);
	Элемент.Свойства.Вставить("Видимость");
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ПодотчетноеЛицо");
	ГруппаНе = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаНе.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаНе;
	Финансы.НовыйОтбор(ГруппаНе, "ХозяйственнаяОперация", Операции.ПогашениеЗаймаСотрудником);
	Элемент.Свойства.Вставить("Заголовок", НСтр("ru = 'Подотчетное лицо'"));
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ПодотчетноеЛицо");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Операции.ПогашениеЗаймаСотрудником);
	Элемент.Свойства.Вставить("Заголовок", НСтр("ru = 'Сотрудник'"));
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ПодотчетноеЛицо");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Операции.ВозвратНеперечисленныхДС);
	Элемент.Свойства.Вставить("Заголовок", НСтр("ru = 'Работник'"));
	
	// СтатьяДвиженияДенежныхСредств
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("СтатьяДвиженияДенежныхСредств");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.КонвертацияВалюты);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВнутренняяПередачаДенежныхСредств);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыПоПлатежнойКарте);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратНеперечисленныхДС);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратНеперечисленнойЗарплатыПоЗарплатномуПроекту);
	Элемент.Свойства.Вставить("Видимость");
	
	// ПодтверждениеЗачисленияЗарплаты
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ПодтверждениеЗачисленияЗарплаты");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Операции.ВозвратНеперечисленнойЗарплатыПоЗарплатномуПроекту);
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие, , , Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "Дополнительно.ИспользоватьНачислениеЗарплатыУТ", Истина);
	Финансы.НовыйОтбор(ГруппаИли, "Дополнительно.ПеречислениеСпискуПодотчетников", Истина);
	Элемент.Свойства.Вставить("Видимость");
	
	// ДокументВыдачи
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ДокументВыдачи");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие, , , Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	ГруппаИ1 = Финансы.НовыйОтбор(ГруппаИли, , , Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ1.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	Финансы.НовыйОтбор(ГруппаИ1, "ХозяйственнаяОперация", Операции.ВозвратНеперечисленнойЗарплатыПоЗарплатномуПроекту);
	Финансы.НовыйОтбор(ГруппаИ1, "Дополнительно.ИспользоватьНачислениеЗарплатыУТ", Истина);
	ГруппаИ2 = Финансы.НовыйОтбор(ГруппаИли, , , Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ2.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	Финансы.НовыйОтбор(ГруппаИ2, "ХозяйственнаяОперация", Операции.ВозвратНеперечисленнойЗарплатыПоЗарплатномуПроекту);
	Финансы.НовыйОтбор(ГруппаИ2, "Дополнительно.ПеречислениеСпискуПодотчетников", Истина);
	//++ Локализация
	ГруппаИ = Финансы.НовыйОтбор(ГруппаИли, , , Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	Финансы.НовыйОтбор(ГруппаИ, "Дополнительно.ИспользоватьНачислениеЗарплатыУТ", Истина);
	Финансы.НовыйОтбор(ГруппаИ, "ХозяйственнаяОперация", Операции.ВозвратНеперечисленныхДС);
	//-- Локализация
	Элемент.Свойства.Вставить("Видимость");
	
	#КонецОбласти
	
	#Область Реквизиты
	// Конвертация
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ВалютаКонвертации");
	Элемент.Поля.Добавить("КурсКонвертации");
	Элемент.Поля.Добавить("СуммаКонвертации");
	Элемент.Поля.Добавить("КратностьКурсаКонвертации");
	Элемент.Поля.Добавить("ВалютаКурсаКонвертации");
	Элемент.Поля.Добавить("ВалютаКратностиКурсаКонвертации");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Операции.КонвертацияВалюты);
	Элемент.Свойства.Вставить("Видимость");
	
	// НадписьВалюта
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("НадписьВалюта");
	Элемент.Поля.Добавить("НадписьВалютаКомиссия");
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ИспользоватьНесколькоВалют", Ложь);
	Элемент.Свойства.Вставить("Видимость");
	
	// Комиссия
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ГруппаКомиссия");
	Финансы.НовыйОтбор(Элемент.Условие, "ОтражатьКомиссию", Истина);
	Элемент.Свойства.Вставить("Видимость");
	
	// НадписьЗеркальныйПлатеж
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("НадписьЗеркальныйПлатеж");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствИзДругойОрганизации);
	Элемент.Свойства.Вставить("Видимость");
	
	// ТипНалога
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ТипНалога");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВозвратНалогов);
	Элемент.Свойства.Вставить("Видимость");
	
	// ПеречислениеВБюджет
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ПеречислениеВБюджет");
	Элемент.Поля.Добавить("Декорация1");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВозвратНалогов);
	Элемент.Свойства.Вставить("Видимость");
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("НадписьРеквизитыПлатежаВБюджет");
	Финансы.НовыйОтбор(Элемент.Условие, "ПеречислениеВБюджет", Истина);
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВозвратНалогов);
	Элемент.Свойства.Вставить("Видимость");
	
	// УИП
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ИдентификаторПлатежа");
	Элемент.Свойства.Вставить("Заголовок", НСтр("ru = 'УИП'"));
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ИдентификаторПлатежа");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВозвратНалогов);
	Финансы.НовыйОтбор(Элемент.Условие, "ПеречислениеВБюджет", Истина);
	Элемент.Свойства.Вставить("Заголовок", НСтр("ru = 'УИН'"));
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ИдентификаторПлатежа");
	Финансы.СкопироватьОтбор(ОтборОперацийСИденификаторомПлатежа, Элемент.Условие);
	Элемент.Свойства.Вставить("Видимость");
	#КонецОбласти
	
	#Область РасшифровкаПлатежа
	// СтраницаРасшифровка
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("СтраницаРасшифровка");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыОтКлиента);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПоставщика);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПрочееПоступлениеДенежныхСредств);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоКредитам);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоДепозитам);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоЗаймамВыданным);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПогашениеЗаймаСотрудником);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПодотчетника);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствИзДругойОрганизации);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтДругойОрганизации);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтАрендодателя);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратНалогов);
	Элемент.Свойства.Вставить("Видимость");
	
	// Аналитика прочей выдачи
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("РасшифровкаПлатежа.Подразделение");
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияПодразделение");
	Элемент.Поля.Добавить("РасшифровкаПлатежа.НаправлениеДеятельности");
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияНаправлениеДеятельности");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств);
	Элемент.Свойства.Вставить("Видимость");
	
	// СтатьяДвиженияДенежныхСредств
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств");
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияСтатьяДвиженияДенежныхСредств");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыОтКлиента);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПоставщика);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПрочееПоступлениеДенежныхСредств);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоКредитам);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоДепозитам);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоЗаймамВыданным);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПогашениеЗаймаСотрудником);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПодотчетника);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствИзДругойОрганизации);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтДругойОрганизации);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтАрендодателя);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратНалогов);
	Элемент.Свойства.Вставить("Видимость");
	
	// СуммаВзаиморасчетов, ВалютаВзаиморасчетов
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("РасшифровкаПлатежа.СуммаВзаиморасчетов");
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияСуммаВзаиморасчетов");
	Элемент.Поля.Добавить("РасшифровкаПлатежа.ВалютаВзаиморасчетов");
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияВалютаВзаиморасчетов");
	
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ИспользоватьНесколькоВалют", Истина);
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыОтКлиента);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПоставщика);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоКредитам);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоДепозитам);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоЗаймамВыданным);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствИзДругойОрганизации);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтДругойОрганизации);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтАрендодателя);
	Элемент.Свойства.Вставить("Видимость");
	
	// СуммаВзаиморасчетов
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияСуммаВзаиморасчетов");
	
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ИспользоватьНесколькоВалют", Истина);
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ВалютаВзаиморасчетов", "Валюта", , , Истина);
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыОтКлиента);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПоставщика);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоКредитам);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоДепозитам);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоЗаймамВыданным);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствИзДругойОрганизации);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтДругойОрганизации);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтАрендодателя);
	Элемент.Свойства.Вставить("ТолькоПросмотр");
	
	// ДекорацияВалютыИКурсПлатежа, КурсЧислительВзаиморасчетов, КурсЗнаменательВзаиморасчетов
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ДекорацияВалютыИКурсПлатежа");
	Элемент.Поля.Добавить("РасшифровкаПлатежа.КурсЧислительВзаиморасчетов");
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияКурсЧислительВзаиморасчетов");
	Элемент.Поля.Добавить("РасшифровкаПлатежа.КурсЧислительВзаиморасчетовТекст");
	Элемент.Поля.Добавить("РасшифровкаПлатежаКурсЗнаменательВзаиморасчетовНадпись");
	Элемент.Поля.Добавить("РасшифровкаПлатежа.ВариантКурсаДоговора");
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияВариантКурсаДоговора");
	
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ИспользоватьНесколькоВалют", Истина);
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыОтКлиента);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПоставщика);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствИзДругойОрганизации);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтДругойОрганизации);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоКредитам);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоДепозитам);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоЗаймамВыданным);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтАрендодателя);
	
	Финансы.НовыйОтбор(Элемент.Условие, "Валюта", "Дополнительно.ВалютаВзаиморасчетов",, ВидСравненияКомпоновкиДанных.НеРавно, Истина);
	ПодгруппаИли = Финансы.НовыйОтбор(Элемент.Условие, , , Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ПодгруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ПодгруппаИли, "Дополнительно.НесколькоВалютВзаиморасчетов", Истина);
	Финансы.НовыйОтбор(ПодгруппаИли, "Дополнительно.ВалютаВзаиморасчетов", Справочники.Валюты.ПустаяСсылка(), , ВидСравненияКомпоновкиДанных.НеРавно);
	Элемент.Свойства.Вставить("Видимость");
	
	// ДекорацияВалютыИКурсПлатежа
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ДекорацияВалютыИКурсПлатежа");
	ГруппаИ = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	Финансы.НовыйОтбор(ГруппаИ, "Дополнительно.ВалютаВзаиморасчетов",,, ВидСравненияКомпоновкиДанных.Заполнено);
	Финансы.НовыйОтбор(ГруппаИ, "Дополнительно.ВариантКурсаДоговора",,, ВидСравненияКомпоновкиДанных.Заполнено);
	Элемент.Свойства.Вставить("Доступность");
	
	// РасшифровкаБезРазбиенияВариантКурсаДоговора
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияВариантКурсаДоговора");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "Дополнительно.ВариантКурсаДоговора", Перечисления.ВариантыКурсаДоговора.УстановленныйВДоговоре,, ВидСравненияКомпоновкиДанных.Равно);
	ГруппаИ = Финансы.НовыйОтбор(ГруппаИли,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	Финансы.НовыйОтбор(ГруппаИ, "Дополнительно.ВариантКурсаДоговора", Перечисления.ВариантыКурсаДоговора.ФиксированныйНаДатуОтгрузки,, ВидСравненияКомпоновкиДанных.Равно);
	Финансы.НовыйОтбор(ГруппаИ, "Дополнительно.ТипОбъектаРасчетов", Перечисления.ТипыОбъектовРасчетов.Накладная,, ВидСравненияКомпоновкиДанных.Равно);
	Элемент.Свойства.Вставить("Гиперссылка");
	
	// ОбъектРасчетов, Основание платежа, СуммаНДС, СтавкаНДС
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("РасшифровкаПлатежа.ОснованиеПлатежа");
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияОснованиеПлатежа");
	Элемент.Поля.Добавить("РасшифровкаПлатежа.ОбъектРасчетов");
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияОбъектРасчетов");
	Элемент.Поля.Добавить("РасшифровкаПлатежа.СуммаНДС");
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияСуммаНДС");
	Элемент.Поля.Добавить("РасшифровкаПлатежа.СтавкаНДС");
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияСтавкаНДС");
	Элемент.Поля.Добавить("ВалютаНДС");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыОтКлиента);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПоставщика);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствИзДругойОрганизации);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтДругойОрганизации);
	Элемент.Свойства.Вставить("Видимость");
	
	// Партнер
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("РасшифровкаПлатежа.Партнер");
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияПартнер");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыОтКлиента);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПоставщика);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоКредитам);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоДепозитам);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоЗаймамВыданным);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтАрендодателя);
	Элемент.Свойства.Вставить("Видимость");
	
	// ДоговорКредитаДепозита
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("РасшифровкаПлатежа.ДоговорКредитаДепозита");
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияДоговорКредитаДепозита");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоКредитам);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоДепозитам);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоЗаймамВыданным);
	Элемент.Свойства.Вставить("Видимость");
	
	// ТипСуммыКредитаДепозита
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("РасшифровкаПлатежа.ТипСуммыКредитаДепозита");
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияТипСуммыКредитаДепозита");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоДепозитам);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоЗаймамВыданным);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПогашениеЗаймаСотрудником);
	Элемент.Свойства.Вставить("Видимость");
	
	// ДоговорАренды, ТипПлатежаПоАренде
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("РасшифровкаПлатежа.ДоговорАренды");
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияДоговорАренды");
	Элемент.Поля.Добавить("РасшифровкаПлатежа.ТипПлатежаПоАренде");
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияТипПлатежаПоАренде");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтАрендодателя);
	Элемент.Свойства.Вставить("Видимость");
	
	// РасчетныйДокументПоАренде
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("РасшифровкаПлатежа.РасчетныйДокументПоАренде");
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияРасчетныйДокументПоАренде");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтАрендодателя);
	Элемент.Свойства.Вставить("Видимость");
	
	// Организация - владелец объекта расчетов
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("РасшифровкаПлатежа.Организация");
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияОрганизация");
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ЕстьФилиалы", Истина);
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыОтКлиента);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПоставщика);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствИзДругойОрганизации);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтДругойОрганизации);
	Элемент.Свойства.Вставить("Видимость");
	
	// ГиперссылкаРеквизитыОбъектаРасчетов
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ГиперссылкаРеквизитыОбъектаРасчетов");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыОтКлиента);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПоставщика);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствИзДругойОрганизации);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтДругойОрганизации);
	Элемент.Свойства.Вставить("Видимость");
	
	// Нераспределенный платеж
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ГруппаФинансовогоУчета");
	Элемент.Поля.Добавить("НаправлениеДеятельности");
	Элемент.Поля.Добавить("ПодразделениеРП");
	ГруппаИ = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	Финансы.НовыйОтбор(ГруппаИ, "Дополнительно.СуммаНераспределено", 0,, ВидСравненияКомпоновкиДанных.НеРавно);
	Финансы.НовыйОтбор(ГруппаИ, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтДругойОрганизации,, ВидСравненияКомпоновкиДанных.НеРавно);
	Финансы.НовыйОтбор(ГруппаИ, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПоставщика,, ВидСравненияКомпоновкиДанных.НеРавно);
	Элемент.Свойства.Вставить("Видимость");

	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ГруппаФинансовогоУчетаВозврата");
	Элемент.Поля.Добавить("НаправлениеДеятельностиВозврата");
	Элемент.Поля.Добавить("ПодразделениеРПВозврата");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтДругойОрганизации);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПоставщика);
	Элемент.Свойства.Вставить("Видимость");
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("Договор");
	
	ГруппаИ1 = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ1.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ГруппаИли = Финансы.НовыйОтбор(ГруппаИ1,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ГруппаИ2 = Финансы.НовыйОтбор(ГруппаИли,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ2.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	Финансы.НовыйОтбор(ГруппаИ2, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыОтКлиента);
	Финансы.НовыйОтбор(ГруппаИ2, "Дополнительно.ИспользоватьДоговорыСКлиентами", Истина);
	
	ГруппаИ3 = Финансы.НовыйОтбор(ГруппаИли,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ3.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	Финансы.НовыйОтбор(ГруппаИ3, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствИзДругойОрганизации);
	Финансы.НовыйОтбор(ГруппаИ3, "Дополнительно.ИспользоватьДоговорыМеждуОрганизациями", Истина);
	
	Элемент.Свойства.Вставить("Видимость");

	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ДоговорВозврата");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ГруппаИ1 = Финансы.НовыйОтбор(ГруппаИли,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ1.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	Финансы.НовыйОтбор(ГруппаИ1, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтДругойОрганизации);
	Финансы.НовыйОтбор(ГруппаИ1, "Дополнительно.ИспользоватьДоговорыМеждуОрганизациями", Истина);
	
	ГруппаИ2 = Финансы.НовыйОтбор(ГруппаИли,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ2.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	Финансы.НовыйОтбор(ГруппаИ2, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПоставщика);
	Финансы.НовыйОтбор(ГруппаИ2, "Дополнительно.ИспользоватьДоговорыСПоставщиками", Истина);
	
	Элемент.Свойства.Вставить("Видимость");
	
	// Договор объекта расчетов
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("РасшифровкаПлатежаДоговор");
	
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ГруппаИ1 = Финансы.НовыйОтбор(ГруппаИли,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ1.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	Финансы.НовыйОтбор(ГруппаИ1, "Дополнительно.ИспользоватьДоговорыСКлиентами", Истина);
	Финансы.НовыйОтбор(ГруппаИ1, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыОтКлиента);
	
	ГруппаИ2 = Финансы.НовыйОтбор(ГруппаИли,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ2.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	Финансы.НовыйОтбор(ГруппаИ2, "Дополнительно.ИспользоватьДоговорыМеждуОрганизациями", Истина);
	ГруппаИли2 = Финансы.НовыйОтбор(ГруппаИ2,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли2.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли2, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствИзДругойОрганизации);
	Финансы.НовыйОтбор(ГруппаИли2, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтДругойОрганизации);
	
	ГруппаИ3 = Финансы.НовыйОтбор(ГруппаИли,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ3.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	Финансы.НовыйОтбор(ГруппаИ3, "Дополнительно.ИспользоватьДоговорыСПоставщиками", Истина);
	ГруппаИли3 = Финансы.НовыйОтбор(ГруппаИ3,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли3.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли3, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПоставщика);
	
	Элемент.Свойства.Вставить("Видимость");
	
	// Дата погашения
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияДатаПогашения");
	Элемент.Поля.Добавить("РасшифровкаПлатежаДатаПогашения");
	
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыОтКлиента);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствИзДругойОрганизации);
	
	Элемент.Свойства.Вставить("Видимость");
	
	#КонецОбласти
	
	#Область Команды
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ЗаполнитьРасшифровкуПлатежаПоЗаймамКонтрагенту");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствПоЗаймамВыданным);
	Элемент.Свойства.Вставить("Видимость");
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ЗаполнитьРасшифровкуПлатежаПоЗаймамСотруднику");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Операции.ПогашениеЗаймаСотрудником);
	Элемент.Свойства.Вставить("Видимость");
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("РасшифровкаПлатежаПодборПоОстаткам");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтПоставщика);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеОплатыОтКлиента);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ПоступлениеДенежныхСредствИзДругойОрганизации);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Операции.ВозвратДенежныхСредствОтДругойОрганизации);
	Элемент.Свойства.Вставить("Видимость");
	
	// ПодборПоУИП
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ПодборПоУИП");
	Финансы.СкопироватьОтбор(ОтборОперацийСИденификаторомПлатежа, Элемент.Условие);
	Элемент.Свойства.Вставить("Видимость");
	#КонецОбласти
	
КонецПроцедуры

// Возвращает параметры выбора статей в документе.
// 
// Параметры:
// 	ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - Хозяйственная операция документа.
// 	ДополнительнаяДоступность - Булево - Дополнительный признак доступности выбора статей и аналитик.
// 
// Возвращаемое значение:
// 	Массив - Массив параметров настройки счетов учета (См. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики)
//
Функция ПараметрыВыбораСтатейИАналитик(ХозяйственнаяОперация, ДополнительнаяДоступность = Истина) Экспорт
	
	МассивПараметровВыбора = Новый Массив;
	
	#Область СтатьяДоходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект.РасшифровкаПлатежа";
	ПараметрыВыбора.Статья = "СтатьяДоходов";
	ПараметрыВыбора.ТипСтатьи = "ТипСтатьи";
	ПараметрыВыбора.ЗначениеПоУмолчанию = ПланыВидовХарактеристик.СтатьиДоходов.ПустаяСсылка();
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("РасшифровкаБезРазбиенияСтатьяДоходов");
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("РасшифровкаПлатежаСтатьяДоходов");
	ПараметрыВыбора.ДоступностьПоОперации = (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств);
	
	#Область АналитикаАктивовПассивов
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивов";
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("РасшифровкаБезРазбиенияАналитикаАктивовПассивов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("РасшифровкаПлатежаАналитикаАктивовПассивов");
	#КонецОбласти
	
	#Область АналитикаДоходов
	ПараметрыВыбора.ВыборСтатьиДоходов = Истина;
	ПараметрыВыбора.АналитикаДоходов = "АналитикаДоходов";
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("РасшифровкаБезРазбиенияАналитикаДоходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("РасшифровкаПлатежаАналитикаДоходов");
	#КонецОбласти
	
	МассивПараметровВыбора.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	ПараметрыВыбора.ТипСтатьи = "ТипСтатьи";
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходов");
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.ЗначениеПоУмолчанию = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходов");
	
	ПараметрыВыбора.ДоступностьПоОперации =
		(ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте) И ДополнительнаяДоступность;
	
	МассивПараметровВыбора.Добавить(ПараметрыВыбора);
	
	Возврат МассивПараметровВыбора;
	
КонецФункции

// Функция получает реквизиты документа для обмена с конфигурацией "Бухгалтерия предприятия".
//
// Параметры:
//	ДокументСсылка - ДокументСсылка - Документ, для которого необходимо получить реквизиты.
//
// Возвращаемое значение:
//	Структура - Структура реквизитов документа.
//
Функция РеквизитыДокументаДляОбменаСБухгалтерией(ДокументСсылка) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДанныеДокумента.СтатьяДвиженияДенежныхСредств.КорреспондирующийСчет КАК КорреспондирующийСчет,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.БанковскийСчет.ВалютаДенежныхСредств КАК ВалютаБанковскогоСчета,
	|	ДанныеДокумента.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета
	|ИЗ
	|	Документ.ПоступлениеБезналичныхДенежныхСредств КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.СтатьяДвиженияДенежныхСредств.КорреспондирующийСчет <> """"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДанныеДокумента.СтатьяДоходов.КорреспондирующийСчет КАК КорреспондирующийСчет,
	|	ДанныеДокумента.СтатьяДоходов КАК СтатьяДоходов
	|ИЗ
	|	Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеДоходы)
	|	И ДанныеДокумента.СтатьяДоходов.КорреспондирующийСчет <> """"
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеДокумента.НомерСтроки");
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Выборка = МассивРезультатов[0].Выбрать();
	Если Выборка.Следующий() Тогда
		КорреспондирующийСчет  = Выборка.КорреспондирующийСчет;
		ХозяйственнаяОперация  = Выборка.ХозяйственнаяОперация;
		ВалютаБанковскогоСчета = Выборка.ВалютаБанковскогоСчета;
		ВалютаРегламентированногоУчета = Выборка.ВалютаБанковскогоСчета;
	Иначе
		КорреспондирующийСчет  = "";
		ХозяйственнаяОперация  = Перечисления.ХозяйственныеОперации.ПустаяСсылка();
		ВалютаБанковскогоСчета = Неопределено;
		ВалютаРегламентированногоУчета = Неопределено;
	КонецЕсли;
	
	Выборка = МассивРезультатов[1].Выбрать();
	Если Выборка.Следующий() Тогда
		КорреспондирующийСчет = Выборка.КорреспондирующийСчет;
		СтатьяДоходов = Выборка.СтатьяДоходов;
	Иначе
		СтатьяДоходов = Неопределено;
	КонецЕсли;
	
	ВидОперации = "ПрочееПоступление";
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств Тогда
		
		Если СтрНайти(КорреспондирующийСчет, "57.02") <> 0
			И ВалютаБанковскогоСчета <> ВалютаРегламентированногоУчета Тогда
			ВидОперации = "ПриобретениеВалюты";
			
		ИначеЕсли СтрНайти(КорреспондирующийСчет, "57.22") <> 0
			И ВалютаБанковскогоСчета = ВалютаРегламентированногоУчета Тогда
			ВидОперации = "ПоступленияОтПродажиИностраннойВалюты";
			
		ИначеЕсли СтрНайти(КорреспондирующийСчет, "66") <> 0
		 ИЛИ СтрНайти(КорреспондирующийСчет, "67") <> 0 Тогда
			ВидОперации = "РасчетыПоКредитамИЗаймам";
			
		ИначеЕсли СтрНайти(КорреспондирующийСчет, "76") <> 0
		 ИЛИ СтрНайти(КорреспондирующийСчет, "60") <> 0
		 ИЛИ СтрНайти(КорреспондирующийСчет, "62") <> 0 Тогда
			ВидОперации = "ПрочиеРасчетыСКонтрагентами";
			
		КонецЕсли;
		
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура("ВидОперации, Счет, СтатьяДоходов",
		ВидОперации,
		КорреспондирующийСчет,
		СтатьяДоходов);
	
	Возврат СтруктураРеквизитов;
		
КонецФункции

#КонецОбласти


#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтение
	|ГДЕ
	|	( ЗначениеРазрешено(Организация)
	|	ИЛИ ЗначениеРазрешено(РасшифровкаПлатежа.Организация, Null КАК Истина) И ХозяйственнаяОперация В (
	|		Значение(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента),
	|		Значение(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика),
	|		Значение(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации),
	|		Значение(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации)
	|		)
	|	)И ЗначениеРазрешено(ХозяйственнаяОперация)
	|	И ВЫБОР КОГДА ХозяйственнаяОперация = Значение(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика) ИЛИ ХозяйственнаяОперация = Значение(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента)
	|	ТОГДА ЗначениеРазрешено(РасшифровкаПлатежа.Партнер, Null КАК Истина)
	|	ИНАЧЕ ИСТИНА КОНЕЦ 
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ХозяйственнаяОперация)
	|	И ВЫБОР КОГДА ХозяйственнаяОперация = Значение(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика) ИЛИ ХозяйственнаяОперация = Значение(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента)
	|	ТОГДА ЗначениеРазрешено(РасшифровкаПлатежа.Партнер, Null КАК Истина)
	|	ИНАЧЕ ИСТИНА КОНЕЦ ";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Данные.Вставить("НаименованиеВходящегоДокумента", СокрЛП(Данные.ТипПлатежногоДокумента));
	Данные.Удалить("ТипПлатежногоДокумента");
	ОбщегоНазначенияУТКлиентСервер.ОбработкаПолученияПредставленияВходящегоДокумента(
		Данные, Представление, СтандартнаяОбработка, "ПоступлениеБезналичныхДенежныхСредств");
	
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Поля.Очистить();
	Поля.Добавить("ТипПлатежногоДокумента");
	Поля.Добавить("НомерВходящегоДокумента");
	Поля.Добавить("ДатаВходящегоДокумента");
	Поля.Добавить("Дата");
	Поля.Добавить("Номер");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;
	ИсточникиДанных.Вставить("РасшифровкаПлатежа.ОбъектРасчетов");

	Возврат ИсточникиДанных; 

КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	Запрос = Новый Запрос();
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстыЗапросаВременныхТаблиц = Новый Соответствие;
	ПереопределениеРасчетаПараметров = Новый Структура;
	СинонимТаблицыДокумента = "";
	
	ПолноеИмяДокумента = "Документ.ПоступлениеБезналичныхДенежныхСредств";
	
	Если ИмяРегистра = "ДенежныеСредстваКВыплате" Тогда
		
		ПереопределениеРасчетаПараметров.Вставить("Период", "
			|ВЫБОР КОГДА НЕ ДанныеДокумента.ПроведеноБанком
			|	ИЛИ НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата) = ДанныеДокумента.ДатаПроведенияБанком
			|	ИЛИ ДанныеДокумента.Исправление ТОГДА
			|		ДанныеДокумента.Дата
			|	ИНАЧЕ
			|		КОНЕЦПЕРИОДА(ДанныеДокумента.ДатаПроведенияБанком, ДЕНЬ)
			|КОНЕЦ");
		
		ТекстЗапроса = ТекстЗапросаТаблицаДенежныеСредстваКВыплате(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	ИначеЕсли ИмяРегистра = "РеестрДокументов" Тогда
		
		ПереопределениеРасчетаПараметров.Вставить("Период", "
			|ВЫБОР КОГДА НЕ ДанныеДокумента.ПроведеноБанком
			|	ИЛИ НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата) = ДанныеДокумента.ДатаПроведенияБанком
			|	ИЛИ ДанныеДокумента.Исправление ТОГДА
			|		ДанныеДокумента.Дата
			|	ИНАЧЕ
			|		КОНЕЦПЕРИОДА(ДанныеДокумента.ДатаПроведенияБанком, ДЕНЬ)
			|КОНЕЦ");
		ПереопределениеРасчетаПараметров.Вставить("ОрганизацияОтправитель", "ДанныеДокумента.БанковскийСчетОтправитель.Владелец");
		ПереопределениеРасчетаПараметров.Вставить("ПартнерИзДокумента", "
			|ВЫБОР
			|	КОГДА
			|		ДанныеДокумента.ХозяйственнаяОперация В (
			|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации),
			|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации),
			|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств))
			|	ТОГДА
			|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
			|	ИНАЧЕ
			|		ДанныеДокумента.Партнер
			|КОНЕЦ");
		ПереопределениеРасчетаПараметров.Вставить("КонтрагентИзДокумента", "
			|ВЫБОР
			|	КОГДА
			|		ДанныеДокумента.ХозяйственнаяОперация В (
			|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации),
			|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации),
			|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств))
			|	ТОГДА
			|		ДанныеДокумента.БанковскийСчетОтправитель.Владелец
			|	ИНАЧЕ
			|		ДанныеДокумента.Контрагент
			|КОНЕЦ");
			
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса, ПолноеИмяДокумента, СинонимТаблицыДокумента, Истина, ПереопределениеРасчетаПараметров);
	Иначе
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса, ПолноеИмяДокумента, СинонимТаблицыДокумента, ПереопределениеРасчетаПараметров, ТекстыЗапросаВременныхТаблиц);
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметровПроведения();
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК ЕстьПодарочныйСертификат
	|ПОМЕСТИТЬ НаличиеВидаОбъектаРасчетовПодарочныйСертификат
	|ИЗ
	|	ДанныеДокументаТаблицаРасшифровкаПлатежа КАК РасшифровкаПлатежа
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО (РасшифровкаПлатежа.ОбъектРасчетов = ОбъектыРасчетов.Ссылка)
	|ГДЕ
	|	ОбъектыРасчетов.Объект ССЫЛКА Справочник.ПодарочныеСертификаты
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА НЕ ДанныеДокумента.ПроведеноБанком
	|		ИЛИ НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата) = ДанныеДокумента.ДатаПроведенияБанком
	|		ИЛИ ДанныеДокумента.Исправление ТОГДА
	|		ДанныеДокумента.Дата
	|	ИНАЧЕ
	|		КОНЕЦПЕРИОДА(ДанныеДокумента.ДатаПроведенияБанком, ДЕНЬ)
	|	КОНЕЦ КАК Период,
	|	ДанныеДокумента.ПроведеноБанком                         КАК ПроведеноБанком,
	|
	|	ДанныеДокумента.НомерВходящегоДокумента                 КАК НомерВходящегоДокумента,
	|	ДанныеДокумента.Номер                                   КАК Номер,
	|	ДанныеДокумента.ДатаВходящегоДокумента                  КАК ДатаВходящегоДокумента,
	|	ДанныеДокумента.Дата                                    КАК Дата,
	|	ДанныеДокумента.НазначениеПлатежа                       КАК НазначениеПлатежа,
	|	ДанныеДокумента.Валюта                                  КАК Валюта,
	|	ДанныеДокумента.ВалютаКонвертации                       КАК ВалютаКонвертации,
	|	ДанныеДокумента.Организация                             КАК Организация,
	|	ДанныеДокумента.ОрганизацияГоловнаяОрганизация          КАК ГоловнаяОрганизация,
	|	ДанныеДокумента.БанковскийСчет                          КАК БанковскийСчет,
	|	ДанныеДокумента.БанковскийСчетПодразделение             КАК Подразделение,
	|	ДанныеДокумента.БанковскийСчетНаправлениеДеятельности   КАК НаправлениеДеятельностиДС,
	
	|	ВЫБОР
	|		КОГДА
	|			ДанныеДокумента.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств))
	|		ТОГДА
	|			ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|	
	|		КОГДА
	|			ЕСТЬNULL(НаличиеВидаПодарочныйСертификат.ЕстьПодарочныйСертификат, ЛОЖЬ)
	|		ТОГДА
	|			ЗНАЧЕНИЕ(Справочник.Партнеры.РозничныйПокупатель)
	|	
	|	ИНАЧЕ
	|		ДанныеДокумента.Партнер
	|	КОНЕЦ                                                   КАК Партнер,
	|
	|	ВЫБОР
	|		КОГДА
	|			ДанныеДокумента.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств))
	|		ТОГДА
	|			ДанныеДокумента.БанковскийСчетОтправительВладелец
	|	
	|		КОГДА
	|			ЕСТЬNULL(НаличиеВидаПодарочныйСертификат.ЕстьПодарочныйСертификат, ЛОЖЬ)
	|		ТОГДА
	|			ЗНАЧЕНИЕ(Справочник.Контрагенты.РозничныйПокупатель)
	|	
	|	ИНАЧЕ
	|		ДанныеДокумента.Контрагент
	|	КОНЕЦ                                                   КАК Контрагент,
	|	ВЫБОР
	|		КОГДА
	|			ДанныеДокумента.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств))
	|		ТОГДА
	|			ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|		ИНАЧЕ
	|			ДанныеДокумента.Партнер
	|	КОНЕЦ                                                   КАК ПартнерИзДокумента,
	|
	|	ВЫБОР
	|		КОГДА
	|			ДанныеДокумента.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств))
	|		ТОГДА
	|			ДанныеДокумента.БанковскийСчетОтправительВладелец
	|		ИНАЧЕ
	|			ДанныеДокумента.Контрагент
	|	КОНЕЦ                                                   КАК КонтрагентИзДокумента,
	
	|	ДанныеДокумента.БанковскийСчетОтправительВладелец       КАК ОрганизацияОтправитель,
	|	ДанныеДокумента.ХозяйственнаяОперация                   КАК ХозяйственнаяОперация,
	
	|	ВЫБОР КОГДА
	|		ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет)
	|	ТОГДА
	|		ВЫБОР КОГДА ДанныеДокумента.Валюта = ДанныеДокумента.ОрганизацияВалютаРегламентированногоУчета ТОГДА
	|			ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.СдачаДенежныхСредствВБанк)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.СдачаДенежныхСредствВБанкВИностраннойВалюте)
	|		КОНЕЦ
	|	КОГДА
	|		ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствСДругогоСчета)
	|	ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПеречислениеДенежныхСредствНаДругойСчет)
	|	ИНАЧЕ
	|		ДанныеДокумента.СтатьяДвиженияДенежныхСредств
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств,
	
	|	ДанныеДокумента.СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
	|
	|	ДанныеДокумента.ДокументВыдачи КАК ДокументВыдачи,
	|	ДанныеДокумента.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
	|	ДанныеДокумента.СуммаДокумента КАК СуммаДокумента,
	|	
	|	ДанныеДокумента.Договор                 КАК Договор,
	|	ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеДокумента.Ответственный           КАК Ответственный,
	|	ДанныеДокумента.Автор                   КАК Автор,
	|	ДанныеДокумента.Комментарий             КАК Комментарий,
	|	ДанныеДокумента.ПометкаУдаления         КАК ПометкаУдаления,
	|	ДанныеДокумента.Ссылка                  КАК Ссылка,
	|	ДанныеДокумента.Исправление             КАК Исправление,
	|	ДанныеДокумента.СторнируемыйДокумент    КАК СторнируемыйДокумент,
	|	ДанныеДокумента.ИсправляемыйДокумент    КАК ИсправляемыйДокумент,
	|	ДанныеДокумента.Проведен                КАК Проведен,
	|	ЕСТЬNULL(НаличиеВидаПодарочныйСертификат.ЕстьПодарочныйСертификат, ЛОЖЬ) КАК ЕстьПодарочныйСертификат,
	|	НастройкиХозяйственныхОпераций.Ссылка   КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаШапка КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиХозяйственныхОпераций КАК НастройкиХозяйственныхОпераций
	|	ПО ДанныеДокумента.ХозяйственнаяОперация = НастройкиХозяйственныхОпераций.ХозяйственнаяОперация
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ НаличиеВидаОбъектаРасчетовПодарочныйСертификат КАК НаличиеВидаПодарочныйСертификат
	|	ПО ИСТИНА
	|";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения(Реквизиты) Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Процедура ПолучитьДанныеДокумента(Запрос, ДокументСсылка)

	СписокЗапросов = Новый Массив;
	
	ТекстЗапросаДанныеДокументаШапка(СписокЗапросов);
	ТекстЗапросаДанныеДокументаТаблицаРасшифровкаПлатежа(СписокЗапросов);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ТекстЗапросаДанныеДокументаШапка(СписокЗапросов)
	
	ТекстЗапросаШапка = "ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.БанковскийСчет КАК БанковскийСчет,
	|	ДанныеДокумента.ТипПлатежногоДокумента КАК ТипПлатежногоДокумента,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.СуммаДокумента КАК СуммаДокумента,
	|	ДанныеДокумента.Контрагент КАК Контрагент,
	|	ДанныеДокумента.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	ДанныеДокумента.БанковскийСчетОтправитель КАК БанковскийСчетОтправитель,
	|	ДанныеДокумента.КассаОтправитель КАК КассаОтправитель,
	|	ДанныеДокумента.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	ДанныеДокумента.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	ДанныеДокумента.НазначениеПлатежа КАК НазначениеПлатежа,
	|	ДанныеДокумента.Валюта КАК Валюта,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ДанныеДокумента.ДокументВыдачи КАК ДокументВыдачи,
	|	ДанныеДокумента.ВалютаКонвертации КАК ВалютаКонвертации,
	|	ДанныеДокумента.КурсКонвертации КАК КурсКонвертации,
	|	ДанныеДокумента.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.СтатьяАктивовПассивов КАК СтатьяАктивовПассивов,
	|	ДанныеДокумента.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	ДанныеДокумента.ПроведеноБанком КАК ПроведеноБанком,
	|	ДанныеДокумента.ДатаПроведенияБанком КАК ДатаПроведенияБанком,
	|	ДанныеДокумента.ДокументОснование КАК ДокументОснование,
	|	ДанныеДокумента.ИдентификаторПлатежа КАК ИдентификаторПлатежа,
	|	ДанныеДокумента.СуммаКонвертации КАК СуммаКонвертации,
	|	ДанныеДокумента.ДатаВыгрузки КАК ДатаВыгрузки,
	|	ДанныеДокумента.ДатаЗагрузки КАК ДатаЗагрузки,
	|	ДанныеДокумента.ОшибкиЗагрузки КАК ОшибкиЗагрузки,
	|	ДанныеДокумента.ФорматированноеНазначениеПлатежа КАК ФорматированноеНазначениеПлатежа,
	|	ДанныеДокумента.ДанныеВыписки КАК ДанныеВыписки,
	|	ДанныеДокумента.ИмяКонтрагента КАК ИмяКонтрагента,
	|	ДанныеДокумента.ПодтверждениеЗачисленияЗарплаты КАК ПодтверждениеЗачисленияЗарплаты,
	|	ДанныеДокумента.ПроводкиПоРаботникам КАК ПроводкиПоРаботникам,
	|	ДанныеДокумента.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеДокумента.Договор КАК Договор,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.КодВалютнойОперации КАК КодВалютнойОперации,
	|	ДанныеДокумента.Партнер КАК Партнер,
	|	ДанныеДокумента.КратностьКурсаКонвертации КАК КратностьКурсаКонвертации,
	|	ДанныеДокумента.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ДанныеДокумента.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	ДанныеДокумента.Автор КАК Автор,
	|	ДанныеДокумента.ДоговорЭквайринга КАК ДоговорЭквайринга,
	|	ДанныеДокумента.ОтражатьКомиссию КАК ОтражатьКомиссию,
	|	ДанныеДокумента.СуммаКомиссии КАК СуммаКомиссии,
	|	ДанныеДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов КАК АналитикаРасходов,
	|	ДанныеДокумента.Исправление КАК Исправление,
	|	ДанныеДокумента.СторнируемыйДокумент КАК СторнируемыйДокумент,
	|	ДанныеДокумента.ИсправляемыйДокумент КАК ИсправляемыйДокумент,
	|	ДанныеДокумента.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ДанныеДокумента.СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
	|	ДанныеДокумента.ПеречислениеВБюджет КАК ПеречислениеВБюджет,
	|	ДанныеДокумента.СтатусСоставителя КАК СтатусСоставителя,
	|	ДанныеДокумента.КодБК КАК КодБК,
	|	ДанныеДокумента.КодОКАТО КАК КодОКАТО,
	|	ДанныеДокумента.ПоказательОснования КАК ПоказательОснования,
	|	ДанныеДокумента.ПоказательПериода КАК ПоказательПериода,
	|	ДанныеДокумента.ПоказательНомера КАК ПоказательНомера,
	|	ДанныеДокумента.ПоказательДаты КАК ПоказательДаты,
	|	ДанныеДокумента.ТипНалога КАК ТипНалога,
	|	ДанныеДокумента.НастройкаСчетовУчета КАК НастройкаСчетовУчета,
	|	ДанныеДокумента.ВидПеречисленияВБюджет КАК ВидПеречисленияВБюджет,
	|	ДанныеДокумента.ПоказательТипа КАК ПоказательТипа,
	|	ДанныеДокумента.Организация.ГоловнаяОрганизация КАК ОрганизацияГоловнаяОрганизация,
	|	ДанныеДокумента.БанковскийСчет.Подразделение КАК БанковскийСчетПодразделение,
	|	ДанныеДокумента.БанковскийСчет.НаправлениеДеятельности КАК БанковскийСчетНаправлениеДеятельности,
	|	ДанныеДокумента.БанковскийСчетОтправитель.Владелец КАК БанковскийСчетОтправительВладелец,
	|	ДанныеДокумента.БанковскийСчетОтправитель.НаправлениеДеятельности КАК
	|		БанковскийСчетОтправительНаправлениеДеятельности,
	|	ДанныеДокумента.Организация.ВалютаРегламентированногоУчета КАК ОрганизацияВалютаРегламентированногоУчета,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеДокумента.Контрагент.Партнер КАК КонтрагентПартнер,
	|	ДанныеДокумента.БанковскийСчет.ВалютаДенежныхСредств КАК БанковскийСчетВалютаДенежныхСредств,
	|	ДанныеДокумента.ДоговорЭквайринга.СтатьяДвиженияДенежныхСредствКомиссия КАК
	|		ДоговорЭквайрингаСтатьяДвиженияДенежныхСредствКомиссия,
	|	ДанныеДокумента.ДоговорЭквайринга.НаправлениеДеятельности КАК ДоговорЭквайрингаНаправлениеДеятельности
	|ПОМЕСТИТЬ ДанныеДокументаШапка
	|ИЗ
	|	Документ.ПоступлениеБезналичныхДенежныхСредств КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	
	СписокЗапросов.Добавить(ТекстЗапросаШапка);
	
КонецПроцедуры

Процедура ТекстЗапросаДанныеДокументаТаблицаРасшифровкаПлатежа(СписокЗапросов)
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ТаблицаРасшифровкаПлатежа.НомерСтроки КАК НомерСтроки,
	|	ТаблицаРасшифровкаПлатежа.Ссылка КАК Ссылка,
	|	ТаблицаРасшифровкаПлатежа.Партнер КАК Партнер,
	|	ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ТаблицаРасшифровкаПлатежа.ОснованиеПлатежа КАК ОснованиеПлатежа,
	|	ТаблицаРасшифровкаПлатежа.Сумма КАК Сумма,
	|	ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ТаблицаРасшифровкаПлатежа.СтатьяДоходов КАК СтатьяДоходов,
	|	ТаблицаРасшифровкаПлатежа.АналитикаДоходов КАК АналитикаДоходов,
	|	ТаблицаРасшифровкаПлатежа.Подразделение КАК Подразделение,
	|	ТаблицаРасшифровкаПлатежа.ДоговорКредитаДепозита КАК ДоговорКредитаДепозита,
	|	ТаблицаРасшифровкаПлатежа.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаРасшифровкаПлатежа.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	ТаблицаРасшифровкаПлатежа.ДоговорЗаймаСотруднику КАК ДоговорЗаймаСотруднику,
	|	ТаблицаРасшифровкаПлатежа.ТипСуммыКредитаДепозита КАК ТипСуммыКредитаДепозита,
	|	ТаблицаРасшифровкаПлатежа.Организация КАК Организация,
	|	ТаблицаРасшифровкаПлатежа.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаРасшифровкаПлатежа.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаРасшифровкаПлатежа.СуммаНДС КАК СуммаНДС,
	|	ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ТаблицаРасшифровкаПлатежа.НастройкаСчетовУчета КАК НастройкаСчетовУчета,
	|	ТаблицаРасшифровкаПлатежа.ДоговорАренды КАК ДоговорАренды,
	|	ТаблицаРасшифровкаПлатежа.ТипПлатежаПоАренде КАК ТипПлатежаПоАренде,
	|	ТаблицаРасшифровкаПлатежа.РасчетныйДокументПоАренде КАК РасчетныйДокументПоАренде,
	|	ТаблицаРасшифровкаПлатежа.КурсЧислительВзаиморасчетов КАК КурсЧислительВзаиморасчетов,
	|	ТаблицаРасшифровкаПлатежа.КурсЗнаменательВзаиморасчетов КАК КурсЗнаменательВзаиморасчетов,
	|	ТаблицаРасшифровкаПлатежа.СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
	|	ТаблицаРасшифровкаПлатежа.ДатаПогашения КАК ДатаПогашения,
	|	ТаблицаРасшифровкаПлатежа.ОбъектРасчетов.Организация КАК ОбъектРасчетовОрганизация,
	|	ТаблицаРасшифровкаПлатежа.ОбъектРасчетов.Партнер КАК ОбъектРасчетовПартнер,
	|	ТаблицаРасшифровкаПлатежа.ОбъектРасчетов.Договор КАК ОбъектРасчетовДоговор,
	|	ТаблицаРасшифровкаПлатежа.ОбъектРасчетов.НалогообложениеНДС КАК ОбъектРасчетовНалогообложениеНДС,
	|	ТаблицаРасшифровкаПлатежа.ОбъектРасчетов.Подразделение КАК ОбъектРасчетовПодразделение,
	|	ТаблицаРасшифровкаПлатежа.ОбъектРасчетов.НаправлениеДеятельности КАК ОбъектРасчетовНаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА ТаблицаРасшифровкаПлатежа.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации)
	|			ТОГДА
	|				 ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|		ИНАЧЕ 
	|			ТаблицаРасшифровкаПлатежа.Партнер
	|	КОНЕЦ КАК ОбъектРасчетовПлатежаПартнер,
	|	ТаблицаРасшифровкаПлатежа.ДоговорКредитаДепозита.Подразделение КАК ДоговорКредитаДепозитаПодразделение
	|ПОМЕСТИТЬ ДанныеДокументаТаблицаРасшифровкаПлатежа
	|ИЗ
	|	Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
	|ГДЕ
	|	ТаблицаРасшифровкаПлатежа.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)
	
	Значения = Новый Структура;
	Значения.Вставить("ИдентификаторМетаданных",                         ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ПоступлениеБезналичныхДенежныхСредств"));
	Значения.Вставить("ИспользоватьСчетаНаОплатуКлиентам",               ПолучитьФункциональнуюОпцию("ИспользоватьСчетаНаОплатуКлиентам"));
	Значения.Вставить("ИспользоватьУчетПрочихДоходовРасходов",           ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов"));
	Значения.Вставить("КонтролироватьВыдачуПодОтчетВРазрезеЦелей",       ПолучитьФункциональнуюОпцию("КонтролироватьВыдачуПодОтчетВРазрезеЦелей"));
	Значения.Вставить("ИдентификаторНеиспользуемойФинЗаписи",            ПроведениеДокументов.ИдентификаторНеиспользуемойФинЗаписи());
	
	Значения.Вставить("ВалютаУправленческогоУчета",                      Константы.ВалютаУправленческогоУчета.Получить());
	
	Если Реквизиты <> Неопределено Тогда
		
		Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(
			Реквизиты.Валюта,
			Неопределено,
			Реквизиты.Период,
			Реквизиты.Организация);
			
		КоэффициентыКонвертации = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(
			Реквизиты.ВалютаКонвертации,
			Неопределено,
			Реквизиты.Период,
			Реквизиты.Организация);
		
		ВалютаРегламентированногоУчета = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Реквизиты.Организация);
		КоэффициентПересчетаВВалютуКонвертации = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентПересчетаИзВалютыВВалюту(
			Реквизиты.Валюта,
			Реквизиты.ВалютаКонвертации,
			Реквизиты.Период,
			ВалютаРегламентированногоУчета);
			
		Значения.Вставить("КоэффициентПересчетаВВалютуУпр",              Коэффициенты.КоэффициентПересчетаВВалютуУпр);
		Значения.Вставить("КоэффициентПересчетаВВалютуРегл",             Коэффициенты.КоэффициентПересчетаВВалютуРегл);
		Значения.Вставить("КоэффициентПересчетаКонвертацииВВалютуУпр",   КоэффициентыКонвертации.КоэффициентПересчетаВВалютуУпр);
		Значения.Вставить("КоэффициентПересчетаКонвертацииВВалютуРегл",  КоэффициентыКонвертации.КоэффициентПересчетаВВалютуРегл);
		Значения.Вставить("КоэффициентПересчетаВВалютуКонвертации",      КоэффициентПересчетаВВалютуКонвертации);
		
		Значения.Вставить("НомерНаПечать",                               ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
		Значения.Вставить("ВалютаРегламентированногоУчета", ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Реквизиты.Организация));
		
	КонецЕсли;
	
	Возврат Значения;
	
КонецФункции

Процедура ИнициализироватьКлючиАналитикиУчетаПоПартнерам(Запрос, ДокументОбъект = Неопределено)
	
	Если Запрос.Параметры.Свойство("КлючиАналитикиУчетаПартнеровИнициализированы") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросПоАналитикам = Новый Запрос;
	ЗапросПоАналитикам.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	
	ЗапросПоАналитикам.Текст ="";
	Если НЕ (ЗапросПоАналитикам.МенеджерВременныхТаблиц <> Неопределено 
		И ЗапросПоАналитикам.МенеджерВременныхТаблиц.Таблицы.Найти("ВтТаблицаОбъектовРасчетов") <> Неопределено) Тогда
		
		ЗапросПоАналитикам.Текст = ТекстЗапросаВременнаяТаблицаОбъектовРасчета(ЗапросПоАналитикам, Новый СписокЗначений, ДокументОбъект);
		ЗапросПоАналитикам.Текст = ЗапросПоАналитикам.Текст + "
		|;
		|/////////////////////////////////////////////////////////////////////////////
		|";
		
	КонецЕсли;
	
	ЗапросПоАналитикам.Текст = СтрЗаменить(ЗапросПоАналитикам.Текст, "ПОМЕСТИТЬ ТаблицаОбъектовРасчетов", "ПОМЕСТИТЬ ВтТаблицаОбъектовРасчетов");
	
	ЗапросПоАналитикам.Текст = ЗапросПоАналитикам.Текст + "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОбъектовРасчетов.Организация КАК Организация,
	|	ТаблицаОбъектовРасчетов.Договор КАК Договор,
	|	&Контрагент КАК Контрагент,
	|	ТаблицаРасшифровкаПлатежа.Партнер КАК Партнер,
	|	ТаблицаОбъектовРасчетов.НаправлениеДеятельности КАК НаправлениеДеятельности 
	|ИЗ
	|	ДанныеДокументаТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТаблицаОбъектовРасчетов КАК ТаблицаОбъектовРасчетов
	|			ПО ТаблицаРасшифровкаПлатежа.ОбъектРасчетов = ТаблицаОбъектовРасчетов.ОбъектРасчетов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО
	|		ТаблицаОбъектовРасчетов.Организация = Аналитика.Организация
	|		И &Контрагент = Аналитика.Контрагент
	|		И ТаблицаРасшифровкаПлатежа.Партнер = Аналитика.Партнер
	|		И ТаблицаОбъектовРасчетов.Договор = Аналитика.Договор
	|		И ТаблицаОбъектовРасчетов.НаправлениеДеятельности = Аналитика.НаправлениеДеятельности
	|ГДЕ
	|	ТаблицаРасшифровкаПлатежа.Ссылка = &Ссылка
	|	И Аналитика.КлючАналитики ЕСТЬ NULL
	|	И &ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика)
	|		)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка),
	|	&Контрагент КАК Контрагент,
	|	ТаблицаРасшифровкаПлатежа.Партнер КАК Партнер,
	|	ДанныеДоговора.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	ДанныеДокументаТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКредитовИДепозитов КАК ДанныеДоговора
	|		ПО ТаблицаРасшифровкаПлатежа.ДоговорКредитаДепозита = ДанныеДоговора.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО
	|		&Организация = Аналитика.Организация
	|		И &Контрагент = Аналитика.Контрагент
	|		И ТаблицаРасшифровкаПлатежа.Партнер = Аналитика.Партнер
	|		И ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) = Аналитика.Договор
	|		И ДанныеДоговора.НаправлениеДеятельности = Аналитика.НаправлениеДеятельности
	|ГДЕ
	|	ТаблицаРасшифровкаПлатежа.Ссылка = &Ссылка
	|	И Аналитика.КлючАналитики ЕСТЬ NULL
	|	И &ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным)
	|		)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОбъектовРасчетов.Организация КАК Организация,
	|	ТаблицаОбъектовРасчетов.Договор КАК Договор,
	|	&Контрагент КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
	|	ТаблицаОбъектовРасчетов.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	ДанныеДокументаТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТаблицаОбъектовРасчетов КАК ТаблицаОбъектовРасчетов
	|		ПО ТаблицаРасшифровкаПлатежа.ОбъектРасчетов = ТаблицаОбъектовРасчетов.ОбъектРасчетов
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО
	|		ТаблицаОбъектовРасчетов.Организация = Аналитика.Организация
	|		И &Контрагент = Аналитика.Контрагент
	|		И ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) = Аналитика.Партнер
	|		И ТаблицаОбъектовРасчетов.Договор = Аналитика.Договор
	|		И ТаблицаОбъектовРасчетов.НаправлениеДеятельности = Аналитика.НаправлениеДеятельности
	|ГДЕ
	|	ТаблицаРасшифровкаПлатежа.Ссылка = &Ссылка
	|	И Аналитика.КлючАналитики ЕСТЬ NULL
	|	И &ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации)
	|		)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Организация КАК Организация,
	|	&Договор КАК Договор,
	|	&Контрагент КАК Контрагент,
	|	&Партнер КАК Партнер,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	ДанныеДокументаШапка КАК ТаблицаПлатеж
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО
	|		&Организация= Аналитика.Организация
	|		И &Контрагент = Аналитика.Контрагент
	|		И &Партнер = Аналитика.Партнер
	|		И &Договор = Аналитика.Договор
	|		И &НаправлениеДеятельности = Аналитика.НаправлениеДеятельности
	|ГДЕ
	|	ТаблицаПлатеж.Ссылка = &Ссылка
	|	И Аналитика.КлючАналитики ЕСТЬ NULL
	|	И &ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации)
	|		)";
	
	Если ДокументОбъект = Неопределено Тогда
		ТекстЗапросаПоАналитикам = СтрЗаменить(ТекстЗапросаПоАналитикам, "ДанныеДокументаТаблицаРасшифровкаПлатежа", "Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа");
		ТекстЗапросаПоАналитикам = СтрЗаменить(ТекстЗапросаПоАналитикам, "ДанныеДокументаШапка", "Документ.ПоступлениеБезналичныхДенежныхСредств");
	КонецЕсли;
	
	ЗапросПоАналитикам.Текст = ЗапросПоАналитикам.Текст + ТекстЗапросаПоАналитикам;
	
	ЗапросПоАналитикам.УстановитьПараметр("Ссылка",                  Запрос.Параметры.Ссылка);
	ЗапросПоАналитикам.УстановитьПараметр("Организация",             Запрос.Параметры.Организация);
	ЗапросПоАналитикам.УстановитьПараметр("Контрагент",              Запрос.Параметры.Контрагент);
	ЗапросПоАналитикам.УстановитьПараметр("Партнер",                 Запрос.Параметры.Партнер);
	ЗапросПоАналитикам.УстановитьПараметр("НаправлениеДеятельности", Запрос.Параметры.НаправлениеДеятельности);
	ЗапросПоАналитикам.УстановитьПараметр("ХозяйственнаяОперация",   Запрос.Параметры.ХозяйственнаяОперация);
	
	Если ЗначениеЗаполнено(Запрос.Параметры.Договор) Тогда
		ЗапросПоАналитикам.УстановитьПараметр("Договор",                         Запрос.Параметры.Договор);
	ИначеЕсли НЕ ЗначениеЗаполнено(Запрос.Параметры.Договор)
			И Запрос.Параметры.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации") Тогда
		ЗапросПоАналитикам.УстановитьПараметр("Договор",                         Справочники.ДоговорыМеждуОрганизациями.ПустаяСсылка());
	Иначе
		ЗапросПоАналитикам.УстановитьПараметр("Договор",                         Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
	КонецЕсли;
	
	Выборка = ЗапросПоАналитикам.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаПоПартнерам.СоздатьКлючАналитики(Выборка);
	КонецЦикла;
	
	Если ВзаиморасчетыСервер.ХозяйственнаяОперацияВозвратДенежныхСредств(Запрос.Параметры.ХозяйственнаяОперация) Тогда
		АналитикаУчетаПоПартнерам = РегистрыСведений.АналитикаУчетаПоПартнерам.ЗначениеКлючаАналитики(ЗапросПоАналитикам.Параметры);
	Иначе
		АналитикаУчетаПоПартнерам = Неопределено;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("АналитикаУчетаПоПартнерам", АналитикаУчетаПоПартнерам);
	Запрос.УстановитьПараметр("КлючиАналитикиУчетаПартнеровИнициализированы", Истина);
	
КонецПроцедуры

Функция ТекстЗапросаВременнаяТаблицаОбъектовРасчета(Запрос, ТекстыЗапроса, ДокументОбъект = Неопределено) Экспорт
	
	ИмяВременнойТаблицы = "ТаблицаОбъектовРасчетов";
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК ОбъектРасчетов,
	|
	|	ТаблицаРасшифровкаПлатежа.ОбъектРасчетовОрганизация КАК Организация,
	|	ТаблицаРасшифровкаПлатежа.ОбъектРасчетовДоговор КАК Договор,
	|	ТаблицаРасшифровкаПлатежа.ОбъектРасчетовНалогообложениеНДС КАК НалогообложениеНДС,
	|	ТаблицаРасшифровкаПлатежа.ОбъектРасчетовПодразделение КАК Подразделение,
	|	ВЫБОР КОГДА &ХозяйственнаяОперация В (
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации))
	|				И ЕСТЬNULL(Направления.УчетРасчетовСПоставщиками, ЛОЖЬ)
	|			ИЛИ НЕ &ХозяйственнаяОперация В (
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации))
	|				И ЕСТЬNULL(Направления.УчетДоходов, ЛОЖЬ)
	|		ТОГДА
	|			ТаблицаРасшифровкаПлатежа.ОбъектРасчетовНаправлениеДеятельности
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ КАК НаправлениеДеятельности
	|
	|ПОМЕСТИТЬ ТаблицаОбъектовРасчетов
	|ИЗ
	|	ДанныеДокументаТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НаправленияДеятельности КАК Направления
	|	ПО ТаблицаРасшифровкаПлатежа.ОбъектРасчетовНаправлениеДеятельности = Направления.Ссылка
	|	
	|ГДЕ
	|	ТаблицаРасшифровкаПлатежа.Ссылка = &Ссылка
	|	И ТаблицаРасшифровкаПлатежа.ОбъектРасчетов <> ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|	И НЕ &ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным)
	|		)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов
	|";
	
	Если ДокументОбъект = Неопределено Тогда
		 ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеДокументаТаблицаРасшифровкаПлатежа", "Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа");
		 ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаРасшифровкаПлатежа.ОбъектРасчетовОрганизация", "ТаблицаРасшифровкаПлатежа.ОбъектРасчетов.Организация");
		 ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаРасшифровкаПлатежа.ОбъектРасчетовДоговор", "ТаблицаРасшифровкаПлатежа.ОбъектРасчетов.Договор");
		 ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаРасшифровкаПлатежа.ОбъектРасчетовНалогообложениеНДС", "ТаблицаРасшифровкаПлатежа.ОбъектРасчетов.НалогообложениеНДС");
		 ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаРасшифровкаПлатежа.ОбъектРасчетовПодразделение", "ТаблицаРасшифровкаПлатежа.ОбъектРасчетов.Подразделение");
		 ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаРасшифровкаПлатежа.ОбъектРасчетовНаправлениеДеятельности", "ТаблицаРасшифровкаПлатежа.ОбъектРасчетов.НаправлениеДеятельности");
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяВременнойТаблицы);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВременнаяТаблицаРасшифровкаПлатежа(Запрос, ТекстыЗапроса, ДокументОбъект = Неопределено) Экспорт
	
	ИмяВременнойТаблицы = "ТаблицаРасшифровкаПлатежа";
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаРасшифровкаПлатежаОбщая", ТекстыЗапроса) Тогда
		ТекстЗапросаВременнаяТаблицаРасшифровкаПлатежаОбщая(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаРасшифровкаПлатежа.НомерСтроки КАК НомерСтроки,
	|	ЕСТЬNULL(Аналитика.Организация, Неопределено) КАК Организация,
	|	ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ТаблицаРасшифровкаПлатежа.Договор КАК Договор,
	|	ТаблицаРасшифровкаПлатежа.СчетНаОплату КАК СчетНаОплату,
	|
	|	ТаблицаРасшифровкаПлатежа.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаРасшифровкаПлатежа.СтатьяДоходов КАК СтатьяДоходов,
	|	ТаблицаРасшифровкаПлатежа.АналитикаДоходов КАК АналитикаДоходов,
	|	ТаблицаРасшифровкаПлатежа.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	ТаблицаРасшифровкаПлатежа.Подразделение КАК Подразделение,
	|	ТаблицаРасшифровкаПлатежа.ПодразделениеЗаказа КАК ПодразделениеЗаказа,
	|
	|	ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ТаблицаРасшифровкаПлатежа.СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорКредитаДепозита,
	|	НЕОПРЕДЕЛЕНО КАК ТипСуммыКредитаДепозита,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорАренды,
	|	НЕОПРЕДЕЛЕНО КАК ТипПлатежаПоАренде,
	|	НЕОПРЕДЕЛЕНО КАК РасчетныйДокументПоАренде,
	|
	|	ТаблицаРасшифровкаПлатежа.Сумма КАК Сумма,
	|	ТаблицаРасшифровкаПлатежа.СуммаУпр КАК СуммаУпр,
	|	ТаблицаРасшифровкаПлатежа.СуммаРегл КАК СуммаРегл,
	|	ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов,
	|	ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов,
	|	
	|	ТаблицаРасшифровкаПлатежа.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаРасшифровкаПлатежа.СуммаНДС КАК СуммаНДС,
	|	ТаблицаРасшифровкаПлатежа.СуммаНДСУпр КАК СуммаНДСУпр,
	|	ТаблицаРасшифровкаПлатежа.СуммаНДСРегл КАК СуммаНДСРегл,
	|
	|	ТаблицаРасшифровкаПлатежа.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	
	|	ТаблицаРасшифровкаПлатежа.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаРасшифровкаПлатежа.ИдентификаторДокумента КАК ИдентификаторДокумента
	|
	|ПОМЕСТИТЬ ТаблицаРасшифровкаПлатежа
	|ИЗ
	|	ТаблицаРасшифровкаПлатежаОбщая КАК ТаблицаРасшифровкаПлатежа
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
	|	ПО
	|		ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам = Аналитика.Ссылка
	|ГДЕ
	|	НЕ &ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтАрендодателя)
	|		)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаРасшифровкаПлатежа.НомерСтроки КАК НомерСтроки,
	|	ТаблицаРасшифровкаПлатежа.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ОбъектРасчетов,
	|	НЕОПРЕДЕЛЕНО КАК Договор,
	|	НЕОПРЕДЕЛЕНО КАК СчетНаОплату,
	|
	|	ДанныеДоговора.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяДоходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	ДанныеДоговора.Подразделение КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеЗаказа,
	|
	|	ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ТаблицаРасшифровкаПлатежа.СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
	|	ТаблицаРасшифровкаПлатежа.ДоговорКредитаДепозита КАК ДоговорКредитаДепозита,
	|	ТаблицаРасшифровкаПлатежа.ТипСуммыКредитаДепозита КАК ТипСуммыКредитаДепозита,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорАренды,
	|	НЕОПРЕДЕЛЕНО КАК ТипПлатежаПоАренде,
	|	НЕОПРЕДЕЛЕНО КАК РасчетныйДокументПоАренде,
	|
	|	ТаблицаРасшифровкаПлатежа.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА &Валюта = &ВалютаУправленческогоУчета
	|			ТОГДА ТаблицаРасшифровкаПлатежа.Сумма
	|		КОГДА ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаРасшифровкаПлатежа.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаУпр,
	|	ВЫБОР
	|		КОГДА &Валюта = &ВалютаРегламентированногоУчета
	|			ТОГДА ТаблицаРасшифровкаПлатежа.Сумма
	|		КОГДА ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|			ТОГДА ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаРасшифровкаПлатежа.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаРегл,
	|	ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов,
	|	ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
	|	0 КАК СуммаНДС,
	|	0 КАК СуммаНДСУпр,
	|	0 КАК СуммаНДСРегл,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
	|	ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	
	|	ТаблицаРасшифровкаПлатежа.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаРасшифровкаПлатежа.ИдентификаторДокумента КАК ИдентификаторДокумента
	|
	|ИЗ
	|	ТаблицаРасшифровкаПлатежаОбщая КАК ТаблицаРасшифровкаПлатежа
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.ДоговорыКредитовИДепозитов КАК ДанныеДоговора
	|	ПО
	|		ТаблицаРасшифровкаПлатежа.ДоговорКредитаДепозита = ДанныеДоговора.Ссылка
	|ГДЕ
	|	&ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным)
	|		)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяВременнойТаблицы);
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВременнаяТаблицаРасшифровкаПлатежаОбщая(Запрос, ТекстыЗапроса, ДокументОбъект = Неопределено) Экспорт
	
	ИмяВременнойТаблицы = "ТаблицаРасшифровкаПлатежаОбщая";
	
	ИнициализироватьКлючиАналитикиУчетаПоПартнерам(Запрос, ДокументОбъект);
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаОбъектовРасчетов", ТекстыЗапроса) Тогда
		ТекстЗапросаВременнаяТаблицаОбъектовРасчета(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаРасшифровкаПлатежа.НомерСтроки КАК НомерСтроки,
	|	Аналитика.Организация КАК Организация,
	|	ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ЕСТЬNULL(ТаблицаОбъектовРасчетов.Договор, Неопределено) КАК Договор,
	|	ВЫРАЗИТЬ(ТаблицаРасшифровкаПлатежа.ОснованиеПлатежа КАК Документ.СчетНаОплатуКлиенту) КАК СчетНаОплату,
	|
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств)
	|			ТОГДА ТаблицаРасшифровкаПлатежа.НаправлениеДеятельности
	|		ИНАЧЕ ЕСТЬNULL(ТаблицаОбъектовРасчетов.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ТаблицаРасшифровкаПлатежа.СтатьяДоходов КАК СтатьяДоходов,
	|	ТаблицаРасшифровкаПлатежа.АналитикаДоходов КАК АналитикаДоходов,
	|	ТаблицаРасшифровкаПлатежа.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств)
	|			ТОГДА ТаблицаРасшифровкаПлатежа.Подразделение
	|	ИНАЧЕ
	|		ТаблицаРасшифровкаПлатежа.Подразделение
	|	КОНЕЦ КАК Подразделение,
	|	ЕСТЬNULL(ТаблицаОбъектовРасчетов.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК ПодразделениеЗаказа,
	|
	|	ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ТаблицаРасшифровкаПлатежа.СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
	|	ТаблицаРасшифровкаПлатежа.ДоговорКредитаДепозита КАК ДоговорКредитаДепозита,
	|	ТаблицаРасшифровкаПлатежа.ТипСуммыКредитаДепозита КАК ТипСуммыКредитаДепозита,
	|	ТаблицаРасшифровкаПлатежа.ДоговорАренды КАК ДоговорАренды,
	|	ТаблицаРасшифровкаПлатежа.ТипПлатежаПоАренде КАК ТипПлатежаПоАренде,
	|	ТаблицаРасшифровкаПлатежа.РасчетныйДокументПоАренде КАК РасчетныйДокументПоАренде,
	|
	|	ТаблицаРасшифровкаПлатежа.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА &Валюта = &ВалютаУправленческогоУчета
	|			ТОГДА ТаблицаРасшифровкаПлатежа.Сумма
	|		КОГДА ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаРасшифровкаПлатежа.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаУпр,
	|	ВЫБОР
	|		КОГДА &Валюта = &ВалютаРегламентированногоУчета
	|			ТОГДА ТаблицаРасшифровкаПлатежа.Сумма
	|		КОГДА ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|			ТОГДА ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаРасшифровкаПлатежа.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаРегл,
	|	ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов,
	|	ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов,
	|	
	|	ТаблицаРасшифровкаПлатежа.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаРасшифровкаПлатежа.СуммаНДС КАК СуммаНДС,
	|	ВЫБОР КОГДА &Валюта = &ВалютаУправленческогоУчета
	|		ТОГДА ТаблицаРасшифровкаПлатежа.СуммаНДС
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаРасшифровкаПлатежа.СуммаНДС * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаНДСУпр,
	|	ВЫБОР КОГДА &Валюта = &ВалютаРегламентированногоУчета
	|		ТОГДА ТаблицаРасшифровкаПлатежа.СуммаНДС
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаРасшифровкаПлатежа.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаНДСРегл,
	|
	|	ЕСТЬNULL(ТаблицаОбъектовРасчетов.НалогообложениеНДС, ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)) КАК НалогообложениеНДС,
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации))
	|		ТОГДА ЕСТЬNULL(АналитикаИнтеркампани.КлючАналитики, Неопределено)
	|		КОГДА &ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным))
	|		ТОГДА ЕСТЬNULL(АналитикаКредитовДепозитов.КлючАналитики, Неопределено)
	|		ИНАЧЕ ЕСТЬNULL(Аналитика.КлючАналитики, Неопределено)
	|	КОНЕЦ КАК АналитикаУчетаПоПартнерам,
	|	
	|	ТаблицаРасшифровкаПлатежа.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ДокументПоступление.ИдентификаторДокумента    КАК ИдентификаторДокумента
	|
	|ПОМЕСТИТЬ ТаблицаРасшифровкаПлатежаОбщая
	|ИЗ
	|	ДанныеДокументаТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОбъектовРасчетов КАК ТаблицаОбъектовРасчетов
	|	ПО ТаблицаРасшифровкаПлатежа.ОбъектРасчетов = ТаблицаОбъектовРасчетов.ОбъектРасчетов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументаШапка КАК ДокументПоступление
	|	ПО ТаблицаРасшифровкаПлатежа.Ссылка = ДокументПоступление.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаИнтеркампани
	|	ПО ТаблицаОбъектовРасчетов.Организация = АналитикаИнтеркампани.Организация
	|		И &Контрагент = АналитикаИнтеркампани.Контрагент
	|		И ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) = АналитикаИнтеркампани.Партнер
	|		И ТаблицаОбъектовРасчетов.Договор = АналитикаИнтеркампани.Договор
	|		И ТаблицаОбъектовРасчетов.НаправлениеДеятельности = АналитикаИнтеркампани.НаправлениеДеятельности
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО ТаблицаОбъектовРасчетов.Организация = Аналитика.Организация
	|		И &Контрагент = Аналитика.Контрагент
	|		И ВЫБОР
	|			КОГДА &ЕстьПодарочныйСертификат = ИСТИНА
	|			ТОГДА &Партнер
	|			КОГДА ТаблицаРасшифровкаПлатежа.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|			ТОГДА ТаблицаРасшифровкаПлатежа.Партнер
	|			ИНАЧЕ ДокументПоступление.КонтрагентПартнер
	|		КОНЕЦ = Аналитика.Партнер
	|		И ТаблицаОбъектовРасчетов.Договор = Аналитика.Договор
	|		И ТаблицаОбъектовРасчетов.НаправлениеДеятельности = Аналитика.НаправлениеДеятельности
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаКредитовДепозитов
	|	ПО &Организация = АналитикаКредитовДепозитов.Организация
	|		И &Контрагент = АналитикаКредитовДепозитов.Контрагент
	|		И ТаблицаРасшифровкаПлатежа.Партнер = АналитикаКредитовДепозитов.Партнер
	|		И ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) = АналитикаКредитовДепозитов.Договор
	|		И ТаблицаРасшифровкаПлатежа.ДоговорКредитаДепозита.НаправлениеДеятельности = АналитикаКредитовДепозитов.НаправлениеДеятельности
	|		И &ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным))
	|	
	|ГДЕ
	|	ТаблицаРасшифровкаПлатежа.Ссылка = &Ссылка
	|";
	
	Если ДокументОбъект = Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеДокументаТаблицаРасшифровкаПлатежа", "Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеДокументаШапка", "Документ.ПоступлениеБезналичныхДенежныхСредств");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДокументПоступление.КонтрагентПартнер", "ДокументПоступление.Контрагент.Партнер");
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяВременнойТаблицы);
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДенежныеСредстваБезналичные(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)
	
	ИмяРегистра = "ДенежныеСредстваБезналичные";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаРасшифровкаПлатежа", ТекстыЗапроса) Тогда
		ТекстЗапросаВременнаяТаблицаРасшифровкаПлатежа(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли;
	
	МассивТекстовЗапросов = Новый Массив;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПогашениеЗаймаСотрудником Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                      КАК ВидДвижения,
		|	&Период                                                                     КАК Период,
		|	
		|	ДанныеДокумента.Организация                                                 КАК Организация,
		|	ДанныеДокумента.БанковскийСчет                                              КАК БанковскийСчет,
		|	
		|	ВЫБОР КОГДА ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ТаблицаРасшифровкаПлатежа.Сумма
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК Сумма,
		|	
		|	ВЫБОР КОГДА ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ТаблицаРасшифровкаПлатежа.СуммаУпр
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК СуммаУпр,
		|	
		|	ВЫБОР КОГДА ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ТаблицаРасшифровкаПлатежа.СуммаРегл
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК СуммаРегл,
		|	
		|	ВЫБОР КОГДА НЕ ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ТаблицаРасшифровкаПлатежа.Сумма
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК КЗачислению,
		|	
		|	&ХозяйственнаяОперация                                     КАК ХозяйственнаяОперация,
		|	ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств                     КАК СтатьяДвиженияДенежныхСредств,
		|	ТаблицаРасшифровкаПлатежа.СтатьяЦелевыхСредств                              КАК СтатьяЦелевыхСредств,
		|	ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам                         КАК АналитикаУчетаПоПартнерам,
		|	ТаблицаРасшифровкаПлатежа.ОбъектРасчетов                                    КАК ОбъектРасчетов,
		|	ДанныеДокумента.Ссылка                                                      КАК ПлатежныйДокумент,
		|	
		|	ВЫБОР КОГДА ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ТаблицаРасшифровкаПлатежа.ИдентификаторСтроки
		|	ИНАЧЕ
		|		&ИдентификаторНеиспользуемойФинЗаписи
		|	КОНЕЦ КАК ИдентификаторФинЗаписи,
		|	ВЫБОР КОГДА ДанныеДокумента.ПроведеноБанком ТОГДА
		|		 &НастройкаХозяйственнойОперации
		|	ИНАЧЕ
		|		НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК НастройкаХозяйственнойОперации
		|	
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|	ПО
		|		ИСТИНА
		|";
		
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНалогов Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                      КАК ВидДвижения,
		|	&Период                                                                     КАК Период,
		|	
		|	ДанныеДокумента.Организация                                                 КАК Организация,
		|	ДанныеДокумента.БанковскийСчет                                              КАК БанковскийСчет,
		|	
		|	ВЫБОР КОГДА ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ТаблицаРасшифровкаПлатежа.Сумма
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК Сумма,
		|	
		|	ВЫБОР КОГДА ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ВЫРАЗИТЬ(ТаблицаРасшифровкаПлатежа.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК СуммаУпр,
		|	
		|	ВЫБОР КОГДА ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ВЫРАЗИТЬ(ТаблицаРасшифровкаПлатежа.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК СуммаРегл,
		|	
		|	ВЫБОР КОГДА НЕ ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ТаблицаРасшифровкаПлатежа.Сумма
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК КЗачислению,
		|	
		|	&ХозяйственнаяОперация                                                      КАК ХозяйственнаяОперация,
		|	ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств                     КАК СтатьяДвиженияДенежныхСредств,
		|	ТаблицаРасшифровкаПлатежа.СтатьяЦелевыхСредств                              КАК СтатьяЦелевыхСредств,
		|	Неопределено                                                                КАК АналитикаУчетаПоПартнерам,
		|	Неопределено                                                                КАК ОбъектРасчетов,
		|	ДанныеДокумента.Ссылка                                                      КАК ПлатежныйДокумент,
		|	
		|	ВЫБОР КОГДА ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ТаблицаРасшифровкаПлатежа.ИдентификаторСтроки
		|	ИНАЧЕ
		|		&ИдентификаторНеиспользуемойФинЗаписи
		|	КОНЕЦ КАК ИдентификаторФинЗаписи,
		|	ВЫБОР КОГДА ДанныеДокумента.ПроведеноБанком ТОГДА
		|		&НастройкаХозяйственнойОперации
		|	ИНАЧЕ
		|		НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК НастройкаХозяйственнойОперации
		|	
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|	ПО
		|		ИСТИНА
		|";
		
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствСДругогоСчета
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленныхДС
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленнойЗарплатыПоЗарплатномуПроекту
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                      КАК ВидДвижения,
		|	&Период                                                                     КАК Период,
		|	
		|	ДанныеДокумента.Организация                                                 КАК Организация,
		|	ДанныеДокумента.БанковскийСчет                                              КАК БанковскийСчет,
		|	
		|	ВЫБОР КОГДА ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ДанныеДокумента.СуммаДокумента
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК Сумма,
		|	
		|	ВЫБОР КОГДА ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК СуммаУпр,
		|	
		|	ВЫБОР КОГДА ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК СуммаРегл,
		|	
		|	ВЫБОР КОГДА НЕ ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ДанныеДокумента.СуммаДокумента
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК КЗачислению,
		|	
		|	ВЫБОР КОГДА ЕСТЬNULL(СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация,
		|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)) = 
		|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику)
		|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника)
		|	ИНАЧЕ
		|		&ХозяйственнаяОперация
		|	КОНЕЦ КАК ХозяйственнаяОперация,
		|	&СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	&СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
		|	Неопределено                   КАК АналитикаУчетаПоПартнерам,
		|	Неопределено                   КАК ОбъектРасчетов,
		
		|	&Ссылка КАК ПлатежныйДокумент,
		|	
		|	ВЫБОР КОГДА ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ДанныеДокумента.ИдентификаторДокумента
		|	ИНАЧЕ
		|		&ИдентификаторНеиспользуемойФинЗаписи
		|	КОНЕЦ КАК ИдентификаторФинЗаписи,
		|	ВЫБОР КОГДА ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ВЫБОР КОГДА ЕСТЬNULL(СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация,
		|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)) =
		|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику)
		|			ТОГДА ЕСТЬNULL(НастройкиХозяйственныхОпераций.Ссылка, &НастройкаХозяйственнойОперации)
		|		ИНАЧЕ
		|			&НастройкаХозяйственнойОперации
		|		КОНЕЦ
		|	ИНАЧЕ
		|		НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК НастройкаХозяйственнойОперации
		|	
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиХозяйственныхОпераций КАК НастройкиХозяйственныхОпераций
		|		ПО НастройкиХозяйственныхОпераций.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
		|		ПО ДанныеДокумента.ДокументВыдачи = СписаниеБезналичныхДенежныхСредств.Ссылка";
		
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты Тогда
	
		ТекстЗапроса = "	
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                      КАК ВидДвижения,
		|	&Период                                                                     КАК Период,
		|	
		|	ДанныеДокумента.Организация                                                 КАК Организация,
		|	ДанныеДокумента.БанковскийСчет                                              КАК БанковскийСчет,
		|	
		|	ВЫБОР КОГДА ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ДанныеДокумента.СуммаДокумента
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК Сумма,
		|	
		|	ВЫБОР КОГДА ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК СуммаУпр,
		|	ВЫБОР КОГДА ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК СуммаРегл,
		|	
		|	ВЫБОР КОГДА НЕ ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ДанныеДокумента.СуммаДокумента
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК КЗачислению,
		|	
		|	Значение(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствОтКонвертацииВалюты) КАК ХозяйственнаяОперация,
		|	&СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	&СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
		|	Неопределено                   КАК АналитикаУчетаПоПартнерам,
		|	Неопределено                   КАК ОбъектРасчетов,
		|	&Ссылка КАК ПлатежныйДокумент,
		|	
		|	ВЫБОР КОГДА ДанныеДокумента.ПроведеноБанком ТОГДА
		|		ДанныеДокумента.ИдентификаторДокумента
		|	ИНАЧЕ
		|		&ИдентификаторНеиспользуемойФинЗаписи
		|	КОНЕЦ КАК ИдентификаторФинЗаписи,
		|	ВЫБОР КОГДА ДанныеДокумента.ПроведеноБанком ТОГДА
		|		&НастройкаХозяйственнойОперации
		|	ИНАЧЕ
		|		НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК НастройкаХозяйственнойОперации
		|	
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента";
		
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если МассивТекстовЗапросов.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДенежныеСредстваВПути(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)
	
	ИмяРегистра = "ДенежныеСредстваВПути";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаКурсовыхРазниц", ТекстыЗапроса) 
		И ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты Тогда
		ТекстЗапросаТаблицаКурсовыхРазниц(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли;
	
	МассивТекстовЗапросов = Новый Массив;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте
		И ДокументОбъект.ПроведеноБанком Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                              КАК ВидДвижения,
		|	&Период                                                                             КАК Период,
		|	
		|	ДанныеДокумента.Организация                                                         КАК Организация,
		|	ДанныеДокумента.БанковскийСчет                                                      КАК Получатель,
		|	НЕОПРЕДЕЛЕНО                                                                        КАК Отправитель,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПоступлениеОтБанкаПоЭквайрингу)  КАК ВидПереводаДенежныхСредств,
		|	ДанныеДокумента.Контрагент                                                          КАК Контрагент,
		|	ДанныеДокумента.ДоговорЭквайринга                                                   КАК Договор,
		|	ДанныеДокумента.Валюта                                                              КАК Валюта,
		|	
		|	ДанныеДокумента.СуммаДокумента                                                      КАК Сумма,
		|	ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр                    КАК СуммаУпр,
		|	ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл                   КАК СуммаРегл,
		|	
		|	&ХозяйственнаяОперация                                                              КАК ХозяйственнаяОперация,
		|	&СтатьяДвиженияДенежныхСредств                                                      КАК СтатьяДвиженияДенежныхСредств,
		|	
		|	ДанныеДокумента.ИдентификаторДокумента                                              КАК ИдентификаторФинЗаписи,
		|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПоступлениеОплатыПоПлатежнойКарте) КАК НастройкаХозяйственнойОперации
		|	
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|";
		
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте
		И ДокументОбъект.ПроведеноБанком И ДокументОбъект.СуммаКомиссии <> 0 И ДокументОбъект.ОтражатьКомиссию Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                               КАК ВидДвижения,
		|	ДанныеДокумента.Дата                                                                 КАК Период,
		|	
		|	ДанныеДокумента.Организация                                                          КАК Организация,
		|	ДанныеДокумента.БанковскийСчет                                                       КАК Получатель,
		|	НЕОПРЕДЕЛЕНО                                                                         КАК Отправитель,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПоступлениеОтБанкаПоЭквайрингу)   КАК ВидПереводаДенежныхСредств,
		|	ДанныеДокумента.Контрагент                                                           КАК Контрагент,
		|	ДанныеДокумента.ДоговорЭквайринга                                                    КАК Договор,
		|	ДанныеДокумента.Валюта                                                               КАК Валюта,
		|	
		|	ДанныеДокумента.СуммаКомиссии                                                               КАК Сумма,
		|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаКомиссии * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))   КАК СуммаУпр,
		|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаКомиссии * &КоэффициентПересчетаВВалютуРегл  КАК ЧИСЛО(31,2)) КАК СуммаРегл,
		|	
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КомиссияПоЭквайрингу)                    КАК ХозяйственнаяОперация,
		|	ДанныеДокумента.ДоговорЭквайринга.СтатьяДвиженияДенежныхСредствКомиссия              КАК СтатьяДвиженияДенежныхСредств,
		|	
		|	ДанныеДокумента.ИдентификаторДокумента                                               КАК ИдентификаторФинЗаписи,
		|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КомиссияПоЭквайрингу)             КАК НастройкаХозяйственнойОперации 
		|	
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|";
		
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствСДругогоСчета
		И ДокументОбъект.ПроведеноБанком Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                              КАК ВидДвижения,
		|	&Период                                                                             КАК Период,
		|	
		|	ДанныеДокумента.Организация                                                         КАК Организация,
		|	ДанныеДокумента.БанковскийСчет                                                      КАК Получатель,
		|	ДанныеДокумента.БанковскийСчетОтправитель                                           КАК Отправитель,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПеречислениеНаДругойСчет)        КАК ВидПереводаДенежныхСредств,
		|	НЕОПРЕДЕЛЕНО                                                                        КАК Контрагент,
		|	НЕОПРЕДЕЛЕНО                                                                        КАК Договор,
		|	ДанныеДокумента.Валюта                                                              КАК Валюта,
		|	
		|	ДанныеДокумента.СуммаДокумента                                                      КАК Сумма,
		|	ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр                    КАК СуммаУпр,
		|	ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл                   КАК СуммаРегл,
		|	
		|	&ХозяйственнаяОперация                                                              КАК ХозяйственнаяОперация,
		|	&СтатьяДвиженияДенежныхСредств                                                      КАК СтатьяДвиженияДенежныхСредств,
		|	
		|	ДанныеДокумента.ИдентификаторДокумента                                              КАК ИдентификаторФинЗаписи,
		|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПоступлениеДенежныхСредствСДругогоСчета) КАК НастройкаХозяйственнойОперации
		|	
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|";
		
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет
		И ДокументОбъект.ПроведеноБанком Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                              КАК ВидДвижения,
		|	&Период                                                                             КАК Период,
		|	
		|	ДанныеДокумента.Организация                                                         КАК Организация,
		|	ДанныеДокумента.БанковскийСчет                                                      КАК Получатель,
		|	ДанныеДокумента.КассаОтправитель                                                    КАК Отправитель,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ИнкассацияВБанк)                 КАК ВидПереводаДенежныхСредств,
		|	НЕОПРЕДЕЛЕНО                                                                        КАК Контрагент,
		|	НЕОПРЕДЕЛЕНО                                                                        КАК Договор,
		|	ДанныеДокумента.Валюта                                                              КАК Валюта,
		|	
		|	ДанныеДокумента.СуммаДокумента                                                      КАК Сумма,
		|	ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр                    КАК СуммаУпр,
		|	ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл                   КАК СуммаРегл,
		|	
		|	&ХозяйственнаяОперация                                                              КАК ХозяйственнаяОперация,
		|	&СтатьяДвиженияДенежныхСредств                                                      КАК СтатьяДвиженияДенежныхСредств,
		|	
		|	ДанныеДокумента.ИдентификаторДокумента                                              КАК ИдентификаторФинЗаписи,
		|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет) КАК НастройкаХозяйственнойОперации
		|	
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|";
		
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты
		И ДокументОбъект.ПроведеноБанком Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                          КАК ВидДвижения,
		|	&Период                                                                         КАК Период,
		|	
		|	ДанныеДокумента.Организация                                                     КАК Организация,
		|	ДанныеДокумента.БанковскийСчет                                                  КАК Получатель,
		|	НЕОПРЕДЕЛЕНО                                                                    КАК Отправитель,
		|	ВЫБОР КОГДА ДанныеДокумента.ВалютаКонвертации = &ВалютаРегламентированногоУчета
		|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПриобретениеВалюты)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.РеализацияВалюты)
		|	КОНЕЦ                                                                           КАК ВидПереводаДенежныхСредств,
		|	ДанныеДокумента.Контрагент                                                      КАК Контрагент,
		|	НЕОПРЕДЕЛЕНО                                                                    КАК Договор,
		|	ДанныеДокумента.ВалютаКонвертации                                               КАК Валюта,
		|	
		|	ДанныеДокумента.СуммаКонвертации                                                КАК Сумма,
		|	ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр                КАК СуммаУпр,
		|	ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл               КАК СуммаРегл,
		|	
		|	&ХозяйственнаяОперация                                                          КАК ХозяйственнаяОперация,
		|	&СтатьяДвиженияДенежныхСредств                                                  КАК СтатьяДвиженияДенежныхСредств,
		|	
		|	ДанныеДокумента.ИдентификаторДокумента                                          КАК ИдентификаторФинЗаписи,
		|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КонвертацияВалюты)           КАК НастройкаХозяйственнойОперации
		|	
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|// Положительные курсовые разницы
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                           КАК ВидДвижения,
		|	&Период                                                                          КАК Период,
		|	
		|	&Организация                                                                     КАК Организация,
		|	&БанковскийСчет                                                                  КАК Получатель,
		|	НЕОПРЕДЕЛЕНО                                                                     КАК Отправитель,
		|	ВЫБОР КОГДА &ВалютаКонвертации = &ВалютаРегламентированногоУчета
		|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПриобретениеВалюты)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.РеализацияВалюты)
		|	КОНЕЦ                                                                            КАК ВидПереводаДенежныхСредств,
		|	&Контрагент                                                                      КАК Контрагент,
		|	НЕОПРЕДЕЛЕНО                                                                     КАК Договор,
		|	&ВалютаКонвертации                                                               КАК Валюта,
		|	
		|	0                                                                                КАК Сумма,
		|	-ТаблицаКурсовыхРазниц.СуммаУпр                                                  КАК СуммаУпр,
		|	-ТаблицаКурсовыхРазниц.СуммаРегл                                                 КАК СуммаРегл,
		|	
		|	&ХозяйственнаяОперация                                                           КАК ХозяйственнаяОперация,
		|	&СтатьяДвиженияДенежныхСредств                                                   КАК СтатьяДвиженияДенежныхСредств,
		|	
		|	ТаблицаКурсовыхРазниц.ИдентификаторДокумента                                     КАК ИдентификаторФинЗаписи,
		|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КурсовыеРазницыДСУбыток)      КАК НастройкаХозяйственнойОперации
		|	
		|ИЗ
		|	ТаблицаКурсовыхРазниц КАК ТаблицаКурсовыхРазниц
		|	
		|ГДЕ
		|	(ТаблицаКурсовыхРазниц.СуммаУпр < 0 ИЛИ ТаблицаКурсовыхРазниц.СуммаРегл < 0)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|// Отрицательные курсовые разницы
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                          КАК ВидДвижения,
		|	&Период                                                                         КАК Период,
		|	
		|	&Организация                                                                    КАК Организация,
		|	&БанковскийСчет                                                                 КАК Получатель,
		|	НЕОПРЕДЕЛЕНО                                                                    КАК Отправитель,
		|	ВЫБОР КОГДА &ВалютаКонвертации = &ВалютаРегламентированногоУчета
		|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПриобретениеВалюты)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.РеализацияВалюты)
		|	КОНЕЦ                                                                           КАК ВидПереводаДенежныхСредств,
		|	&Контрагент                                                                     КАК Контрагент,
		|	НЕОПРЕДЕЛЕНО                                                                    КАК Договор,
		|	&ВалютаКонвертации                                                              КАК Валюта,
		|	
		|	0                                                                               КАК Сумма,
		|	ТаблицаКурсовыхРазниц.СуммаУпр                                                  КАК СуммаУпр,
		|	ТаблицаКурсовыхРазниц.СуммаРегл                                                 КАК СуммаРегл,
		|	
		|	&ХозяйственнаяОперация                                                          КАК ХозяйственнаяОперация,
		|	&СтатьяДвиженияДенежныхСредств                                                  КАК СтатьяДвиженияДенежныхСредств,
		|	
		|	ТаблицаКурсовыхРазниц.ИдентификаторДокумента                                    КАК ИдентификаторФинЗаписи,
		|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КурсовыеРазницыДСПрибыль)    КАК НастройкаХозяйственнойОперации
		|	
		|ИЗ
		|	ТаблицаКурсовыхРазниц КАК ТаблицаКурсовыхРазниц
		|	
		|ГДЕ
		|	(ТаблицаКурсовыхРазниц.СуммаУпр > 0 ИЛИ ТаблицаКурсовыхРазниц.СуммаРегл > 0)";
		
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленнойЗарплатыПоЗарплатномуПроекту
		И ДокументОбъект.ПроведеноБанком Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                          КАК ВидДвижения,
		|	&Период                                                                         КАК Период,
		|	
		|	ДанныеДокумента.Организация                                                     КАК Организация,
		|	НЕОПРЕДЕЛЕНО                                                                    КАК Получатель,
		|	ДанныеДокумента.БанковскийСчет                                                  КАК Отправитель,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПеречислениеФизическимЛицам) КАК ВидПереводаДенежныхСредств,
		|	ДанныеДокумента.Контрагент                                                      КАК Контрагент,
		|	НЕОПРЕДЕЛЕНО                                                                    КАК Договор,
		|	ДанныеДокумента.Валюта                                                          КАК Валюта,
		|	
		|	ДанныеДокумента.СуммаДокумента                                                  КАК Сумма,
		|	ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр                КАК СуммаУпр,
		|	ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл               КАК СуммаРегл,
		|	
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника) КАК ХозяйственнаяОперация,
		|	&СтатьяДвиженияДенежныхСредств                                                  КАК СтатьяДвиженияДенежныхСредств,
		|	
		|	ДанныеДокумента.ИдентификаторДокумента                                          КАК ИдентификаторФинЗаписи,
		|	ЕСТЬNULL(НастройкиХозяйственныхОпераций.Ссылка, ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПустаяСсылка)) КАК НастройкаХозяйственнойОперации
		|	
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиХозяйственныхОпераций КАК НастройкиХозяйственныхОпераций
		|		ПО НастройкиХозяйственныхОпераций.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
		|		ПО ДанныеДокумента.ДокументВыдачи = СписаниеБезналичныхДенежныхСредств.Ссылка
		|	
		|ГДЕ
		|	СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику)
		|
		|";
		
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если МассивТекстовЗапросов.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)
	
	ИмяРегистра = "ПрочиеДоходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаРасшифровкаПлатежа", ТекстыЗапроса) Тогда
		ТекстЗапросаВременнаяТаблицаРасшифровкаПлатежа(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаКурсовыхРазниц", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаКурсовыхРазниц(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли;
	
	МассивТекстовЗапросов = Новый Массив;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ТаблицаРасшифровкаПлатежа.НомерСтроки КАК ТаблицаРасшифровкаПлатежаНомерСтроки,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&Организация КАК Организация,
		|	ТаблицаРасшифровкаПлатежа.СтатьяДоходов КАК СтатьяДоходов,
		|	ТаблицаРасшифровкаПлатежа.АналитикаДоходов КАК АналитикаДоходов,
		|	ТаблицаРасшифровкаПлатежа.Подразделение КАК Подразделение,
		|	ТаблицаРасшифровкаПлатежа.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	
		|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	
		|	// Рассчитаем сумму в валюте управленческого учета.
		|	ТаблицаРасшифровкаПлатежа.СуммаУпр КАК Сумма,
		|	(ВЫБОР
		|		КОГДА &УправленческийУчетОрганизаций
		|			ТОГДА ТаблицаРасшифровкаПлатежа.СуммаУпр
		|		ИНАЧЕ 0 КОНЕЦ) КАК СуммаУпр,
		|	(ВЫБОР
		|		КОГДА &ИспользоватьУчетПрочихДоходовРасходовРегл
		|			ТОГДА ТаблицаРасшифровкаПлатежа.СуммаРегл
		|		ИНАЧЕ 0 КОНЕЦ) КАК СуммаРегл,
		|	
		|	ТаблицаРасшифровкаПлатежа.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
		|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПрочееПоступлениеДенежныхСредств) КАК НастройкаХозяйственнойОперации
		|	
		|ИЗ
		|	ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|ГДЕ
		|	ТаблицаРасшифровкаПлатежа.СтатьяДоходов ССЫЛКА ПланВидовХарактеристик.СтатьиДоходов
		|	И &ИспользоватьУчетПрочихДоходовРасходов";
	
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
		
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты Тогда
		
		ТекстЗапроса = "
		|// Расчет курсовых разниц
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	0 КАК ТаблицаРасшифровкаПлатежаНомерСтроки,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&Организация КАК Организация,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ДоходыПриКонвертацииВалюты) КАК СтатьяДоходов,
		|	ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства) КАК АналитикаДоходов,
		|	ТаблицаКурсовыхРазниц.Подразделение КАК Подразделение,
		|	ТаблицаКурсовыхРазниц.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСПрибыль) КАК ХозяйственнаяОперация,
		|	
		|	ТаблицаКурсовыхРазниц.СуммаУпр КАК Сумма,
		|	(ВЫБОР
		|		КОГДА &УправленческийУчетОрганизаций
		|			ТОГДА ТаблицаКурсовыхРазниц.СуммаУпр
		|		ИНАЧЕ 0 КОНЕЦ) КАК СуммаУпр,
		|	(ВЫБОР
		|		КОГДА &ИспользоватьУчетПрочихДоходовРасходовРегл
		|			ТОГДА ТаблицаКурсовыхРазниц.СуммаРегл
		|		ИНАЧЕ 0 КОНЕЦ) КАК СуммаРегл,
		|	
		|	ТаблицаКурсовыхРазниц.ИдентификаторДокумента КАК ИдентификаторФинЗаписи,
		|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КурсовыеРазницыДСПрибыль) КАК НастройкаХозяйственнойОперации
		|	
		|ИЗ
		|	ТаблицаКурсовыхРазниц КАК ТаблицаКурсовыхРазниц
		|
		|ГДЕ
		|	(ТаблицаКурсовыхРазниц.СуммаУпр > 0 ИЛИ ТаблицаКурсовыхРазниц.СуммаРегл > 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТаблицаРасшифровкаПлатежаНомерСтроки
		|";
		
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
		
	КонецЕсли;
	
	Если МассивТекстовЗапросов.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса, ДокументОбъект)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаКурсовыхРазниц", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаКурсовыхРазниц(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("втСуммыДокументовВВалютахУчета", ТекстыЗапроса) Тогда
		СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса, Неопределено, ДокументОбъект);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы();
	
	МассивТекстовЗапросов = Новый Массив;
	МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&Организация КАК Организация,
		|	ТаблицаКурсовыхРазниц.Подразделение КАК Подразделение,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.РасходыПриКонвертацииВалюты) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства) КАК АналитикаРасходов,
		|	ТаблицаКурсовыхРазниц.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
		|
		|	-ТаблицаКурсовыхРазниц.СуммаУпр КАК СуммаСНДС,
		|	-ТаблицаКурсовыхРазниц.СуммаУпр КАК СуммаБезНДС,
		|	-ТаблицаКурсовыхРазниц.СуммаУпр КАК СуммаБезНДСУпр,
		|	-ТаблицаКурсовыхРазниц.СуммаРегл КАК СуммаСНДСРегл,
		|	-ТаблицаКурсовыхРазниц.СуммаРегл КАК СуммаБезНДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСУбыток) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
		|	
		|	ТаблицаКурсовыхРазниц.ИдентификаторДокумента КАК ИдентификаторФинЗаписи,
		|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КурсовыеРазницыДСУбыток) КАК НастройкаХозяйственнойОперации
		|	
		|ИЗ
		|	ТаблицаКурсовыхРазниц КАК ТаблицаКурсовыхРазниц
		|
		|ГДЕ
		|	(ТаблицаКурсовыхРазниц.СуммаУпр < 0 ИЛИ ТаблицаКурсовыхРазниц.СуммаРегл < 0)";
	
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте
		И ДокументОбъект.СуммаКомиссии <> 0 И ДокументОбъект.ОтражатьКомиссию И ДокументОбъект.ПроведеноБанком Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ДанныеДокумента.Дата КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ДанныеДокумента.Подразделение КАК Подразделение,
		|	ДанныеДокумента.СтатьяРасходов КАК СтатьяРасходов,
		|	ДанныеДокумента.АналитикаРасходов КАК АналитикаРасходов,
		|	ДанныеДокумента.ДоговорЭквайринга.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
		|	
		|	// Рассчитаем сумму в валюте управленческого учета.
		|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаКомиссии * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаСНДС,
		|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаКомиссии * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаБезНДС,
		|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаКомиссии * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаБезНДСУпр,
		|
		|	ЕСТЬNULL(СуммыДокументовВВалютахУчета.СуммаБезНДСРегл + СуммыДокументовВВалютахУчета.СуммаНДСРегл, ДанныеДокумента.СуммаКомиссии) КАК СуммаСНДСРегл,
		|	ЕСТЬNULL(СуммыДокументовВВалютахУчета.СуммаБезНДСРегл, ДанныеДокумента.СуммаКомиссии) КАК СуммаБезНДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеРасходы) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
		|	
		|	ДанныеДокумента.ИдентификаторДокумента КАК ИдентификаторФинЗаписи,
		|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КомиссияПоЭквайрингу) КАК НастройкаХозяйственнойОперации 
		|
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		втСуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
		|	ПО
		|		СуммыДокументовВВалютахУчета.Ссылка = ДанныеДокумента.Ссылка
		|		И СуммыДокументовВВалютахУчета.ИдентификаторСтроки = ""1""
		|";
			
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса, ДокументОбъект)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)
	
	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ((ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств
			ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПогашениеЗаймаСотрудником
			ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленныхДС
			ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленнойЗарплатыПоЗарплатномуПроекту
			ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНалогов) И ДокументОбъект.ПроведеноБанком) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаРасшифровкаПлатежа", ТекстыЗапроса) Тогда
		ТекстЗапросаВременнаяТаблицаРасшифровкаПлатежа(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли;
	
	МассивТестовЗапросов = Новый Массив;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств 
		И ДокументОбъект.ПроведеноБанком Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Организация КАК Организация,
		|	ТаблицаРасшифровкаПлатежа.Подразделение КАК Подразделение,
		|	ТаблицаРасшифровкаПлатежа.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ТаблицаРасшифровкаПлатежа.СтатьяДоходов КАК Статья,
		|	ВЫБОР
		|		КОГДА ТаблицаРасшифровкаПлатежа.АналитикаАктивовПассивов = ЗНАЧЕНИЕ(Перечисление.СтатьиБезАналитики.ПустаяСсылка)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ТаблицаРасшифровкаПлатежа.АналитикаАктивовПассивов
		|	КОНЕЦ КАК Аналитика,
		|	СУММА(ТаблицаРасшифровкаПлатежа.СуммаУпр) КАК Сумма
		|ИЗ
		|	ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|ГДЕ
		|	ТаблицаРасшифровкаПлатежа.СтатьяДоходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаРасшифровкаПлатежа.СтатьяДоходов,
		|	ТаблицаРасшифровкаПлатежа.АналитикаАктивовПассивов,
		|	ТаблицаРасшифровкаПлатежа.Подразделение,
		|	ТаблицаРасшифровкаПлатежа.НаправлениеДеятельности";
	
		МассивТестовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПогашениеЗаймаСотрудником 
		И ДокументОбъект.ПроведеноБанком Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Организация КАК Организация,
		|	НЕОПРЕДЕЛЕНО КАК Подразделение,
		|	ТаблицаРасшифровкаПлатежа.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗаймыВыданные) КАК Статья,
		|	НЕОПРЕДЕЛЕНО КАК Аналитика,
		|	СУММА(ТаблицаРасшифровкаПлатежа.СуммаУпр) КАК Сумма
		|ИЗ
		|	ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаРасшифровкаПлатежа.НаправлениеДеятельности";
		
		МассивТестовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленныхДС
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленнойЗарплатыПоЗарплатномуПроекту)
		И ДокументОбъект.ПроведеноБанком Тогда
	
		ТекстЗапроса = "		
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Организация КАК Организация,
		|	НЕОПРЕДЕЛЕНО КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда) КАК Статья,
		|	НЕОПРЕДЕЛЕНО КАК Аналитика,
		|	СУММА(ВЫБОР
		|		КОГДА &Валюта = &ВалютаУправленческогоУчета
		|			ТОГДА ДД.СуммаДокумента
		|		ИНАЧЕ ВЫРАЗИТЬ(ДД.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
		|	КОНЕЦ) КАК Сумма
		|ИЗ
		|	ДанныеДокументаШапка КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
		|		ПО ДД.ДокументВыдачи = СписаниеБезналичныхДенежныхСредств.Ссылка
		|ГДЕ
		|	НЕ СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику)
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВЫБОР
		|		КОГДА &Валюта = &ВалютаУправленческогоУчета
		|			ТОГДА ДД.СуммаДокумента
		|		ИНАЧЕ ВЫРАЗИТЬ(ДД.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
		|	КОНЕЦ) <> 0";
		
		МассивТестовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНалогов
		И ДокументОбъект.ПроведеноБанком Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Организация КАК Организация,
		|	ДанныеДокумента.Подразделение КАК Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) КАК Статья,
		|	ДанныеДокумента.ТипНалога КАК Аналитика,
		|	ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК Сумма
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|";
		
		МассивТестовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если МассивТестовЗапросов.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = СтрСоединить(МассивТестовЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)
	
	ИмяРегистра = "ДвиженияПоПрочимАктивамПассивам";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ((ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента 
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПогашениеЗаймаСотрудником
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленныхДС
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленнойЗарплатыПоЗарплатномуПроекту
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНалогов) 
			И ДокументОбъект.ПроведеноБанком) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаРасшифровкаПлатежа", ТекстыЗапроса) Тогда
		ТекстЗапросаВременнаяТаблицаРасшифровкаПлатежа(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли;
	
	ОтражатьВзаимозачетСФилиалами = Ложь;
	
	Если (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента 
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика) 
		И ДокументОбъект.ПроведеноБанком Тогда
			
		ОтражатьВзаимозачетСФилиалами = Истина;
		
	КонецЕсли;
	
	Если ОтражатьВзаимозачетСФилиалами И НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтДвиженияКонтрагентКонтрагент", ТекстыЗапроса) Тогда
		ТекстЗапросаВременнаяТаблицаДвиженияКонтрагентКонтрагент(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли;
	
	МассивТекстовЗапроса = Новый Массив;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств
		И ДокументОбъект.ПроведеноБанком Тогда
			
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	&Организация КАК Организация,
		|	ТаблицаРасшифровкаПлатежа.Подразделение КАК Подразделение,
		|	ТаблицаРасшифровкаПлатежа.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ТаблицаРасшифровкаПлатежа.СтатьяДоходов КАК Статья,
		|	ТаблицаРасшифровкаПлатежа.АналитикаАктивовПассивов КАК Аналитика,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
		|
		|	ТаблицаРасшифровкаПлатежа.СуммаРегл КАК СуммаРегл,
		|	ТаблицаРасшифровкаПлатежа.СуммаУпр КАК СуммаУпр,
		|	ТаблицаРасшифровкаПлатежа.СуммаУпр КАК СуммаСНДС,
		|	ТаблицаРасшифровкаПлатежа.СуммаУпр КАК СуммаБезНДС,
		|
		|	ТаблицаРасшифровкаПлатежа.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
		|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
		|ИЗ
		|	ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|ГДЕ
		|	ТаблицаРасшифровкаПлатежа.СтатьяДоходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов";
	
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПогашениеЗаймаСотрудником
		И ДокументОбъект.ПроведеноБанком Тогда
			
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	&Организация КАК Организация,
		|	НЕОПРЕДЕЛЕНО КАК Подразделение,
		|	ТаблицаРасшифровкаПлатежа.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗаймыВыданные) КАК Статья,
		|	&ПодотчетноеЛицо КАК Аналитика,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
		|
		|	ТаблицаРасшифровкаПлатежа.СуммаРегл КАК СуммаРегл,
		|	ТаблицаРасшифровкаПлатежа.СуммаУпр КАК СуммаУпр,
		|	ТаблицаРасшифровкаПлатежа.СуммаУпр КАК СуммаСНДС,
		|	ТаблицаРасшифровкаПлатежа.СуммаУпр КАК СуммаБезНДС,
		|
		|	ТаблицаРасшифровкаПлатежа.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
		|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
		|ИЗ
		|	ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленныхДС
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленнойЗарплатыПоЗарплатномуПроекту)
		И ДокументОбъект.ПроведеноБанком И ДокументОбъект.СуммаДокумента <> 0 Тогда
			
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	&Организация КАК Организация,
		|	НЕОПРЕДЕЛЕНО КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда) КАК Статья,
		|	НЕОПРЕДЕЛЕНО КАК Аналитика,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
		|
		|	ВЫБОР
		|		КОГДА &Валюта = &ВалютаРегламентированногоУчета
		|			ТОГДА ДД.СуммаДокумента
		|		ИНАЧЕ ВЫРАЗИТЬ(ДД.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
		|	КОНЕЦ КАК СуммаРегл,
		|	ВЫБОР
		|		КОГДА &Валюта = &ВалютаУправленческогоУчета
		|			ТОГДА ДД.СуммаДокумента
		|		ИНАЧЕ ВЫРАЗИТЬ(ДД.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
		|	КОНЕЦ КАК СуммаУпр,
		|	ВЫБОР
		|		КОГДА &Валюта = &ВалютаУправленческогоУчета
		|			ТОГДА ДД.СуммаДокумента
		|		ИНАЧЕ ВЫРАЗИТЬ(ДД.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
		|	КОНЕЦ КАК СуммаСНДС,
		|	ВЫБОР
		|		КОГДА &Валюта = &ВалютаУправленческогоУчета
		|			ТОГДА ДД.СуммаДокумента
		|		ИНАЧЕ ВЫРАЗИТЬ(ДД.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
		|	КОНЕЦ КАК СуммаБезНДС,
		|
		|	ДД.ИдентификаторДокумента КАК ИдентификаторФинЗаписи,
		|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
		|ИЗ
		|	ДанныеДокументаШапка КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
		|		ПО ДД.ДокументВыдачи = СписаниеБезналичныхДенежныхСредств.Ссылка
		|ГДЕ
		|	НЕ СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику)";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств
		И ДокументОбъект.ПроведеноБанком И ДокументОбъект.СуммаДокумента <> 0 Тогда
			
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	&Организация КАК Организация,
		|	&Подразделение КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыСФилиалами) КАК Статья,
		|	ДД.БанковскийСчетОтправительВладелец КАК Аналитика,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
		|
		|	ВЫБОР
		|		КОГДА &Валюта = &ВалютаРегламентированногоУчета
		|			ТОГДА ДД.СуммаДокумента
		|		ИНАЧЕ ВЫРАЗИТЬ(ДД.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
		|	КОНЕЦ КАК СуммаРегл,
		|	ВЫБОР
		|		КОГДА &Валюта = &ВалютаУправленческогоУчета
		|			ТОГДА ДД.СуммаДокумента
		|		ИНАЧЕ ВЫРАЗИТЬ(ДД.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
		|	КОНЕЦ КАК СуммаУпр,
		|	ВЫБОР
		|		КОГДА &Валюта = &ВалютаУправленческогоУчета
		|			ТОГДА ДД.СуммаДокумента
		|		ИНАЧЕ ВЫРАЗИТЬ(ДД.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
		|	КОНЕЦ КАК СуммаСНДС,
		|	ВЫБОР
		|		КОГДА &Валюта = &ВалютаУправленческогоУчета
		|			ТОГДА ДД.СуммаДокумента
		|		ИНАЧЕ ВЫРАЗИТЬ(ДД.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
		|	КОНЕЦ КАК СуммаБезНДС,
		|
		|	ДД.ИдентификаторДокумента КАК ИдентификаторФинЗаписи,
		|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
		|ИЗ
		|	ДанныеДокументаШапка КАК ДД
		|";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНалогов
		И ДокументОбъект.ПроведеноБанком Тогда
			
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	&Организация КАК Организация,
		|	ДанныеДокумента.Подразделение КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) КАК Статья,
		|	ДанныеДокумента.ТипНалога КАК Аналитика,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
		|
		|	ТаблицаРасшифровкаПлатежа.СуммаРегл КАК СуммаРегл,
		|	ТаблицаРасшифровкаПлатежа.СуммаУпр  КАК СуммаУпр,
		|	ТаблицаРасшифровкаПлатежа.СуммаУпр  КАК СуммаСНДС,
		|	ТаблицаРасшифровкаПлатежа.СуммаУпр  КАК СуммаБезНДС,
		|
		|	ТаблицаРасшифровкаПлатежа.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
		|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|	ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|		ПО ИСТИНА
		|";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если МассивТекстовЗапроса.Количество() = 0 Тогда
		
		Если ОтражатьВзаимозачетСФилиалами Тогда
			ТекстЗапроса = ПрочиеАктивыПассивыСервер.ТекстЗапросаВзаимозачетСФилиалами();
			МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		КонецЕсли;
		
	Иначе
		
		Если ОтражатьВзаимозачетСФилиалами Тогда
			МассивТекстовЗапроса.Добавить(ПрочиеАктивыПассивыСервер.ТекстЗапросаВзаимозачетСФилиалами());
		КонецЕсли;
		
	КонецЕсли;
	
	Если МассивТекстовЗапроса.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаКурсовыхРазниц(Запрос, ТекстыЗапроса, ДокументОбъект = Неопределено) Экспорт
	
	ИмяРегистра = "ТаблицаКурсовыхРазниц";
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.БанковскийСчетПодразделение КАК Подразделение,
	|	ДанныеДокумента.БанковскийСчетНаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаДенежныхСредств) КАК ХозяйственнаяОперация,
	|
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуКонвертации КАК ЧИСЛО(31,2))
	|		- ДанныеДокумента.СуммаКонвертации КАК Сумма,
	|
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		- ВЫРАЗИТЬ(ДанныеДокумента.СуммаКонвертации * &КоэффициентПересчетаКонвертацииВВалютуУпр КАК ЧИСЛО(31,2))
	|	КАК СуммаУпр,
	|	
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|		- ВЫРАЗИТЬ(ДанныеДокумента.СуммаКонвертации * &КоэффициентПересчетаКонвертацииВВалютуРегл КАК ЧИСЛО(31,2))
	|	КАК СуммаРегл,
	|	ДанныеДокумента.ИдентификаторДокумента КАК ИдентификаторДокумента
	|
	|ПОМЕСТИТЬ ТаблицаКурсовыхРазниц
	|ИЗ
	|	ДанныеДокументаШапка КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты)
	|";
	
	Если ДокументОбъект = Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеДокументаШапка", "Документ.ПоступлениеБезналичныхДенежныхСредств");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеДокумента.БанковскийСчетПодразделение", "ДанныеДокумента.БанковскийСчет.Подразделение");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеДокумента.БанковскийСчетНаправлениеДеятельности", "ДанныеДокумента.БанковскийСчет.НаправлениеДеятельности");
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса, Регистры = Неопределено, ДокументОбъект = Неопределено) Экспорт
	
	ТекстЗапросаДанных = "
	|ВЫБРАТЬ
	|	""ДанныеДокумента""     КАК ИсточникДанных,
	|	ЛОЖЬ                    КАК РаспределятьОбщуюСумму,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ВЫБОР 
	|		КОГДА НЕ ТаблицаДокумента.ПроведеноБанком
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаДокумента.Дата, ДЕНЬ) = ТаблицаДокумента.ДатаПроведенияБанком
	|				ИЛИ ТаблицаДокумента.Исправление ТОГДА
	|			ТаблицаДокумента.Дата
	|		ИНАЧЕ
	|			КОНЕЦПЕРИОДА(ТаблицаДокумента.ДатаПроведенияБанком, ДЕНЬ)
	|	КОНЕЦ                   КАК Дата,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Валюта КАК ВалютаДокумента,
	|	ЕСТЬNULL(Строки.ВалютаВзаиморасчетов, НЕОПРЕДЕЛЕНО) КАК ВалютаВзаиморасчетов,
	|	НЕОПРЕДЕЛЕНО КАК ПериодБазыНДС,
	|	ВЫБОР 
	|		КОГДА НЕ ТаблицаДокумента.ПроведеноБанком
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаДокумента.Дата, ДЕНЬ) = ТаблицаДокумента.ДатаПроведенияБанком
	|				ИЛИ ТаблицаДокумента.Исправление ТОГДА
	|			ТаблицаДокумента.Дата
	|		ИНАЧЕ
	|			КОНЕЦПЕРИОДА(ТаблицаДокумента.ДатаПроведенияБанком, ДЕНЬ)
	|	КОНЕЦ                   КАК ДатаКурса,
	|
	|	ЕСТЬNULL(Строки.НомерСтроки, 0) КАК НомерСтроки,
	|	ЕСТЬNULL(Строки.ИдентификаторСтроки, """") КАК ИдентификаторСтроки,
	|	ЕСТЬNULL(Строки.Сумма - Строки.СуммаНДС, ТаблицаДокумента.СуммаДокумента) КАК СуммаБезНДС,
	|	ЕСТЬNULL(Строки.СтавкаНДС, НЕОПРЕДЕЛЕНО) КАК СтавкаНДС,
	|	ЕСТЬNULL(Строки.СуммаНДС, 0) КАК СуммаНДС,
	|	ЕСТЬNULL(Строки.СуммаВзаиморасчетов, 0) КАК СуммаВзаиморасчетов,
	|	0 КАК СуммаНДСВзаиморасчетов,
	|	0 КАК СуммаБезНДСРегл,
	|	0 КАК СуммаБезНДСУпр,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Строки.ОбъектРасчетов, ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОтражаетсяВРасчетах,
	|	ЕСТЬNULL(Строки.ОбъектРасчетов, ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)) КАК ОбъектРасчетов,
	|	ЛОЖЬ КАК ПересчитыватьПоДаннымРасчетов
	|ИЗ
	|	ДанныеДокументаШапка КАК ТаблицаДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаРасшифровкаПлатежа КАК Строки
	|		ПО Строки.Ссылка = ТаблицаДокумента.Ссылка
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ДанныеДокумента"" КАК ИсточникДанных,
	|	ЛОЖЬ КАК РаспределятьОбщуюСумму,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Дата КАК Дата,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Валюта КАК ВалютаДокумента,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаВзаиморасчетов,
	|	НЕОПРЕДЕЛЕНО КАК ПериодБазыНДС,
	|	ТаблицаДокумента.Дата КАК ДатаКурса,
	|
	|	0 КАК НомерСтроки,
	|	""1"" КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.СуммаКомиссии КАК СуммаБезНДС,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
	|	0 КАК СуммаНДС,
	|	0 КАК СуммаВзаиморасчетов,
	|	0 КАК СуммаНДСВзаиморасчетов,
	|	0 КАК СуммаБезНДСРегл,
	|	0 КАК СуммаБезНДСУпр,
	|
	|	ЛОЖЬ КАК ОтражаетсяВРасчетах,
	|	НЕОПРЕДЕЛЕНО КАК ОбъектРасчетов,
	|	ЛОЖЬ КАК ПересчитыватьПоДаннымРасчетов
	|ИЗ
	|	ДанныеДокументаШапка КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|	И ТаблицаДокумента.ОтражатьКомиссию
	|	И ТаблицаДокумента.СуммаКомиссии <> 0
	|";
	
	Если ДокументОбъект = Неопределено Тогда
		ТекстЗапросаДанных = СтрЗаменить(ТекстЗапросаДанных, "ДанныеДокументаШапка", "Документ.ПоступлениеБезналичныхДенежныхСредств");
		ТекстЗапросаДанных = СтрЗаменить(ТекстЗапросаДанных, "ДанныеДокументаШапка", "Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа");
	КонецЕсли;
	
	РегистрыСведений.СуммыДокументовВВалютахУчета.СформироватьПоДаннымДокумента(
		Запрос, ТекстыЗапроса, Регистры, ТекстЗапросаДанных);

КонецПроцедуры

Функция ТекстЗапросаТаблицаРасчетыПоФинансовымИнструментам(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)
	
	ИмяРегистра = "РасчетыПоФинансовымИнструментам";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ((ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтАрендодателя)
		И ДокументОбъект.ПроведеноБанком) Тогда
		Возврат "";
	КонецЕсли;
		
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаРасшифровкаПлатежа", ТекстыЗапроса) Тогда
		ТекстЗапросаВременнаяТаблицаРасшифровкаПлатежа(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли;
	
	МассивТекстовЗапросов = Новый Массив;
	
	Если (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным)
		И ДокументОбъект.ПроведеноБанком Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ТаблицаРасшифровкаПлатежа.НомерСтроки                             КАК НомерСтроки,
		|	&Период                                                           КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                            КАК ВидДвижения,
		
		|	ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам               КАК АналитикаУчетаПоПартнерам,
		|	ТаблицаРасшифровкаПлатежа.ДоговорКредитаДепозита                  КАК Договор,
		|	ВЫБОР
		|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг)
		|		ИНАЧЕ ТаблицаРасшифровкаПлатежа.ТипСуммыКредитаДепозита
		|	КОНЕЦ КАК ТипСуммы,
		|	НЕОПРЕДЕЛЕНО КАК РасчетныйДокумент,
		|	ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов                    КАК Валюта,
		
		|	ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов                     КАК Сумма,
		|	ТаблицаРасшифровкаПлатежа.СуммаРегл                               КАК СуммаРегл,
		|	ТаблицаРасшифровкаПлатежа.СуммаУпр                                КАК СуммаУпр,
		
		|	&ХозяйственнаяОперация                                            КАК ХозяйственнаяОперация,
		|	ВЫБОР
		|		КОГДА ДанныеДоговора.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыГрафиковФинансовыхИнструментов.Транши)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыГрафиковФинансовыхИнструментов.Оплаты)
		|	КОНЕЦ КАК ТипГрафика,
		|	ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств           КАК СтатьяАналитики,
		|	
		|	ТаблицаРасшифровкаПлатежа.ИдентификаторСтроки                     КАК ИдентификаторФинЗаписи,
		|	ВЫБОР 
		|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам) 
		|			 ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПоступлениеДенежныхСредствПоКредитам) 
		|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам) 
		|			 ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПоступлениеДенежныхСредствПоДепозитам)
		|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным) 
		|			 ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПоступлениеДенежныхСредствПоЗаймамВыданным)
		|	КОНЕЦ КАК НастройкаХозяйственнойОперации
		|	
		|ИЗ
		|	ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКредитовИДепозитов КАК ДанныеДоговора
		|		ПО ТаблицаРасшифровкаПлатежа.ДоговорКредитаДепозита = ДанныеДоговора.Ссылка
		|";
		
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	
	Если МассивТекстовЗапросов.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДенежныеСредстваУПодотчетныхЛиц(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)
	
	ИмяРегистра = "ДенежныеСредстваУПодотчетныхЛиц";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника
		И ДокументОбъект.ПроведеноБанком) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаРасшифровкаПлатежа", ТекстыЗапроса) Тогда
		ТекстЗапросаВременнаяТаблицаРасшифровкаПлатежа(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.ПодотчетноеЛицо,
	|	ДанныеДокумента.Подразделение,
	|	ДанныеДокумента.БанковскийСчетВалютаДенежныхСредств КАК Валюта,
	|	
	|	ВЫБОР КОГДА &КонтролироватьВыдачуПодОтчетВРазрезеЦелей ТОГДА
	|		ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств
	|	КОНЕЦ КАК ЦельВыдачи,
	|	
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ЕстьNULL(ТаблицаРасшифровкаПлатежа.Сумма, ДанныеДокумента.СуммаДокумента) КАК Сумма,
	|	ВЫБОР КОГДА ТаблицаРасшифровкаПлатежа.Сумма ЕСТЬ NULL
	|		ТОГДА ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|		ИНАЧЕ ТаблицаРасшифровкаПлатежа.СуммаРегл
	|	КОНЕЦ КАК СуммаРегл,
	|	ВЫБОР КОГДА ТаблицаРасшифровкаПлатежа.Сумма ЕСТЬ NULL
	|		ТОГДА ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ ТаблицаРасшифровкаПлатежа.СуммаУпр
	|	КОНЕЦ КАК СуммаУпр,
	|	ЕстьNULL(ТаблицаРасшифровкаПлатежа.Сумма, ДанныеДокумента.СуммаДокумента) КАК КОтчету,
	|
	|	ТаблицаРасшифровкаПлатежа.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВозвратДенежныхСредствОтПодотчетника) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаШапка КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
	|		ПО ИСТИНА
	|   	ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
	|		ПО ДанныеДокумента.ДокументОснование = СписаниеБезналичныхДенежныхСредств.Ссылка
	|
	|ГДЕ
	|	НЕ ЕСТЬNULL(СписаниеБезналичныхДенежныхСредств.СписокФизЛиц, ЛОЖЬ)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияДенежныхСредств(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)
	
	ИмяРегистра = "ДвиженияДенежныхСредств";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	МассивТекстовЗапросов = Новый Массив;
	
	Если (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствСДругогоСчета
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет)
		И ДокументОбъект.ПроведеноБанком Тогда
			
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	&Период КАК Период,
		|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ДанныеДокумента.БанковскийСчетПодразделение КАК Подразделение,
		|	
		|	ВЫБОР КОГДА ДанныеДокумента.ХозяйственнаяОперация = Значение(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствСДругогоСчета) ТОГДА
		|		ДанныеДокумента.БанковскийСчет
		|	КОГДА ДанныеДокумента.ХозяйственнаяОперация = Значение(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет) ТОГДА
		|		ДанныеДокумента.КассаОтправитель
		|	КОНЕЦ КАК ДенежныеСредства,
		|	&НаправлениеДеятельностиДС КАК НаправлениеДеятельности,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваВПути) КАК ТипДенежныхСредств,
		|	
		|	&СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	&СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
		|	ДанныеДокумента.Валюта КАК Валюта,
		|	
		|	ДанныеДокумента.БанковскийСчет КАК КорДенежныеСредства,
		|	&НаправлениеДеятельностиДС КАК КорНаправлениеДеятельности,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.Безналичные) КАК КорТипДенежныхСредств,
		|	ДанныеДокумента.Валюта КАК КорВалюта,
		|
		|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК Сумма,
		|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)) КАК СуммаРегл,
		|	ДанныеДокумента.СуммаДокумента КАК СуммаВВалюте,
		|	ДанныеДокумента.СуммаДокумента КАК СуммаВКорВалюте,
		|
		|	ВЫБОР КОГДА ДанныеДокумента.ХозяйственнаяОперация = Значение(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствСДругогоСчета) ТОГДА
		|		ДанныеДокумента.БанковскийСчет
		|	КОГДА ДанныеДокумента.ХозяйственнаяОперация = Значение(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет) ТОГДА
		|		ДанныеДокумента.КассаОтправитель
		|	КОНЕЦ КАК ИсточникГФУДенежныхСредств,
		|	ДанныеДокумента.БанковскийСчет КАК ИсточникКорГФУДенежныхСредств
		|	
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|";
		
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника
		И ДокументОбъект.ПроведеноБанком Тогда
			
		ТекстЗапроса ="
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ДанныеДокумента.Подразделение КАК Подразделение,
		|
		|	ДанныеДокумента.ПодотчетноеЛицо КАК ДенежныеСредства,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваУПодотчетногоЛица) КАК ТипДенежныхСредств,
		|	ТаблицаПлатежногоДокумента.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ТаблицаПлатежногоДокумента.СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
		|	ДанныеДокумента.Валюта КАК Валюта,
		|
		|	ДанныеДокумента.БанковскийСчет КАК КорДенежныеСредства,
		|	&НаправлениеДеятельностиДС КАК КорНаправлениеДеятельности,
		|	Значение(Перечисление.ТипыДенежныхСредств.Безналичные) КАК КорТипДенежныхСредств,
		|	ДанныеДокумента.Валюта КАК КорВалюта,
		|
		|	ВЫРАЗИТЬ(ТаблицаПлатежногоДокумента.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК Сумма,
		|	ВЫРАЗИТЬ(ТаблицаПлатежногоДокумента.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)) КАК СуммаРегл,
		|	ТаблицаПлатежногоДокумента.Сумма КАК СуммаВВалюте,
		|	ТаблицаПлатежногоДокумента.Сумма КАК СуммаВКорВалюте,
		|
		|	НЕОПРЕДЕЛЕНО КАК ИсточникГФУДенежныхСредств,
		|	ДанныеДокумента.БанковскийСчет КАК ИсточникКорГФУДенежныхСредств
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|			Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК ТаблицаПлатежногоДокумента
		|		ПО
		|			ТаблицаПлатежногоДокумента.Ссылка = ДанныеДокумента.Ссылка
		|";
		
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленнойЗарплатыПоЗарплатномуПроекту
		И ДокументОбъект.ПроведеноБанком Тогда
			
		ТекстЗапроса ="
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника) КАК ХозяйственнаяОперация,
		|	ДанныеДокумента.Организация КАК Организация,
		|	СписаниеБезналичныхДенежныхСредств.Подразделение КАК Подразделение,
		|
		|	ДанныеДокумента.ПодотчетноеЛицо КАК ДенежныеСредства,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваУПодотчетногоЛица) КАК ТипДенежныхСредств,
		|	ДанныеДокумента.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ДанныеДокумента.СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
		|	ДанныеДокумента.Валюта КАК Валюта,
		|
		|	ДанныеДокумента.БанковскийСчет КАК КорДенежныеСредства,
		|	&НаправлениеДеятельностиДС КАК КорНаправлениеДеятельности,
		|	Значение(Перечисление.ТипыДенежныхСредств.Безналичные) КАК КорТипДенежныхСредств,
		|	ДанныеДокумента.Валюта КАК КорВалюта,
		|
		|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК Сумма,
		|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)) КАК СуммаРегл,
		|	ДанныеДокумента.СуммаДокумента КАК СуммаВВалюте,
		|	ДанныеДокумента.СуммаДокумента КАК СуммаВКорВалюте,
		|
		|	НЕОПРЕДЕЛЕНО КАК ИсточникГФУДенежныхСредств,
		|	ДанныеДокумента.БанковскийСчет КАК ИсточникКорГФУДенежныхСредств
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
		|		ПО ДанныеДокумента.ДокументВыдачи = СписаниеБезналичныхДенежныхСредств.Ссылка
		|
		|ГДЕ
		|	СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику)";
		
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте
		И ДокументОбъект.ПроведеноБанком Тогда
	
		ТекстЗапроса ="
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ДанныеДокумента.Организация КАК Организация,
		|	НЕОПРЕДЕЛЕНО КАК Подразделение,
		|
		|	ДанныеДокумента.ДоговорЭквайринга КАК ДенежныеСредства,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	Значение(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваУЭквайера) КАК ТипДенежныхСредств,
		|	ДанныеДокумента.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ДанныеДокумента.СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
		|	ДанныеДокумента.Валюта КАК Валюта,
		|
		|	ДанныеДокумента.БанковскийСчет КАК КорДенежныеСредства,
		|	&НаправлениеДеятельностиДС КАК КорНаправлениеДеятельности,
		|	Значение(Перечисление.ТипыДенежныхСредств.Безналичные) КАК КорТипДенежныхСредств,
		|	ДанныеДокумента.Валюта КАК КорВалюта,
		|
		|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК Сумма,
		|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)) КАК СуммаРегл,
		|	ДанныеДокумента.СуммаДокумента КАК СуммаВВалюте,
		|	ДанныеДокумента.СуммаДокумента КАК СуммаВКорВалюте,
		|
		|	НЕОПРЕДЕЛЕНО КАК ИсточникГФУДенежныхСредств,
		|	ДанныеДокумента.БанковскийСчет КАК ИсточникКорГФУДенежныхСредств
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|";
		
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам
		И ДокументОбъект.ПроведеноБанком Тогда
			
		ТекстЗапроса ="
		| // Операции поступления денежных средств по депозитам
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ТаблицаПлатежногоДокумента.ДоговорКредитаДепозитаПодразделение КАК Подразделение,
		|
		|	ТаблицаПлатежногоДокумента.ДоговорКредитаДепозита КАК ДенежныеСредства,
		|	ДанныеДоговора.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Значение(Перечисление.ТипыДенежныхСредств.Депозиты) КАК ТипДенежныхСредств,
		|	ТаблицаПлатежногоДокумента.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ТаблицаПлатежногоДокумента.СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
		|	ДанныеДокумента.Валюта КАК Валюта,
		|
		|	ДанныеДокумента.БанковскийСчет КАК КорДенежныеСредства,
		|	&НаправлениеДеятельностиДС КАК КорНаправлениеДеятельности,
		|	Значение(Перечисление.ТипыДенежныхСредств.Безналичные) КАК КорТипДенежныхСредств,
		|	ДанныеДокумента.Валюта КАК КорВалюта,
		|
		|	ВЫРАЗИТЬ(ТаблицаПлатежногоДокумента.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК Сумма,
		|	ВЫРАЗИТЬ(ТаблицаПлатежногоДокумента.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)) КАК СуммаРегл,
		|	ТаблицаПлатежногоДокумента.Сумма КАК СуммаВВалюте,
		|	ТаблицаПлатежногоДокумента.Сумма КАК СуммаВКорВалюте,
		|
		|	ТаблицаПлатежногоДокумента.ДоговорКредитаДепозита КАК ИсточникГФУДенежныхСредств,
		|	ДанныеДокумента.БанковскийСчет КАК ИсточникКорГФУДенежныхСредств
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|			ДанныеДокументаТаблицаРасшифровкаПлатежа КАК ТаблицаПлатежногоДокумента
		|		ПО
		|			ТаблицаПлатежногоДокумента.Ссылка = ДанныеДокумента.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКредитовИДепозитов КАК ДанныеДоговора
		|			ПО ТаблицаПлатежногоДокумента.ДоговорКредитаДепозита = ДанныеДоговора.Ссылка
		|";
		
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты
		И ДокументОбъект.ПроведеноБанком Тогда
			
		ТекстЗапроса ="
		|
		|// Операции поступления денежных средств от конвертации валюты
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	Значение(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствОтКонвертацииВалюты) КАК ХозяйственнаяОперация,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ДанныеДокумента.Подразделение КАК Подразделение,
		|
		|	ДанныеДокумента.Контрагент КАК ДенежныеСредства,
		|	&НаправлениеДеятельностиДС КАК НаправлениеДеятельности,
		|	Значение(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваВПути) КАК ТипДенежныхСредств,
		|	ДанныеДокумента.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ДанныеДокумента.СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
		|	ДанныеДокумента.ВалютаКонвертации КАК Валюта,
		|
		|	ДанныеДокумента.БанковскийСчет КАК КорДенежныеСредства,
		|	&НаправлениеДеятельностиДС КАК КорНаправлениеДеятельности,
		|	Значение(Перечисление.ТипыДенежныхСредств.Безналичные) КАК КорТипДенежныхСредств,
		|	ДанныеДокумента.Валюта КАК КорВалюта,
		|
		|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК Сумма,
		|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)) КАК СуммаРегл,
		|	ДанныеДокумента.СуммаКонвертации КАК СуммаВВалюте,
		|	ДанныеДокумента.СуммаДокумента КАК СуммаВКорВалюте,
		|
		|	НЕОПРЕДЕЛЕНО КАК ИсточникГФУДенежныхСредств,
		|	ДанныеДокумента.БанковскийСчет КАК ИсточникКорГФУДенежныхСредств
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|
		|";
		
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если МассивТекстовЗапросов.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияДенежныеСредстваКонтрагент(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)
	
	ИмяРегистра = "ДвиженияДенежныеСредстваКонтрагент";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаРасшифровкаПлатежа", ТекстыЗапроса) Тогда
		ТекстЗапросаВременнаяТаблицаРасшифровкаПлатежа(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли;
	
	МассивТекстовПодзапроса = Новый Массив;
	
	Если (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика)
		И ДокументОбъект.ПроведеноБанком Тогда
		
		ТекстПодзапросаЗапроса = "
		|// Операции по поступлению денежных средств
		|	ВЫБРАТЬ
		|		&Период КАК Период,
		|		&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|		ДанныеДокумента.Организация КАК Организация,
		|		ТаблицаРасшифровкаПлатежа.ПодразделениеЗаказа КАК Подразделение,
		|
		|		ДанныеДокумента.БанковскийСчет КАК ДенежныеСредства,
		|		ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|		ТаблицаРасшифровкаПлатежа.СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
		|		ДанныеДокумента.Валюта КАК ВалютаПлатежа,
		|
		|		ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
		|		ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
		|		ЕСТЬNULL(ТаблицаРасшифровкаПлатежа.ОбъектРасчетов.НаправлениеДеятельности, ДанныеДокумента.НаправлениеДеятельности) КАК НаправлениеДеятельности,
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов.Договор КАК Договор,
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК ОбъектРасчетов,
		|	
		|		ТаблицаРасшифровкаПлатежа.СуммаУпр КАК СуммаОплаты,
		|		ТаблицаРасшифровкаПлатежа.СуммаРегл КАК СуммаОплатыРегл,
		|		ТаблицаРасшифровкаПлатежа.Сумма КАК СуммаОплатыВВалютеПлатежа,
		|
		|		0 КАК СуммаПостоплаты,
		|		0 КАК СуммаПостоплатыРегл,
		|		0 КАК СуммаПостоплатыВВалютеПлатежа,
		|		
		|		0 КАК СуммаПредоплаты,
		|		0 КАК СуммаПредоплатыРегл,
		|		0 КАК СуммаПредоплатыВВалютеПлатежа,
		|
		|		0 КАК СуммаНДС,
		|		0 КАК СуммаНДСРегл,
		|		0 КАК СуммаНДСВВалютеПлатежа,
		|
		|		ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|
		|		ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов КАК СуммаОплатыВВалютеВзаиморасчетов,
		|		0 КАК СуммаПостоплатыВВалютеВзаиморасчетов,
		|		0 КАК СуммаПредоплатыВВалютеВзаиморасчетов,
		|
		|		ДанныеДокумента.БанковскийСчет КАК ИсточникГФУДенежныхСредств,
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК ИсточникГФУРасчетов,
		|		НЕОПРЕДЕЛЕНО КАК СтавкаНДС
		|	ИЗ
		|		ДанныеДокументаШапка КАК ДанныеДокумента
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|	ПО
		|		ИСТИНА
		|";
	
		МассивТекстовПодзапроса.Добавить(ТекстПодзапросаЗапроса);
		
	КонецЕсли;
	
	Если (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации)
		И ДокументОбъект.ПроведеноБанком Тогда
		
		ТекстПодзапросаЗапроса = "
		|// Операции по поступлению денежных средств от другой организации
		|	ВЫБРАТЬ
		|		&Период КАК Период,
		|		ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|		ДанныеДокумента.Организация КАК Организация,
		|		НЕОПРЕДЕЛЕНО КАК Подразделение,
		|
		|		ДанныеДокумента.БанковскийСчет КАК ДенежныеСредства,
		|		ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|		ТаблицаРасшифровкаПлатежа.СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
		|		ДанныеДокумента.Валюта КАК ВалютаПлатежа,
		|
		|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
		|		ДанныеДокумента.БанковскийСчетОтправительВладелец КАК Контрагент,
		|		ЕСТЬNULL(ТаблицаРасшифровкаПлатежа.ОбъектРасчетов.НаправлениеДеятельности, ДанныеДокумента.НаправлениеДеятельности) КАК НаправлениеДеятельности,
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов.Договор КАК Договор,
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК ОбъектРасчетов,
		|	
		|		ТаблицаРасшифровкаПлатежа.Сумма * &КоэффициентПересчетаВВалютуУпр КАК СуммаОплаты,
		|		ТаблицаРасшифровкаПлатежа.Сумма * &КоэффициентПересчетаВВалютуРегл КАК СуммаОплатыРегл,
		|		ТаблицаРасшифровкаПлатежа.Сумма КАК СуммаОплатыВВалютеПлатежа,
		|
		|		0 КАК СуммаПостоплаты,
		|		0 КАК СуммаПостоплатыРегл,
		|		0 КАК СуммаПостоплатыВВалютеПлатежа,
		|		
		|		0 КАК СуммаПредоплаты,
		|		0 КАК СуммаПредоплатыРегл,
		|		0 КАК СуммаПредоплатыВВалютеПлатежа,
		|
		|		0 КАК СуммаНДС,
		|		0 КАК СуммаНДСРегл,
		|		0 КАК СуммаНДСВВалютеПлатежа,
		|
		|		ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|
		|		ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов КАК СуммаОплатыВВалютеВзаиморасчетов,
		|		0 КАК СуммаПостоплатыВВалютеВзаиморасчетов,
		|		0 КАК СуммаПредоплатыВВалютеВзаиморасчетов,
		|
		|		ДанныеДокумента.БанковскийСчет КАК ИсточникГФУДенежныхСредств,
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК ИсточникГФУРасчетов,
		|		НЕОПРЕДЕЛЕНО КАК СтавкаНДС
		|	ИЗ
		|		ДанныеДокументаШапка КАК ДанныеДокумента
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|	ПО
		|		ИСТИНА
		|";
	
		МассивТекстовПодзапроса.Добавить(ТекстПодзапросаЗапроса);
		
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств
		И ДокументОбъект.ПроведеноБанком Тогда
		
		ТекстПодзапросаЗапроса = "
		|// Операции по внутренней передаче денежных средств
		|	ВЫБРАТЬ
		|		&Период КАК Период,
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВнутреннееПоступлениеДенежныхСредств) КАК ХозяйственнаяОперация,
		|		ДанныеДокумента.Организация КАК Организация,
		|		НЕОПРЕДЕЛЕНО КАК Подразделение,
		|
		|		ДанныеДокумента.БанковскийСчет КАК ДенежныеСредства,
		|		&СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|		&СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
		|		ДанныеДокумента.Валюта КАК ВалютаПлатежа,
		|
		|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
		|		ДанныеДокумента.БанковскийСчетОтправительВладелец КАК Контрагент,
		|		ДанныеДокумента.БанковскийСчетОтправительНаправлениеДеятельности КАК НаправлениеДеятельности,
		|		НЕОПРЕДЕЛЕНО КАК Договор,
		|		НЕОПРЕДЕЛЕНО КАК ОбъектРасчетов,
		|	
		|		ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК СуммаОплаты,
		|		ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл КАК СуммаОплатыРегл,
		|		ДанныеДокумента.СуммаДокумента КАК СуммаОплатыВВалютеПлатежа,
		|
		|		0 КАК СуммаПостоплаты,
		|		0 КАК СуммаПостоплатыРегл,
		|		0 КАК СуммаПостоплатыВВалютеПлатежа,
		|		
		|		0 КАК СуммаПредоплаты,
		|		0 КАК СуммаПредоплатыРегл,
		|		0 КАК СуммаПредоплатыВВалютеПлатежа,
		|
		|		0 КАК СуммаНДС,
		|		0 КАК СуммаНДСРегл,
		|		0 КАК СуммаНДСВВалютеПлатежа,
		|
		|		ДанныеДокумента.Валюта КАК ВалютаВзаиморасчетов,
		|
		|		ДанныеДокумента.СуммаДокумента КАК СуммаОплатыВВалютеВзаиморасчетов,
		|		0 КАК СуммаПостоплатыВВалютеВзаиморасчетов,
		|		0 КАК СуммаПредоплатыВВалютеВзаиморасчетов,
		|
		|		ДанныеДокумента.БанковскийСчет КАК ИсточникГФУДенежныхСредств,
		|		НЕОПРЕДЕЛЕНО КАК ИсточникГФУРасчетов,
		|		НЕОПРЕДЕЛЕНО КАК СтавкаНДС
		|	ИЗ
		|		ДанныеДокументаШапка КАК ДанныеДокумента
		|";
	
		МассивТекстовПодзапроса.Добавить(ТекстПодзапросаЗапроса);
		
	КонецЕсли;
	
	Если (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам
			ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным)
		И ДокументОбъект.ПроведеноБанком Тогда
		
		ТекстПодзапросаЗапроса = "
		|// Операции поступления по кредитам и займам выданным
		|	ВЫБРАТЬ
		|		&Период КАК Период,
		|		ВЫБОР
		|			КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным)
		|				  И (ТаблицаРасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСПоступленияВыдачи = ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств
		|					 ИЛИ ТаблицаРасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСОсновногоДолга = ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств)
		|				ТОГДА Значение(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным)
		|			КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным)
		|				  И (ТаблицаРасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСПроцентов = ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств
		|					 ИЛИ ТаблицаРасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСКомиссии = ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств)
		|				ТОГДА Значение(Перечисление.ХозяйственныеОперации.ПоступлениеПроцентовПоЗаймамВыданным)
		|			ИНАЧЕ &ХозяйственнаяОперация
		|		КОНЕЦ  КАК ХозяйственнаяОперация,
		|		ДанныеДокумента.Организация КАК Организация,
		|		ТаблицаРасшифровкаПлатежа.ДоговорКредитаДепозита.Подразделение КАК Подразделение,
		|
		|		ДанныеДокумента.БанковскийСчет КАК ДенежныеСредства,
		|		ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|		ТаблицаРасшифровкаПлатежа.СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
		|		ДанныеДокумента.Валюта КАК ВалютаПлатежа,
		|
		|		ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
		|		ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
		|		ТаблицаРасшифровкаПлатежа.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		ТаблицаРасшифровкаПлатежа.ДоговорКредитаДепозита КАК Договор,
		|		НЕОПРЕДЕЛЕНО КАК ОбъектРасчетов,
		|	
		|		ТаблицаРасшифровкаПлатежа.СуммаУпр КАК СуммаОплаты,
		|		ТаблицаРасшифровкаПлатежа.СуммаРегл КАК СуммаОплатыРегл,
		|		ЕСТЬNULL(ТаблицаРасшифровкаПлатежа.Сумма, ДанныеДокумента.СуммаДокумента) КАК СуммаОплатыВВалютеПлатежа,
		|
		|		0 КАК СуммаПостоплаты,
		|		0 КАК СуммаПостоплатыРегл,
		|		0 КАК СуммаПостоплатыВВалютеПлатежа,
		|		
		|		0 КАК СуммаПредоплаты,
		|		0 КАК СуммаПредоплатыРегл,
		|		0 КАК СуммаПредоплатыВВалютеПлатежа,
		|
		|		0 КАК СуммаНДС,
		|		0 КАК СуммаНДСРегл,
		|		0 КАК СуммаНДСВВалютеПлатежа,
		|
		|		ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|
		|		ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов КАК СуммаОплатыВВалютеВзаиморасчетов,
		|		0 КАК СуммаПостоплатыВВалютеВзаиморасчетов,
		|		0 КАК СуммаПредоплатыВВалютеВзаиморасчетов,
		|
		|		ДанныеДокумента.БанковскийСчет КАК ИсточникГФУДенежныхСредств,
		|		ТаблицаРасшифровкаПлатежа.ДоговорКредитаДепозита КАК ИсточникГФУРасчетов,
		|		НЕОПРЕДЕЛЕНО КАК СтавкаНДС
		|	ИЗ
		|		ДанныеДокументаШапка КАК ДанныеДокумента
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|	ПО
		|		ИСТИНА
		|";
	
		МассивТекстовПодзапроса.Добавить(ТекстПодзапросаЗапроса);
		
	КонецЕсли;
	
	Если (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента
			ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика)
		И ДокументОбъект.ПроведеноБанком Тогда
		
		ТекстПодзапросаЗапроса = "
		|ВЫБРАТЬ 
		|		&Период КАК Период,
		|		&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|		ДанныеДокумента.Организация КАК Организация,
		|		ТаблицаРасшифровкаПлатежа.ПодразделениеЗаказа КАК Подразделение,
		|
		|		ДанныеДокумента.БанковскийСчет КАК ДенежныеСредства,
		|		ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|		ТаблицаРасшифровкаПлатежа.СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
		|		ДанныеДокумента.Валюта КАК ВалютаПлатежа,
		|
		|		ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
		|		ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
		|		ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов.Договор КАК Договор,
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК ОбъектРасчетов,
		|	
		|		0 КАК СуммаОплаты,
		|		0 КАК СуммаОплатыРегл,
		|		0 КАК СуммаОплатыВВалютеПлатежа,
		|
		|		0 КАК СуммаПостоплаты,
		|		0 КАК СуммаПостоплатыРегл,
		|		0 КАК СуммаПостоплатыВВалютеПлатежа,
		|		
		|		0 КАК СуммаПредоплаты,
		|		0 КАК СуммаПредоплатыРегл,
		|		0 КАК СуммаПредоплатыВВалютеПлатежа,
		|
		|		ТаблицаРасшифровкаПлатежа.СуммаНДСУпр КАК СуммаНДС,
		|		ТаблицаРасшифровкаПлатежа.СуммаНДСРегл КАК СуммаНДСРегл,
		|		ТаблицаРасшифровкаПлатежа.СуммаНДС КАК СуммаНДСВВалютеПлатежа,
		|
		|		ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|
		|		0 КАК СуммаОплатыВВалютеВзаиморасчетов,
		|		0 КАК СуммаПостоплатыВВалютеВзаиморасчетов,
		|		0 КАК СуммаПредоплатыВВалютеВзаиморасчетов,
		|
		|		ДанныеДокумента.БанковскийСчет КАК ИсточникГФУДенежныхСредств,
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК ИсточникГФУРасчетов,
		|		ТаблицаРасшифровкаПлатежа.СтавкаНДС КАК СтавкаНДС
		|	ИЗ
		|		ДанныеДокументаШапка КАК ДанныеДокумента
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|	ПО
		|		ИСТИНА
		|	ГДЕ
		|		ТаблицаРасшифровкаПлатежа.СуммаНДС <> 0";
	
		МассивТекстовПодзапроса.Добавить(ТекстПодзапросаЗапроса);
		
	КонецЕсли;
	
	Если (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации
			ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации)
		И ДокументОбъект.ПроведеноБанком Тогда
		
		ТекстПодзапросаЗапроса = "
		|ВЫБРАТЬ
		|		&Период КАК Период,
		|		ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|		ДанныеДокумента.Организация КАК Организация,
		|		НЕОПРЕДЕЛЕНО КАК Подразделение,
		|
		|		ДанныеДокумента.БанковскийСчет КАК ДенежныеСредства,
		|		ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|		ТаблицаРасшифровкаПлатежа.СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
		|		ДанныеДокумента.Валюта КАК ВалютаПлатежа,
		|
		|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
		|		ДанныеДокумента.БанковскийСчетОтправитель.Владелец КАК Контрагент,
		|		ТаблицаРасшифровкаПлатежа.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов.Договор КАК Договор,
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК ОбъектРасчетов,
		|	
		|		0 КАК СуммаОплаты,
		|		0 КАК СуммаОплатыРегл,
		|		0 КАК СуммаОплатыВВалютеПлатежа,
		|
		|		0 КАК СуммаПостоплаты,
		|		0 КАК СуммаПостоплатыРегл,
		|		0 КАК СуммаПостоплатыВВалютеПлатежа,
		|		
		|		0 КАК СуммаПредоплаты,
		|		0 КАК СуммаПредоплатыРегл,
		|		0 КАК СуммаПредоплатыВВалютеПлатежа,
		|
		|		ТаблицаРасшифровкаПлатежа.СуммаНДСУпр КАК СуммаНДС,
		|		ТаблицаРасшифровкаПлатежа.СуммаНДСРегл КАК СуммаНДСРегл,
		|		ТаблицаРасшифровкаПлатежа.СуммаНДС КАК СуммаНДСВВалютеПлатежа,
		|
		|		ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|
		|		0 КАК СуммаОплатыВВалютеВзаиморасчетов,
		|		0 КАК СуммаПостоплатыВВалютеВзаиморасчетов,
		|		0 КАК СуммаПредоплатыВВалютеВзаиморасчетов,
		|
		|		ДанныеДокумента.БанковскийСчет КАК ИсточникГФУДенежныхСредств,
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК ИсточникГФУРасчетов,
		|		ТаблицаРасшифровкаПлатежа.СтавкаНДС КАК СтавкаНДС
		|	ИЗ
		|		ДанныеДокументаШапка КАК ДанныеДокумента
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|	ПО
		|		ИСТИНА
		|	ГДЕ
		|		ТаблицаРасшифровкаПлатежа.СуммаНДС <> 0";
	
		МассивТекстовПодзапроса.Добавить(ТекстПодзапросаЗапроса);
		
	КонецЕсли;
	
	Если МассивТекстовПодзапроса.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстПодзапросаЗапроса = СтрСоединить(МассивТекстовПодзапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период КАК Период,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|
	|	ДанныеДокумента.ДенежныеСредства КАК ДенежныеСредства,
	|	&НаправлениеДеятельностиДС КАК НаправлениеДеятельностиДС,
	|	Значение(Перечисление.ТипыДенежныхСредств.Безналичные) КАК ТипДенежныхСредств,
	|	ДанныеДокумента.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ДанныеДокумента.СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
	|	ДанныеДокумента.ВалютаПлатежа КАК ВалютаПлатежа,
	|
	|	ДанныеДокумента.Партнер КАК Партнер,
	|	ДанныеДокумента.Контрагент КАК Контрагент,
	|	ВЫБОР КОГДА &ХозяйственнаяОперация В (
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации))
	|				И ЕСТЬNULL(ДанныеДокумента.НаправлениеДеятельности.УчетРасчетовСПоставщиками, ЛОЖЬ)
	|			ИЛИ НЕ &ХозяйственнаяОперация В (
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации))
	|				И ЕСТЬNULL(ДанныеДокумента.НаправлениеДеятельности.УчетДоходов, ЛОЖЬ)
	|		ТОГДА
	|			ДанныеДокумента.НаправлениеДеятельности
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ КАК НаправлениеДеятельностиКонтрагента,
	|	ДанныеДокумента.Договор КАК Договор,
	|	ДанныеДокумента.ОбъектРасчетов КАК ОбъектРасчетов,
	|	НЕОПРЕДЕЛЕНО КАК РасчетныйДокумент,
	|	
	|	СУММА(ДанныеДокумента.СуммаОплаты) КАК СуммаОплаты,
	|	СУММА(ДанныеДокумента.СуммаОплатыРегл) КАК СуммаОплатыРегл,
	|	СУММА(ДанныеДокумента.СуммаОплатыВВалютеПлатежа) КАК СуммаОплатыВВалютеПлатежа,
	|
	|	СУММА(ДанныеДокумента.СуммаПостоплаты) КАК СуммаПостоплаты,
	|	СУММА(ДанныеДокумента.СуммаПостоплатыРегл) КАК СуммаПостоплатыРегл,
	|	СУММА(ДанныеДокумента.СуммаПостоплатыВВалютеПлатежа) КАК СуммаПостоплатыВВалютеПлатежа,
	|
	|	СУММА(ДанныеДокумента.СуммаПредоплаты) КАК СуммаПредоплаты,
	|	СУММА(ДанныеДокумента.СуммаПредоплатыРегл) КАК СуммаПредоплатыРегл,
	|	СУММА(ДанныеДокумента.СуммаПредоплатыВВалютеПлатежа) КАК СуммаПредоплатыВВалютеПлатежа,
	|
	|	СУММА(ДанныеДокумента.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ДанныеДокумента.СуммаНДСРегл) КАК СуммаНДСРегл,
	|	СУММА(ДанныеДокумента.СуммаНДСВВалютеПлатежа) КАК СуммаНДСВВалютеПлатежа,
	|
	|	ДанныеДокумента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|
	|	СУММА(ДанныеДокумента.СуммаОплатыВВалютеВзаиморасчетов) КАК СуммаОплатыВВалютеВзаиморасчетов,
	|	СУММА(ДанныеДокумента.СуммаПостоплатыВВалютеВзаиморасчетов) КАК СуммаПостоплатыВВалютеВзаиморасчетов,
	|	СУММА(ДанныеДокумента.СуммаПредоплатыВВалютеВзаиморасчетов) КАК СуммаПредоплатыВВалютеВзаиморасчетов,
	|
	|	ДанныеДокумента.ИсточникГФУДенежныхСредств КАК ИсточникГФУДенежныхСредств,
	|	ДанныеДокумента.ИсточникГФУРасчетов КАК ИсточникГФУРасчетов,
	|	ДанныеДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ЛОЖЬ КАК ОтложенноеПроведение
	|
	|ИЗ &ТекстВложенногоЗапроса КАК ДанныеДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Период,
	|	ДанныеДокумента.ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.Подразделение,
	|	ДанныеДокумента.ДенежныеСредства,
	|	ДанныеДокумента.СтатьяДвиженияДенежныхСредств,
	|	ДанныеДокумента.СтатьяЦелевыхСредств,
	|	ДанныеДокумента.ВалютаПлатежа,
	|	ДанныеДокумента.Партнер,
	|	ДанныеДокумента.Контрагент,
	|	ВЫБОР КОГДА &ХозяйственнаяОперация В (
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации))
	|				И ЕСТЬNULL(ДанныеДокумента.НаправлениеДеятельности.УчетРасчетовСПоставщиками, ЛОЖЬ)
	|			ИЛИ НЕ &ХозяйственнаяОперация В (
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации))
	|				И ЕСТЬNULL(ДанныеДокумента.НаправлениеДеятельности.УчетДоходов, ЛОЖЬ)
	|		ТОГДА
	|			ДанныеДокумента.НаправлениеДеятельности
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ,
	|	ДанныеДокумента.Договор,
	|	ДанныеДокумента.ОбъектРасчетов,
	|	ДанныеДокумента.ВалютаВзаиморасчетов,
	|	ДанныеДокумента.ИсточникГФУДенежныхСредств,
	|	ДанныеДокумента.ИсточникГФУРасчетов,
	|	ДанныеДокумента.СтавкаНДС
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстВложенногоЗапроса", "(" + ТекстПодзапросаЗапроса + ")");
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияДенежныеСредстваДоходыРасходы(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)
	
	ИмяРегистра = "ДвиженияДенежныеСредстваДоходыРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаРасшифровкаПлатежа", ТекстыЗапроса) Тогда
		ТекстЗапросаВременнаяТаблицаРасшифровкаПлатежа(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаКурсовыхРазниц", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаКурсовыхРазниц(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли;
	
	МассивТекстовЗапроса = Новый Массив;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств 
		И ДокументОбъект.ПроведеноБанком Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ДанныеПлатежногоДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ДанныеПлатежногоДокумента.Организация КАК Организация,
		|	ТаблицаРасшифровкаПлатежа.Подразделение КАК Подразделение,
		|	&НаправлениеДеятельностиДС КАК НаправлениеДеятельностиДС,
		|	ТаблицаРасшифровкаПлатежа.Подразделение КАК ПодразделениеДоходовРасходов,
		|
		|	ДанныеПлатежногоДокумента.БанковскийСчет КАК ДенежныеСредства,
		|	Значение(Перечисление.ТипыДенежныхСредств.Безналичные) КАК ТипДенежныхСредств,
		|	ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ТаблицаРасшифровкаПлатежа.СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
		|	ДанныеПлатежногоДокумента.Валюта КАК Валюта,
		|
		|	ТаблицаРасшифровкаПлатежа.НаправлениеДеятельности КАК НаправлениеДеятельностиСтатьи,
		|	ТаблицаРасшифровкаПлатежа.СтатьяДоходов КАК СтатьяДоходовРасходов,
		|	ТаблицаРасшифровкаПлатежа.АналитикаДоходов КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	ТаблицаРасшифровкаПлатежа.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
		|
		|	ТаблицаРасшифровкаПлатежа.СуммаУпр КАК Сумма,
		|	ТаблицаРасшифровкаПлатежа.СуммаРегл КАК СуммаРегл,
		|	ТаблицаРасшифровкаПлатежа.Сумма КАК СуммаВВалюте,
		|
		|	0 КАК СуммаНДС,
		|	0 КАК СуммаНДСРегл,
		|	0 КАК СуммаНДСВВалютеПлатежа,
		|
		|	ДанныеПлатежногоДокумента.БанковскийСчет КАК ИсточникГФУДенежныхСредств,
		|	ТаблицаРасшифровкаПлатежа.СтатьяДоходов КАК ИсточникГФУДоходовРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС
		|
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеПлатежногоДокумента
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|	ПО
		|		ИСТИНА
		|";
	
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте 
		И ДокументОбъект.ПроведеноБанком И ДокументОбъект.ОтражатьКомиссию И ДокументОбъект.СуммаКомиссии <> 0 Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КомиссияПоЭквайрингу) КАК ХозяйственнаяОперация,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ДанныеДокумента.Подразделение КАК Подразделение,
		|	&НаправлениеДеятельностиДС КАК НаправлениеДеятельностиДС,
		|	ДанныеДокумента.Подразделение КАК ПодразделениеДоходовРасходов,
		|
		|	ДанныеДокумента.БанковскийСчет КАК ДенежныеСредства,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваУЭквайера) КАК ТипДенежныхСредств,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ДанныеДокумента.ДоговорЭквайрингаСтатьяДвиженияДенежныхСредствКомиссия, ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка) ТОГДА
		|			ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ОплатаПоставщику)
		|		ИНАЧЕ
		|			ДанныеДокумента.ДоговорЭквайрингаСтатьяДвиженияДенежныхСредствКомиссия
		|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств,
		|	&СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
		
		|	ДанныеДокумента.Валюта КАК Валюта,
		|
		|	ДанныеДокумента.ДоговорЭквайрингаНаправлениеДеятельности КАК НаправлениеДеятельностиСтатьи,
		|	ДанныеДокумента.СтатьяРасходов КАК СтатьяДоходовРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
		|	ДанныеДокумента.АналитикаРасходов КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|
		|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаКомиссии * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК Сумма,
		|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаКомиссии * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаРегл,
		|	ДанныеДокумента.СуммаКомиссии КАК СуммаВВалюте,
		|
		|	0 КАК СуммаНДС,
		|	0 КАК СуммаНДСРегл,
		|	0 КАК СуммаНДСВВалютеПлатежа,
		|
		|	ДанныеДокумента.БанковскийСчет КАК ИсточникГФУДенежныхСредств,
		|	ДанныеДокумента.СтатьяРасходов КАК ИсточникГФУДоходовРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС
		|
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	КонецЕсли;
		
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты Тогда
	
		ТекстЗапроса = "
		|// Расчет доходов от курсовых разниц
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСПрибыль) КАК ХозяйственнаяОперация,
		|	&Организация КАК Организация,
		|	ТаблицаКурсовыхРазниц.Подразделение КАК Подразделение,
		|	&НаправлениеДеятельностиДС,
		|	ТаблицаКурсовыхРазниц.Подразделение КАК ПодразделениеДоходовРасходов,
		|
		|	&Контрагент КАК ДенежныеСредства,
		|	Значение(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваВПути) КАК ТипДенежныхСредств,
		|	&СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	&СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
		|	&ВалютаКонвертации КАК Валюта,
		|
		|	&НаправлениеДеятельностиДС КАК НаправлениеДеятельностиСтатьи,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ДоходыПриКонвертацииВалюты) КАК СтатьяДоходовРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства) КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	
		|	ТаблицаКурсовыхРазниц.СуммаУпр КАК Сумма,
		|	ТаблицаКурсовыхРазниц.СуммаРегл КАК СуммаРегл,
		|	0 КАК СуммаВВалюте,
		|
		|	0 КАК СуммаНДС,
		|	0 КАК СуммаНДСРегл,
		|	0 КАК СуммаНДСВВалютеПлатежа,
		|
		|	НЕОПРЕДЕЛЕНО КАК ИсточникГФУДенежныхСредств,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ДоходыПриКонвертацииВалюты) КАК ИсточникГФУДоходовРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС
		|ИЗ
		|	ТаблицаКурсовыхРазниц КАК ТаблицаКурсовыхРазниц
		|
		|ГДЕ
		|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты)
		|	И (ТаблицаКурсовыхРазниц.СуммаУпр > 0 ИЛИ ТаблицаКурсовыхРазниц.СуммаРегл > 0)";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты Тогда
	
		ТекстЗапроса = "
		|// Расчет расходов от курсовых разниц
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСУбыток) КАК ХозяйственнаяОперация,
		|	&Организация КАК Организация,
		|	ТаблицаКурсовыхРазниц.Подразделение КАК Подразделение,
		|	&НаправлениеДеятельностиДС,
		|	ТаблицаКурсовыхРазниц.Подразделение КАК ПодразделениеДоходовРасходов,
		|
		|	&Контрагент КАК ДенежныеСредства,
		|	Значение(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваВПути) КАК ТипДенежныхСредств,
		|	&СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	&СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
		|	&ВалютаКонвертации КАК Валюта,
		|
		|	&НаправлениеДеятельностиДС КАК НаправлениеДеятельностиСтатьи,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.РасходыПриКонвертацииВалюты) КАК СтатьяДоходовРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
		|	ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства) КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|
		|	-ТаблицаКурсовыхРазниц.СуммаУпр КАК Сумма,
		|	-ТаблицаКурсовыхРазниц.СуммаРегл КАК СуммаРегл,
		|	0 КАК СуммаВВалюте,
		|
		|	0 КАК СуммаНДС,
		|	0 КАК СуммаНДСРегл,
		|	0 КАК СуммаНДСВВалютеПлатежа,
		|
		|	НЕОПРЕДЕЛЕНО КАК ИсточникГФУДенежныхСредств,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.РасходыПриКонвертацииВалюты) КАК ИсточникГФУДоходовРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС
		|
		|ИЗ
		|	ТаблицаКурсовыхРазниц КАК ТаблицаКурсовыхРазниц
		|
		|ГДЕ
		|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты)
		|	И (ТаблицаКурсовыхРазниц.СуммаУпр < 0 ИЛИ ТаблицаКурсовыхРазниц.СуммаРегл < 0)";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленныхДС
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленнойЗарплатыПоЗарплатномуПроекту)
		И ДокументОбъект.ПроведеноБанком Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	&Организация КАК Организация,
		|	ВЫБОР 
		|		КОГДА ДанныеПлатежногоДокумента.ДокументВыдачи ССЫЛКА Документ.РасходныйКассовыйОрдер
		|			ТОГДА ВЫРАЗИТЬ(ДанныеПлатежногоДокумента.ДокументВыдачи КАК Документ.РасходныйКассовыйОрдер).Подразделение
		|		КОГДА ДанныеПлатежногоДокумента.ДокументВыдачи ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств
		|			ТОГДА ВЫРАЗИТЬ(ДанныеПлатежногоДокумента.ДокументВыдачи КАК Документ.СписаниеБезналичныхДенежныхСредств).Подразделение
		|	КОНЕЦ КАК Подразделение,
		|	&НаправлениеДеятельностиДС,
		|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДоходовРасходов,
		|
		|	&БанковскийСчет КАК ДенежныеСредства,
		|	Значение(Перечисление.ТипыДенежныхСредств.Безналичные) КАК ТипДенежныхСредств,
		|	ЕСТЬNULL(ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств, ДанныеПлатежногоДокумента.СтатьяДвиженияДенежныхСредств) КАК СтатьяДвиженияДенежныхСредств,
		|	ЕСТЬNULL(ТаблицаРасшифровкаПлатежа.СтатьяЦелевыхСредств, ДанныеПлатежногоДокумента.СтатьяЦелевыхСредств) КАК СтатьяЦелевыхСредств,
		|	&Валюта КАК Валюта,
		|
		|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиСтатьи,
		|	ЕСТЬNULL(ТаблицаРасшифровкаПлатежа.СтатьяДоходов, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда)) КАК СтатьяДоходовРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	
		|	СУММА(ВЫРАЗИТЬ(ЕСТЬNULL(ТаблицаРасшифровкаПлатежа.Сумма, ДанныеПлатежногоДокумента.СуммаДокумента) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК Сумма,
		|	СУММА(ВЫРАЗИТЬ(ЕСТЬNULL(ТаблицаРасшифровкаПлатежа.Сумма, ДанныеПлатежногоДокумента.СуммаДокумента) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))) КАК СуммаРегл,
		|	СУММА(ЕСТЬNULL(ТаблицаРасшифровкаПлатежа.Сумма, ДанныеПлатежногоДокумента.СуммаДокумента)) КАК СуммаВВалюте,
		|
		|	0 КАК СуммаНДС,
		|	0 КАК СуммаНДСРегл,
		|	0 КАК СуммаНДСВВалютеПлатежа,
		|
		|	&БанковскийСчет КАК ИсточникГФУДенежныхСредств,
		|	НЕОПРЕДЕЛЕНО КАК ИсточникГФУДоходовРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеПлатежногоДокумента
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|	ПО
		|		ИСТИНА
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
		|		ПО ДанныеПлатежногоДокумента.ДокументВыдачи = СписаниеБезналичныхДенежныхСредств.Ссылка
		|ГДЕ
		|	НЕ ЕСТЬNULL(СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)) =
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств,
		|	ДанныеПлатежногоДокумента.СтатьяДвиженияДенежныхСредств,
		|	ТаблицаРасшифровкаПлатежа.СтатьяЦелевыхСредств,
		|	ДанныеПлатежногоДокумента.СтатьяЦелевыхСредств,
		|	ТаблицаРасшифровкаПлатежа.СтатьяДоходов,
		|	ТаблицаРасшифровкаПлатежа.АналитикаАктивовПассивов,
		|	ДанныеПлатежногоДокумента.ДокументВыдачи,
		|	ДанныеПлатежногоДокумента.БанковскийСчет,
		|	ДанныеПлатежногоДокумента.Подразделение";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПогашениеЗаймаСотрудником
		И ДокументОбъект.ПроведеноБанком Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	&Организация КАК Организация,
		|	&Подразделение КАК Подразделение,
		|	&НаправлениеДеятельностиДС,
		|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДоходовРасходов,
		|
		|	&БанковскийСчет КАК ДенежныеСредства,
		|	Значение(Перечисление.ТипыДенежныхСредств.Безналичные) КАК ТипДенежныхСредств,
		|	ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	&Валюта КАК Валюта,
		|
		|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиСтатьи,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗаймыВыданные) КАК СтатьяДоходовРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	
		|	СУММА(ВЫРАЗИТЬ(ТаблицаРасшифровкаПлатежа.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))) КАК Сумма,
		|	СУММА(ВЫРАЗИТЬ(ТаблицаРасшифровкаПлатежа.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))) КАК СуммаРегл,
		|	СУММА(ТаблицаРасшифровкаПлатежа.Сумма) КАК СуммаВВалюте,
		|
		|	0 КАК СуммаНДС,
		|	0 КАК СуммаНДСРегл,
		|	0 КАК СуммаНДСВВалютеПлатежа,
		|
		|	&БанковскийСчет КАК ИсточникГФУДенежныхСредств,
		|	НЕОПРЕДЕЛЕНО КАК ИсточникГФУДоходовРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС
		|ИЗ
		|	ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|ГДЕ
		|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПогашениеЗаймаСотрудником)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНалогов
		И ДокументОбъект.ПроведеноБанком Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ДанныеПлатежногоДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ДанныеПлатежногоДокумента.Организация КАК Организация,
		|	ДанныеПлатежногоДокумента.Подразделение КАК Подразделение,
		|	&НаправлениеДеятельностиДС КАК НаправлениеДеятельностиДС,
		|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДоходовРасходов,
		|
		|	ДанныеПлатежногоДокумента.БанковскийСчет КАК ДенежныеСредства,
		|	Значение(Перечисление.ТипыДенежныхСредств.Безналичные) КАК ТипДенежныхСредств,
		|	ТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ТаблицаРасшифровкаПлатежа.СтатьяЦелевыхСредств КАК СтатьяЦелевыхСредств,
		|	ДанныеПлатежногоДокумента.Валюта КАК Валюта,
		|
		|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиСтатьи,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) КАК СтатьяДоходовРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	ДанныеПлатежногоДокумента.ТипНалога КАК АналитикаАктивовПассивов,
		|
		|	ТаблицаРасшифровкаПлатежа.СуммаУпр КАК Сумма,
		|	ТаблицаРасшифровкаПлатежа.СуммаРегл КАК СуммаРегл,
		|	ТаблицаРасшифровкаПлатежа.Сумма КАК СуммаВВалюте,
		|
		|	0 КАК СуммаНДС,
		|	0 КАК СуммаНДСРегл,
		|	0 КАК СуммаНДСВВалютеПлатежа,
		|
		|	ДанныеПлатежногоДокумента.БанковскийСчет КАК ИсточникГФУДенежныхСредств,
		|	НЕОПРЕДЕЛЕНО КАК ИсточникГФУДоходовРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС
		|
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеПлатежногоДокумента
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|	ПО
		|		ИСТИНА";
	
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
	КонецЕсли;
	
	Если МассивТекстовЗапроса.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВременнаяТаблицаДвиженияКонтрагентКонтрагент(Запрос, ТекстыЗапроса, ДокументОбъект)
	
	ИмяВременнойТаблицы = "ВтДвиженияКонтрагентКонтрагент";
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаРасшифровкаПлатежа", ТекстыЗапроса) Тогда
		ТекстЗапросаВременнаяТаблицаРасшифровкаПлатежа(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	МассивТестовПодзапроса = Новый Массив;
	
	Если (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента 
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика)
		И ДокументОбъект.ПроведеноБанком Тогда

		ТекстПодзапроса = "
		|// Списали задолженность клиента преред филиалом
		|	ВЫБРАТЬ
		|		&Период КАК Период,
		|		ВЫБОР КОГДА &ХозяйственнаяОперация В (
		|						ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента),
		|						ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации))
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПлатежаИзФилиала)
		|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности)
		|		КОНЕЦ КАК ХозяйственнаяОперация,//Дебиторская
		|		ТаблицаРасшифровкаПлатежа.Организация КАК Организация,
		|		ТаблицаРасшифровкаПлатежа.ПодразделениеЗаказа КАК Подразделение,
		|
		|		ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
		|		ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
		|		ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам.Договор КАК Договор,
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК ОбъектРасчетов,
		|
		|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК КорПартнер,
		|		ДанныеДокумента.Организация КАК КорКонтрагент,
		|		&НаправлениеДеятельностиДС КАК КорНаправлениеДеятельности,
		|		ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам.Договор КАК КорДоговор,
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК КорОбъектРасчетов,
		|	
		|		ТаблицаРасшифровкаПлатежа.СуммаУпр КАК Сумма,
		|		ТаблицаРасшифровкаПлатежа.СуммаРегл КАК СуммаРегл,
		|		ТаблицаРасшифровкаПлатежа.Сумма КАК СуммаВВалюте,
		|
		|		ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|		ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
		|
		|		ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов КАК КорВалютаВзаиморасчетов,
		|		ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов КАК КорСуммаВВалютеВзаиморасчетов,
		|
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК ИсточникГФУРасчетов,
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК КорИсточникГФУРасчетов
		|	ИЗ
		|		ДанныеДокументаШапка КАК ДанныеДокумента
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|			ПО ИСТИНА
		|	ГДЕ
		|		ДанныеДокумента.Организация <> ТаблицаРасшифровкаПлатежа.Организация
		|		И ТаблицаРасшифровкаПлатежа.Организация <> НЕОПРЕДЕЛЕНО
		|		"; 
		
		МассивТестовПодзапроса.Добавить(ТекстПодзапроса);
	
	КонецЕсли;
	
	Если (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента 
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика)
		И ДокументОбъект.ПроведеноБанком Тогда

		ТекстПодзапроса = "
		|// Передали задолженность клиента в организацию
		|	ВЫБРАТЬ
		|		&Период КАК Период,
		|		ВЫБОР КОГДА &ХозяйственнаяОперация В (
		|						ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента),
		|						ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации))
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемПлатежаВФилиале)
		|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности)
		|		КОНЕЦ КАК ХозяйственнаяОперация,//Кредиторская
		|		ДанныеДокумента.Организация КАК Организация,
		|		ТаблицаРасшифровкаПлатежа.ПодразделениеЗаказа КАК Подразделение,
		|
		|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
		|		ТаблицаРасшифровкаПлатежа.Организация КАК Контрагент,
		|		&НаправлениеДеятельностиДС КАК НаправлениеДеятельности,
		|		ТаблицаРасшифровкаПлатежа.Договор КАК Договор,
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК ОбъектРасчетов,
		|
		|		ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам.Партнер КАК КорПартнер,
		|		ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам.Контрагент КАК КорКонтрагент,
		|		ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
		|		ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам.Договор КАК КорДоговор,
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК КорОбъектРасчетов,
		|		
		|	
		|		ТаблицаРасшифровкаПлатежа.СуммаУпр КАК Сумма,
		|		ТаблицаРасшифровкаПлатежа.СуммаРегл КАК СуммаРегл,
		|		ТаблицаРасшифровкаПлатежа.Сумма КАК СуммаВВалюте,
		|
		|		ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|		ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
		|
		|		ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов КАК КорВалютаВзаиморасчетов,
		|		ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов КАК КорСуммаВВалютеВзаиморасчетов,
		|
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК ИсточникГФУРасчетов,
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК КорИсточникГФУРасчетов
		|	ИЗ
		|		ДанныеДокументаШапка КАК ДанныеДокумента
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|			ПО ИСТИНА
		|	ГДЕ
		|		ДанныеДокумента.Организация <> ТаблицаРасшифровкаПлатежа.Организация
		|		И ТаблицаРасшифровкаПлатежа.Организация <> НЕОПРЕДЕЛЕНО
		|		"; 

		МассивТестовПодзапроса.Добавить(ТекстПодзапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика
		И ДокументОбъект.ПроведеноБанком Тогда

		ТекстПодзапроса = "
		|// Возврат оплаты по курсу аванса
		|	ВЫБРАТЬ
		|		&Период КАК Период,
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности) КАК ХозяйственнаяОперация,
		|		ДанныеДокумента.Организация КАК Организация,
		|		ТаблицаРасшифровкаПлатежа.ПодразделениеЗаказа КАК Подразделение,
		|
		|		ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
		|		ДанныеДокумента.Организация КАК Контрагент,
		|		&НаправлениеДеятельностиДС КАК НаправлениеДеятельности,
		|		ТаблицаРасшифровкаПлатежа.Договор КАК Договор,
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК ОбъектРасчетов,
		|
		|		ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам.Партнер КАК КорПартнер,
		|		ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам.Контрагент КАК КорКонтрагент,
		|		ТаблицаРасшифровкаПлатежа.АналитикаУчетаПоПартнерам.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
		|		ДанныеДокумента.Договор КАК КорДоговор,
		|		ДанныеДокумента.ОбъектРасчетов КАК КорОбъектРасчетов,
		|		
		|	
		|		ТаблицаРасшифровкаПлатежа.СуммаУпр КАК Сумма,
		|		ТаблицаРасшифровкаПлатежа.СуммаРегл КАК СуммаРегл,
		|		ТаблицаРасшифровкаПлатежа.Сумма КАК СуммаВВалюте,
		|
		|		ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|		ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
		|
		|		ТаблицаРасшифровкаПлатежа.ВалютаВзаиморасчетов КАК КорВалютаВзаиморасчетов,
		|		ТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов КАК КорСуммаВВалютеВзаиморасчетов,
		|
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов КАК ИсточникГФУРасчетов,
		|		ДанныеДокумента.ОбъектРасчетов КАК КорИсточникГФУРасчетов
		|	ИЗ
		|		ДанныеДокументаШапка КАК ДанныеДокумента
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		ТаблицаРасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|			ПО ИСТИНА
		|	ГДЕ
		|		ТаблицаРасшифровкаПлатежа.ОбъектРасчетов.Объект <> &Ссылка"; 

		МассивТестовПодзапроса.Добавить(ТекстПодзапроса);
	
	КонецЕсли;
	
	ТекстПодзапроса = СтрСоединить(МассивТестовПодзапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период КАК Период,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|
	|	ДанныеДокумента.Партнер КАК Партнер,
	|	ДанныеДокумента.Контрагент КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДанныеДокумента.НаправлениеДеятельности.УчетРасчетовСПоставщиками, ЛОЖЬ) ТОГДА
	|			ДанныеДокумента.НаправлениеДеятельности 
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ДанныеДокумента.Договор КАК Договор,
	|	ДанныеДокумента.ОбъектРасчетов КАК ОбъектРасчетов,
	|
	|	ДанныеДокумента.КорПартнер КАК КорПартнер,
	|	ДанныеДокумента.КорКонтрагент КАК КорКонтрагент,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДанныеДокумента.КорНаправлениеДеятельности.УчетРасчетовСПоставщиками, ЛОЖЬ) ТОГДА
	|			ДанныеДокумента.КорНаправлениеДеятельности
	|	КОНЕЦ КАК КорНаправлениеДеятельности,
	|	ДанныеДокумента.КорДоговор КАК КорДоговор,
	|	ДанныеДокумента.КорОбъектРасчетов КАК КорОбъектРасчетов,
	|	&Валюта КАК Валюта,
	|
	|	СУММА(ДанныеДокумента.Сумма) КАК Сумма,
	|	СУММА(ДанныеДокумента.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ДанныеДокумента.СуммаВВалюте) КАК СуммаВВалюте,
	|
	|	ДанныеДокумента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	СУММА(ДанныеДокумента.СуммаВВалютеВзаиморасчетов) КАК СуммаВВалютеВзаиморасчетов,
	|	ДанныеДокумента.КорВалютаВзаиморасчетов КАК КорВалютаВзаиморасчетов,
	|	СУММА(ДанныеДокумента.КорСуммаВВалютеВзаиморасчетов) КАК КорСуммаВВалютеВзаиморасчетов,
	|
	|	ДанныеДокумента.ИсточникГФУРасчетов КАК ИсточникГФУРасчетов,
	|	ДанныеДокумента.КорИсточникГФУРасчетов КАК КорИсточникГФУРасчетов
	|ПОМЕСТИТЬ ВтДвиженияКонтрагентКонтрагент
	|
	|ИЗ &ТекстВложенногоЗапроса КАК ДанныеДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Период,
	|	ДанныеДокумента.ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.Подразделение,
	|	ДанныеДокумента.Партнер,
	|	ДанныеДокумента.Контрагент,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДанныеДокумента.НаправлениеДеятельности.УчетРасчетовСПоставщиками, ЛОЖЬ) ТОГДА
	|			ДанныеДокумента.НаправлениеДеятельности 
	|	КОНЕЦ,
	|	ДанныеДокумента.Договор,
	|	ДанныеДокумента.ОбъектРасчетов,
	|	ДанныеДокумента.КорПартнер,
	|	ДанныеДокумента.КорКонтрагент,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДанныеДокумента.КорНаправлениеДеятельности.УчетРасчетовСПоставщиками, ЛОЖЬ) ТОГДА
	|			ДанныеДокумента.КорНаправлениеДеятельности
	|	КОНЕЦ,
	|	ДанныеДокумента.КорДоговор,
	|	ДанныеДокумента.КорОбъектРасчетов,
	|	ДанныеДокумента.ВалютаВзаиморасчетов,
	|	ДанныеДокумента.КорВалютаВзаиморасчетов,
	|	ДанныеДокумента.ИсточникГФУРасчетов,
	|	ДанныеДокумента.КорИсточникГФУРасчетов
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстВложенногоЗапроса", "(" + ТекстПодзапроса + ")");
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяВременнойТаблицы);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияКонтрагентКонтрагент(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)
	
	ИмяРегистра = "ДвиженияКонтрагентКонтрагент";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ((ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента 
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика) 
			И ДокументОбъект.ПроведеноБанком) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтДвиженияКонтрагентКонтрагент", ТекстыЗапроса) Тогда
		ТекстЗапросаВременнаяТаблицаДвиженияКонтрагентКонтрагент(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДвиженияКонтрагентКонтрагент.Период                        КАК Период,
	|	ДвиженияКонтрагентКонтрагент.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|	ДвиженияКонтрагентКонтрагент.Организация                   КАК Организация,
	|	ДвиженияКонтрагентКонтрагент.Подразделение                 КАК Подразделение,
	|
	|	ДвиженияКонтрагентКонтрагент.Партнер                       КАК Партнер,
	|	ДвиженияКонтрагентКонтрагент.Контрагент                    КАК Контрагент,
	|	ДвиженияКонтрагентКонтрагент.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	ДвиженияКонтрагентКонтрагент.Договор                       КАК Договор,
	|	ДвиженияКонтрагентКонтрагент.ОбъектРасчетов                КАК ОбъектРасчетов,
	|
	|	ДвиженияКонтрагентКонтрагент.КорПартнер                    КАК КорПартнер,
	|	ДвиженияКонтрагентКонтрагент.КорКонтрагент                 КАК КорКонтрагент,
	|	ДвиженияКонтрагентКонтрагент.КорНаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	ДвиженияКонтрагентКонтрагент.КорДоговор                    КАК КорДоговор,
	|	ДвиженияКонтрагентКонтрагент.КорОбъектРасчетов             КАК КорОбъектРасчетов,
	|	ДвиженияКонтрагентКонтрагент.Валюта                        КАК Валюта,
	|
	|	ДвиженияКонтрагентКонтрагент.Сумма                         КАК Сумма,
	|	ДвиженияКонтрагентКонтрагент.СуммаРегл                     КАК СуммаРегл,
	|	ДвиженияКонтрагентКонтрагент.СуммаВВалюте                  КАК СуммаВВалюте,
	|
	|	ДвиженияКонтрагентКонтрагент.ВалютаВзаиморасчетов          КАК ВалютаВзаиморасчетов,
	|	ДвиженияКонтрагентКонтрагент.СуммаВВалютеВзаиморасчетов    КАК СуммаВВалютеВзаиморасчетов,
	|	ДвиженияКонтрагентКонтрагент.КорВалютаВзаиморасчетов       КАК КорВалютаВзаиморасчетов,
	|	ДвиженияКонтрагентКонтрагент.КорСуммаВВалютеВзаиморасчетов КАК КорСуммаВВалютеВзаиморасчетов,
	|
	|	ДвиженияКонтрагентКонтрагент.ИсточникГФУРасчетов           КАК ИсточникГФУРасчетов,
	|	ДвиженияКонтрагентКонтрагент.КорИсточникГФУРасчетов        КАК КорИсточникГФУРасчетов
	|
	|ИЗ
	|	ВтДвиженияКонтрагентКонтрагент КАК ДвиженияКонтрагентКонтрагент
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДенежныеСредстваКВыплате(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект = Неопределено)
	
	ИмяРегистра = "ДенежныеСредстваКВыплате";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ (ДокументОбъект = Неопределено 
		ИЛИ (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленныхДС)) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ДенежныеСредстваКВыплате.МоментВремени,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ДенежныеСредстваКВыплате.ЗаявкаНаРасходованиеДенежныхСредств,
	|	ДенежныеСредстваКВыплате.БанковскийСчетКасса,
	|	ДенежныеСредстваКВыплате.Получатель,
	|	ДенежныеСредстваКВыплате.Организация,
	|	-ДенежныеСредстваКВыплате.Сумма КАК Сумма,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ИЗ
	|	ДанныеДокументаШапка КАК ДанныеДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДенежныеСредстваКВыплате КАК ДенежныеСредстваКВыплате
	|	ПО
	|		ДенежныеСредстваКВыплате.Регистратор = ДанныеДокумента.ДокументВыдачи
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНеперечисленныхДС)
	|";
	
	Если ДокументОбъект = Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеДокументаШапка", "Документ.ПоступлениеБезналичныхДенежныхСредств");
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаЛимитыРасходаДенежныхСредств(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)
	
	ИмяРегистра = "ЛимитыРасходаДенежныхСредств";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленныхДС Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	Лимиты.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	Лимиты.Организация КАК Организация,
	|	Лимиты.Подразделение КАК Подразделение,
	|	- Лимиты.Лимит КАК Лимит,
	|	- Лимиты.РасходВПределахЛимита КАК РасходВПределахЛимита,
	|	- Лимиты.РасходСверхЛимита КАК РасходСверхЛимита
	|ИЗ
	|	РегистрНакопления.ЛимитыРасходаДенежныхСредств КАК Лимиты
	|ГДЕ
	|	Лимиты.Регистратор = &ДокументВыдачи
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНеперечисленныхДС)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект = Неопределено)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	МассивТекстовЗапроса = Новый Массив; 
	
	Если ДокументОбъект = Неопределено 
		ИЛИ (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленнойЗарплатыПоЗарплатномуПроекту
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНалогов
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПустаяСсылка()) Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&ИдентификаторМетаданных                КАК ТипСсылки,
		|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
		|	&Организация                            КАК Организация,
		|	&ПартнерИзДокумента                     КАК Партнер,
		|	&БанковскийСчет                         КАК МестоХранения,
		|	&КонтрагентИзДокумента                  КАК Контрагент,
		|	ДанныеДокумента.БанковскийСчетПодразделение КАК Подразделение,
		|	&Период                                 КАК ДатаДокументаИБ,
		|	&Ссылка                                 КАК Ссылка,
		|	&Номер                                  КАК НомерДокументаИБ,
		|	НЕОПРЕДЕЛЕНО                            КАК Статус,
		|	&Ответственный                          КАК Ответственный,
		|	&Автор                                  КАК Автор,
		|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
		|	&НазначениеПлатежа                      КАК Дополнительно,
		|	&Комментарий                            КАК Комментарий,
		|	&Проведен                               КАК Проведен,
		|	&ПометкаУдаления                        КАК ПометкаУдаления,
		|	&ДатаВходящегоДокумента                 КАК ДатаПервичногоДокумента,
		|	&НомерВходящегоДокумента                КАК НомерПервичногоДокумента,
		|	&СуммаДокумента                         КАК Сумма,
		|	&Валюта                                 КАК Валюта,
		|	&Договор                                КАК Договор,
		|	&Исправление                            КАК СторноИсправление,
		|	&СторнируемыйДокумент                   КАК СторнируемыйДокумент,
		|	&ИсправляемыйДокумент                   КАК ИсправляемыйДокумент,
		|	&НаправлениеДеятельности                КАК НаправлениеДеятельности,
		|	ВЫБОР КОГДА &ПроведеноБанком ТОГДА
		|		&Период
		|	ИНАЧЕ
		|		ДАТАВРЕМЯ(1,1,1)
		|	КОНЕЦ КАК ДатаОтраженияВУчете,
		|	НЕОПРЕДЕЛЕНО КАК Приоритет
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|	И &ХозяйственнаяОперация В (
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНеперечисленнойЗарплатыПоЗарплатномуПроекту),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНалогов),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
		|	)";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	
	Если ДокументОбъект = Неопределено 
		ИЛИ (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПогашениеЗаймаСотрудником
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленныхДС
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника) Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&ИдентификаторМетаданных                КАК ТипСсылки,
		|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
		|	&Организация                            КАК Организация,
		|	&Партнер                                КАК Партнер,
		|	&БанковскийСчет                         КАК МестоХранения,
		|	&ПодотчетноеЛицо                        КАК Контрагент,
		|	ДанныеДокумента.БанковскийСчетПодразделение КАК Подразделение,
		|	&Период                                 КАК ДатаДокументаИБ,
		|	&Ссылка                                 КАК Ссылка,
		|	&Номер                                  КАК НомерДокументаИБ,
		|	НЕОПРЕДЕЛЕНО                            КАК Статус,
		|	&Ответственный                          КАК Ответственный,
		|	&Автор                                  КАК Автор,
		|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
		|	&НазначениеПлатежа                      КАК Дополнительно,
		|	&Комментарий                            КАК Комментарий,
		|	&Проведен                               КАК Проведен,
		|	&ПометкаУдаления                        КАК ПометкаУдаления,
		|	&ДатаВходящегоДокумента                 КАК ДатаПервичногоДокумента,
		|	&НомерВходящегоДокумента                КАК НомерПервичногоДокумента,
		|	&СуммаДокумента                         КАК Сумма,
		|	&Валюта                                 КАК Валюта,
		|	&Договор                                КАК Договор,
		|	&Исправление                            КАК СторноИсправление,
		|	&СторнируемыйДокумент                   КАК СторнируемыйДокумент,
		|	&ИсправляемыйДокумент                   КАК ИсправляемыйДокумент,
		|	&НаправлениеДеятельности                КАК НаправлениеДеятельности,
		|	ВЫБОР КОГДА &ПроведеноБанком ТОГДА
		|		&Период
		|	ИНАЧЕ
		|		ДАТАВРЕМЯ(1,1,1)
		|	КОНЕЦ КАК ДатаОтраженияВУчете,
		|	НЕОПРЕДЕЛЕНО КАК Приоритет
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|	И &ХозяйственнаяОперация В (
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПогашениеЗаймаСотрудником),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНеперечисленныхДС),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника)
		|	)";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект = Неопределено 
			ИЛИ (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации
			ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации
			ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств) Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&ИдентификаторМетаданных                КАК ТипСсылки,
		|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
		|	&Организация                            КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) КАК Партнер,
		|	&БанковскийСчет                         КАК МестоХранения,
		|	&ОрганизацияОтправитель                 КАК Контрагент,
		|	ДанныеДокумента.БанковскийСчетПодразделение КАК Подразделение,
		|	&Период                                 КАК ДатаДокументаИБ,
		|	&Ссылка                                 КАК Ссылка,
		|	&Номер                                  КАК НомерДокументаИБ,
		|	НЕОПРЕДЕЛЕНО                            КАК Статус,
		|	&Ответственный                          КАК Ответственный,
		|	&Автор                                  КАК Автор,
		|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
		|	&НазначениеПлатежа                      КАК Дополнительно,
		|	&Комментарий                            КАК Комментарий,
		|	&Проведен                               КАК Проведен,
		|	&ПометкаУдаления                        КАК ПометкаУдаления,
		|	&ДатаВходящегоДокумента                 КАК ДатаПервичногоДокумента,
		|	&НомерВходящегоДокумента                КАК НомерПервичногоДокумента,
		|	&СуммаДокумента                         КАК Сумма,
		|	&Валюта                                 КАК Валюта,
		|	&Договор                                КАК Договор,
		|	&Исправление                            КАК СторноИсправление,
		|	&СторнируемыйДокумент                   КАК СторнируемыйДокумент,
		|	&ИсправляемыйДокумент                   КАК ИсправляемыйДокумент,
		|	&НаправлениеДеятельности                КАК НаправлениеДеятельности,
		|	ВЫБОР КОГДА &ПроведеноБанком ТОГДА
		|		&Период
		|	ИНАЧЕ
		|		ДАТАВРЕМЯ(1,1,1)
		|	КОНЕЦ КАК ДатаОтраженияВУчете,
		|	НЕОПРЕДЕЛЕНО КАК Приоритет
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|	И &ХозяйственнаяОперация В (
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств)
		|	)";
	
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект = Неопределено 
		ИЛИ (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствСДругогоСчета
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет) Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&ИдентификаторМетаданных                КАК ТипСсылки,
		|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
		|	&Организация                            КАК Организация,
		|	&Партнер                                КАК Партнер,
		|	&БанковскийСчет                         КАК МестоХранения,
		|	НЕОПРЕДЕЛЕНО                            КАК Контрагент,
		|	ДанныеДокумента.БанковскийСчетПодразделение КАК Подразделение,
		|	&Период                                 КАК ДатаДокументаИБ,
		|	&Ссылка                                 КАК Ссылка,
		|	&Номер                                  КАК НомерДокументаИБ,
		|	НЕОПРЕДЕЛЕНО                            КАК Статус,
		|	&Ответственный                          КАК Ответственный,
		|	&Автор                                  КАК Автор,
		|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
		|	&НазначениеПлатежа                      КАК Дополнительно,
		|	&Комментарий                            КАК Комментарий,
		|	&Проведен                               КАК Проведен,
		|	&ПометкаУдаления                        КАК ПометкаУдаления,
		|	&ДатаВходящегоДокумента                 КАК ДатаПервичногоДокумента,
		|	&НомерВходящегоДокумента                КАК НомерПервичногоДокумента,
		|	&СуммаДокумента                         КАК Сумма,
		|	&Валюта                                 КАК Валюта,
		|	&Договор                                КАК Договор,
		|	&Исправление                            КАК СторноИсправление,
		|	&СторнируемыйДокумент                   КАК СторнируемыйДокумент,
		|	&ИсправляемыйДокумент                   КАК ИсправляемыйДокумент,
		|	&НаправлениеДеятельности                КАК НаправлениеДеятельности,
		|	ВЫБОР КОГДА &ПроведеноБанком ТОГДА
		|		&Период
		|	ИНАЧЕ
		|		ДАТАВРЕМЯ(1,1,1)
		|	КОНЕЦ КАК ДатаОтраженияВУчете,
		|	НЕОПРЕДЕЛЕНО КАК Приоритет
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|	И &ХозяйственнаяОперация В (
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствСДругогоСчета),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет)
		|	)";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект = Неопределено 
		ИЛИ (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте) Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&ИдентификаторМетаданных                КАК ТипСсылки,
		|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
		|	&Организация                            КАК Организация,
		|	&Партнер                                КАК Партнер,
		|	&БанковскийСчет                         КАК МестоХранения,
		|	&Контрагент                             КАК Контрагент,
		|	ДанныеДокумента.БанковскийСчетПодразделение КАК Подразделение,
		|	&Период                                 КАК ДатаДокументаИБ,
		|	&Ссылка                                 КАК Ссылка,
		|	&Номер                                  КАК НомерДокументаИБ,
		|	НЕОПРЕДЕЛЕНО                            КАК Статус,
		|	&Ответственный                          КАК Ответственный,
		|	&Автор                                  КАК Автор,
		|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
		|	&НазначениеПлатежа                      КАК Дополнительно,
		|	&Комментарий                            КАК Комментарий,
		|	&Проведен                               КАК Проведен,
		|	&ПометкаУдаления                        КАК ПометкаУдаления,
		|	&ДатаВходящегоДокумента                 КАК ДатаПервичногоДокумента,
		|	&НомерВходящегоДокумента                КАК НомерПервичногоДокумента,
		|	&СуммаДокумента                         КАК Сумма,
		|	&Валюта                                 КАК Валюта,
		|	ДанныеДокумента.ДоговорЭквайринга       КАК Договор,
		|	&Исправление                            КАК СторноИсправление,
		|	&СторнируемыйДокумент                   КАК СторнируемыйДокумент,
		|	&ИсправляемыйДокумент                   КАК ИсправляемыйДокумент,
		|	&НаправлениеДеятельности                КАК НаправлениеДеятельности,
		|	ВЫБОР КОГДА &ПроведеноБанком ТОГДА
		|		&Период
		|	ИНАЧЕ
		|		ДАТАВРЕМЯ(1,1,1)
		|	КОНЕЦ КАК ДатаОтраженияВУчете,
		|	НЕОПРЕДЕЛЕНО КАК Приоритет
		|ИЗ
		|	ДанныеДокументаШапка КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|	И &ХозяйственнаяОперация В (
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте)
		|	)
		|";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	Если ДокументОбъект <> Неопределено И МассивТекстовЗапроса.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	Если ДокументОбъект = Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеДокументаТаблицаРасшифровкаПлатежа", "Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеДокументаШапка", "Документ.ПоступлениеБезналичныхДенежныхСредств");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеДокумента.БанковскийСчетПодразделение", "ДанныеДокумента.БанковскийСчет.Подразделение");
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)
	
	ИмяРегистра = "ОтражениеДокументовВМеждународномУчете";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ТаблицаОбъектовРасчетов", ТекстыЗапроса) Тогда
		ТекстЗапросаВременнаяТаблицаОбъектовРасчета(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период 	 				 КАК Период,
	|	&Организация 				 КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период 							КАК Период,
	|	ТаблицаОбъектовРасчетов.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) 		КАК ДатаОтражения
	|ИЗ
	|	ТаблицаОбъектовРасчетов КАК ТаблицаОбъектовРасчетов
	|ГДЕ
	|	ТаблицаОбъектовРасчетов.Организация <> &Организация
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ДобавитьТекстыОтраженияВзаиморасчетов(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)
	
	Если НЕ (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации) Тогда
			
		Возврат;
			
	КонецЕсли;
	
	#Область КурсыВалютДокумента
	
	ИмяРегистра = "ВременнаяТаблицаКурсыВалютДокумента";

	ТекстЗапросаКурсыВалютДокумента = 
		"ВЫБРАТЬ
		|	ДанныеДокументаШапка.Ссылка КАК Ссылка,
		|	ДанныеДокументаШапка.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА НЕ ДанныеДокументаШапка.ПроведеноБанком
		|		ИЛИ НАЧАЛОПЕРИОДА(ДанныеДокументаШапка.Дата, ДЕНЬ) = ДанныеДокументаШапка.ДатаПроведенияБанком
		|		ИЛИ ДанныеДокументаШапка.Исправление
		|			ТОГДА ДанныеДокументаШапка.Дата
		|		ИНАЧЕ КОНЕЦПЕРИОДА(ДанныеДокументаШапка.ДатаПроведенияБанком, ДЕНЬ)
		|	КОНЕЦ КАК ДатаКурса,
		|	Таблица.ОбъектРасчетов КАК ОбъектРасчетов,
		|	ДанныеДокументаШапка.Валюта КАК ВалютаДокумента,
		|	Таблица.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов
		|ИЗ
		|	ДанныеДокументаТаблицаРасшифровкаПлатежа КАК Таблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументаШапка КАК ДанныеДокументаШапка
		|		ПО Таблица.Ссылка = ДанныеДокументаШапка.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокументаШапка.Ссылка КАК Ссылка,
		|	ДанныеДокументаШапка.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА НЕ ДанныеДокументаШапка.ПроведеноБанком
		|		ИЛИ НАЧАЛОПЕРИОДА(ДанныеДокументаШапка.Дата, ДЕНЬ) = ДанныеДокументаШапка.ДатаПроведенияБанком
		|		ИЛИ ДанныеДокументаШапка.Исправление
		|			ТОГДА ДанныеДокументаШапка.Дата
		|		ИНАЧЕ КОНЕЦПЕРИОДА(ДанныеДокументаШапка.ДатаПроведенияБанком, ДЕНЬ)
		|	КОНЕЦ КАК ДатаКурса,
		|	ОбъектыРасчетов.Ссылка КАК ОбъектРасчетов,
		|	ДанныеДокументаШапка.Валюта КАК ВалютаДокумента,
		|	Таблица.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов
		|ИЗ
		|	ДанныеДокументаТаблицаРасшифровкаПлатежа КАК Таблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументаШапка КАК ДанныеДокументаШапка
		|			ПО Таблица.Ссылка = ДанныеДокументаШапка.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
		|			ПО Таблица.Ссылка = ОбъектыРасчетов.Объект
		|				И Таблица.ОбъектРасчетовПлатежаПартнер = ОбъектыРасчетов.Партнер
		|				И Таблица.ВалютаВзаиморасчетов = ОбъектыРасчетов.ВалютаВзаиморасчетов
		|				И НЕ ОбъектыРасчетов.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокументаШапка.Ссылка КАК Ссылка,
		|	ДанныеДокументаШапка.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА НЕ ДанныеДокументаШапка.ПроведеноБанком
		|		ИЛИ НАЧАЛОПЕРИОДА(ДанныеДокументаШапка.Дата, ДЕНЬ) = ДанныеДокументаШапка.ДатаПроведенияБанком
		|		ИЛИ ДанныеДокументаШапка.Исправление
		|			ТОГДА ДанныеДокументаШапка.Дата
		|		ИНАЧЕ КОНЕЦПЕРИОДА(ДанныеДокументаШапка.ДатаПроведенияБанком, ДЕНЬ)
		|	КОНЕЦ КАК ДатаКурса,
		|	Таблица.ОбъектРасчетов КАК ОбъектРасчетов,
		|	ДанныеДокументаШапка.Валюта КАК ВалютаДокумента,
		|	Таблица.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов
		|ИЗ
		|	ДанныеДокументаТаблицаРасшифровкаПлатежа КАК Таблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументаШапка КАК ДанныеДокументаШапка
		|		ПО Таблица.Ссылка = ДанныеДокументаШапка.Ссылка
		|";
		
	ТекстЗапросаКурсыВалютДокумента = ВзаиморасчетыСервер.ПолучитьТаблицуКурсовВалютНаправленийДеятельности(ТекстЗапросаКурсыВалютДокумента, Истина);

	ТекстыЗапроса.Добавить(ТекстЗапросаКурсыВалютДокумента, ИмяРегистра);
	
	#КонецОбласти
	
	#Область РасчетыСКлиентами
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации Тогда
	
	ТекстОплата = 
		"ВЫБРАТЬ
		|	Таблица.Ссылка                                                           КАК Ссылка,
		|	ДанныеДокументаШапка.Организация                                         КАК Организация,
		|	ВЫБОР
		|		КОГДА ДанныеДокументаШапка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
		|		КОГДА &ЕстьПодарочныйСертификат = Истина
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.РозничныйПокупатель)
		|		ИНАЧЕ Таблица.Партнер
		|	КОНЕЦ                                                                    КАК Партнер,
		|	
		|	Таблица.ВалютаВзаиморасчетов                                             КАК ВалютаВзаиморасчетов,
		|	Таблица.ОбъектРасчетов                                                   КАК ОбъектРасчетов,
		|	
		|	ВЫБОР КОГДА ДанныеДокументаШапка.ПроведеноБанком 
		|			ТОГДА Таблица.СуммаВзаиморасчетов
		|		ИНАЧЕ 0 
		|	КОНЕЦ                                                                    КАК СуммаВзаиморасчетов,
		|	ВЫБОР КОГДА ДанныеДокументаШапка.ПроведеноБанком 
		|			ТОГДА Таблица.Сумма
		|		ИНАЧЕ 0 
		|	КОНЕЦ                                                                    КАК Сумма,
		|	Таблица.СуммаВзаиморасчетов                                              КАК УвеличениеОплачивается,
		|	ВЫБОР КОГДА ДанныеДокументаШапка.ПроведеноБанком 
		|			ТОГДА Таблица.СуммаВзаиморасчетов
		|		ИНАЧЕ 0 
		|	КОНЕЦ                                                                    КАК УменьшениеОплачивается,
		|	
		|	ВЫБОР КОГДА НЕ ДанныеДокументаШапка.ПроведеноБанком
		|		ИЛИ НАЧАЛОПЕРИОДА(ДанныеДокументаШапка.Дата, ДЕНЬ) = ДанныеДокументаШапка.ДатаПроведенияБанком
		|		ИЛИ ДанныеДокументаШапка.Исправление
		|			ТОГДА ДанныеДокументаШапка.Дата
		|		ИНАЧЕ КОНЕЦПЕРИОДА(ДанныеДокументаШапка.ДатаПроведенияБанком, ДЕНЬ)
		|	КОНЕЦ                                                                    КАК ДатаРегистратора,
		|	ВЫБОР КОГДА НЕ ДанныеДокументаШапка.ПроведеноБанком
		|		ИЛИ НАЧАЛОПЕРИОДА(ДанныеДокументаШапка.Дата, ДЕНЬ) = ДанныеДокументаШапка.ДатаПроведенияБанком
		|		ИЛИ ДанныеДокументаШапка.Исправление
		|			ТОГДА ДанныеДокументаШапка.Дата
		|		ИНАЧЕ КОНЕЦПЕРИОДА(ДанныеДокументаШапка.ДатаПроведенияБанком, ДЕНЬ)
		|	КОНЕЦ                                                                    КАК ДатаКурса,
		|	Таблица.ДатаПогашения                                                    КАК ДатаПогашения,
		|	ДанныеДокументаШапка.Номер                                                     КАК НомерРегистратора,
		|	ДанныеДокументаШапка.ХозяйственнаяОперация                                     КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)                           КАК ФормаОплаты,
		|	Таблица.СтатьяДвиженияДенежныхСредств                                    КАК СтатьяДвиженияДенежныхСредств,
		|	ВЫБОР 
		|		КОГДА ДанныеДокументаШапка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента)
		|			ТОГДА ВЫРАЗИТЬ(Таблица.ОснованиеПлатежа КАК Документ.СчетНаОплатуКлиенту)
		|		ИНАЧЕ Неопределено 
		|	КОНЕЦ                                                                    КАК СчетНаОплату,
		|	ДанныеДокументаШапка.Валюта                                              КАК ВалютаДокумента,
		|	Неопределено                                                             КАК СвязанныйДокумент,
		|	Таблица.ИдентификаторСтроки                                              КАК ИдентификаторФинЗаписи,
		|	&НастройкаХозяйственнойОперации                                          КАК НастройкаХозяйственнойОперации
		|ИЗ
		|	ДанныеДокументаТаблицаРасшифровкаПлатежа КАК Таблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументаШапка
		|	ПО Таблица.Ссылка = ДанныеДокументаШапка.Ссылка
		|";
		
	ВзаиморасчетыСервер.ПроведениеОплатыОтКлиента(Запрос, ТекстыЗапроса, Регистры, ТекстОплата);
	
	КонецЕсли;
	
	#КонецОбласти
	
	#Область РасчетыСПоставщиками
	
	Если (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика
		ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации)
		И ДокументОбъект.ПроведеноБанком Тогда
			
	ТекстВозврат = "
		|ВЫБРАТЬ
		|	Таблица.Ссылка                                                           КАК Ссылка,
		|	Документ.Организация                                                     КАК Организация,
		|	ВЫБОР КОГДА Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации)
		|		ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
		|		ИНАЧЕ Таблица.Партнер
		|	КОНЕЦ                                                                    КАК Партнер,
		|	ОбъектРасчетовПлатеж.Ссылка                                              КАК ОбъектРасчетов,
		|	
		|	Таблица.ВалютаВзаиморасчетов                                             КАК ВалютаВзаиморасчетов,
		|	Таблица.СуммаВзаиморасчетов                                              КАК СуммаВзаиморасчетов,
		|	Таблица.Сумма                                                            КАК Сумма,
		|	
		|	ВЫБОР КОГДА НЕ Документ.ПроведеноБанком
		|		ИЛИ НАЧАЛОПЕРИОДА(Документ.Дата, ДЕНЬ) = Документ.ДатаПроведенияБанком
		|		ИЛИ Документ.Исправление
		|			ТОГДА Документ.Дата
		|		ИНАЧЕ КОНЕЦПЕРИОДА(Документ.ДатаПроведенияБанком, ДЕНЬ)
		|	КОНЕЦ                                                                    КАК ДатаРегистратора,
		|	ВЫБОР КОГДА НЕ Документ.ПроведеноБанком
		|		ИЛИ НАЧАЛОПЕРИОДА(Документ.Дата, ДЕНЬ) = Документ.ДатаПроведенияБанком
		|		ИЛИ Документ.Исправление
		|			ТОГДА Документ.Дата
		|		ИНАЧЕ КОНЕЦПЕРИОДА(Документ.ДатаПроведенияБанком, ДЕНЬ)
		|	КОНЕЦ                                                                    КАК ДатаКурса,
		|	Документ.Номер                                                           КАК НомерРегистратора,
		|	Документ.ХозяйственнаяОперация                                           КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)                           КАК ФормаОплаты,
		|	Таблица.СтатьяДвиженияДенежныхСредств                                    КАК СтатьяДвиженияДенежныхСредств,
		|	Документ.Валюта                                                          КАК ВалютаДокумента,
		|	Таблица.ИдентификаторСтроки                                              КАК ИдентификаторФинЗаписи,
		|	ВЫБОР КОГДА Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации)
		|		ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВозвратДенежныхСредствОтДругойОрганизации)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВозвратДенежныхСредствОтПоставщика)
		|	КОНЕЦ                                                                    КАК НастройкаХозяйственнойОперации
		|ИЗ
		|	ДанныеДокументаТаблицаРасшифровкаПлатежа КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаШапка КАК Документ
		|			ПО Документ.Ссылка = Таблица.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектРасчетовПлатеж
		|			ПО Таблица.Ссылка = ОбъектРасчетовПлатеж.Объект
		|				И Таблица.ОбъектРасчетовПлатежаПартнер = ОбъектРасчетовПлатеж.Партнер
		|				И Таблица.ВалютаВзаиморасчетов = ОбъектРасчетовПлатеж.ВалютаВзаиморасчетов
		|				И НЕ ОбъектРасчетовПлатеж.ПометкаУдаления";
	
	ТекстПереносЗадолженности = "
		|ВЫБРАТЬ
		|	Таблица.Ссылка                                                           КАК Ссылка,
		|	Документ.Организация                                                     КАК Организация,
		|	ВЫБОР КОГДА Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации)
		|		ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
		|		ИНАЧЕ Таблица.Партнер
		|	КОНЕЦ                                                                    КАК Партнер,
		|	ОбъектРасчетовПлатеж.Ссылка                                              КАК ОбъектРасчетовПриемник,
		|	Таблица.ОбъектРасчетов                                                   КАК ОбъектРасчетовИсточник,
		|	
		|	Таблица.ВалютаВзаиморасчетов                                             КАК ВалютаВзаиморасчетов,
		|	Документ.Валюта                                                          КАК ВалютаДокумента,
		|	Таблица.СуммаВзаиморасчетов                                              КАК СуммаВзаиморасчетов,
		|	Таблица.Сумма                                                            КАК Сумма,
		|	
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса)               КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)                           КАК ФормаОплаты,
		|	Документ.Номер                                                           КАК НомерРегистратора,
		|	ВЫБОР КОГДА НЕ Документ.ПроведеноБанком
		|		ИЛИ НАЧАЛОПЕРИОДА(Документ.Дата, ДЕНЬ) = Документ.ДатаПроведенияБанком
		|		ИЛИ Документ.Исправление
		|			ТОГДА Документ.Дата
		|		ИНАЧЕ КОНЕЦПЕРИОДА(Документ.ДатаПроведенияБанком, ДЕНЬ)
		|	КОНЕЦ                                                                    КАК ДатаРегистратора,
		|	ВЫБОР КОГДА НЕ Документ.ПроведеноБанком
		|		ИЛИ НАЧАЛОПЕРИОДА(Документ.Дата, ДЕНЬ) = Документ.ДатаПроведенияБанком
		|		ИЛИ Документ.Исправление
		|			ТОГДА Документ.Дата
		|		ИНАЧЕ КОНЕЦПЕРИОДА(Документ.ДатаПроведенияБанком, ДЕНЬ)
		|	КОНЕЦ                                                                    КАК ДатаКурса,
		|	Таблица.СтатьяДвиженияДенежныхСредств                                    КАК СтатьяДвиженияДенежныхСредств,
		|	Таблица.ИдентификаторСтроки                                              КАК ИдентификаторФинЗаписи
		|
		|ИЗ
		|	ДанныеДокументаТаблицаРасшифровкаПлатежа КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаШапка КАК Документ
		|			ПО Документ.Ссылка = Таблица.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектРасчетовПлатеж
		|			ПО Таблица.Ссылка = ОбъектРасчетовПлатеж.Объект
		|				И Таблица.ОбъектРасчетовПлатежаПартнер = ОбъектРасчетовПлатеж.Партнер
		|				И Таблица.ВалютаВзаиморасчетов = ОбъектРасчетовПлатеж.ВалютаВзаиморасчетов
		|				И НЕ ОбъектРасчетовПлатеж.ПометкаУдаления
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
		|			ПО Таблица.ОбъектРасчетов = ОбъектыРасчетов.Ссылка
		|ГДЕ
		|	ОбъектыРасчетов.Ссылка <> ОбъектРасчетовПлатеж.Ссылка";
		
	ВзаиморасчетыСервер.ПроведениеВозвратаОплатыОтПоставщика(Запрос, ТекстыЗапроса, Регистры, ТекстВозврат, ТекстПереносЗадолженности);
	
	КонецЕсли;
	
	#КонецОбласти
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	ПоступлениеБезналичныхДенежныхСредствЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

// Добавляет команду создания документа "Поступление безналичных ДС".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//  ТаблицаЗначений, Неопределено - сформированные команды для вывода в подменю.
Функция ДобавитьКомандуСоздатьНаОснованииОтчетаПоКомиссии_Вознаграждение(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.ПоступлениеБезналичныхДенежныхСредств) Тогда		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Обработчик = "СозданиеНаОснованииУТКлиент.ПоступлениеБезналичныхДсНаОснованииОтчетаПоКомиссии_Вознаграждение";
		КомандаСоздатьНаОсновании.Идентификатор = "ПоступлениеБезналичныхДсНаОснованииОтчетаПоКомиссии_Вознаграждение";
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ПоступлениеБезналичныхДенежныхСредств) 
			+ " " + НСтр("ru = 'комиссионеру (вознаграждение от комитента)'");
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

#КонецОбласти

#Область Прочее

// Функция ищет зеркальный документ СписаниеБДС для операций между Организациями (Интеркампани).
//
// Параметры:
//	ДокументСсылка - ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств - Документ, для которого необходимо найти зеркальный документ СписаниеБДС.
//
// Возвращаемое значение:
//	ДокументСсылка.СписаниеБезналичныхДенежныхСредств
//  Неопределено - если документ не найден.
//
Функция НайтиЗеркальныйПлатеж(ДокументСсылка) Экспорт
	
	ЗеркальныйПлатеж = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СписаниеБезналичныхДенежныхСредств.Ссылка
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеБезналичныхДенежныхСредств КАК ПоступлениеБезналичныхДенежныхСредств
	|ПО
	|	СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию)
	|	И СписаниеБезналичныхДенежныхСредств.БанковскийСчет = ПоступлениеБезналичныхДенежныхСредств.БанковскийСчетОтправитель
	|	И СписаниеБезналичныхДенежныхСредств.СуммаДокумента = ПоступлениеБезналичныхДенежныхСредств.СуммаДокумента
	|	И СписаниеБезналичныхДенежныхСредств.Валюта = ПоступлениеБезналичныхДенежныхСредств.Валюта
	|	И СписаниеБезналичныхДенежныхСредств.ДатаВходящегоДокумента = ПоступлениеБезналичныхДенежныхСредств.ДатаВходящегоДокумента
	|	И СписаниеБезналичныхДенежныхСредств.НомерВходящегоДокумента = ПоступлениеБезналичныхДенежныхСредств.НомерВходящегоДокумента
	|	И СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель = ПоступлениеБезналичныхДенежныхСредств.БанковскийСчет
	|ГДЕ
	|	ПоступлениеБезналичныхДенежныхСредств.Ссылка = &ДокументСсылка";
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() И Выборка.Количество() = 1 Тогда
		ЗеркальныйПлатеж = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат ЗеркальныйПлатеж;

КонецФункции

// Проверяет соответствие расшифровок платежа в зеркальном СписаниеБДС,
// для операций между Организациями (Интеркампани).
//
// Параметры:
//	ДокументСсылка - ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств - Документ поступления
//  ЗеркальныйПлатежСсылка - ДокументСсылка.СписаниеБезналичныхДенежныхСредств - Документ списания
//  ТолькоРасшифровкаПлатежа - Булево - Признак сравнения документов только по расшифровке платежа
//
// Возвращаемое значение:
//  Булево - Признак соответствия платежей
Функция СоответствуетЗеркальномуПлатежу(ДокументСсылка, ЗеркальныйПлатежСсылка, ТолькоРасшифровкаПлатежа=Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ИСТИНА КАК ЕстьРазличия
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА
	|				ПоступлениеБДСРасшифровка.ОбъектРасчетов.Объект = &ПоступлениеБДС ТОГДА
	|					НЕОПРЕДЕЛЕНО
	|			ИНАЧЕ
	|				ПоступлениеБДСРасшифровка.ОбъектРасчетов.Объект
	|		КОНЕЦ КАК Заказ,
	|		ПоступлениеБДСРасшифровка.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|		СУММА(ПоступлениеБДСРасшифровка.Сумма) КАК СуммаПоступление,
	|		СУММА(ПоступлениеБДСРасшифровка.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетовПоступление,
	|		СУММА(0) КАК СуммаСписание,
	|		СУММА(0) КАК СуммаВзаиморасчетовСписание,
	|		ПоступлениеБДСРасшифровка.Ссылка.БанковскийСчет КАК БанковскийСчетПоступление,
	|		ПоступлениеБДСРасшифровка.Ссылка.БанковскийСчетОтправитель КАК БанковскийСчетОтправительПоступление,
	|		ПоступлениеБДСРасшифровка.Ссылка.ДатаВходящегоДокумента КАК ДатаВходящегоДокументаПоступление,
	|		ПоступлениеБДСРасшифровка.Ссылка.НомерВходящегоДокумента КАК НомерВходящегоДокументаПоступление,
	|		NULL КАК БанковскийСчетПолучательСписание,
	|		NULL КАК БанковскийСчетСписание,
	|		NULL КАК ДатаВходящегоДокументаСписание,
	|		NULL КАК НомерВходящегоДокументаСписание
	|	ИЗ
	|		Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК ПоступлениеБДСРасшифровка
	|	ГДЕ
	|		ПоступлениеБДСРасшифровка.Ссылка = &ПоступлениеБДС
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПоступлениеБДСРасшифровка.ВалютаВзаиморасчетов,
	|		ВЫБОР
	|			КОГДА
	|				ПоступлениеБДСРасшифровка.ОбъектРасчетов.Объект = &ПоступлениеБДС ТОГДА
	|					НЕОПРЕДЕЛЕНО
	|			ИНАЧЕ
	|				ПоступлениеБДСРасшифровка.ОбъектРасчетов.Объект
	|		КОНЕЦ,
	|		ПоступлениеБДСРасшифровка.Ссылка.БанковскийСчет,
	|		ПоступлениеБДСРасшифровка.Ссылка.БанковскийСчетОтправитель,
	|		ПоступлениеБДСРасшифровка.Ссылка.ДатаВходящегоДокумента,
	|		ПоступлениеБДСРасшифровка.Ссылка.НомерВходящегоДокумента
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА
	|				СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.ОбъектРасчетов.Объект = &СписаниеБДС ТОГДА
	|					НЕОПРЕДЕЛЕНО
	|			ИНАЧЕ
	|				СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.ОбъектРасчетов.Объект
	|		КОНЕЦ КАК Заказ,
	|		СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.ВалютаВзаиморасчетов,
	|		0,
	|		0,
	|		СУММА(СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Сумма),
	|		СУММА(СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.СуммаВзаиморасчетов),
	|		NULL,
	|		NULL,
	|		NULL,
	|		NULL,
	|		СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка.БанковскийСчетПолучатель,
	|		СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка.БанковскийСчет,
	|		СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка.ДатаВходящегоДокумента,
	|		СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка.НомерВходящегоДокумента
	|	ИЗ
	|		Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа
	|	ГДЕ
	|		СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка = &СписаниеБДС
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВЫБОР
	|			КОГДА
	|				СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.ОбъектРасчетов.Объект = &СписаниеБДС ТОГДА
	|					НЕОПРЕДЕЛЕНО
	|			ИНАЧЕ
	|				СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.ОбъектРасчетов.Объект
	|		КОНЕЦ,
	|		СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.ВалютаВзаиморасчетов,
	|		СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка.БанковскийСчет,
	|		СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка.ДатаВходящегоДокумента,
	|		СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка.НомерВходящегоДокумента,
	|		СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка.БанковскийСчетПолучатель) КАК РасшифровкаПоступлениеСписание
	|
	|СГРУППИРОВАТЬ ПО
	|	РасшифровкаПоступлениеСписание.Заказ,
	|	РасшифровкаПоступлениеСписание.ВалютаВзаиморасчетов
	|
	|ИМЕЮЩИЕ
	|	(СУММА(РасшифровкаПоступлениеСписание.СуммаПоступление) <> СУММА(РасшифровкаПоступлениеСписание.СуммаСписание)
	|		ИЛИ СУММА(РасшифровкаПоступлениеСписание.СуммаВзаиморасчетовПоступление) <> СУММА(РасшифровкаПоступлениеСписание.СуммаВзаиморасчетовСписание)
	|		ИЛИ НЕ &ТолькоРасшифровкаПлатежа
	|			И (МАКСИМУМ(РасшифровкаПоступлениеСписание.БанковскийСчетПоступление) <> МАКСИМУМ(РасшифровкаПоступлениеСписание.БанковскийСчетПолучательСписание)
	|				ИЛИ МАКСИМУМ(РасшифровкаПоступлениеСписание.БанковскийСчетОтправительПоступление) <> МАКСИМУМ(РасшифровкаПоступлениеСписание.БанковскийСчетСписание)
	|				ИЛИ МАКСИМУМ(РасшифровкаПоступлениеСписание.ДатаВходящегоДокументаПоступление) <> МАКСИМУМ(РасшифровкаПоступлениеСписание.ДатаВходящегоДокументаСписание)
	|				ИЛИ МАКСИМУМ(РасшифровкаПоступлениеСписание.НомерВходящегоДокументаПоступление) <> МАКСИМУМ(РасшифровкаПоступлениеСписание.НомерВходящегоДокументаСписание)))
	|";
	
	Запрос.УстановитьПараметр("ПоступлениеБДС", ДокументСсылка);
	Запрос.УстановитьПараметр("СписаниеБДС", ЗеркальныйПлатежСсылка);
	Запрос.УстановитьПараметр("ТолькоРасшифровкаПлатежа", ТолькоРасшифровкаПлатежа);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Не Выборка.Следующий();
	
КонецФункции

// Приводит в соответствие расшифровку платежа в зеркальном СписаниеБДС,
// для операций между Организациями (Интеркампани).
//
// Параметры:
//	ДокументСсылка - ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств - Документ поступления
//  ЗеркальныйПлатежОбъект - ДокументОбъект.СписаниеБезналичныхДенежныхСредств - Документ списания
//
// Возвращаемое значение:
//  Булево - Признак успеха операции
Функция ПривестиВСоответствиеЗеркальныйПлатеж(ДокументСсылка, ЗеркальныйПлатежОбъект) Экспорт
	
	Реквизиты = Новый Структура("РасшифровкаПлатежа, СуммаДокумента, БанковскийСчетОтправитель,
		|БанковскийСчет,ДатаВходящегоДокумента, НомерВходящегоДокумента, ХозяйственнаяОперация");
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, Реквизиты);
	
	ЗеркальныйПлатежОбъект.СуммаДокумента = Реквизиты.СуммаДокумента;
	ЗеркальныйПлатежОбъект.БанковскийСчет = Реквизиты.БанковскийСчетОтправитель;
	ЗеркальныйПлатежОбъект.БанковскийСчетПолучатель = Реквизиты.БанковскийСчет;
	ЗеркальныйПлатежОбъект.ДатаВходящегоДокумента= Реквизиты.ДатаВходящегоДокумента;
	ЗеркальныйПлатежОбъект.НомерВходящегоДокумента= Реквизиты.НомерВходящегоДокумента;
	
	Если ЗначениеЗаполнено(ЗеркальныйПлатежОбъект.Ссылка)
		И СоответствуетЗеркальномуПлатежу(ДокументСсылка, ЗеркальныйПлатежОбъект.Ссылка, Истина) Тогда
		Возврат Истина;
	КонецЕсли;
	
	РасшифровкаПлатежа = Реквизиты.РасшифровкаПлатежа.Выгрузить();
	РасшифровкаПлатежаЗеркальный = ЗеркальныйПлатежОбъект.РасшифровкаПлатежа;
		
	Если РасшифровкаПлатежа.Количество() = 1 И РасшифровкаПлатежаЗеркальный.Количество() = 1 Тогда
		ЗаполнитьЗначенияСвойств(РасшифровкаПлатежаЗеркальный[0], РасшифровкаПлатежа[0], "ОбъектРасчетов, Сумма, СуммаВзаиморасчетов, ВалютаВзаиморасчетов");
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РасшифровкаПлатежа[0].ОбъектРасчетов,"Объект") = ДокументСсылка Тогда
			РасшифровкаПлатежаЗеркальный[0].ОбъектРасчетов = Справочники.ОбъектыРасчетов.ПустаяСсылка();
		КонецЕсли;
		Возврат Истина;
	КонецЕсли;
		
	СтатьяПоУмолчанию = Справочники.СтатьиДвиженияДенежныхСредств.СтатьяДвиженияДенежныхСредствПоХозяйственнойОперации(Реквизиты.ХозяйственнаяОперация);
	Если Не ЗначениеЗаполнено(СтатьяПоУмолчанию) Тогда
		МассивСтатей = РасшифровкаПлатежаЗеркальный.ВыгрузитьКолонку("СтатьяДвиженияДенежныхСредств");
		МассивСтатей = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивСтатей);

		Если МассивСтатей.Количество() = 1 Тогда
			СтатьяПоУмолчанию = МассивСтатей[0];
		КонецЕсли;
	КонецЕсли;	
	
	ЗаявкаПоУмолчанию = Неопределено;
	Если ЗеркальныйПлатежОбъект.ОплатаПоЗаявкам Тогда
		МассивЗаявок = РасшифровкаПлатежаЗеркальный.ВыгрузитьКолонку("ЗаявкаНаРасходованиеДенежныхСредств");
		МассивЗаявок = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивЗаявок);

		Если МассивЗаявок.Количество() = 1 Тогда
			ЗаявкаПоУмолчанию = МассивЗаявок[0];
		ИначеЕсли МассивЗаявок.Количество() = 0 Тогда
			ЗаявкаПоУмолчанию = РасшифровкаПлатежаЗеркальный.ЗаявкаНаРасходованиеДенежныхСредств;
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не приведено в соответствие %1, 
					|не удалось заполнить заявки на расходование дс в расшифровке платежа.'"),ЗеркальныйПлатежОбъект.Ссылка));
			Возврат Ложь;

		КонецЕсли;
	КонецЕсли;
				
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ОбъектыРасчетов.Ссылка КАК ОбъектРасчетов,
	                      |	СУММА(РасшифровкаПлатежа.Сумма) КАК Сумма,
	                      |	СУММА(РасшифровкаПлатежа.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов,
	                      |	РасшифровкаПлатежа.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	                      |	ВЫБОР
	                      |		КОГДА РасшифровкаПлатежа.ОбъектРасчетов.Объект ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
	                      |				ИЛИ РасшифровкаПлатежа.ОбъектРасчетов.Объект ССЫЛКА Документ.ОтчетПоКомиссииМеждуОрганизациями
	                      |			ТОГДА ВЫБОР
	                      |					КОГДА РасшифровкаПлатежа.ОбъектРасчетов.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка)
	                      |							ИЛИ РасшифровкаПлатежа.ОбъектРасчетов.Договор.СтатьяДвиженияДенежныхСредствПолучателя = ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
	                      |						ТОГДА &СтатьяПоУмолчанию
	                      |					ИНАЧЕ РасшифровкаПлатежа.ОбъектРасчетов.Договор.СтатьяДвиженияДенежныхСредствПолучателя
	                      |				КОНЕЦ
	                      |		ИНАЧЕ &СтатьяПоУмолчанию
	                      |	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств,
	                      |	&ЗаявкаПоУмолчанию КАК ЗаявкаНаРасходованиеДенежныхСредств
	                      |ИЗ
	                      |	Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК РасшифровкаПлатежа
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
						  |			ПО РасшифровкаПлатежа.ОбъектРасчетов.Объект = ОбъектыРасчетов.Объект
						  |				И РасшифровкаПлатежа.ОбъектРасчетов <> ОбъектыРасчетов.Ссылка
						  |				И РасшифровкаПлатежа.ОбъектРасчетов.Организация <> ОбъектыРасчетов.Организация
						  |				И РасшифровкаПлатежа.ОбъектРасчетов.ТипРасчетов <> ОбъектыРасчетов.ТипРасчетов
						  |				И НЕ ОбъектыРасчетов.ПометкаУдаления
	                      |ГДЕ
	                      |	РасшифровкаПлатежа.Ссылка = &Ссылка
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	РасшифровкаПлатежа.ВалютаВзаиморасчетов,
	                      |	ОбъектыРасчетов.Ссылка,
	                      |	ВЫБОР
	                      |		КОГДА РасшифровкаПлатежа.ОбъектРасчетов.Объект ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
	                      |				ИЛИ РасшифровкаПлатежа.ОбъектРасчетов.Объект ССЫЛКА Документ.ОтчетПоКомиссииМеждуОрганизациями
	                      |			ТОГДА ВЫБОР
	                      |					КОГДА РасшифровкаПлатежа.ОбъектРасчетов.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка)
	                      |							ИЛИ РасшифровкаПлатежа.ОбъектРасчетов.Договор.СтатьяДвиженияДенежныхСредствПолучателя = ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
	                      |						ТОГДА &СтатьяПоУмолчанию
	                      |					ИНАЧЕ РасшифровкаПлатежа.ОбъектРасчетов.Договор.СтатьяДвиженияДенежныхСредствПолучателя
	                      |				КОНЕЦ
	                      |		ИНАЧЕ &СтатьяПоУмолчанию
	                      |	КОНЕЦ");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("СтатьяПоУмолчанию",СтатьяПоУмолчанию);
	Запрос.УстановитьПараметр("ЗаявкаПоУмолчанию",ЗаявкаПоУмолчанию);
	
	РасшифровкаРезультат = Запрос.Выполнить().Выгрузить();
	Для Каждого Стр Из РасшифровкаРезультат Цикл
		Если Не ЗначениеЗаполнено(Стр.СтатьяДвиженияДенежныхСредств) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не приведено в соответствие %1, 
					|не удалось заполнить статьи движения дс в расшифровке платежа.'"),ЗеркальныйПлатежОбъект.Ссылка));
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	ЗеркальныйПлатежОбъект.РасшифровкаПлатежа.Загрузить(РасшифровкаРезультат);
	
	Возврат Истина;
						
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.ПоступлениеБезналичныхДенежныхСредств.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.20.26";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Некритичный;
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("2efe3191-eda0-42ca-8f76-90ee0dedfd02");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.ПоступлениеБезналичныхДенежныхСредств.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	
	СписокОписаний = Новый Массив;
	СписокОписаний.Добавить(НСтр("ru = 'Обновление документов ""Поступление безналичных денежных средств"":'"));
	СписокОписаний.Добавить(НСтр("ru = '- заполнение реквизита ""Тип налога""';"));
	СписокОписаний.Добавить(НСтр("ru = '- замена аналитики с перечисления ""Типы налогов"" на справочник ""Виды налогов и взносов"".';"));
	
	Обработчик.Комментарий = СтрСоединить(СписокОписаний, Символы.ПС);
		
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.ПоступлениеБезналичныхДенежныхСредств.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Документы.ПоступлениеБезналичныхДенежныхСредств.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.ПоступлениеБезналичныхДенежныхСредств.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = ПустаяСсылка().Метаданные().ПолноеИмя();
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Дата УБЫВ");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоступлениеБезналичныхДенежныхСредств.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПоступлениеБезналичныхДенежныхСредств КАК ПоступлениеБезналичныхДенежныхСредств
	|ГДЕ
	|	ПоступлениеБезналичныхДенежныхСредств.УдалитьТипНалога <> ЗНАЧЕНИЕ(Перечисление.УдалитьТипыНалогов.ПустаяСсылка)";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

// Обработчик обновления
// 
// Параметры:
// 	Параметры - См. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляМногопоточнойОбработки 
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	ПолноеИмяОбъекта = ПустаяСсылка().Метаданные().ПолноеИмя();
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		
		Параметры.ОбработкаЗавершена =
			ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
		
	КонецЕсли;
	
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	ИсключенияПриОбновлении = Новый Массив;
	
	СписокОписаний = Новый Массив;
	СписокОписаний.Добавить(НСтр("ru = 'Не удалось обработать документы ""Поступление безналичных денежных средств"" по обработчику:'"));
	СписокОписаний.Добавить(НСтр("ru = '- заполнение реквизита ""Тип налога""';"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеДляОбновления.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ТаблицаДокументов
		|ИЗ
		|	&ДанныеДляОбновления КАК ДанныеДляОбновления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокументов.Ссылка КАК Ссылка,
		|	ДанныеДокумента.ВерсияДанных КАК ВерсияДанных
		|ИЗ
		|	ТаблицаДокументов КАК ТаблицаДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеБезналичныхДенежныхСредств КАК ДанныеДокумента
		|		ПО ТаблицаДокументов.Ссылка = ДанныеДокумента.Ссылка
		|";
	
	Запрос.УстановитьПараметр("ДанныеДляОбновления", ОбновляемыеДанные);
	
	Документ = Запрос.Выполнить().Выбрать();
	
	Пока Документ.Следующий() Цикл
		
		ПричинаИсключения = 0;
		Рекомендация = "";
		
		НачатьТранзакцию();
		
		Попытка
			
			ПричинаИсключения = 1; // Блокировка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Документ.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			ДокументОбъект = ОбновлениеИнформационнойБазыУТ.ПроверитьПолучитьОбъект(
				Документ.Ссылка, Документ.ВерсияДанных, Параметры.Очередь); // ДокументОбъект
			
			ПричинаИсключения = 2; // ПлохиеДанные
			Рекомендация = НСтр("ru = 'Перепроведите документ вручную.'");
			
			ОбъектИзменен = Ложь;
			
			Если ДокументОбъект <> Неопределено Тогда
				Перечисления.УдалитьТипыНалогов.ЗаполнитьРеквизитТипНалога(ДокументОбъект,"УдалитьТипНалога","ТипНалога");
				Перечисления.УдалитьТипыНалогов.ЗаполнитьРеквизитТипНалога(ДокументОбъект,"АналитикаАктивовПассивов");
				Перечисления.УдалитьТипыНалогов.ЗаполнитьРеквизитТипНалога(ДокументОбъект,"РасшифровкаПлатежа.АналитикаАктивовПассивов");
				ОбъектИзменен = Истина;
			КонецЕсли;
			
			ПричинаИсключения = 3; // Запись
			
			Если ОбъектИзменен Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Документ.Ссылка);
			КонецЕсли;
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Документ.Ссылка);
			
			Если ПричинаИсключения = 2 Тогда
				
				ОписаниеПроблемы = ОбновлениеИнформационнойБазыУТ.ПроблемаСДанными(
					Документ.Ссылка, Рекомендация, ИнформацияОбОшибке());
				ИсключенияПриОбновлении.Добавить(ОписаниеПроблемы);
				
			ИначеЕсли ПричинаИсключения = 3 Тогда
				
				ОбновлениеИнформационнойБазыУТ.ЗаписатьПлохиеДанные(
					ИсключенияПриОбновлении, ОбъектовОбработано, Параметры);
				ВызватьИсключение СтрСоединить(СписокОписаний, Символы.ПС);
				
			КонецЕсли;
			
		КонецПопытки;
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена =
		ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти

#КонецЕсли
