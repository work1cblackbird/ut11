
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем ЗначениеОтбора;
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	ЗаполнитьЗначенияСвойств(СвойстваСписка, Список);
	СвойстваСписка.ТекстЗапроса = ТекстЗапросаСписок();
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список, СвойстваСписка);
	
	Если Параметры.Отбор.Свойство("ХозяйственнаяОперация") Тогда
		
		Если ЗначениеЗаполнено(Параметры.Отбор.ХозяйственнаяОперация) Тогда
			
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список,
				"ХозяйственнаяОперация",
				Параметры.Отбор.ХозяйственнаяОперация,
				,,Истина);
			
		Иначе
			
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список,
				"ХозяйственнаяОперация",
				Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию,
				ВидСравненияКомпоновкиДанных.НеРавно,, Истина);
			
		КонецЕсли;
		
		Параметры.Отбор.Удалить("ХозяйственнаяОперация");
		
	КонецЕсли;
	
	МассивОтборов = Новый Массив;
	МассивОтборов.Добавить("Организация");
	МассивОтборов.Добавить("Валюта");
	МассивОтборов.Добавить("Контрагент");
	МассивОтборов.Добавить("Договор");
	МассивОтборов.Добавить("НалогообложениеНДС");
	МассивОтборов.Добавить("Партнер");
	МассивОтборов.Добавить("Соглашение");
	МассивОтборов.Добавить("ЦенаВключаетНДС");
	
	Для каждого ИмяОтбора Из МассивОтборов Цикл
		Если Параметры.Отбор.Свойство(ИмяОтбора, ЗначениеОтбора) Тогда
			
			ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
				Список,
				ИмяОтбора,
				ЗначениеОтбора);
			
			Параметры.Отбор.Удалить(ИмяОтбора);
			
		КонецЕсли;
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список,
		"Склад",
		Параметры.Склад);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список,
		"ДокументОснование",
		Параметры.ДокументОснование);
		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список,
		"ИспользоватьСоглашенияСКлиентами",
		ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами"));

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	ОповеститьОВыборе(ТекущаяСтрока.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьРаспоряжение(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	
	Если ТекущаяСтрока <> Неопределено Тогда
		ОповеститьОВыборе(ТекущаяСтрока.Ссылка);
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДокумент(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	
	Если ТекущаяСтрока <> Неопределено Тогда
		ПоказатьЗначение(Неопределено, ТекущаяСтрока.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ТекстЗапросаСписок()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаЗаказы.ЗаказКлиента КАК Ссылка,
	|	ТИПЗНАЧЕНИЯ(ТаблицаЗаказы.ЗаказКлиента) КАК ТипРаспоряжения,
	|	ТаблицаЗаказы.ЗаказКлиента.Дата КАК Дата,
	|	ТаблицаЗаказы.ЗаказКлиента.Номер КАК Номер,
	|	ТаблицаЗаказы.ЗаказКлиента.Партнер КАК Партнер,
	|	ТаблицаЗаказы.ЗаказКлиента.Контрагент КАК Контрагент,
	|	ТаблицаЗаказы.ЗаказКлиента.Договор КАК Договор,
	|	ТаблицаЗаказы.ЗаказКлиента.Соглашение КАК Соглашение,
	|	ТаблицаЗаказы.ЗаказКлиента.Организация КАК Организация,
	|	ТаблицаЗаказы.ЗаказКлиента.Склад КАК Склад,
	|	ТаблицаЗаказы.ЗаказКлиента.Валюта КАК Валюта,
	|	ТаблицаЗаказы.ЗаказКлиента.Менеджер КАК Менеджер,
	|	ТаблицаЗаказы.ЗаказКлиента.Статус КАК Статус,
	|	ТаблицаЗаказы.ЗаказКлиента.СуммаДокумента КАК СуммаДокумента,
	|	ТаблицаЗаказы.ЗаказКлиента.Приоритет КАК Приоритет,
	|	ТаблицаЗаказы.ЗаказКлиента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ТаблицаЗаказы.ЗаказКлиента.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ТаблицаЗаказы.ЗаказКлиента.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	ТаблицаЗаказы.ЗаказКлиента.Комментарий КАК Комментарий,
	|	ВЫБОР
	|		КОГДА ТаблицаЗаказы.ЗаказКлиента.Приоритет В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				Приоритеты.Ссылка КАК Приоритет
	|			ИЗ
	|				Справочник.Приоритеты КАК Приоритеты
	|
	|			УПОРЯДОЧИТЬ ПО
	|				Приоритеты.РеквизитДопУпорядочивания)
	|			ТОГДА 0
	|		КОГДА ТаблицаЗаказы.ЗаказКлиента.Приоритет В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				Приоритеты.Ссылка КАК Приоритет
	|			ИЗ
	|				Справочник.Приоритеты КАК Приоритеты
	|
	|			УПОРЯДОЧИТЬ ПО
	|				Приоритеты.РеквизитДопУпорядочивания УБЫВ)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК КартинкаПриоритета
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаЗаказы.ЗаказКлиента КАК ЗаказКлиента,
	|		СУММА(ТаблицаЗаказы.КОформлению) КАК КОформлению
	|	ИЗ
	|		(ВЫБРАТЬ
	|			РаспоряженияОбороты.Распоряжение КАК ЗаказКлиента,
	|			РаспоряженияОбороты.КОформлениюОборот КАК КОформлению
	|		ИЗ
	|			РегистрНакопления.РаспоряженияНаОтгрузку.Обороты(,,, Распоряжение ССЫЛКА Документ.ЗаказКлиента
	|			И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).Организация = &Организация
	|			И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).Валюта = &Валюта
	|			И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).Контрагент = &Контрагент
	|			И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).Договор = &Договор
	|			И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).НалогообложениеНДС В (&НалогообложениеНДС)
	|			И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).Партнер = &Партнер
	|			И ВЫБОР
	|				КОГДА НЕ &ИспользоватьСоглашенияСКлиентами
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).Соглашение = &Соглашение
	|			КОНЕЦ
	|			И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).ЦенаВключаетНДС = &ЦенаВключаетНДС
	|			И (Склад = &Склад
	|			ИЛИ Склад В ИЕРАРХИИ (&Склад)
	|			ИЛИ Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))) КАК РаспоряженияОбороты
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	|		ВЫБРАТЬ
	|			РаспоряженияОбороты.Распоряжение КАК ЗаказКлиента,
	|			РаспоряженияОбороты.КОформлениюОборот КАК КОформлению
	|		ИЗ
	|			РегистрНакопления.РаспоряженияНаОтгрузку.Обороты(,,, Распоряжение ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Организация = &Организация
	|			И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Валюта = &Валюта
	|			И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Контрагент = &Контрагент
	|			И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Договор = &Договор
	|			И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).НалогообложениеНДС В (&НалогообложениеНДС)
	|			И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Партнер = &Партнер
	|			И ВЫБОР
	|				КОГДА НЕ &ИспользоватьСоглашенияСКлиентами
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Соглашение = &Соглашение
	|			КОНЕЦ
	|			И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).ЦенаВключаетНДС = &ЦенаВключаетНДС
	|			И (Склад = &Склад
	|			ИЛИ Склад В ИЕРАРХИИ (&Склад)
	|			ИЛИ Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))) КАК РаспоряженияОбороты
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	|		ВЫБРАТЬ
	|			РаспоряженияДвижения.Распоряжение,
	|			-РаспоряженияДвижения.КОформлению
	|		ИЗ
	|			РегистрНакопления.РаспоряженияНаОтгрузку КАК РаспоряженияДвижения
	|		ГДЕ
	|			(РаспоряженияДвижения.Регистратор = &ДокументОснование
	|			ИЛИ РаспоряженияДвижения.Регистратор В
	|				(ВЫБРАТЬ
	|					Корректировки.Ссылка
	|				ИЗ
	|					Документ.КорректировкаРеализации КАК Корректировки
	|				ГДЕ
	|					Корректировки.ДокументОснование = &ДокументОснование
	|					И Корректировки.Проведен))
	|			И РаспоряженияДвижения.Активность
	|			И (РаспоряженияДвижения.Распоряжение ССЫЛКА Документ.ЗаказКлиента
	|				ИЛИ РаспоряженияДвижения.Распоряжение ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента)) КАК ТаблицаЗаказы
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаЗаказы.ЗаказКлиента) КАК ТаблицаЗаказы
	|ГДЕ
	|	ТаблицаЗаказы.КОформлению > 0";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти