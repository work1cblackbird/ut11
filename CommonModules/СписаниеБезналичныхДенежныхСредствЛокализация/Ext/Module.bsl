#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация
	
	МеханизмыДокумента.Добавить("ПодарочныеСертификаты");
	

	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
	//++ Локализация
	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет Тогда
		ЗарегистрироватьОплатуНДСПоСчетамФактурам(Объект, Истина);
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив из Строка - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	//++ Локализация
	Перем МассивВсехРеквизитов;
	Перем МассивРеквизитовОперации;
	
	ФлагОбменСБанками = Ложь;
	ОшибкиЗаполнения = "";
	РасшифровкаБезРазбиения = Ложь;
	
	ДополнительныеСвойства = Объект.ДополнительныеСвойства;
	
	Если ДополнительныеСвойства.Свойство("ОбменСБанками")
		И ДополнительныеСвойства.ОбменСБанками Тогда
		ФлагОбменСБанками = Истина;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("РасшифровкаБезРазбиения")
		И ДополнительныеСвойства.РасшифровкаБезРазбиения Тогда
		РасшифровкаБезРазбиения = Истина;
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(Объект, МассивВсехРеквизитов, МассивРеквизитовОперации);
	ОбщегоНазначенияУТКлиентСервер.ЗаполнитьМассивНепроверяемыхРеквизитов(
		МассивВсехРеквизитов, МассивРеквизитовОперации, МассивНепроверяемыхРеквизитов);
	
	МассивНепроверяемыхРеквизитов.Добавить("ВидПеречисленияВБюджет");
	
	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет
		И Не Объект.НДФЛПоВедомостям Тогда
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.Ведомость");
	КонецЕсли;
	
	
		МассивНепроверяемыхРеквизитов.Добавить("ТипПлатежаФЗ275");
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорСЗаказчиком");
	
	Если Объект.ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов.РаспоряжениеОбОбязательнойПродаже Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДатаВходящегоДокумента");
		МассивНепроверяемыхРеквизитов.Добавить("НомерВходящегоДокумента");
	КонецЕсли;
	
	Если Не Объект.СписокКонтрагентов Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("БанковскиеСчетаСпискаКонтрагентов");
		МассивНепроверяемыхРеквизитов.Добавить("БанковскиеСчетаСпискаКонтрагентов.Контрагент");
		МассивНепроверяемыхРеквизитов.Добавить("БанковскиеСчетаСпискаКонтрагентов.БанковскийСчетКонтрагента");
		МассивНепроверяемыхРеквизитов.Добавить("БанковскиеСчетаСпискаКонтрагентов.Сумма");
		
	КонецЕсли;
	
	РеквизитыПлатежаВБюджет = Новый Массив;
	РеквизитыПлатежаВБюджет.Добавить("ВидПеречисленияВБюджет");
	РеквизитыПлатежаВБюджет.Добавить("ПеречислениеВБюджет");
	РеквизитыПлатежаВБюджет.Добавить("КодБК");
	РеквизитыПлатежаВБюджет.Добавить("КодОКАТО");
	РеквизитыПлатежаВБюджет.Добавить("ПоказательДаты");
	РеквизитыПлатежаВБюджет.Добавить("ПоказательНомера");
	РеквизитыПлатежаВБюджет.Добавить("ПоказательОснования");
	РеквизитыПлатежаВБюджет.Добавить("ПоказательПериода");
	РеквизитыПлатежаВБюджет.Добавить("СтатусСоставителя");
	
	РеквизитыДокумента = Объект.Метаданные().Реквизиты;
	
	ДатаНачалаПримененияПриказа126н = Константы.ДатаНачалаПримененияПриказа126н.Получить();
	Если ДатаНачалаПримененияПриказа126н <> '00010101' И Объект.Дата >= ДатаНачалаПримененияПриказа126н Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ПоказательТипа");
	Иначе
		РеквизитыПлатежаВБюджет.Добавить("ПоказательТипа");
	КонецЕсли;
	
	Для каждого РеквизитПлатежаВБюджет Из РеквизитыПлатежаВБюджет Цикл
		МассивНепроверяемыхРеквизитов.Добавить(РеквизитПлатежаВБюджет);
		Если Объект.ПеречислениеВБюджет Тогда
			Если Не ЗначениеЗаполнено(Объект[РеквизитПлатежаВБюджет]) Тогда
				Реквизит = РеквизитыДокумента.Найти(РеквизитПлатежаВБюджет);
				ТекстОшибки = СтрШаблон(НСтр("ru = 'Поле ""%1"" не заполнено'"), Реквизит.Синоним);
				Если ФлагОбменСБанками Тогда
					ДенежныеСредстваСервер.ДобавитьОшибкуЗаполнения(ОшибкиЗаполнения, ТекстОшибки);
				Иначе
					ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,, "НадписьРеквизитыПлатежаВБюджет",, Отказ);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если Объект.ПеречислениеВБюджет Тогда
		ДенежныеСредстваСерверЛокализация.ПроверитьЗаполнениеНалоговыхРеквизитов(Объект, Отказ, МассивНепроверяемыхРеквизитов);
		ДенежныеСредстваСерверЛокализация.ПроверитьИННиКППНаСоответствие148н(Объект, Отказ);
	КонецЕсли;
	
	Если Не Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику
		И Не Объект.СписокФизЛиц Тогда
		Объект.ВидЗачисления = "";
	КонецЕсли;
	
	ДенежныеСредстваСерверЛокализация.ПроверитьЗаполнениеКодаВидаДохода(Объект,
																		Объект.ХозяйственнаяОперация,
																		Отказ);
	ДенежныеСредстваСерверЛокализация.ПроверитьВыгрузкуРеестраНаЗачислениеДенежныхСредств(Объект, Отказ, ФлагОбменСБанками);
	
	ДенежныеСредстваСерверЛокализация.ПроверитьЗаполнениеДокументаСФлагомСамозанятого(
		Объект, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения);
	
	ПодарочныеСертификатыСервер.ОбработкаПроверкиЗаполнения(Объект, Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	//-- Локализация
	Возврат;
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	ТипОснования = ТипЗнч(ДанныеЗаполнения);
	
	//++ Локализация
	Если ТипОснования = Тип("Структура")
		И ДанныеЗаполнения.Свойство("НесколькоЗаявокНаРасходованиеСредств") Тогда
		
		Если Не ДанныеЗаполнения.НесколькоЗаявокНаРасходованиеСредств
			И ТипЗнч(ДанныеЗаполнения.ДокументОснование[0]) = Тип("ДокументСсылка.УведомлениеОЗачисленииВалюты") Тогда
			
			ДенежныеСредстваСерверЛокализация.ЗаполнитьДокументПоУведомлениюОЗачисленииВалюты(
				ДанныеЗаполнения.ДокументОснование[0], Объект, ДанныеЗаполнения);
		КонецЕсли;
		
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.СчетФактураНалоговыйАгент") Тогда
		
		ЗаполнитьПоСчетуФактуреНалоговогоАгента(
			Объект,
			ДанныеЗаполнения,
			ДанныеЗаполнения);
			
	
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ЗаявлениеОВвозеТоваров") Тогда
		
		ЗаполнитьПоЗаявлениюОВвозеТоваров(
			Объект,
			ДанныеЗаполнения,
			ДанныеЗаполнения);
			
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ОперацияПоЯндексКассе") Тогда
		
		ЗаполнитьПоОперацииПоЯндексКассе(
			Объект,
			ДанныеЗаполнения,
			ДанныеЗаполнения);
			
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.УведомлениеОЗачисленииВалюты") Тогда
		
		ДенежныеСредстваСерверЛокализация.ЗаполнитьДокументПоУведомлениюОЗачисленииВалюты(
			ДанныеЗаполнения,
			Объект,
			ДанныеЗаполнения);
			
	ИначеЕсли ТипОснования = Тип("Массив") И ДанныеЗаполнения.Количество() > 0 Тогда
		
		ТипОснования = ТипЗнч(ДанныеЗаполнения[0]);
		
		Если ТипОснования = Тип("ДокументСсылка.СчетФактураНалоговыйАгент") Тогда 
			
			ЗаполнитьПоСчетуФактуреНалоговогоАгента(
				Объект,
				ДанныеЗаполнения[0], 
				ДанныеЗаполнения);
				
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.ЗаявлениеОВвозеТоваров") Тогда 
				
			ЗаполнитьПоЗаявлениюОВвозеТоваров(
				Объект,
				ДанныеЗаполнения[0], 
				ДанныеЗаполнения);
				
		КонецЕсли;
		
	ИначеЕсли ТипОснования = Тип("Структура")
		И ПолучитьФункциональнуюОпцию("ИспользоватьВыплатыСамозанятым") Тогда
		
		Если ДанныеЗаполнения.Свойство("СписокКонтрагентов")
			И ДанныеЗаполнения.СписокКонтрагентов Тогда
			
			Если Не ЗначениеЗаполнено(Объект.Валюта)
				И ДанныеЗаполнения.Свойство("Валюта") Тогда
				Объект.Валюта = ДанныеЗаполнения.Валюта;
			КонецЕсли;
			
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеЗаполнения, "БанковскиеСчетаСпискаКонтрагентов") Тогда
				Объект.БанковскиеСчетаСпискаКонтрагентов.Загрузить(ДанныеЗаполнения.БанковскиеСчетаСпискаКонтрагентов);
			КонецЕсли;
			
			Объект.СписокКонтрагентов = ДанныеЗаполнения.СписокКонтрагентов;
			ДенежныеСредстваСерверЛокализация.ЗаполнитьКонтрагентаРасшифровкиПлатежа(Объект);
			Объект.БанковскиеСчетаСпискаКонтрагентов.Загрузить(ДенежныеСредстваСерверЛокализация.РеестрКонтрагентов(Объект));
			
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Договор")
			И ЗначениеЗаполнено(ДанныеЗаполнения.Договор)
			И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеЗаполнения.Договор, "ДоговорССамозанятым") Тогда
			Объект.ОперацияССамозанятым =
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.Договор, "ДоговорССамозанятым");
		ИначеЕсли Не ДанныеЗаполнения.Свойство("ОперацияССамозанятым") Тогда
			
			ДанныеЗаполнения.Вставить("ОперацияССамозанятым", Ложь);
			ОписаниеРеквизитов = Новый Структура;
			ДенежныеСредстваСерверЛокализация.ЗадатьОписаниеЗаполняемыхРеквизитовПоСтатистике(ОписаниеРеквизитов);
			ЗаполнениеОбъектовПоСтатистике.ЗаполнитьРеквизитыОбъекта(Объект, ДанныеЗаполнения, ОписаниеРеквизитов);
			Объект.ОперацияССамозанятым = ДанныеЗаполнения.ОперацияССамозанятым;
			
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Контрагент")
			И Не Объект.СписокКонтрагентов Тогда
			Объект.Контрагент = ДанныеЗаполнения.Контрагент;
		КонецЕсли;
		
	КонецЕсли;
	
	
	ИнициализироватьДокумент(Объект, ДанныеЗаполнения);
	
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	//++ Локализация
	ВыполнитьПроверкуВозвратовНеперечисленныхДенежныхСредств(Объект, Отказ);
	//-- Локализация
	Возврат;
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	//++ Локализация
	НеиспользуемыеРеквизиты = Новый Массив;
	МассивВсехРеквизитов = Новый Массив;
	МассивРеквизитовОперации = Новый Массив;
	
	ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(Объект, МассивВсехРеквизитов, МассивРеквизитовОперации);
	
	Если НЕ Объект.СписокФизЛиц Тогда
		
		НеиспользуемыеРеквизиты.Добавить("НомерДоговораСБанком");
		НеиспользуемыеРеквизиты.Добавить("ДатаДоговораСБанком");
		НеиспользуемыеРеквизиты.Добавить("ОтделениеБанка");
		НеиспользуемыеРеквизиты.Добавить("ФилиалОтделенияБанка");
		НеиспользуемыеРеквизиты.Добавить("КодировкаФайла");
		
	КонецЕсли;
	
	
	Если Не Объект.СписокКонтрагентов Тогда
		НеиспользуемыеРеквизиты.Добавить("БанковскиеСчетаСпискаКонтрагентов");
		НеиспользуемыеРеквизиты.Добавить("БанковскиеСчетаСпискаКонтрагентов.Контрагент");
		НеиспользуемыеРеквизиты.Добавить("БанковскиеСчетаСпискаКонтрагентов.БанковскийСчетКонтрагента");
		НеиспользуемыеРеквизиты.Добавить("БанковскиеСчетаСпискаКонтрагентов.Сумма");
	КонецЕсли;
	
	Для каждого НеиспользуемыйРеквизит Из НеиспользуемыеРеквизиты Цикл
		УдаляемыйРеквизит = МассивРеквизитовОперации.Найти(НеиспользуемыйРеквизит);
		Если УдаляемыйРеквизит <> Неопределено Тогда
			МассивРеквизитовОперации.Удалить(УдаляемыйРеквизит);
		КонецЕсли;
	КонецЦикла;
	
	
	ПроверитьИзменениеТипаПлатежногоДокумента(Объект);
	
	Если Лев(Объект.ПоказательДаты, 1) = "0" И Сред(Объект.ПоказательДаты, 2, 2) = " ." Тогда
		Объект.ПоказательДаты = "0";
	КонецЕсли;
	
	Если ПустаяСтрока(СтрЗаменить(Объект.ПоказательДаты, ".", "")) Тогда
		Объект.ПоказательДаты = "";
	КонецЕсли;
	
	Если Объект.ПеречислениеВБюджет И Не ЗначениеЗаполнено(Объект.ИдентификаторПлатежа) Тогда
		Объект.ИдентификаторПлатежа = "0";
	КонецЕсли;
	
	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты 
		И НЕ ЗначениеЗаполнено(Объект.ИдентификаторПлатежа) Тогда
		Объект.ИдентификаторПлатежа = ОбщегоНазначенияУТ.ПолучитьУникальныйИдентификаторПлатежа(Объект);
	КонецЕсли;
	
	ЭтоФизЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Контрагент, "ЮрФизЛицо") = Перечисления.ЮрФизЛицо.ФизЛицо;
	ВалютаРегламентированногоУчета = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Объект.Организация);
	ВалютныйПлатеж = (ЗначениеЗаполнено(Объект.Валюта) И Объект.Валюта <> ВалютаРегламентированногоУчета);
	
	Если Не Объект.ДополнительныеСвойства.Свойство("ОбменСБанками") Или Не Объект.ДополнительныеСвойства.ОбменСБанками Тогда
		Если Не (Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыПоЗарплатномуПроекту
			Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыНаЛицевыеСчета
			Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику
			Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоставщику
			Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств
			Или (Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаАрендодателю
			И ЭтоФизЛицо)
			Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоКредитам)
			Или ВалютныйПлатеж Тогда
			Объект.КодВидаДохода = "";
		КонецЕсли;
	КонецЕсли;
	
	Если Не Объект.ДополнительныеСвойства.Свойство("ОбменСБанками") Или Не Объект.ДополнительныеСвойства.ОбменСБанками Тогда
		Если Не (Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыПоЗарплатномуПроекту
			Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыНаЛицевыеСчета) Тогда
			Объект.КодВыплат = "";
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов.РаспоряжениеОбОбязательнойПродаже
		И Не (ЗначениеЗаполнено(Объект.НомерВходящегоДокумента) Или ЗначениеЗаполнено(Объект.ДатаВходящегоДокумента)) Тогда
		Объект.НомерВходящегоДокумента = СокрЛП(ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Объект.Номер, Истина, Истина));
		Объект.ДатаВходящегоДокумента = Объект.Дата;
	КонецЕсли;
	
	ДенежныеСредстваСерверЛокализация.ОчиститьНеиспользуемыеРеквизитыОплатыСамозанятым(Объект);
	
	
	ДенежныеСредстваСервер.ОчиститьНеиспользуемыеРеквизиты(Объект, МассивВсехРеквизитов, МассивРеквизитовОперации);
	//-- Локализация
	Возврат;
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	//++ Локализация
	Если ПроведениеДокументов.СвойстваДокумента(Объект).РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения
		И Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет Тогда
		
		ЗарегистрироватьОплатуНДСПоСчетамФактурам(Объект, Ложь);
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  СтандартнаяОбработка - Булево - Признак выполнения стандартной обработки
//  Префикс - Строка - Префикс, который будет использован для генерации нового номера
//
Процедура ПриУстановкеНовогоНомера(Объект, СтандартнаяОбработка, Префикс) Экспорт
	
	Если Объект.ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов.ПлатежноеПоручение Тогда
		Префикс = НСтр("ru = 'П'");
	Иначе
		Префикс = "0";
	КонецЕсли;
	Возврат;
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	//++ Локализация
	Если ДействуетФедеральныйЗакон345ФЗ(ОбъектКопирования.Дата) <> ДействуетФедеральныйЗакон345ФЗ(ТекущаяДатаСеанса()) Тогда
		Если ОбъектКопирования.ОчередностьПлатежа = 6 Тогда
			Объект.ОчередностьПлатежа = 5;
		Иначе
			Объект.ОчередностьПлатежа = 0;
		КонецЕсли;
	КонецЕсли;
	
	ДенежныеСредстваСерверЛокализация.ПриИзмененииПравилПлатежейВБюджет(Объект, ОбъектКопирования.Дата, ТекущаяДатаСеанса());
	
	Если Объект.ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов.ИнкассовоеПоручение
		Или Объект.ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов.ПлатежныйОрдер
		Или Объект.ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов.БанковскийОрдер Тогда
		Объект.ПроведеноБанком = Истина;
		Объект.ДатаПроведенияБанком = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Объект.ДатаВыгрузкиРеестра = '00010101';
	
	Если Не Объект.ПеречислениеВБюджет Тогда
		Объект.ИдентификаторПлатежа = "";
	КонецЕсли;
	
	ДенежныеСредстваСерверЛокализация.ИнициализироватьБанковскиеСчетаСпискаКонтрагентов(Объект);
	//-- Локализация
	Возврат;
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  ВидФормы - Строка - Имя стандартной формы
//  Параметры - Структура - Параметры формы
//  ВыбраннаяФорма - Строка, Форма - Содержит имя открываемой формы или объект метаданных Форма
//  ДополнительнаяИнформация - Структура - Дополнительная информация открытия формы
//  СтандартнаяОбработка - Булево - Признак стандартной обработки
Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка) Экспорт
	
	//++ Локализация


	//-- Локализация
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	//++ Локализация
	КомандаСозданияСчетФактуры = Документы.СчетФактураНалоговыйАгент.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	Если КомандаСозданияСчетФактуры <> Неопределено Тогда
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаСозданияСчетФактуры,
															"ОперацияССамозанятым",
															Истина,
															ВидСравненияКомпоновкиДанных.НеРавно);
		ДоступныеХозОперации = Новый Массив;
		ДоступныеХозОперации.Добавить(Перечисления.ХозяйственныеОперации.ОплатаПоставщику);
		ДоступныеХозОперации.Добавить(Перечисления.ХозяйственныеОперации.ОплатаАрендодателю);
		ДоступныеХозОперации.Добавить(Перечисления.ХозяйственныеОперации.СписаниеКредиторскойЗадолженности);
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаСозданияСчетФактуры,
															"ХозяйственнаяОперация",
															ДоступныеХозОперации,
															ВидСравненияКомпоновкиДанных.ВСписке);
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	//++ Локализация


	//-- Локализация
	Возврат;
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	//++ Локализация
	// Платежное поручение
	Если ПравоДоступа("Чтение", Метаданные.Документы.ЗаявкаНаРасходованиеДенежныхСредств) Тогда
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "ПлатежноеПоручение";
		КомандаПечати.Представление = НСтр("ru = 'Платежное поручение'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КонецЕсли;
	

	//-- Локализация
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	//++ Локализация
	
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПлатежноеПоручение") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ПлатежноеПоручение",
			НСтр("ru='Платежное поручение'"),
			СформироватьПлатежноеПоручение(МассивОбъектов, ОбъектыПечати),
			,
			"Документ.СписаниеБезналичныхДенежныхСредств.ПФ_MXL_ПлатежноеПоручение_ru");
			
	КонецЕсли;
	//-- Локализация
КонецПроцедуры

//++ Локализация
Функция СформироватьПлатежноеПоручение(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СписаниеБезналичныхДенежныхСредств_ПлатежноеПоручение";
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.СписаниеБезналичныхДенежныхСредств.ПФ_MXL_ПлатежноеПоручение_ru");
	Макет.КодЯзыка = Метаданные.Языки.Русский.КодЯзыка;

	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента
	|ПОМЕСТИТЬ БанковскиеСчетаКонтрагентовВрем
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&МассивДокументов)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ДанныеДокумента.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента
	|ИЗ
	|	Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&МассивДокументов)
	|	И ДанныеДокумента.ФормаОплатыБезналичная
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ДанныеДокумента.БанковскийСчетПолучатель КАК БанковскийСчетКонтрагента
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&МассивДокументов)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ДанныеДокумента.БанковскийСчетПолучатель КАК БанковскийСчетКонтрагента
	|ИЗ
	|	Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&МассивДокументов)
	|	И ДанныеДокумента.ФормаОплатыБезналичная
	|;
	|
	|//////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Документ.БанковскийСчет КАК БанковскийСчет
	|ПОМЕСТИТЬ БанковскиеСчетаОрганизацийВрем
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств КАК Документ
	|ГДЕ
	|	Документ.Ссылка В (&МассивДокументов)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ДанныеДокумента.БанковскийСчет КАК БанковскийСчет
	|ИЗ
	|	Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&МассивДокументов)
	|	И ДанныеДокумента.ФормаОплатыБезналичная
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ДанныеДокумента.БанковскийСчетКасса КАК БанковскийСчет
	|ИЗ
	|	Документ.ЗаявкаНаРасходованиеДенежныхСредств.РаспределениеПоСчетам КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&МассивДокументов)
	|	И ДанныеДокумента.Ссылка.ФормаОплатыБезналичная
	|;
	|
	|//////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БанковскиеСчета.Ссылка КАК Ссылка,
	|	БанковскиеСчета.Владелец КАК Владелец,
	|	БанковскиеСчета.НомерСчета КАК НомерСчета,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчета.НаименованиеБанка
	|	ИНАЧЕ
	|		БанковскиеСчета.Банк.Наименование
	|	КОНЕЦ КАК НаименованиеБанка,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчета.БИКБанка
	|	ИНАЧЕ
	|		БанковскиеСчета.Банк.Код
	|	КОНЕЦ КАК БИК,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчета.КоррСчетБанка
	|	ИНАЧЕ
	|		БанковскиеСчета.Банк.КоррСчет
	|	КОНЕЦ КАК КоррСчет,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчета.ГородБанка
	|	ИНАЧЕ
	|		БанковскиеСчета.Банк.Город
	|	КОНЕЦ КАК Город,
	|	БанковскиеСчета.ТекстКорреспондента КАК ТекстКорреспондента,
	|	БанковскиеСчета.ИННКорреспондента КАК ИННКорреспондента,
	|	БанковскиеСчета.КППКорреспондента КАК КППКорреспондента,
	|	ЕСТЬNULL(БанковскиеСчета.Банк.БИКРКЦ.Наименование, """") КАК НаименованиеРКЦ
	|
	|ПОМЕСТИТЬ БанковскиеСчетаКонтрагентов
	|ИЗ
	|	Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчета
	|ГДЕ
	|	(БанковскиеСчета.БанкДляРасчетов = ЗНАЧЕНИЕ(Справочник.КлассификаторБанков.ПустаяСсылка)
	|		И БанковскиеСчета.БИКБанкаДляРасчетов = """"
	|		И БанковскиеСчета.СВИФТБанкаДляРасчетов = """")
	|	И БанковскиеСчета.Ссылка В (
	|		ВЫБРАТЬ
	|			БанковскиеСчета.БанковскийСчетКонтрагента
	|		ИЗ
	|			БанковскиеСчетаКонтрагентовВрем КАК БанковскиеСчета
	|		)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БанковскиеСчета.Ссылка,
	|	БанковскиеСчета.Владелец,
	|	ВЫБОР
	|		КОГДА ИностранныйБанк ТОГДА
	|			БанковскиеСчета.СчетВБанкеДляРасчетов
	|		ИНАЧЕ
	|			ВЫБОР
	|				КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|					БанковскиеСчета.КоррСчетБанка
	|				ИНАЧЕ
	|					БанковскиеСчета.Банк.КоррСчет
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанкаДляРасчетов ТОГДА
	|		БанковскиеСчета.НаименованиеБанкаДляРасчетов
	|	ИНАЧЕ
	|		БанковскиеСчета.БанкДляРасчетов.Наименование
	|	КОНЕЦ,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанкаДляРасчетов ТОГДА
	|		БанковскиеСчета.БИКБанкаДляРасчетов
	|	ИНАЧЕ
	|		БанковскиеСчета.БанкДляРасчетов.Код
	|	КОНЕЦ,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанкаДляРасчетов ТОГДА
	|		БанковскиеСчета.КоррСчетБанкаДляРасчетов
	|	ИНАЧЕ
	|		БанковскиеСчета.БанкДляРасчетов.КоррСчет
	|	КОНЕЦ,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанкаДляРасчетов ТОГДА
	|		БанковскиеСчета.ГородБанкаДляРасчетов
	|	ИНАЧЕ
	|		БанковскиеСчета.БанкДляРасчетов.Город
	|	КОНЕЦ,
	|	БанковскиеСчета.ТекстКорреспондента,
	|	БанковскиеСчета.ИННКорреспондента,
	|	БанковскиеСчета.КППКорреспондента,
	|	ЕСТЬNULL(БанковскиеСчета.БанкДляРасчетов.БИКРКЦ.Наименование, """") КАК НаименованиеРКЦ
	|ИЗ
	|	Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчета
	|ГДЕ
	|	НЕ (БанковскиеСчета.БанкДляРасчетов = ЗНАЧЕНИЕ(Справочник.КлассификаторБанков.ПустаяСсылка)
	|		И БанковскиеСчета.БИКБанкаДляРасчетов = """"
	|		И БанковскиеСчета.СВИФТБанкаДляРасчетов = """")
	|	И БанковскиеСчета.Ссылка В (
	|		ВЫБРАТЬ
	|			БанковскиеСчета.БанковскийСчетКонтрагента
	|		ИЗ
	|			БанковскиеСчетаКонтрагентовВрем КАК БанковскиеСчета
	|		)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БанковскиеСчета.Ссылка,
	|	БанковскиеСчета.Владелец,
	|	БанковскиеСчета.НомерСчета,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчета.НаименованиеБанка
	|	ИНАЧЕ
	|		БанковскиеСчета.Банк.Наименование
	|	КОНЕЦ,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчета.БИКБанка
	|	ИНАЧЕ
	|		БанковскиеСчета.Банк.Код
	|	КОНЕЦ,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчета.КоррСчетБанка
	|	ИНАЧЕ
	|		БанковскиеСчета.Банк.КоррСчет
	|	КОНЕЦ,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчета.ГородБанка
	|	ИНАЧЕ
	|		БанковскиеСчета.Банк.Город
	|	КОНЕЦ,
	|	БанковскиеСчета.ТекстКорреспондента,
	|	"""",
	|	"""",
	|	БанковскиеСчета.Банк.БИКРКЦ.Наименование КАК НаименованиеРКЦ
	|ИЗ
	|	Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчета
	|ГДЕ
	|	(БанковскиеСчета.БанкДляРасчетов = ЗНАЧЕНИЕ(Справочник.КлассификаторБанков.ПустаяСсылка)
	|		И БанковскиеСчета.БИКБанкаДляРасчетов = """"
	|		И БанковскиеСчета.СВИФТБанкаДляРасчетов = """")
	|	И БанковскиеСчета.Ссылка В (
	|		ВЫБРАТЬ
	|			БанковскиеСчета.БанковскийСчетКонтрагента
	|		ИЗ
	|			БанковскиеСчетаКонтрагентовВрем КАК БанковскиеСчета
	|		)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БанковскиеСчета.Ссылка,
	|	БанковскиеСчета.Владелец,
	|	ВЫБОР
	|		КОГДА ИностранныйБанк ТОГДА
	|			БанковскиеСчета.СчетВБанкеДляРасчетов
	|		ИНАЧЕ
	|			ВЫБОР
	|				КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|					БанковскиеСчета.КоррСчетБанка
	|				ИНАЧЕ
	|					БанковскиеСчета.Банк.КоррСчет
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанкаДляРасчетов ТОГДА
	|		БанковскиеСчета.НаименованиеБанкаДляРасчетов
	|	ИНАЧЕ
	|		БанковскиеСчета.БанкДляРасчетов.Наименование
	|	КОНЕЦ,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанкаДляРасчетов ТОГДА
	|		БанковскиеСчета.БИКБанкаДляРасчетов
	|	ИНАЧЕ
	|		БанковскиеСчета.БанкДляРасчетов.Код
	|	КОНЕЦ,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанкаДляРасчетов ТОГДА
	|		БанковскиеСчета.КоррСчетБанкаДляРасчетов
	|	ИНАЧЕ
	|		БанковскиеСчета.БанкДляРасчетов.КоррСчет
	|	КОНЕЦ,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанкаДляРасчетов ТОГДА
	|		БанковскиеСчета.ГородБанкаДляРасчетов
	|	ИНАЧЕ
	|		БанковскиеСчета.БанкДляРасчетов.Город
	|	КОНЕЦ,
	|	БанковскиеСчета.ТекстКорреспондента,
	|	"""",
	|	"""",
	|	БанковскиеСчета.БанкДляРасчетов.БИКРКЦ.Наименование КАК НаименованиеРКЦ
	|ИЗ
	|	Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчета
	|ГДЕ
	|	НЕ (БанковскиеСчета.БанкДляРасчетов = ЗНАЧЕНИЕ(Справочник.КлассификаторБанков.ПустаяСсылка)
	|		И БанковскиеСчета.БИКБанкаДляРасчетов = """"
	|		И БанковскиеСчета.СВИФТБанкаДляРасчетов = """")
	|	И БанковскиеСчета.Ссылка В (
	|		ВЫБРАТЬ
	|			БанковскиеСчета.БанковскийСчетКонтрагента
	|		ИЗ
	|			БанковскиеСчетаКонтрагентовВрем КАК БанковскиеСчета
	|		)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БанковскиеСчета.Ссылка КАК Ссылка,
	|	БанковскиеСчета.Владелец КАК Владелец,
	|	БанковскиеСчета.НомерСчета КАК НомерСчета,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчета.НаименованиеБанка
	|	ИНАЧЕ
	|		БанковскиеСчета.Банк.Наименование
	|	КОНЕЦ КАК НаименованиеБанка,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчета.БИКБанка
	|	ИНАЧЕ
	|		БанковскиеСчета.Банк.Код
	|	КОНЕЦ КАК БИК,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчета.КоррСчетБанка
	|	ИНАЧЕ
	|		БанковскиеСчета.Банк.КоррСчет
	|	КОНЕЦ КАК КоррСчет,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчета.ГородБанка
	|	ИНАЧЕ
	|		БанковскиеСчета.Банк.Город
	|	КОНЕЦ КАК Город,
	|	БанковскиеСчета.Банк.БИКРКЦ.Наименование КАК НаименованиеРКЦ
	|ПОМЕСТИТЬ БанковскиеСчетаОрганизаций
	|ИЗ
	|	Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчета
	|ГДЕ
	|	(БанковскиеСчета.БанкДляРасчетов = ЗНАЧЕНИЕ(Справочник.КлассификаторБанков.ПустаяСсылка)
	|		И БанковскиеСчета.БИКБанкаДляРасчетов = """"
	|		И БанковскиеСчета.СВИФТБанкаДляРасчетов = """")
	|	И БанковскиеСчета.Ссылка В (
	|		ВЫБРАТЬ
	|			БанковскиеСчета.БанковскийСчет КАК БанковскийСчет
	|		ИЗ
	|			БанковскиеСчетаОрганизацийВрем КАК БанковскиеСчета
	|		)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БанковскиеСчета.Ссылка,
	|	БанковскиеСчета.Владелец,
	|	ВЫБОР
	|		КОГДА ИностранныйБанк ТОГДА
	|			БанковскиеСчета.СчетВБанкеДляРасчетов
	|		ИНАЧЕ
	|			ВЫБОР
	|				КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|					БанковскиеСчета.КоррСчетБанка
	|				ИНАЧЕ
	|					БанковскиеСчета.Банк.КоррСчет
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанкаДляРасчетов ТОГДА
	|		БанковскиеСчета.НаименованиеБанкаДляРасчетов
	|	ИНАЧЕ
	|		БанковскиеСчета.БанкДляРасчетов.Наименование
	|	КОНЕЦ,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанкаДляРасчетов ТОГДА
	|		БанковскиеСчета.БИКБанкаДляРасчетов
	|	ИНАЧЕ
	|		БанковскиеСчета.БанкДляРасчетов.Код
	|	КОНЕЦ,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанкаДляРасчетов ТОГДА
	|		БанковскиеСчета.КоррСчетБанкаДляРасчетов
	|	ИНАЧЕ
	|		БанковскиеСчета.БанкДляРасчетов.КоррСчет
	|	КОНЕЦ,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанкаДляРасчетов ТОГДА
	|		БанковскиеСчета.ГородБанкаДляРасчетов
	|	ИНАЧЕ
	|		БанковскиеСчета.БанкДляРасчетов.Город
	|	КОНЕЦ,
	|	БанковскиеСчета.БанкДляРасчетов.БИКРКЦ.Наименование
	|ИЗ
	|	Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчета
	|ГДЕ
	|	НЕ (БанковскиеСчета.БанкДляРасчетов = ЗНАЧЕНИЕ(Справочник.КлассификаторБанков.ПустаяСсылка)
	|		И БанковскиеСчета.БИКБанкаДляРасчетов = """"
	|		И БанковскиеСчета.СВИФТБанкаДляРасчетов = """")
	|	И БанковскиеСчета.Ссылка В (
	|		ВЫБРАТЬ
	|			БанковскиеСчета.БанковскийСчет КАК БанковскийСчет
	|		ИЗ
	|			БанковскиеСчетаОрганизацийВрем КАК БанковскиеСчета
	|		)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(ДанныеДокумента.ИсправляемыйДокумент.Номер, ДанныеДокумента.Номер) КАК Номер,
	|	ЕСТЬNULL(ДанныеДокумента.ИсправляемыйДокумент.Дата, ДанныеДокумента.Дата) КАК ДатаДокумента,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Организация.Префикс КАК Префикс,
	|	ДанныеДокумента.Организация.НаименованиеСокращенное КАК ПлательщикНаименование,
	|	
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ИННПлательщика = """" ТОГДА
	|			ДанныеДокумента.Организация.ИНН
	|		ИНАЧЕ
	|			ДанныеДокумента.ИННПлательщика
	|	КОНЕЦ КАК ИННПлательщика,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.КПППлательщика = """"
	|				И НЕ (ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств)
	|				И ДанныеДокумента.ИННПлательщика <> """") ТОГДА
	|			""""
	|		ИНАЧЕ
	|			ДанныеДокумента.КПППлательщика
	|	КОНЕЦ КАК КПППлательщика,
	|	ДанныеДокумента.ТекстПлательщика КАК ТекстПлательщика,
	|	ДанныеДокумента.БанковскийСчет.ТекстКорреспондента КАК ТекстКорреспондента,
	|	
	|	БанковскиеСчетаКонтрагентов.Владелец.Наименование КАК ПолучательНаименование,
	|	БанковскиеСчетаКонтрагентов.Владелец.НаименованиеПолное КАК ПолучательНаименованиеПолное,
	|	БанковскиеСчетаКонтрагентов.Владелец.НаименованиеСокращенное КАК ПолучательНаименованиеСокращенное,
	|	БанковскиеСчетаКонтрагентов.Владелец КАК Получатель,
	|	
	|	ВЫБОР
	|		КОГДА БанковскиеСчетаКонтрагентов.ИННКорреспондента = """" ТОГДА
	|			БанковскиеСчетаКонтрагентов.Владелец.ИНН
	|		ИНАЧЕ
	|			БанковскиеСчетаКонтрагентов.ИННКорреспондента
	|	КОНЕЦ КАК ИННПолучателя,
	|	ВЫБОР
	|		КОГДА БанковскиеСчетаКонтрагентов.Ссылка ССЫЛКА Справочник.БанковскиеСчетаОрганизаций ТОГДА
	// КПП организации
	|			""""
	|		ИНАЧЕ ЕСТЬNULL(БанковскиеСчетаКонтрагентов.КППКорреспондента, """")
	|	КОНЕЦ КАК КПППолучателя,
	|	ЕСТЬNULL(БанковскиеСчетаКонтрагентов.ТекстКорреспондента, """") КАК ТекстПолучателя,
	|	ЕСТЬNULL(БанковскиеСчетаКонтрагентов.НаименованиеРКЦ, """") КАК НаименованиеРКЦ,
	|
	|	ДанныеДокумента.НазначениеПлатежа КАК НазначениеПлатежа,
	|	ДанныеДокумента.ВидПлатежа КАК ВидПлатежа,
	|	ДанныеДокумента.ОчередностьПлатежа КАК Очередность,
	|	ДанныеДокумента.ИдентификаторПлатежа КАК ИдентификаторПлатежа,
	|	ДанныеДокумента.КодБК КАК КодБК,
	|	ДанныеДокумента.КодОКАТО КАК КодОКАТО,
	|	ДанныеДокумента.ПоказательДаты КАК ПоказательДаты,
	|	ДанныеДокумента.ПоказательНомера КАК ПоказательНомера,
	|	ДанныеДокумента.ПоказательОснования КАК ПоказательОснования,
	|	ДанныеДокумента.ПоказательПериода КАК ПоказательПериода,
	|	ДанныеДокумента.КодВыплат КАК ПоказательТипа,
	|	ДанныеДокумента.СтатусСоставителя КАК СтатусСоставителя,
	|	ДанныеДокумента.СуммаДокумента КАК СуммаДокумента,
	|	ДанныеДокумента.ТипПлатежногоДокумента КАК ТипПлатежногоДокумента,
	|	ДанныеДокумента.КодВидаДохода КАК КодВидаДохода,
	|
	|	ЕСТЬNULL(БанковскиеСчетаКонтрагентов.НомерСчета, """") КАК НомерСчетаПолучателя,
	|	ЕСТЬNULL(БанковскиеСчетаКонтрагентов.НаименованиеБанка, """") КАК НаименованиеБанкаПолучателя,
	|	ЕСТЬNULL(БанковскиеСчетаКонтрагентов.БИК, """") КАК БИКБанкаПолучателя,
	|	ЕСТЬNULL(БанковскиеСчетаКонтрагентов.КоррСчет, """") КАК СчетБанкаПолучателя,
	|	ЕСТЬNULL(БанковскиеСчетаКонтрагентов.Город, """") КАК ГородБанкаПолучателя,
	|
	|	ДанныеДокумента.БанковскийСчет.ВариантВыводаМесяца КАК ВариантВыводаМесяца,
	|	ДанныеДокумента.БанковскийСчет.ВыводитьСуммуБезКопеек КАК ВыводитьСуммуБезКопеек,
	|	ДанныеДокумента.БанковскийСчет.ВалютаДенежныхСредств КАК ВалютаДенежныхСредств,
	|
	|	ВЫБОР КОГДА ДанныеДокумента.БанковскийСчет.РучноеИзменениеРеквизитовБанкаДляРасчетов ТОГДА
	|		ДанныеДокумента.БанковскийСчет.НаименованиеБанкаДляРасчетов
	|	ИНАЧЕ
	|		ДанныеДокумента.БанковскийСчет.БанкДляРасчетов.Наименование
	|	КОНЕЦ КАК БанкДляРасчетов,
	|	ДанныеДокумента.БанковскийСчет.НомерСчета КАК НомерСчета,
	|	ВЫБОР КОГДА ДанныеДокумента.БанковскийСчет.РучноеИзменениеРеквизитовБанка ТОГДА
	|		ДанныеДокумента.БанковскийСчет.НаименованиеБанка
	|	ИНАЧЕ
	|		ДанныеДокумента.БанковскийСчет.Банк.Наименование
	|	КОНЕЦ КАК Банк,
	|	ВЫБОР КОГДА ДанныеДокумента.БанковскийСчет.РучноеИзменениеРеквизитовБанка ТОГДА
	|		ДанныеДокумента.БанковскийСчет.ГородБанка
	|	ИНАЧЕ
	|		ДанныеДокумента.БанковскийСчет.Банк.Город
	|	КОНЕЦ КАК Город,
	|
	|	ЕСТЬNULL(БанковскиеСчетаОрганизаций.НомерСчета, """") КАК НомерСчетаПлательщика,
	|	ЕСТЬNULL(БанковскиеСчетаОрганизаций.НаименованиеБанка, """") КАК НаименованиеБанкаПлательщика,
	|	ЕСТЬNULL(БанковскиеСчетаОрганизаций.БИК, """") КАК БИКБанкаПлательщика,
	|	ЕСТЬNULL(БанковскиеСчетаОрганизаций.КоррСчет, """") КАК СчетБанкаПлательщика,
	|	ЕСТЬNULL(БанковскиеСчетаОрганизаций.Город, """") КАК ГородБанкаПлательщика,
	|	ЕСТЬNULL(БанковскиеСчетаОрганизаций.НаименованиеРКЦ, """") КАК НаименованиеРКЦПлательщика
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств КАК ДанныеДокумента
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		БанковскиеСчетаКонтрагентов КАК БанковскиеСчетаКонтрагентов
	|	ПО
	|		ДанныеДокумента.БанковскийСчетКонтрагента = БанковскиеСчетаКонтрагентов.Ссылка
	|		ИЛИ 
	|		(ДанныеДокумента.БанковскийСчетПолучатель = БанковскиеСчетаКонтрагентов.Ссылка
	|		И ДанныеДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты))
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		БанковскиеСчетаОрганизаций КАК БанковскиеСчетаОрганизаций
	|	ПО
	|		ДанныеДокумента.БанковскийСчет = БанковскиеСчетаОрганизаций.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&МассивДокументов)
	|	И ДанныеДокумента.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Дата КАК ДатаДокумента,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Организация.Префикс КАК Префикс,
	|	ДанныеДокумента.Организация.НаименованиеСокращенное КАК ПлательщикНаименование,
	|	
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ИННПлательщика = """" ТОГДА
	|			ДанныеДокумента.Организация.ИНН
	|		ИНАЧЕ
	|			ДанныеДокумента.ИННПлательщика
	|	КОНЕЦ КАК ИННПлательщика,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.КПППлательщика = """"
	|				И НЕ (ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств)
	|				И ДанныеДокумента.ИННПлательщика <> """") ТОГДА
	// КПП организации
	|			""""
	|		ИНАЧЕ
	|			ДанныеДокумента.КПППлательщика
	|	КОНЕЦ КАК КПППлательщика,
	|	ДанныеДокумента.ТекстПлательщика КАК ТекстПлательщика,
	|	ЕстьNULL(Распределение.БанковскийСчетКасса.ТекстКорреспондента, ДанныеДокумента.БанковскийСчет.ТекстКорреспондента) КАК ТекстКорреспондента,
	|	
	|	БанковскиеСчетаКонтрагентов.Владелец.Наименование КАК ПолучательНаименование,
	|	БанковскиеСчетаКонтрагентов.Владелец.НаименованиеПолное КАК ПолучательНаименованиеПолное,
	|	БанковскиеСчетаКонтрагентов.Владелец.НаименованиеСокращенное КАК ПолучательНаименованиеСокращенное,
	|	БанковскиеСчетаКонтрагентов.Владелец КАК Получатель,
	|	
	|	ВЫБОР
	|		КОГДА БанковскиеСчетаКонтрагентов.ИННКорреспондента = """" ТОГДА
	|			БанковскиеСчетаКонтрагентов.Владелец.ИНН
	|		ИНАЧЕ
	|			БанковскиеСчетаКонтрагентов.ИННКорреспондента
	|	КОНЕЦ КАК ИННПолучателя,
	|	ВЫБОР
	|		КОГДА БанковскиеСчетаКонтрагентов.Ссылка ССЫЛКА Справочник.БанковскиеСчетаОрганизаций ТОГДА
	// КПП организации
	|			""""
	|		ИНАЧЕ ЕСТЬNULL(БанковскиеСчетаКонтрагентов.КППКорреспондента, """")
	|	КОНЕЦ КАК КПППолучателя,
	|	ЕСТЬNULL(БанковскиеСчетаКонтрагентов.ТекстКорреспондента, """") КАК ТекстПолучателя,
	|	ЕСТЬNULL(БанковскиеСчетаКонтрагентов.НаименованиеРКЦ, """") КАК НаименованиеРКЦ,
	|
	|	ДанныеДокумента.НазначениеПлатежа КАК НазначениеПлатежа,
	|	"""" КАК ВидПлатежа,
	|	0 КАК Очередность,
	|	ДанныеДокумента.ИдентификаторПлатежа КАК ИдентификаторПлатежа,
	|	ДанныеДокумента.КодБК КАК КодБК,
	|	ДанныеДокумента.КодОКАТО КАК КодОКАТО,
	|	ДанныеДокумента.ПоказательДаты КАК ПоказательДаты,
	|	ДанныеДокумента.ПоказательНомера КАК ПоказательНомера,
	|	ДанныеДокумента.ПоказательОснования КАК ПоказательОснования,
	|	ДанныеДокумента.ПоказательПериода КАК ПоказательПериода,
	|	ДанныеДокумента.КодВыплат КАК ПоказательТипа,
	|	ДанныеДокумента.СтатусСоставителя КАК СтатусСоставителя,
	|	ЕстьNULL(Распределение.Сумма, ДанныеДокумента.СуммаДокумента) КАК СуммаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежныхДокументов.ПлатежноеПоручение) КАК ТипПлатежногоДокумента,
	|	ДанныеДокумента.КодВидаДохода КАК КодВидаДохода,
	|
	|	ЕСТЬNULL(БанковскиеСчетаКонтрагентов.НомерСчета, """") КАК НомерСчетаПолучателя,
	|	ЕСТЬNULL(БанковскиеСчетаКонтрагентов.НаименованиеБанка, """") КАК НаименованиеБанкаПолучателя,
	|	ЕСТЬNULL(БанковскиеСчетаКонтрагентов.БИК, """") КАК БИКБанкаПолучателя,
	|	ЕСТЬNULL(БанковскиеСчетаКонтрагентов.КоррСчет, """") КАК СчетБанкаПолучателя,
	|	ЕСТЬNULL(БанковскиеСчетаКонтрагентов.Город, """") КАК ГородБанкаПолучателя,
	|
	|	ЕСТЬNULL(ЕстьNULL(Распределение.БанковскийСчетКасса.ВариантВыводаМесяца, ДанныеДокумента.БанковскийСчет.ВариантВыводаМесяца), ЗНАЧЕНИЕ(Перечисление.ВариантыВыводаМесяцаВДатеДокумента.Числом)) КАК ВариантВыводаМесяца,
	|	ЕСТЬNULL(ЕстьNULL(Распределение.БанковскийСчетКасса.ВыводитьСуммуБезКопеек, ДанныеДокумента.БанковскийСчет.ВыводитьСуммуБезКопеек), ЛОЖЬ) КАК ВыводитьСуммуБезКопеек,
	|	ЕСТЬNULL(ЕстьNULL(Распределение.БанковскийСчетКасса.ВалютаДенежныхСредств, ДанныеДокумента.БанковскийСчет.ВалютаДенежныхСредств), ДанныеДокумента.Валюта) КАК ВалютаДенежныхСредств,
	|
	|	ВЫБОР КОГДА ДанныеДокумента.БанковскийСчет.РучноеИзменениеРеквизитовБанкаДляРасчетов ТОГДА
	|		ДанныеДокумента.БанковскийСчет.НаименованиеБанкаДляРасчетов
	|	ИНАЧЕ
	|		ДанныеДокумента.БанковскийСчет.БанкДляРасчетов.Наименование
	|	КОНЕЦ КАК БанкДляРасчетов,
	|	ДанныеДокумента.БанковскийСчет.НомерСчета КАК НомерСчета,
	|	ВЫБОР КОГДА ДанныеДокумента.БанковскийСчет.РучноеИзменениеРеквизитовБанка ТОГДА
	|		ДанныеДокумента.БанковскийСчет.НаименованиеБанка
	|	ИНАЧЕ
	|		ДанныеДокумента.БанковскийСчет.Банк.Наименование
	|	КОНЕЦ КАК Банк,
	|	ВЫБОР КОГДА ДанныеДокумента.БанковскийСчет.РучноеИзменениеРеквизитовБанка ТОГДА
	|		ДанныеДокумента.БанковскийСчет.ГородБанка
	|	ИНАЧЕ
	|		ДанныеДокумента.БанковскийСчет.Банк.Город
	|	КОНЕЦ КАК Город,
	|
	|	ЕСТЬNULL(ЕстьNULL(БанковскиеСчетаОрганизацийРаспределение.НомерСчета, БанковскиеСчетаОрганизаций.НомерСчета), """") КАК НомерСчетаПлательщика,
	|	ЕСТЬNULL(ЕстьNULL(БанковскиеСчетаОрганизацийРаспределение.НаименованиеБанка, БанковскиеСчетаОрганизаций.НаименованиеБанка), """") КАК НаименованиеБанкаПлательщика,
	|	ЕСТЬNULL(ЕстьNULL(БанковскиеСчетаОрганизацийРаспределение.БИК, БанковскиеСчетаОрганизаций.БИК), """") КАК БИКБанкаПлательщика,
	|	ЕСТЬNULL(ЕстьNULL(БанковскиеСчетаОрганизацийРаспределение.КоррСчет, БанковскиеСчетаОрганизаций.КоррСчет), """") КАК СчетБанкаПлательщика,
	|	ЕСТЬNULL(ЕстьNULL(БанковскиеСчетаОрганизацийРаспределение.Город, БанковскиеСчетаОрганизаций.Город), """") КАК ГородБанкаПлательщика,
	|	ЕСТЬNULL(ЕСТЬNULL(БанковскиеСчетаОрганизацийРаспределение.НаименованиеРКЦ, """"), """") КАК НаименованиеРКЦПлательщика
	|ИЗ
	|	Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		БанковскиеСчетаКонтрагентов КАК БанковскиеСчетаКонтрагентов
	|	ПО
	|		ДанныеДокумента.БанковскийСчетКонтрагента = БанковскиеСчетаКонтрагентов.Ссылка
	|		ИЛИ (ДанныеДокумента.БанковскийСчетПолучатель = БанковскиеСчетаКонтрагентов.Ссылка
	|			И ДанныеДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты))
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		БанковскиеСчетаОрганизаций КАК БанковскиеСчетаОрганизаций
	|	ПО
	|		ДанныеДокумента.БанковскийСчет = БанковскиеСчетаОрганизаций.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ЗаявкаНаРасходованиеДенежныхСредств.РаспределениеПоСчетам КАК Распределение
	|	ПО
	|		Распределение.Ссылка = ДанныеДокумента.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		БанковскиеСчетаОрганизаций КАК БанковскиеСчетаОрганизацийРаспределение
	|	ПО
	|		Распределение.БанковскийСчетКасса = БанковскиеСчетаОрганизацийРаспределение.Ссылка
	|	
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&МассивДокументов)
	|	И ДанныеДокумента.Проведен
	|	И ДанныеДокумента.ФормаОплатыБезналичная
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номер
	|";
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ДанныеКонтрагентов = ДенежныеСредстваСервер.СведенияОПолучателеПлательщикеПоДокументам(МассивОбъектов);
	
	Пока Выборка.Следующий() Цикл
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если Выборка.ТипПлатежногоДокумента <> Перечисления.ТипыПлатежныхДокументов.ПлатежноеПоручение Тогда
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Платежное поручение не формируется для типа документа: %1'"),
				Выборка.ТипПлатежногоДокумента);
			ОбщегоНазначения.СообщитьПользователю(
				Текст,
				Выборка.Ссылка.ПолучитьОбъект(),
				"ТипПлатежногоДокумента",
				, // ПутьКДанным
				// Отказ
			);
		Иначе
		
			ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
			ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьМакета, Выборка.Ссылка);
			ОбластьМакета.Параметры.Заполнить(Выборка);
			
			// Заполним текст плательщика.
			Если Не ПустаяСтрока(Выборка.ТекстПлательщика) Тогда
				ТекстПлательщика = Выборка.ТекстПлательщика;
			Иначе
				Если Не ПустаяСтрока(Выборка.ТекстКорреспондента) Тогда
					ТекстПлательщика = Выборка.ТекстКорреспондента;
				Иначе
					// Наименование сокращенное плательщика
					ОтборНаименования = Новый Структура("Документ, Признак", Выборка.Ссылка, 4);
					СведенияОНаименовании = ДанныеКонтрагентов.НайтиСтроки(ОтборНаименования);
					ТекстПлательщика = ?(СведенияОНаименовании.Количество() > 0, 
										СведенияОНаименовании[0].ЗначениеРеквизита,
										Выборка.ПлательщикНаименование);
					Если ЗначениеЗаполнено(Выборка.БанкДляРасчетов) Тогда
						ТекстПлательщика = ТекстПлательщика
						+ " р/с " + СокрЛП(Выборка.НомерСчета)
						+ " в " + Строка(Выборка.Банк)
						+ " " + Выборка.Город;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			ОбластьМакета.Параметры.ТекстПлательщика = ТекстПлательщика;
			
			Если ПустаяСтрока(ТекстПлательщика) Тогда
				Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'У организации %1 не заполнено поле ""Сокращенное наименование""'"),
					Выборка.Организация);
				ОбщегоНазначения.СообщитьПользователю(
					Текст,
					Выборка.Организация.ПолучитьОбъект(),
					"НаименованиеСокращенное");
			КонецЕсли;
			
			// Заполним текст получателя.
			Если Не ПустаяСтрока(Выборка.ТекстПолучателя) Тогда
				ТекстПолучателя = Выборка.ТекстПолучателя;
			ИначеЕсли ТипЗнч(Выборка.Получатель) = Тип("СправочникСсылка.Организации") Тогда
				// Наименование сокращенное получателя
				ОтборНаименования = Новый Структура("Документ, Признак", Выборка.Ссылка, 2);
				СведенияОНаименовании = ДанныеКонтрагентов.НайтиСтроки(ОтборНаименования);
				ТекстПолучателя = ?(СведенияОНаименовании.Количество() > 0, 
									СведенияОНаименовании[0].ЗначениеРеквизита,
									Выборка.ПолучательНаименованиеСокращенное);
			ИначеЕсли ТипЗнч(Выборка.Получатель) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
				ТекстПолучателя = Выборка.ПолучательНаименование;
			Иначе
				ОтборНаименования = Новый Структура("Документ, Признак", Выборка.Ссылка, 0);
				СведенияОНаименовании = ДанныеКонтрагентов.НайтиСтроки(ОтборНаименования);
				ТекстПолучателя = ?(СведенияОНаименовании.Количество() > 0, 
									СведенияОНаименовании[0].ЗначениеРеквизита,
									Выборка.ПолучательНаименованиеПолное);
			КонецЕсли;
			ОбластьМакета.Параметры.ТекстПолучателя = ТекстПолучателя;
			
			Если ПустаяСтрока(ТекстПолучателя) И ЗначениеЗаполнено(Выборка.Получатель) Тогда
				Если ТипЗнч(Выборка.Получатель) = Тип("СправочникСсылка.Организации") Тогда
					ИмяПоля = "НаименованиеСокращенное";
					Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'У организации-получателя %1 не заполнено поле ""Сокращенное наименование""'"),
						Выборка.Получатель);
				ИначеЕсли ТипЗнч(Выборка.Получатель) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
					ИмяПоля = "ФИО";
					Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'У физического лица %1 не заполнено поле ""ФИО""'"),
						Выборка.Получатель);
				Иначе
					ИмяПоля = "НаименованиеПолное";
					Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'У контрагента %1 не заполнено поле ""Сокращенное наименование""'"),
						Выборка.Получатель);
				КонецЕсли; 
				ОбщегоНазначения.СообщитьПользователю(
					Текст,
					Выборка.Получатель.ПолучитьОбъект(),
					ИмяПоля);
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(Выборка.НаименованиеРКЦПлательщика) Тогда
				ОбластьМакета.Параметры.НаименованиеБанкаПлательщика =
					Выборка.НаименованиеБанкаПлательщика + " " + Выборка.ГородБанкаПлательщика;
			Иначе
				ОбластьМакета.Параметры.НаименованиеБанкаПлательщика = Выборка.НаименованиеРКЦПлательщика
																		+ "//"
																		+ Выборка.НаименованиеБанкаПлательщика
																		+ " "
																		+ Выборка.ГородБанкаПлательщика;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Выборка.НаименованиеРКЦ) Тогда
				ОбластьМакета.Параметры.НаименованиеБанкаПолучателя =
					Выборка.НаименованиеРКЦ + "//" + Выборка.НаименованиеБанкаПолучателя + " " + Выборка.ГородБанкаПолучателя;
			Иначе
				ОбластьМакета.Параметры.НаименованиеБанкаПолучателя =
					Выборка.НаименованиеБанкаПолучателя + " " + Выборка.ГородБанкаПолучателя;
			КонецЕсли;
			
			// Заполним ИНН и КПП.
			ОбластьМакета.Параметры.ИННПлательщика = "ИНН " + ?(ПустаяСтрока(Выборка.ИННПлательщика), "0", Выборка.ИННПлательщика);
			ОбластьМакета.Параметры.ИННПолучателя = "ИНН " + ?(ПустаяСтрока(Выборка.ИННПолучателя), "0", Выборка.ИННПолучателя);
			
			ОтборКПППлательщика = Новый Структура("Документ, Признак", Выборка.Ссылка, 7);
			СведенияОКПППлательщика = ДанныеКонтрагентов.НайтиСтроки(ОтборКПППлательщика);
			КПППлательщика = "0";
			
			Если СведенияОКПППлательщика.Количество() > 0 Тогда
				КПППлательщика =
					?(СведенияОКПППлательщика[0].ЗначениеРеквизита = "", "0", СведенияОКПППлательщика[0].ЗначениеРеквизита);
			КонецЕсли;
			
			ОбластьМакета.Параметры.КПППлательщика =
				"КПП " + ?(ПустаяСтрока(Выборка.КПППлательщика), КПППлательщика, Выборка.КПППлательщика);
			
			ОтборКПП = Новый Структура("Документ, Признак", Выборка.Ссылка, 1);
			СведенияОКПП = ДанныеКонтрагентов.НайтиСтроки(ОтборКПП);
			КППКонтрагента = "0";
			
			Если СведенияОКПП.Количество() > 0 Тогда
				КППКонтрагента = ?(СведенияОКПП[0].ЗначениеРеквизита = "", "0", СведенияОКПП[0].ЗначениеРеквизита);
			КонецЕсли;
			
			ОбластьМакета.Параметры.КПППолучателя = "КПП " + ?(Выборка.КПППолучателя <> "", Выборка.КПППолучателя, КППКонтрагента);
			
			Если ТипЗнч(Выборка.Ссылка) = Тип("ДокументСсылка.СписаниеБезналичныхДенежныхСредств") Тогда
				НомерДокументаДляПечати = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(
					Выборка.Номер,
					Истина, // УдалитьПрефиксИнформационнойБазы
					Истина); // УдалитьПользовательскийПрефикс
				ОбластьМакета.Параметры.НаименованиеНомер = НСтр("ru='ПЛАТЕЖНОЕ ПОРУЧЕНИЕ №'", Метаданные.Языки.Русский.КодЯзыка) + " " + НомерДокументаДляПечати;
			Иначе
				ОбластьМакета.Параметры.НаименованиеНомер = НСтр("ru='ОБРАЗЕЦ ПЛАТЕЖНОГО ПОРУЧЕНИЯ'", Метаданные.Языки.Русский.КодЯзыка);
			КонецЕсли;
			ОбластьМакета.Параметры.СуммаЧислом = ФормированиеПечатныхФорм.СуммаПлатежногоДокумента(
				Выборка.СуммаДокумента,
				Выборка.ВыводитьСуммуБезКопеек);
			ОбластьМакета.Параметры.СуммаПрописью = РаботаСКурсамиВалютУТ.СформироватьСуммуПрописью(
				Выборка.СуммаДокумента,
				Выборка.ВалютаДенежныхСредств,
				Выборка.ВыводитьСуммуБезКопеек);
			Если Выборка.ВариантВыводаМесяца = Перечисления.ВариантыВыводаМесяцаВДатеДокумента.Прописью Тогда
				ФорматДаты = "ДЛФ=ДД'";
			Иначе
				ФорматДаты = "ДЛФ=D'";
			КонецЕсли;
			ОбластьМакета.Параметры.ДатаДокумента = Формат(Выборка.ДатаДокумента, ФорматДаты);
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(
				ТабличныйДокумент,
				НомерСтрокиНачало,
				ОбъектыПечати,
				Выборка.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции


// Возвращает строковое представление реквизитов платежного документа.
//
// Параметры:
//   ДатаПП - Дата - дата платежного поручения
//   НомерПП - Строка - номер платежного поручения.
//
// Возвращаемое значение:
//   Строка - строковое представление
//
Функция РеквизитыПлатежногоПорученияПоДатеНомеру(ДатаПП, НомерПП) Экспорт

	Возврат СтрШаблон(НСтр("ru='№ %1 от %2'", Метаданные.Языки.Русский.КодЯзыка), НомерПП, Формат(ДатаПП, "ДЛФ=D"));

КонецФункции

//-- Локализация
#КонецОбласти


#Область Фискализация

//++ Локализация

// Возвращает параметры операции фискализации чека для печати чека по документу
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Форма документ, из которого печатается чек - содержит:
// 	* Объект - ДокументОбъект - Документ-объект, основной параметр формы.
// Возвращаемое значение:
// 	Структура - Структура параметров операции фискализации чека
Функция ОсновныеПараметрыОперации(Форма) Экспорт
	
	Объект = Форма.Объект;
	
	ОсновныеПараметрыОперации = ФормированиеФискальныхЧековСерверПереопределяемый.СтруктураОсновныхПараметровОперации();
	
	ОсновныеПараметрыОперации.ДокументСсылка       = Объект.Ссылка;
	ОсновныеПараметрыОперации.Организация          = Объект.Организация;
	ОсновныеПараметрыОперации.Контрагент           = Объект.Контрагент;
	ОсновныеПараметрыОперации.Партнер              = Форма.Партнер;
	ОсновныеПараметрыОперации.Валюта               = Объект.Валюта;
	ОсновныеПараметрыОперации.СуммаДокумента       = Объект.СуммаДокумента;
	
	ОсновныеПараметрыОперации.ИмяРеквизитаГиперссылкиНаФорме = "ФискальнаяОперацияСтатус";

	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту Тогда
		ОсновныеПараметрыОперации.ИмяКомандыПробитияЧека = "ПробитьЧек";
	КонецЕсли;
	
	Возврат ОсновныеПараметрыОперации;
	
КонецФункции

// Определяет, разрешено ли пробитие фискального чека по документу
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Форма документ, из которого печатается чек - содержит:
// 	* Объект - ДокументОбъект - Документ-объект, основной параметр формы.
// Возвращаемое значение:
// 	Булево - Истина, если разрешено пробитие чека
Функция РазрешеноПробитиеФискальныхЧековПоДокументу(Форма) Экспорт
	
	Объект = Форма.Объект;
	
	РазрешеноПробитиеФискальныхЧековПоДокументу =
		(Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоставщику
			Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту
			Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств)
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Контрагент, "ЮрФизЛицо") = Перечисления.ЮрФизЛицо.ФизЛицо
		И Объект.ПроведеноБанком;
	
	Возврат РазрешеноПробитиеФискальныхЧековПоДокументу;
	
КонецФункции

// Формирует массив форматированных строк для формирования гиперссылки пробития фискального чека
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ-ссылка, по которому пробивается фискальный чек
// 	Форма - ФормаКлиентскогоПриложения - Форма документ, из которого печатается чек - содержит:
// 	* Объект - ДокументОбъект - Документ-объект, основной параметр формы.
// 	МассивПредставлений - Массив из ФорматированнаяСтрока - Массив форматированных строк для формирования гиперссылки
//    пробития фискального чека.
Процедура ОбновитьГиперссылкуПробитияФискальногоЧека(ДокументСсылка, Форма, МассивПредставлений) Экспорт
	
	ФискальнаяОперацияДанныеЖурнала = ФормированиеФискальныхЧековСервер.ДанныеПробитогоФискальногоЧекаПоДокументу(ДокументСсылка);
	
	Если ФискальнаяОперацияДанныеЖурнала <> Неопределено Тогда
		
		НомерЧекаККМ = ФискальнаяОперацияДанныеЖурнала.НомерЧекаККМ;
		ТекстСсылки = "ОткрытьЗаписьФискальнойОперации";
		
		ФормированиеФискальныхЧековСервер.ДобавитьВПредставлениеГиперссылкиКомандуЧекПробит(МассивПредставлений, НомерЧекаККМ, ТекстСсылки);
		
	ИначеЕсли ФормированиеФискальныхЧековСервер.ЕстьПравоНаПробитиеФискальногоЧекаПоДокументу(ДокументСсылка) Тогда
		
		Если ФормированиеФискальныхЧековСервер.ЕстьПодключенноеОборудованиеККассамОрганизации(Форма.Объект.Организация) Тогда 
			ФормированиеФискальныхЧековСервер.ДобавитьВПредставлениеГиперссылкиКомандуПробитьЧек(МассивПредставлений, "ПробитьЧек");
		Иначе
			ФормированиеФискальныхЧековСервер.ДобавитьВПредставлениеГиперссылкиСтатусЧекНеПробит(МассивПредставлений, "НастроитьОборудование");
		КонецЕсли;
		
	Иначе
		
		ФормированиеФискальныхЧековСервер.ДобавитьВПредставлениеГиперссылкиСтатусЧекНеПробит(МассивПредставлений);
		
	КонецЕсли;
	
КонецПроцедуры

// Определяет виды фискальных чеков, доступных по документу
// 
// Параметры:
// 	ВидыЧеков - ТаблицаЗначений - Таблица значений, содержащая виды фискальных чеков, доступных по документу
// 	Операция - ПеречислениеСсылка.ХозяйственныеОперации - Хозяйственная операция по документу
// 	ИмяКомандыПробитияЧека - Строка - Имя команды пробития чека
Процедура ЗаполнитьВидыФискальныхЧековПоДокументу(ВидыЧеков, Операция, ИмяКомандыПробитияЧека) Экспорт
	
	ТипРасчетовДенежнымиСредствами = Перечисления.ТипыРасчетаДенежнымиСредствами.РасходДенежныхСредств;
	Если Операция = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту Тогда
		ТипРасчетовДенежнымиСредствами = Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств;
	КонецЕсли;
	
	ВидЧека = ВидыЧеков.Добавить();
	ВидЧека.ТипФискальногоДокумента = Перечисления.ТипыФискальныхДокументовККТ.КассовыйЧек;
	ВидЧека.ТипРасчетаДенежнымиСредствами = ТипРасчетовДенежнымиСредствами;
	ВидЧека.ВидЧекаКоррекции = Неопределено;
	
	Если ФормированиеФискальныхЧековСерверПереопределяемый.РазрешенКассовыйЧекКоррекцииДляТипаРасчетов(ТипРасчетовДенежнымиСредствами) Тогда
		ВидЧека = ВидыЧеков.Добавить();
		ВидЧека.ТипФискальногоДокумента = Перечисления.ТипыФискальныхДокументовККТ.КассовыйЧекКоррекции;
		ВидЧека.ТипРасчетаДенежнымиСредствами = ТипРасчетовДенежнымиСредствами;
		ВидЧека.ВидЧекаКоррекции = Перечисления.ВидыЧековКоррекции.НеприменениеККТ;
	КонецЕсли;
	
КонецПроцедуры

// Определяет систему налогообложения по документу
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ для определения системы налогообложения
// Возвращаемое значение:
// 	ПеречислениеСсылка.ТипыСистемНалогообложенияККТ - Система налогообложения по документу
Функция СистемаНалогообложенияПоДокументу(ДокументСсылка) Экспорт
	
	РасшифровкаПлатежа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "РасшифровкаПлатежа").Выгрузить();
	
	МассивОбъектовРасчетов = РасшифровкаПлатежа.ВыгрузитьКолонку("ОбъектРасчетов");
	МассивОбъектовРасчетов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивОбъектовРасчетов, "Объект, НалогообложениеНДС");
	
	СистемаНалогообложения = Неопределено;
	
	Для Каждого ОбъектРасчетов Из МассивОбъектовРасчетов Цикл
		
		Если РозничныеПродажиЛокализация.ДокументЗакупкиПоПатенту(ОбъектРасчетов.Значение.Объект) Тогда
			СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.Патент;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если СистемаНалогообложения = Неопределено Тогда
		НалогообложениеНДСПоОбъектамРасчетов = Новый Массив();
		Для Каждого ОбъектРасчетов Из МассивОбъектовРасчетов Цикл
			
			Если ЗначениеЗаполнено(ОбъектРасчетов.Значение.НалогообложениеНДС) Тогда
				Если НалогообложениеНДСПоОбъектамРасчетов.Найти(ОбъектРасчетов.Значение.НалогообложениеНДС) = Неопределено Тогда
					НалогообложениеНДСПоОбъектамРасчетов.Добавить(ОбъектРасчетов.Значение.НалогообложениеНДС);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		Если НалогообложениеНДСПоОбъектамРасчетов.Количество() = 1 Тогда
			Если НалогообложениеНДСПоОбъектамРасчетов[0] = Перечисления.ТипыНалогообложенияНДС.ПродажаПоПатенту Тогда
				СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.Патент;
			ИначеЕсли НалогообложениеНДСПоОбъектамРасчетов[0] = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД Тогда
				СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если СистемаНалогообложения = Неопределено Тогда
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Организация");
		СистемаНалогообложения = РозничныеПродажиЛокализация.СистемаНалогообложенияФискальнойОперации(РеквизитыДокумента.Организация);
	КонецЕсли;
	
	Возврат СистемаНалогообложения;
	
КонецФункции

// Возвращает наименование клиента, кто внес или получил денежные средства в качестве аванса
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ для определения системы налогообложения
// Возвращаемое значение:
// 	Строка - Наименование клиента платежа-аванса
Функция КлиентАвансовогоПлатежаНаименование(ДокументСсылка) Экспорт
	
	Клиент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Контрагент");
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Клиент, "НаименованиеПолное");
	
КонецФункции

//-- Локализация

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация

	
	ТекстЗапросаТаблицаРасчетыПоЕдиномуНалоговомуСчету(Запрос, ТекстыЗапроса, Регистры);

	//-- Локализация
	
КонецПроцедуры

//++ Локализация


Функция ТекстЗапросаТаблицаРасчетыПоЕдиномуНалоговомуСчету(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасчетыПоЕдиномуНалоговомуСчету";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ДанныеДокумента.Ссылка КАК ПлатежныйДокумент,
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15,2)) КАК Сумма
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеВБюджет)
	|	И ДанныеДокумента.ТипНалога = ЗНАЧЕНИЕ(Справочник.ВидыНалоговВзносов.ЕдиныйНалоговыйПлатеж)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции



//-- Локализация

Функция ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам() Экспорт
	
	//++ Локализация
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыСФилиалами) КАК Статья,
	|	Организации.Ссылка КАК Аналитика,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)) КАК СуммаРегл,
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаУпр,
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаСНДС,
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаБезНДС,
	|
	|	ДанныеДокумента.ИдентификаторДокумента КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств КАК ДанныеДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ДанныеДокумента.РегистрацияВНалоговомОргане <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|		 И ДанныеДокумента.РегистрацияВНалоговомОргане = Организации.РегистрацияВНалоговомОргане
	|		 И ДанныеДокумента.Организация <> Организации.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеВБюджет)
	|	И &ПроведеноБанком
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) КАК Статья,
	|	ДанныеДокумента.ТипНалога КАК Аналитика,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)) КАК СуммаРегл,
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаУпр,
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаСНДС,
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаБезНДС,
	|
	|	ДанныеДокумента.ИдентификаторДокумента КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств КАК ДанныеДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ДанныеДокумента.РегистрацияВНалоговомОргане <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|		 И ДанныеДокумента.РегистрацияВНалоговомОргане = Организации.РегистрацияВНалоговомОргане
	|		 И ДанныеДокумента.Организация <> Организации.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеВБюджет)
	|	И &ПроведеноБанком
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	Организации.Ссылка КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыСФилиалами) КАК Статья,
	|	&Организация КАК Аналитика,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)) КАК СуммаРегл,
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаУпр,
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаСНДС,
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаБезНДС,
	|
	|	ДанныеДокумента.ИдентификаторДокумента КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств КАК ДанныеДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ДанныеДокумента.РегистрацияВНалоговомОргане <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|		 И ДанныеДокумента.РегистрацияВНалоговомОргане = Организации.РегистрацияВНалоговомОргане
	|		 И ДанныеДокумента.Организация <> Организации.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеВБюджет)
	|	И &ПроведеноБанком
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	Организации.Ссылка КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) КАК Статья,
	|	ДанныеДокумента.ТипНалога КАК Аналитика,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)) КАК СуммаРегл,
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаУпр,
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаСНДС,
	|	ВЫРАЗИТЬ(ДанныеДокумента.СуммаДокумента * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаБезНДС,
	|
	|	ДанныеДокумента.ИдентификаторДокумента КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств КАК ДанныеДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ДанныеДокумента.РегистрацияВНалоговомОргане <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|		 И ДанныеДокумента.РегистрацияВНалоговомОргане = Организации.РегистрацияВНалоговомОргане
	|		 И ДанныеДокумента.Организация <> Организации.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеВБюджет)
	|	И &ПроведеноБанком";
	
	Возврат ТекстЗапроса;
	
	//-- Локализация
	
	Возврат "";
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеНаОсновании

//++ Локализация
Процедура ЗаполнитьПоСчетуФактуреНалоговогоАгента(Объект, Знач ДокументОснование, ДанныеЗаполнения)
	
	// Заполним данные шапки документа.
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.СуммаНДС КАК СуммаНДС,
	|	ДанныеДокумента.Валюта,
	|	&Ссылка КАК ДокументОснование,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеВБюджет) КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.СчетФактураНалоговыйАгент КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&Ссылки)
	|");
	Запрос.УстановитьПараметр("Ссылки", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ДанныеЗаполнения = Новый Структура;
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		ДанныеЗаполнения.Вставить(Колонка.Имя);
	КонецЦикла;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, Выборка);
		
		НоваяСтрока = Объект.РасшифровкаПлатежа.Добавить();
		НоваяСтрока.ВалютаВзаиморасчетов = Выборка.Валюта;
		НоваяСтрока.Сумма = Выборка.СуммаНДС;
		НоваяСтрока.СуммаВзаиморасчетов = Выборка.СуммаНДС;
	КонецЦикла;
	
	Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("Сумма");
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаявлениюОВвозеТоваров(Объект, Знач ДокументОснование, ДанныеЗаполнения)
	
	ВалютаРегламентированногоУчета = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Объект.Организация);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеДокумента.Организация    КАК Организация,
	|	&ТекущаяДата                   КАК ЖелательнаяДатаПлатежа,
	|	ДанныеДокумента.СуммаДокумента КАК СуммаНДС,
	|	&Ссылка                        КАК ДокументОснование,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеВБюджет) КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.ЗаявлениеОВвозеТоваров КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&Ссылки)
	|");
	Запрос.УстановитьПараметр("Ссылки", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ДанныеЗаполнения = Новый Структура;
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		ДанныеЗаполнения.Вставить(Колонка.Имя);
	КонецЦикла;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, Выборка);
		
		НоваяСтрока = Объект.РасшифровкаПлатежа.Добавить();
		НоваяСтрока.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета;
		НоваяСтрока.Сумма = Выборка.СуммаНДС;
		НоваяСтрока.СуммаВзаиморасчетов = Выборка.СуммаНДС;
	КонецЦикла;
	
	Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("Сумма");
	
КонецПроцедуры

Процедура ЗаполнитьПоОперацииПоЯндексКассе(Объект, Знач ДокументОснование, ДанныеЗаполнения)
	
	// Заполним данные шапки документа.
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	&ТекущаяДата КАК Дата,
	                      |	ДанныеДокумента.ИдентификаторТранзакции КАК ИдентификаторТранзакции,
	                      |
	                      |	ДанныеДокумента.Организация КАК Организация,
	                      |	ДанныеДокумента.Подразделение КАК Подразделение,
	                      |
	                      |	ДанныеДокумента.Эквайер.Партнер КАК Партнер,
	                      |	ДанныеДокумента.Эквайер КАК Контрагент,
	                      |	ДанныеДокумента.Эквайер.Наименование КАК КонтрагентНаименование,
	                      |	ДанныеДокумента.Эквайер.НаименованиеПолное КАК КонтрагентНаименованиеПолное,
	                      |
	                      |	ДанныеДокумента.Валюта КАК ВалютаВзаиморасчетов,
	                      |	ДанныеДокумента.БанковскийСчет КАК БанковскийСчет,
	                      |	ДанныеДокумента.БанковскийСчет.ВалютаДенежныхСредств КАК Валюта,
	                      |	ДанныеДокумента.СуммаДокумента КАК Сумма,
	                      |
	                      |	ДанныеДокумента.СтатьяРасходов КАК СтатьяРасходов,
	                      |	ДанныеДокумента.АналитикаРасходов КАК АналитикаРасходов,
	                      |	ДанныеДокумента.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	                      |
	                      |	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту) КАК ХозяйственнаяОперация,
	                      |	&Ссылка КАК ДокументОснование
	                      |
	                      |ИЗ
	                      |	Документ.ОперацияПоЯндексКассе КАК ДанныеДокумента
	                      |ГДЕ
	                      |	ДанныеДокумента.Ссылка = &Ссылка
	                      |	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента)");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ДанныеЗаполнения = Новый Структура;
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		ДанныеЗаполнения.Вставить(Колонка.Имя);
	КонецЦикла;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, Выборка);
	
	Если ДанныеЗаполнения.ДокументОснование = Неопределено Тогда 
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Не требуется вводить Заявку на расходование денежных средств на основании документа %1, поскольку ввод доступен только на основании операций Поступления оплаты от клиента'"),
		ДокументОснование);
		ВызватьИсключение Текст;
	КонецЕсли;	
	
	ДанныеЗаполнения.Вставить("НазначениеПлатежа",
		 СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			 НСтр("ru = 'Возврат средств по договору %1 по переводу № %2 пользователя № %3, без НДС'"),
			 НСтр("ru = '<номер и дата Договора с Яндексом>'"),
			 Формат(ДанныеЗаполнения.ИдентификаторТранзакции, "ЧГ="),
			 НСтр("ru = '<номер, присвоенный Плательщику Оператором>'")));
	
	НоваяСтрока = Объект.РасшифровкаПлатежа.Добавить();
	НоваяСтрока.ВалютаВзаиморасчетов 	= Выборка.ВалютаВзаиморасчетов;
	НоваяСтрока.Сумма 					= Выборка.Сумма;
	НоваяСтрока.СуммаВзаиморасчетов 	= Выборка.Сумма;
	НоваяСтрока.Партнер 				= Выборка.Партнер;
	НоваяСтрока.СтатьяРасходов 			= Выборка.СтатьяРасходов; 
	НоваяСтрока.АналитикаРасходов 		= Выборка.АналитикаРасходов; 
	НоваяСтрока.СтавкаНДС 				= Справочники.СтавкиНДС.БезНДС;
	НоваяСтрока.СтатьяДвиженияДенежныхСредств = Выборка.СтатьяДвиженияДенежныхСредств;
	
	Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("Сумма");
	
КонецПроцедуры


Процедура ИнициализироватьДокумент(Объект, ДанныеЗаполнения = Неопределено)
	
	
	Если ТипЗнч(ДанныеЗаполнения) <> Тип("Структура")
		Или Не ДанныеЗаполнения.Свойство("ОчередностьПлатежа")
		Или Не ЗначениеЗаполнено(ДанныеЗаполнения.ОчередностьПлатежа) Тогда
		
		Если ДействуетФедеральныйЗакон345ФЗ(Объект.Дата) Тогда
			Объект.ОчередностьПлатежа = 5;
		Иначе
			Объект.ОчередностьПлатежа = 6;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("ХозяйственнаяОперация") Тогда
		Если ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет Тогда
			ДанныеЗаполнения.Вставить("ПеречислениеВБюджет", Истина);
		ИначеЕсли ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеТаможне Тогда
			ДанныеЗаполнения.Вставить("ПеречислениеВБюджет", Истина);
			ДанныеЗаполнения.Вставить("ВидПеречисленияВБюджет", Перечисления.ВидыПеречисленийВБюджет.ТаможенныйПлатеж);
			Если Не ЗначениеЗаполнено(Объект.СтатусСоставителя) Тогда
				ДанныеЗаполнения.Вставить("СтатусСоставителя", "06");
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Объект.ПоказательОснования) Тогда
				Если Не ДанныеЗаполнения.Свойство("ПоказательОснования")
					ИЛИ (ДанныеЗаполнения.Свойство("ПоказательОснования")
					И Не ЗначениеЗаполнено(ДанныеЗаполнения.ПоказательОснования)) Тогда
						ДанныеЗаполнения.Вставить("ПоказательОснования", "ДЕ");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("ХозяйственнаяОперация")
			И НЕ ЗначениеЗаполнено (Объект.ХозяйственнаяОперация)Тогда
			Объект.ХозяйственнаяОперация = ДанныеЗаполнения.ХозяйственнаяОперация;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("БанковскийСчет")
			И НЕ ЗначениеЗаполнено (Объект.БанковскийСчет)Тогда
			Объект.БанковскийСчет = ДанныеЗаполнения.БанковскийСчет;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Организация")
			И НЕ ЗначениеЗаполнено (Объект.Организация)Тогда
			Объект.Организация = ДанныеЗаполнения.Организация;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет
		ИЛИ Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств
		ИЛИ Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеТаможне Тогда
		РеквизитыПлательщика = ДенежныеСредстваСерверЛокализация.РеквизитыПлательщика(Объект);
		Если ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Или Не ДанныеЗаполнения.Свойство("ИННПлательщика") Тогда
			Объект.ИННПлательщика = РеквизитыПлательщика.ИННПлательщика;
		КонецЕсли;
		Если ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Или Не ДанныеЗаполнения.Свойство("КПППлательщика") Тогда
			Объект.КПППлательщика = РеквизитыПлательщика.КПППлательщика;
		КонецЕсли;
		Если ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Или Не ДанныеЗаполнения.Свойство("ТекстПлательщика") Тогда
			Объект.ТекстПлательщика = РеквизитыПлательщика.ТекстПлательщика;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("ХозяйственнаяОперация")
		И Не ДанныеЗаполнения.Свойство("ТипПлатежногоДокумента") Тогда
		
		Если ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СнятиеНаличныхДенежныхСредств Тогда
			
			ДанныеЗаполнения.Вставить("ТипПлатежногоДокумента", Перечисления.ТипыПлатежныхДокументов.ПлатежныйОрдер);
			ДанныеЗаполнения.Вставить("ПроведеноБанком", Истина);
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику Тогда
		
		
		Если Объект.КодировкаФайла = "" Тогда
			Объект.КодировкаФайла = "UTF-8";
		КонецЕсли;
		
		Если Объект.СписокФизЛиц Тогда
			Объект.ВидЗачисления = "07";
		КонецЕсли;
		
	КонецЕсли;
	
	Если (Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоставщику
		И Объект.ОперацияССамозанятым) Тогда
		
		Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС;
		Объект.ПеречислениеВБюджет = Ложь;
		Объект.ПлатежиПо275ФЗ = Ложь;
		ДенежныеСредстваСерверЛокализация.УстановитьКодВидаДоходаСамозанятого(Объект);
		
	КонецЕсли;
	
КонецПроцедуры

//-- Локализация
#КонецОбласти

#Область Прочее

Процедура ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра, ИсточникиДанных) Экспорт
	
	//++ Локализация
	Если ИмяРегистра = "ЛимитыРасходаДенежныхСредств" Тогда
		ИсточникиДанных.Вставить("РасшифровкаПлатежа.Ведомость");
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

//++ Локализация

// Определяет свойства полей формы в зависимости от данных
//
// Параметры:
//    Настройки - см. ДенежныеСредстваСервер.ИнициализироватьНастройкиПолейФормы.
//
Процедура ЗаполнитьНастройкиПолейФормы(Настройки) Экспорт
	
	Финансы = ФинансоваяОтчетностьСервер;
	Операции = Перечисления.ХозяйственныеОперации;
	
	ОтборВыплатаСамозанятому = ДенежныеСредстваСервер.ОтборВыплатаСамозанятому();
	ОтборСписокКонтрагентов = ДенежныеСредстваСервер.ОтборСписокКонтрагентов(ОтборВыплатаСамозанятому);
	
	#Область Шапка
	// ТипНалога
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ТипНалога");
	Элемент.Поля.Добавить("ЗаполнитьПоПредыдущемуПлатежу");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет);
	Элемент.Свойства.Вставить("Видимость");
	
	//
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ПодобратьСчетаФактуры");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет);
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ОплатаНДСВБюджетПоОтдельнымОперациям", Истина);
	Элемент.Свойства.Вставить("Видимость");
	
	
	// НадписьРеквизитыПлательщика
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("НадписьРеквизитыПлательщика");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие, , , Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПеречислениеТаможне);
	Финансы.НовыйОтбор(Элемент.Условие, "ТипПлатежногоДокумента", Перечисления.ТипыПлатежныхДокументов.ПлатежноеПоручение);
	Элемент.Свойства.Вставить("Видимость");
	
	// УведомлениеОЗачисленииВалюты
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("УведомлениеОЗачисленииВалюты");
	Финансы.НовыйОтбор(Элемент.Условие, "ТипПлатежногоДокумента", Перечисления.ТипыПлатежныхДокументов.РаспоряжениеОбОбязательнойПродаже);
	Элемент.Свойства.Вставить("Видимость");
	
	// СПАРК
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ГруппаИндексыСПАРКРиски");
	
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ОплатаПоставщику);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПеречислениеТаможне);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.КонвертацияВалюты);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ОплатаПоКредитам);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПеречислениеНаДепозиты);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВыдачаЗаймов);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыПоЗарплатномуПроекту);
	
	Финансы.НовыйОтбор(Элемент.Условие, "ТипПлатежногоДокумента", Перечисления.ТипыПлатежныхДокументов.ПлатежноеПоручение);
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ИспользованиеСПАРКРазрешено", Истина);
	Элемент.Свойства.Вставить("Видимость");
	
	// Группа реквизитов обмена с банком
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ГруппаРеквизитыОбменаСБанком");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику);
	Финансы.НовыйОтбор(Элемент.Условие, "СписокФизЛиц", Истина);
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ИспользоватьПеречислениеДенежныхСредствСпискуСотрудников", Истина);
	Элемент.Свойства.Вставить("Видимость");
	#КонецОбласти
	
	#Область Реквизиты
	// НомерВедомостиНаВыплатуЗарплаты, ДатаВедомостиНаВыплатуЗарплаты
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("НомерВедомостиНаВыплатуЗарплаты");
	Элемент.Поля.Добавить("ДатаВедомостиНаВыплатуЗарплаты");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ГруппаИ1 = Финансы.НовыйОтбор(ГруппаИли,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ1.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	Финансы.НовыйОтбор(ГруппаИ1, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыПоЗарплатномуПроекту);
	Финансы.НовыйОтбор(ГруппаИ1, "Дополнительно.ИспользоватьНачислениеЗарплатыУТ", Ложь);
	
	ГруппаИ1 = Финансы.НовыйОтбор(ГруппаИли,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ1.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	Финансы.НовыйОтбор(ГруппаИ1, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыНаЛицевыеСчета);
	Финансы.НовыйОтбор(ГруппаИ1, "Дополнительно.ИспользоватьНачислениеЗарплатыУТ", Ложь);
	Элемент.Свойства.Вставить("Видимость");
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("НомерВедомостиНаВыплатуЗарплаты");
	Элемент.Поля.Добавить("ДатаВедомостиНаВыплатуЗарплаты");
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ИспользоватьСинхронизациюДанных", Ложь);
	Элемент.Свойства.Вставить("АвтоОтметкаНезаполненного", Ложь);
	Элемент.Свойства.Вставить("ОтметкаНезаполненного", Ложь);
	
	// ПеречислениеВБюджет
	ОтборОперацийПеречисленияВБюджет = Новый ОтборКомпоновкиДанных();
	ГруппаИли = Финансы.НовыйОтбор(ОтборОперацийПеречисленияВБюджет,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ГруппаИлиИ = Финансы.НовыйОтбор(ГруппаИли, , , Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИлиИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	Финансы.НовыйОтбор(ГруппаИлиИ, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ОплатаПоставщику);
	Финансы.НовыйОтбор(ГруппаИлиИ, "ОперацияССамозанятым", Ложь);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПеречислениеТаможне);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ОплатаПоКредитам);
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ПеречислениеВБюджет");
	Элемент.Поля.Добавить("Декорация1");
	Финансы.СкопироватьОтбор(ОтборОперацийПеречисленияВБюджет, Элемент.Условие);
	Элемент.Свойства.Вставить("Видимость");
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("НадписьРеквизитыПлатежаВБюджет");
	Финансы.СкопироватьОтбор(ОтборОперацийПеречисленияВБюджет, Элемент.Условие);
	Финансы.НовыйОтбор(Элемент.Условие, "ПеречислениеВБюджет", Истина);
	Элемент.Свойства.Вставить("Видимость");
	
	// УИП
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ИдентификаторПлатежа");
	Элемент.Свойства.Вставить("Заголовок", НСтр("ru = 'УИП'"));
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ИдентификаторПлатежа");
	Финансы.СкопироватьОтбор(ОтборОперацийПеречисленияВБюджет, Элемент.Условие);
	Финансы.НовыйОтбор(Элемент.Условие, "ПеречислениеВБюджет", Истина);
	Элемент.Свойства.Вставить("Заголовок", НСтр("ru = 'УИН'"));
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ИдентификаторПлатежа");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.КонвертацияВалюты);
	Элемент.Свойства.Вставить("ТолькоПросмотр");
	
	// Код вида дохода
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("КодВидаДохода");
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ДействуетУказание2019_5286У", Истина);
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыПоЗарплатномуПроекту);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыНаЛицевыеСчета);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ОплатаПоставщику);
	Финансы.СкопироватьОтбор(ОтборВыплатаСамозанятому, ГруппаИли);
	Элемент.Свойства.Вставить("Видимость");
	
	// Код выплат
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("КодВыплат");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыПоЗарплатномуПроекту);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыНаЛицевыеСчета);
	Элемент.Свойства.Вставить("Видимость");
	
	// Вид зачисления
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ВидЗачисления");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику);
	Финансы.НовыйОтбор(Элемент.Условие, "СписокФизЛиц", Истина);
	Элемент.Свойства.Вставить("Видимость");
	
	// Рублевый платеж
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ИдентификаторПлатежа");
	Элемент.Поля.Добавить("ВидПлатежа");
	Элемент.Поля.Добавить("ОчередностьПлатежа");
	Элемент.Поля.Добавить("КодВидаДохода");
	Элемент.Поля.Добавить("КодВыплат");
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ВалютныйПлатеж", Ложь);
	Элемент.Свойства.Вставить("Видимость");
	
	// СтраницаВалютныйКонтроль
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("СтраницаВалютныйКонтроль");
	Элемент.Поля.Добавить("НадписьФайлыДляПередачиВБанк");
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ВалютныйКонтроль", Истина);
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ИспользоватьВалютныеПлатежи", Истина);
	Элемент.Свойства.Вставить("Видимость");
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("КодВалютнойОперации");
	Элемент.Поля.Добавить("КодВалютнойОперацииНаименованиеПолное");
	Элемент.Поля.Добавить("ИнформацияДляВалютногоКонтроля");
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ВалютныйКонтроль", Истина);
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ИспользоватьВалютныеПлатежи", Истина);
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ЭтоТранзитныйСчет", Ложь);
	Элемент.Свойства.Вставить("Видимость");
	
	// НДФЛПоВедомостям
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("НДФЛПоВедомостям");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет);
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "ТипНалога", Справочники.ВидыНалоговВзносов.НДФЛ);
	Финансы.НовыйОтбор(ГруппаИли, "ТипНалога", Справочники.ВидыНалоговВзносов.НДФЛСПревышения);
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ИспользоватьНачислениеЗарплаты", Истина);
	Элемент.Свойства.Вставить("Видимость");
	
	// КодВалютнойОперацииНаименованиеПолное
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("КодВалютнойОперацииНаименованиеПолное");
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.КодВалютнойОперацииЗаполнен", Истина);
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ВалютныйКонтроль", Истина);
	Элемент.Свойства.Вставить("Видимость");
	#КонецОбласти
	
	
	#Область РасшифровкаПлатежа
	// Ведомость
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("РасшифровкаПлатежа.Ведомость");
	Элемент.Поля.Добавить("РасшифровкаБезРазбиенияВедомость");
	
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ИспользоватьНачислениеЗарплаты", Истина);
	
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ГруппаИ = Финансы.НовыйОтбор(ГруппаИли,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ГруппаИли1 = Финансы.НовыйОтбор(ГруппаИ,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли1.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли1, "ТипНалога", Справочники.ВидыНалоговВзносов.НДФЛ);
	Финансы.НовыйОтбор(ГруппаИли1, "ТипНалога", Справочники.ВидыНалоговВзносов.НДФЛСПревышения);

	Финансы.НовыйОтбор(ГруппаИ, "НДФЛПоВедомостям", Истина);
	Финансы.НовыйОтбор(ГруппаИ, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет);
	
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыПоЗарплатномуПроекту);
	Финансы.НовыйОтбор(ГруппаИли, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыНаЛицевыеСчета);
	
	Элемент.Свойства.Вставить("Видимость");
	#КонецОбласти
	
	#Область Команды
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ФормаВыгрузитьФайлДляОбменаСБанком");
	ГруппаИЛИ = Финансы.НовыйОтбор(Элемент.Условие, , , Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИЛИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	ГруппаИ = Финансы.НовыйОтбор(ГруппаИЛИ, , , Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	Финансы.НовыйОтбор(ГруппаИ, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыПоЗарплатномуПроекту);
	Финансы.НовыйОтбор(ГруппаИ, "Дополнительно.ИспользоватьЗаявкиНаРасходованиеДенежныхСредств", Ложь);
	Финансы.НовыйОтбор(ГруппаИ, "Дополнительно.ИспользоватьНачислениеЗарплаты", Истина);
	
	ГруппаИ_2 = Финансы.НовыйОтбор(ГруппаИЛИ, , , Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ_2.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	Финансы.НовыйОтбор(ГруппаИ_2, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику);
	Финансы.НовыйОтбор(ГруппаИ_2, "СписокФизЛиц", Истина);
	Финансы.НовыйОтбор(ГруппаИ_2, "Дополнительно.ИспользоватьПеречислениеДенежныхСредствСпискуСотрудников", Истина);
	
	ГруппаИ_3 = Финансы.НовыйОтбор(ГруппаИЛИ, , , Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ_3.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	Финансы.СкопироватьОтбор(ОтборСписокКонтрагентов, ГруппаИ_3);
	Элемент.Свойства.Вставить("Видимость");
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ФормаВыгрузитьФайлДляОбменаСБанком");
	ГруппаИЛИ = Финансы.НовыйОтбор(Элемент.Условие, , , Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИЛИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИЛИ, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику);
	Финансы.СкопироватьОтбор(ОтборВыплатаСамозанятому, ГруппаИЛИ);
	Элемент.Свойства.Вставить("Заголовок", НСтр("ru = 'Выгрузить реестр'"));
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ЗаполнитьОстаткамиНевыданныхСуммСотруднику");
	Финансы.НовыйОтбор(Элемент.Условие, "ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВыдачаЗаймаСотруднику);
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.ИспользоватьНачислениеЗарплатыУТ", Истина);
	Элемент.Свойства.Вставить("Видимость");
	
	#КонецОбласти
	
	ДенежныеСредстваСерверЛокализация.ЗаполнитьНастройкиПолейФормы(Настройки);
	
КонецПроцедуры

// Подбирает последний платеж в бюджет по указанному контрагенту с указанной хозоперацией
// и заполняет реквизиты платежа.
//
// Параметры:
//    Объект - ДанныеФормыКоллекция - Данные документа
//    ИсключаяРеквизиты - Строка - Список имен реквизитов, разделенный запятыми, которые необходимо исключить из заполнения.
//
// Возвращаемое значение:
//    Структура, Неопределено - реквизиты платежа (для получения реквизитов, отсутствующих у объекта), либо Неопределено.
//
Функция ПодобратьРеквизитыПлатежаВБюджет(Объект, ИсключаяРеквизиты = "") Экспорт
	
	Если Не ЗначениеЗаполнено(Объект.ХозяйственнаяОперация)
		Или Не ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СписаниеБезналичныхДенежныхСредств.ВидПлатежа,
	|	СписаниеБезналичныхДенежныхСредств.ОчередностьПлатежа,
	|	СписаниеБезналичныхДенежныхСредств.ВидПеречисленияВБюджет,
	|	СписаниеБезналичныхДенежныхСредств.СтатусСоставителя,
	|	СписаниеБезналичныхДенежныхСредств.КодБК,
	|	СписаниеБезналичныхДенежныхСредств.КодОКАТО,
	|	СписаниеБезналичныхДенежныхСредств.ПоказательОснования,
	|	СписаниеБезналичныхДенежныхСредств.ПоказательТипа,
	|	ВЫБОР КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеТаможне) ТОГДА
	|		СписаниеБезналичныхДенежныхСредств.ПоказательПериода
	|	КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств) ТОГДА
	|		0
	|	ИНАЧЕ """"
	|	КОНЕЦ КАК ПоказательПериода,
	|	ВЫБОР КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств) ТОГДА
	|		0
	|	ИНАЧЕ """"
	|	КОНЕЦ КАК ПоказательНомера,
	|	ВЫБОР КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств) ТОГДА
	|		0
	|	ИНАЧЕ """"
	|	КОНЕЦ КАК ПоказательДаты,
	|	СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента,
	|	СписаниеРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств,
	|	ВЫБОР КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеВБюджет) ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ СписаниеРасшифровкаПлатежа.СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов,
	|	СписаниеБезналичныхДенежныхСредств.ТипНалога КАК ТипНалога,
	|	СписаниеРасшифровкаПлатежа.АналитикаРасходов КАК АналитикаРасходов,
	|	СписаниеРасшифровкаПлатежа.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	СписаниеРасшифровкаПлатежа.Подразделение
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК СписаниеРасшифровкаПлатежа
	|	ПО
	|		СписаниеРасшифровкаПлатежа.Ссылка = СписаниеБезналичныхДенежныхСредств.Ссылка
	|	
	|ГДЕ
	|	СписаниеБезналичныхДенежныхСредств.Проведен
	|	И СписаниеБезналичныхДенежныхСредств.ПеречислениеВБюджет
	|	И СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация = &ХозяйственнаяОперация
	|	И СписаниеБезналичныхДенежныхСредств.Контрагент = &Контрагент
	|	И СписаниеБезналичныхДенежныхСредств.Организация = &Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	СписаниеБезналичныхДенежныхСредств.Дата УБЫВ";
	
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", Объект.ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		
		Объект.ПеречислениеВБюджет = Истина;
		
		Если ЗначениеЗаполнено(ИсключаяРеквизиты) Тогда
			ИсключаяРеквизиты = ИсключаяРеквизиты + ", СтатьяРасходов, АналитикаРасходов, Подразделение";
		Иначе
			ИсключаяРеквизиты = "СтатьяРасходов, АналитикаРасходов, Подразделение";
		КонецЕсли;
		
		РеквизитыПлатежа = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Результат.Выгрузить()[0]);
		ЗаполнитьЗначенияСвойств(Объект, РеквизитыПлатежа,, ИсключаяРеквизиты);

		
		Если Не Объект.РасшифровкаПлатежа.Количество() Тогда
			Объект.РасшифровкаПлатежа.Добавить();
		КонецЕсли;
		
		Если Объект.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет Тогда
			ЗаполнитьЗначенияСвойств(Объект.РасшифровкаПлатежа[0], РеквизитыПлатежа,
				"СтатьяРасходов, АналитикаРасходов, АналитикаАктивовПассивов, Подразделение");
		КонецЕсли;
		
		Если Не СтрНайти(ИсключаяРеквизиты, "СтатьяДвиженияДенежныхСредств") Тогда
			ЗаполнитьЗначенияСвойств(Объект.РасшифровкаПлатежа[0], РеквизитыПлатежа, "СтатьяДвиженияДенежныхСредств");
		КонецЕсли;
		
		
		Возврат РеквизитыПлатежа;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ДействуетФедеральныйЗакон345ФЗ(Период) Экспорт
	
	// Федеральный закон от 2 декабря 2013 г. N 345-ФЗ "О внесении изменения в статью 855 части второй Гражданского кодекса
	// Российской Федерации". Уточнена очередность списания денежных средств со счетов.
	
	ДатаВступленияВСилу = '20131214';
	
	Возврат Период >= ДатаВступленияВСилу Или (Период = '00010101' И ТекущаяДатаСеанса() >= ДатаВступленияВСилу);
	
КонецФункции

// Функция получает реквизиты документа для обмена с конфигурацией "Бухгалтерия предприятия".
//
// Параметры:
//	ДокументСсылка - ДокументСсылка - Документ, для которого необходимо получить реквизиты.
//
// Возвращаемое значение:
//	Структура - Структура реквизитов документа.
//
Функция РеквизитыДокументаДляОбменаСБухгалтерией(ДокументСсылка) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДанныеДокумента.СтатьяДвиженияДенежныхСредств.КорреспондирующийСчет КАК КорреспондирующийСчет,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.СтатьяДвиженияДенежныхСредств.КорреспондирующийСчет <> """"
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДанныеДокумента.СтатьяРасходов.КорреспондирующийСчет КАК КорреспондирующийСчет,
	|	ДанныеДокумента.СтатьяРасходов КАК СтатьяРасходов
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.СтатьяРасходов.КорреспондирующийСчет <> """"
	|	И ДанныеДокумента.Ссылка.ХозяйственнаяОперация =
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеРасходы)
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеДокумента.НомерСтроки
	|");
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Выборка = МассивРезультатов[0].Выбрать();
	Если Выборка.Следующий() Тогда
		КорреспондирующийСчет = Выборка.КорреспондирующийСчет;
		ХозяйственнаяОперация = Выборка.ХозяйственнаяОперация;
	Иначе
		КорреспондирующийСчет = "";
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПустаяСсылка();
	КонецЕсли;
	
	Выборка = МассивРезультатов[1].Выбрать();
	Если Выборка.Следующий() Тогда
		КорреспондирующийСчет = Выборка.КорреспондирующийСчет;
		СтатьяРасходов = Выборка.СтатьяРасходов;
	Иначе
		СтатьяРасходов = Неопределено;
	КонецЕсли;
	
	ВидОперации = "ПрочееСписание";
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеРасходы Тогда
		
		Если СтрНайти(КорреспондирующийСчет, "70") <> 0 Тогда
			ВидОперации = "ПеречислениеЗП";	
		КонецЕсли;
	
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств Тогда
		
		Если СтрНайти(КорреспондирующийСчет, "66") <> 0
		 ИЛИ СтрНайти(КорреспондирующийСчет, "67") <> 0 Тогда
			ВидОперации = "РасчетыПоКредитамИЗаймам";
			
		ИначеЕсли СтрНайти(КорреспондирующийСчет, "76") <> 0
		 ИЛИ СтрНайти(КорреспондирующийСчет, "60") <> 0
		 ИЛИ СтрНайти(КорреспондирующийСчет, "62") <> 0 Тогда
			ВидОперации = "ПрочиеРасчетыСКонтрагентами";
			
		ИначеЕсли СтрНайти(КорреспондирующийСчет, "70") <> 0 Тогда
			ВидОперации = "ПеречислениеЗП";	
			
		КонецЕсли;
		
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура("ВидОперации, Счет, СтатьяРасходов",
		ВидОперации,
		КорреспондирующийСчет,
		СтатьяРасходов);
	
	Возврат СтруктураРеквизитов;
		
КонецФункции

// Процедура заполняет массивы реквизитов, зависимых от хозяйственной операции документа.
//
// Параметры:
//	Объект - ДокументОбъект.СписаниеБезналичныхДенежныхСредств - данные объекта.
//	ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - Выбранная хозяйственная операция.
//	МассивВсехРеквизитов - Массив из Строка - Массив всех имен реквизитов, зависимых от хозяйственной операции.
//	МассивРеквизитовОперации - Массив из Строка - Массив имен реквизитов, используемых в выбранной хозяйственной операции.
//
Процедура ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(Объект, МассивВсехРеквизитов, МассивРеквизитовОперации)
	
	ХозяйственнаяОперация = Объект.ХозяйственнаяОперация;
	ПеречислениеВБюджет = Объект.ПеречислениеВБюджет;
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("ВидПеречисленияВБюджет");
	МассивВсехРеквизитов.Добавить("ДатаВедомостиНаВыплатуЗарплаты");
	МассивВсехРеквизитов.Добавить("ИдентификаторПлатежа");
	МассивВсехРеквизитов.Добавить("КодБК");
	МассивВсехРеквизитов.Добавить("КодОКАТО");
	МассивВсехРеквизитов.Добавить("НомерВедомостиНаВыплатуЗарплаты");
	МассивВсехРеквизитов.Добавить("ПеречислениеВБюджет");
	МассивВсехРеквизитов.Добавить("ПлатежиПо275ФЗ");
	МассивВсехРеквизитов.Добавить("ПоказательДаты");
	МассивВсехРеквизитов.Добавить("ПоказательНомера");
	МассивВсехРеквизитов.Добавить("ПоказательОснования");
	МассивВсехРеквизитов.Добавить("ПоказательПериода");
	МассивВсехРеквизитов.Добавить("ПоказательТипа");
	МассивВсехРеквизитов.Добавить("РегистрацияВНалоговомОргане");
	МассивВсехРеквизитов.Добавить("СтатусСоставителя");
	МассивВсехРеквизитов.Добавить("ТипНалога");
	МассивВсехРеквизитов.Добавить("ИННПлательщика");
	МассивВсехРеквизитов.Добавить("КПППлательщика");
	МассивВсехРеквизитов.Добавить("ТекстПлательщика");
	МассивВсехРеквизитов.Добавить("НомерДоговораСБанком");
	МассивВсехРеквизитов.Добавить("ДатаДоговораСБанком");
	МассивВсехРеквизитов.Добавить("ОтделениеБанка");
	МассивВсехРеквизитов.Добавить("ФилиалОтделенияБанка");
	МассивВсехРеквизитов.Добавить("СтатьяЦелевыхСредств");
	
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.Ведомость");
	МассивВсехРеквизитов.Добавить("РасшифровкаПлатежа.СтатьяЦелевыхСредств");
	
	МассивВсехРеквизитов.Добавить("БанковскиеСчетаСпискаКонтрагентов");
	МассивВсехРеквизитов.Добавить("БанковскиеСчетаСпискаКонтрагентов.Контрагент");
	МассивВсехРеквизитов.Добавить("БанковскиеСчетаСпискаКонтрагентов.БанковскийСчетКонтрагента");
	МассивВсехРеквизитов.Добавить("БанковскиеСчетаСпискаКонтрагентов.Сумма");
	
	МассивРеквизитовОперации = Новый Массив;
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоставщику Тогда
		МассивРеквизитовОперации.Добавить("ИдентификаторПлатежа");
		МассивРеквизитовОперации.Добавить("ПлатежиПо275ФЗ");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяЦелевыхСредств");
		МассивРеквизитовОперации.Добавить("БанковскиеСчетаСпискаКонтрагентов");
		МассивРеквизитовОперации.Добавить("БанковскиеСчетаСпискаКонтрагентов.Контрагент");
		МассивРеквизитовОперации.Добавить("БанковскиеСчетаСпискаКонтрагентов.БанковскийСчетКонтрагента");
		МассивРеквизитовОперации.Добавить("БанковскиеСчетаСпискаКонтрагентов.Сумма");
	
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту Тогда
		МассивРеквизитовОперации.Добавить("ИдентификаторПлатежа");
		МассивРеквизитовОперации.Добавить("ПлатежиПо275ФЗ");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяЦелевыхСредств");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику Тогда
		МассивРеквизитовОперации.Добавить("ИдентификаторПлатежа");
		МассивРеквизитовОперации.Добавить("ПлатежиПо275ФЗ");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяЦелевыхСредств");
		
		МассивРеквизитовОперации.Добавить("НомерДоговораСБанком");
		МассивРеквизитовОперации.Добавить("ДатаДоговораСБанком");
		МассивРеквизитовОперации.Добавить("ОтделениеБанка");
		МассивРеквизитовОперации.Добавить("ФилиалОтделенияБанка");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет Тогда
		МассивРеквизитовОперации.Добавить("ИдентификаторПлатежа");
		МассивРеквизитовОперации.Добавить("ПлатежиПо275ФЗ");
		МассивРеквизитовОперации.Добавить("СтатьяЦелевыхСредств");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию Тогда
		МассивРеквизитовОперации.Добавить("ИдентификаторПлатежа");
		МассивРеквизитовОперации.Добавить("ПлатежиПо275ФЗ");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяЦелевыхСредств");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств
		ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеТаможне Тогда
		
		МассивРеквизитовОперации.Добавить("ИдентификаторПлатежа");
		МассивРеквизитовОперации.Добавить("ПлатежиПо275ФЗ");
		МассивРеквизитовОперации.Добавить("ИННПлательщика");
		МассивРеквизитовОперации.Добавить("КПППлательщика");
		МассивРеквизитовОперации.Добавить("ТекстПлательщика");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяЦелевыхСредств");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет Тогда
		МассивРеквизитовОперации.Добавить("ИдентификаторПлатежа");
		МассивРеквизитовОперации.Добавить("ПлатежиПо275ФЗ");
		МассивРеквизитовОперации.Добавить("РегистрацияВНалоговомОргане");
		МассивРеквизитовОперации.Добавить("ИННПлательщика");
		МассивРеквизитовОперации.Добавить("КПППлательщика");
		МассивРеквизитовОперации.Добавить("ТекстПлательщика");
		МассивРеквизитовОперации.Добавить("ТипНалога");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяЦелевыхСредств");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию Тогда
		МассивРеквизитовОперации.Добавить("ИдентификаторПлатежа");
		МассивРеквизитовОперации.Добавить("ПлатежиПо275ФЗ");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяЦелевыхСредств");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств Тогда
		МассивРеквизитовОперации.Добавить("ИдентификаторПлатежа");
		МассивРеквизитовОперации.Добавить("СтатьяЦелевыхСредств");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеНаДепозиты Тогда
		МассивРеквизитовОперации.Добавить("ИдентификаторПлатежа");
		МассивРеквизитовОперации.Добавить("ПлатежиПо275ФЗ");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяЦелевыхСредств");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаЗаймов Тогда
		МассивРеквизитовОперации.Добавить("ИдентификаторПлатежа");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяЦелевыхСредств");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоКредитам Тогда
		МассивРеквизитовОперации.Добавить("ИдентификаторПлатежа");
		МассивРеквизитовОперации.Добавить("ПлатежиПо275ФЗ");
		МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяЦелевыхСредств");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаЗаймаСотруднику Тогда
		МассивРеквизитовОперации.Добавить("ИдентификаторПлатежа");
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплатыУТ") Тогда
			МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяЦелевыхСредств");
		Иначе
			МассивРеквизитовОперации.Добавить("СтатьяЦелевыхСредств");
		КонецЕсли;
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыПоЗарплатномуПроекту Тогда
		МассивРеквизитовОперации.Добавить("ИдентификаторПлатежа");
		МассивРеквизитовОперации.Добавить("ПлатежиПо275ФЗ");
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплатыУТ") Тогда
			МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.Ведомость");
			МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяЦелевыхСредств");
		Иначе
			МассивРеквизитовОперации.Добавить("ДатаВедомостиНаВыплатуЗарплаты");
			МассивРеквизитовОперации.Добавить("НомерВедомостиНаВыплатуЗарплаты");
			МассивРеквизитовОперации.Добавить("СтатьяЦелевыхСредств");
		КонецЕсли;
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыНаЛицевыеСчета Тогда
		МассивРеквизитовОперации.Добавить("ИдентификаторПлатежа");
		МассивРеквизитовОперации.Добавить("ПлатежиПо275ФЗ");
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплатыУТ") Тогда
			МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.Ведомость");
			МассивРеквизитовОперации.Добавить("РасшифровкаПлатежа.СтатьяЦелевыхСредств");
		Иначе
			МассивРеквизитовОперации.Добавить("ДатаВедомостиНаВыплатуЗарплаты");
			МассивРеквизитовОперации.Добавить("НомерВедомостиНаВыплатуЗарплаты");
			МассивРеквизитовОперации.Добавить("СтатьяЦелевыхСредств");
		КонецЕсли;
		
	
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты Тогда
		МассивРеквизитовОперации.Добавить("ИдентификаторПлатежа");
	КонецЕсли;
	
	ДопускаетсяПеречислениеВБюджет =
		(ДенежныеСредстваСерверЛокализация.МассивОперацийПеречисленияВБюджет().Найти(ХозяйственнаяОперация) <> Неопределено);
	
	Если ДопускаетсяПеречислениеВБюджет Тогда
		МассивРеквизитовОперации.Добавить("ПеречислениеВБюджет");
	КонецЕсли;
	
	Если ДопускаетсяПеречислениеВБюджет И (ПеречислениеВБюджет = Неопределено Или ПеречислениеВБюджет) Тогда
		МассивРеквизитовОперации.Добавить("ВидПеречисленияВБюджет");
		МассивРеквизитовОперации.Добавить("КодБК");
		МассивРеквизитовОперации.Добавить("КодОКАТО");
		МассивРеквизитовОперации.Добавить("ПоказательДаты");
		МассивРеквизитовОперации.Добавить("ПоказательНомера");
		МассивРеквизитовОперации.Добавить("ПоказательОснования");
		МассивРеквизитовОперации.Добавить("ПоказательПериода");
		МассивРеквизитовОперации.Добавить("ПоказательТипа");
		МассивРеквизитовОперации.Добавить("СтатусСоставителя");
	КонецЕсли;
	
КонецПроцедуры


Процедура ПроверитьИзменениеТипаПлатежногоДокумента(Объект)
	
	Если Не Объект.ЭтоНовый() Тогда
		
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ДанныеДокумента.ТипПлатежногоДокумента КАК ТипПлатежногоДокумента
		|ИЗ
		|	Документ.СписаниеБезналичныхДенежныхСредств КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|");
		Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если Выборка.ТипПлатежногоДокумента <> Объект.ТипПлатежногоДокумента Тогда
				Объект.Номер = "";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьПроверкуВозвратовНеперечисленныхДенежныхСредств(Объект, Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	""1"" КАК Результат
	|ИЗ
	|	Документ.ПоступлениеБезналичныхДенежныхСредств КАК Поступления
	|ГДЕ
	|	Поступления.Проведен
	|	И Поступления.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНеперечисленныхДС)
	|	И Поступления.ДокументВыдачи = &Списание";
	
	Запрос.УстановитьПараметр("Списание", Объект.Ссылка);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		ТекстСообщения = НСтр("ru = 'По документу уже введено поступление денежных средств с операцией ""Возврат неперечисленной зарплаты""'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект,,, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьОплатуНДСПоСчетамФактурам(Объект, Проведен)
	
	СчетаФактуры = УчетНДСРФ.НоваяТаблицаСчетовФактур();
	Если СчетаФактуры.Количество() = 0 Тогда
		СтрокаСчетаФактуры = СчетаФактуры.Добавить();
		СтрокаСчетаФактуры.СчетФактура = Объект.ДокументОснование;
		СтрокаСчетаФактуры.СуммаОплаты = Объект.СуммаДокумента;
	КонецЕсли;
	
	РеквизитыДокументаОплаты = Новый Структура();
	РеквизитыДокументаОплаты.Вставить("ДатаПеречисленияВБюджет", Объект.ДатаПроведенияБанком);
	РеквизитыДокументаОплаты.Вставить("НомерДокумента",          Объект.НомерВходящегоДокумента);
	РеквизитыДокументаОплаты.Вставить("ДатаДокумента",           Объект.ДатаВходящегоДокумента);
	РеквизитыДокументаОплаты.Вставить("ТипНалога",               Объект.ТипНалога);
	
	УчетНДСРФ.ЗарегистрироватьОплатуНДСПоСчетамФактурам(СчетаФактуры, Объект.Ссылка, РеквизитыДокументаОплаты, Проведен);
	
КонецПроцедуры


// Запускает процедуру сбора данных по документам, указанным в параметре, и процедуру создания файла реестра для
// зачисления банком денежных средств на каждый документ.
// 
// Параметры:
//  СтруктураПараметров - Структура:
//    * МассивДокументов - Массив из ДокументСсылка.СписаниеБезналичныхДенежныхСредств - список документов, для которых выполняется выгрузка файла.
//    * УникальныйИдентификаторФормы - Строка - идентификатор формы документа, из которого выполняется формирование файла
//    * МассивОписанийФайлов - Массив из Неопределено - пустой массив для заполнения структурой будущего файла
//
Процедура ВыгрузитьФайлыОбменаСБанком(СтруктураПараметров) Экспорт

	Если СтруктураПараметров.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику Тогда
	
		ДанныеДокументов = ДанныеВыгрузкиРеестраЗачисленияДенежныхСредств(СтруктураПараметров.МассивДокументов);
		СтруктураПараметров.Вставить("ВидОперации", "Зачисление");
		
	Иначе
		
		ДанныеДокументов = РеестрСамозанятыхПоДокументам(СтруктураПараметров.МассивДокументов);
		СтруктураПараметров.Вставить("ВидОперации", "ОплатаСамозанятому");
		
	КонецЕсли;
		
	Для Каждого СтрокаДанных Из ДанныеДокументов Цикл
		ОбменСБанкамиУТ.СоздатьФайлРеестраОбменаСБанком(СтрокаДанных.Ключ,
														СтрокаДанных.Значение,
														СтруктураПараметров);
	КонецЦикла;

КонецПроцедуры

// Возвращает собранные данные для списка переданных документов.
// 
// Параметры:
//  КоллекцияДокументов - Массив из ДокументСсылка.СписаниеБезналичныхДенежныхСредств - документы, по которым необходимо
// получить данные для выгрузки.
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение - содержит структуру данных отдельно по документам:
//  * Ключ - ДокументСсылка.СписаниеБезналичныхДенежныхСредств - документ.
//  * Значение - см. ДанныеВыгрузкиОсновныеРеквизиты.
//
Функция ДанныеВыгрузкиРеестраЗачисленияДенежныхСредств(КоллекцияДокументов) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	&ДатаФормирования КАК ДатаФормирования,
		|	СписаниеБезналичныхДенежныхСредств.Ссылка КАК Документ,
		|	СписаниеБезналичныхДенежныхСредств.Номер КАК Номер,
		|	СписаниеБезналичныхДенежныхСредств.Дата КАК Дата,
		|	СписаниеБезналичныхДенежныхСредств.НомерВходящегоДокумента КАК НомерРеестра,
		|	СписаниеБезналичныхДенежныхСредств.ДатаВходящегоДокумента КАК ДатаРеестра,
		|	СписаниеБезналичныхДенежныхСредств.Организация КАК Организация,
		|	СписаниеБезналичныхДенежныхСредствЛицевыеСчетаСотрудников.НомерСтроки КАК НомерСтроки,
		|	СписаниеБезналичныхДенежныхСредствЛицевыеСчетаСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СписаниеБезналичныхДенежныхСредствЛицевыеСчетаСотрудников.ЛицевойСчет КАК ЛицевойСчет,
		|	СписаниеБезналичныхДенежныхСредствЛицевыеСчетаСотрудников.Сумма КАК Сумма,
		|	СписаниеБезналичныхДенежныхСредств.БанковскийСчет КАК БанковскийСчет,
		|	СписаниеБезналичныхДенежныхСредств.НомерДоговораСБанком КАК НомерДоговораСБанком,
		|	СписаниеБезналичныхДенежныхСредств.ДатаДоговораСБанком КАК ДатаДоговораСБанком,
		|	СписаниеБезналичныхДенежныхСредств.Валюта КАК Валюта,
		|	СписаниеБезналичныхДенежныхСредств.ОтделениеБанка КАК ОтделениеБанка,
		|	СписаниеБезналичныхДенежныхСредств.ФилиалОтделенияБанка КАК ФилиалОтделенияБанка,
		|	СписаниеБезналичныхДенежныхСредств.КодировкаФайла КАК КодировкаФайла,
		|	СписаниеБезналичныхДенежныхСредств.КодВидаДохода КАК КодВидаДохода,
		|	СписаниеБезналичныхДенежныхСредств.ВидЗачисления КАК ВидЗачисления
		|ПОМЕСТИТЬ ТаблицаОсновныхДанных
		|ИЗ
		|	Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств.ЛицевыеСчетаСотрудников КАК СписаниеБезналичныхДенежныхСредствЛицевыеСчетаСотрудников
		|		ПО СписаниеБезналичныхДенежныхСредств.Ссылка = СписаниеБезналичныхДенежныхСредствЛицевыеСчетаСотрудников.Ссылка
		|ГДЕ
		|	СписаниеБезналичныхДенежныхСредств.Ссылка В(&Документы)
		|	И СписаниеБезналичныхДенежныхСредств.Проведен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Документ КАК Документ,
		|	ВложенныйЗапрос.ФизическоеЛицо КАК ФизическоеЛицо,
		|	МАКСИМУМ(ФИОФизическихЛиц.Период) КАК Период
		|ПОМЕСТИТЬ ПоследнееИзменениеФИОФизЛицПоДокументам
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТаблицаОсновныхДанных.Документ КАК Документ,
		|		ТаблицаОсновныхДанных.Дата КАК Дата,
		|		ТаблицаОсновныхДанных.ФизическоеЛицо КАК ФизическоеЛицо
		|	ИЗ
		|		ТаблицаОсновныхДанных КАК ТаблицаОсновныхДанных) КАК ВложенныйЗапрос
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизическихЛиц КАК ФИОФизическихЛиц
		|		ПО ВложенныйЗапрос.ФизическоеЛицо = ФИОФизическихЛиц.ФизическоеЛицо
		|			И ВложенныйЗапрос.Дата >= ФИОФизическихЛиц.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Документ,
		|	ВложенныйЗапрос.ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоследнееИзменениеФИОФизЛицПоДокументам.Документ КАК Документ,
		|	ПоследнееИзменениеФИОФизЛицПоДокументам.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ФИОФизическихЛиц.Фамилия КАК Фамилия,
		|	ФИОФизическихЛиц.Имя КАК Имя,
		|	ФИОФизическихЛиц.Отчество КАК Отчество
		|ПОМЕСТИТЬ ФИОФизЛицРаздельно
		|ИЗ
		|	ПоследнееИзменениеФИОФизЛицПоДокументам КАК ПоследнееИзменениеФИОФизЛицПоДокументам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизическихЛиц КАК ФИОФизическихЛиц
		|		ПО ПоследнееИзменениеФИОФизЛицПоДокументам.ФизическоеЛицо = ФИОФизическихЛиц.ФизическоеЛицо
		|			И ПоследнееИзменениеФИОФизЛицПоДокументам.Период = ФИОФизическихЛиц.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОсновныхДанных.Документ КАК Документ,
		|	ТаблицаОсновныхДанных.Организация КАК Организация,
		|	ТаблицаОсновныхДанных.Номер КАК НомерДокумента,
		|	ТаблицаОсновныхДанных.Дата КАК ДатаДокумента,
		|	ТаблицаОсновныхДанных.НомерРеестра КАК НомерРеестра,
		|	ТаблицаОсновныхДанных.ДатаФормирования КАК ДатаФормирования,
		|	ТаблицаОсновныхДанных.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ТаблицаОсновныхДанных.Сумма КАК Сумма,
		|	ЕСТЬNULL(ФИОФизЛицРаздельно.Фамилия, """") КАК Фамилия,
		|	ЕСТЬNULL(ФИОФизЛицРаздельно.Имя, """") КАК Имя,
		|	ЕСТЬNULL(ФИОФизЛицРаздельно.Отчество, """") КАК Отчество,
		|	ВЫБОР
		|		КОГДА Организации.НаименованиеПолное ПОДОБНО """"
		|			ТОГДА Организации.Наименование
		|		ИНАЧЕ Организации.НаименованиеПолное
		|	КОНЕЦ КАК НаименованиеОрганизации,
		|	Организации.ИНН КАК ИНН,
		|	БанковскиеСчетаОрганизаций.НомерСчета КАК НомерРасчетногоСчетаОрганизации,
		|	ВЫБОР
		|		КОГДА БанковскиеСчетаОрганизаций.БИКБанка = """"
		|			ТОГДА БанковскиеСчетаОрганизаций.Банк.Код
		|		ИНАЧЕ БанковскиеСчетаОрганизаций.БИКБанка
		|	КОНЕЦ КАК БИК,
		|	БанковскиеСчетаКонтрагентов.НомерСчета КАК НомерЛицевогоСчета,
		|	БанковскиеСчетаОрганизаций.ИспользоватьОбменСБанком КАК ИспользоватьОбменСБанком,
		|	БанковскиеСчетаОрганизаций.ФорматОбмена КАК ФорматОбмена,
		|	1 КАК КоличествоЗаписей,
		|	ТаблицаОсновныхДанных.НомерДоговораСБанком КАК НомерДоговораСБанком,
		|	ТаблицаОсновныхДанных.ДатаДоговораСБанком КАК ДатаДоговораСБанком,
		|	ТаблицаОсновныхДанных.Валюта КАК Валюта,
		|	ТаблицаОсновныхДанных.НомерСтроки КАК НомерСтроки,
		|	ТаблицаОсновныхДанных.ДатаРеестра КАК ДатаРеестра,
		|	ТаблицаОсновныхДанных.ОтделениеБанка КАК ОтделениеБанка,
		|	ТаблицаОсновныхДанных.ФилиалОтделенияБанка КАК ФилиалОтделенияБанка,
		|	ТаблицаОсновныхДанных.КодировкаФайла КАК КодировкаФайла,
		|	ТаблицаОсновныхДанных.КодВидаДохода КАК КодВидаДохода,
		|	ТаблицаОсновныхДанных.ВидЗачисления КАК ВидЗачисления
		|ПОМЕСТИТЬ ИтоговаяТаблица
		|ИЗ
		|	ТаблицаОсновныхДанных КАК ТаблицаОсновныхДанных
		|		ЛЕВОЕ СОЕДИНЕНИЕ ФИОФизЛицРаздельно КАК ФИОФизЛицРаздельно
		|		ПО ТаблицаОсновныхДанных.Документ = ФИОФизЛицРаздельно.Документ
		|			И ТаблицаОсновныхДанных.ФизическоеЛицо = ФИОФизЛицРаздельно.ФизическоеЛицо
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчетаОрганизаций
		|		ПО ТаблицаОсновныхДанных.БанковскийСчет = БанковскиеСчетаОрганизаций.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО ТаблицаОсновныхДанных.Организация = Организации.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчетаКонтрагентов
		|		ПО ТаблицаОсновныхДанных.ЛицевойСчет = БанковскиеСчетаКонтрагентов.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИтоговаяТаблица.Документ КАК Документ,
		|	ИтоговаяТаблица.Организация КАК Организация,
		|	ИтоговаяТаблица.НомерДокумента КАК НомерДокумента,
		|	ИтоговаяТаблица.ДатаДокумента КАК ДатаДокумента,
		|	ИтоговаяТаблица.НомерРеестра КАК НомерРеестра,
		|	ИтоговаяТаблица.ДатаФормирования КАК ДатаФормирования,
		|	СУММА(ИтоговаяТаблица.Сумма) КАК СуммаИтого,
		|	ИтоговаяТаблица.НаименованиеОрганизации КАК НаименованиеОрганизации,
		|	ИтоговаяТаблица.ИНН КАК ИНН,
		|	ИтоговаяТаблица.НомерРасчетногоСчетаОрганизации КАК РасчетныйСчетОрганизации,
		|	ИтоговаяТаблица.БИК КАК БИК,
		|	ИтоговаяТаблица.ИспользоватьОбменСБанком КАК ИспользоватьЭлектронныйДокументооборотСБанком,
		|	ИтоговаяТаблица.ФорматОбмена КАК ФорматОбмена,
		|	КОЛИЧЕСТВО(ИтоговаяТаблица.КоличествоЗаписей) КАК КоличествоЗаписей,
		|	ИтоговаяТаблица.НомерДоговораСБанком КАК НомерДоговора,
		|	ИтоговаяТаблица.ДатаДоговораСБанком КАК ДатаДоговора,
		|	ИтоговаяТаблица.ДатаРеестра КАК ДатаРеестра,
		|	ИтоговаяТаблица.ОтделениеБанка КАК ОтделениеБанка,
		|	ИтоговаяТаблица.КодировкаФайла КАК КодировкаФайла,
		|	ИтоговаяТаблица.КодВидаДохода КАК КодВидаДохода,
		|	ИтоговаяТаблица.ВидЗачисления КАК ВидЗачисления
		|ИЗ
		|	ИтоговаяТаблица КАК ИтоговаяТаблица
		|
		|СГРУППИРОВАТЬ ПО
		|	ИтоговаяТаблица.Документ,
		|	ИтоговаяТаблица.Организация,
		|	ИтоговаяТаблица.НомерДокумента,
		|	ИтоговаяТаблица.ДатаДокумента,
		|	ИтоговаяТаблица.НомерРеестра,
		|	ИтоговаяТаблица.ДатаФормирования,
		|	ИтоговаяТаблица.НаименованиеОрганизации,
		|	ИтоговаяТаблица.ИНН,
		|	ИтоговаяТаблица.НомерРасчетногоСчетаОрганизации,
		|	ИтоговаяТаблица.БИК,
		|	ИтоговаяТаблица.ИспользоватьОбменСБанком,
		|	ИтоговаяТаблица.ФорматОбмена,
		|	ИтоговаяТаблица.НомерДоговораСБанком,
		|	ИтоговаяТаблица.ДатаДоговораСБанком,
		|	ИтоговаяТаблица.ДатаРеестра,
		|	ИтоговаяТаблица.ОтделениеБанка,
		|	ИтоговаяТаблица.КодировкаФайла,
		|	ИтоговаяТаблица.КодВидаДохода,
		|	ИтоговаяТаблица.ВидЗачисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИтоговаяТаблица.Документ КАК Документ,
		|	ИтоговаяТаблица.НомерСтроки КАК Нпп,
		|	ИтоговаяТаблица.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ИтоговаяТаблица.Фамилия КАК Фамилия,
		|	ИтоговаяТаблица.Имя КАК Имя,
		|	ИтоговаяТаблица.Отчество КАК Отчество,
		|	ИтоговаяТаблица.Сумма КАК Сумма,
		|	ИтоговаяТаблица.НомерЛицевогоСчета КАК ЛицевойСчет,
		|	ИтоговаяТаблица.Валюта.Код КАК КодВалюты,
		|	ИтоговаяТаблица.ОтделениеБанка КАК ОтделениеБанка,
		|	ИтоговаяТаблица.ФилиалОтделенияБанка КАК ФилиалОтделенияБанка
		|
		|ИЗ
		|	ИтоговаяТаблица КАК ИтоговаяТаблица
		|ИТОГИ ПО
		|	Документ";
	
	Запрос.УстановитьПараметр("ДатаФормирования", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("Документы", КоллекцияДокументов);
	
	ДанныеДокументов = Новый Соответствие;
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивЗапросов = Запрос.ВыполнитьПакет();
	
	ТаблицаДокументов = МассивЗапросов[МассивЗапросов.ВГраница() - 1].Выгрузить();
	ВыборкаПоДокументам = МассивЗапросов[МассивЗапросов.ВГраница()].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	СписокДокументов = ТаблицаДокументов.Скопировать(, "Документ, Организация, БИК, КодировкаФайла");
	КодировкаПоДокументам = ЗаполнитьКодировкуРеестраЗачисленияДенежныхСредств(СписокДокументов);
	
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		ДанныеДокумента = ДанныеВыгрузкиОсновныеРеквизиты();
		
		СтрокаПоДокументу = ТаблицаДокументов.Найти(ВыборкаПоДокументам.Документ, "Документ");
		ЗаполнитьЗначенияСвойств(ДанныеДокумента, СтрокаПоДокументу);
		ДанныеДокумента.ИдПервичногоДокумента = СтрокаПоДокументу.Документ.УникальныйИдентификатор();
		ДанныеДокумента.ПлатежноеПоручение = Прав(СтрокаПоДокументу.НомерРеестра, 6);
		ДанныеДокумента.ДатаПлатежногоПоручения = СтрокаПоДокументу.ДатаРеестра;
		
		ДанныеДокумента.ИмяФайла = СтрШаблон("%1%2z.xml",
			СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ДанныеДокумента.ОтделениеБанка, 4),
			Прав(СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ДанныеДокумента.НомерДокумента, 3), 3));
		
		ВыборкаПоФизическимЛицам = ВыборкаПоДокументам.Выбрать();
		
		Пока ВыборкаПоФизическимЛицам.Следующий() Цикл
			
			ДанныеСтроки = ДанныеВыгрузкиСписокФизическихЛиц();
			ЗаполнитьЗначенияСвойств(ДанныеСтроки, ВыборкаПоФизическимЛицам);
			
			Если СтрокаПоДокументу.ИспользоватьЭлектронныйДокументооборотСБанком 
				И СтрДлина(ВыборкаПоФизическимЛицам.ЛицевойСчет) <> 20 Тогда
				
				СообщениеОбОшибке = СтрШаблон(НСтр("ru='В строке №%1 у сотрудника %2 лицевой счет не соответствует формату в 20 цифр.
												     |Возможно банк не поддерживает такой формат.'"), 
										ВыборкаПоФизическимЛицам.Нпп,
										ВыборкаПоФизическимЛицам.ФизическоеЛицо);
				ОбщегоНазначения.СообщитьПользователю(СообщениеОбОшибке, ДанныеДокумента.Документ,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ЛицевыеСчетаСотрудников", ВыборкаПоФизическимЛицам.Нпп, "НомерЛицевогоСчета"));
				
			КонецЕсли;
			
			ДанныеДокумента.Сотрудник.Добавить(ДанныеСтроки);
			
		КонецЦикла;
		
		ДанныеДокумента.КодировкаФайла = КодировкаПоДокументам.Получить(ДанныеДокумента.Документ);
		
		ДанныеДокументов.Вставить(ДанныеДокумента.Документ, ДанныеДокумента);
		
	КонецЦикла;
		
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ДанныеДокументов;

КонецФункции

// Возвращает структуру документа, используемую для формирования файла обмена.
// 
// Возвращаемое значение:
//  Структура - Данные выгрузки основные реквизиты:
// * Документ - ДокументСсылка.СписаниеБезналичныхДенежныхСредств - пустая ссылка на документ
// * НомерДокумента - Строка - 
// * ДатаДокумента - Дата - 
// * НомерРеестра - Строка - 
// * ДатаРеестра - Дата - 
// * НаименованиеОрганизации - Строка - 
// * ИНН - Строка - 
// * Организация - СправочникСсылка.Организации - пустая ссылка на справочник
// * НомерДоговора - Строка - 
// * ДатаДоговора - Дата - 
// * РасчетныйСчетОрганизации - Строка - 
// * ИспользоватьЭлектронныйДокументооборотСБанком - Булево - 
// * БИК - Строка - 
// * ВидЗачисления - Строка - 
// * КодВидаДохода - Строка - 
// * КоличествоЗаписей - Число - 
// * СуммаИтого - Число - 
// * ИдПервичногоДокумента - Строка - 
// * ДатаФормирования - Дата - 
// * ИмяФайла - Строка - 
// * ПлатежноеПоручение - Строка - 
// * ДатаПлатежногоПоручения - Дата - 
// * ОтделениеБанка - Строка - 
// * Сотрудник - Массив из Неопределено - пустой массив.
// * КодировкаФайла - Строка - кодировка файла обмена.
//
Функция ДанныеВыгрузкиОсновныеРеквизиты() Экспорт
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("Документ", Документы.СписаниеБезналичныхДенежныхСредств.ПустаяСсылка());
	ДанныеЗаполнения.Вставить("НомерДокумента", "");
	ДанныеЗаполнения.Вставить("ДатаДокумента", Дата("00010101"));
	ДанныеЗаполнения.Вставить("НомерРеестра", "");
	ДанныеЗаполнения.Вставить("ДатаРеестра", Дата("00010101"));
	ДанныеЗаполнения.Вставить("НаименованиеОрганизации", "");
	ДанныеЗаполнения.Вставить("ИНН", "");
	ДанныеЗаполнения.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	ДанныеЗаполнения.Вставить("НомерДоговора", "");
	ДанныеЗаполнения.Вставить("ДатаДоговора", Дата("00010101"));
	ДанныеЗаполнения.Вставить("РасчетныйСчетОрганизации", "");
	ДанныеЗаполнения.Вставить("ИспользоватьЭлектронныйДокументооборотСБанком", Ложь);
	ДанныеЗаполнения.Вставить("БИК", "");
	ДанныеЗаполнения.Вставить("ВидЗачисления", "07");
	ДанныеЗаполнения.Вставить("КодВидаДохода", "");
	ДанныеЗаполнения.Вставить("КоличествоЗаписей", 0);
	ДанныеЗаполнения.Вставить("СуммаИтого", 0);
	ДанныеЗаполнения.Вставить("ИдПервичногоДокумента", "");
	ДанныеЗаполнения.Вставить("ДатаФормирования", Дата("00010101"));
	ДанныеЗаполнения.Вставить("ИмяФайла", "");
	ДанныеЗаполнения.Вставить("ПлатежноеПоручение", "");
	ДанныеЗаполнения.Вставить("ДатаПлатежногоПоручения", Дата("00010101"));
	ДанныеЗаполнения.Вставить("ОтделениеБанка", "");
	ДанныеЗаполнения.Вставить("Сотрудник", Новый Массив);
	ДанныеЗаполнения.Вставить("КодировкаФайла", "");
	
	Возврат ДанныеЗаполнения;
	
КонецФункции

// Возвращает структуру строки документа, используемую для формирования файла обмена.
//
// Возвращаемое значение:
//   Структура:
//    * ФизическоеЛицо - СправочникСсылка.ФизическиеЛица - пустая ссылка на справочник
//    * Нпп - Число
//    * Фамилия - Строка
//    * Имя - Строка
//    * Отчество - Строка
//    * ЛицевойСчет - Строка
//    * Сумма - Число
//    * КодВалюты - Строка
//    * ОтделениеБанка - Строка
//    * ФилиалОтделенияБанка - Строка
//
Функция ДанныеВыгрузкиСписокФизическихЛиц() Экспорт
	
	ДанныеЗаполнения = Новый Структура();
	ДанныеЗаполнения.Вставить("ФизическоеЛицо", Справочники.ФизическиеЛица.ПустаяСсылка());
	ДанныеЗаполнения.Вставить("Нпп", 0);
	ДанныеЗаполнения.Вставить("Фамилия", "");
	ДанныеЗаполнения.Вставить("Имя", "");
	ДанныеЗаполнения.Вставить("Отчество", "");
	ДанныеЗаполнения.Вставить("ЛицевойСчет", "");
	ДанныеЗаполнения.Вставить("Сумма", 0);
	ДанныеЗаполнения.Вставить("КодВалюты", "");
	ДанныеЗаполнения.Вставить("ОтделениеБанка", "");
	ДанныеЗаполнения.Вставить("ФилиалОтделенияБанка", "");
	
	Возврат ДанныеЗаполнения;
	
КонецФункции

Функция РеестрСамозанятыхПоДокументам(СписокДокументов)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК Ссылка,
		|	БанковскиеСчетаКонтрагентов.НомерСчета КАК НомерСчета,
		|	Контрагенты.НаименованиеПолное КАК Контрагент,
		|	ВЫБОР
		|		КОГДА БанковскиеСчетаКонтрагентов.БанкДляРасчетов = ЗНАЧЕНИЕ(Справочник.КлассификаторБанков.ПустаяСсылка)
		|				И БанковскиеСчетаКонтрагентов.БИКБанкаДляРасчетов = """"
		|			ТОГДА ВЫБОР
		|					КОГДА БанковскиеСчетаКонтрагентов.РучноеИзменениеРеквизитовБанка
		|						ТОГДА БанковскиеСчетаКонтрагентов.БИКБанка
		|					ИНАЧЕ БанковскиеСчетаКонтрагентов.Банк.Код
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА БанковскиеСчетаКонтрагентов.РучноеИзменениеРеквизитовБанкаДляРасчетов
		|					ТОГДА БанковскиеСчетаКонтрагентов.БИКБанкаДляРасчетов
		|				ИНАЧЕ БанковскиеСчетаКонтрагентов.БанкДляРасчетов.Код
		|			КОНЕЦ
		|	КОНЕЦ КАК БИКБанка,
		|	ВЫБОР
		|		КОГДА БанковскиеСчетаКонтрагентов.ИННКорреспондента = """"
		|			ТОГДА РеестрСамозанятых.Контрагент.ИНН
		|		ИНАЧЕ БанковскиеСчетаКонтрагентов.ИННКорреспондента
		|	КОНЕЦ КАК ИНН,
		|	БанковскиеСчетаКонтрагентов.ТекстНазначения КАК НазначениеПлатежа,
		|	РеестрСамозанятых.Сумма КАК Сумма,
		|	ВЫБОР
		|		КОГДА РеестрСамозанятых.Контрагент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
		|			ТОГДА ""1""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК КодВидаДохода,
		|	РеестрСамозанятых.НомерСтроки КАК Номер,
		|	ДанныеДокумента.Номер КАК НомерДокумента,
		|	ДанныеДокумента.Дата КАК ДатаДокумента
		|ИЗ
		|	Документ.СписаниеБезналичныхДенежныхСредств.БанковскиеСчетаСпискаКонтрагентов КАК РеестрСамозанятых
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств КАК ДанныеДокумента
		|		ПО РеестрСамозанятых.Ссылка = ДанныеДокумента.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчетаКонтрагентов
		|		ПО РеестрСамозанятых.БанковскийСчетКонтрагента = БанковскиеСчетаКонтрагентов.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО РеестрСамозанятых.Контрагент = Контрагенты.Ссылка
		|ГДЕ
		|	ДанныеДокумента.Ссылка В(&Ссылки)
		|	И ДанныеДокумента.Проведен
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка,
		|	РеестрСамозанятых.НомерСтроки
		|ИТОГИ ПО
		|	Ссылка";
	
	Запрос.УстановитьПараметр("Ссылки", СписокДокументов);
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаПоДокументам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	РеестрыПоДокументам = Новый Соответствие;
	
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		Реестр = НовыйРеестр();
		
		Выборка = ВыборкаПоДокументам.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если ПустаяСтрока(СокрЛП(Выборка.НомерСчета)) Тогда
				
				СообщениеОбОшибке = СтрШаблон(НСтр("ru = 'В строке №%1 не заполнен номер счета самозанятого %2.'"), 
										Выборка.Номер,
										Выборка.Контрагент);
				ОбщегоНазначения.СообщитьПользователю(
					СообщениеОбОшибке, 
					ВыборкаПоДокументам.Ссылка,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
						"БанковскиеСчетаСпискаКонтрагентов", Выборка.Номер, "БанковскийСчетКонтрагента"));
				
			КонецЕсли;
			
			ТекстОшибки = "";
			ПравильныйНомерСчета = ДенежныеСредстваКлиентСерверЛокализация.ПроверитьКорректностьНомераСчета(
				Выборка.НомерСчета, , ТекстОшибки);
			
			Если Не ПравильныйНомерСчета Тогда
				
				СообщениеОбОшибке = СтрШаблон(НСтр("ru = 'В строке №%1 номер счета самозанятого %2 заполнен не корректно.
													|%3'"), 
										Выборка.Номер,
										Выборка.Контрагент,
										ТекстОшибки);
				ОбщегоНазначения.СообщитьПользователю(
					СообщениеОбОшибке, 
					ВыборкаПоДокументам.Ссылка,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
						"БанковскиеСчетаСпискаКонтрагентов", Выборка.Номер, "БанковскийСчетКонтрагента"));
				
			КонецЕсли;
			
			Сведения = ФизическиеЛицаКлиентСервер.ЧастиИмени(Выборка.Контрагент);
			
			НоваяСтрока = Реестр.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Сведения);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
		КонецЦикла;
		
		ИмяФайла = СтрШаблон(НСтр("ru = 'Реестр выплат самозанятым № %1 от %2'"), 
						ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаПоДокументам.НомерДокумента, Истина, Истина), 
						Формат(ВыборкаПоДокументам.ДатаДокумента, НСтр("ru='ДФ=''d MMMM'''")));
		
		ДанныеПоДокументу = Новый Структура;
		ДанныеПоДокументу.Вставить("ИмяФайла", ИмяФайла);
		ДанныеПоДокументу.Вставить("Реестр", Реестр);
		
		РеестрыПоДокументам.Вставить(ВыборкаПоДокументам.Ссылка, ДанныеПоДокументу);
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат РеестрыПоДокументам;

КонецФункции

Функция НовыйРеестр()

	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Номер", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ИНН", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Фамилия", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Отчество", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("НомерСчета", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Сумма", Метаданные.ОпределяемыеТипы.ДенежнаяСуммаНеотрицательная.Тип);
	Результат.Колонки.Добавить("БИКБанка", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("КодВидаДохода", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("НазначениеПлатежа", Новый ОписаниеТипов("Строка"));
	
	Возврат Результат;

КонецФункции

Функция ЗаполнитьКодировкуРеестраЗачисленияДенежныхСредств(ТаблицаДокументов)

	
	КодировкаПоДокументам = Новый Соответствие;
	
	Для Каждого СтрокаТаблицы Из ТаблицаДокументов Цикл
		
		Если СтрокаТаблицы.КодировкаФайла = "" Тогда
			СтрокаТаблицы.КодировкаФайла = "UTF-8";
		КонецЕсли;
		
		КодировкаПоДокументам.Вставить(СтрокаТаблицы.Документ, СтрокаТаблицы.КодировкаФайла);
		
	КонецЦикла;
	
	Возврат КодировкаПоДокументам;

КонецФункции

//-- Локализация
#КонецОбласти

//++ Локализация


//-- Локализация

#КонецОбласти
