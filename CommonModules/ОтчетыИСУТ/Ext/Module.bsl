#Область ПрограммныйИнтерфейс

// Процедура предназначена для переопределения отчетов о расхождений ИС МП, ЕГАИС, Ветис, Зерно и Сатурн
//  для учета прикладных документов.
// 
// Параметры:
//  ТекстЗапроса - Строка - Текст запроса для подмены переопределяемой части отчета
//  ОтчетОбъект  - ОбъектМетаданныхОтчет - дорабатываемый отчет
Процедура ПриОпределенииТекстаЗапросаОформленныхДокументов(ТекстЗапроса, ОтчетОбъект) Экспорт
	
	Текст = Неопределено;
	Отчет = ОтчетОбъект.Метаданные();
	
	Если Отчет = Метаданные.Отчеты.АнализРасхожденийПриПоступленииАлкогольнойПродукции Тогда
		Текст = ОформленныеДокументыПоПоступлениюАлкогольнойПродукции();
	ИначеЕсли Отчет = Метаданные.Отчеты.АнализРасхожденийПриОтгрузкеАлкогольнойПродукции Тогда
		Текст = ОформленныеДокументыПоОтгрузкеАлкогольнойПродукции();
	ИначеЕсли Отчет = Метаданные.Отчеты.АнализРасхожденийПриДвиженииАлкогольнойПродукции Тогда
		Текст = ОформленныеДокументыПоДвижениюАлкогольнойПродукции();
	
	ИначеЕсли Отчет = Метаданные.Отчеты.АнализРасхожденийПриПоступленииПродукцииВЕТИС Тогда
		Текст = ОформленныеДокументыПоПоступлениюПродукцииВЕТИС();
	ИначеЕсли Отчет = Метаданные.Отчеты.АнализРасхожденийПриОтгрузкеПродукцииВЕТИС Тогда
		Текст = ОформленныеДокументыПоОтгрузкеПродукцииВЕТИС();
	ИначеЕсли Отчет = Метаданные.Отчеты.АнализРасхожденийПриИнвентаризацииПродукцииВЕТИС Тогда
		Текст = ОформленныеДокументыПоИнвентаризацииПродукцииВЕТИС();
	ИначеЕсли Отчет = Метаданные.Отчеты.АнализРасхожденийПриПроизводствеПродукцииВЕТИС Тогда
		Текст = ОформленныеДокументыПоПроизводственнойОперацииВЕТИС();
	
	ИначеЕсли Отчет = Метаданные.Отчеты.АнализРасхожденийПриМаркировкеТоваровИСМП Тогда
		Текст = ОформленныеДокументыПоМаркировкеТоваровИСМП();
	ИначеЕсли Отчет = Метаданные.Отчеты.АнализРасхожденийПриВыводеИзОборотаИСМП Тогда
		Текст = ОформленныеДокументыВыводуИзОборотаИСМП();
		
	ИначеЕсли Отчет = Метаданные.Отчеты.АнализРасхожденийПриДвиженииЗЕРНО Тогда
		Текст = ОформленныеДокументыПоДвижениюЗЕРНО();
		
	ИначеЕсли Отчет = Метаданные.Отчеты.АнализРасхожденийПриДвиженииСАТУРН Тогда
		Текст = ОформленныеДокументыПоДвижениюСАТУРН();
	КонецЕсли;
	
	Если Не(Текст = Неопределено) Тогда
		ТекстЗапроса = Текст;
	КонецЕсли;
	
КонецПроцедуры

// Процедура предназначена для переопределения параметров отчетов о расхождений ИС МП, ЕГАИС, Ветис, Зерно и Сатурн
//  для учета прикладных документов.
// 
// Параметры:
//  ОтчетОбъект  - ОбъектМетаданныхОтчет - дорабатываемый отчет
Процедура ПриУстановкеПараметровОтчета(ОтчетОбъект) Экспорт
	
	Отчет = ОтчетОбъект.Метаданные();
	
	Если Отчет = Метаданные.Отчеты.АнализРасхожденийПриДвиженииСАТУРН Тогда
		УстановитьПараметрыОтчетаСАТУРН(ОтчетОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ТекстыЗапросов

Функция ОформленныеДокументыВыводуИзОборотаИСМП()
	
	МассивТекстовЗапросов = Новый Массив;
	
	Реализации = "";
	Если ДобавитьОснование(МассивТекстовЗапросов, "РеализацияТоваровУслуг", "Товары", Истина) Тогда
		ДобавитьОснование(МассивТекстовЗапросов, "РеализацияТоваровУслуг", "Серии");
		Реализации = СтрСоединить(МассивТекстовЗапросов, " ОБЪЕДИНИТЬ ВСЕ ");
		ДопОтбор = СтрШаблон("И (%1 ИЛИ %2)",
			"ТаблицаТовары.Ссылка.Контрагент.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)",
			"ТаблицаТовары.Ссылка.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.РозничныйПокупатель)
			|И ТаблицаТовары.Ссылка.Касса В(ВЫБРАТЬ Касса Из РегистрСведений.КассыНеПередающиеДанныеВИСМП)");
		Реализации = СтрЗаменить(Реализации, ".Проведен", ".Проведен "+ДопОтбор);
	КонецЕсли;
	МассивТекстовЗапросов.Очистить();
	
	Розница = "";
	ДобавитьОснование(МассивТекстовЗапросов, "ЧекККМ", "Товары", Истина);
	ДобавитьОснование(МассивТекстовЗапросов, "ЧекККМ", "Серии");
	ДобавитьОснование(МассивТекстовЗапросов, "ОтчетОРозничныхПродажах", "Товары", Истина);
	ДобавитьОснование(МассивТекстовЗапросов, "ОтчетОРозничныхПродажах", "Серии");
	Если МассивТекстовЗапросов.Количество() Тогда
		Розница = СтрСоединить(МассивТекстовЗапросов, " ОБЪЕДИНИТЬ ВСЕ ");
		Розница = СтрЗаменить(Розница, ".Проведен", ".Проведен И ТаблицаТовары.Ссылка.КассаККМ В(ВЫБРАТЬ Касса Из РегистрСведений.КассыНеПередающиеДанныеВИСМП)");
	КонецЕсли;
	МассивТекстовЗапросов.Очистить();
	
	Возвраты = "";
	Если ДобавитьОснование(МассивТекстовЗапросов, "ВозвратТоваровПоставщику", "Товары", Истина) Тогда
		ДобавитьОснование(МассивТекстовЗапросов, "ВозвратТоваровПоставщику", "Серии");
		Возвраты = СтрСоединить(МассивТекстовЗапросов, " ОБЪЕДИНИТЬ ВСЕ ");
		Возвраты = СтрЗаменить(Возвраты, ".Проведен", ".Проведен И ТаблицаТовары.Ссылка.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.РозничныйПокупатель)");
	КонецЕсли;
	МассивТекстовЗапросов.Очистить();
	
	ЗнакСборки = "ВЫБОР КОГДА ТаблицаТовары.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров) ТОГДА 0 ИНАЧЕ 1 КОНЕЦ * ";
	ЗнакЭтоСерииШапки = "ВЫБОР КОГДА ТаблицаТовары.Номенклатура = ТаблицаТовары.Ссылка.Номенклатура И ТаблицаТовары.Характеристика = ТаблицаТовары.Ссылка.Характеристика ТОГДА 1 ИНАЧЕ 0 КОНЕЦ * ";
	Если ДобавитьОснование(МассивТекстовЗапросов, "СборкаТоваров", "", Истина, ЗнакСборки) Тогда
		МассивТекстовЗапросов[0] = СтрЗаменить(МассивТекстовЗапросов[0], "Документ.СборкаТоваров.", "Документ.СборкаТоваров");
	КонецЕсли;
	ДобавитьОснование(МассивТекстовЗапросов, "СборкаТоваров", "Товары",, ЗнакСборки+ЗнакЭтоСерииШапки);
	ДобавитьОснование(МассивТекстовЗапросов, "ВнутреннееПотребление", "Товары", Истина);
	ДобавитьОснование(МассивТекстовЗапросов, "ВнутреннееПотребление", "Серии");
	ДобавитьОснование(МассивТекстовЗапросов, "СписаниеНедостачТоваров", "Товары");
	
	Если ЗначениеЗаполнено(Реализации) Тогда
		МассивТекстовЗапросов.Добавить(Реализации);
	КонецЕсли;
	Если ЗначениеЗаполнено(Возвраты) Тогда
		МассивТекстовЗапросов.Добавить(Возвраты);
	КонецЕсли;
	Если ЗначениеЗаполнено(Розница) Тогда
		МассивТекстовЗапросов.Добавить(Розница);
	КонецЕсли;
	
	Текст = СтрСоединить(МассивТекстовЗапросов, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|");
	ДоработатьЗапросПродукцииИСМП(Текст);
	
	Возврат Текст;
	
КонецФункции

Функция ОформленныеДокументыПоМаркировкеТоваровИСМП()
	
	МассивТекстовЗапросов = Новый Массив;
	
	ЗнакСборки = "ВЫБОР КОГДА ТаблицаТовары.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ * ";
	ЗнакЭтоСерииШапки = "ВЫБОР КОГДА ТаблицаТовары.Номенклатура = ТаблицаТовары.Ссылка.Номенклатура И ТаблицаТовары.Характеристика = ТаблицаТовары.Ссылка.Характеристика ТОГДА 1 ИНАЧЕ 0 КОНЕЦ * ";
	Если ДобавитьОснование(МассивТекстовЗапросов, "СборкаТоваров", "", Истина, ЗнакСборки) Тогда
		МассивТекстовЗапросов[0] = СтрЗаменить(МассивТекстовЗапросов[0], "Документ.СборкаТоваров.", "Документ.СборкаТоваров");
	КонецЕсли;
	ДобавитьОснование(МассивТекстовЗапросов, "СборкаТоваров", "Товары",, ЗнакСборки+ЗнакЭтоСерииШапки);
	
	Импорт = СтрЗаменить(РасчетСтатусовОформленияИСУТ.ХозяйственнаяОперацияИмпорт(), "ТаблицаДокумента", "ТаблицаТовары.Ссылка");
	Знак = СтрШаблон("(%1 ИЛИ %2)", Импорт, "ТаблицаТовары.Ссылка.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.РозничныйПокупатель)");
	ЗнакПоступление = СтрШаблон("ВЫБОР КОГДА %1 ТОГДА 1 ИНАЧЕ 0 КОНЕЦ * ", Знак);
	ДобавитьОснование(МассивТекстовЗапросов,"ПриобретениеТоваровУслуг", "Товары", Истина, ЗнакПоступление);
	ДобавитьОснование(МассивТекстовЗапросов,"ПриобретениеТоваровУслуг", "Серии",, ЗнакПоступление);
	
	ДобавитьОснование(МассивТекстовЗапросов, "ПрочееОприходованиеТоваров", "Товары", Истина);
	ДобавитьОснование(МассивТекстовЗапросов, "ПрочееОприходованиеТоваров", "Серии");
	ДобавитьОснование(МассивТекстовЗапросов, "ОприходованиеИзлишковТоваров", "Товары");

	Текст = СтрСоединить(МассивТекстовЗапросов, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|");
	ДоработатьЗапросПродукцииИСМП(Текст);
	
	Возврат Текст;
	
КонецФункции

Функция ОформленныеДокументыПоПроизводственнойОперацииВЕТИС()
	
	МассивТекстовЗапросов = Новый Массив;
	
	ЗнакСборки = "ВЫБОР КОГДА ТаблицаТовары.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров) ТОГДА 1 ИНАЧЕ -1 КОНЕЦ *";
	ЗнакЭтоСерииШапки = "ВЫБОР КОГДА ТаблицаТовары.Номенклатура = ТаблицаТовары.Ссылка.Номенклатура И ТаблицаТовары.Характеристика = ТаблицаТовары.Ссылка.Характеристика ТОГДА 1 ИНАЧЕ -1 КОНЕЦ *";
	Если ДобавитьОснование(МассивТекстовЗапросов, "СборкаТоваров", "", Истина, ЗнакСборки) Тогда
		МассивТекстовЗапросов[0] = СтрЗаменить(МассивТекстовЗапросов[0], "Документ.СборкаТоваров.", "Документ.СборкаТоваров");
	КонецЕсли;
	ДобавитьОснование(МассивТекстовЗапросов, "СборкаТоваров", "Товары", Истина, "-"+ЗнакСборки);
	ДобавитьОснование(МассивТекстовЗапросов, "СборкаТоваров", "Товары",, ЗнакСборки+ЗнакЭтоСерииШапки);
	

	Текст = СтрСоединить(МассивТекстовЗапросов, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|");
	ДоработатьЗапросПродукцииВЕТИС(Текст);
	
	Возврат Текст;
	
КонецФункции

Функция ОформленныеДокументыПоИнвентаризацииПродукцииВЕТИС()
	
	МассивТекстовЗапросов = Новый Массив;
	ДобавитьОснование(МассивТекстовЗапросов, "ВнутреннееПотребление", "Товары", Истина, "-");
	ДобавитьОснование(МассивТекстовЗапросов, "ВнутреннееПотребление", "Серии",, "-");
	ДобавитьОснование(МассивТекстовЗапросов, "ПрочееОприходованиеТоваров", "Товары", Истина);
	ДобавитьОснование(МассивТекстовЗапросов, "ПрочееОприходованиеТоваров", "Серии");
	ДобавитьОснование(МассивТекстовЗапросов, "СписаниеНедостачТоваров", "Товары",, "-");
	ДобавитьОснование(МассивТекстовЗапросов, "ОприходованиеИзлишковТоваров", "Товары");
	ДобавитьОснование(МассивТекстовЗапросов, "ПересортицаТоваров", "Товары",, "-");
	ДобавитьОснование(МассивТекстовЗапросов, "ПересортицаТоваров", "Товары",,, "Оприходование");
	ДобавитьОснование(МассивТекстовЗапросов, "ПорчаТоваров", "Товары",, "-");
	Если ДобавитьОснование(МассивТекстовЗапросов, "ПорчаТоваров", "Товары",,, "Оприходование") Тогда
		МассивТекстовЗапросов[МассивТекстовЗапросов.ВГраница()] =
			СтрЗаменить(МассивТекстовЗапросов[МассивТекстовЗапросов.ВГраница()], "СерияОприходование", "Серия");
	КонецЕсли;
	Текст = СтрСоединить(МассивТекстовЗапросов, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|");
	ДоработатьЗапросПродукцииВЕТИС(Текст);
	
	Возврат Текст;
	
КонецФункции

Функция ОформленныеДокументыПоОтгрузкеПродукцииВЕТИС()
	
	МассивТекстовЗапросов = Новый Массив;
	ДобавитьОснование(МассивТекстовЗапросов, "РеализацияТоваровУслуг", "Товары", Истина);
	ДобавитьОснование(МассивТекстовЗапросов, "РеализацияТоваровУслуг", "Серии");
	ДобавитьОснование(МассивТекстовЗапросов, "ВозвратТоваровПоставщику", "Товары", Истина);
	ДобавитьОснование(МассивТекстовЗапросов, "ВозвратТоваровПоставщику", "Серии");
	ДобавитьОснование(МассивТекстовЗапросов, "ПеремещениеТоваров", "Товары", Истина);
	ДобавитьОснование(МассивТекстовЗапросов, "ПеремещениеТоваров", "Серии");
	ДобавитьОснование(МассивТекстовЗапросов, "ВозвратТоваровМеждуОрганизациями", "Товары");
	ДобавитьОснование(МассивТекстовЗапросов, "ПередачаТоваровМеждуОрганизациями", "Товары");

	
	Текст = СтрСоединить(МассивТекстовЗапросов, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|");
	ДоработатьЗапросПродукцииВЕТИС(Текст);
	
	Возврат Текст;
	
КонецФункции

Функция ОформленныеДокументыПоПоступлениюПродукцииВЕТИС()
	
	МассивТекстовЗапросов = Новый Массив;
	ДобавитьОснование(МассивТекстовЗапросов,"ПриобретениеТоваровУслуг", "Товары", Истина);
	ДобавитьОснование(МассивТекстовЗапросов,"ПриобретениеТоваровУслуг", "Серии");
	ДобавитьОснование(МассивТекстовЗапросов,"ВозвратТоваровОтКлиента", "Товары", Истина);
	ДобавитьОснование(МассивТекстовЗапросов,"ВозвратТоваровОтКлиента", "Серии");
	ДобавитьОснование(МассивТекстовЗапросов,"ПеремещениеТоваров", "Товары", Истина);
	ДобавитьОснование(МассивТекстовЗапросов,"ПеремещениеТоваров", "Серии");
	ДобавитьОснование(МассивТекстовЗапросов,"ВозвратТоваровМеждуОрганизациями", "Товары");
	ДобавитьОснование(МассивТекстовЗапросов,"ПередачаТоваровМеждуОрганизациями", "Товары");

	
	Текст = СтрСоединить(МассивТекстовЗапросов, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|");
	ДоработатьЗапросПродукцииВЕТИС(Текст);
	
	Возврат Текст;
	
КонецФункции

Функция ОформленныеДокументыПоОтгрузкеАлкогольнойПродукции()
	
	МассивТекстовЗапросов = Новый Массив;
	ДобавитьОснование(МассивТекстовЗапросов,"ВозвратТоваровПоставщику", "Товары", Истина);
	ДобавитьОснование(МассивТекстовЗапросов,"ВозвратТоваровПоставщику", "Серии");
	ДобавитьОснование(МассивТекстовЗапросов,"ПеремещениеТоваров", "Товары", Истина);
	ДобавитьОснование(МассивТекстовЗапросов,"ПеремещениеТоваров", "Серии");
	ДобавитьОснование(МассивТекстовЗапросов,"РеализацияТоваровУслуг", "Товары", Истина);
	ДобавитьОснование(МассивТекстовЗапросов,"РеализацияТоваровУслуг", "Серии");
	ДобавитьОснование(МассивТекстовЗапросов,"ВозвратТоваровМеждуОрганизациями", "Товары");
	ДобавитьОснование(МассивТекстовЗапросов,"ПередачаТоваровМеждуОрганизациями", "Товары");
	
	Текст = СтрСоединить(МассивТекстовЗапросов, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|");
	ДоработатьЗапросПродукцииЕГАИС(Текст);
	
	Возврат Текст;
	
КонецФункции

Функция ОформленныеДокументыПоДвижениюАлкогольнойПродукции()
	
	МассивТекстовЗапросов = Новый Массив;
	ДобавитьОснование(МассивТекстовЗапросов,"ВозвратТоваровПоставщику", "Товары", Истина, "-");
	ДобавитьОснование(МассивТекстовЗапросов,"ВозвратТоваровПоставщику", "Серии",, "-");
	ДобавитьОснование(МассивТекстовЗапросов,"РеализацияТоваровУслуг", "Товары", Истина, "-");
	ДобавитьОснование(МассивТекстовЗапросов,"РеализацияТоваровУслуг", "Серии",, "-");
	ДобавитьОснование(МассивТекстовЗапросов,"ПриобретениеТоваровУслуг", "Товары", Истина);
	ДобавитьОснование(МассивТекстовЗапросов,"ПриобретениеТоваровУслуг", "Серии");
	ДобавитьОснование(МассивТекстовЗапросов,"ОприходованиеИзлишковТоваров", "Товары");
	ДобавитьОснование(МассивТекстовЗапросов,"ВнутреннееПотребление", "Товары", Истина, "-");
	ДобавитьОснование(МассивТекстовЗапросов,"ВнутреннееПотребление", "Серии",, "-");
	ДобавитьОснование(МассивТекстовЗапросов,"СписаниеНедостачТоваров", "Товары",, "-");
	ДобавитьОснование(МассивТекстовЗапросов,"ВозвратТоваровОтКлиента", "Товары", Истина);
	ДобавитьОснование(МассивТекстовЗапросов,"ВозвратТоваровОтКлиента", "Серии");
	
	ЗнакСборки = "ВЫБОР КОГДА ТаблицаТовары.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров) ТОГДА 1 ИНАЧЕ -1 КОНЕЦ *";
	ЗнакЭтоСерииШапки = "ВЫБОР КОГДА ТаблицаТовары.Номенклатура = ТаблицаТовары.Ссылка.Номенклатура И ТаблицаТовары.Характеристика = ТаблицаТовары.Ссылка.Характеристика ТОГДА 1 ИНАЧЕ -1 КОНЕЦ *";
	Если ДобавитьОснование(МассивТекстовЗапросов, "СборкаТоваров", "", Истина, ЗнакСборки) Тогда
		МассивТекстовЗапросов[0] = СтрЗаменить(МассивТекстовЗапросов[0], "Документ.СборкаТоваров.", "Документ.СборкаТоваров");
	КонецЕсли;
	ДобавитьОснование(МассивТекстовЗапросов, "СборкаТоваров", "Товары", Истина, "-" + ЗнакСборки);
	ДобавитьОснование(МассивТекстовЗапросов, "СборкаТоваров", "Товары",, ЗнакСборки + ЗнакЭтоСерииШапки);
	
	МассивТекстовЗапросовЗаказПоставщику = Новый Массив;
	ЗаказыПоставщику = "";
	Если ДобавитьОснование(МассивТекстовЗапросовЗаказПоставщику, "ЗаказПоставщику", "Товары") Тогда
		ЗаказыПоставщику = СтрСоединить(МассивТекстовЗапросовЗаказПоставщику, " ОБЪЕДИНИТЬ ВСЕ ");
		ЗаказыПоставщику = СтрЗаменить(ЗаказыПоставщику, "ТаблицаТовары.Серия", "ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)");
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ЗаказыПоставщику) Тогда
		МассивТекстовЗапросов.Добавить(ЗаказыПоставщику);
	КонецЕсли;
	
	
	
	ТолькоНемаркируемаяПродукция = Новый Массив;
	ДобавитьОснование(ТолькоНемаркируемаяПродукция,"ОтчетОРозничныхПродажах", "Товары", Истина, "-"); //!
	ДобавитьОснование(ТолькоНемаркируемаяПродукция,"ОтчетОРозничныхПродажах", "Серии",, "-"); //!
	ТолькоНемаркируемаяПродукция = СтрСоединить(ТолькоНемаркируемаяПродукция, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|");
	ТолькоНемаркируемаяПродукция = СтрЗаменить(ТолькоНемаркируемаяПродукция, "ГДЕ", "ГДЕ
	|	ТаблицаТовары.Номенклатура.ВидАлкогольнойПродукции.Маркируемый = Ложь И ");
	
	Текст = СтрСоединить(МассивТекстовЗапросов, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|")
	 + "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|"
	+ ТолькоНемаркируемаяПродукция;
	
	ДоработатьЗапросПродукцииЕГАИС(Текст);
	
	Возврат Текст;
	
КонецФункции

Функция ОформленныеДокументыПоПоступлениюАлкогольнойПродукции()
	
	МассивТекстовЗапросов = Новый Массив;
	ДобавитьОснование(МассивТекстовЗапросов,"ПриобретениеТоваровУслуг", "Товары", Истина);
	ДобавитьОснование(МассивТекстовЗапросов,"ПриобретениеТоваровУслуг", "Серии");
	ДобавитьОснование(МассивТекстовЗапросов,"ПеремещениеТоваров", "Товары", Истина);
	ДобавитьОснование(МассивТекстовЗапросов,"ПеремещениеТоваров", "Серии");
	ДобавитьОснование(МассивТекстовЗапросов,"ВозвратТоваровМеждуОрганизациями", "Товары");
	ДобавитьОснование(МассивТекстовЗапросов,"ПередачаТоваровМеждуОрганизациями", "Товары");
	
	Текст = СтрСоединить(МассивТекстовЗапросов, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|");
	ДоработатьЗапросПродукцииЕГАИС(Текст);
	
	Возврат Текст;
	
КонецФункции

Функция ОформленныеДокументыПоДвижениюЗЕРНО()

	МассивТекстовЗапросов = Новый Массив;
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ОприходованиеИзлишковТоваров", "Товары");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПересортицаТоваров", "Товары",,, "Оприходование");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПересортицаТоваров", "Товары",, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "РеализацияТоваровУслуг", "Товары", Истина, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "РеализацияТоваровУслуг", "Серии",, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ВозвратТоваровПоставщику", "Товары", Истина, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ВозвратТоваровПоставщику", "Серии",, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ВозвратТоваровМеждуОрганизациями", "Товары",, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ВозвратТоваровМеждуОрганизациями", "Товары");	
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПередачаТоваровМеждуОрганизациями", "Товары",, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПередачаТоваровМеждуОрганизациями", "Товары");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "СписаниеНедостачТоваров", "Товары",, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ВнутреннееПотребление", "Товары", Истина, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ВнутреннееПотребление", "Серии",, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ВозвратТоваровОтКлиента", "Товары", Истина);
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ВозвратТоваровОтКлиента", "Серии");
	
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПорчаТоваров", "Товары",, "-");
	Если ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПорчаТоваров", "Товары",,, "Оприходование") Тогда
		МассивТекстовЗапросов[МассивТекстовЗапросов.ВГраница()] =
			СтрЗаменить(МассивТекстовЗапросов[МассивТекстовЗапросов.ВГраница()], "СерияОприходование", "Серия");
	КонецЕсли;
	
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПрочееОприходованиеТоваров", "Товары", Истина);
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПрочееОприходованиеТоваров", "Серии");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПеремещениеТоваров", "Товары", Истина);
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПеремещениеТоваров", "Серии");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПеремещениеТоваров", "Товары", Истина, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПеремещениеТоваров", "Серии",, "-");
	
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ВыкупПринятыхНаХранениеТоваров", "Товары",, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ВыкупТоваровХранителем", "Товары",, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ОприходованиеИзлишковТоваровУХранителя", "Товары");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ОтчетОСписанииТоваровУХранителя", "Товары",, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПорчаТоваровУХранителя", "Товары",, "-");
	
	Если ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПорчаТоваровУХранителя", "Товары",,, "Оприходование") Тогда
		МассивТекстовЗапросов[МассивТекстовЗапросов.ВГраница()] =
			СтрЗаменить(МассивТекстовЗапросов[МассивТекстовЗапросов.ВГраница()], "СерияОприходование", "Серия");
	КонецЕсли;
	
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПересортицаТоваровУХранителя", "Товары",, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПересортицаТоваровУХранителя", "Товары",,, "Оприходование");
	
	ЗнакСборки = "ВЫБОР КОГДА ТаблицаТовары.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров) ТОГДА 1 ИНАЧЕ -1 КОНЕЦ *";
	ЗнакЭтоСерииШапки = "ВЫБОР КОГДА ТаблицаТовары.Номенклатура = ТаблицаТовары.Ссылка.Номенклатура И ТаблицаТовары.Характеристика = ТаблицаТовары.Ссылка.Характеристика ТОГДА 1 ИНАЧЕ -1 КОНЕЦ *";
	Если ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "СборкаТоваров", "", Истина, ЗнакСборки) Тогда
		МассивТекстовЗапросов[0] = СтрЗаменить(МассивТекстовЗапросов[0], "Документ.СборкаТоваров.", "Документ.СборкаТоваров");
	КонецЕсли;
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "СборкаТоваров", "Товары", Истина, "-" + ЗнакСборки);
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "СборкаТоваров", "Товары",, ЗнакСборки + ЗнакЭтоСерииШапки);
	
	МассивТекстовЗапросовЗаказПоставщику = Новый Массив;
	ЗаказыПоставщику = "";
	Если ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросовЗаказПоставщику, "ЗаказПоставщику", "Товары") Тогда
		ЗаказыПоставщику = СтрСоединить(МассивТекстовЗапросовЗаказПоставщику, " ОБЪЕДИНИТЬ ВСЕ ");
		ЗаказыПоставщику = СтрЗаменить(ЗаказыПоставщику, "ТаблицаТовары.Серия", "ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)");
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ЗаказыПоставщику) Тогда
		МассивТекстовЗапросов.Добавить(ЗаказыПоставщику);
	КонецЕсли;
	
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ОтгрузкаТоваровСХранения", "Товары", Истина);
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ОтгрузкаТоваровСХранения", "Серии");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПередачаТоваровХранителю", "Товары", Истина, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПередачаТоваровХранителю", "Серии",, "-");
	
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов,"КорректировкаРеализации", "Расхождения",, "-");
	
	
	
	Текст = СтрСоединить(МассивТекстовЗапросов, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|");
	ДоработатьЗапросПродукцииЗЕРНО(Текст);
	
	Возврат Текст;
	
КонецФункции

Функция ОформленныеДокументыПоДвижениюСАТУРН()
	
	МассивТекстовЗапросов = Новый Массив;
	
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПриобретениеТоваровУслуг", "Товары", Истина);
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПриобретениеТоваровУслуг", "Серии");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ОприходованиеИзлишковТоваров", "Товары");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ОприходованиеИзлишковТоваровУХранителя", "Товары");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПрочееОприходованиеТоваров", "Товары", Истина,,,
		"ТаблицаТовары.Ссылка.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеИзПроизводства)");
	
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПрочееОприходованиеТоваров", "Серии",,,,
		"ТаблицаТовары.Ссылка.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеИзПроизводства)");
		
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПересортицаТоваров", "Товары",,, "Оприходование");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПересортицаТоваров", "Товары",, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПересортицаТоваровУХранителя", "Товары",, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПересортицаТоваровУХранителя", "Товары",,, "Оприходование");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПорчаТоваров", "Товары",, "-");
	Если ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПорчаТоваров", "Товары",,, "Оприходование") Тогда
		МассивТекстовЗапросов[МассивТекстовЗапросов.ВГраница()] =
			СтрЗаменить(МассивТекстовЗапросов[МассивТекстовЗапросов.ВГраница()], "СерияОприходование", "Серия");
	КонецЕсли;

	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "СписаниеНедостачТоваров", "Товары",, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ОтчетОСписанииТоваровУХранителя", "Товары",, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ВнутреннееПотребление", "Товары", Истина, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ВнутреннееПотребление", "Серии",, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "РеализацияТоваровУслуг", "Товары", Истина, "-",,
		"ТаблицаТовары.Ссылка.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)");
	
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "РеализацияТоваровУслуг", "Серии",, "-",,
		"ТаблицаТовары.Ссылка.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)");
	
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ВозвратТоваровПоставщику", "Товары", Истина, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ВозвратТоваровПоставщику", "Серии",, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ВозвратТоваровМеждуОрганизациями", "Товары",, "-",,
		"ТаблицаТовары.Ссылка.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)");
		
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ВозвратТоваровМеждуОрганизациями", "Товары",,,,
		"ТаблицаТовары.Ссылка.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)");
	
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПередачаТоваровМеждуОрганизациями", "Товары",, "-",,
		"ТаблицаТовары.Ссылка.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)");
		
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПередачаТоваровМеждуОрганизациями", "Товары",,,,
		"ТаблицаТовары.Ссылка.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)");
	
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПеремещениеТоваров", "Товары", Истина);
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПеремещениеТоваров", "Серии");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПеремещениеТоваров", "Товары", Истина, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПеремещениеТоваров", "Серии",, "-");
	
	ЗнакСборки = "ВЫБОР КОГДА ТаблицаТовары.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров) ТОГДА 1 ИНАЧЕ -1 КОНЕЦ *";
	ЗнакЭтоСерииШапки = "ВЫБОР КОГДА ТаблицаТовары.Номенклатура = ТаблицаТовары.Ссылка.Номенклатура И ТаблицаТовары.Характеристика = ТаблицаТовары.Ссылка.Характеристика ТОГДА 1 ИНАЧЕ -1 КОНЕЦ *";
	Если ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "СборкаТоваров", "", Истина, ЗнакСборки) Тогда
		МассивТекстовЗапросов[0] = СтрЗаменить(МассивТекстовЗапросов[0], "Документ.СборкаТоваров.", "Документ.СборкаТоваров");
	КонецЕсли;
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "СборкаТоваров", "Товары", Истина, "-" + ЗнакСборки);
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "СборкаТоваров", "Товары",, ЗнакСборки + ЗнакЭтоСерииШапки);
	
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ВыкупТоваровХранителем", "Товары",, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ОтгрузкаТоваровСХранения", "Товары", Истина);
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ОтгрузкаТоваровСХранения", "Серии");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПередачаТоваровХранителю", "Товары", Истина, "-");
	ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, "ПередачаТоваровХранителю", "Серии",, "-");
	
	
	
	Текст = СтрСоединить(МассивТекстовЗапросов, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|");
	
	ДоработатьЗапросПродукцииСАТУРН(Текст, "БезСкладскихДвижений", "БезСкладскихДвижений");
	
	ПолучитьПредварительныеДанныеДляСкладкихДвиженийПоДокументамОснования(Текст, "ПроизводственнаяОперацияСАТУРН", "Товары");
	ПолучитьПредварительныеДанныеДляСкладкихДвиженийПоДокументамОснования(Текст, "ПроизводственнаяОперацияСАТУРН", "Сырье", "-");
	
	МассивТекстовЗапросов = Новый Массив;
	
	ДобавитьСкладскиеДвиженияПоДокументамОснования(МассивТекстовЗапросов, "ПроизводственнаяОперацияСАТУРН", "Товары");
	ДобавитьСкладскиеДвиженияПоДокументамОснования(МассивТекстовЗапросов, "ПроизводственнаяОперацияСАТУРН", "Сырье");
	
	ТекстСкладскиеДвижения = СтрСоединить(МассивТекстовЗапросов, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|");
	
	ДоработатьЗапросПродукцииСАТУРН(ТекстСкладскиеДвижения, "СкладскиеДвижения", "СкладскиеДвижения");
	
	Текст = Текст + Символы.ПС + ТекстСкладскиеДвижения;
	
	РассчитатьИтоговоеКоличествоПоСкладскимДвижениям(Текст, "СкладскиеДвижения", "СкладскиеДвиженияИтоговые");
	ОбъединитьЗапросыСАТУРН(Текст, "СкладскиеДвижения", "СкладскиеДвиженияИтоговые", "БезСкладскихДвижений");
	
	Возврат Текст;
	
КонецФункции

#КонецОбласти

#Область Шаблоны

Функция ДобавитьОснование(МассивТекстовЗапросов, ИмяДокумента, ИмяТабличнойЧасти, СерииОтдельно = Ложь, Знак = "", Постфикс = "")
	
	Если Не ПравоДоступа("Просмотр", Метаданные.Документы[ИмяДокумента]) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Шаблон = 
	"ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка              КАК Ссылка,
	|	ТаблицаТовары.Номенклатура%5      КАК Номенклатура,
	|	ТаблицаТовары.Характеристика%5    КАК Характеристика,
	|	ТаблицаТовары.Серия%5             КАК Серия,
	|	%4СУММА(ТаблицаТовары.Количество) КАК Количество
	|ИЗ
	|	Документ.%1.%2 КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка.Проведен
	|	И ТаблицаТовары.Ссылка В
	|		(ВЫБРАТЬ
	|			ПрикладныеДокументы.Ссылка
	|		ИЗ
	|			ПрикладныеДокументы КАК ПрикладныеДокументы)
	|	%3
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Ссылка,
	|	ТаблицаТовары.Номенклатура%5,
	|	ТаблицаТовары.Характеристика%5,
	|	ТаблицаТовары.Серия%5";
	
	ШаблонСерииОтдельно = СтрШаблон(
	"И (ТаблицаТовары.Серия%1 <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			ИЛИ ТаблицаТовары.СтатусУказанияСерий = 0)", Постфикс);
	
	Шаблон = СтрШаблон(Шаблон,
		ИмяДокумента,
		ИмяТабличнойЧасти,
		?(СерииОтдельно, ШаблонСерииОтдельно, ""),
		Знак,
		Постфикс);
	
	МассивТекстовЗапросов.Добавить(Шаблон);
	Возврат Истина;
	
КонецФункции

Функция ПолучитьПредварительныеДанныеДляСкладкихДвиженийПоДокументамОснования(ТекстЗапроса, ИмяДокумента, ИмяТабличнойЧасти, Знак = "")
	
	Если Не ПравоДоступа("Просмотр", Метаданные.Документы[ИмяДокумента]) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры") Тогда
		Возврат Ложь;
	ИначеЕсли Не ПолучитьФункциональнуюОпцию("ИспользоватьОрдерныеСклады") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Шаблон =
	"ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка.ДокументОснование КАК Ссылка,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Серия КАК Серия,
	|	ВЫБОР 
	|		КОГДА %3 = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			СУММА(ТаблицаТовары.Количество)
	|		ИНАЧЕ
	|			0 
	|	КОНЕЦ КАК КоличествоПриход,
	|	ВЫБОР 
	|		КОГДА %3 = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|			СУММА(ТаблицаТовары.Количество)
	|		ИНАЧЕ
	|			0 
	|	КОНЕЦ КАК КоличествоРасход
	|ПОМЕСТИТЬ ТаблицаТовары%1%2
	|ИЗ
	|	Документ.%1.%2 КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка.Проведен
	|	И ТаблицаТовары.Ссылка.ДокументОснование В
	|		(ВЫБРАТЬ
	|			ПрикладныеДокументы.Ссылка
	|		ИЗ
	|			ПрикладныеДокументы КАК ПрикладныеДокументы)
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия,
	|	ТаблицаТовары.Ссылка.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТоварыПоРаспоряжениям.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ РасходныеОрдера%1%2
	|ИЗ
	|	Документ.РасходныйОрдерНаТовары.ТоварыПоРаспоряжениям КАК ТоварыПоРаспоряжениям
	|ГДЕ
	|	ТоварыПоРаспоряжениям.Распоряжение В
	|		(ВЫБРАТЬ
	|			ТаблицаТовары%1%2.Ссылка КАК ДокументОснование
	|		ИЗ
	|			ТаблицаТовары%1%2 КАК ТаблицаТовары%1%2)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПриходныйОрдерНаТовары.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПриходныеОрдера%1%2
	|ИЗ
	|	Документ.ПриходныйОрдерНаТовары КАК ПриходныйОрдерНаТовары
	|ГДЕ
	|	ПриходныйОрдерНаТовары.Распоряжение В
	|		(ВЫБРАТЬ
	|			ТаблицаТовары%1%2.Ссылка КАК ДокументОснование
	|		ИЗ
	|			ТаблицаТовары%1%2 КАК ТаблицаТовары%1%2)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	0 КАК Порядок,
	|	Номенклатура КАК Номенклатура,
	|	Характеристика КАК Характеристика,
	|	Серия КАК Серия,
	|	ВЫБОР 
	|		КОГДА %3 = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			СУММА(КоличествоОборот)
	|		ИНАЧЕ
	|			0 
	|	КОНЕЦ КАК КоличествоПриход,
	|	ВЫБОР 
	|		КОГДА %3 = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|			СУММА(КоличествоОборот)
	|		ИНАЧЕ
	|			0 
	|	КОНЕЦ КАК КоличествоРасход
	|ПОМЕСТИТЬ СкладскиеДвижения%1%2
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров.Обороты(,,, Документ В
	|		(ВЫБРАТЬ
	|			Ссылка
	|		Из
	|			РасходныеОрдера%1%2))
	|СГРУППИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Серия
	|ИМЕЮЩИЕ
	|	СУММА(КоличествоОборот) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1 КАК Порядок,
	|	Номенклатура КАК Номенклатура,
	|	Характеристика КАК Характеристика,
	|	Серия КАК Серия,
	|	ВЫБОР 
	|		КОГДА %3 = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			СУММА(КоличествоОборот)
	|		ИНАЧЕ
	|			0 
	|	КОНЕЦ КАК КоличествоПриход,
	|	ВЫБОР 
	|		КОГДА %3 = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|			СУММА(КоличествоОборот)
	|		ИНАЧЕ
	|			0 
	|	КОНЕЦ КАК КоличествоРасход
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров.Обороты(,,, Документ В
	|		(ВЫБРАТЬ
	|			Ссылка
	|		Из
	|			ПриходныеОрдера%1%2))
	|СГРУППИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Серия
	|ИМЕЮЩИЕ
	|	СУММА(КоличествоОборот) > 0
	|;";
	
	Шаблон = СтрШаблон(Шаблон,
		ИмяДокумента,
		ИмяТабличнойЧасти,
		?(Знак = "-", "ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)", "ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)"));
	
	ТекстЗапроса = ТекстЗапроса + Символы.ПС + Шаблон;
	Возврат Истина;
	
КонецФункции

Функция ДобавитьСкладскиеДвиженияПоДокументамОснования(МассивТекстовЗапросов, ИмяДокумента, ИмяТабличнойЧасти)
	
	Если Не ПравоДоступа("Просмотр", Метаданные.Документы[ИмяДокумента]) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры") Тогда
		Возврат Ложь;
	ИначеЕсли Не ПолучитьФункциональнуюОпцию("ИспользоватьОрдерныеСклады") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Шаблон = 
	"////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СкладскиеДвижения.Номенклатура,
	|	СкладскиеДвижения.Характеристика,
	|	СкладскиеДвижения.Серия,
	|	ВЫБОР
	|		КОГДА
	|			ТаблицаТовары%1%2.КоличествоПриход > СкладскиеДвижения.КоличествоПриход
	|		ТОГДА СкладскиеДвижения.КоличествоПриход
	|		ИНАЧЕ СкладскиеДвижения.КоличествоПриход
	|	КОНЕЦ КАК КоличествоПриход,
	|	ВЫБОР
	|		КОГДА
	|			ТаблицаТовары%1%2.КоличествоРасход > СкладскиеДвижения.КоличествоРасход
	|		ТОГДА СкладскиеДвижения.КоличествоРасход
	|		ИНАЧЕ СкладскиеДвижения.КоличествоРасход
	|	КОНЕЦ КАК КоличествоРасход,
	|	ТаблицаТовары%1%2.Ссылка
	|ИЗ
	|	ТаблицаТовары%1%2 КАК ТаблицаТовары%1%2
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СкладскиеДвижения%1%2 КАК СкладскиеДвижения
	|		ПО СкладскиеДвижения.Номенклатура = ТаблицаТовары%1%2.Номенклатура
	|		И СкладскиеДвижения.Характеристика = ТаблицаТовары%1%2.Характеристика
	|		И СкладскиеДвижения.Серия = ТаблицаТовары%1%2.Серия";
	
	Шаблон = СтрШаблон(Шаблон,
		ИмяДокумента,
		ИмяТабличнойЧасти);
	
	МассивТекстовЗапросов.Добавить(Шаблон);
	Возврат Истина;
	
КонецФункции


Функция ДобавитьОснованиеСВидомДвижения(МассивТекстовЗапросов, ИмяДокумента, ИмяТабличнойЧасти, СерииОтдельно = Ложь, Знак = "", Постфикс = "", УсловиеОтбора = "ИСТИНА")
	
	Если Не ПравоДоступа("Просмотр", Метаданные.Документы[ИмяДокумента]) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Шаблон = 
	"ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка              КАК Ссылка,
	|	ТаблицаТовары.Номенклатура%4      КАК Номенклатура,
	|	ТаблицаТовары.Характеристика%4    КАК Характеристика,
	|	ТаблицаТовары.Серия%4             КАК Серия,
	|	ВЫБОР 
	|		КОГДА %5 = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			СУММА(ТаблицаТовары.Количество)
	|		ИНАЧЕ
	|			0 
	|	КОНЕЦ КАК КоличествоПриход,
	|	ВЫБОР 
	|		КОГДА %5 = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|			СУММА(ТаблицаТовары.Количество)
	|		ИНАЧЕ
	|			0 
	|	КОНЕЦ КАК КоличествоРасход
	|ИЗ
	|	Документ.%1.%2 КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка.Проведен
	|	И ТаблицаТовары.Ссылка В
	|		(ВЫБРАТЬ
	|			ПрикладныеДокументы.Ссылка
	|		ИЗ
	|			ПрикладныеДокументы КАК ПрикладныеДокументы)
	|	%3 И %6
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Ссылка,
	|	ТаблицаТовары.Номенклатура%4,
	|	ТаблицаТовары.Характеристика%4,
	|	ТаблицаТовары.Серия%4";
	
	ШаблонСерииОтдельно = СтрШаблон(
	"И (ТаблицаТовары.Серия%1 <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			ИЛИ ТаблицаТовары.СтатусУказанияСерий = 0)", Постфикс);
	
	Шаблон = СтрШаблон(Шаблон,
		ИмяДокумента,
		ИмяТабличнойЧасти,
		?(СерииОтдельно, ШаблонСерииОтдельно, ""),
		Постфикс,
		?(Знак = "-", "ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)", "ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)"),
		УсловиеОтбора);
	
	МассивТекстовЗапросов.Добавить(Шаблон);
	Возврат Истина;
	
КонецФункции

Процедура ДоработатьЗапросПродукцииВЕТИС(Текст)
	
	Позиция = СтрНайти(Текст, "ВЫБРАТЬ");
	Текст = Лев(Текст, Позиция-1) + "ВЫБРАТЬ РАЗРЕШЕННЫЕ" + Сред(Текст, Позиция + СтрДлина("ВЫБРАТЬ"));
	Позиция = СтрНайти(Текст, "
	|ИЗ");
	Текст = Лев(Текст, Позиция -1)+ "
	|ПОМЕСТИТЬ ТоварыНакладнойПредварительно
	|"+ Сред(Текст, Позиция);
	
	Текст = Текст + 
	";
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ ТоварыНакладной
	|ИЗ
	|	ТоварыНакладнойПредварительно КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|		И СправочникНоменклатура.ОсобенностьУчета В(
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МорепродуктыПодконтрольныеВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ЗерноВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПродуктыПереработкиЗернаВЕТИС))
	|;
	|
	|";
КонецПроцедуры

Процедура ДоработатьЗапросПродукцииЕГАИС(Текст)
	
	Позиция = СтрНайти(Текст, "ВЫБРАТЬ");
	Текст = Лев(Текст, Позиция-1) + "ВЫБРАТЬ РАЗРЕШЕННЫЕ" + Сред(Текст, Позиция + СтрДлина("ВЫБРАТЬ"));
	Позиция = СтрНайти(Текст, "
	|ИЗ");
	Текст = Лев(Текст, Позиция -1)+ "
	|ПОМЕСТИТЬ ТоварыНакладнойПредварительно
	|"+ Сред(Текст, Позиция);
	
	Текст = Текст + 
	";
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ ТоварыНакладной
	|ИЗ
	|	ТоварыНакладнойПредварительно КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|		И СправочникНоменклатура.ОсобенностьУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.АлкогольнаяПродукция),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.Пиво),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПивоВПотребительскихУпаковках))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ТоварыНакладной.Номенклатура КАК Номенклатура,
	|	ТоварыНакладной.Характеристика КАК Характеристика,
	|	ТоварыНакладной.Номенклатура.ОбъемДАЛ КАК ОбъемДАЛ
	|ПОМЕСТИТЬ НоменклатураПереопределяемый
	|ИЗ
	|	ТоварыНакладной КАК ТоварыНакладной
	|;
	|
	|";
	
КонецПроцедуры

Процедура ДоработатьЗапросПродукцииИСМП(Текст)
	
	Позиция = СтрНайти(Текст, "ВЫБРАТЬ");
	Текст = Лев(Текст, Позиция-1) + "ВЫБРАТЬ РАЗРЕШЕННЫЕ" + Сред(Текст, Позиция + СтрДлина("ВЫБРАТЬ"));
	
	Позиция = СтрНайти(Текст, "
	|ИЗ");
	Текст = Лев(Текст, Позиция -1)+ "
	|ПОМЕСТИТЬ ТоварыНакладнойПредварительно
	|"+ Сред(Текст, Позиция)+"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Текст = Текст + 
	
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ ТоварыНакладной
	|ИЗ
	|	ТоварыНакладнойПредварительно КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|		И СправочникНоменклатура.ОсобенностьУчета В(
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЛегкаяПромышленность),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.Шины),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.Фотоаппараты),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.Духи),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.Велосипеды),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КреслаКоляски),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.АльтернативныйТабак),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.УпакованнаяВода),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияБезВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.Антисептики),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.БАДы),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.НикотиносодержащаяПродукция),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.Пиво),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродукцияИзНатуральногоМеха),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.СоковаяПродукция),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.БезалкогольноеПиво),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхБезВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ВетеринарныеПрепараты),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ИгрыИИгрушкиДляДетей),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.РадиоэлектроннаяПродукция),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ТитановаяМеталлопродукция),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияБезВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.РастительныеМасла),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ОптоволокноИОптоволоконнаяПродукция),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПарфюмерныеИКосметическиеСредстваИБытоваяХимия),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПечатнаяПродукция),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.СтроительныеМатериалы),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ОтопительныеПриборы),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПиротехническиеИзделияИСредстваПожарнойБезопасности),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.АлкогольнаяПродукцияДо9Процентов),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ТелефоныИНоутбуки),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КабельнаяПродукция),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.Бакалея),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МоторныеМасла),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.БезалкогольныеНапитки),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПивоВПотребительскихУпаковках),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ТехническиеСредстваРеабилитации),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МедицинскиеИзделия))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ТоварыНакладной.Номенклатура КАК Номенклатура,
	|	&ОпределениеВидаПродукции КАК ВидПродукции
	|ПОМЕСТИТЬ НоменклатураПереопределяемый
	|ИЗ
	|	ТоварыНакладной КАК ТоварыНакладной
	|;
	|
	|";
	
	ИнтеграцияИСУТ.ОпределитьВидПродукцииТекстаЗапроса(Текст,"ТоварыНакладной.Номенклатура");
	
КонецПроцедуры

Процедура ДоработатьЗапросПродукцииЗЕРНО(Текст)
	
	Позиция = СтрНайти(Текст, "ВЫБРАТЬ");
	Текст = Лев(Текст, Позиция-1) + "ВЫБРАТЬ РАЗРЕШЕННЫЕ" + Сред(Текст, Позиция + СтрДлина("ВЫБРАТЬ"));
	Позиция = СтрНайти(Текст, "
	|ИЗ");
	Текст = Лев(Текст, Позиция -1)+ "
	|ПОМЕСТИТЬ ТоварыНакладнойПредварительно
	|"+ Сред(Текст, Позиция);
	
	Текст = Текст + 
	";
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	Товары.КоличествоПриход КАК КоличествоПриход,
	|	Товары.КоличествоРасход КАК КоличествоРасход
	|ПОМЕСТИТЬ ТоварыНакладной
	|ИЗ
	|	ТоварыНакладнойПредварительно КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|		И СправочникНоменклатура.ОсобенностьУчета В(
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.Зерно),
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗерна))
	|;
	|
	|";
	
КонецПроцедуры

Процедура ДоработатьЗапросПродукцииСАТУРН(Текст, ПостфиксВременнойТаблицыИз = "", ПостфиксВременнойТаблицыПоместить = "")
	
	Позиция = СтрНайти(Текст, "ВЫБРАТЬ");
	Текст = Лев(Текст, Позиция - 1) + "ВЫБРАТЬ РАЗРЕШЕННЫЕ" + Сред(Текст, Позиция + СтрДлина("ВЫБРАТЬ"));
	Позиция = СтрНайти(Текст, "
	|ИЗ");
	
	ШаблонПоместить = "
	|ПОМЕСТИТЬ ТоварыНакладнойПредварительно%1
	|";
	
	Текст = Лев(Текст, Позиция - 1) +
		СтрШаблон(ШаблонПоместить, ПостфиксВременнойТаблицыИз) + Сред(Текст, Позиция);
	
	Текст = Текст + СтрШаблон(
	";
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	Товары.КоличествоПриход КАК КоличествоПриход,
	|	Товары.КоличествоРасход КАК КоличествоРасход
	|ПОМЕСТИТЬ ТоварыНакладной%1
	|ИЗ
	|	ТоварыНакладнойПредварительно%2 КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|		И СправочникНоменклатура.ОсобенностьУчета =
	|			ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН)
	|;
	|
	|", ПостфиксВременнойТаблицыПоместить, ПостфиксВременнойТаблицыИз);
	
КонецПроцедуры

Процедура РассчитатьИтоговоеКоличествоПоСкладскимДвижениям(Текст, ПостфиксВременнойТаблицыИз = "", ПостфиксВременнойТаблицыПоместить = "");
	
	Текст = Текст + СтрШаблон(
	"////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	СУММА(Товары.КоличествоПриход) КАК КоличествоПриход,
	|	СУММА(Товары.КоличествоРасход) КАК КоличествоРасход
	|ПОМЕСТИТЬ ТоварыНакладной%2
	|ИЗ
	|	ТоварыНакладной%1 КАК Товары
	|СГРУППИРОВАТЬ ПО
	|	Товары.Ссылка,
	|	Товары.Номенклатура,
	|	Товары.Характеристика
	|;
	|", ПостфиксВременнойТаблицыИз, ПостфиксВременнойТаблицыПоместить);
	
КонецПроцедуры

Процедура ОбъединитьЗапросыСАТУРН(Текст, ПостфиксВременнойТаблицы1 = "", ПостфиксВременнойТаблицы2 = "", ПостфиксВременнойТаблицы3 = "")
	
	Текст = Текст + СтрШаблон(
	"////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	Товары.КоличествоПриход КАК КоличествоПриход,
	|	Товары.КоличествоРасход КАК КоличествоРасход
	|ПОМЕСТИТЬ ТоварыНакладной
	|ИЗ
	|	ТоварыНакладной%1 КАК Товары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Ссылка,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия,
	|	Товары.КоличествоПриход - ЕСТЬNULL(ТоварыСкладские.КоличествоПриход, 0),
	|	Товары.КоличествоРасход - ЕСТЬNULL(ТоварыСкладские.КоличествоРасход, 0)
	|ИЗ
	|	ТоварыНакладной%3 КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыНакладной%2 КАК ТоварыСкладские
	|			ПО Товары.Ссылка = ТоварыСкладские.Ссылка
	|			И Товары.Номенклатура = ТоварыСкладские.Номенклатура
	|			И Товары.Характеристика = ТоварыСкладские.Характеристика
	|;
	|
	|", ПостфиксВременнойТаблицы1, ПостфиксВременнойТаблицы2, ПостфиксВременнойТаблицы3);
	
КонецПроцедуры

#КонецОбласти

#Область ПараметрыОтчета

Процедура УстановитьПараметрыОтчетаСАТУРН(ОтчетОбъект)
	
	ТипыДокументаОснованияВходящейНакладной = Новый Массив;
	ТипыДокументаОснованияВходящейНакладной.Добавить(Тип("ДокументСсылка.ПриобретениеТоваровУслуг"));
	ТипыДокументаОснованияВходящейНакладной.Добавить(Тип("ДокументСсылка.ПеремещениеТоваров"));
	ТипыДокументаОснованияВходящейНакладной.Добавить(Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями"));
	ТипыДокументаОснованияВходящейНакладной.Добавить(Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями"));
	ТипыДокументаОснованияВходящейНакладной.Добавить(Тип("ДокументСсылка.ОтгрузкаТоваровСХранения"));
	ТипыДокументаОснованияВходящейНакладной.Добавить(Тип("Неопределено"));
	
	
	ТипыДокументаОснованияИсходящейНакладной = Новый Массив;
	ТипыДокументаОснованияИсходящейНакладной.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	ТипыДокументаОснованияИсходящейНакладной.Добавить(Тип("ДокументСсылка.ВнутреннееПотребление"));
	ТипыДокументаОснованияИсходящейНакладной.Добавить(Тип("ДокументСсылка.ВозвратТоваровПоставщику"));
	ТипыДокументаОснованияИсходящейНакладной.Добавить(Тип("ДокументСсылка.ПеремещениеТоваров"));
	ТипыДокументаОснованияИсходящейНакладной.Добавить(Тип("ДокументСсылка.ПередачаТоваровХранителю"));
	ТипыДокументаОснованияИсходящейНакладной.Добавить(Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями"));
	ТипыДокументаОснованияИсходящейНакладной.Добавить(Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями"));
	ТипыДокументаОснованияИсходящейНакладной.Добавить(Тип("ДокументСсылка.ВыкупТоваровХранителем"));
	ТипыДокументаОснованияИсходящейНакладной.Добавить(Тип("Неопределено"));
	
	
	
	Настройки = ОтчетОбъект.КомпоновщикНастроек.ПолучитьНастройки();
	
	Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ТипыДокументаОснованияВходящейНакладной",  ТипыДокументаОснованияВходящейНакладной);
	Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ТипыДокументаОснованияИсходящейНакладной", ТипыДокументаОснованияИсходящейНакладной);
	
	ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
