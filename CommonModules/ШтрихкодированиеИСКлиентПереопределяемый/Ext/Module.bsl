#Область ПрограммныйИнтерфейс

// В процедуре нужно показать диалоговое окно для ввода штрихкода и передать полученные данные в описание оповещения.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - процедура, которую нужно вызвать после ввода штрихкода.
//  СтандартнаяОбработка - Булево - признак дальнейшей стандартной обработки события.
Процедура ПоказатьВводШтрихкода(Оповещение, СтандартнаяОбработка) Экспорт
	
	Возврат;
	
КонецПроцедуры

// В процедуре необходимо реализовать алгоритм обработки после получения данных по штрихкодам и попытки обработать эти данные.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма документа, в которой были обработаны штрихкоды,
//  ОбработанныеДанные - Структура - подготовленные ранее данные штрихкодов,
//  КэшированныеЗначения - Структура - используется механизмом обработки изменения реквизитов ТЧ.
Процедура ПослеОбработкиШтрихкодов(Форма, ОбработанныеДанные, КэшированныеЗначения) Экспорт
	
	//++ НЕ ГОСИС
	СтруктураДействий = ОбработанныеДанные;
	
	Если СтруктураДействий.НеизвестныеШтрихкоды.Количество() > 0 Тогда
		ДополнительныеПараметры = Новый Структура("Форма, КэшированныеЗначения", Форма, КэшированныеЗначения);
		ОписаниеОповещения = Новый ОписаниеОповещения("СопоставлениеНоменклатурыШтрихкодамЗавершение", ШтрихкодированиеИСКлиент, ДополнительныеПараметры);
		РаботаСНоменклатуройКлиент.ОткрытьФормуПодбораНоменклатурыПоШтрихкодам(СтруктураДействий, Форма, ОписаниеОповещения);
	КонецЕсли;
	
	//-- НЕ ГОСИС
	
	Возврат;
	
КонецПроцедуры

// В процедуре необходимо реализовать открытие формы, в которой будет возможность сопоставить неизвестные штрихкоды с номенклатурой.
//
// Параметры:
//  НеизвестныеШтрихкоды - Массив - Штрихкоды для сопоставления.
//  ФормаВладелец - ФормаКлиентскогоПриложения - Форма владелец.
//  ОповещениеОЗакрытии - ОписаниеОповещения - Оповещение, которое должно быть вызвано после сопоставления номенклатуры.
Процедура ОткрытьФормуПодбораНоменклатурыПоШтрихкодам(НеизвестныеШтрихкоды, ФормаВладелец = Неопределено, ОповещениеОЗакрытии = Неопределено) Экспорт
	
	//++ НЕ ГОСИС
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("НеизвестныеШтрихкоды", НеизвестныеШтрихкоды);
	ОткрытьФорму(
		"Обработка.РаботаСНоменклатурой.Форма.ПоискНоменклатурыПоШтрихкоду",
		СтруктураПараметров, ФормаВладелец, Новый УникальныйИдентификатор,,,
		ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

// В процедуре необходимо реализовать очистку коллекции штрихкодов, которые находятся в структуре кэшированные значения.
//
// Параметры:
//  ДанныеШтрихкодов - Соответствие - Данные штрихкодов.
//  КэшированныеЗначения - Структура - Содержит поля кэшируемых значений.
Процедура ОчиститьКэшированныеШтрихкоды(ДанныеШтрихкодов, КэшированныеЗначения) Экспорт
	
	//++ НЕ ГОСИС
	КэшированныеЗначения.Штрихкоды.Очистить();
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

// Вызывается после загрузки данных из ТСД. В процедуре нужно проанализировать полученные штрихкоды на предмет сканирования акцизной марки, а также
// обработать штрихкоды, не привязанные к номенклатуре.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма документа, в которой были обработаны штрихкоды,
//  ОбработанныеДанные - Структура - подготовленные ранее данные штрихкодов,
//  КэшированныеЗначения - Структура - используется механизмом обработки изменения реквизитов ТЧ.
Процедура ПослеОбработкиТаблицыТоваровПолученнойИзТСД(Форма, ОбработанныеДанные, КэшированныеЗначения) Экспорт
	
	//++ НЕ ГОСИС
	СтруктураДействий = ОбработанныеДанные;
	
	ШтрихкодированиеНоменклатурыКлиент.ОбработатьНеизвестныеШтрихкоды(СтруктураДействий, КэшированныеЗначения, Форма);
	
	Если СтруктураДействий.ТекущаяСтрока <> Неопределено Тогда
		Форма.Элементы.Товары.ТекущаяСтрока = СтруктураДействий.ТекущаяСтрока;
	КонецЕсли;
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

// В процедуре нужно реализовать подготовку данных для дальнейшей обработки штрихкодов, полученных из ТСД.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма документа, в которой происходит обработка,
//  ТаблицаТоваров - Массив - полученные данные из ТСД,
//  КэшированныеЗначения - Структура - используется механизмом обработки изменения реквизитов ТЧ,
//  ПараметрыЗаполнения - Структура - параметры заполнения (см. ИнтеграцияЕГАИСКлиентСервер.ПараметрыЗаполненияТабличнойЧасти()).
//  СтруктураДействий - Структура - подготовленные данные.
Процедура ПодготовитьДанныеДляОбработкиТаблицыТоваровПолученнойИзТСД(
	Форма, ТаблицаТоваров, КэшированныеЗначения, ПараметрыЗаполнения, СтруктураДействий) Экспорт

	//++ НЕ ГОСИС
	ПараметрыСканирования = ШтрихкодированиеОбщегоНазначенияИСКлиент.ПараметрыСканирования(Форма);
	Если ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.ДопустимВидПродукции(ПараметрыСканирования, ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная")) Тогда
		СтруктураДействий = Неопределено;
		ШтрихкодированиеНоменклатурыЕГАИСКлиентПереопределяемый.ПодготовитьДанныеДляОбработкиТаблицыТоваровПолученнойИзТСД(
			ЭтотОбъект,
			ТаблицаТоваров,
			КэшированныеЗначения,
			ПараметрыЗаполнения,
			СтруктураДействий);
	КонецЕсли;
	//-- НЕ ГОСИС
	Возврат;

КонецПроцедуры

// В данной процедуре нужно переопределить параметры записи журнала регистрации при отказе ввода кода маркировки.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, для которой происходит обработка штрихкода.
//  СтруктураСообщения - Структура:
//   * ИмяСобытия - Строка - Имя события журнала регистрации.
//   * Уровень - Строка - Уровень журнала регистрации. Возможные уровни: "Информация", "Ошибка", "Предупреждение",
//        "Примечание".
//   * Данные - Произвольный - Данные журнала регистрации.
//   * СсылкаНаОбъект - ЛюбаяСсылка - Ссылка на объект, на основании которого будут полученные метаданные для записи
//        в журнал регистрации.
//   * КодМаркировки - Строка - Введенный код маркировки. Если значение кода не заполнено - ввод кода маркировки отменен
//        по инициативе пользователя.
Процедура ПриОпределенииИнформацииОбОтказеВводаКодаМаркиДляЖурналаРегистрации(Форма, СтруктураСообщения) Экспорт
	
	//++ НЕ ГОСИС
	Если ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Форма, "Документ.ЧекККМ") Тогда
		СтруктураСообщения.СсылкаНаОбъект = Форма.Объект.Ссылка;
	КонецЕсли;
	//-- НЕ ГОСИС
	Возврат;

КонецПроцедуры

#КонецОбласти