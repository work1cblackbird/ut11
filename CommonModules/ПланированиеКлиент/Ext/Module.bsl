////////////////////////////////////////////////////////////////////////////////
// Общие процедуры и функции планирования
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Процедура изменения флага отмены строк плана
//
// Параметры:
//  Форма				 - ФормаКлиентскогоПриложения	 - Форма документа плана
//  Таблица				 - ТаблицаЗначений				 - Таблица в которой устанавливается флаг отмены строки
//  ИмяЭлементаТаблицы	 - Строка						 - Имя элемента формы таблицы
//  ОповещениеОЗакрытии	 - ОписаниеОповещения			 - Оповещение, выполняемое после закрытия.
//
Процедура ИзменитьФлагОтменыСтрокПлана(Форма, Таблица, ИмяЭлементаТаблицы, ОповещениеОЗакрытии = Неопределено) Экспорт 

	Элементы = Форма.Элементы;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("ИмяЭлементаТаблицы", ИмяЭлементаТаблицы);
	ДополнительныеПараметры.Вставить("Таблица", Таблица);
	Если ОповещениеОЗакрытии <> Неопределено Тогда
		ДополнительныеПараметры.Вставить("ОповещениеОЗакрытии", ОповещениеОЗакрытии);
	КонецЕсли; 
	
	Если Форма.Объект.КроссТаблица Тогда
		
		Если Элементы[ИмяЭлементаТаблицы].ВыделенныеСтроки.Количество() = 0 Тогда
			ПоказатьОповещениеПользователя(НСтр("ru = 'Выделите строки в списке'"));
			Возврат;
		КонецЕсли;
		
		АктивныеПериоды = Форма.Периоды.НайтиСтроки(Новый Структура("Активная", Истина)); // см. Планирование.ТаблицаПериоды
		Если АктивныеПериоды.Количество() = 0  Тогда
			Возврат;
		КонецЕсли;
		
		Если АктивныеПериоды.Количество() > 1  Тогда
			
			ВыбранПериод = Ложь;
			ТекущийЭлемент = Элементы[ИмяЭлементаТаблицы].ТекущийЭлемент; // ПолеФормы - 
			ТекущийЭлементИмя = ТекущийЭлемент.Имя;
			Если СтрНайти(ТекущийЭлементИмя, ИмяЭлементаТаблицы + "Количество_") > 0
				ИЛИ СтрНайти(ТекущийЭлементИмя, ИмяЭлементаТаблицы + "Комментарий_") > 0
				ИЛИ СтрНайти(ТекущийЭлементИмя, ИмяЭлементаТаблицы + "Цена_") > 0
				ИЛИ СтрНайти(ТекущийЭлементИмя, ИмяЭлементаТаблицы + "Сумма_") > 0 Тогда
				ВыбранПериод = Истина;
			КонецЕсли;
			
			Если ВыбранПериод Тогда
				Для каждого Период Из АктивныеПериоды Цикл
					Если ТекущийЭлементИмя = ИмяЭлементаТаблицы + "Количество_"+Период.ИмяКолонки
						ИЛИ ТекущийЭлементИмя = ИмяЭлементаТаблицы + "КартинкаКомментарий_"+Период.ИмяКолонки
						ИЛИ ТекущийЭлементИмя = ИмяЭлементаТаблицы + "Цена_"+Период.ИмяКолонки
						ИЛИ ТекущийЭлементИмя = ИмяЭлементаТаблицы + "Сумма_"+Период.ИмяКолонки Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
			Иначе
				Период = АктивныеПериоды[0];
			КонецЕсли; 
			
			Если Элементы[ИмяЭлементаТаблицы].ТекущиеДанные["Отменено_"+Период.ИмяКолонки] Тогда
				ТекстВопроса = НСтр("ru='Снять пометку отмены ячеек плана для всех периодов?'");
			Иначе
				ТекстВопроса = НСтр("ru='Установить пометку отмены ячеек плана для всех периодов?'");
			КонецЕсли; 
			
			ДополнительныеПараметры.Вставить("ИмяКолонки", Период.ИмяКолонки);
			ДополнительныеПараметры.Вставить("Отменено",   НЕ Элементы[ИмяЭлементаТаблицы].ТекущиеДанные["Отменено_"+Период.ИмяКолонки]);
			
			Кнопки = Новый СписокЗначений;
			Кнопки.Добавить(КодВозвратаДиалога.Да,     НСтр("ru='Да, для всех'"));
			Кнопки.Добавить(КодВозвратаДиалога.Нет,    НСтр("ru='Только'") + " " + Период.Заголовок);
			Кнопки.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru='Отмена'"));
			
			Оповещение = Новый ОписаниеОповещения("ИзменитьФлагОтменыСтрокПланаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			
			ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки);
		Иначе
		
			Период = АктивныеПериоды[0];
			
			ДополнительныеПараметры.Вставить("ИмяКолонки", Период.ИмяКолонки);
			ДополнительныеПараметры.Вставить("Отменено",   НЕ Элементы[ИмяЭлементаТаблицы].ТекущиеДанные["Отменено_"+Период.ИмяКолонки]);
			ИзменитьФлагОтменыСтрокПланаЗавершение(КодВозвратаДиалога.Нет, ДополнительныеПараметры);
		
		КонецЕсли; 
	Иначе
		
		Если Элементы[ИмяЭлементаТаблицы].ВыделенныеСтроки.Количество() = 0 Тогда
			ПоказатьОповещениеПользователя(НСтр("ru = 'Выделите строки в списке'"));
			Возврат;
		КонецЕсли;
		
		ДополнительныеПараметры.Вставить("Отменено", НЕ Элементы[ИмяЭлементаТаблицы].ТекущиеДанные.Отменено);
		ИзменитьФлагОтменыСтрокПланаЗавершение(КодВозвратаДиалога.Да, ДополнительныеПараметры);
		
	КонецЕсли;

КонецПроцедуры

// Процедура - Показать вопрос при изменении сценария
//
// Параметры:
//  Форма				 - ФормаКлиентскогоПриложения				 - Форма документа плана
//  ОповещениеОЗакрытии	 - ОписаниеОповещения			 - Оповещение, выполняемое после закрытия.
Процедура ПоказатьВопросПриИзмененииСценария(Форма, ОповещениеОЗакрытии = Неопределено) Экспорт 
	
	Если ЗначениеЗаполнено(Форма["РеквизитыДоИзменения"].Сценарий) Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Форма", Форма);
		Если ОповещениеОЗакрытии <> Неопределено Тогда
			ДополнительныеПараметры.Вставить("ОповещениеОЗакрытии", ОповещениеОЗакрытии);
		КонецЕсли; 
		
		Оповещение = Новый ОписаниеОповещения(
			"ПоказатьВопросПриИзмененииСценарияЗавершение", 
			ЭтотОбъект, 
			ДополнительныеПараметры);
		
		ТекстВопроса = НСтр("ru='Сценарий изменен. Правило заполнения будет обновлено. Обновить?'");
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Обновить'"));
		Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Отменить изменение сценария'"));
		ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки);
		
		
	ИначеЕсли ОповещениеОЗакрытии <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОповещениеОЗакрытии, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

// Процедура - Показать вопрос при изменении вид плана
//
// Параметры:
//  Форма				 - ФормаКлиентскогоПриложения				 - Форма документа плана
//  ОповещениеОЗакрытии	 - ОписаниеОповещения			 - Оповещение, выполняемое после закрытия.
Процедура ПоказатьВопросПриИзмененииВидПлана(Форма, ОповещениеОЗакрытии = Неопределено) Экспорт 
	
	Если ЗначениеЗаполнено(Форма["РеквизитыДоИзменения"].Сценарий) Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Форма", Форма);
		Если ОповещениеОЗакрытии <> Неопределено Тогда
			ДополнительныеПараметры.Вставить("ОповещениеОЗакрытии", ОповещениеОЗакрытии);
		КонецЕсли; 
		
		Оповещение = Новый ОписаниеОповещения(
			"ПоказатьВопросПриИзмененииВидПланаЗавершение", 
			ЭтотОбъект, 
			ДополнительныеПараметры);
		
		ТекстВопроса = НСтр("ru='Вид плана изменен. Правило заполнения будет обновлено. Обновить?'");
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Обновить'"));
		Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Отменить изменение вида плана'"));
		ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки);
		
		
	ИначеЕсли ОповещениеОЗакрытии <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОповещениеОЗакрытии, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

// Процедура - Изменить формулу на процент
//
// Параметры:
//  Форма				 - ФормаКлиентскогоПриложения	 - Форма документа плана
//  Таблица				 - ТаблицаЗначений				 - Таблица в которой устанавливается формула
//  ИмяЭлементаТаблицы	 - Строка						 - Имя элемента формы таблицы
//  ОповещениеОЗакрытии	 - ОписаниеОповещения			 - Оповещение, выполняемое после закрытия.
//
Процедура ИзменитьФормулуНаПроцент(Форма, Таблица, ИмяЭлементаТаблицы, ОповещениеОЗакрытии = Неопределено) Экспорт 

	Элементы = Форма.Элементы;
	ВыделенныеСтроки = Элементы[ИмяЭлементаТаблицы].ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество() = 0  Тогда
	
		ПоказатьПредупреждение(, НСтр("ru = 'Необходимо выделить строки в списке!'"));
		Возврат;
	
	КонецЕсли; 
	
	АктивныеПериоды = Форма.Периоды.НайтиСтроки(Новый Структура("Активная", Истина)); // см. Планирование.ТаблицаПериоды
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("ИмяЭлементаТаблицы", ИмяЭлементаТаблицы);
	ДополнительныеПараметры.Вставить("Таблица", Таблица);
	Если ОповещениеОЗакрытии <> Неопределено Тогда
		ДополнительныеПараметры.Вставить("ОповещениеОЗакрытии", ОповещениеОЗакрытии);
	КонецЕсли; 
	
	Если Форма.Объект.КроссТаблица И АктивныеПериоды.Количество() > 1 Тогда
		
		ТекущийЭлемент = Элементы[ИмяЭлементаТаблицы].ТекущийЭлемент; // ПолеФормы - 
		ТекущийЭлементИмя = ТекущийЭлемент.Имя;
		
		Если СтрНайти(ТекущийЭлементИмя, ИмяЭлементаТаблицы + "Количество_") = 0 Тогда
			Период = АктивныеПериоды[0];
		Иначе
			Для каждого Период Из АктивныеПериоды Цикл
				Если ТекущийЭлементИмя = ИмяЭлементаТаблицы + "Количество_"+Период.ИмяКолонки Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		 
		ТекстВопроса = НСтр("ru='Установить формулу для всех периодов?'");
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да,     НСтр("ru='Да, для всех'"));
		Кнопки.Добавить(КодВозвратаДиалога.Нет,    НСтр("ru='Только'") + " " + Период.Заголовок);
		Кнопки.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru='Отмена'"));
		
		ДополнительныеПараметры.Вставить("ИмяКолонки",Период.ИмяКолонки);
		
		Оповещение = Новый ОписаниеОповещения("ВвестиПроцентИИзменитьФормулу", ЭтотОбъект, ДополнительныеПараметры);
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки);
		
	ИначеЕсли Форма.Объект.КроссТаблица И АктивныеПериоды.Количество() = 1 Тогда
		
		Период = АктивныеПериоды[0];
		ДополнительныеПараметры.Вставить("ИмяКолонки",Период.ИмяКолонки);
		ВвестиПроцентИИзменитьФормулу(КодВозвратаДиалога.Нет, ДополнительныеПараметры);
		
	Иначе
		
		ДополнительныеПараметры.Вставить(
			"ИмяКолонки",
			?(Таблица[0].Свойство("КоличествоУпаковок"), "КоличествоУпаковок", "Количество"));
		
		ВвестиПроцентИИзменитьФормулу(КодВозвратаДиалога.Нет, ДополнительныеПараметры);
		
	КонецЕсли; 

КонецПроцедуры

// Процедура - Округлить формулу
//
// Параметры:
//  Форма				 - ФормаКлиентскогоПриложения - Форма документа плана
//  Таблица				 - ДанныеФормыКоллекция		  - Таблица в которой устанавливается формула
//  ИмяЭлементаТаблицы	 - Строка					  - Имя элемента формы таблицы
//  ОповещениеОЗакрытии	 - ОписаниеОповещения		  - Оповещение, выполняемое после закрытия.
//
Процедура ОкруглитьФормулу(Форма, Таблица, ИмяЭлементаТаблицы, ОповещениеОЗакрытии = Неопределено) Экспорт 

	Элементы = Форма.Элементы;
	ВыделенныеСтроки = Элементы[ИмяЭлементаТаблицы].ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество() = 0  Тогда
	
		ПоказатьПредупреждение(, НСтр("ru = 'Необходимо выделить строки в списке!'"));
		Возврат;
	
	КонецЕсли; 
	
	АктивныеПериоды = Форма.Периоды.НайтиСтроки(Новый Структура("Активная", Истина)); // см. Планирование.ТаблицаПериоды
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("ИмяЭлементаТаблицы", ИмяЭлементаТаблицы);
	ДополнительныеПараметры.Вставить("Таблица", Таблица);
	Если ОповещениеОЗакрытии <> Неопределено Тогда
		ДополнительныеПараметры.Вставить("ОповещениеОЗакрытии", ОповещениеОЗакрытии);
	КонецЕсли; 
	
	Если Форма.Объект.КроссТаблица И АктивныеПериоды.Количество() > 1 Тогда
	
		ТекущийЭлемент = Элементы[ИмяЭлементаТаблицы].ТекущийЭлемент; // ПолеФормы - 
		ТекущийЭлементИмя = ТекущийЭлемент.Имя;
		
		Если СтрНайти(ТекущийЭлементИмя, ИмяЭлементаТаблицы + "Количество_") = 0 Тогда
			Период = АктивныеПериоды[0];
		Иначе
			Для каждого Период Из АктивныеПериоды Цикл
				Если ТекущийЭлементИмя = ИмяЭлементаТаблицы + "Количество_"+Период.ИмяКолонки Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		 
		ТекстВопроса = НСтр("ru='Установить формулу для всех периодов?'");
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да,     НСтр("ru='Да, для всех'"));
		Кнопки.Добавить(КодВозвратаДиалога.Нет,    НСтр("ru='Только'") + " " + Период.Заголовок);
		Кнопки.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru='Отмена'"));
		
		ДополнительныеПараметры.Вставить("ИмяКолонки",Период.ИмяКолонки);
		
		Оповещение = Новый ОписаниеОповещения("ВыбратьТочностьОкругленияИИзменитьФормулу", ЭтотОбъект, ДополнительныеПараметры);
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки);
		
	ИначеЕсли Форма.Объект.КроссТаблица И АктивныеПериоды.Количество() = 1 Тогда
		
		Период = АктивныеПериоды[0];
		ДополнительныеПараметры.Вставить("ИмяКолонки",Период.ИмяКолонки);
		ВыбратьТочностьОкругленияИИзменитьФормулу(КодВозвратаДиалога.Нет, ДополнительныеПараметры);
		
	Иначе
		
		ДополнительныеПараметры.Вставить(
			"ИмяКолонки",
			?(Таблица[0].Свойство("КоличествоУпаковок"), "КоличествоУпаковок", "Количество"));
		
		ВыбратьТочностьОкругленияИИзменитьФормулу(КодВозвратаДиалога.Нет, ДополнительныеПараметры);
		
	КонецЕсли; 

КонецПроцедуры

// Процедура - При изменении периода плана
//
// Параметры:
//  Форма				 - ФормаКлиентскогоПриложения	 - Форма документа плана:
//    * Объект - ДокументОбъект.ПланЗакупок - 
// 	          - ДокументОбъект.ПланОстатков -
// 	          - ДокументОбъект.ПланПродаж -
// 	          - ДокументОбъект.ПланПродажПоКатегориям -
// 	          - ДокументОбъект.ПланПроизводства -
// 	          - ДокументОбъект.ПланСборкиРазборки -
//  ИмяКроссТаблицы		 - Строка						 - Имя элемента формы таблицы
//  ОповещениеОбновленияИнтерфейса	 - ОписаниеОповещения			 - Оповещение, выполняемое после закрытия.
Процедура ПриИзмененииПериодаПлана(Форма, Знач ИмяКроссТаблицы, ОповещениеОбновленияИнтерфейса) Экспорт 
	
	Объект = Форма.Объект;
	ТребуетсяПересчетОкончанияПериода = ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ПланОстатков");
	ТекущаяДатаСеанса = ОбщегоНазначенияКлиент.ДатаСеанса();
	ПланированиеКлиентСервер.УстановитьНачалоОкончаниеПериодаПлана(Объект.Периодичность, Объект.НачалоПериода, Объект.ОкончаниеПериода, ТекущаяДатаСеанса, , ТребуетсяПересчетОкончанияПериода);
	
	ПревышеноКоличествоПериодов = Ложь;
	
	Если Объект.Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Год")
		ИЛИ Объект.Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Полугодие") Тогда
		Если Объект.ОкончаниеПериода > ДобавитьМесяц(Объект.НачалоПериода, 120) Тогда // 10 лет
			ПревышеноКоличествоПериодов = Истина;
			СтрокаПериод = НСтр("ru='10 лет'");
		КонецЕсли;
	ИначеЕсли Объект.Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Квартал")
		ИЛИ Объект.Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Месяц") Тогда
		Если Объект.ОкончаниеПериода > ДобавитьМесяц(Объект.НачалоПериода, 60) Тогда // 5 лет
			ПревышеноКоличествоПериодов = Истина;
			СтрокаПериод = НСтр("ru='5 лет'");
		КонецЕсли;
	ИначеЕсли Объект.Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Декада")
		ИЛИ Объект.Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Неделя") Тогда
		Если Объект.ОкончаниеПериода > Объект.НачалоПериода + 86400 * 365 * 2 Тогда // 2 года
			ПревышеноКоличествоПериодов = Истина;
			СтрокаПериод = НСтр("ru='2 года'");
		КонецЕсли;
	ИначеЕсли Объект.Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.День") Тогда
		Если Объект.ОкончаниеПериода > Объект.НачалоПериода + 86400 * 365 Тогда // 1 год
			ПревышеноКоличествоПериодов = Истина;
			СтрокаПериод = НСтр("ru='1 год'");
		КонецЕсли;
	КонецЕсли;
	
	Если ПревышеноКоличествоПериодов Тогда
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Продолжить'"));
		Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Отменить изменение периода'"));
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Превышено рекомендуемое количество периодов (%1).
			|Это может замедлить работу системы.
			|Продолжить?'"), СтрокаПериод);
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Форма", Форма);
		ДополнительныеПараметры.Вставить("ОповещениеОбновленияИнтерфейса", ОповещениеОбновленияИнтерфейса);
		Оповещение = Новый ОписаниеОповещения("ПриИзмененииПериодаПланаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки);
		Возврат;
	КонецЕсли;
	
	Если ПланированиеКлиентСервер.НеобходимоОбновитьИнтерфейс(Объект, Форма, "РеквизитыДоИзменения") Тогда
		
		КроссТаблица = Форма[ИмяКроссТаблицы];
		Если Объект.КроссТаблица
			И КроссТаблица.Количество() > 0
			И Форма.Периоды.НайтиСтроки(Новый Структура("Активная", Истина)).Количество() > 0 Тогда
			ЗадаватьВопрос = Ложь;
			
			Если Форма["РеквизитыДоИзменения"].НачалоПериода <> Объект.НачалоПериода 
				И Объект.НачалоПериода > Форма["РеквизитыДоИзменения"].НачалоПериода Тогда
				ЗадаватьВопрос = Истина;
			КонецЕсли; 
			
			Если Форма["РеквизитыДоИзменения"].ОкончаниеПериода <> Объект.ОкончаниеПериода 
				И Объект.ОкончаниеПериода < Форма["РеквизитыДоИзменения"].ОкончаниеПериода Тогда
				ЗадаватьВопрос = Истина;
			КонецЕсли;
			
			Если ЗадаватьВопрос Тогда
				Кнопки = Новый СписокЗначений;
				Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Удалить периоды'"));
				Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Отменить изменение периода'"));
				ТекстВопроса = НСтр("ru='Изменение периода плана приведет к удалению данных в некоторых периодах. Удалить периоды?'");
				
				ДополнительныеПараметры = Новый Структура;
				ДополнительныеПараметры.Вставить("Форма", Форма);
				ДополнительныеПараметры.Вставить("ОповещениеОбновленияИнтерфейса", ОповещениеОбновленияИнтерфейса);
				Оповещение = Новый ОписаниеОповещения("ПриИзмененииПериодаПланаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
				
				ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки);
				Возврат;
				
			КонецЕсли;
			
		КонецЕсли;
		
		ВыполнитьОбработкуОповещения(ОповещениеОбновленияИнтерфейса);
		
	КонецЕсли;
	
КонецПроцедуры

// Вычисление по формуле
//
// Параметры:
//  ИсходнаяФормула	 - Строка					 - Значение исходной формулы
//  ТекущиеДанные	 - ДанныеФормыЭлементКоллекции	 - Значение текущих данных табличной части
//  Периоды			 - ДанныеФормыКоллекция			 - Значения периодов
//  Период			 - ДанныеФормыЭлементКоллекции	 - Значение текущего периода из таблицы периодов
//  Представление	 - Строка						 - Значение пользовательского представления формулы.
//  СерверныйВызов - Булево
// 
// Возвращаемое значение:
//  Структура - Возвращает структуру с новыми параметрами представления формулы.
//
Функция ВычислитьПоФормуле(Знач ИсходнаяФормула, Знач ТекущиеДанные, Знач Периоды, Знач Период, Знач Представление = Неопределено, СерверныйВызов = Ложь) Экспорт
	
	РасчетнаяФормула = ИсходнаяФормула;
	ВыводитьПромежуточныеВычисления = Ложь;
	Постфикс = Неопределено;
	
	Параметры = Новый Структура();
	Параметры.Вставить("Период", Период);
	Параметры.Вставить("Периоды", Периоды);
	Параметры.Вставить("ТекущиеДанные", ТекущиеДанные);
	
	Если Период <> Неопределено Тогда
		Постфикс = Период.ИмяКолонки;
	КонецЕсли;
	
	МассивОперандов = ПланированиеКлиентСервер.ОперандыТекстовойФормулы(РасчетнаяФормула);
	
	Для каждого Операнд Из МассивОперандов Цикл
		
		Если Постфикс <> Неопределено Тогда
			ИмяОперанда = Операнд + "_" + Постфикс;
		Иначе
			ИмяОперанда = Операнд;
		КонецЕсли;
		
		Если Не ТекущиеДанные.Свойство(ИмяОперанда) Тогда
			Продолжить;
		КонецЕсли;
		
		ЗначениеОперанда = ТекущиеДанные[ИмяОперанда];
		Если НЕ ВыводитьПромежуточныеВычисления Тогда
			ВыводитьПромежуточныеВычисления = НЕ ПустаяСтрока(СтрЗаменить(РасчетнаяФормула, "["+Операнд+"]", ""));
		КонецЕсли; 
		РасчетнаяФормула = СтрЗаменить(РасчетнаяФормула, "["+Операнд+"]", Формат(ЗначениеОперанда, "ЧРД=.; ЧН=0; ЧГ=0"));
		
	КонецЦикла;
	
	РасчетнаяФормула = ПланированиеКлиентСервер.ДобавитьПараметрыКФункциям(РасчетнаяФормула, "Параметры");
	
	Попытка
		РезультатВычисления = Формат(Вычислить(РасчетнаяФормула),"ЧЦ=15; ЧДЦ=3; ЧН=0");
	Исключение
		РезультатВычисления = 0;
	КонецПопытки;
	
	РасчетнаяФормула = ПланированиеКлиентСервер.УдалитьПараметрыВФункциях(РасчетнаяФормула, "Параметры");
	
	Если ЗначениеЗаполнено(ИсходнаяФормула) И ИсходнаяФормула <> ПланированиеКлиентСервер.ТекстУстановкиНовойФормулы() Тогда
		Вычисление = ИсходнаяФормула + ?(ВыводитьПромежуточныеВычисления, " = " + РасчетнаяФормула, "") + " = " + РезультатВычисления;
	Иначе
		Вычисление = ПланированиеКлиентСервер.ТекстУстановкиНовойФормулы();
	КонецЕсли;
	
	Если ИсходнаяФормула <> ПланированиеКлиентСервер.ТекстУстановкиНовойФормулы() Тогда
		Представление = ?(ЗначениеЗаполнено(Представление),Представление,ИсходнаяФормула);
	Иначе
		Представление = "";
	КонецЕсли;
	
	Возврат Новый Структура("Вычисление,Результат,Представление", Вычисление,РезультатВычисления,Представление);

КонецФункции

// Сформировать расшифровку.
// 
// Параметры:
//  СтрокаТЧ - ДанныеФормыЭлементКоллекции -
//  ПутьКРасшифровке - Строка
// 
// Возвращаемое значение:
//  Строка, ФорматированнаяСтрока - Сформировать расшифровку
Функция СформироватьРасшифровку(СтрокаТЧ, ПутьКРасшифровке = "Расшифровка") Экспорт
	
	Если НЕ ЗначениеЗаполнено(СтрокаТЧ[ПутьКРасшифровке]) Тогда
		Возврат "";
	КонецЕсли;
	
	Если СтрокаТЧ = Неопределено Тогда
		Возврат НСтр("ru = 'Строка не выбрана'");
	ИначеЕсли СтрокаТЧ.Количество = 0 Тогда
		Возврат "";
	Иначе
		
		Если СтрокаТЧ.Свойство("Номенклатура") Тогда
			
			Если НЕ СтрокаТЧ.Характеристика.Пустая() Тогда
				
				Возврат СтроковыеФункцииКлиент.ФорматированнаяСтрока(НСтр("ru = '<a href=""%1"">%2</a>, <a href=""%3"">%4</a>: %5'"),
					ПолучитьНавигационнуюСсылку(СтрокаТЧ.Номенклатура),
					Строка(СтрокаТЧ.Номенклатура),
					ПолучитьНавигационнуюСсылку(СтрокаТЧ.Характеристика),
					Строка(СтрокаТЧ.Характеристика),
					СтрокаТЧ[ПутьКРасшифровке]);
					
			КонецЕсли;
			
			Возврат СтроковыеФункцииКлиент.ФорматированнаяСтрока(НСтр("ru = '<a href=""%1"">%2</a>: %3'"),
					ПолучитьНавигационнуюСсылку(СтрокаТЧ.Номенклатура),
					Строка(СтрокаТЧ.Номенклатура),
					СтрокаТЧ[ПутьКРасшифровке]);
				
		ИначеЕсли СтрокаТЧ.Свойство("ТоварнаяКатегория") Тогда
			
			Возврат СтроковыеФункцииКлиент.ФорматированнаяСтрока(НСтр("ru = '<a href=""%1"">%2</a>: %3'"),
					ПолучитьНавигационнуюСсылку(СтрокаТЧ.ТоварнаяКатегория),
					Строка(СтрокаТЧ.ТоварнаяКатегория),
					СтрокаТЧ[ПутьКРасшифровке]);
				
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ФункцииИзОбщегоМодуля() Экспорт
	
	Описания = РаботаСФормуламиКлиентСервер.ФункцииОбщегоМодуля();
	
	ОписаниеФункции = РаботаСФормуламиКлиентСервер.ОписаниеФункцииОбщегоМодуля();
	ОписаниеФункции.Идентификатор = "НомерТекущейКолонки";
	ОписаниеФункции.ПолныйПуть = "ПланированиеКлиентСервер.НомерТекущейКолонки";
	ОписаниеФункции.Представление = НСтр("ru = 'Номер текущей колонки'");
	ОписаниеФункции.КонструкцияДляВставки = "НомерТекущейКолонки()";
	ОписаниеФункции.Пояснение = НСтр("ru = 'Вычисление номера текущей колонки'");
	Описания.Добавить(ОписаниеФункции);
	
	ОписаниеФункции = РаботаСФормуламиКлиентСервер.ОписаниеФункцииОбщегоМодуля();
	ОписаниеФункции.Идентификатор = "ЗначениеКолонки";
	ОписаниеФункции.ПолныйПуть = "ПланированиеКлиентСервер.ЗначениеКолонки";
	ОписаниеФункции.Представление = НСтр("ru = 'Значение колонки'");
	ОписаниеФункции.КонструкцияДляВставки = "ЗначениеКолонки()";
	ОписаниеФункции.Пояснение = НСтр("ru = 'Вычисление значения колонки'");
	Описания.Добавить(ОписаниеФункции);
	
	Возврат Описания;
	
КонецФункции

// Параметры:
// 	Результат - Структура -
// 	ДополнительныеПараметры - Структура - состав:
// 	  * Форма - ФормаКлиентскогоПриложения - 
// 	ШаблонФормулы - Строка - Описание
Процедура ИзменитьФлагОтменыСтрокПланаЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат = Неопределено ИЛИ Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат
	КонецЕсли;
	
	Форма = ДополнительныеПараметры.Форма;
	ИмяЭлементаТаблицы = ДополнительныеПараметры.ИмяЭлементаТаблицы;
	Элементы = Форма.Элементы;
	
	Если Форма.Объект.КроссТаблица Тогда
		
		ВыделенныеСтроки = Элементы[ИмяЭлементаТаблицы].ВыделенныеСтроки;
		
		Если Результат = КодВозвратаДиалога.Да Тогда
		
			АктивныеПериоды = Форма.Периоды.НайтиСтроки(Новый Структура("Активная", Истина));
			Если АктивныеПериоды.Количество() = 0  Тогда
				Возврат;
			КонецЕсли;
			
			Для каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
				
				СтрокаТЧ = ДополнительныеПараметры.Таблица.НайтиПоИдентификатору(ВыделеннаяСтрока);
				Для каждого Период Из АктивныеПериоды Цикл
					СтрокаТЧ["Отменено_"+Период.ИмяКолонки] = ДополнительныеПараметры.Отменено;
				КонецЦикла;
				
			КонецЦикла; 
			
		Иначе
		
			Для каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
				
				СтрокаТЧ = ДополнительныеПараметры.Таблица.НайтиПоИдентификатору(ВыделеннаяСтрока);
				СтрокаТЧ["Отменено_"+ДополнительныеПараметры.ИмяКолонки] = ДополнительныеПараметры.Отменено;
				
			КонецЦикла; 
		
		КонецЕсли; 
	
	Иначе
	
		ВыделенныеСтроки = Элементы[ИмяЭлементаТаблицы].ВыделенныеСтроки;
		Для каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		
			СтрокаТЧ = ДополнительныеПараметры.Таблица.НайтиПоИдентификатору(ВыделеннаяСтрока);
			СтрокаТЧ.Отменено = ДополнительныеПараметры.Отменено;
		
		КонецЦикла; 
	
	КонецЕсли; 
	
	Если ДополнительныеПараметры.Свойство("ОповещениеОЗакрытии") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗакрытии, Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказатьВопросПриИзмененииСценарияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		ПланированиеКлиентСервер.ВосстановитьЗначенияИзПроверяемыхРеквизитов(
			ДополнительныеПараметры.Форма.Объект, 
			ДополнительныеПараметры.Форма, 
			"РеквизитыДоИзменения", 
			"Сценарий, ВидПлана");
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("ОповещениеОЗакрытии") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗакрытии, Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказатьВопросПриИзмененииВидПланаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		ПланированиеКлиентСервер.ВосстановитьЗначенияИзПроверяемыхРеквизитов(
			ДополнительныеПараметры.Форма.Объект, 
			ДополнительныеПараметры.Форма, 
			"РеквизитыДоИзменения", 
			"ВидПлана");
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("ОповещениеОЗакрытии") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗакрытии, Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВвестиПроцентИИзменитьФормулу(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ДополнительныеПараметры.Вставить("ИмяКолонки", "ВсеПериоды");
	ИначеЕсли Результат <> КодВозвратаДиалога.Нет Тогда
		Если ДополнительныеПараметры.Свойство("ОповещениеОЗакрытии") Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗакрытии, Результат);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ВвестиПроцентИИзменитьФормулуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ПоказатьВводЧисла(Оповещение, 0, НСтр("ru = 'Введите процент'"), 5, 2);
	
КонецПроцедуры

Процедура ВвестиПроцентИИзменитьФормулуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ Результат = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Процент = Формат(1 + Результат / 100 ,"ЧЦ=15; ЧДЦ=4; ЧРД=.");
	
	ШаблонФормулы = НСтр("ru = '(%ТекущаяФормула%) * %Процент%'");
	ШаблонФормулы = СтрЗаменить(ШаблонФормулы, "%Процент%", Процент);
	
	ИзменитьФормулуВыделенныхСтрокПоШаблону(Результат, ДополнительныеПараметры, ШаблонФормулы);
	
КонецПроцедуры

Процедура ВыбратьТочностьОкругленияИИзменитьФормулу(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ДополнительныеПараметры.Вставить("ИмяКолонки", "ВсеПериоды");
	ИначеЕсли Результат <> КодВозвратаДиалога.Нет Тогда
		Если ДополнительныеПараметры.Свойство("ОповещениеОЗакрытии") Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗакрытии, Результат);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	СписокЗначений = Новый СписокЗначений();
	СписокЗначений.Добавить( 3, "0,001");
	СписокЗначений.Добавить( 2, "0,01");
	СписокЗначений.Добавить( 1, "0,1");
	СписокЗначений.Добавить( 0, "1");
	СписокЗначений.Добавить(-1, "10");
	СписокЗначений.Добавить(-2, "100");
	СписокЗначений.Добавить(-3, "1000");
	
	
	Оповещение = Новый ОписаниеОповещения("ВыбратьТочностьОкругленияИИзменитьФормулуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	СписокЗначений.ПоказатьВыборЭлемента(Оповещение,НСтр("ru = 'Точность округления'"));
	
КонецПроцедуры

Процедура ВыбратьТочностьОкругленияИИзменитьФормулуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонФормулы = НСтр("ru = 'Окр((%ТекущаяФормула%), %ТочностьОкругления%)'");
	ШаблонФормулы = СтрЗаменить(ШаблонФормулы, "%ТочностьОкругления%", Результат.Значение);
	
	ИзменитьФормулуВыделенныхСтрокПоШаблону(Результат, ДополнительныеПараметры, ШаблонФормулы);
	
КонецПроцедуры

// Параметры:
// 	Результат - Структура -
// 	Параметры - Структура - состав:
// 	  * Форма - ФормаКлиентскогоПриложения - 
// 	ШаблонФормулы - Строка - Описание
Процедура ИзменитьФормулуВыделенныхСтрокПоШаблону(Результат, Параметры, ШаблонФормулы)
	
	Форма = Параметры.Форма;
	Элементы = Форма.Элементы;
	ИмяЭлементаТаблицы = Параметры.ИмяЭлементаТаблицы;
	Таблица = Параметры.Таблица;
	ИмяКолонки = Параметры.ИмяКолонки;
	
	ВыделенныеСтроки = Элементы[ИмяЭлементаТаблицы].ВыделенныеСтроки;
	
	Если Форма.Объект.КроссТаблица Тогда
		
		Для каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
			
			Если ИмяКолонки = "ВсеПериоды" Тогда
				Периоды = Форма.Периоды.НайтиСтроки(Новый Структура("Активная", Истина));
			Иначе
				Периоды = Форма.Периоды.НайтиСтроки(Новый Структура("Активная, ИмяКолонки", Истина, ИмяКолонки))
			КонецЕсли;
			
			Для каждого Период Из Периоды Цикл
				
				СтрокаТЧ = Таблица.НайтиПоИдентификатору(ВыделеннаяСтрока);
				Если СтрокаТЧ.Свойство("Полуфабрикат")
					И СтрокаТЧ.Полуфабрикат Тогда
					Продолжить;
				КонецЕсли;
				Если ЗначениеЗаполнено(СтрокаТЧ["Формула_" + Период.ИмяКолонки]) Тогда
					СтрокаТЧ["Формула_" + Период.ИмяКолонки] = СтрЗаменить(ШаблонФормулы, "%ТекущаяФормула%", СтрокаТЧ["Формула_" + Период.ИмяКолонки]);
				Иначе
					СтрокаТЧ["Формула_" + Период.ИмяКолонки] = Формат(СтрокаТЧ["Количество_" + Период.ИмяКолонки], "ЧРД=.; ЧН=0; ЧГ=0");
					СтрокаТЧ["Формула_" + Период.ИмяКолонки] = СтрЗаменить(ШаблонФормулы, "%ТекущаяФормула%", СтрокаТЧ["Формула_" + Период.ИмяКолонки]);
				КонецЕсли; 
				РезультатВычисления = ВычислитьПоФормуле(СтрокаТЧ["Формула_" + Период.ИмяКолонки], СтрокаТЧ, Периоды, Период);
				СтрокаТЧ["ФормулаВычисление_" + Период.ИмяКолонки] = РезультатВычисления.Вычисление;
				СтрокаТЧ["Количество_" + Период.ИмяКолонки]= РезультатВычисления.Результат;
				СтрокаТЧ["Отклонение_" + Период.ИмяКолонки]= 0;
				
			КонецЦикла;
			
		КонецЦикла; 

	Иначе
		
		Для каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
			
			СтрокаТЧ = Таблица.НайтиПоИдентификатору(ВыделеннаяСтрока);
			Если ЗначениеЗаполнено(СтрокаТЧ.Формула) Тогда
				СтрокаТЧ.Формула = СтрЗаменить(ШаблонФормулы, "%ТекущаяФормула%", СтрокаТЧ.Формула);
			Иначе
				СтрокаТЧ.Формула = Формат(СтрокаТЧ[ИмяКолонки], "ЧРД=.; ЧН=0; ЧГ=0");
				СтрокаТЧ.Формула = СтрЗаменить(ШаблонФормулы, "%ТекущаяФормула%", СтрокаТЧ.Формула);
			КонецЕсли; 
			РезультатВычисления = ВычислитьПоФормуле(СтрокаТЧ.Формула, СтрокаТЧ, Неопределено, Неопределено);
			СтрокаТЧ.ФормулаВычисление = РезультатВычисления.Вычисление;
			СтрокаТЧ[ИмяКолонки] = РезультатВычисления.Результат;
			СтрокаТЧ.Отклонение = 0;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Параметры.Свойство("ОповещениеОЗакрытии") Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеОЗакрытии, Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриИзмененииПериодаПланаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
	
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОбновленияИнтерфейса);
	
	Иначе
	
		ПланированиеКлиентСервер.ВосстановитьЗначенияИзПроверяемыхРеквизитов(
			ДополнительныеПараметры.Форма.Объект, 
			ДополнительныеПараметры.Форма, 
			"РеквизитыДоИзменения", 
			"НачалоПериода, ОкончаниеПериода");
	
	КонецЕсли; 
	
КонецПроцедуры


// Описание
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - 
// 	ИмяСтраницыТовары - Строка -
// 	ИмяТчТовары - Строка -
// 	ИмяТчТоварыПоПериодам - Строка -
Процедура СпозиционироватьсяПоСтроке(Форма, ИмяСтраницыТовары, ИмяТчТовары, ИмяТчТоварыПоПериодам) Экспорт
	
	Если ЗначениеЗаполнено(Форма.ТекущаяСтрокаПриОткрытии) Тогда
		Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы[ИмяСтраницыТовары];
		Если Форма.Объект.КроссТаблица Тогда
			СтрокиПоиска = Форма[ИмяТчТоварыПоПериодам].НайтиСтроки(Форма.ТекущаяСтрокаПриОткрытии);
			Если СтрокиПоиска.Количество()>0 Тогда
				ТоварыПоПериодам = Форма.Элементы[ИмяТчТоварыПоПериодам]; // ТаблицаФормы - 
				ТоварыПоПериодам.ТекущаяСтрока = СтрокиПоиска[0].ПолучитьИдентификатор();
			КонецЕсли;
		Иначе
			СтрокиПоиска = Форма.Объект[ИмяТчТовары].НайтиСтроки(Форма.ТекущаяСтрокаПриОткрытии);
			Если СтрокиПоиска.Количество()>0 Тогда
				Товары = Форма.Элементы[ИмяТчТоварыПоПериодам]; // ТаблицаФормы - 
				Товары.ТекущаяСтрока = СтрокиПоиска[0].ПолучитьИдентификатор();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ВыбранноеЗначение - СправочникСсылка.Склады, СправочникСсылка.Назначения, СправочникСсылка.Партнеры - значение для заполнения колонок
//  ДополнительныеПараметры - Структура:
//	*ВыделенныеСтроки - Массив из Число
//	*ТаблицаТовары - ТаблицаЗначений, ТабличнаяЧасть -
//	*Колонка - Строка
Процедура ЗаполнитьПоЗначениюЗавершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		
		ВыделенныеСтроки = ДополнительныеПараметры.ВыделенныеСтроки;
		Для Каждого ТекСтрока Из ВыделенныеСтроки Цикл
			
			СтрокаТаблицы = ДополнительныеПараметры.ТаблицаТовары.НайтиПоИдентификатору(ТекСтрока);
			
			Если ДополнительныеПараметры.Колонка = "Склад" И СтрокаТаблицы.Свойство("ТипНоменклатуры")
				И (СтрокаТаблицы.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга")
				ИЛИ СтрокаТаблицы.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Работа")) Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаТаблицы[ДополнительныеПараметры.Колонка] = ВыбранноеЗначение;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#Область ЗагрузкаДанныхИзФайла

// Параметры:
//  ДополнительныеПараметры - см. ПланированиеКлиент.ДополнительныеПараметрыЗагрузкиИзФайла
// 
// Возвращаемое значение:
//  Массив из см. ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета - Описание колонок макета для загрузки номенклатуры
Функция ОписаниеКолонокМакетаДляЗагрузкиНоменклатуры(ДополнительныеПараметры) Экспорт
	
	КроссТаблица = ДополнительныеПараметры.КроссТаблица;
	ТаблицаПериоды = ДополнительныеПараметры.Периоды;
	
	КолонкиМакета = Новый Массив;
	
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Штрихкод", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(200)), НСтр("ru = 'Штрихкод'"), 10);
	Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
	Колонка.Родитель = "Номенклатура";
	КолонкиМакета.Добавить(Колонка);
	
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Артикул", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50)), НСтр("ru = 'Артикул'"), 10);
	Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
	Колонка.Родитель = "Номенклатура";
	КолонкиМакета.Добавить(Колонка);
	
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("КодНоменклатуры", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(11)), НСтр("ru = 'Код номенклатуры'"), 15);
	Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
	Колонка.Родитель = "Номенклатура";
	КолонкиМакета.Добавить(Колонка);
	
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Номенклатура", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)), НСтр("ru = 'Номенклатура'"), 15);
	Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
	КолонкиМакета.Добавить(Колонка);
	
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Характеристика", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)), НСтр("ru = 'Характеристика'"), 15);
	Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
	КолонкиМакета.Добавить(Колонка);
	
	Если ДополнительныеПараметры.ИспользуютсяНазначения Тогда
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Назначение", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)), НСтр("ru = 'Назначение'"), 15);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
	КонецЕсли;
	
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("КодУпаковки", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(4)), НСтр("ru = 'Код упаковки'"), 12);
	Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
	Колонка.Родитель = "Упаковка";
	КолонкиМакета.Добавить(Колонка);
	
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Упаковка", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50)), НСтр("ru = 'Упаковка'"), 10);
	Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
	КолонкиМакета.Добавить(Колонка);
	
	Если НЕ КроссТаблица Тогда
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("КоличествоУпаковок", Новый ОписаниеТипов("Число",, Новый КвалификаторыЧисла(15, 3)), НСтр("ru = 'Количество'"), 10);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
	КонецЕсли;
	
	
	Если ДополнительныеПараметры.ИспользуютсяСклады Тогда
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Склад", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)), НСтр("ru = 'Склад'"), 15);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
	КонецЕсли;
	
	Если ДополнительныеПараметры.ИспользуютсяПартнеры Тогда
		Заголовок = ?(ДополнительныеПараметры.ТипПлана = "ПланЗакупок", НСтр("ru = 'Поставщик'"), НСтр("ru = 'Клиент'"));
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Партнер", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)), Заголовок, 15);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
	КонецЕсли;
	
	Если ДополнительныеПараметры.ИспользуютсяСоглашения Тогда
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Соглашение", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)), НСтр("ru = 'Соглашение'"), 15);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
	КонецЕсли;
	
	Если ДополнительныеПараметры.ИспользуютсяВидыЦен Тогда
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("ВидЦены", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)), НСтр("ru = 'Вид цены'"), 10);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
	КонецЕсли;
	
	Если НЕ КроссТаблица И ДополнительныеПараметры.ПланироватьПоСумме Тогда
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Цена", Новый ОписаниеТипов("Число",, Новый КвалификаторыЧисла(15, 3)), НСтр("ru = 'Цена'"), 10);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
		
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Сумма", Новый ОписаниеТипов("Число",, Новый КвалификаторыЧисла(15, 3)), НСтр("ru = 'Сумма'"), 10);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
	КонецЕсли;
	
	Если ДополнительныеПараметры.ТипПлана = "ПланСборкиРазборки" Тогда
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("ВариантКомплектации", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)), НСтр("ru = 'Комплектация'"), 15);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
		
		Если НЕ КроссТаблица Тогда
			Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("ДатаСборкиРазборки", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты()), НСтр("ru = 'Дата сборки (разборки)'"), 15);
			Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
			КолонкиМакета.Добавить(Колонка);
		КонецЕсли;
	КонецЕсли;
	
	
	Если НЕ КроссТаблица И ДополнительныеПараметры.ТипПлана = "ПланЗакупок" Тогда
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("ДатаПоступления", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты()), НСтр("ru = 'Дата поступления'"), 15);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
		
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("ДатаЗаказа", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты()), НСтр("ru = 'Дата заказа'"), 10);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
		
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("СрокИсполненияЗаказа", Новый ОписаниеТипов("Число",, Новый КвалификаторыЧисла(8, 0)), НСтр("ru = 'Срок исполнения'"), 13);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
	КонецЕсли;
	
	Если НЕ КроссТаблица И (ДополнительныеПараметры.ТипПлана = "ПланВнутреннихПотреблений"
		ИЛИ ДополнительныеПараметры.ТипПлана = "ПланПродаж") Тогда
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("ДатаОтгрузки", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты()), НСтр("ru = 'Дата отгрузки'"), 15);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
	КонецЕсли;
	
	Если КроссТаблица Тогда
		Для Каждого Период Из ТаблицаПериоды Цикл
			
			Если ДополнительныеПараметры.ПланироватьПоСумме Тогда
				ИмяКолонки = "КоличествоУпаковок_" + Период.ИмяКолонки;
				Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета(ИмяКолонки, Новый ОписаниеТипов("Число",, Новый КвалификаторыЧисла(15, 3)), НСтр("ru = 'Количество'") + " " + Период.Заголовок, 15);
				Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
				КолонкиМакета.Добавить(Колонка);
				
				ИмяКолонки = "Цена_" + Период.ИмяКолонки;
				Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета(ИмяКолонки, Новый ОписаниеТипов("Число",, Новый КвалификаторыЧисла(15, 3)), НСтр("ru = 'Цена'") + " " + Период.Заголовок, 15);
				Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
				КолонкиМакета.Добавить(Колонка);
				
				ИмяКолонки = "Сумма_" + Период.ИмяКолонки;
				Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета(ИмяКолонки, Новый ОписаниеТипов("Число",, Новый КвалификаторыЧисла(15, 3)), НСтр("ru = 'Сумма'") + " " + Период.Заголовок, 15);
				Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
				КолонкиМакета.Добавить(Колонка);
			Иначе
				ИмяКолонки = "КоличествоУпаковок_" + Период.ИмяКолонки;
				Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета(ИмяКолонки, Новый ОписаниеТипов("Число",, Новый КвалификаторыЧисла(15, 3)), Период.Заголовок, 10);
				Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
				КолонкиМакета.Добавить(Колонка);
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Комментарий", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)), НСтр("ru = 'Комментарий'"), 20);
	Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
	КолонкиМакета.Добавить(Колонка);
	
	Возврат КолонкиМакета;
	
КонецФункции

// Параметры:
//  ДополнительныеПараметры - см. ПланированиеКлиент.ДополнительныеПараметрыЗагрузкиИзФайла
// 
// Возвращаемое значение:
//  Массив из см. ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета - Описание колонок макета для загрузки номенклатуры
Функция ОписаниеКолонокМакетаДляЗагрузкиТоварныхКатегорий(ДополнительныеПараметры) Экспорт
	КолонкиМакета = Новый Массив;
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("ТоварнаяКатегория", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)), НСтр("ru = 'Товарная категория'"), 15);
	Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
	КолонкиМакета.Добавить(Колонка);
	
	Если ДополнительныеПараметры.ИспользуютсяНазначения Тогда
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Назначение", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)), НСтр("ru = 'Назначение'"), 15);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
	КонецЕсли;
	
	Если ДополнительныеПараметры.ИспользуетсяРасчетПоСкоростиПродаж Тогда
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("РейтингПродаж", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(100)), НСтр("ru = 'Рейтинг продаж'"), 15);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
	КонецЕсли;
	
	Если ДополнительныеПараметры.ИспользуютсяСклады Тогда
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Склад", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)), НСтр("ru = 'Склад'"), 15);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
	КонецЕсли;
	
	Если ДополнительныеПараметры.КроссТаблица Тогда
		Для Каждого Период Из ДополнительныеПараметры.Периоды Цикл
			ИмяКолонки = "Количество_" + Период.ИмяКолонки;
			Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета(ИмяКолонки, Новый ОписаниеТипов("Число",, Новый КвалификаторыЧисла(15, 3)), Период.Заголовок, 10);
			Колонка.Позиция = КолонкиМакета.ВГраница() + 1;;
			КолонкиМакета.Добавить(Колонка);
		КонецЦикла;
	Иначе
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Количество", Новый ОписаниеТипов("Число",, Новый КвалификаторыЧисла(15, 3)), НСтр("ru = 'Количество'"), 10);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
		
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("ДатаОтгрузки", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты()), НСтр("ru = 'Дата отгрузки'"), 15);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
	КонецЕсли;
	
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Комментарий", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)), НСтр("ru = 'Комментарий'"), 20);
	Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
	КолонкиМакета.Добавить(Колонка);
	
	Возврат КолонкиМакета;
КонецФункции

// Дополнительные параметры загрузки из файла.
// 
// Возвращаемое значение:
//  Структура - Дополнительные параметры загрузки из файла:
// * ТипПлана - Строка -
// * ИспользуютсяНазначения - Булево -
// * ИспользуютсяПартнеры - Булево -
// * ИспользуютсяВидыЦен - Булево -
// * ИспользуютсяСклады - Булево -
// * ИспользуютсяСоглашения - Булево -
// * ПланироватьПоСумме - Булево -
// * Поставщик - СправочникСсылка.Партнеры,Неопределено -
// * ИспользуютсяСпецификации - Булево -
// * ИспользуютсяДатыВыпуска - Булево -
// * ИспользуютсяДатыВыпускаПродукцииПолуфабриката - Булево -
// * ИспользуетсяРасчетПоСкоростиПродаж - Булево -
// * ДоступныеТипыНоменклатуры - Массив из ПеречислениеСсылка.ТипыНоменклатуры -
// * КроссТаблица - Булево -
// * Периоды - ДанныеФормыКоллекция -
Функция ДополнительныеПараметрыЗагрузкиИзФайла() Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ТипПлана",               "");
	ДополнительныеПараметры.Вставить("ИспользуютсяНазначения", Ложь);
	ДополнительныеПараметры.Вставить("ИспользуютсяПартнеры",   Ложь);
	ДополнительныеПараметры.Вставить("ИспользуютсяВидыЦен",    Ложь);
	ДополнительныеПараметры.Вставить("ИспользуютсяСклады",     Ложь);
	ДополнительныеПараметры.Вставить("ИспользуютсяСоглашения", Ложь);
	ДополнительныеПараметры.Вставить("ПланироватьПоСумме",     Ложь);
	ДополнительныеПараметры.Вставить("Партнер",              Неопределено);
	
	
	ДополнительныеПараметры.Вставить("ИспользуетсяРасчетПоСкоростиПродаж", Ложь);
	
	ДополнительныеПараметры.Вставить("ДоступныеТипыНоменклатуры", НоменклатураКлиентСервер.ОтборПоТоваруМногооборотнойТареУслугеРаботе(Ложь));
	
	ДополнительныеПараметры.Вставить("КроссТаблица", Ложь);
	ДополнительныеПараметры.Вставить("Периоды", Неопределено);
	
	Возврат ДополнительныеПараметры;
КонецФункции

#КонецОбласти

#КонецОбласти