#Область СлужебныйПрограммныйИнтерфейс

// Функция формирует список номенклатуры, по которым было движение в регистре:
// РегистрацияПрослеживаемыхТоваровСрезПоследних по первичному документу
//
// Параметры:
//  ПервичныйДокумент - ДокументСсылка - ссылка на первичный документ
//  КодТНВЭД -  - Код ТНВЭД для отбора
//  Период - Дата - дата, на которую необходимо получить список актуальных ТНВЭД
// 
// Возвращаемое значение:
//  Массив - массив Номенклатуры
//
Функция СписокНоменклатуры(ПервичныйДокумент, КодТНВЭД, Период) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПервичныйДокумент", ПервичныйДокумент);
	Запрос.УстановитьПараметр("КодТНВЭД", КодТНВЭД);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НоменклатураСправочник.Ссылка КАК Номенклатура
	|ИЗ
	|	РегистрСведений.РегистрацияПрослеживаемыхТоваров КАК РегистрацияПрослеживаемыхТоваровСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСправочник
	|		ПО РегистрацияПрослеживаемыхТоваровСрезПоследних.Номенклатура = НоменклатураСправочник.Ссылка
	|ГДЕ
	|	РегистрацияПрослеживаемыхТоваровСрезПоследних.Основание = &ПервичныйДокумент
	|	И НоменклатураСправочник.КодТНВЭД = &КодТНВЭД";
	
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаТоваров.ВыгрузитьКолонку("Номенклатура");
	
КонецФункции

// Возвращает структуру реквизитов документов 
// Параметры: 
//  Номенклатура - СправочникСсылка.Номенклатура - номенклатура
// 
// Возвращаемое значение:
//  Структура - КодТНВЭД - СправочникСсылка.КлассификаторТНВЭД
//            - ЕдиницаИзмерения - СправочникСсылка.КлассификаторЕдиницИзмерения
//
Функция ПараметрыНоменклатуры(Номенклатура) Экспорт
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура, "КодТНВЭД, ЕдиницаИзмерения");
	
КонецФункции

// Функция формирует список ТНВЭД, по которым было движение в регистре:
// РегистрацияПрослеживаемыхТоваровСрезПоследних по первичному документу
//
// Параметры:
//  ПервичныйДокумент - ДокументСсылка - ссылка на первичный документ
//  Ссылка - ДокументСсылка - Ссылка на документ в котором необходимо отобрать список ТНВЭД
//  Период - Дата - дата, на которую необходимо получить список актуальных ТНВЭД
// 
// Возвращаемое значение:
//  Массив - массив ТНВЭД
//
Функция СписокТНВЭД(ПервичныйДокумент, Ссылка, Период) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПервичныйДокумент", ПервичныйДокумент);
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		МоментСписания = Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая);
	Иначе
		МоментСписания = Период;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НоменклатураСправочник.КодТНВЭД КАК КодТНВЭД
	|ИЗ
	|	РегистрСведений.РегистрацияПрослеживаемыхТоваров КАК РегистрацияПрослеживаемыхТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСправочник
	|		ПО РегистрацияПрослеживаемыхТоваров.Номенклатура = НоменклатураСправочник.Ссылка
	|ГДЕ
	|	РегистрацияПрослеживаемыхТоваров.Основание = &ПервичныйДокумент
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураСправочник.КодТНВЭД";
	
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаТоваров.ВыгрузитьКолонку("КодТНВЭД");
	
КонецФункции

#КонецОбласти