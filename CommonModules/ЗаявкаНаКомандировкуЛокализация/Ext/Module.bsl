// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
//++ Локализация
	Если Не Объект.ЭтоНовый()
		И ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСоСмартвей") Тогда
		ИсходныеРеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Ссылка, "Статус, Проведен");
		Если ИсходныеРеквизитыДокумента.Статус = Перечисления.СтатусыЗаявокСотрудников.Согласована
			И ИсходныеРеквизитыДокумента.Проведен
			И (Объект.Статус = Перечисления.СтатусыЗаявокСотрудников.Отклонена
			Или Объект.Статус = Перечисления.СтатусыЗаявокСотрудников.Отозвана
			Или РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения) Тогда
			Объект.ДополнительныеСвойства.Вставить("ОтменитьЗаявкуВСмартвей", Истина);
		КонецЕсли;
	КонецЕсли;
//-- Локализация
	
	Возврат;
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
//++ Локализация
	Если Объект.ДополнительныеСвойства.Свойство("ОтменитьЗаявкуВСмартвей")
		И Объект.ДополнительныеСвойства.ОтменитьЗаявкуВСмартвей = Истина Тогда
		ОтменитьЗаявкуВСмартвей(Объект.Ссылка);
	КонецЕсли;
//-- Локализация
	
	Возврат;
	
КонецПроцедуры

//++ Локализация
Процедура ОтменитьЗаявкуВСмартвей(Ссылка)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Выполняется отмена заявки в Смартвей'");
	ПараметрыВыполнения.ОжидатьЗавершение           = 0;
	ПараметрыВыполнения.КлючФоновогоЗадания         = Строка(Ссылка.УникальныйИдентификатор());
	ПараметрыВыполнения.ЗапуститьВФоне              = Истина;
	
	ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, "ИнтеграцияСмартвей.ОтменитьЗаявку", Ссылка);

КонецПроцедуры
//-- Локализация
