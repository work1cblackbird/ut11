
#Область ПрограммныйИнтерфейс

// Возвращает доступные условия предоставления предоставления скидок (наценок)
//
// Возвращаемое значение:
//  Массив - Доступные условия предоставления скидок (наценок).
//
Функция ДоступныеУсловияПредоставленияСкидокНаценок() Экспорт
	
	ДоступныеУсловияПредоставления = Новый СписокЗначений;
	ДоступныеУсловияПредоставления.Добавить(Перечисления.УсловияПредоставленияСкидокНаценок.ЗаРазовыйОбъемПродаж);
	ДоступныеУсловияПредоставления.Добавить(Перечисления.УсловияПредоставленияСкидокНаценок.ЗаНакопленныйОбъемПродаж);
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКартыЛояльности") Тогда
		ДоступныеУсловияПредоставления.Добавить(Перечисления.УсловияПредоставленияСкидокНаценок.КартаЛояльностиНеЗарегистрирована);
		ДоступныеУсловияПредоставления.Добавить(Перечисления.УсловияПредоставленияСкидокНаценок.ЗаНаличиеКартыЛояльности);
	КонецЕсли;
	ДоступныеУсловияПредоставления.Добавить(Перечисления.УсловияПредоставленияСкидокНаценок.ЗаФормуОплаты);
	ДоступныеУсловияПредоставления.Добавить(Перечисления.УсловияПредоставленияСкидокНаценок.ЗаВремяПродажи);
	ДоступныеУсловияПредоставления.Добавить(Перечисления.УсловияПредоставленияСкидокНаценок.ЗаДеньРожденияКлиента);
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиОплаты") Тогда
		ДоступныеУсловияПредоставления.Добавить(Перечисления.УсловияПредоставленияСкидокНаценок.ЗаГрафикОплаты);
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГруппыПользователей") Тогда
		ДоступныеУсловияПредоставления.Добавить(Перечисления.УсловияПредоставленияСкидокНаценок.ОграничениеПоГруппеПользователей);
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСегментыПартнеров") Тогда
		ДоступныеУсловияПредоставления.Добавить(Перечисления.УсловияПредоставленияСкидокНаценок.ВхождениеПартнераВСегмент);
	КонецЕсли;
	
	Для Каждого ЭлементСписка Из ДоступныеУсловияПредоставления Цикл
		ЭлементСписка.Представление = Строка(ЭлементСписка.Значение);
	КонецЦикла;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеОтчетыИОбработки") Тогда
		
		ЗапросУсловияИзВнешнихОбработок = Новый Запрос(
		"ВЫБРАТЬ
		|	Т.Ссылка КАК Ссылка,
		|	Т.Наименование КАК Наименование,
		|	Т.Информация КАК Информация
		|ИЗ
		|	Справочник.ДополнительныеОтчетыИОбработки КАК Т
		|ГДЕ
		|	Т.Публикация В (ЗНАЧЕНИЕ(Перечисление.ВариантыПубликацииДополнительныхОтчетовИОбработок.Используется),
		|	                ЗНАЧЕНИЕ(Перечисление.ВариантыПубликацииДополнительныхОтчетовИОбработок.РежимОтладки))
		|	И Вид = &Вид
		|");
		
		ЗапросУсловияИзВнешнихОбработок.Параметры.Вставить("Вид", Перечисления.ВидыДополнительныхОтчетовИОбработок.ПроверкаУсловияПредоставленияСкидкиНаценки);
		
		Выборка = ЗапросУсловияИзВнешнихОбработок.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ДоступныеУсловияПредоставления.Добавить(Выборка.Ссылка, Выборка.Наименование);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ДоступныеУсловияПредоставления;
	
КонецФункции


// Возвращает параметры регистрации внешней обработки для скидки (наценки)
//
// Параметры:
//  Наименование - Строка - Наименование внешней обработки.
//  Информация - Структура - Информация.
//
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * Вид - ПеречислениеСсылка.ВидыДополнительныхОтчетовИОбработок - Вид дополнительной обработки.
//   * Версия - Строка - Версия внешний обработки.
//   * Назначение - Массив - Назначение обработки.
//   * Наименование - Строка - Наименование внешний обработки.
//   * БезопасныйРежим - Булево - Признак безопасного режима.
//   * Информация - Структура - Информация.
//   * ВерсияБСП - Строка - Требуемая версия БСП.
//   * Команды - ТаблицаЗначений - Команды внешней обработки.
//
Функция ПараметрыРегистрацииВнешнейОбработкиСкидкиНаценки(Наименование, Информация) Экспорт
	
	ПараметрыРегистрации = Новый Структура;
	
	ПараметрыРегистрации.Вставить("Вид", Перечисления.ВидыДополнительныхОтчетовИОбработок.РасчетСкидкиНаценки);
	ПараметрыРегистрации.Вставить("Версия", "1.0");
	ПараметрыРегистрации.Вставить("Назначение", Новый Массив);
	ПараметрыРегистрации.Вставить("Наименование", Наименование);
	ПараметрыРегистрации.Вставить("БезопасныйРежим", Истина);
	ПараметрыРегистрации.Вставить("Информация", Информация);
	ПараметрыРегистрации.Вставить("ВерсияБСП", "2.1.2.1");
	
	ПараметрыРегистрации.Вставить("Команды", Новый ТаблицаЗначений);
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

// Возвращает параметры регистрации внешней обработки для условия предоставления скидки (наценки).
//
// Параметры:
//  Наименование - Строка - Наименование внешней обработки.
//  Информация - Структура - Информация.
//
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * Вид - ПеречислениеСсылка.ВидыДополнительныхОтчетовИОбработок - Вид дополнительной обработки.
//   * Версия - Строка - Версия внешний обработки.
//   * Назначение - Массив - Назначение обработки.
//   * Наименование - Строка - Наименование внешний обработки.
//   * БезопасныйРежим - Булево - Признак безопасного режима.
//   * Информация - Структура - Информация.
//   * ВерсияБСП - Строка - Требуемая версия БСП.
//   * Команды - ТаблицаЗначений - Команды внешней обработки.
//
Функция ПараметрыРегистрацииВнешнейОбработкиУсловияПредоставленияСкидокНаценок(Наименование, Информация) Экспорт
	
	ПараметрыРегистрации = Новый Структура;
	
	ПараметрыРегистрации.Вставить("Вид", Перечисления.ВидыДополнительныхОтчетовИОбработок.ПроверкаУсловияПредоставленияСкидкиНаценки);
	ПараметрыРегистрации.Вставить("Версия", "1.0");
	ПараметрыРегистрации.Вставить("Назначение", Новый Массив);
	ПараметрыРегистрации.Вставить("Наименование", Наименование);
	ПараметрыРегистрации.Вставить("БезопасныйРежим", Истина);
	ПараметрыРегистрации.Вставить("Информация", Информация);
	ПараметрыРегистрации.Вставить("ВерсияБСП", "2.1.2.1");
	
	ПараметрыРегистрации.Вставить("Команды", Новый ТаблицаЗначений);
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

#Область ПроцедурыРасчетаСкидокНаценокПоДокументам


// Выполняет расчет скидок по документу.
// Вызывается из форм документов.
//
// Параметры:
//  Объект - ДокументОбъект, ДанныеФормыСтруктура - Объект, в котором требуется рассчитать скидки (наценки).
//  ВходныеПараметры - см. СкидкиНаценкиЗаполнениеСервер.НовыйПараметрыРассчитать
//
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * ДеревоСкидок - см. СформироватьДеревоСкидок
//   * ТаблицаСкидкиНаценки - ТаблицаЗначений - Таблица с рассчитанными скидками.
//   * ПараметрыРасчета - Структура - Структура параметров расчета.
//
Функция Рассчитать(Объект, ВходныеПараметры) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		
		РезультатРасчета = РассчитатьПоЗаказуКлиента(Объект, ВходныеПараметры);
		
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента") Тогда
		
		РезультатРасчета = РассчитатьПоЗаявкеНаВозвратТоваровОтКлиента(Объект, ВходныеПараметры);
		
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.КоммерческоеПредложениеКлиенту") Тогда
		
		РезультатРасчета = РассчитатьПоКоммерческомуПредложениюКлиенту(Объект, ВходныеПараметры);
		
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ЧекККМ") Тогда
		
		РезультатРасчета = РассчитатьПоЧекуККМ(Объект, ВходныеПараметры);
		
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		РезультатРасчета = РассчитатьПоРеализацииТоваровУслуг(Объект, ВходныеПараметры);
		
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.АктВыполненныхРабот") Тогда
		
		РезультатРасчета = РассчитатьПоАктуВыполненныхРабот(Объект, ВходныеПараметры);
		
	КонецЕсли;
	
	Возврат РезультатРасчета;
	
КонецФункции

// Процедура применяет результат расчет скидок к документу.
// Вызывается из форм документов.
//
// Параметры:
//  Объект - ДокументОбъект, ДанныеФормыСтруктура - Объект, в котором требуется рассчитать скидки (наценки).
//  ПримененныеСкидки - Структура - Структура со свойствами:
//    * ДеревоСкидок - ДеревоЗначений - Дерево скидок (наценок).
//    * ТаблицаСкидкиНаценки - ТаблицаЗначений - Таблица с рассчитанными скидками.
//    * ПараметрыРасчета - Структура - Структура параметров расчета.
//  РеализацияСверхЗаказа - Булево - Реализация сверх заказа.
//
Процедура ПрименитьРезультатРасчета(Объект, ПримененныеСкидки, РеализацияСверхЗаказа = Ложь) Экспорт
	
	Если ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		
		ПрименитьРезультатРасчетаКЗаказуКлиента(Объект, ПримененныеСкидки);
		
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента") Тогда
		
		ПрименитьРезультатРасчетаКВозвратуТоваровОтКлиента(Объект, ПримененныеСкидки);
		
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.КоммерческоеПредложениеКлиенту") Тогда
		
		ПрименитьРезультатРасчетаККоммерческомуПредложениюКлиенту(Объект, ПримененныеСкидки);
		
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ЧекККМ") Тогда
		
		ПрименитьРезультатРасчетаКЧекуККМ(Объект, ПримененныеСкидки);
		
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		ПрименитьРезультатРасчетаКРеализацииТоваровУслуг(Объект, ПримененныеСкидки, РеализацияСверхЗаказа);
		
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.АктВыполненныхРабот") Тогда
		
		ПрименитьРезультатРасчетаКАктуВыполненныхРабот(Объект, ПримененныеСкидки);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция РассчитатьСуммуНДС(Сумма, СтавкаНДС, Объект) Экспорт
	
	НалогообложениеНДС = Неопределено;
	СтандартнаяОбработка = Истина;
	
	ПриОпределенииНалогообложенияНДС(Объект, НалогообложениеНДС, СтандартнаяОбработка);
	
	Если СтандартнаяОбработка Тогда
		НалогообложениеНДС = Объект.НалогообложениеНДС;
	КонецЕсли;
	
	Возврат УчетНДСУПКлиентСервер.РассчитатьСуммуНДС(
		Сумма,
		УчетНДСУПКлиентСервер.ЗначениеСтавкиНДС(СтавкаНДС),
		Объект.ЦенаВключаетНДС,
		НалогообложениеНДС);
	
КонецФункции

// Возвращает доступные способы предоставления скидок (наценок)
//
// Возвращаемое значение:
//  Массив - Доступные способы предоставления.
//
Функция ДоступныеСпособыПредоставления() Экспорт
	
	ДоступныеСпособыПредоставленияСкидкиНаценки = Новый СписокЗначений;
	ДоступныеСпособыПредоставленияБонусныеБаллы = Новый СписокЗначений;
	
	СпособыПредоставленияСкидкиНаценки = Новый Массив;
	СпособыПредоставленияСкидкиНаценки.Добавить(Перечисления.СпособыПредоставленияСкидокНаценок.Процент);
	СпособыПредоставленияСкидкиНаценки.Добавить(Перечисления.СпособыПредоставленияСкидокНаценок.Сумма);
	СпособыПредоставленияСкидкиНаценки.Добавить(Перечисления.СпособыПредоставленияСкидокНаценок.СуммаДляКаждойСтроки);
	СпособыПредоставленияСкидкиНаценки.Добавить(Перечисления.СпособыПредоставленияСкидокНаценок.Количество);
	СпособыПредоставленияСкидкиНаценки.Добавить(Перечисления.СпособыПредоставленияСкидокНаценок.Подарок);
	СпособыПредоставленияСкидкиНаценки.Добавить(Перечисления.СпособыПредоставленияСкидокНаценок.ВидЦены);
	СпособыПредоставленияСкидкиНаценки.Добавить(Перечисления.СпособыПредоставленияСкидокНаценок.Сообщение);
	СпособыПредоставленияСкидкиНаценки.Добавить(Перечисления.СпособыПредоставленияСкидокНаценок.ОкруглениеСуммы);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКартыЛояльности") Тогда
		СпособыПредоставленияСкидкиНаценки.Добавить(Перечисления.СпособыПредоставленияСкидокНаценок.КартаЛояльности);
	КонецЕсли;
	
	Для Каждого СпособПредоставления Из СпособыПредоставленияСкидкиНаценки Цикл
		ДоступныеСпособыПредоставленияСкидкиНаценки.Добавить(СпособПредоставления, Строка(СпособПредоставления));
	КонецЦикла;
	
	ДоступныеСпособыПредоставленияБонусныеБаллы.Добавить(
		Перечисления.СпособыПредоставленияСкидокНаценок.Процент, НСтр("ru = 'Бонус процентом'"));
	ДоступныеСпособыПредоставленияБонусныеБаллы.Добавить(
		Перечисления.СпособыПредоставленияСкидокНаценок.Сумма, НСтр("ru = 'Бонус суммой'"));
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеОтчетыИОбработки") Тогда
		
		ЗапросСпособыПредоставленияИзВнешнихОбработок = Новый Запрос(
		"ВЫБРАТЬ
		|	Т.Ссылка КАК Ссылка,
		|	Т.Наименование КАК Наименование
		|ИЗ
		|	Справочник.ДополнительныеОтчетыИОбработки КАК Т
		|ГДЕ
		|	Т.Публикация В (ЗНАЧЕНИЕ(Перечисление.ВариантыПубликацииДополнительныхОтчетовИОбработок.Используется),
		|	                ЗНАЧЕНИЕ(Перечисление.ВариантыПубликацииДополнительныхОтчетовИОбработок.РежимОтладки))
		|	И Вид = &Вид
		|");
		
		ЗапросСпособыПредоставленияИзВнешнихОбработок.Параметры.Вставить("Вид", Перечисления.ВидыДополнительныхОтчетовИОбработок.РасчетСкидкиНаценки);
		
		Выборка = ЗапросСпособыПредоставленияИзВнешнихОбработок.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ВнешняяОбработка = СкидкиНаценкиПовтИсп.ОбъектВнешнейОбработки(Выборка.Ссылка);
			УстановитьБезопасныйРежим(Истина);
			СпособПрименения = ВнешняяОбработка.СпособПрименения();
			УстановитьБезопасныйРежим(Ложь);
			
			Если СпособПрименения = Перечисления.СпособыПримененияСкидокНаценок.НачислитьБонусныеБаллы Тогда
				ДоступныеСпособыПредоставленияБонусныеБаллы.Добавить(Выборка.Ссылка, Выборка.Наименование);
			Иначе
				ДоступныеСпособыПредоставленияСкидкиНаценки.Добавить(Выборка.Ссылка, Выборка.Наименование);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	СпособыПредоставления = Новый Структура;
	СпособыПредоставления.Вставить("СкидкиНаценки", ДоступныеСпособыПредоставленияСкидкиНаценки);
	Если ПолучитьФункциональнуюОпцию("ИспользоватьБонусныеПрограммыЛояльности") Тогда
		СпособыПредоставления.Вставить("БонусныеБаллы", ДоступныеСпособыПредоставленияБонусныеБаллы);
	КонецЕсли;
	
	Возврат СпособыПредоставления;
	
КонецФункции

// Возвращает индекс картинки для скидки.
//
// Параметры:
//  СтрокаДерева - СтрокаДереваЗначений - Строка дерева значений.
//
// Возвращаемое значение:
//  Число - Индекс картинки.
//
Функция ПолучитьИндексКартинкиДляГруппы(СтрокаДерева) Экспорт
	
	Индекс = 0;
	Если СтрокаДерева.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Максимум Тогда
		Индекс = 8;
	ИначеЕсли СтрокаДерева.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Минимум Тогда
		Индекс = 16;
	ИначеЕсли СтрокаДерева.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Сложение Тогда
		Индекс = 0;
	ИначеЕсли СтрокаДерева.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Умножение Тогда
		Индекс = 4;
	ИначеЕсли СтрокаДерева.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Вытеснение Тогда
		Индекс = 12;
	КонецЕсли;
	
	Если СтрокаДерева.ПометкаУдаления Тогда
		Индекс = Индекс + 3;
	КонецЕсли;
	
	Возврат Индекс;
	
КонецФункции

// Возвращает индекс картинки для скидки.
//
// Параметры:
//  СтрокаДерева - СтрокаДереваЗначений - Строка дерева значений.
//
// Возвращаемое значение:
//  Число - Индекс картинки.
//
Функция ПолучитьИндексКартинкиДляСкидки(СтрокаДерева) Экспорт
	
	Индекс = 0;
	Если СтрокаДерева.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.Процент Тогда
		Если СтрокаДерева.ЗначениеСкидкиНаценки < 0 Тогда
			Индекс = 32;
		Иначе
			Индекс = 28;
		КонецЕсли;
	ИначеЕсли СтрокаДерева.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.Количество Тогда
		Индекс = 36;
	ИначеЕсли СтрокаДерева.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.Подарок Тогда
		Индекс = 36;
	ИначеЕсли СтрокаДерева.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.Сумма Тогда
		Если СтрокаДерева.ЗначениеСкидкиНаценки < 0 Тогда
			Индекс = 40;
		Иначе
			Индекс = 44;
		КонецЕсли;
	ИначеЕсли СтрокаДерева.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.ОкруглениеСуммы Тогда
		Если СтрокаДерева.ЗначениеСкидкиНаценки < 0 Тогда
			Индекс = 40;
		Иначе
			Индекс = 44;
		КонецЕсли;
	ИначеЕсли СтрокаДерева.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.СуммаДляКаждойСтроки Тогда
		Если СтрокаДерева.ЗначениеСкидкиНаценки < 0 Тогда
			Индекс = 40;
		Иначе
			Индекс = 44;
		КонецЕсли;
	ИначеЕсли СтрокаДерева.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.ВидЦены Тогда
		Индекс = 48;
	ИначеЕсли СтрокаДерева.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.Сообщение Тогда
		Индекс = 52;
	ИначеЕсли СтрокаДерева.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.КартаЛояльности Тогда
		Индекс = 52;
	ИначеЕсли ТипЗнч(СтрокаДерева.СпособПредоставления) = Тип("СправочникСсылка.ДополнительныеОтчетыИОбработки") Тогда
		Индекс = 56;
	КонецЕсли;
	
	Если СтрокаДерева.ПометкаУдаления Тогда
		Индекс = Индекс + 3;
	КонецЕсли;
	
	Возврат Индекс;
	
КонецФункции

// Обновить кешированные запросы по всем элементам справочника.
Процедура ОбновитьКешированныеЗапросыСкидкиНаценки() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДляОбновленияКешированныхЗапросовСкидкиНаценки();
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Не НеобходимаРегистрация(Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();//СправочникОбъект.СкидкиНаценки
		
		ОбновитьКэшированныйЗапрос(СправочникОбъект);
		
		Попытка
			СправочникОбъект.Записать();
		Исключение
			ТекстСообщения = НСтр("ru = 'Не удалось обработать объект: %Объект% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Объект%", Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ОписаниеОшибки()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Обновление кешированных запросов'", ОбщегоНазначения.КодОсновногоЯзыка()),
									УровеньЖурналаРегистрации.Предупреждение,
									Выборка.Ссылка.Метаданные(),
									Выборка.Ссылка,
									ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

// Обновить кешированные запросы по всем элементам справочника.
Процедура ОбновитьКешированныеЗапросыУсловияПредоставленияСкидокНаценок() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДляОбновленияКешированныхЗапросовУсловияПредоставленияСкидокНаценок();
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Не НеобходимаРегистрация(Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();//СправочникОбъект.УсловияПредоставленияСкидокНаценок
		
		ОбновитьКэшированныйЗапрос(СправочникОбъект);
		
		Попытка
			СправочникОбъект.Записать();
		Исключение
			ТекстСообщения = НСтр("ru = 'Не удалось обработать объект: %Объект% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Объект%", Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ОписаниеОшибки()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Обновление кешированных запросов'", ОбщегоНазначения.КодОсновногоЯзыка()),
									УровеньЖурналаРегистрации.Предупреждение,
									Выборка.Ссылка.Метаданные(),
									Выборка.Ссылка,
									ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

// Очистить ссылки на внешние обработки по всем элементам справочника.
// 
// Параметры:
//  ПометкаНаУдаление - Булево - Установить указанную пометку на удаление
Процедура ОчиститьОтВнешнихОбработокСкидкиНаценки(ПометкаНаУдаление = Истина) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СкидкиНаценки.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СкидкиНаценки КАК СкидкиНаценки
	|ГДЕ
	|	НЕ СкидкиНаценки.ЭтоГруппа
	|	И ТИПЗНАЧЕНИЯ(СкидкиНаценки.СпособПредоставления) = ТИП(Справочник.ДополнительныеОтчетыИОбработки)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();//СправочникОбъект.СкидкиНаценки
		СправочникОбъект.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.Процент;
		СправочникОбъект.ПометкаУдаления = ПометкаНаУдаление;
		
		Попытка
			СправочникОбъект.Записать();
		Исключение
			ТекстСообщения = НСтр("ru = 'Не удалось обработать объект: %Объект% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Объект%", Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ОписаниеОшибки()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Очистка от внешних обработок'", ОбщегоНазначения.КодОсновногоЯзыка()),
									УровеньЖурналаРегистрации.Предупреждение,
									Выборка.Ссылка.Метаданные(),
									Выборка.Ссылка,
									ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

// Очистить ссылки на внешние обработки по всем элементам справочника.
// 
// Параметры:
//  ПометкаНаУдаление - Булево - Установить указанную пометку на удаление
Процедура ОчиститьОтВнешнихОбработокУсловияПредоставленияСкидокНаценок(ПометкаНаУдаление = Истина) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	УсловияПредоставленияСкидокНаценок.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.УсловияПредоставленияСкидокНаценок КАК УсловияПредоставленияСкидокНаценок
	|ГДЕ
	|	НЕ УсловияПредоставленияСкидокНаценок.ЭтоГруппа
	|	И ТИПЗНАЧЕНИЯ(УсловияПредоставленияСкидокНаценок.УсловиеПредоставления) = ТИП(Справочник.ДополнительныеОтчетыИОбработки)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();//СправочникОбъект.УсловияПредоставленияСкидокНаценок
		СправочникОбъект.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ВнешняяОбработка;
		СправочникОбъект.ПометкаУдаления = ПометкаНаУдаление;
		
		Попытка
			СправочникОбъект.Записать();
		Исключение
			ТекстСообщения = НСтр("ru = 'Не удалось обработать объект: %Объект% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Объект%", Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ОписаниеОшибки()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Очистка от внешних обработок'", ОбщегоНазначения.КодОсновногоЯзыка()),
									УровеньЖурналаРегистрации.Предупреждение,
									Выборка.Ссылка.Метаданные(),
									Выборка.Ссылка,
									ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Определяет использование скидки (наценки)
//
// Параметры:
//  СкидкаНаценка  - СправочникСсылка.СкидкиНаценки - скидка (наценка), для которой определяется использование
//  ДатаДействия   - Дата - дата, на которую определяется использование.
//
// Возвращаемое значение:
//	Структура - содержит следующие поля:
//		* ДействуетНаСкладах 					- Число - количество складов, для которых действует скидка (наценка).
//		* ВсегоНаСкладах 						- Число - количество складов, для которых выполнялась настройка 
//															скидки (наценки).
//		* ДействуетВТиповыхСоглашениях 			- Число - количество типовых соглашений, для которых действует 
//															скидка (наценка).
//		* ДействуетВИндивидуальныхСоглашениях 	- Число - количество индивидуальных соглашений, для которых действует
//                                                        	скидка (наценка).
//		* ВсегоВСоглашениях 					- Число - количество соглашений, для которых выполнялась настройка 
//															скидки (наценки).
//		* ДействуетВКартахЛояльности 			- Число - количество видов карт лояльности, для которых действует 
//															скидка (наценка).
//		* ВсегоВКартахЛояльности 				- Число - количество видов карт лояльности, для которых выполнялась 
//															скидки скидок (наценки).
//		* ДействуетНаВсехСкладах 				- Булево - признак того, что скидка (наценка) действует для всех складов
//		* ДатаНачалаБезусловногоДействия 		- Дата - дата начала общего действия скидки (наценки).
//		* ДатаОкончанияБезусловногоДействия 	- Дата - дата окончания общего действия скидки (наценки), пустая дата,
//                                                     если действие бессрочно.
//
Функция ИспользованиеСкидкиНаценки(СкидкаНаценка, ДатаДействия) Экспорт
	
	Если СкидкаНаценка = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ДействуетНаСкладах", 0);
	Результат.Вставить("ВсегоНаСкладах", 0);
	Результат.Вставить("ДействуетВТиповыхСоглашениях", 0);
	Результат.Вставить("ВсегоВТиповыхСоглашениях", 0);
	Результат.Вставить("ДействуетВИндивидуальныхСоглашениях", 0);
	Результат.Вставить("ВсегоВИндивидуальныхСоглашениях", 0);
	Результат.Вставить("ДействуетВКартахЛояльности", 0);
	Результат.Вставить("ВсегоВКартахЛояльности", 0);
	Результат.Вставить("ДействуетНаВсехСкладах", Ложь);
	Результат.Вставить("ДатаНачалаБезусловногоДействия", Дата(1, 1, 1));
	Результат.Вставить("ДатаОкончанияБезусловногоДействия", Дата(1, 1, 1));
	
	ЗаполнитьЗначенияСвойств(Результат, ЗначениеНастроекПовтИсп.КоличествоИсточниковСкидокНаценок());
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ДействиеСкидокНаценокСрезПоследних.Источник КАК Источник,
	|	ДействиеСкидокНаценокСрезПоследних.Статус
	|ПОМЕСТИТЬ Источники
	|ИЗ
	|	РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(&ТекущаяДата, СкидкаНаценка = &СкидкаНаценка) КАК ДействиеСкидокНаценокСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР
	|		КОГДА ТипЗначения(Источники.Источник) = Тип(Справочник.Склады)
	|			ТОГДА ""ДействуетНаСкладах""
	|		КОГДА ТипЗначения(Источники.Источник) = Тип(Справочник.ВидыКартЛояльности)
	|			ТОГДА ""ДействуетПоВидамКартЛояльности""
	|		КОГДА &ТекстПоСоглашениям Тогда """"
	|	КОНЕЦ КАК ГдеПрименяется,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА Источники.Источник = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|					И Источники.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДействияСкидок.Действует)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ДействуетНаПустомСкладе,
	|	СУММА(ВЫБОР
	|			КОГДА Источники.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДействияСкидок.Действует)
	|					И Источники.Источник <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ДействуетПоИсточникам
	|ИЗ
	|	Источники КАК Источники
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ТипЗначения(Источники.Источник) = Тип(Справочник.Склады)
	|			ТОГДА ""ДействуетНаСкладах""
	|		КОГДА ТипЗначения(Источники.Источник) = Тип(Справочник.ВидыКартЛояльности)
	|			ТОГДА ""ДействуетПоВидамКартЛояльности""
	|		КОГДА &ТекстПоСоглашениям Тогда """"
	|	КОНЕЦ");
	
	Если ПравоДоступа("Чтение", Метаданные.Справочники.СоглашенияСКлиентами) Тогда
		
		ТекстПоСоглашениям ="
		|		КОГДА ТипЗначения(Источники.Источник) = Тип(Справочник.СоглашенияСКлиентами)
		|			ТОГДА ВЫБОР
		|					КОГДА ВЫРАЗИТЬ(Источники.Источник КАК Справочник.СоглашенияСКлиентами).Типовое
		|						ТОГДА ""ДействуетПоТиповымСоглашениямСКлиентами""
		|					ИНАЧЕ ""ДействуетПоИндивидуальнымСоглашениямСКлиентами""
		|				КОНЕЦ";
		
	Иначе 
		ТекстПоСоглашениям = "";
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "КОГДА &ТекстПоСоглашениям Тогда """"", ТекстПоСоглашениям);

	Запрос.УстановитьПараметр("ТекущаяДата", ДатаДействия);
	Запрос.УстановитьПараметр("СкидкаНаценка", СкидкаНаценка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ГдеПрименяется = "ДействуетНаСкладах" Тогда
			
			Если ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов") Тогда
				Результат.ДействуетНаСкладах  = Выборка.ДействуетПоИсточникам;
			КонецЕсли;
			Результат.ДействуетНаВсехСкладах = Выборка.ДействуетНаПустомСкладе;
			
		ИначеЕсли Выборка.ГдеПрименяется = "ДействуетПоВидамКартЛояльности" 
			И ПолучитьФункциональнуюОпцию("ИспользоватьКартыЛояльности") Тогда
		
			Результат.ДействуетВКартахЛояльности = Выборка.ДействуетПоИсточникам;
			
		ИначеЕсли Выборка.ГдеПрименяется = "ДействуетПоТиповымСоглашениямСКлиентами" 
			И ПолучитьФункциональнуюОпцию("ИспользоватьТиповыеСоглашенияСКлиентами") Тогда
		
			Результат.ДействуетВТиповыхСоглашениях = Выборка.ДействуетПоИсточникам;
			
		ИначеЕсли Выборка.ГдеПрименяется = "ДействуетПоИндивидуальнымСоглашениямСКлиентами" 
			И ПолучитьФункциональнуюОпцию("ИспользоватьИндивидуальныеСоглашенияСКлиентами") Тогда
		
			Результат.ДействуетВИндивидуальныхСоглашениях = Выборка.ДействуетПоИсточникам;
		
		КонецЕсли;
		
	КонецЦикла;
	
	Если Результат.ДействуетНаВсехСкладах Тогда
		
		Запрос.Текст = "
		|ВЫБРАТЬ 
		|	ДействиеСкидокНаценокСрезПоследних.Период КАК Период
		|ПОМЕСТИТЬ НачалоДействия
		|ИЗ
		|	РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(
		|			&ТекущаяДата,
		|			СкидкаНаценка = &СкидкаНаценка
		|				И Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДействияСкидок.Действует)
		|				И Источник = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) КАК ДействиеСкидокНаценокСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ 
		|	МИНИМУМ(ДействиеСкидокНаценок.Период) КАК Период
		|ПОМЕСТИТЬ ОкончаниеДействия
		|ИЗ
		|	РегистрСведений.ДействиеСкидокНаценок КАК ДействиеСкидокНаценок
		|ГДЕ
		|	ДействиеСкидокНаценок.Период > &ТекущаяДата
		|	И ДействиеСкидокНаценок.Источник = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|	И ДействиеСкидокНаценок.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДействияСкидок.НеДействует)
		|	И ДействиеСкидокНаценок.СкидкаНаценка = &СкидкаНаценка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачалоДействия.Период КАК НачалоДействия,
		|	ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ОкончаниеДействия.Период, ДЕНЬ, -1), ДАТАВРЕМЯ(1, 1, 1)) КАК ОкончаниеДействия
		|ИЗ
		|	НачалоДействия КАК НачалоДействия
		|	ЛЕВОЕ СОЕДИНЕНИЕ ОкончаниеДействия КАК ОкончаниеДействия
		|	ПО (ИСТИНА)";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			Результат.ДатаОкончанияБезусловногоДействия = Выборка.ОкончаниеДействия;
			Результат.ДатаНачалаБезусловногоДействия    = Выборка.НачалоДействия;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Формирует информационные строки по использованию скидки (наценки) в разрезе типов источников действия.
//
// Параметры:
//  Форма  - ФормаКлиентскогоПриложения - форма, для которой заполняются строковые переменные, содержащие 
//                                        информацию о количестве скидок (наценок).
//  ИспользованиеСкидкиНаценки          - см. ИспользованиеСкидкиНаценки
//
Процедура СформироватьИнформациюОКоличествеИспользуемыхСкидок(Форма, ИспользованиеСкидкиНаценки) Экспорт

	ШаблонЗаголовка          = НСтр("ru = '%1 из %2'");
	ИнформацияСоглашения         = "";
	ИнформацияСклады             = "";
	ИнформацияВидыКартЛояльности = "";
	
	Если Не ИспользованиеСкидкиНаценки = Неопределено Тогда
		
		Если ИспользованиеСкидкиНаценки.ВсегоВТиповыхСоглашениях > 0 Тогда
			
			ИнформацияТиповыеСоглашения = СтрШаблон(ШаблонЗаголовка, 
				                                    ИспользованиеСкидкиНаценки.ДействуетВТиповыхСоглашениях,
				                                    ИспользованиеСкидкиНаценки.ВсегоВТиповыхСоглашениях);
		КонецЕсли;
			
		Если ИспользованиеСкидкиНаценки.ВсегоВИндивидуальныхСоглашениях > 0 Тогда
			
			ИнформацияИндивидуальныеСоглашения = СтрШаблон(ШаблонЗаголовка, 
				                                           ИспользованиеСкидкиНаценки.ДействуетВИндивидуальныхСоглашениях,
				                                           ИспользованиеСкидкиНаценки.ВсегоВИндивидуальныхСоглашениях);
		КонецЕсли;
		
		Если ИспользованиеСкидкиНаценки.ВсегоНаСкладах > 0 Тогда
			
			ИнформацияСклады = СтрШаблон(ШаблонЗаголовка, 
				                         ИспользованиеСкидкиНаценки.ДействуетНаСкладах,
				                         ИспользованиеСкидкиНаценки.ВсегоНаСкладах);
		КонецЕсли;
		
		Если ИспользованиеСкидкиНаценки.ВсегоВКартахЛояльности > 0 Тогда
			
			ИнформацияВидыКартЛояльности = 
			      СтрШаблон(ШаблонЗаголовка, 
			                ИспользованиеСкидкиНаценки.ДействуетВКартахЛояльности,
			                ИспользованиеСкидкиНаценки.ВсегоВКартахЛояльности);
		КонецЕсли;
		
	КонецЕсли;
		
	Форма.ИнформацияКоличествоСкидокИндивидуальныеСоглашения = ИнформацияИндивидуальныеСоглашения;
	Форма.ИнформацияКоличествоСкидокТиповыеСоглашения        = ИнформацияТиповыеСоглашения;
	Форма.ИнформацияКоличествоСкидокСклады                   = ИнформацияСклады;
	Форма.ИнформацияКоличествоСкидокКартыЛояльности          = ИнформацияВидыКартЛояльности;

КонецПроцедуры

// Формирует и устанавливает информационную надпись о использовании скидки(наценки)
//
// Параметры:
//  ИнформацияОДействииСкидок  - Строка - Строка информации.
//  ИспользованиеСкидкиНаценки - Структура - содержит информацию о использовании скидки (наценки). Подробно о полях
//                                           структуры можно прочитать в описании функции ИспользованиеСкидкиНаценки.
//  ДополнительнаяКоманда          - Строка - имя дополнительной команды, которая должна быть выведена.
//  ВыводитьПояснениеОбщегоСтатуса - Булево - признак необходимости вывода информации, поясняющей общий статус.
//
Процедура СформироватьИнформационнуюНадписьИспользованиеСкидокНаценок(ИнформацияОДействииСкидок,
	                                                                  ИспользованиеСкидкиНаценки,
	                                                                  ДополнительнаяКоманда = Неопределено,
	                                                                  ВыводитьПояснениеОбщегоСтатуса = Истина) Экспорт
	
	Если ИспользованиеСкидкиНаценки = Неопределено Тогда
		ИнформацияОДействииСкидок = "";
		Если ДополнительнаяКоманда = "НастроитьСкидки" Тогда
			ИнформацияОДействииСкидок = Новый ФорматированнаяСтрока(НСтр("ru = 'Настроить скидки (наценки)'"),, ЦветаСтиля.ГиперссылкаЦвет,,"НастроитьСкидки");
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ИспользоватьНесколькоСкладов                   = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов");
	ИспользоватьТиповыеСоглашенияСКлиентами        = ПолучитьФункциональнуюОпцию("ИспользоватьТиповыеСоглашенияСКлиентами");
	ИспользоватьИндивидуальныеСоглашенияСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьИндивидуальныеСоглашенияСКлиентами");
	ИспользоватьКартыЛояльности                    = ПолучитьФункциональнуюОпцию("ИспользоватьКартыЛояльности");
	ИспользоватьУпрощенныйРежим =   Не ИспользоватьНесколькоСкладов
	                              И Не ИспользоватьТиповыеСоглашенияСКлиентами
	                              И Не ИспользоватьИндивидуальныеСоглашенияСКлиентами
	                              И Не ИспользоватьКартыЛояльности;
	
	ТекстНадписи = "";
	
	Если (ИспользованиеСкидкиНаценки.ДействуетНаСкладах = 0) 
		И (ИспользованиеСкидкиНаценки.ДействуетВКартахЛояльности = 0) 
		И (ИспользованиеСкидкиНаценки.ДействуетВТиповыхСоглашениях = 0)
		И (ИспользованиеСкидкиНаценки.ДействуетВИндивидуальныхСоглашениях = 0)
		И НЕ ИспользованиеСкидкиНаценки.ДействуетНаВсехСкладах  Тогда
		
		ТекстНадписи = ТекстНадписи + " " + НСтр("ru = 'Не предоставляется'");
		
	Иначе
		
		ТекстНадписи = ТекстНадписи + " " + НСтр("ru = 'Предоставляется'");
		ТребуетсяЗапятая = Ложь;
		
		Если НЕ ИспользованиеСкидкиНаценки.ДействуетНаВсехСкладах Тогда
			
			ТекстНадписи = ТекстНадписи + " ";
			
			Если ИспользованиеСкидкиНаценки.ДействуетНаСкладах > 0 Тогда
				
				ТекстНадписи = ТекстНадписи + НСтр("ru = 'получателям складов'") + " (" + ИспользованиеСкидкиНаценки.ДействуетНаСкладах + ")";
				ТребуетсяЗапятая = Истина;
				
			КонецЕсли;
			
			Если ИспользованиеСкидкиНаценки.ДействуетВКартахЛояльности > 0 Тогда
				
				ТекстНадписи = ?(ТребуетсяЗапятая,ТекстНадписи + ", ", ТекстНадписи);
				ТекстНадписи = ТекстНадписи + НСтр("ru = 'держателям карт лояльности'") + " (" + ИспользованиеСкидкиНаценки.ДействуетВКартахЛояльности + ")";
				ТребуетсяЗапятая = Истина;
				
			КонецЕсли;
			
			Если ИспользованиеСкидкиНаценки.ДействуетВТиповыхСоглашениях > 0 Тогда
				
				ТекстНадписи = ?(ТребуетсяЗапятая,ТекстНадписи + ", ", ТекстНадписи);
				ТекстНадписи = ТекстНадписи + НСтр("ru = 'участникам типовых соглашений'") + " (" + ИспользованиеСкидкиНаценки.ДействуетВТиповыхСоглашениях + ")";
				ТребуетсяЗапятая = Истина;
				
			КонецЕсли;
			
			Если ИспользованиеСкидкиНаценки.ДействуетВИндивидуальныхСоглашениях > 0 Тогда
				
				ТекстНадписи = ?(ТребуетсяЗапятая,ТекстНадписи + ", ", ТекстНадписи);
				ТекстНадписи = ТекстНадписи + НСтр("ru = 'участникам индивидуальных соглашений'") + " (" + ИспользованиеСкидкиНаценки.ДействуетВИндивидуальныхСоглашениях + ")";
				ТребуетсяЗапятая = Истина;
				
			КонецЕсли;
			
		Иначе
			
			ТекстНадписи = НСтр("ru = 'Предоставляется всем клиентам'");
			
			ШаблонПериода = НСтр("ru = '(с %1 %2 %3)'");
			ТекстНадписи = ТекстНадписи + " " + СтрШаблон(
			              ШаблонПериода,
			              Формат(ИспользованиеСкидкиНаценки.ДатаНачалаБезусловногоДействия, "ДЛФ=DD"),
			              ?(ИспользованиеСкидкиНаценки.ДатаОкончанияБезусловногоДействия = Дата(1, 1, 1),НСтр("ru = 'и'"),НСтр("ru = 'по'")),
			              ?(ИспользованиеСкидкиНаценки.ДатаОкончанияБезусловногоДействия = Дата(1, 1, 1),
			                 НСтр("ru = 'бессрочно'"),
			                 Формат(ИспользованиеСкидкиНаценки.ДатаОкончанияБезусловногоДействия, "ДЛФ=DD")));
			
			Если ВыводитьПояснениеОбщегоСтатуса 
				И (ИспользованиеСкидкиНаценки.ВсегоНаСкладах > 0 
				ИЛИ ИспользованиеСкидкиНаценки.ВсегоВКартахЛояльности > 0
				ИЛИ ИспользованиеСкидкиНаценки.ДействуетВТиповыхСоглашениях > 0
				ИЛИ ИспользованиеСкидкиНаценки.ДействуетВИндивидуальныхСоглашениях > 0)  Тогда
				
				Если ДополнительнаяКоманда = "НастроитьСкидки" Тогда
					ТекстПояснения = НСтр("ru = 'В период действия общего статуса настройки, сделанные в данной форме не актуальны.'");
				Иначе
					ТекстПояснения = НСтр("ru = 'В период действия общего статуса вышеуказанные уточнения не актуальны.'");
				КонецЕсли;
				
				ТекстНадписи = ТекстНадписи + "." + Символы.ПС + ТекстПояснения;
				
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЕсли;
	
	СтрокаИнформация  = Новый ФорматированнаяСтрока(ТекстНадписи + "  ",, ЦветаСтиля.ЦветТекстаПоля);
	
	Если ДополнительнаяКоманда <> Неопределено
		И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ДействиеСкидокНаценок) Тогда
		
		Если ДополнительнаяКоманда = "Изменить" Тогда
			СтрокаИзменить = Новый ФорматированнаяСтрока(НСтр("ru = 'Изменить'"),, ЦветаСтиля.ГиперссылкаЦвет,,"Изменить");
		ИначеЕсли ДополнительнаяКоманда = "НастроитьСкидки" Тогда
			СтрокаИзменить = Новый ФорматированнаяСтрока(НСтр("ru = 'Настроить скидки (наценки)'"),, ЦветаСтиля.ГиперссылкаЦвет,,"НастроитьСкидки");
		ИначеЕсли ДополнительнаяКоманда = "ИзменитьОбщийСтатус" Тогда
			СтрокаИзменить = Новый ФорматированнаяСтрока(НСтр("ru = 'Изменить общий статус'"),, ЦветаСтиля.ГиперссылкаЦвет,,"ИзменитьОбщийСтатус");
		Иначе
			СтрокаИзменить = "";
		КонецЕсли;
		
		ИнформацияОДействииСкидок = Новый ФорматированнаяСтрока(СтрокаИнформация, СтрокаИзменить);
	Иначе
		ИнформацияОДействииСкидок = СтрокаИнформация;
	КонецЕсли;

КонецПроцедуры

// Формирует информацию о количестве действующих скидок (наценок) для источника действия.
//
// Параметры:
//  Источник  - СправочникСсылка.Склады, СправочникСсылка.ВидыКартЛояльности, СправочникСсылка.СоглашенияСКлиентами -
//              источник действия скидок.
//  ДатаСреза  - Дата - дата, на которую получается информация о действии скидок.
//
// Возвращаемое значение:
//   Строка   - в формате "КоличествоДействует из КоличествоВсего".
//
Функция ИнформацияОКоличествеСкидок(Источник, ДатаСреза) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НЕ ЗначениеЗаполнено(Источник) Тогда
		Возврат "";
	КонецЕсли;
	
	Всего     = 0;
	Действует = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СУММА(1) КАК КоличествоДействующихСкидок
	|ПОМЕСТИТЬ ДействующиеСкидки
	|ИЗ
	|	РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(&ДатаСреза, Источник = &Источник) КАК ДействиеСкидокНаценокСрезПоследних
	|ГДЕ
	|	ДействиеСкидокНаценокСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДействияСкидок.Действует)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(1) КАК КоличествоСкидокВсего
	|ПОМЕСТИТЬ ВсегоСкидки
	|ИЗ
	|	Справочник.СкидкиНаценки КАК СкидкиНаценки
	|ГДЕ
	|	НЕ СкидкиНаценки.ЭтоГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсегоСкидки.КоличествоСкидокВсего КАК Всего,
	|	ЕСТЬNULL(ДействующиеСкидки.КоличествоДействующихСкидок, 0) КАК Действует
	|ИЗ
	|	ВсегоСкидки КАК ВсегоСкидки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДействующиеСкидки КАК ДействующиеСкидки
	|		ПО (ИСТИНА)";
	
	Запрос.УстановитьПараметр("Источник", Источник);
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	
	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Следующий() Тогда
		Всего     = Выборка.Всего;
		Действует = Выборка.Действует;
	КонецЕсли;
	
	Если Всего = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат СтрШаблон(НСтр("ru = '%1 из %2'"),Действует, Всего);

КонецФункции 

// Получает значения необходимых в различных формах скидок (наценок) функциональных опций.
//
// Возвращаемое значение:
//   Структура   - содержит значения требуемых функциональных опций.
//
Функция СтруктураИсточниковДействияСкидокСогласноФО() Экспорт
	
	СтруктураКВозврату = Новый Структура;
	
	СтруктураКВозврату.Вставить("ИспользоватьКартыЛояльности", ПолучитьФункциональнуюОпцию("ИспользоватьКартыЛояльности"));
	СтруктураКВозврату.Вставить("ИспользоватьБонусныеПрограммыЛояльности", ПолучитьФункциональнуюОпцию("ИспользоватьБонусныеПрограммыЛояльности"));
	СтруктураКВозврату.Вставить("ИспользоватьНесколькоСкладов", ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов"));
	СтруктураКВозврату.Вставить("ИспользоватьСоглашенияСКлиентами", ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами"));
	
	Возврат СтруктураКВозврату;
	
КонецФункции

// Параметры:
// 	ТаблицаСкидкиНаценки - см. ДействиеСкидокНаценокПоИсточнику
//
Процедура ПостроитьДеревоСкидкиНаценкиРекурсивно(ДеревоСкидок, ТаблицаСкидкиНаценки)
	
	Для Каждого СтрокаДерева Из ДеревоСкидок.Строки Цикл
		
		Если СтрокаДерева.ЭтоГруппа Тогда
			
			ПостроитьДеревоСкидкиНаценкиРекурсивно(СтрокаДерева, ТаблицаСкидкиНаценки);
			
			СтрокаДерева.ИндексКартинки = ПолучитьИндексКартинкиДляГруппы(СтрокаДерева);
			
		Иначе
			
			СтрокаДерева.ИндексКартинки = ПолучитьИндексКартинкиДляСкидки(СтрокаДерева);
			
			Отбор = Новый Структура;
			Отбор.Вставить("СкидкаНаценка", СтрокаДерева.Ссылка);
			НайденныеСтроки = ТаблицаСкидкиНаценки.НайтиСтроки(Отбор);
			Если НайденныеСтроки.Количество() > 0 Тогда
				СтрокаДерева.Статус = НайденныеСтроки[0].Статус;
				СтрокаДерева.ДатаНачала = НайденныеСтроки[0].ДатаНачала;
				СтрокаДерева.ДатаОкончания = НайденныеСтроки[0].ДатаОкончания;
			Иначе
				СтрокаДерева.Статус = Перечисления.СтатусыДействияСкидок.НеДействует;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Выполняет построение дерева скидок (наценок) для источника
//
// Параметры:
//  Форма     - ФормаКлиентскогоПриложения - форма, в которой строится дерево
//  Источник  - СправочникСсылка.Склады, СправочникСсылка.ВидыКартЛояльности, СправочникСсылка.СоглашенияСКлиентами -
//              источник действия скидок.
//
Процедура ПостроитьДеревоСкидкиНаценкиВФорме(Форма, Источник) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаСкидкиНаценки = ДействиеСкидокНаценокПоИсточнику(Источник, Форма.ДатаСреза);
	
	МассивСкидок = Новый Массив;
	
	Если ЗначениеЗаполнено(Форма.ВариантОтображенияСкидок) Тогда
		
		Отбор = Новый Структура;
		Если Форма.ВариантОтображенияСкидок = "ТолькоДействующие" Тогда
			Отбор.Вставить("Статус", Перечисления.СтатусыДействияСкидок.Действует);
		ИначеЕсли Форма.ВариантОтображенияСкидок = "ДействующиеИОтмененные" Тогда
			Отбор.Вставить("Использование", Истина);
		КонецЕсли;
		
		НайденныеСтроки = ТаблицаСкидкиНаценки.НайтиСтроки(Отбор);
		Для Каждого СтрокаТЧ Из НайденныеСтроки Цикл
			МассивСкидок.Добавить(СтрокаТЧ.СкидкаНаценка);
		КонецЦикла;
		
	Иначе
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СкидкиНаценки.Ссылка
		|ИЗ
		|	Справочник.СкидкиНаценки КАК СкидкиНаценки
		|ГДЕ
		|	НЕ СкидкиНаценки.ЭтоГруппа");
		МассивСкидок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли;
	
	ДеревоСкидок = СформироватьДеревоСкидок(МассивСкидок);
	
	ДеревоСкидок.Колонки.Добавить("ИндексКартинки", Новый ОписаниеТипов("Число"));
	ДеревоСкидок.Колонки.Добавить("Статус",         Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыДействияСкидок"));
	ДеревоСкидок.Колонки.Добавить("ДатаНачала",     Новый ОписаниеТипов("Дата"));
	ДеревоСкидок.Колонки.Добавить("ДатаОкончания",  Новый ОписаниеТипов("Дата"));
	
	ПостроитьДеревоСкидкиНаценкиРекурсивно(ДеревоСкидок, ТаблицаСкидкиНаценки);
	
	Форма.ЗначениеВРеквизитФормы(ДеревоСкидок, "СкидкиНаценки");
	
КонецПроцедуры

// Общая часть обработчиков события "ПриСозданииНаСервере" источников действия скидок.
//
// Параметры:
//  Форма     - ФормаКлиентскогоПриложения - форма, для которой выполняются действия
//  Источник  - СправочникСсылка.Склады, СправочникСсылка.ВидыКартЛояльности, СправочникСсылка.СоглашенияСКлиентами -
//              источник действия скидок.
//  РассчитыватьИнформациюОКоличестве - Булево - признак необходимости расчета информации о количестве действующих скидок по источнику.
//
Процедура ПриСозданииНаСервереИсточниковДействияСкидок(Форма, Источник, РассчитыватьИнформациюОКоличестве = Истина) Экспорт

	Форма.ВариантОтображенияСкидок = "ТолькоДействующие";
	Форма.ДатаСреза = ТекущаяДатаСеанса();
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьАвтоматическиеСкидкиВПродажах") Тогда
		Возврат;
	КонецЕсли;
	
	ПостроитьДеревоСкидкиНаценкиВФорме(Форма, Источник);
	Если РассчитыватьИнформациюОКоличестве Тогда
		Форма.ИнформацияОКоличествеСкидок = ИнформацияОКоличествеСкидок(Источник, ТекущаяДатаСеанса());
	КонецЕсли;

КонецПроцедуры

// Заполняет копированием регистр сведений "ДействиеСкидокНаценок". 
//
// Параметры:
// 	ТекущийОбъект - СправочникОбъект.СоглашенияСКлиентами, СправочникОбъект.ВидыКартЛояльности, СправочникОбъект.Склады -
// 	                Объект для новой записи.
// 	ЗначениеКопирования - СправочникСсылка.СоглашенияСКлиентами, СправочникСсылка.ВидыКартЛояльности, СправочникСсылка.Склады -
//                        Ссылка на источник - объект копирования.
Процедура ПриЗаписиНаСервереИсточниковДействияСкидокНаценок(ТекущийОбъект, ЗначениеКопирования) Экспорт
	
	Если Не ЗначениеКопирования.Пустая() Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ДействиеСкидокНаценокСрезПоследних.Период КАК Период,
		|	ДействиеСкидокНаценокСрезПоследних.СкидкаНаценка КАК СкидкаНаценка,
		|	ДействиеСкидокНаценокСрезПоследних.Статус КАК Статус,
		|	ДействиеСкидокНаценокСрезПоследних.СегментПартнеров КАК СегментПартнеров
		|ИЗ
		|	РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(&ДатаСреза, Источник = &Источник) КАК
		|		ДействиеСкидокНаценокСрезПоследних
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДействиеСкидокНаценок.Период,
		|	ДействиеСкидокНаценок.СкидкаНаценка,
		|	ДействиеСкидокНаценок.Статус,
		|	ДействиеСкидокНаценок.СегментПартнеров
		|ИЗ
		|	РегистрСведений.ДействиеСкидокНаценок КАК ДействиеСкидокНаценок
		|ГДЕ
		|	ДействиеСкидокНаценок.Период > &ДатаСреза
		|	И ДействиеСкидокНаценок.Источник = &Источник";
		
		Запрос.УстановитьПараметр("ДатаСреза", ТекущаяДатаСеанса());
		Запрос.УстановитьПараметр("Источник", ЗначениеКопирования);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Количество() > 0 Тогда
			Ответственный = Пользователи.ТекущийПользователь();;
		КонецЕсли;
		
		Пока Выборка.Следующий() Цикл
			
			МенеджерЗаписи = РегистрыСведений.ДействиеСкидокНаценок.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
			МенеджерЗаписи.Источник = ТекущийОбъект.Ссылка;
			МенеджерЗаписи.Ответственный = Ответственный;
			МенеджерЗаписи.Записать(Истина);
			
		КонецЦикла;
		
		ЗначениеКопирования = Справочники.СделкиСКлиентами.ПустаяСсылка();
		
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает условное оформление для дерева скидок (наценок) в форме источника
//
// Параметры:
//  УсловноеОформление - УсловноеОформлениеКомпоновкиДанных - условное оформление формы, в которое добавляются элементы
//  Элементы - ЭлементыФормы - коллекция элементов формы, для которых устанавливается условное оформление.
//  ИмяТаблицы - Строка - имя таблицы скидок
//
Процедура УстановитьУсловноеОформлениеФормыИсточникаДействияСкидок(УсловноеОформление, Элементы, ИмяТаблицы = "СкидкиНаценки") Экспорт
	
	// "Статус" - не действует
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ЭлементТаблица = Элементы[ИмяТаблицы]; // ТаблицаФормы
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементТаблица.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяТаблицы + ".Статус");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыДействияСкидок.НеДействует;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	
	// Скидка (наценка) действует бессрочно

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ЭлементТаблица = Элементы["СкидкиНаценкиДатаОкончанияДействия"]; // ТаблицаФормы
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементТаблица.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяТаблицы + ".ДатаОкончания");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = '00010101';

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяТаблицы + ".ДатаНачала");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = '00010101';

	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<бессрочно>'"));
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуДанныхПодчиненныхСтрок(РезультатРасчета, ПодчиненнаяСтрока)
	
	Для Каждого СтрокаТаблицы Из ПодчиненнаяСтрока.РезультатРасчета Цикл
		Если ПодчиненнаяСтрока.ЭтоГруппа Тогда
			НоваяСтрока = РезультатРасчета.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.РеквизитДопУпорядочивания = ПодчиненнаяСтрока.РеквизитДопУпорядочивания;
		Иначе
			Если СтрокаТаблицы.Действует Тогда
				НоваяСтрока = РезультатРасчета.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				НоваяСтрока.РеквизитДопУпорядочивания = ПодчиненнаяСтрока.РеквизитДопУпорядочивания;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Функция выполняет объединения подчиненных таблиц данных.
//
// Возвращаемое значение:
//	ТаблицаЗначений - объединенная таблица данных:
//		* КлючСвязи - Число
//		* Сумма - Число
//		* Расшифровка - ТаблицаЗначений
//		* РеквизитДопУпорядочивания - Число
//
Функция ТаблицаДанныхПоСпособуСовместногоПримененияДляДокументаВЦелом(СтрокаДерева)
	
	РезультатРасчета = Новый ТаблицаЗначений;
	РезультатРасчета.Колонки.Добавить("КлючСвязи",                 Новый ОписаниеТипов("Число"));
	РезультатРасчета.Колонки.Добавить("Сумма",                     Новый ОписаниеТипов("Число"));
	РезультатРасчета.Колонки.Добавить("Расшифровка",               Новый ОписаниеТипов("ТаблицаЗначений"));
	РезультатРасчета.Колонки.Добавить("РеквизитДопУпорядочивания", Новый ОписаниеТипов("Число"));
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("СтрокаДерева");
	Таблица.Колонки.Добавить("Сумма",         Новый ОписаниеТипов("Число"));
	Таблица.Колонки.Добавить("РеквизитДопУпорядочивания", Новый ОписаниеТипов("Число"));
	
	Для Каждого ПодчиненнаяСтрока Из СтрокаДерева.Строки Цикл
		
		Если НЕ ПодчиненнаяСтрока.ЭтоГруппа Тогда // Это скидка а не группа
			
			Если НЕ ПодчиненнаяСтрока.ПараметрыУсловий.УсловияВыполнены Тогда
				Продолжить;
			КонецЕсли;
			
			Если ПодчиненнаяСтрока.Управляемая И НЕ ПодчиненнаяСтрока.НазначенаПользователем Тогда
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		НоваяСтрока = Таблица.Добавить();
		НоваяСтрока.СтрокаДерева              = ПодчиненнаяСтрока;
		НоваяСтрока.РеквизитДопУпорядочивания = ПодчиненнаяСтрока.РеквизитДопУпорядочивания;
		
		Для Каждого СтрокаТаблицы Из ПодчиненнаяСтрока.РезультатРасчета Цикл
			Если ПодчиненнаяСтрока.ЭтоГруппа Тогда
				НоваяСтрока.Сумма = НоваяСтрока.Сумма + СтрокаТаблицы.Сумма;
			Иначе
				Если СтрокаТаблицы.Действует Тогда
					НоваяСтрока.Сумма = НоваяСтрока.Сумма + СтрокаТаблицы.Сумма;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Если СтрокаДерева.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Максимум Тогда
		Таблица.Сортировать("Сумма Убыв, РеквизитДопУпорядочивания");
	ИначеЕсли СтрокаДерева.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Минимум Тогда
		Таблица.Сортировать("Сумма Возр, РеквизитДопУпорядочивания");
	ИначеЕсли СтрокаДерева.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Вытеснение Тогда
		Таблица.Сортировать("РеквизитДопУпорядочивания");
	КонецЕсли;
	
	Для Каждого СтрокаТЧ Из Таблица Цикл
		ЗаполнитьТаблицуДанныхПодчиненныхСтрок(РезультатРасчета, СтрокаТЧ.СтрокаДерева);
		Прервать;
	КонецЦикла;
	
	Возврат РезультатРасчета;
	
КонецФункции

// Функция возвращает таблицу действующих скидок (наценок)
//
// Возвращаемое значение:
//	Массив из СправочникСсылка.СкидкиНаценки - Расшифровка скидок.
//
Функция СкидкиНаценкиДляРозничнойТорговли(ТекущаяДата, Склад, КартаЛояльности)
	
	ИспользоватьНесколькоСкладов = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов");
	ИспользоватьКартыЛояльности  = ПолучитьФункциональнуюОпцию("ИспользоватьКартыЛояльности");
	
	ГруппыСкладов = Новый Массив;
	Если ИспользоватьНесколькоСкладов Тогда
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Ссылка.Родитель КАК Ссылка
		|ИЗ
		|	Справочник.Склады КАК Т
		|ГДЕ
		|	Т.Ссылка В (&Склад)
		|ИТОГИ ПО
		|	Ссылка ТОЛЬКО ИЕРАРХИЯ
		|");
		Запрос.Параметры.Вставить("Склад", Склад);
		ГруппыСкладов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.СкидкаНаценка КАК СкидкаНаценка
	|ИЗ
	|	РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(&ТекущаяДата,
	|		Источник = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		ИЛИ &ИспользоватьНесколькоСкладов
	|	) КАК Таблица
	|ГДЕ
	|	Таблица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДействияСкидок.Действует)
	|";

	Если ИспользоватьНесколькоСкладов Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИЛИ &ИспользоватьНесколькоСкладов", " ИЛИ Источник = &Склад ИЛИ Источник В (&ГруппыСкладов)");
	Иначе	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИЛИ &ИспользоватьНесколькоСкладов", "");
	КонецЕсли;
	
	Если ИспользоватьКартыЛояльности Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Таблица.СкидкаНаценка
		|ИЗ
		|	РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(&ТекущаяДата, Источник ССЫЛКА Справочник.ВидыКартЛояльности) КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КартыЛояльности КАК КартыЛояльности
		|		ПО (КартыЛояльности.Владелец = Таблица.Источник)
		|			И (КартыЛояльности.Ссылка = &КартаЛояльности)
		|ГДЕ
		|	КартыЛояльности.Владелец.ДатаНачалаДействия <= &ТекущаяДата
		|	И ВЫБОР
		|			КОГДА КартыЛояльности.Владелец.ДатаОкончанияДействия = ДАТАВРЕМЯ(1, 1, 1)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ КОНЕЦПЕРИОДА(КартыЛояльности.Владелец.ДатаОкончанияДействия, ДЕНЬ) >= &ТекущаяДата
		|		КОНЕЦ
		|	И Таблица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДействияСкидок.Действует)
		|	И (НЕ Таблица.СкидкаНаценка.СпособПримененияСкидки = ЗНАЧЕНИЕ(Перечисление.СпособыПримененияСкидокНаценок.НачислитьБонусныеБаллы)
		|		ИЛИ Таблица.СкидкаНаценка.СпособПримененияСкидки = ЗНАЧЕНИЕ(Перечисление.СпособыПримененияСкидокНаценок.НачислитьБонусныеБаллы)
		|			И НЕ КартыЛояльности.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.РозничныйПокупатель))
		|";
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.Параметры.Вставить("ТекущаяДата", ТекущаяДата);
	
	Если ИспользоватьКартыЛояльности Тогда
		Запрос.Параметры.Вставить("КартаЛояльности", КартаЛояльности);
	КонецЕсли;
	Если ИспользоватьНесколькоСкладов Тогда
		Запрос.Параметры.Вставить("Склад",         Склад);
		Запрос.Параметры.Вставить("ГруппыСкладов", ГруппыСкладов);
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("СкидкаНаценка");
	
КонецФункции

// Функция возвращает таблицу действующих скидок (наценок)
//
// Возвращаемое значение:
//	Массив из СправочникСсылка.СкидкиНаценки - Расшифровка скидок.
//
Функция СкидкиНаценкиДляОптовойТорговли(ТекущаяДата, Склад, Соглашение, КартаЛояльности) Экспорт
	
	ИспользоватьНесколькоСкладов     = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов");
	ИспользоватьСоглашенияСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами");
	ИспользоватьКартыЛояльности      = ПолучитьФункциональнуюОпцию("ИспользоватьКартыЛояльности");
	
	ГруппыСкладов = Новый Массив;
	Если ИспользоватьНесколькоСкладов Тогда
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Ссылка.Родитель КАК Ссылка
		|ИЗ
		|	Справочник.Склады КАК Т
		|ГДЕ
		|	Т.Ссылка В (&Склад)
		|ИТОГИ ПО
		|	Ссылка ТОЛЬКО ИЕРАРХИЯ
		|");
		Запрос.Параметры.Вставить("Склад", Склад);
		ГруппыСкладов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.СкидкаНаценка КАК СкидкаНаценка
	|ИЗ
	|	РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(&ТекущаяДата,
	|		Источник = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		ИЛИ &ИспользоватьНесколькоСкладов
	|		ИЛИ ИспользоватьСоглашенияСКлиентами
	|	) КАК Таблица
	|ГДЕ
	|	Таблица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДействияСкидок.Действует)
	|";
	
	Если ИспользоватьНесколькоСкладов Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИЛИ &ИспользоватьНесколькоСкладов", " ИЛИ Источник = &Склад ИЛИ Источник В (&ГруппыСкладов)");
	Иначе	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИЛИ &ИспользоватьНесколькоСкладов", "");
	КонецЕсли;
	
	Если ИспользоватьСоглашенияСКлиентами Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИЛИ ИспользоватьСоглашенияСКлиентами", " ИЛИ Источник = &Соглашение");
	Иначе	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИЛИ ИспользоватьСоглашенияСКлиентами", "");
	КонецЕсли;
	
	Если ИспользоватьКартыЛояльности Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Таблица.СкидкаНаценка
		|ИЗ
		|	РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(&ТекущаяДата, Источник ССЫЛКА Справочник.ВидыКартЛояльности) КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КартыЛояльности КАК КартыЛояльности
		|		ПО (КартыЛояльности.Владелец = Таблица.Источник)
		|			И (КартыЛояльности.Ссылка = &КартаЛояльности)
		|ГДЕ
		|	КартыЛояльности.Владелец.ДатаНачалаДействия <= &ТекущаяДата
		|	И ВЫБОР
		|			КОГДА КартыЛояльности.Владелец.ДатаОкончанияДействия = ДАТАВРЕМЯ(1, 1, 1)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ КОНЕЦПЕРИОДА(КартыЛояльности.Владелец.ДатаОкончанияДействия, ДЕНЬ) >= &ТекущаяДата
		|		КОНЕЦ
		|	И Таблица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДействияСкидок.Действует)
		|	И (НЕ Таблица.СкидкаНаценка.СпособПримененияСкидки = ЗНАЧЕНИЕ(Перечисление.СпособыПримененияСкидокНаценок.НачислитьБонусныеБаллы)
		|		ИЛИ Таблица.СкидкаНаценка.СпособПримененияСкидки = ЗНАЧЕНИЕ(Перечисление.СпособыПримененияСкидокНаценок.НачислитьБонусныеБаллы)
		|			И НЕ КартыЛояльности.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.РозничныйПокупатель))
		|";
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.Параметры.Вставить("ТекущаяДата", ТекущаяДата);
	
	Если ИспользоватьСоглашенияСКлиентами Тогда
		Запрос.Параметры.Вставить("Соглашение", Соглашение);
	КонецЕсли;
	Если ИспользоватьКартыЛояльности Тогда
		Запрос.Параметры.Вставить("КартаЛояльности", КартаЛояльности);
	КонецЕсли;
	Если ИспользоватьНесколькоСкладов Тогда
		Запрос.Параметры.Вставить("Склад",         Склад);
		Запрос.Параметры.Вставить("ГруппыСкладов", ГруппыСкладов);
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("СкидкаНаценка");
	
КонецФункции

// Функция возвращает таблицу действующих скидок (наценок)
//
// Возвращаемое значение:
//	Массив из СправочникСсылка.СкидкиНаценки - Расшифровка скидок.
//
Функция СкидкиНаценкиДляУслуг(ТекущаяДата, Соглашение, КартаЛояльности)
	
	ИспользоватьСоглашенияСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами");
	ИспользоватьКартыЛояльности      = ПолучитьФункциональнуюОпцию("ИспользоватьКартыЛояльности");
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.СкидкаНаценка КАК СкидкаНаценка
	|ИЗ
	|	РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(&ТекущаяДата,
	|		Источник = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		ИЛИ &ИспользоватьСоглашенияСКлиентами
	|	) КАК Таблица
	|ГДЕ
	|	Таблица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДействияСкидок.Действует)
	|";
	Если ИспользоватьСоглашенияСКлиентами Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИЛИ &ИспользоватьСоглашенияСКлиентами", "ИЛИ Источник = &Соглашение");
	Иначе	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИЛИ &ИспользоватьСоглашенияСКлиентами", "");
	КонецЕсли;
	
	Если ИспользоватьКартыЛояльности Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Таблица.СкидкаНаценка
		|ИЗ
		|	РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(&ТекущаяДата, Источник ССЫЛКА Справочник.ВидыКартЛояльности) КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КартыЛояльности КАК КартыЛояльности
		|		ПО (КартыЛояльности.Владелец = Таблица.Источник)
		|			И (КартыЛояльности.Ссылка = &КартаЛояльности)
		|ГДЕ
		|	КартыЛояльности.Владелец.ДатаНачалаДействия <= &ТекущаяДата
		|	И ВЫБОР
		|			КОГДА КартыЛояльности.Владелец.ДатаОкончанияДействия = ДАТАВРЕМЯ(1, 1, 1)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ КОНЕЦПЕРИОДА(КартыЛояльности.Владелец.ДатаОкончанияДействия, ДЕНЬ) >= &ТекущаяДата
		|		КОНЕЦ
		|	И Таблица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДействияСкидок.Действует)
		|";
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.Параметры.Вставить("ТекущаяДата", ТекущаяДата);
	
	Если ИспользоватьСоглашенияСКлиентами Тогда
		Запрос.Параметры.Вставить("Соглашение", Соглашение);
	КонецЕсли;
	Если ИспользоватьКартыЛояльности Тогда
		Запрос.Параметры.Вставить("КартаЛояльности", КартаЛояльности);
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("СкидкаНаценка");
	
КонецФункции

// Функция заполняет ключи связи в табличной части "Товары" документа.
//
Процедура ЗаполнитьКлючиСвязиВТабличнойЧастиТовары(Объект, ИмяТЧ, РеализацияСверхЗаказа = Ложь) Экспорт
	
	Если РеализацияСверхЗаказа Тогда
	
		Индекс = 0;
		МассивСтрок = Новый массив;
		Для Каждого СтрокаТЧ Из Объект[ИмяТЧ] Цикл
			Если СтрокаТЧ.КодСтроки = 0 Тогда	
				МассивСтрок.Добавить(СтрокаТЧ);
			Иначе
				Индекс = Макс(Индекс, СтрокаТЧ.КлючСвязи);
			КонецЕсли;
		КонецЦикла;
		
		Для каждого СтрокаТЧ Из МассивСтрок Цикл
			Индекс = Индекс + 1;
			СтрокаТЧ.КлючСвязи = Индекс;
		КонецЦикла;
	Иначе
	
		Индекс = 0;
		Для Каждого СтрокаТЧ Из Объект[ИмяТЧ] Цикл
			Индекс = Индекс + 1;
			СтрокаТЧ.КлючСвязи = Индекс;
		КонецЦикла;
	
	КонецЕсли;
	
КонецПроцедуры

// Функция получает текущее время объекта
//
// Параметры:
//  Объект  - ДокументОбъект - объект для которого нужно получить текущее время.
//
// Возвращаемое значение:
//   Дата   - Текущее время объекта.
//
Функция ПолучитьТекущееВремяОбъекта(Объект) Экспорт
	
	ТекущаяДата = ?(ЗначениеЗаполнено(Объект.Ссылка), Объект.Дата,
		?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса()));
	
	ТекущееВремя = '00010101' + (ТекущаяДата - НачалоДня(ТекущаяДата));
	
	Возврат ТекущееВремя;
	
КонецФункции

// Функция получает текущую дату время объекта
//
// Параметры:
//  Объект  - ДокументОбъект - объект для которого нужно получить текущее время.
//
// Возвращаемое значение:
//   Дата   - Текущее время объекта.
//
Функция ПолучитьТекущуюДатуОбъекта(Объект) Экспорт
	
	ТекущаяДата = ?(ЗначениеЗаполнено(Объект.Ссылка), Объект.Дата,
		?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса()));
	
	Возврат ТекущаяДата;
	
КонецФункции

// Процедура заполняет служебную структуру "УправляемыеСкидки" структуры "ВходныеПараметры".
//
Процедура ПодготовитьДанныеОВыбранныхУправляемыхСкидках(Объект, ПараметрыРасчета, ВходныеПараметры) Экспорт
	
	ПараметрыРасчета.Вставить("УправляемыеСкидки", Новый Массив);
	
	// Если управляемые скидки не переданы, то получим их из предыдущего расчета
	Если ВходныеПараметры.Свойство("УправляемыеСкидки") И ВходныеПараметры.УправляемыеСкидки <> Неопределено И ВходныеПараметры.УправляемыеСкидки.Количество() > 0 Тогда
		Для Каждого УправляемаяСкидка Из ВходныеПараметры.УправляемыеСкидки Цикл
			ПараметрыРасчета.УправляемыеСкидки.Добавить(УправляемаяСкидка.Значение);
		КонецЦикла;
	Иначе
		Если ВходныеПараметры.ВосстанавливатьУправляемыеСкидки Тогда
			НайденныеСтроки = Объект.СкидкиНаценки.НайтиСтроки(Новый Структура("КлючСвязи", 0));
			Для Каждого СтрокаТЧ Из НайденныеСтроки Цикл
				ПараметрыРасчета.УправляемыеСкидки.Добавить(СтрокаТЧ.СкидкаНаценка);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Сообщения = Новый ТаблицаЗначений;
	Сообщения.Колонки.Добавить("СкидкаНаценка",  Новый ОписаниеТипов("СправочникСсылка.СкидкиНаценки"));
	Сообщения.Колонки.Добавить("НапомнитьПозже", Новый ОписаниеТипов("Булево"));
	Сообщения.Индексы.Добавить("СкидкаНаценка");
	
	НайденныеСтроки = Объект.СкидкиНаценки.НайтиСтроки(Новый Структура("КлючСвязи", -1));
	Для Каждого СтрокаТЧ Из НайденныеСтроки Цикл
		Сообщение = Сообщения.Добавить();
		Сообщение.СкидкаНаценка  = СтрокаТЧ.СкидкаНаценка;
		Сообщение.НапомнитьПозже = СтрокаТЧ.НапомнитьПозже;
	КонецЦикла;
	
	ПараметрыРасчета.Вставить("Сообщения", Сообщения);
	
КонецПроцедуры

// Процедура сохраняет выбранные управляемые скидки в табличной части "СкидкиНаценки".
//
Процедура СохранитьДанныеОВыбранныхУправляемыхСкидках(ТаблицаСкидкиНаценки, ПараметрыРасчета)
	
	Для Каждого УправляемаяСкидка Из ПараметрыРасчета.УправляемыеСкидки Цикл
		НоваяСтрока = ТаблицаСкидкиНаценки.Добавить();
		НоваяСтрока.КлючСвязи     = 0;
		НоваяСтрока.Сумма         = 0;
		НоваяСтрока.СкидкаНаценка = УправляемаяСкидка;
	КонецЦикла;
	
	ТекущиеСообщения = ТаблицаСкидкиНаценки.Скопировать(Новый Структура("ЭтоСообщение", Истина), "СкидкаНаценка");
	ТекущиеСообщения.Свернуть("СкидкаНаценка");
	
	Для Каждого СтрокаТЧСообщение Из ТекущиеСообщения Цикл
		
		СтрокаТЧ = ТаблицаСкидкиНаценки.Добавить();
		СтрокаТЧ.КлючСвязи     = -1;
		СтрокаТЧ.СкидкаНаценка = СтрокаТЧСообщение.СкидкаНаценка;
		
		СохраненныеСообщения = ПараметрыРасчета.Сообщения.НайтиСтроки(Новый Структура("СкидкаНаценка", СтрокаТЧСообщение.СкидкаНаценка));
		Если СохраненныеСообщения.Количество() > 0 Тогда
			Для Каждого Сообщение Из СохраненныеСообщения Цикл
				СтрокаТЧ.НапомнитьПозже = Сообщение.НапомнитьПозже;
			КонецЦикла;
		Иначе
			// Новое сообщение
			СтрокаТЧ.НапомнитьПозже = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Формирует таблицу действия скидок (наценок) по источнику
//
// Параметры:
//  Источник  - СправочникСсылка.Склады, СправочникСсылка.ВидыКартЛояльности, СправочникСсылка.СоглашенияСКлиентами -
//              источник действия скидок.
//  Дата      - Дата - дата среза действия скидок (наценок).
//
// Возвращаемое значение:
//   ТаблицаЗначений - таблица содержащая скидки, статус и период действия. Состав:
//   	* СкидкаНаценка - СправочникСсылка.СкидкиНаценки - 
//   	* Статус        - ПеречислениеСсылка.СтатусыДействияСкидок - 
//   	* Использование - Булево - Использование
//   	* ДатаНачала    - Дата - Дата начала
//   	* ДатаОкончания - Дата - Дата окончания
//
Функция ДействиеСкидокНаценокПоИсточнику(Источник, Дата)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДействиеСкидокНаценокСрезПоследних.СкидкаНаценка                                                              КАК СкидкаНаценка,
	|	ЕСТЬNULL(ДействиеСкидокНаценокСрезПоследних.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыДействияСкидок.НеДействует)) КАК Статус,
	|	
	|	ВЫБОР
	|		КОГДА ДействиеСкидокНаценокСрезПоследних.Статус ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Использование,
	|	
	|	ЕСТЬNULL(ДействиеСкидокНаценокСрезПоследних.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаНачала,
	|	
	|	ВЫБОР КОГДА ДействиеСкидокНаценокСрезПоследних.Период ЕСТЬ NULL ТОГДА
	|		ЕСТЬNULL(ДобавитьКДате(ДействиеСкидокНаценокСрезПервых.Период, ДЕНЬ, -1), ДАТАВРЕМЯ(1, 1, 1)) 
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДобавитьКДате(Таблица.Период, ДЕНЬ, -1), ДАТАВРЕМЯ(1, 1, 1))
	|	КОНЕЦ КАК ДатаОкончания
	|	
	|ИЗ
	|	Справочник.СкидкиНаценки КАК ТаблицаСкидкиНаценки
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(&ДатаСреза, Источник = &Источник) КАК ДействиеСкидокНаценокСрезПоследних
	|			ПО ДействиеСкидокНаценокСрезПоследних.СкидкаНаценка = ТаблицаСкидкиНаценки.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МИНИМУМ(Т.Период) КАК Период,
	|			Т.СкидкаНаценка КАК СкидкаНаценка
	|		ИЗ
	|			РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(&ДатаСреза, Источник = &Источник) КАК Срез
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДействиеСкидокНаценок КАК Т
	|				ПО (Т.СкидкаНаценка = Срез.СкидкаНаценка)
	|					И Т.Источник = Срез.Источник
	|					И Т.Период > Срез.Период
	|					И Т.Статус <> Срез.Статус
	|		СГРУППИРОВАТЬ ПО
	|			Т.СкидкаНаценка) КАК Таблица
	|		ПО (Таблица.СкидкаНаценка = ТаблицаСкидкиНаценки.Ссылка)
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДействиеСкидокНаценок.СрезПервых(
	|				&ДатаСреза,
	|				Источник = &Источник) КАК ДействиеСкидокНаценокСрезПервых
	|		ПО ДействиеСкидокНаценокСрезПервых.СкидкаНаценка = ТаблицаСкидкиНаценки.Ссылка
	|ГДЕ
	|	    НЕ ДействиеСкидокНаценокСрезПоследних.Период ЕСТЬ NULL
	|	ИЛИ НЕ ДействиеСкидокНаценокСрезПервых.Период ЕСТЬ NULL
	|");
	
	Запрос.УстановитьПараметр("ДатаСреза", Дата);
	Запрос.УстановитьПараметр("Источник", Источник);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Параметры:
// 	Объект 	- ДанныеФормыСтруктура - должна содержать:
// 				* Ссылка - ДокументСсылка.КоммерческоеПредложениеКлиенту
// 				* Налогообложение - см. КоммерческиеПредложенияДокументыКлиентСерверУТ.НалогообложениеНДСПоНалогообложениюКоммерческихПредложений.Налогообложение
//			- ДокументОбъект.КоммерческоеПредложениеКлиенту - 
//
//	НалогообложениеНДС - см. КоммерческиеПредложенияДокументыКлиентСерверУТ.НалогообложениеНДСПоНалогообложениюКоммерческихПредложений.Налогообложение
//	СтандартнаяОбработка - Булево - возвращаемое значение
Процедура ПриОпределенииНалогообложенияНДС(Объект, НалогообложениеНДС, СтандартнаяОбработка) Экспорт
	
	Если ТипЗнч(Объект) = Тип("ДанныеФормыСтруктура")
		И Объект.Свойство("Ссылка") Тогда
			
		Если ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.КоммерческоеПредложениеКлиенту")
			И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Объект, "Налогообложение") Тогда

			СтандартнаяОбработка = Ложь;
			
			НалогообложениеНДС = 
				КоммерческиеПредложенияДокументыКлиентСерверУТ.НалогообложениеНДСПоНалогообложениюКоммерческихПредложений(Объект.Налогообложение);
				
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаДляОбновленияКешированныхЗапросовСкидкиНаценки()
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СкидкиНаценки.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СкидкиНаценки КАК СкидкиНаценки
	|ГДЕ
	|	НЕ СкидкиНаценки.ЭтоГруппа
	|	И НЕ СкидкиНаценки.СпособПредоставления В
	|		(ВЫБРАТЬ
	|			ДополнительныеОтчетыИОбработки.Ссылка КАК Ссылка
	|		ИЗ
	|			Справочник.ДополнительныеОтчетыИОбработки КАК ДополнительныеОтчетыИОбработки)";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаДляОбновленияКешированныхЗапросовУсловияПредоставленияСкидокНаценок()
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	УсловияПредоставленияСкидокНаценок.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.УсловияПредоставленияСкидокНаценок КАК УсловияПредоставленияСкидокНаценок
	|ГДЕ
	|	(УсловияПредоставленияСкидокНаценок.УсловиеПредоставления = ЗНАЧЕНИЕ(Перечисление.УсловияПредоставленияСкидокНаценок.ЗаРазовыйОбъемПродаж)
	|	ИЛИ
	|		УсловияПредоставленияСкидокНаценок.УсловиеПредоставления = ЗНАЧЕНИЕ(Перечисление.УсловияПредоставленияСкидокНаценок.ЗаНакопленныйОбъемПродаж))";
	
	Возврат ТекстЗапроса;
КонецФункции

#Область ПроцедурыРасчетаСкидокНаценокПоДокументам

// Выполняет расчет скидок по заказу клиента.
//
// Параметры:
//  Объект - ДокументОбъект, ДанныеФормыСтруктура - Объект, в котором требуется рассчитать скидки (наценки).
//  ВходныеПараметры - Структура - Структура со свойствами:
//   * ТолькоПредварительныйРасчет - Булево - Только предварительный расчет.
//   * ПрименятьКОбъекту - Булево - Применять к объекту.
//   * ВосстанавливатьУправляемыеСкидки - Булево - Восстанавливать управляемые скидки (наценки).
//   * УправляемыеСкидки - Массив - Управляемые скидки (наценки).
//
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * ДеревоСкидок - см. СформироватьДеревоСкидок
//   * ТаблицаСкидкиНаценки - ТаблицаЗначений - Таблица с рассчитанными скидками.
//   * ПараметрыРасчета - Структура - Структура параметров расчета.
//
Функция РассчитатьПоЗаказуКлиента(Объект, ВходныеПараметры)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаполнитьКлючиСвязиВТабличнойЧастиТовары(Объект, "Товары");
	
	ИспользоватьНаборы = ПолучитьФункциональнуюОпцию("ИспользоватьНаборы");
	
	// Обработка табличной части "Товары".
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.КлючСвязи          КАК КлючСвязи,
	|	&ПоляНабора,
	|	Товары.Номенклатура       КАК Номенклатура,
	|	Товары.Характеристика     КАК Характеристика,
	|	Товары.Упаковка           КАК Упаковка,
	|	Товары.Серия              КАК Серия,
	|	Товары.Склад              КАК Склад,
	|	Товары.ВидЦены            КАК ВидЦены,
	|	Товары.Количество         КАК Количество,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Товары.Цена                             КАК Цена,
	|	Товары.Цена * Товары.КоличествоУпаковок КАК Сумма
	|ПОМЕСТИТЬ ВТТовары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.КлючСвязи,
	|	&ПоляТаблицы,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	Таблица.Упаковка,
	|	Таблица.Серия,
	|	Таблица.Склад,
	|	Таблица.ВидЦены,
	|	СправочникНоменклатура.ЦеноваяГруппа КАК ЦеноваяГруппа,
	|	Таблица.Количество,
	|	Таблица.КоличествоУпаковок,
	|	Таблица.Цена,
	|	Таблица.Сумма
	|,&ИмяВременнойТаблицы
	|ИЗ
	|	ВТТовары КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО СправочникНоменклатура.Ссылка = Таблица.Номенклатура
	|		,&Соединение
	|ГДЕ
	|	ВЫБОР 
	|		КОГДА &ВозвратТары ТОГДА
	|			Таблица.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	ИНАЧЕ
	|		ИСТИНА
	|	КОНЕЦ
	|; ВЫБРАТЬ &ЗапросНаборы
	|";
	
	ТаблицаТовары = Объект.Товары.Выгрузить();
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("Товары", ТаблицаТовары);
	ПараметрыЗапроса.Вставить("ВозвратТары", Объект.ВернутьМногооборотнуюТару);
	ДанныеПоТоварамИНаборам = ДанныеПоТоварамИНаборам(ТекстЗапроса, ПараметрыЗапроса, ИспользоватьНаборы);
	
	СкидкиНаценки = СкидкиНаценкиДляОптовойТорговли(
		Объект.Дата,
		Объект.Склад,
		Объект.Соглашение,
		Объект.КартаЛояльности);
	
	ПараметрыРасчета = ПараметрыРасчета();
	ПараметрыРасчета.СкидкиНаценки = СкидкиНаценки;
	ПараметрыРасчета.Партнер       = Объект.Партнер;
	ПараметрыРасчета.Соглашение    = Объект.Соглашение;
	ПараметрыРасчета.Регистратор   = Объект.Ссылка;
	
	// Для скидки "За форму оплаты".
	ПараметрыРасчета.ФормаОплаты = Объект.ФормаОплаты;
	
	// Для скидки "За время продажи".
	ПараметрыРасчета.ДеньНедели   = Перечисления.ДниНедели.Получить(ДеньНедели(Объект.Дата) - 1);
	ПараметрыРасчета.ТекущееВремя = ПолучитьТекущееВремяОбъекта(Объект);
	ПараметрыРасчета.ТекущаяДата  = ПолучитьТекущуюДатуОбъекта(Объект);
	
	// Для скидки "За соблюдение графика оплаты".
	ПараметрыРасчета.ГрафикОплаты = Объект.ГрафикОплаты;
	
	// Карты лояльности
	ПараметрыРасчета.КартаЛояльности = Объект.КартаЛояльности;
	
	ПараметрыРасчета.Товары                     = ДанныеПоТоварамИНаборам.Товары;
	ПараметрыРасчета.ВалютаДокумента            = Объект.Валюта;
	ПараметрыРасчета.ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();
	ПараметрыРасчета.БазоваяВалюта              = ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию();
	ПараметрыРасчета.Пользователь               = Объект.Менеджер;
	ПараметрыРасчета.Объект                     = Объект;
	
	ПодготовитьДанныеОВыбранныхУправляемыхСкидках(Объект, ПараметрыРасчета, ВходныеПараметры);
	ПримененныеСкидкиНаценки = РассчитатьДеревоСкидокНаценок(ПараметрыРасчета, ВходныеПараметры);
	
	Если ИспользоватьНаборы И НЕ ВходныеПараметры.ТолькоПредварительныйРасчет Тогда
		РаспределитьСкидкиНабораПоКомплектующим(ПримененныеСкидкиНаценки, ДанныеПоТоварамИНаборам.СоответствиеКлючей, ТаблицаТовары);
	КонецЕсли;
	
	Если ВходныеПараметры.ПрименятьКОбъекту Тогда
		
		ПрименитьРезультатРасчетаКЗаказуКлиента(Объект, ПримененныеСкидкиНаценки);
		
	КонецЕсли;
	
	Возврат ПримененныеСкидкиНаценки;
	
КонецФункции

// Выполняет расчет скидок по заявке на возврат товаров от клиента.
//
// Параметры:
//  Объект - ДокументОбъект, ДанныеФормыСтруктура - Объект, в котором требуется рассчитать скидки (наценки).
//  ВходныеПараметры - Структура - Структура со свойствами:
//   * ТолькоПредварительныйРасчет - Булево - Только предварительный расчет.
//   * ПрименятьКОбъекту - Булево - Применять к объекту.
//   * ВосстанавливатьУправляемыеСкидки - Булево - Восстанавливать управляемые скидки (наценки).
//   * УправляемыеСкидки - Массив - Управляемые скидки (наценки).
//
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * ДеревоСкидок - см. СформироватьДеревоСкидок
//   * ТаблицаСкидкиНаценки - ТаблицаЗначений - Таблица с рассчитанными скидками.
//   * ПараметрыРасчета - Структура - Структура параметров расчета.
//
Функция РассчитатьПоЗаявкеНаВозвратТоваровОтКлиента(Объект, ВходныеПараметры)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаполнитьКлючиСвязиВТабличнойЧастиТовары(Объект, "ЗаменяющиеТовары");
	
	ИспользоватьНаборы = ПолучитьФункциональнуюОпцию("ИспользоватьНаборы");
	
	// Обработка табличной части "Товары".
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.КлючСвязи          КАК КлючСвязи,
	|	&ПоляНабора,
	|	Товары.Номенклатура       КАК Номенклатура,
	|	Товары.Характеристика     КАК Характеристика,
	|	Товары.Упаковка           КАК Упаковка,
	|	Товары.Серия              КАК Серия,
	|	&Склад                    КАК Склад,
	|	Товары.ВидЦены            КАК ВидЦены,
	|	Товары.Количество         КАК Количество,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Товары.Цена                             КАК Цена,
	|	Товары.Цена * Товары.КоличествоУпаковок КАК Сумма
	|ПОМЕСТИТЬ ВТТовары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.КлючСвязи,
	|	&ПоляТаблицы,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	Таблица.Упаковка,
	|	Таблица.Серия,
	|	Таблица.Склад,
	|	Таблица.ВидЦены,
	|	СправочникНоменклатура.ЦеноваяГруппа КАК ЦеноваяГруппа,
	|	Таблица.Количество,
	|	Таблица.КоличествоУпаковок,
	|	Таблица.Цена,
	|	Таблица.Сумма
	|,&ИмяВременнойТаблицы
	|ИЗ
	|	ВТТовары КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО СправочникНоменклатура.Ссылка = Таблица.Номенклатура
	|		,&Соединение
	|ГДЕ
	|	ВЫБОР 
	|		КОГДА &ВозвратТары ТОГДА
	|			Таблица.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	ИНАЧЕ
	|		ИСТИНА
	|	КОНЕЦ
	|; ВЫБРАТЬ &ЗапросНаборы
	|";
	
	ТаблицаТовары = Объект.ЗаменяющиеТовары.Выгрузить();
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("Товары", ТаблицаТовары);
	ПараметрыЗапроса.Вставить("ВозвратТары", Объект.ВернутьМногооборотнуюТару);
	ПараметрыЗапроса.Вставить("Склад", Объект.Склад);
	ДанныеПоТоварамИНаборам = ДанныеПоТоварамИНаборам(ТекстЗапроса, ПараметрыЗапроса, ИспользоватьНаборы);
	
	СкидкиНаценки = СкидкиНаценкиДляОптовойТорговли(
		Объект.Дата,
		Объект.Склад,
		Объект.Соглашение,
		Объект.КартаЛояльности);
	
	ПараметрыРасчета = ПараметрыРасчета();
	ПараметрыРасчета.СкидкиНаценки = СкидкиНаценки;
	ПараметрыРасчета.Партнер       = Объект.Партнер;
	ПараметрыРасчета.Соглашение    = Объект.Соглашение;
	ПараметрыРасчета.Регистратор   = Объект.Ссылка;
	
	// Для скидки "За форму оплаты".
	ПараметрыРасчета.ФормаОплаты = Объект.ФормаОплаты;
	
	// Для скидки "За время продажи".
	ПараметрыРасчета.ДеньНедели   = Перечисления.ДниНедели.Получить(ДеньНедели(Объект.Дата) - 1);
	ПараметрыРасчета.ТекущееВремя = ПолучитьТекущееВремяОбъекта(Объект);
	ПараметрыРасчета.ТекущаяДата  = ПолучитьТекущуюДатуОбъекта(Объект);
	
	// Для скидки "За соблюдение графика оплаты".
	ПараметрыРасчета.ГрафикОплаты = Объект.ГрафикОплаты;
	
	// Карты лояльности
	ПараметрыРасчета.КартаЛояльности = Объект.КартаЛояльности;
	
	ПараметрыРасчета.Товары                     = ДанныеПоТоварамИНаборам.Товары;
	ПараметрыРасчета.ВалютаДокумента            = Объект.Валюта;
	ПараметрыРасчета.ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();
	ПараметрыРасчета.БазоваяВалюта              = ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию();
	ПараметрыРасчета.Пользователь               = Объект.Менеджер;
	ПараметрыРасчета.Объект                     = Объект;
	
	ПодготовитьДанныеОВыбранныхУправляемыхСкидках(Объект, ПараметрыРасчета, ВходныеПараметры);
	ПримененныеСкидкиНаценки = РассчитатьДеревоСкидокНаценок(ПараметрыРасчета, ВходныеПараметры);
	
	Если ИспользоватьНаборы И НЕ ВходныеПараметры.ТолькоПредварительныйРасчет Тогда
		РаспределитьСкидкиНабораПоКомплектующим(ПримененныеСкидкиНаценки, ДанныеПоТоварамИНаборам.СоответствиеКлючей, ТаблицаТовары);
	КонецЕсли;
	
	Если ВходныеПараметры.ПрименятьКОбъекту Тогда
		
		ПрименитьРезультатРасчетаКВозвратуТоваровОтКлиента(Объект, ПримененныеСкидкиНаценки);
		
	КонецЕсли;
	
	Возврат ПримененныеСкидкиНаценки;
	
КонецФункции

// Выполняет расчет скидок по коммерческому предложению клиенту.
//
// Параметры:
//  Объект - ДокументОбъект, ДанныеФормыСтруктура - Объект, в котором требуется рассчитать скидки (наценки).
//  ВходныеПараметры - Структура - Структура со свойствами:
//   * ТолькоПредварительныйРасчет - Булево - Только предварительный расчет.
//   * ПрименятьКОбъекту - Булево - Применять к объекту.
//   * ВосстанавливатьУправляемыеСкидки - Булево - Восстанавливать управляемые скидки (наценки).
//   * УправляемыеСкидки - Массив - Управляемые скидки (наценки).
//   * ИмяКолонкиУпаковка - Строка - Имя колонки с данными упаковки (не обязательно).
//
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * ДеревоСкидок - ДеревоЗначений - Дерево скидок (наценок).
//   * ТаблицаСкидкиНаценки - ТаблицаЗначений - Таблица с рассчитанными скидками.
//   * ПараметрыРасчета - Структура - Структура параметров расчета.
//
Функция РассчитатьПоКоммерческомуПредложениюКлиенту(Объект, ВходныеПараметры)
	
	УстановитьПривилегированныйРежим(Истина);
	
	СкидкиНаценкиСервер.ЗаполнитьКлючиСвязиВТабличнойЧастиТовары(Объект, "Товары");
	
	ИспользоватьНаборы = Ложь;
	
	ИмяКолонкиУпаковка           = "ЕдиницаИзмерения";
	ИмяКолонкиКоличествоУпаковок = "Количество";
	
	Если ВходныеПараметры.Свойство("ИмяКолонкиУпаковка") 
		И НЕ ПустаяСтрока(ВходныеПараметры.ИмяКолонкиУпаковка) Тогда
		ИмяКолонкиУпаковка = "Товары." + ВходныеПараметры.ИмяКолонкиУпаковка;
	КонецЕсли;
	
	Если ВходныеПараметры.Свойство("ИмяКолонкиКоличествоУпаковок") Тогда
		 ИмяКолонкиКоличествоУпаковок = "Товары." + ИмяКолонкиКоличествоУпаковок;
	КонецЕсли;
	
	// Обработка табличной части "Товары".
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.КлючСвязи                                    КАК КлючСвязи,
	|	Товары.Номенклатура                                 КАК Номенклатура,
	|	Товары.Характеристика                               КАК Характеристика,
	|	&ТоварыУпаковка                                     КАК Упаковка,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)            КАК Склад,
	|	Товары.ВидЦены                                      КАК ВидЦены,
	|	Товары.Количество                                   КАК Количество,
	|	&ТоварыКоличествоУпаковок                           КАК КоличествоУпаковок,
	|	Товары.Цена                                         КАК Цена,
	|	Товары.Цена * &ТоварыКоличествоУпаковок             КАК Сумма
	|ПОМЕСТИТЬ ВТТовары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.КлючСвязи,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	Таблица.Упаковка,
	|	Таблица.Серия,
	|	Таблица.Склад,
	|	Таблица.ВидЦены,
	|	СправочникНоменклатура.ЦеноваяГруппа КАК ЦеноваяГруппа,
	|	Таблица.Количество,
	|	Таблица.КоличествоУпаковок,
	|	Таблица.Цена,
	|	Таблица.Сумма
	|ИЗ
	|	ВТТовары КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО СправочникНоменклатура.Ссылка = Таблица.Номенклатура
	|		//&Соединение
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТоварыУпаковка",           ИмяКолонкиУпаковка);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТоварыКоличествоУпаковок", ИмяКолонкиКоличествоУпаковок);
	
	ТаблицаТовары = Объект.Товары.Выгрузить();

	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("Товары", ТаблицаТовары);
	ДанныеПоТоварамИНаборам = СкидкиНаценкиСервер.ДанныеПоТоварамИНаборам(ТекстЗапроса, ПараметрыЗапроса, ИспользоватьНаборы);
	
	ПараметрыСкидокДляОптовойТорговли = Новый Структура;
	ПараметрыСкидокДляОптовойТорговли.Вставить("НеИспользоватьСоглашенияСКлиентами", Истина);
	
	СкидкиНаценки = СкидкиНаценкиСервер.СкидкиНаценкиДляОптовойТорговли(
		Объект.Дата,
		Справочники.Склады.ПустаяСсылка(),
		Справочники.СоглашенияСКлиентами.ПустаяСсылка(),
		Объект.КартаЛояльности);
	
	ПараметрыРасчета = СкидкиНаценкиСервер.ПараметрыРасчета();
	ПараметрыРасчета.СкидкиНаценки = СкидкиНаценки;
	ПараметрыРасчета.Партнер       = Объект.Клиент;
	ПараметрыРасчета.Регистратор   = Объект.Ссылка;
	
	// Для скидки "За время продажи".
	ПараметрыРасчета.ДеньНедели   = Перечисления.ДниНедели.Получить(ДеньНедели(Объект.Дата) - 1);
	ПараметрыРасчета.ТекущееВремя = СкидкиНаценкиСервер.ПолучитьТекущееВремяОбъекта(Объект);
	ПараметрыРасчета.ТекущаяДата  = СкидкиНаценкиСервер.ПолучитьТекущуюДатуОбъекта(Объект);
	
	// Карты лояльности
	ПараметрыРасчета.КартаЛояльности = Объект.КартаЛояльности;
	
	ПараметрыРасчета.Товары                     = ДанныеПоТоварамИНаборам.Товары;
	ПараметрыРасчета.ВалютаДокумента            = Объект.Валюта;
	ПараметрыРасчета.ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();
	ПараметрыРасчета.БазоваяВалюта              = ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию();
	ПараметрыРасчета.Пользователь               = Объект.Менеджер;
	ПараметрыРасчета.Объект                     = Объект;
	
	СкидкиНаценкиСервер.ПодготовитьДанныеОВыбранныхУправляемыхСкидках(Объект, ПараметрыРасчета, ВходныеПараметры);
	ПримененныеСкидкиНаценки = СкидкиНаценкиСервер.РассчитатьДеревоСкидокНаценок(ПараметрыРасчета, ВходныеПараметры);
	
	Если ВходныеПараметры.ПрименятьКОбъекту Тогда
		
		ПрименитьРезультатРасчетаККоммерческомуПредложениюКлиенту(Объект, ПримененныеСкидкиНаценки);
		
	КонецЕсли;
	
	Возврат ПримененныеСкидкиНаценки;
	
КонецФункции

// Выполняет расчет скидок по реализации товаров и услуг.
//
// Параметры:
//  Объект - ДокументОбъект.РеализацияТоваровУслуг, ДанныеФормыСтруктура - Объект, в котором требуется рассчитать
//  																		 скидки (наценки).
//  ВходныеПараметры - Структура - Структура со свойствами:
//   * ТолькоПредварительныйРасчет - Булево - Только предварительный расчет.
//   * ПрименятьКОбъекту - Булево - Применять к объекту.
//   * ВосстанавливатьУправляемыеСкидки - Булево - Восстанавливать управляемые скидки (наценки).
//   * УправляемыеСкидки - Массив - Управляемые скидки (наценки).
//
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * ДеревоСкидок - см. СформироватьДеревоСкидок
//   * ТаблицаСкидкиНаценки - ТаблицаЗначений - Таблица с рассчитанными скидками.
//   * ПараметрыРасчета - Структура - Структура параметров расчета.
//
Функция РассчитатьПоРеализацииТоваровУслуг(Объект, ВходныеПараметры)
	
	Если ВходныеПараметры.Свойство("ЗаполнитьРеализациюПоЗаказу") 
		И ВходныеПараметры.ЗаполнитьРеализациюПоЗаказу Тогда
		
		Возврат РассчитатьПоРеализацииТоваровУслугПоЗаказу(Объект);
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаполнитьКлючиСвязиВТабличнойЧастиТовары(Объект, "Товары", ВходныеПараметры.РеализацияСверхЗаказа);
	
	ИспользоватьНаборы = ПолучитьФункциональнуюОпцию("ИспользоватьНаборы");
	
	// Обработка табличной части "Товары".
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.КодСтроки          КАК КодСтроки,
	|	Товары.КлючСвязи          КАК КлючСвязи,
	|	&ПоляНабора,
	|	Товары.Номенклатура       КАК Номенклатура,
	|	Товары.Характеристика     КАК Характеристика,
	|	Товары.Упаковка           КАК Упаковка,
	|	Товары.Серия              КАК Серия,
	|	Товары.Склад              КАК Склад,
	|	Товары.ВидЦены            КАК ВидЦены,
	|	Товары.Количество         КАК Количество,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Товары.Цена                             КАК Цена,
	|	Товары.КоличествоУпаковок * Товары.Цена КАК Сумма
	|ПОМЕСТИТЬ ВТТовары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.КлючСвязи,
	|	&ПоляТаблицы,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	Таблица.Упаковка,
	|	Таблица.Серия,
	|	Таблица.Склад,
	|	Таблица.ВидЦены,
	|	СправочникНоменклатура.ЦеноваяГруппа КАК ЦеноваяГруппа,
	|	Таблица.Количество,
	|	Таблица.КоличествоУпаковок,
	|	Таблица.Цена,
	|	Таблица.Сумма
	|,&ИмяВременнойТаблицы
	|ИЗ
	|	ВТТовары КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО СправочникНоменклатура.Ссылка = Таблица.Номенклатура
	|		,&Соединение
	|ГДЕ
	|	ВЫБОР 
	|		КОГДА &ВозвратТары ТОГДА
	|			Таблица.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	ИНАЧЕ
	|		ИСТИНА
	|	КОНЕЦ
	|	И ВЫБОР 
	|		КОГДА &РеализацияСверхЗаказа ТОГДА 
	|			Таблица.КодСтроки = 0 
	|		ИНАЧЕ 
	|			ИСТИНА
	|	КОНЕЦ
	|; ВЫБРАТЬ &ЗапросНаборы
	|";
	
	ТаблицаТовары = Объект.Товары.Выгрузить();
	
	ПараметрыЗапроса = ПараметрыРасчета();
	ПараметрыЗапроса.Вставить("Товары", ТаблицаТовары);
	ПараметрыЗапроса.Вставить("РеализацияСверхЗаказа", ВходныеПараметры.РеализацияСверхЗаказа);
	ПараметрыЗапроса.Вставить("ВозвратТары", Объект.ВернутьМногооборотнуюТару);
	
	ДанныеПоТоварамИНаборам = ДанныеПоТоварамИНаборам(ТекстЗапроса, ПараметрыЗапроса, ИспользоватьНаборы);
	
	СкидкиНаценки = СкидкиНаценкиДляОптовойТорговли(
		Объект.Дата,
		Объект.Склад,
		Объект.Соглашение,
		Объект.КартаЛояльности);
	
	ПараметрыРасчета = ПараметрыРасчета();
	ПараметрыРасчета.СкидкиНаценки = СкидкиНаценки;
	ПараметрыРасчета.Партнер       = Объект.Партнер;
	ПараметрыРасчета.Соглашение    = Объект.Соглашение;
	ПараметрыРасчета.Регистратор   = Объект.Ссылка;
	
	// Для скидки "За форму оплаты".
	ПараметрыРасчета.ФормаОплаты = Объект.ФормаОплаты;
	
	// Для скидки "За время продажи".
	ПараметрыРасчета.ДеньНедели   = Перечисления.ДниНедели.Получить(ДеньНедели(Объект.Дата) - 1);
	ПараметрыРасчета.ТекущееВремя = ПолучитьТекущееВремяОбъекта(Объект);
	ПараметрыРасчета.ТекущаяДата  = ПолучитьТекущуюДатуОбъекта(Объект);
	
	// Для скидки "За соблюдение графика оплаты".
	ПараметрыРасчета.ГрафикОплаты = Неопределено;
	
	// Карты лояльности
	ПараметрыРасчета.КартаЛояльности = Объект.КартаЛояльности;
	
	ПараметрыРасчета.Товары                     = ДанныеПоТоварамИНаборам.Товары;
	ПараметрыРасчета.ВалютаДокумента            = Объект.Валюта;
	ПараметрыРасчета.ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();
	ПараметрыРасчета.БазоваяВалюта              = ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию();
	ПараметрыРасчета.Пользователь               = Объект.Менеджер;
	ПараметрыРасчета.Объект                     = Объект;
	
	ПодготовитьДанныеОВыбранныхУправляемыхСкидках(Объект, ПараметрыРасчета, ВходныеПараметры);
	ПримененныеСкидкиНаценки = РассчитатьДеревоСкидокНаценок(ПараметрыРасчета, ВходныеПараметры);
	
	Если ИспользоватьНаборы И НЕ ВходныеПараметры.ТолькоПредварительныйРасчет Тогда
		РаспределитьСкидкиНабораПоКомплектующим(ПримененныеСкидкиНаценки, ДанныеПоТоварамИНаборам.СоответствиеКлючей, ТаблицаТовары);
	КонецЕсли;
	
	Если ВходныеПараметры.ПрименятьКОбъекту Тогда
		
		ПрименитьРезультатРасчетаКРеализацииТоваровУслуг(Объект, ПримененныеСкидкиНаценки, ВходныеПараметры.РеализацияСверхЗаказа);
		
	КонецЕсли;
	
	Возврат ПримененныеСкидкиНаценки;
	
КонецФункции

// Выполняет расчет скидок по реализации товаров и услуг.
//
// Параметры:
//  Объект - ДокументОбъект, ДанныеФормыСтруктура - Объект, в котором требуется рассчитать скидки (наценки).
//
// Возвращаемое значение:
//  Структура - Структура без свойств
//
Функция РассчитатьПоРеализацииТоваровУслугПоЗаказу(Объект)
	
	Объект.СкидкиНаценки.Очистить();
	Объект.НачислениеБонусныхБаллов.Очистить();
	
	ЗаполнитьКлючиСвязиВТабличнойЧастиТовары(Объект, "Товары");
	
	ИспользоватьБонусныеПрограммыЛояльности = ПолучитьФункциональнуюОпцию("ИспользоватьБонусныеПрограммыЛояльности");

	ДанныеПоЗаказу = СкидкиНаценкиЗаполнениеСервер.СкидкиНаценкиПоЗаказу(Объект);
	СкидкиНаценкиЗаказа = ДанныеПоЗаказу.СкидкиНаценкиЗаказа;
	ТаблицаТоваров = ДанныеПоЗаказу.ТаблицаТоваров;
	
	Если ИспользоватьБонусныеПрограммыЛояльности Тогда
	
		НачислениеБонусныхБалловЗаказа = ДанныеПоЗаказу.НачислениеБонусныхБалловЗаказа;
	
		БонуснаяПрограммаЛояльности = Новый Структура("БонуснаяПрограммаЛояльности, 
			|ВалютаКонвертацииБонусов, 
			|КурсКонвертацииБонусовВВалюту,
			|СтруктураКурсовВалютыКонвертации,
			|СтруктураКурсовВалютыДокумента");
		
		БонусныеПрограммыЛояльности = НачислениеБонусныхБалловЗаказа.Скопировать();
		
		БонусныеПрограммыЛояльности.Свернуть("БонуснаяПрограммаЛояльности, 
			|ВалютаКонвертацииБонусов, 
			|КурсКонвертацииБонусовВВалюту");
		
		Если БонусныеПрограммыЛояльности.Количество() > 1 Тогда
			
			ИспользоватьБонусныеПрограммыЛояльности = Ложь;
			
			ТекстОшибки = НСтр("ru='Обнаружены разные программы лояльности для начисления бонусных баллов. Заказы 
				|с разными бонусными программами лояльности не могут быть отгружены в одном документе.'");
			ВызватьИсключение ТекстОшибки;
		ИначеЕсли БонусныеПрограммыЛояльности.Количество() = 1 Тогда
			ЗаполнитьЗначенияСвойств(БонуснаяПрограммаЛояльности, БонусныеПрограммыЛояльности[0]);
			
			Если Объект.Валюта <> БонуснаяПрограммаЛояльности.ВалютаКонвертацииБонусов Тогда
				
				БонуснаяПрограммаЛояльности.СтруктураКурсовВалютыКонвертации = РаботаСКурсамиВалютУТ.ПолучитьКурсВалюты(БонуснаяПрограммаЛояльности.ВалютаКонвертацииБонусов, 
					ТекущаяДатаСеанса(),
					ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Объект.Организация));
				
				БонуснаяПрограммаЛояльности.СтруктураКурсовВалютыДокумента = РаботаСКурсамиВалютУТ.ПолучитьКурсВалюты(Объект.Валюта, 
					ТекущаяДатаСеанса(),
					ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Объект.Организация));
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	НомерОбрабатываемойСтроки = 0;
	ПредыдущийКодСтроки       = 0;
	ПредыдущийЗаказ           = Неопределено;
	Для Каждого ВыборкаТовары Из ТаблицаТоваров Цикл
		
		НайденныеСтроки = Объект.Товары.НайтиСтроки(Новый Структура("ЗаказКлиента, КодСтроки", ВыборкаТовары.ЗаказКлиента, ВыборкаТовары.КодСтроки));
		Если НайденныеСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПредыдущийКодСтроки = ВыборкаТовары.КодСтроки И ПредыдущийЗаказ = ВыборкаТовары.ЗаказКлиента Тогда
			НомерОбрабатываемойСтроки = НомерОбрабатываемойСтроки + 1;
		Иначе
			НомерОбрабатываемойСтроки = 0;
		КонецЕсли;
		
		СтрокаТаблицы = НайденныеСтроки[НомерОбрабатываемойСтроки];
		
		ПредыдущийКодСтроки = ВыборкаТовары.КодСтроки;
		ПредыдущийЗаказ     = ВыборкаТовары.ЗаказКлиента;
		
		СписокПолей = "ПроцентАвтоматическойСкидки, ПроцентРучнойСкидки";
		
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ВыборкаТовары, СписокПолей);
		
		Если ВыборкаТовары.СуммаАвтоматическойСкидки = Null или ВыборкаТовары.СуммаРучнойСкидки = Null Тогда
			ТекстОшибки = НСтр("ru='Возникла проблемная ситуация при переносе скидок из заказа. Возможно не задан курс для валюты ""%Валюта%""'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Валюта%", Объект.ВалютаВзаиморасчетов);
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;
		
		ТекСуммаАвтоматическойСкидки = Окр(
				ВыборкаТовары.ЗаказСуммаАвтоматическойСкидки 
				* (ВыборкаТовары.РеализацияКоличество / ВыборкаТовары.ЗаказКоличество) 
				* ВыборкаТовары.ВалютаКоэффициентПересчета, 
			2);
		ТекСуммаРучнойСкидки = Окр(
				ВыборкаТовары.ЗаказСуммаРучнойСкидки 
				* (ВыборкаТовары.РеализацияКоличество / ВыборкаТовары.ЗаказКоличество) 
				* ВыборкаТовары.ВалютаКоэффициентПересчета, 
			2);
		ТекСуммаНачисленныхБонусныхБалловВВалюте = Окр(
				ВыборкаТовары.ЗаказСуммаНачисленныхБонусныхБалловВВалюте 
				* (ВыборкаТовары.РеализацияКоличество / ВыборкаТовары.ЗаказКоличество) 
				* ВыборкаТовары.ВалютаКоэффициентПересчета, 
			2);
		ТекСуммаБонусныхБалловКСписаниюВВалюте = Окр(
				ВыборкаТовары.ЗаказСуммаБонусныхБалловКСписаниюВВалюте 
				* (ВыборкаТовары.РеализацияКоличество / ВыборкаТовары.ЗаказКоличество) 
				* ВыборкаТовары.ВалютаКоэффициентПересчета, 
			2);
		ТекСуммаБонусныхБалловКСписанию = Окр(
				ВыборкаТовары.ЗаказСуммаБонусныхБалловКСписанию 
				* (ВыборкаТовары.РеализацияКоличество / ВыборкаТовары.ЗаказКоличество), 
			2);
		
		СтрокаТаблицы.СуммаАвтоматическойСкидки 				= ТекСуммаАвтоматическойСкидки;
		СтрокаТаблицы.СуммаРучнойСкидки 						= ТекСуммаРучнойСкидки;
		СтрокаТаблицы.СуммаНачисленныхБонусныхБалловВВалюте 	= ТекСуммаНачисленныхБонусныхБалловВВалюте;
		СтрокаТаблицы.СуммаБонусныхБалловКСписаниюВВалюте 		= ТекСуммаБонусныхБалловКСписаниюВВалюте;
		СтрокаТаблицы.СуммаБонусныхБалловКСписанию 				= ТекСуммаБонусныхБалловКСписанию;
		
		// Пересчет сумм
		СтруктураДействий = Новый Структура;
		КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
		СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Объект);
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать, ПересчитыватьСуммуРучнойСкидки", Ложь, Ложь));
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Ложь));
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомСкидкиБонуснымиБаллами");
		СтруктураДействий.Вставить("ОчиститьСуммуВзаиморасчетов");
		СтруктураДействий.Вставить("ПересчитатьСумму", "КоличествоУпаковок");
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТаблицы, СтруктураДействий, КэшированныеЗначения);
		
		СуммаКРаспределению = СтрокаТаблицы.СуммаАвтоматическойСкидки + СтрокаТаблицы.СуммаНачисленныхБонусныхБалловВВалюте;
		
		Если СтрокаТаблицы.КлючСвязи <> 0 Тогда
		
			НайденныеСтроки = СкидкиНаценкиЗаказа.НайтиСтроки(Новый Структура("ЗаказКлиента, КлючСвязи", СтрокаТаблицы.ЗаказКлиента, ВыборкаТовары.КлючСвязи));
			Если НайденныеСтроки.Количество() <> 0 Тогда
				Для Каждого СтрокаСкидкиЗаказа Из НайденныеСтроки Цикл
					
					СтрокаСкидки 			= Объект.СкидкиНаценки.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаСкидки, СтрокаСкидкиЗаказа);
					СтрокаСкидки.КлючСвязи	= СтрокаТаблицы.КлючСвязи;
 					СтрокаСкидки.Сумма 		= (ВыборкаТовары.РеализацияКоличество / ВыборкаТовары.ЗаказКоличество) * СтрокаСкидки.Сумма;
					
					СуммаКРаспределению 	= СуммаКРаспределению - СтрокаСкидки.Сумма;
					
				КонецЦикла;
				Если СуммаКРаспределению <> 0 Тогда
					СтрокаСкидки.Сумма = СтрокаСкидки.Сумма + СуммаКРаспределению;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
			
		Если ИспользоватьБонусныеПрограммыЛояльности Тогда
			СуммаКРаспределению = СтрокаТаблицы.СуммаНачисленныхБонусныхБалловВВалюте;

			Если ЗначениеЗаполнено(БонуснаяПрограммаЛояльности.ВалютаКонвертацииБонусов)
				И Объект.Валюта <> БонуснаяПрограммаЛояльности.ВалютаКонвертацииБонусов Тогда
				
				СуммаКРаспределению = РаботаСКурсамиВалютУТ.ПересчитатьПоКурсу(
					СуммаКРаспределению,
					БонуснаяПрограммаЛояльности.СтруктураКурсовВалютыДокумента,
					БонуснаяПрограммаЛояльности.СтруктураКурсовВалютыКонвертации);
					
			КонецЕсли;
			
			Если СуммаКРаспределению <> 0
				И ЗначениеЗаполнено(БонуснаяПрограммаЛояльности.КурсКонвертацииБонусовВВалюту) Тогда
				
				СуммаКРаспределению = СуммаКРаспределению / БонуснаяПрограммаЛояльности.КурсКонвертацииБонусовВВалюту;
			КонецЕсли;
			
			Если СтрокаТаблицы.КлючСвязи <> 0 Тогда
			
				НайденныеСтроки = НачислениеБонусныхБалловЗаказа.НайтиСтроки(Новый Структура("ЗаказКлиента, КлючСвязи", СтрокаТаблицы.ЗаказКлиента, ВыборкаТовары.КлючСвязи));
				Если НайденныеСтроки.Количество() <> 0 Тогда
					
					Для Каждого СтрокаБонусныеБаллыЗаказа Из НайденныеСтроки Цикл
						
						СтрокаБонусныеБаллы = Объект.НачислениеБонусныхБаллов.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаБонусныеБаллы, СтрокаБонусныеБаллыЗаказа);
						СтрокаБонусныеБаллы.СуммаБонусныхБаллов = (ВыборкаТовары.РеализацияКоличество / ВыборкаТовары.ЗаказКоличество) * СтрокаБонусныеБаллы.СуммаБонусныхБаллов;
						
						СуммаКРаспределению = СуммаКРаспределению - СтрокаБонусныеБаллы.СуммаБонусныхБаллов;
						
					КонецЦикла;
					
					Если СуммаКРаспределению <> 0 Тогда
						СтрокаСкидки.Сумма = СтрокаСкидки.Сумма + СуммаКРаспределению;
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
			
	КонецЦикла;
	
	Объект.СкидкиРассчитаны = Истина;
	
	Возврат Новый Структура;
	
КонецФункции

// Определяется использовании скидки (наценки) для начисления бонусных баллов.
// 
// Параметры:
//  СкидкаНаценка - СправочникСсылка.СкидкиНаценки - - скидка (наценка), для которой производится проверка
// 
// Возвращаемое значение:
//  Булево - Истина , используется
Функция ЭтоСкидкаДляНачислениеБонусныхБаллов(СкидкаНаценка) Экспорт
	
	Если Не ЗначениеЗаполнено(СкидкаНаценка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА
	|			СкидкиНаценки.СпособПримененияСкидки = ЗНАЧЕНИЕ(Перечисление.СпособыПримененияСкидокНаценок.НачислитьБонусныеБаллы)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоСкидкаДляНачислениеБонусныхБаллов
	|ИЗ
	|	Справочник.СкидкиНаценки КАК СкидкиНаценки
	|ГДЕ
	|	СкидкиНаценки.Ссылка = &СкидкаНаценка";
	
	Запрос.УстановитьПараметр("СкидкаНаценка", СкидкаНаценка);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат[0].ЭтоСкидкаДляНачислениеБонусныхБаллов;
	
КонецФункции

// Выполняет расчет скидок по акту выполненных работ.
//
// Параметры:
//  Объект - ДокументОбъект, ДанныеФормыСтруктура - Объект, в котором требуется рассчитать скидки (наценки).
//  ВходныеПараметры - Структура - Структура со свойствами:
//   * ТолькоПредварительныйРасчет - Булево - Только предварительный расчет.
//   * ПрименятьКОбъекту - Булево - Применять к объекту.
//   * ВосстанавливатьУправляемыеСкидки - Булево - Восстанавливать управляемые скидки (наценки).
//   * УправляемыеСкидки - Массив - Управляемые скидки (наценки).
//
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * ДеревоСкидок - см. СформироватьДеревоСкидок
//   * ТаблицаСкидкиНаценки - ТаблицаЗначений - Таблица с рассчитанными скидками.
//   * ПараметрыРасчета - Структура - Структура параметров расчета.
//
Функция РассчитатьПоАктуВыполненныхРабот(Объект, ВходныеПараметры)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаполнитьКлючиСвязиВТабличнойЧастиТовары(Объект, "Услуги");
	
	ИспользоватьНаборы = ПолучитьФункциональнуюОпцию("ИспользоватьНаборы");
	
	// Обработка табличной части "Услуги".
	ТаблицаТовары = Объект.Услуги.Выгрузить(
		,
		"КлючСвязи,
		|НоменклатураНабора,
		|ХарактеристикаНабора,
		|Номенклатура,
		|Характеристика,
		|ВидЦены,
		|Количество,
		|Цена");
	ТаблицаТовары.Колонки.Добавить("Упаковка",           Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения"));
	ТаблицаТовары.Колонки.Добавить("Серия",              Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	ТаблицаТовары.Колонки.Добавить("КоличествоУпаковок", Новый ОписаниеТипов("Число"));
	Для Каждого СтрокаТЧ Из ТаблицаТовары Цикл
		СтрокаТЧ.КоличествоУпаковок = СтрокаТЧ.Количество;
	КонецЦикла;
	
	// Обработка табличной части "Товары".
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.КлючСвязи            КАК КлючСвязи,
	|	&ПоляНабора,
	|	Товары.Номенклатура         КАК Номенклатура,
	|	Товары.Характеристика       КАК Характеристика,
	|	Товары.Упаковка             КАК Упаковка,
	|	Товары.Серия                КАК Серия,
	|	&Склад                      КАК Склад,
	|	Товары.ВидЦены              КАК ВидЦены,
	|	Товары.Количество           КАК Количество,
	|	Товары.КоличествоУпаковок   КАК КоличествоУпаковок,
	|	Товары.Цена                             КАК Цена,
	|	Товары.Цена * Товары.КоличествоУпаковок КАК Сумма
	|ПОМЕСТИТЬ ВТТовары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.КлючСвязи,
	|	&ПоляТаблицы,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	Таблица.Упаковка,
	|	Таблица.Серия,
	|	Таблица.Склад,
	|	Таблица.ВидЦены,
	|	СправочникНоменклатура.ЦеноваяГруппа КАК ЦеноваяГруппа,
	|	Таблица.Количество,
	|	Таблица.КоличествоУпаковок,
	|	Таблица.Цена,
	|	Таблица.Сумма
	|,&ИмяВременнойТаблицы
	|ИЗ
	|	ВТТовары КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО СправочникНоменклатура.Ссылка = Таблица.Номенклатура
	|		,&Соединение
	|; ВЫБРАТЬ &ЗапросНаборы
	|";
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("Товары", ТаблицаТовары);
	ПараметрыЗапроса.Вставить("Склад", Справочники.Склады.ПустаяСсылка());
	ДанныеПоТоварамИНаборам = ДанныеПоТоварамИНаборам(ТекстЗапроса, ПараметрыЗапроса, ИспользоватьНаборы);
	
	СкидкиНаценки = СкидкиНаценкиДляУслуг(
		Объект.Дата,
		Объект.Соглашение,
		Объект.КартаЛояльности);
	
	ПараметрыРасчета = ПараметрыРасчета();
	ПараметрыРасчета.СкидкиНаценки = СкидкиНаценки;
	ПараметрыРасчета.Партнер       = Объект.Партнер;
	ПараметрыРасчета.Соглашение    = Объект.Соглашение;
	ПараметрыРасчета.Регистратор   = Объект.Ссылка;
	
	// Для скидки "За форму оплаты".
	ПараметрыРасчета.ФормаОплаты = Объект.ФормаОплаты;
	
	// Для скидки "За время продажи".
	ПараметрыРасчета.ДеньНедели   = Перечисления.ДниНедели.Получить(ДеньНедели(Объект.Дата) - 1);
	ПараметрыРасчета.ТекущееВремя = ПолучитьТекущееВремяОбъекта(Объект);
	ПараметрыРасчета.ТекущаяДата  = ПолучитьТекущуюДатуОбъекта(Объект);
	
	// Для скидки "За соблюдение графика оплаты".
	ПараметрыРасчета.ГрафикОплаты = Неопределено;
	
	// Карты лояльности
	ПараметрыРасчета.КартаЛояльности = Объект.КартаЛояльности;
	
	ПараметрыРасчета.Товары                     = ДанныеПоТоварамИНаборам.Товары;
	ПараметрыРасчета.ВалютаДокумента            = Объект.Валюта;
	ПараметрыРасчета.ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();
	ПараметрыРасчета.БазоваяВалюта              = ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию();
	ПараметрыРасчета.Пользователь               = Объект.Менеджер;
	ПараметрыРасчета.Объект                     = Объект;
	
	ПодготовитьДанныеОВыбранныхУправляемыхСкидках(Объект, ПараметрыРасчета, ВходныеПараметры);
	ПримененныеСкидкиНаценки = РассчитатьДеревоСкидокНаценок(ПараметрыРасчета, ВходныеПараметры);
	
	Если ИспользоватьНаборы И НЕ ВходныеПараметры.ТолькоПредварительныйРасчет Тогда
		РаспределитьСкидкиНабораПоКомплектующим(ПримененныеСкидкиНаценки, ДанныеПоТоварамИНаборам.СоответствиеКлючей, ТаблицаТовары);
	КонецЕсли;
	
	Если ВходныеПараметры.ПрименятьКОбъекту Тогда
		
		ПрименитьРезультатРасчетаКАктуВыполненныхРабот(Объект, ПримененныеСкидкиНаценки);
		
	КонецЕсли;
	
	Возврат ПримененныеСкидкиНаценки;
	
КонецФункции

// Выполняет расчет скидок по чеку ККМ.
//
// Параметры:
//  Объект - ДокументОбъект, ДанныеФормыСтруктура - Объект, в котором требуется рассчитать скидки (наценки).
//  ВходныеПараметры - Структура - Структура со свойствами:
//   * ТолькоПредварительныйРасчет - Булево - Только предварительный расчет.
//   * ПрименятьКОбъекту - Булево - Применять к объекту.
//   * ВосстанавливатьУправляемыеСкидки - Булево - Восстанавливать управляемые скидки (наценки).
//   * УправляемыеСкидки - Массив - Управляемые скидки (наценки).
//
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * ДеревоСкидок - см. СформироватьДеревоСкидок
//   * ТаблицаСкидкиНаценки - ТаблицаЗначений - Таблица с рассчитанными скидками.
//   * ПараметрыРасчета - Структура - Структура параметров расчета.
//
Функция РассчитатьПоЧекуККМ(Объект, ВходныеПараметры)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаполнитьКлючиСвязиВТабличнойЧастиТовары(Объект, "Товары");
	
	ИспользоватьНаборы = ПолучитьФункциональнуюОпцию("ИспользоватьНаборы");
	
	// Обработка табличной части "Товары".
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.КлючСвязи          КАК КлючСвязи,
	|	&ПоляНабора,
	|	Товары.Номенклатура       КАК Номенклатура,
	|	Товары.Характеристика     КАК Характеристика,
	|	Товары.Упаковка           КАК Упаковка,
	|	Товары.Серия              КАК Серия,
	|	&Склад                    КАК Склад,
	|	&ВидЦены                  КАК ВидЦены,
	|	Товары.Количество         КАК Количество,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Товары.Цена                             КАК Цена,
	|	Товары.Цена * Товары.КоличествоУпаковок КАК Сумма
	|ПОМЕСТИТЬ ВТТовары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.КлючСвязи,
	|	&ПоляТаблицы,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	Таблица.Упаковка,
	|	Таблица.Серия,
	|	Таблица.Склад,
	|	Таблица.ВидЦены,
	|	СправочникНоменклатура.ЦеноваяГруппа КАК ЦеноваяГруппа,
	|	Таблица.Количество,
	|	Таблица.КоличествоУпаковок,
	|	Таблица.Цена,
	|	Таблица.Сумма
	|,&ИмяВременнойТаблицы
	|ИЗ
	|	ВТТовары КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО СправочникНоменклатура.Ссылка = Таблица.Номенклатура
	|		,&Соединение
	|; ВЫБРАТЬ &ЗапросНаборы
	|";
	
	ТаблицаТовары = Объект.Товары.Выгрузить();
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("Товары", ТаблицаТовары);
	ПараметрыЗапроса.Вставить("ВидЦены", Объект.ВидЦены);
	ПараметрыЗапроса.Вставить("Склад", Объект.Склад);
	ДанныеПоТоварамИНаборам = ДанныеПоТоварамИНаборам(ТекстЗапроса, ПараметрыЗапроса, ИспользоватьНаборы);
	
	СкидкиНаценки = СкидкиНаценкиДляРозничнойТорговли(
		Объект.Дата,
		Объект.Склад,
		Объект.КартаЛояльности);
	
	ПараметрыРасчета = ПараметрыРасчета();
	ПараметрыРасчета.СкидкиНаценки = СкидкиНаценки;
	ПараметрыРасчета.Партнер       = Объект.Партнер;
	ПараметрыРасчета.Соглашение    = Неопределено;
	ПараметрыРасчета.Регистратор   = Объект.Ссылка;
	
	// Для скидки "За форму оплаты".
	ПараметрыРасчета.ФормаОплаты = Объект.ФормаОплаты;
	
	// Для скидки "За время продажи".
	ПараметрыРасчета.ДеньНедели   = Перечисления.ДниНедели.Получить(ДеньНедели(Объект.Дата) - 1);
	ПараметрыРасчета.ТекущееВремя = ПолучитьТекущееВремяОбъекта(Объект);
	ПараметрыРасчета.ТекущаяДата  = ПолучитьТекущуюДатуОбъекта(Объект);
	
	// Для скидки "За соблюдение графика оплаты".
	ПараметрыРасчета.ГрафикОплаты = Неопределено;
	
	// Карты лояльности
	ПараметрыРасчета.КартаЛояльности = Объект.КартаЛояльности;
	
	ПараметрыРасчета.Товары                     = ДанныеПоТоварамИНаборам.Товары;
	ПараметрыРасчета.ВалютаДокумента            = Объект.Валюта;
	ПараметрыРасчета.ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();
	ПараметрыРасчета.БазоваяВалюта              = ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию();
	ПараметрыРасчета.Пользователь               = Объект.Кассир;
	ПараметрыРасчета.Объект                     = Объект;
	
	ПодготовитьДанныеОВыбранныхУправляемыхСкидках(Объект, ПараметрыРасчета, ВходныеПараметры);
	ПримененныеСкидкиНаценки = РассчитатьДеревоСкидокНаценок(ПараметрыРасчета, ВходныеПараметры);
	
	Если ИспользоватьНаборы И НЕ ВходныеПараметры.ТолькоПредварительныйРасчет Тогда
		РаспределитьСкидкиНабораПоКомплектующим(ПримененныеСкидкиНаценки, ДанныеПоТоварамИНаборам.СоответствиеКлючей, ТаблицаТовары);
	КонецЕсли;
	
	Если ВходныеПараметры.ПрименятьКОбъекту Тогда
		
		ПрименитьРезультатРасчетаКЧекуККМ(Объект, ПримененныеСкидкиНаценки);
		
	КонецЕсли;
	
	Возврат ПримененныеСкидкиНаценки;
	
КонецФункции

// Процедура применяет результат расчет скидок к документу.
// Вызывается из форм документов.
//
// Параметры:
//  Объект - ДокументОбъект, ДанныеФормыСтруктура - Объект, в котором требуется рассчитать скидки (наценки).
//  ПримененныеСкидкиНаценки - Структура - Структура со свойствами:
//    * ДеревоСкидок - ДеревоЗначений - Дерево скидок (наценок).
//    * ТаблицаСкидкиНаценки - ТаблицаЗначений - Таблица с рассчитанными скидками.
//    * ПараметрыРасчета - Структура - Структура параметров расчета.
//
Процедура ПрименитьРезультатРасчетаКЗаказуКлиента(Объект, ПримененныеСкидкиНаценки)
	
	ИспользоватьОплатуБонуснымиБаллами = ИспользоватьБонусныеПрограммыЛояльности(Объект.КартаЛояльности);
	ДополнительныеПараметры = Новый Структура("ИспользоватьОплатуБонуснымиБаллами", ИспользоватьОплатуБонуснымиБаллами);
	
	ПрименитьРезультатРасчетаКОбъекту(Объект, "Товары", ПримененныеСкидкиНаценки, Истина,,,ДополнительныеПараметры);
	
	ПрименитьРезультатРасчетаПоБонуснымБаллам(Объект, "Товары", ПримененныеСкидкиНаценки, ИспользоватьОплатуБонуснымиБаллами);
	
КонецПроцедуры

// Использовать бонусные программы лояльности.
// 
// Параметры:
//  КартаЛояльности - СправочникСсылка.КартыЛояльности - Карта лояльности
// 
// Возвращаемое значение:
//  Булево - Использовать бонусные программы лояльности
Функция ИспользоватьБонусныеПрограммыЛояльности(КартаЛояльности) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьБонусныеПрограммыЛояльности") 
		ИЛИ Не ЗначениеЗаполнено(КартаЛояльности) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	КартыЛояльности.Владелец.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности
	|ИЗ
	|	Справочник.КартыЛояльности КАК КартыЛояльности
	|ГДЕ
	|	КартыЛояльности.Ссылка = &КартаЛояльности";
	
	Запрос.УстановитьПараметр("КартаЛояльности", КартаЛояльности);
	
	БонуснаяПрограммаЗаполнена = Ложь;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		БонуснаяПрограммаЗаполнена = ЗначениеЗаполнено(Выборка.БонуснаяПрограммаЛояльности);
	КонецЕсли;
	
	Возврат БонуснаяПрограммаЗаполнена;
	
КонецФункции

// Процедура применяет результат расчет скидок к документу.
// Вызывается из форм документов.
//
// Параметры:
//  Объект - ДокументОбъект, ДанныеФормыСтруктура - Объект, в котором требуется рассчитать скидки (наценки).
//  ПримененныеСкидкиНаценки - Структура - Структура со свойствами:
//    * ДеревоСкидок - ДеревоЗначений - Дерево скидок (наценок).
//    * ТаблицаСкидкиНаценки - ТаблицаЗначений - Таблица с рассчитанными скидками.
//    * ПараметрыРасчета - Структура - Структура параметров расчета.
//
Процедура ПрименитьРезультатРасчетаКВозвратуТоваровОтКлиента(Объект, ПримененныеСкидкиНаценки)
	
	ИспользоватьОплатуБонуснымиБаллами = ИспользоватьБонусныеПрограммыЛояльности(Объект.КартаЛояльности);
	ДополнительныеПараметры = Новый Структура("ИспользоватьОплатуБонуснымиБаллами", ИспользоватьОплатуБонуснымиБаллами);

	ПрименитьРезультатРасчетаКОбъекту(Объект, "ЗаменяющиеТовары", ПримененныеСкидкиНаценки, Истина, , , ДополнительныеПараметры);
	
	ПрименитьРезультатРасчетаПоБонуснымБаллам(Объект, "ЗаменяющиеТовары", ПримененныеСкидкиНаценки, ИспользоватьОплатуБонуснымиБаллами);

	
КонецПроцедуры

// Процедура применяет результат расчет скидок к документу.
// Вызывается из форм документов.
//
// Параметры:
//  Объект - ДокументОбъект, ДанныеФормыСтруктура - Объект, в котором требуется рассчитать скидки (наценки).
//  ПримененныеСкидкиНаценки - Структура - со свойствами:
//    * ДеревоСкидок - ДеревоЗначений - Дерево скидок (наценок).
//    * ТаблицаСкидкиНаценки - ТаблицаЗначений - Таблица с рассчитанными скидками.
//    * ПараметрыРасчета - Структура - Структура параметров расчета.
//
Процедура ПрименитьРезультатРасчетаККоммерческомуПредложениюКлиенту(Объект, ПримененныеСкидкиНаценки)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяКолонкиКоличество",             "Количество");
	СкидкиНаценкиСервер.ПрименитьРезультатРасчетаКОбъекту(Объект, "Товары", ПримененныеСкидкиНаценки, Истина,,,ДополнительныеПараметры);
	
КонецПроцедуры

// Процедура применяет результат расчет скидок к документу.
// Вызывается из форм документов.
//
// Параметры:
//  Объект - ДокументОбъект, ДанныеФормыСтруктура - Объект, в котором требуется рассчитать скидки (наценки).
//  ПримененныеСкидкиНаценки - Структура - Структура со свойствами:
//    * ДеревоСкидок - ДеревоЗначений - Дерево скидок (наценок).
//    * ТаблицаСкидкиНаценки - ТаблицаЗначений - Таблица с рассчитанными скидками.
//    * ПараметрыРасчета - Структура - Структура параметров расчета.
//
Процедура ПрименитьРезультатРасчетаКРеализацииТоваровУслуг(Объект, ПримененныеСкидкиНаценки, РеализацияСверхЗаказа)
	
	ИспользоватьОплатуБонуснымиБаллами = ИспользоватьБонусныеПрограммыЛояльности(Объект.КартаЛояльности);
	ДополнительныеПараметры = Новый Структура("ИспользоватьОплатуБонуснымиБаллами", ИспользоватьОплатуБонуснымиБаллами);

	ПрименитьРезультатРасчетаКОбъекту(Объект, "Товары", ПримененныеСкидкиНаценки, Истина, Истина, РеализацияСверхЗаказа, ДополнительныеПараметры);
	
	ПрименитьРезультатРасчетаПоБонуснымБаллам(Объект, "Товары", ПримененныеСкидкиНаценки, ИспользоватьОплатуБонуснымиБаллами, РеализацияСверхЗаказа);
	
КонецПроцедуры

// Процедура применяет результат расчет скидок к документу.
// Вызывается из форм документов.
//
// Параметры:
//  Объект - ДокументОбъект, ДанныеФормыСтруктура - Объект, в котором требуется рассчитать скидки (наценки).
//  ПримененныеСкидкиНаценки - Структура - Структура со свойствами:
//    * ДеревоСкидок - ДеревоЗначений - Дерево скидок (наценок).
//    * ТаблицаСкидкиНаценки - ТаблицаЗначений - Таблица с рассчитанными скидками.
//    * ПараметрыРасчета - Структура - Структура параметров расчета.
//
Процедура ПрименитьРезультатРасчетаКАктуВыполненныхРабот(Объект, ПримененныеСкидкиНаценки)
	
	ИспользоватьОплатуБонуснымиБаллами = ИспользоватьБонусныеПрограммыЛояльности(Объект.КартаЛояльности);
	ДополнительныеПараметры = Новый Структура("ИспользоватьОплатуБонуснымиБаллами", ИспользоватьОплатуБонуснымиБаллами);

	ПрименитьРезультатРасчетаКОбъекту(Объект, "Услуги", ПримененныеСкидкиНаценки, Истина, Истина,,ДополнительныеПараметры);
	
	ПрименитьРезультатРасчетаПоБонуснымБаллам(Объект, "Услуги", ПримененныеСкидкиНаценки, ИспользоватьОплатуБонуснымиБаллами);
	
КонецПроцедуры

// Процедура применяет результат расчет скидок к документу.
// Вызывается из форм документов.
//
// Параметры:
//  Объект - ДокументОбъект, ДанныеФормыСтруктура - Объект, в котором требуется рассчитать скидки (наценки).
//  ПримененныеСкидкиНаценки - Структура - Структура со свойствами:
//    * ДеревоСкидок - ДеревоЗначений - Дерево скидок (наценок).
//    * ТаблицаСкидкиНаценки - ТаблицаЗначений - Таблица с рассчитанными скидками.
//    * ПараметрыРасчета - Структура - Структура параметров расчета.
//
Процедура ПрименитьРезультатРасчетаКЧекуККМ(Объект, ПримененныеСкидкиНаценки)
	
	ДополнительныеПараметры = Новый Структура("ИспользоватьОплатуБонуснымиБаллами", Объект.ИспользоватьОплатуБонуснымиБаллами);
	ПрименитьРезультатРасчетаКОбъекту(Объект, "Товары", ПримененныеСкидкиНаценки, Ложь,,,ДополнительныеПараметры);
	
	ПрименитьРезультатРасчетаПоБонуснымБаллам(Объект, "Товары", ПримененныеСкидкиНаценки, Объект.ИспользоватьОплатуБонуснымиБаллами);
	
КонецПроцедуры

// Применить результат расчета по бонусным баллам.
// 
// Параметры:
//  Объект - ДокументОбъект, ДанныеФормыСтруктура, ДокументОбъект.РеализацияТоваровУслуг - Объект
//  ИмяТЧ  - Строка - имя табличной части для расчета скидок
//  ПримененныеСкидкиНаценки - Строка, Структура - Примененные скидки наценки:
// * ДеревоСкидок - ДеревоЗначений -
// * ТаблицаСкидкиНаценки - ТаблицаЗначений -
// * ПараметрыРасчета - Структура -
//  ИспользоватьОплатуБонуснымиБаллами - Булево, Структура - Использовать оплату бонусными баллами:
// * ДеревоСкидок - ДеревоЗначений -
// * ТаблицаСкидкиНаценки - ТаблицаЗначений -
// * ПараметрыРасчета - Структура -
//  РеализацияСверхЗаказа - Булево -
Процедура ПрименитьРезультатРасчетаПоБонуснымБаллам(Объект, 
													ИмяТЧ, 
													ПримененныеСкидкиНаценки, 
													ИспользоватьОплатуБонуснымиБаллами = Ложь, 
													РеализацияСверхЗаказа = Ложь)
	
	Если ТипЗнч(Объект) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ЧекККМ") Тогда
		ТаблицаБонусныеБаллы = Объект.БонусныеБаллы;
		ИспользоватьКлючСвязи = Ложь;
	Иначе
		ТаблицаБонусныеБаллы = Объект.НачислениеБонусныхБаллов;
		ИспользоватьКлючСвязи = Истина;
	КонецЕсли;

	Если Объект.Партнер = Справочники.Партнеры.РозничныйПокупатель 
		Или Не РеализацияСверхЗаказа Тогда
		
		Если ТаблицаБонусныеБаллы.Количество() > 0 Тогда
			ТаблицаБонусныеБаллы.Очистить();
		КонецЕсли;
		
		Если Объект.Партнер = Справочники.Партнеры.РозничныйПокупатель Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	НачисленныеБонусныеБаллы = БонусныеБаллыСервер.ТаблицаБонусныеБаллы(ПримененныеСкидкиНаценки.ТаблицаСкидкиНаценки, Объект.Дата);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(КартыЛояльности.Владелец.БонуснаяПрограммаЛояльности.НеНачислятьБаллыПриОплатеБонусами, Ложь) КАК НеНачислятьБаллыПриОплатеБонусами
	|ИЗ
	|	Справочник.КартыЛояльности КАК КартыЛояльности
	|ГДЕ
	|	КартыЛояльности.Ссылка = &КартаЛояльности";
	
	Запрос.УстановитьПараметр("КартаЛояльности", Объект.КартаЛояльности);
	
	ИспользуетсяОдновременноеСписаниеИНачисление = Ложь;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ИспользуетсяОдновременноеСписаниеИНачисление = Не Выборка.НеНачислятьБаллыПриОплатеБонусами;
	КонецЕсли;
	
	БонусныеБаллы = Новый ТаблицаЗначений;
	Если ИспользоватьКлючСвязи Тогда
		БонусныеБаллы.Колонки.Добавить("КлючСвязи",                   Новый ОписаниеТипов("Число"));
	КонецЕсли;
	БонусныеБаллы.Колонки.Добавить("БонуснаяПрограммаЛояльности", Новый ОписаниеТипов("СправочникСсылка.БонусныеПрограммыЛояльности"));
	БонусныеБаллы.Колонки.Добавить("ДатаНачисления",              ОбщегоНазначенияУТ.ПолучитьОписаниеТиповДаты(ЧастиДаты.ДатаВремя));
	БонусныеБаллы.Колонки.Добавить("ДатаСписания",                ОбщегоНазначенияУТ.ПолучитьОписаниеТиповДаты(ЧастиДаты.ДатаВремя));
	БонусныеБаллы.Колонки.Добавить("СуммаБонусныхБаллов",         ОбщегоНазначенияУТ.ОписаниеТипаДенежногоПоля());
	
	Для Каждого СтрокаТЧ Из Объект[ИмяТЧ] Цикл
		Если РеализацияСверхЗаказа И СтрокаТЧ.КодСтроки > 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		Если ИспользоватьОплатуБонуснымиБаллами И (ИспользуетсяОдновременноеСписаниеИНачисление ИЛИ СтрокаТЧ.СуммаБонусныхБалловКСписанию = 0)
			Или Не ИспользоватьОплатуБонуснымиБаллами Тогда 
			
			Начислено = 0;
			НайденныеСтроки = НачисленныеБонусныеБаллы.НайтиСтроки(Новый Структура("КлючСвязи", СтрокаТЧ.КлючСвязи));
			Для Каждого СтрокаНачислениеБаллов Из НайденныеСтроки Цикл
				
				НоваяСтрока = БонусныеБаллы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачислениеБаллов);
				
				// Конвертация начисленных баллов из валюты документа в баллы.
				НоваяВалюта = СтрокаНачислениеБаллов.ВалютаКонвертацииБонусов;
				СтруктураКурсовНовойВалюты = ПримененныеСкидкиНаценки.ПараметрыРасчета.КурсыВалют.Найти(НоваяВалюта, "Валюта");
				ТекущаяВалюта = Объект.Валюта;
				СтруктураКурсовТекущейВалюты = ПримененныеСкидкиНаценки.ПараметрыРасчета.КурсыВалют.Найти(ТекущаяВалюта, "Валюта");
				
				Начислено = Начислено + СтрокаНачислениеБаллов.СуммаБонусныхБаллов;
				
				НоваяСтрока.СуммаБонусныхБаллов = РаботаСКурсамиВалютУТ.ПересчитатьПоКурсу(НоваяСтрока.СуммаБонусныхБаллов,
					СтруктураКурсовТекущейВалюты,
					СтруктураКурсовНовойВалюты) / СтрокаНачислениеБаллов.КурсКонвертацииБонусовВВалюту;
				
			КонецЦикла;
			
			СтрокаТЧ.СуммаНачисленныхБонусныхБалловВВалюте = Начислено;
			
		Иначе
			
			СтрокаТЧ.СуммаНачисленныхБонусныхБалловВВалюте = 0;
			
		КонецЕсли;
	КонецЦикла;
	
	Если ИспользоватьКлючСвязи Тогда
		БонусныеБаллы.Свернуть("КлючСвязи, БонуснаяПрограммаЛояльности, ДатаНачисления, ДатаСписания", "СуммаБонусныхБаллов");
	Иначе
		БонусныеБаллы.Свернуть("БонуснаяПрограммаЛояльности, ДатаНачисления, ДатаСписания", "СуммаБонусныхБаллов");
	КонецЕсли;
	
	Для Каждого СтрокаТЧ Из БонусныеБаллы Цикл
		НоваяСтрока = ТаблицаБонусныеБаллы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ФункцииРасчетаСкидокНаценокПоДеревуСкидокНаценок

Функция ПолучитьГруппуУмножениеРодитель(СтрокаДерева, ПараметрыРасчета)
	
	Результат = ПараметрыРасчета.КешУмножениеГруппы.Получить(СтрокаДерева);
	Если Результат <> Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если СтрокаДерева.Родитель = Неопределено Тогда
		Результат = Неопределено;
	Иначе
		
		Родитель = СтрокаДерева.Родитель;
		ГруппаУмноженияВерхнегоУровня = ПолучитьГруппуУмножениеРодитель(Родитель, ПараметрыРасчета);
		
		Если ГруппаУмноженияВерхнегоУровня = Неопределено
			И Родитель.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Умножение Тогда
			Результат = Родитель;
		Иначе
			Результат = ПолучитьГруппуУмножениеРодитель(Родитель, ПараметрыРасчета);
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыРасчета.КешУмножениеГруппы.Вставить(СтрокаДерева, Результат);
	
	Возврат Результат;
	
КонецФункции

// Параметры:
// 	СтрокаДерева - СтрокаДереваЗначений - описание см. СформироватьДеревоСкидок
// 
Функция ПолучитьИтогРезультатаРасчета(СтрокаДерева, КлючСвязи, ПараметрыРасчета)
	
	Идентификатор = Строка(СтрокаДерева.Ссылка.УникальныйИдентификатор()) + КлючСвязи;
	Сумма = ПараметрыРасчета.КешУмножениеСуммы.Получить(Идентификатор);
	Если Сумма <> Неопределено Тогда
		Возврат Сумма;
	КонецЕсли;
	
	Сумма = 0;
	
	Для Каждого ТекСтрокаДерева Из СтрокаДерева.РезультатРасчета Цикл
		
		Если Не ТекСтрокаДерева.Действует Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекСтрокаДерева.КлючСвязи <> КлючСвязи Тогда
			Продолжить;
		КонецЕсли;
		
		Сумма = Сумма + ТекСтрокаДерева.Сумма;
		
	КонецЦикла;
	
	ПараметрыРасчета.КешУмножениеСуммы.Вставить(Идентификатор, Сумма);
	
	Возврат Сумма;
	
КонецФункции

// Функция получает сумму примененной скидки подчиненных групп для строки товара с учетом иерархии скидок.
//
// Возвращаемое значение:
//	Число - Сумма примененной скидки.
//
Функция ПолучитьСуммуПримененныхСкидок(СтрокиДерева, КлючСвязи, ПараметрыРасчета, ГруппаИгнорирования)
	
	Сумма = 0;
	
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		Если СтрокаДерева.ЭтоГруппа Тогда			
			Если СтрокаДерева = ГруппаИгнорирования Тогда			
				Продолжить;                              			
			КонецЕсли;
			Если СтрокаДерева.Рассчитано Тогда
				Сумма = Сумма + ПолучитьИтогРезультатаРасчета(СтрокаДерева, КлючСвязи, ПараметрыРасчета);
			Иначе
				Сумма = Сумма + ПолучитьСуммуПримененныхСкидок(СтрокаДерева.Строки, КлючСвязи, ПараметрыРасчета, ГруппаИгнорирования);
			КонецЕсли;
		Иначе
			Если СтрокаДерева.Рассчитано Тогда
				Сумма = Сумма + ПолучитьИтогРезультатаРасчета(СтрокаДерева, КлючСвязи, ПараметрыРасчета);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Сумма;
	
КонецФункции


// Функция получает цену номенклатуры по виду цен заданному в параметрах скидки.
//
// Возвращаемое значение:
//	Число - цена номенклатуры.
//
Функция ПолучитьЦенуНоменклатурыПоВидуЦен(Товар, Параметры, ВидЦены)
	
	Цена = 0;
	
	Отбор = Новый Структура(
		"Номенклатура,
		|Характеристика,
		|ВидЦены",
		Товар.Номенклатура,
		Товар.Характеристика,
		ВидЦены);
	
	МассивСтрокЦены = Параметры.ЦеныНоменклатуры.НайтиСтроки(Отбор);
	Для Каждого СтрокаЦена Из МассивСтрокЦены Цикл
		
		Если СтрокаЦена.Упаковка = Товар.Упаковка Тогда
			Цена = СтрокаЦена.Цена;
		Иначе
			
			// Приведем цену к цене за упаковку сегмента.
			// Цена не округляется для повышения точности...
			Если Товар.КоличествоУпаковок = 0 Тогда
				КоэффициентПересчетаУпаковок = 0;
			Иначе
				КоэффициентПересчетаУпаковок = Товар.Количество / Товар.КоличествоУпаковок;
			КонецЕсли;
			Если СтрокаЦена.Упаковка = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка() Тогда
				Цена = СтрокаЦена.Цена * КоэффициентПересчетаУпаковок;
			Иначе
				Цена = (СтрокаЦена.Цена / СтрокаЦена.УпаковкаКоэффициент) * КоэффициентПересчетаУпаковок;
			КонецЕсли;
			
		КонецЕсли;
		
		Прервать;
		
	КонецЦикла;
	
	Возврат Цена;
	
КонецФункции


// Функция выполняет объединение подчиненных таблиц данных.
//
// Возвращаемое значение:
//	ТаблицаЗначений - объединенная таблица данных:
//		* КлючСвязи					- Число
//		* Сумма						- Число
//		* Расшифровка				- ТаблицаЗначений
//		* РеквизитДопУпорядочивания	- Число
//
Функция ОбъединитьРезультатыРасчетаПодчиненныхСтрок(СтрокаДерева)
	
	РезультатРасчета = Новый ТаблицаЗначений;
	РезультатРасчета.Колонки.Добавить("КлючСвязи",                 Новый ОписаниеТипов("Число"));
	РезультатРасчета.Колонки.Добавить("Сумма",                     Новый ОписаниеТипов("Число"));
	РезультатРасчета.Колонки.Добавить("Расшифровка",               Новый ОписаниеТипов("ТаблицаЗначений"));
	РезультатРасчета.Колонки.Добавить("РеквизитДопУпорядочивания", Новый ОписаниеТипов("Число"));
	
	Для Каждого ПодчиненнаяСтрока Из СтрокаДерева.Строки Цикл
		
		Если НЕ ПодчиненнаяСтрока.ЭтоГруппа Тогда // Это скидка а не группа
			
			Если НЕ ПодчиненнаяСтрока.ПараметрыУсловий.УсловияВыполнены Тогда
				Продолжить;
			КонецЕсли;
			
			Если ПодчиненнаяСтрока.Управляемая И НЕ ПодчиненнаяСтрока.НазначенаПользователем Тогда
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		ЗаполнитьТаблицуДанныхПодчиненныхСтрок(РезультатРасчета, ПодчиненнаяСтрока);
		
	КонецЦикла;
	
	Возврат РезультатРасчета;
	
КонецФункции

// Процедура выполняет расчет скидки по группе совместного применения: максимум, минимум, вытеснение.
//
Процедура РассчитатьСкидкиПоГруппеСовместногоПримененияМаксимумМинимумВытеснение(СтрокаДерева, Параметры)
	
	ТЗ = ПустаяТаблицаСкидокСРасшифровкой(Параметры);
	
	Если СтрокаДерева.ВариантРасчетаРезультатаСовместногоПрименения = Перечисления.ВариантыРасчетаРезультатаСовместногоПрименения.ПоСтроке Тогда
		
		РезультатРасчета = ОбъединитьРезультатыРасчетаПодчиненныхСтрок(СтрокаДерева);
		Если СтрокаДерева.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Максимум Тогда
			РезультатРасчета.Сортировать("КлючСвязи, Сумма Убыв, РеквизитДопУпорядочивания");
		ИначеЕсли СтрокаДерева.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Минимум Тогда
			РезультатРасчета.Сортировать("КлючСвязи, Сумма Возр, РеквизитДопУпорядочивания");
		ИначеЕсли СтрокаДерева.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Вытеснение Тогда
			РезультатРасчета.Сортировать("КлючСвязи, РеквизитДопУпорядочивания");
		КонецЕсли;
		
		КлючСвязи = Неопределено;
		Для Каждого СтрокаТаблицы Из РезультатРасчета Цикл
			
			Если СтрокаТаблицы.КлючСвязи <> КлючСвязи Тогда
				
				НоваяСтрока = ТЗ.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				НоваяСтрока.Действует = Истина;
				
				КлючСвязи = СтрокаТаблицы.КлючСвязи;
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		РезультатРасчета = ТаблицаДанныхПоСпособуСовместногоПримененияДляДокументаВЦелом(СтрокаДерева);
		Для Каждого СтрокаТаблицы Из РезультатРасчета Цикл
			
			НоваяСтрока = ТЗ.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.Действует = Истина;
			
		КонецЦикла;
		
	КонецЕсли;
	
	СтрокаДерева.РезультатРасчета = ТЗ;
	
КонецПроцедуры

// Процедура выполняет расчет скидки по группе совместного применения: сложение.
//
Процедура РассчитатьСкидкиПоГруппеСовместногоПримененияСложениеУмножение(СтрокаДерева, ПараметрыРасчета)
	
	РезультатРасчета = ОбъединитьРезультатыРасчетаПодчиненныхСтрок(СтрокаДерева);
	РезультатРасчета.Сортировать("КлючСвязи");
	
	ТЗ = ПустаяТаблицаСкидокСРасшифровкой(ПараметрыРасчета);
	
	КлючСвязи = Неопределено;
	Для Каждого СтрокаТаблицы Из РезультатРасчета Цикл
		
		Если СтрокаТаблицы.КлючСвязи <> КлючСвязи Тогда
			
			НоваяСтрокаТЗ = ТЗ.Добавить();
			НоваяСтрокаТЗ.КлючСвязи = СтрокаТаблицы.КлючСвязи;
			НоваяСтрокаТЗ.Сумма = СтрокаТаблицы.Сумма;
			НоваяСтрокаТЗ.Действует = Истина;
			
			// Расшифровка скидки.
			НоваяСтрокаТЗ.Расшифровка = ПараметрыРасчета.ПустаяТаблицаРасшифровка.СкопироватьКолонки();
			Для Каждого СтрокаРасшифровки Из СтрокаТаблицы.Расшифровка Цикл
				ЗаполнитьЗначенияСвойств(НоваяСтрокаТЗ.Расшифровка.Добавить(), СтрокаРасшифровки);
			КонецЦикла;
			
			КлючСвязи = СтрокаТаблицы.КлючСвязи;
			
		Иначе
			
			НоваяСтрокаТЗ.Сумма = НоваяСтрокаТЗ.Сумма + СтрокаТаблицы.Сумма;
			Для Каждого СтрокаРасшифровки Из СтрокаТаблицы.Расшифровка Цикл
				ЗаполнитьЗначенияСвойств(НоваяСтрокаТЗ.Расшифровка.Добавить(), СтрокаРасшифровки);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтрокаДерева.РезультатРасчета = ТЗ;
	
КонецПроцедуры

// Процедура выполняет расчет скидки по группе совместного применения.
//
Процедура РассчитатьСкидкиПоГруппеСовместногоПрименения(СтрокаДерева, ПараметрыРасчета)
	
	Если    СтрокаДерева.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Максимум
		ИЛИ СтрокаДерева.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Минимум
		ИЛИ СтрокаДерева.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Вытеснение Тогда
		
		РассчитатьСкидкиПоГруппеСовместногоПримененияМаксимумМинимумВытеснение(СтрокаДерева, ПараметрыРасчета);
		
	ИначеЕсли СтрокаДерева.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Умножение
		  ИЛИ СтрокаДерева.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Сложение Тогда
		
		РассчитатьСкидкиПоГруппеСовместногоПримененияСложениеУмножение(СтрокаДерева, ПараметрыРасчета);
		
	КонецЕсли;
	
	СтрокаДерева.Рассчитано = Истина;
	
КонецПроцедуры // РассчитатьСкидкиПоГруппеСовместногоПрименения()

Функция СтрокиТоваровДляРаспределения(СтрокаДерева, ПараметрыРасчета)
	
	Если СтрокаДерева.ПараметрыУсловий.ЕстьУсловияПоСтроке Тогда
		Товары = Новый Массив;
		Для Каждого СтрокаТЧ Из СтрокаДерева.ПараметрыУсловий.УсловияПоСтроке.ТаблицаПроверкиУсловий Цикл
			ТаблицаТовары = ПараметрыРасчета.Товары;//ТаблицаЗначений
			Товары.Добавить(ТаблицаТовары.Найти(СтрокаТЧ.КлючСвязи, "КлючСвязи"));
		КонецЦикла;
	Иначе
		Товары = ПараметрыРасчета.Товары;
	КонецЕсли;
	
	Возврат Товары;
	
КонецФункции

Функция ОстатокСуммыПоСтроке(СтрокаДерева, Товар, ПараметрыРасчета, ЭтоУмножение = Истина)
	
	Если ЭтоУмножение Тогда
		
		ГруппаУмножение = ПолучитьГруппуУмножениеРодитель(СтрокаДерева, ПараметрыРасчета);
		
		ГруппаИгнорирования = ПолучитьГруппуИгнорирования(СтрокаДерева, ГруппаУмножение);
		
		Если ГруппаУмножение <> Неопределено Тогда
			СуммаПримененнойСкидки = Окр(ПолучитьСуммуПримененныхСкидок(ГруппаУмножение.Строки, Товар.КлючСвязи, ПараметрыРасчета, ГруппаИгнорирования), 2);
		Иначе
			СуммаПримененнойСкидки = 0;
		КонецЕсли;		
	
		Сумма = Товар.Сумма - СуммаПримененнойСкидки;
		Если Сумма < 0 Тогда
			Сумма = 0;
		КонецЕсли;
		
	Иначе
		Сумма = Товар.Сумма;
	КонецЕсли;
	
	Возврат Сумма;
	
КонецФункции

Функция ПолучитьГруппуИгнорирования(Знач СтрокаДерева, Знач ГруппаУмножение)
	
	Перем РодительскаяГруппа;
	
	РодительскаяГруппа = СтрокаДерева.Родитель;
	Если НЕ (ГруппаУмножение <> РодительскаяГруппа И 
		(РодительскаяГруппа.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Максимум
		ИЛИ РодительскаяГруппа.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Минимум
		ИЛИ РодительскаяГруппа.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Вытеснение)) Тогда
		
		РодительскаяГруппа = Неопределено;
		
	КонецЕсли;
	
	Возврат РодительскаяГруппа;

КонецФункции

Функция КратностьВыполнения(СтрокаДерева)
	
	МинимальнаяКратностьВыполнения = Неопределено;
	Условия = СтрокаДерева.ПараметрыУсловий.ТаблицаУсловий.НайтиСтроки(Новый Структура("ОбластьОграничения", Перечисления.ВариантыОбластейОграниченияСкидокНаценок.ВДокументе));
	Для Каждого СтрокаУсловия Из Условия Цикл
		Если СтрокаУсловия.КратностьВыполнения < 0 Тогда
			Продолжить;
		КонецЕсли;
		Если МинимальнаяКратностьВыполнения = Неопределено Или СтрокаУсловия.КратностьВыполнения < МинимальнаяКратностьВыполнения Тогда
			МинимальнаяКратностьВыполнения = СтрокаУсловия.КратностьВыполнения;
		КонецЕсли;
	КонецЦикла;
	Если МинимальнаяКратностьВыполнения = Неопределено Тогда
		Если СтрокаДерева.ПараметрыУсловий.УсловияВыполнены Тогда
			МинимальнаяКратностьВыполнения = 1;
		Иначе
			МинимальнаяКратностьВыполнения = 0;
		КонецЕсли;
	КонецЕсли;
	Если Не СтрокаДерева.ИспользоватьКратность Тогда
		Если МинимальнаяКратностьВыполнения > 0 Тогда
			МинимальнаяКратностьВыполнения = 1;
		КонецЕсли;
	КонецЕсли;
	
	Возврат МинимальнаяКратностьВыполнения;
	
КонецФункции

Функция ЗначениеСкидкиНаценки(СтрокаДерева, СтрокаТЧ, ПараметрыРасчета)
	
	ЗначениеСкидкиНаценки = СтрокаДерева.ЗначениеСкидкиНаценки;
	
	Если ПараметрыРасчета.ИспользоватьЦеновыеГруппы Тогда
		УточненияЗначенияСкидкиНаценки = ПараметрыРасчета.УточненияЗначенияСкидкиНаценки.Получить(СтрокаДерева.Ссылка);
		Если УточненияЗначенияСкидкиНаценки <> Неопределено Тогда
			Для Каждого Уточнение Из УточненияЗначенияСкидкиНаценки Цикл
				Если Уточнение.Ключ = СтрокаТЧ.ЦеноваяГруппа Тогда
					ЗначениеСкидкиНаценки = Уточнение.Значение;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЗначениеСкидкиНаценки;
	
КонецФункции


Функция КонвертироватьСуммуВБонусы(СтрокаДерева, Сумма, ПараметрыРасчета)
	
	Значение = Сумма;
	
	Если СтрокаДерева.СпособПримененияСкидки = Перечисления.СпособыПримененияСкидокНаценок.НачислитьБонусныеБаллы Тогда
		
		НоваяВалюта = СтрокаДерева.ВалютаКонвертацииБонусов;
		СтруктураКурсовНовойВалюты = ПараметрыРасчета.КурсыВалют.Найти(НоваяВалюта, "Валюта");
		ТекущаяВалюта = ПараметрыРасчета.ВалютаДокумента;
		СтруктураКурсовТекущейВалюты = ПараметрыРасчета.КурсыВалют.Найти(ТекущаяВалюта, "Валюта");
		
		Значение = РаботаСКурсамиВалютУТ.ПересчитатьПоКурсу(Сумма * СтрокаДерева.КурсКонвертацииБонусовВВалюту,
			СтруктураКурсовНовойВалюты,
			СтруктураКурсовТекущейВалюты);
		
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

Функция ПолучитьРасшифровкуСкидки(СтрокаДерева, Сумма, ПараметрыРасчета, ЭтоСообщение = Ложь)
	
	Расшифровка = ПараметрыРасчета.ПустаяТаблицаРасшифровка.СкопироватьКолонки();
	
	СтрокаРасшифровки = Расшифровка.Добавить();
	СтрокаРасшифровки.СкидкаНаценка = СтрокаДерева.Ссылка;
	СтрокаРасшифровки.Сумма         = Сумма;
	СтрокаРасшифровки.ЭтоСообщение  = ЭтоСообщение;
	
	Возврат Расшифровка;
	
КонецФункции


Функция СтоимостьСамыхДешевыхТоваров(Количество, Товары)
	
	ТаблицаСтоимостейТоваров = Новый ТаблицаЗначений;
	ТаблицаСтоимостейТоваров.Колонки.Добавить("Цена", Новый ОписаниеТипов("Число"));
	ТаблицаСтоимостейТоваров.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
	Для Каждого Товар Из Товары Цикл
		
		Если Товар.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаСтоимостейТоваров.Добавить();
		НоваяСтрока.Цена       = Товар.Сумма / Товар.Количество;
		НоваяСтрока.Количество = Товар.Количество;
		
	КонецЦикла;
	ТаблицаСтоимостейТоваров.Сортировать("Цена Возр");
	
	Стоимость = 0;
	Для Итерация = 1 По Количество Цикл
		Для Каждого СтрокаТЧ Из ТаблицаСтоимостейТоваров Цикл
			Если СтрокаТЧ.Количество > 0 Тогда
				СтрокаТЧ.Количество = СтрокаТЧ.Количество - 1;
				Стоимость = Стоимость + СтрокаТЧ.Цена;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Стоимость;
	
КонецФункции

Процедура ПрименитьЗначениеСкидкиКТовару(СтрокаДерева, СуммаСкидки, Товар, РезультатРасчета, ПараметрыРасчета) Экспорт
	
	НоваяСтрока           = РезультатРасчета.Добавить();
	НоваяСтрока.КлючСвязи = Товар.КлючСвязи;
	НоваяСтрока.Действует = Истина;
	
	Если СтрокаДерева.Управляемая И НЕ СтрокаДерева.НазначенаПользователем Тогда
		НоваяСтрока.Действует = Ложь;
	КонецЕсли;
	
	// Если скидка на данную строку не действует - пропускаем.
	Если СтрокаДерева.ПараметрыУсловий.ЕстьУсловияПоСтроке
		И СтрокаДерева.ПараметрыУсловий.КодыСтрок.Найти(Товар.КлючСвязи) = Неопределено Тогда
		НоваяСтрока.Действует = Ложь;
	КонецЕсли;
	
	НоваяСтрока.Сумма = КонвертироватьСуммуВБонусы(СтрокаДерева, СуммаСкидки, ПараметрыРасчета);
	
	НоваяСтрока.Расшифровка = ПолучитьРасшифровкуСкидки(СтрокаДерева, НоваяСтрока.Сумма, ПараметрыРасчета);
	
КонецПроцедуры

Процедура РаспределитьЗначениеСкидкиНаТовары(СтрокаДерева, СуммаСкидки, Товары, РезультатРасчета, ПараметрыРасчета) Экспорт
	
	СуммаСкидкиКРаспределению = СуммаСкидки;
	
	// Расчет общей суммы товаров.
	ОбщаяСуммаТоваров = 0;
	Для Каждого Товар Из Товары Цикл
		
		Сумма = ОстатокСуммыПоСтроке(СтрокаДерева, Товар, ПараметрыРасчета);
		ОбщаяСуммаТоваров = ОбщаяСуммаТоваров + Сумма;
		
	КонецЦикла;
	
	// Распределение скидки на товары сегмента.
	Для Каждого Товар Из Товары Цикл
		
		// Расчет суммы скидки.
		Сумма = ОстатокСуммыПоСтроке(СтрокаДерева, Товар, ПараметрыРасчета);
		
		Если ОбщаяСуммаТоваров <> 0 Тогда
			СуммаСкидки = Окр(Сумма * (СуммаСкидкиКРаспределению / ОбщаяСуммаТоваров), 2);
		Иначе
			СуммаСкидки = 0;
		КонецЕсли;
		
		ПрименитьЗначениеСкидкиКТовару(СтрокаДерева, СуммаСкидки, Товар, РезультатРасчета, ПараметрыРасчета);
		
		СуммаСкидкиКРаспределению = СуммаСкидкиКРаспределению - СуммаСкидки;
		ОбщаяСуммаТоваров = ОбщаяСуммаТоваров - Сумма;
		
	КонецЦикла;
	
КонецПроцедуры


// Процедура выполняет расчет скидки дерева скидок.
//
Процедура РассчитатьСкидку(СтрокаДерева, ПараметрыРасчета)
	
	Товары = СтрокиТоваровДляРаспределения(СтрокаДерева, ПараметрыРасчета);
	
	РезультатРасчета = ПустаяТаблицаСкидокСРасшифровкой(ПараметрыРасчета);
	
	Если СтрокаДерева.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.Процент Тогда
		
		ЭтоУмножение = СтрокаДерева.ПрименятьУмножениеВРамкахВышестоящейГруппы;
		
		Для Каждого Товар Из Товары Цикл
			
			Если СтрокаДерева.ПараметрыУсловий.УсловияВыполнены Тогда
				Сумма = ЗначениеСкидкиНаценки(СтрокаДерева, Товар, ПараметрыРасчета) / 100 * ОстатокСуммыПоСтроке(СтрокаДерева, Товар, ПараметрыРасчета, ЭтоУмножение);
			Иначе
				Сумма = 0;
			КонецЕсли; 
			
			ПрименитьЗначениеСкидкиКТовару(СтрокаДерева, Сумма, Товар, РезультатРасчета, ПараметрыРасчета);
			
		КонецЦикла;
		
	ИначеЕсли СтрокаДерева.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.Сумма Тогда
		
		СуммаСкидки = СтрокаДерева.ЗначениеСкидкиНаценки * КратностьВыполнения(СтрокаДерева);
		
		РаспределитьЗначениеСкидкиНаТовары(СтрокаДерева, СуммаСкидки, Товары, РезультатРасчета, ПараметрыРасчета);
		
	ИначеЕсли СтрокаДерева.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.СуммаДляКаждойСтроки Тогда
		
		КратностьВыполнения = КратностьВыполнения(СтрокаДерева);
		
		Для Каждого Товар Из Товары Цикл
			
			СуммаСкидки = ЗначениеСкидкиНаценки(СтрокаДерева, Товар, ПараметрыРасчета) * КратностьВыполнения;
			
			ПрименитьЗначениеСкидкиКТовару(СтрокаДерева, СуммаСкидки, Товар, РезультатРасчета, ПараметрыРасчета);
			
		КонецЦикла;
		
	ИначеЕсли СтрокаДерева.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.ВидЦены Тогда
		
		Для Каждого Товар Из Товары Цикл
			
			ЦенаЗаУпаковку = ПолучитьЦенуНоменклатурыПоВидуЦен(Товар, ПараметрыРасчета, СтрокаДерева.ВидЦены);
			Если ЦенаЗаУпаковку <> 0 Тогда
				СуммаСкидки = Товар.Сумма - Товар.КоличествоУпаковок * ЦенаЗаУпаковку;
			Иначе
				СуммаСкидки = 0;
			КонецЕсли;
			
			ПрименитьЗначениеСкидкиКТовару(СтрокаДерева, СуммаСкидки, Товар, РезультатРасчета, ПараметрыРасчета);
			
		КонецЦикла;
		
	ИначеЕсли СтрокаДерева.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.Сообщение
		ИЛИ СтрокаДерева.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.КартаЛояльности Тогда
		
		Для Каждого Товар Из Товары Цикл
			
			НоваяСтрока              = РезультатРасчета.Добавить();
			НоваяСтрока.КлючСвязи    = Товар.КлючСвязи;
			НоваяСтрока.Действует    = Истина;
			НоваяСтрока.Расшифровка  = ПолучитьРасшифровкуСкидки(СтрокаДерева, 0, ПараметрыРасчета, Истина);
			
		КонецЦикла;
		
	ИначеЕсли СтрокаДерева.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.Количество Тогда
		
		ТаблицаОдинаковыхТоваров = Новый ТаблицаЗначений;
		ТаблицаОдинаковыхТоваров.Колонки.Добавить("КлючСвязи",      Новый ОписаниеТипов("Число"));
		ТаблицаОдинаковыхТоваров.Колонки.Добавить("Номенклатура",   Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		Если СтрокаДерева.УчитыватьХарактеристики Тогда
			ТаблицаОдинаковыхТоваров.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		КонецЕсли;
		ТаблицаОдинаковыхТоваров.Колонки.Добавить("Количество",     Новый ОписаниеТипов("Число"));
		ТаблицаОдинаковыхТоваров.Колонки.Добавить("Сумма",     Новый ОписаниеТипов("Число"));
		
		Для Каждого Товар Из Товары Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаОдинаковыхТоваров.Добавить(), Товар);
		КонецЦикла;
		
		ТаблицаОдинаковыхТоваровКопия = ТаблицаОдинаковыхТоваров.Скопировать();
		Если СтрокаДерева.УчитыватьХарактеристики Тогда
			ТаблицаОдинаковыхТоваров.Свернуть("Номенклатура, Характеристика", "Количество, Сумма");
		Иначе
			ТаблицаОдинаковыхТоваров.Свернуть("Номенклатура", "Количество, Сумма");
		КонецЕсли;
		
		Отбор = Новый Структура;
		Отбор.Вставить("Номенклатура");
		Если СтрокаДерева.УчитыватьХарактеристики Тогда
			Отбор.Вставить("Характеристика");
		КонецЕсли;
		
		Для Каждого Товар Из ТаблицаОдинаковыхТоваров Цикл
			
			Если Товар.Количество <> 0 Тогда
				КратностьВыполнения = Окр((Товар.Количество / СтрокаДерева.УсловиеДляСкидкиКоличеством) - 0.5, 0, РежимОкругления.Окр15как20);
			Иначе
				КратностьВыполнения = 0;
			КонецЕсли;
			
			Если КратностьВыполнения > 0 Тогда
				
				ЗаполнитьЗначенияСвойств(Отбор, Товар);
				СтрокиТоваров = ТаблицаОдинаковыхТоваровКопия.НайтиСтроки(Отбор);
				
				СуммаСкидки = СтоимостьСамыхДешевыхТоваров(КратностьВыполнения * СтрокаДерева.ЗначениеСкидкиНаценки, СтрокиТоваров);
				РаспределитьЗначениеСкидкиНаТовары(СтрокаДерева, СуммаСкидки, СтрокиТоваров, РезультатРасчета, ПараметрыРасчета);
				
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли СтрокаДерева.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.Подарок Тогда
		
		Если ЗначениеЗаполнено(СтрокаДерева.СегментПодарков) Тогда
			ТоварыСегментаПодарков = ПараметрыРасчета.ТоварыПоСегментам.НайтиСтроки(Новый Структура("СегментНоменклатуры", СтрокаДерева.СегментПодарков));
		Иначе
			ТоварыСегментаПодарков = ПараметрыРасчета.Товары;
		КонецЕсли;
		
		СуммаСкидки = СтоимостьСамыхДешевыхТоваров(КратностьВыполнения(СтрокаДерева) * СтрокаДерева.ЗначениеСкидкиНаценки, ТоварыСегментаПодарков);
		
		РаспределитьЗначениеСкидкиНаТовары(СтрокаДерева, СуммаСкидки, Товары, РезультатРасчета, ПараметрыРасчета);
		
	ИначеЕсли СтрокаДерева.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.ОкруглениеСуммы Тогда
		
		СуммаТоваров = 0;
		Для Каждого Товар Из Товары Цикл
			СуммаТоваров = СуммаТоваров + Окр(ОстатокСуммыПоСтроке(СтрокаДерева, Товар, ПараметрыРасчета), 2);
		КонецЦикла;
		
		СуммаСкидки = 0;
		Если СтрокаДерева.ЗначениеСкидкиНаценки <= СуммаТоваров Тогда
			
			Если ЗначениеЗаполнено(СтрокаДерева.ТочностьОкругления) Тогда
				СуммаСкидки = СуммаТоваров - ЦенообразованиеКлиентСервер.ОкруглитьЦену(СуммаТоваров, СтрокаДерева.ТочностьОкругления, СтрокаДерева.ВариантОкругления);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаДерева.ПсихологическоеОкругление) Тогда
				СуммаСкидки = СуммаТоваров + СуммаСкидки - ЦенообразованиеКлиентСервер.ПрименитьПсихологическоеОкругление(СуммаТоваров, СтрокаДерева.ПсихологическоеОкругление);
			КонецЕсли;
			
		КонецЕсли;
		
		РаспределитьЗначениеСкидкиНаТовары(СтрокаДерева, СуммаСкидки, Товары, РезультатРасчета, ПараметрыРасчета);
		
	ИначеЕсли ТипЗнч(СтрокаДерева.СпособПредоставления) = Тип("СправочникСсылка.ДополнительныеОтчетыИОбработки") Тогда
	
		ВнешняяОбработка = СкидкиНаценкиПовтИсп.ОбъектВнешнейОбработки(СтрокаДерева.СпособПредоставления);
		УстановитьБезопасныйРежим(Истина);
		РезультатРасчета = ВнешняяОбработка.Рассчитать(
			СтрокаДерева,
			СтрокаДерева.ПараметрыВнешнейОбработки.Получить(),
			Товары,
			ПараметрыРасчета);
		УстановитьБезопасныйРежим(Ложь);
		
	КонецЕсли;
	
	СтрокаДерева.Рассчитано = Истина;
	СтрокаДерева.РезультатРасчета = РезультатРасчета;
	
КонецПроцедуры

// Процедура рекурсивно обходит дерево и производит расчет
// скидок снизу-вверх: от подчиненного элемента дерева к родителю.
//
// Параметры:
// ДеревоСкидок - см. СформироватьДеревоСкидок
// Параметры - Структура - Структура параметров расчета.
//
Процедура РассчитатьСкидкиРекурсивно(ДеревоСкидок, Параметры)
	
	Для Каждого СтрокаДерева Из ДеревоСкидок.Строки Цикл
		
		Если СтрокаДерева.ЭтоГруппа Тогда
			
			РассчитатьСкидкиРекурсивно(СтрокаДерева, Параметры);
			
			// Скидки по подчиненным элементам рассчитаны.
			// Рассчитываем скидки по группе совместного применения (родителю).
			РассчитатьСкидкиПоГруппеСовместногоПрименения(СтрокаДерева, Параметры);
			
		Иначе
			
			РассчитатьСкидку(СтрокаДерева, Параметры);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Функция производит полный расчет скидок в дереве.
//
// Параметры:
//  ДеревоСкидок - ДеревоЗначений - Дерево скидок (наценок).
//  ПараметрыРасчета - Структура - Структура параметров расчета.
//
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * ДеревоСкидок - ДеревоЗначений - Дерево скидок (наценок).
//   * ТаблицаСкидкиНаценки - ТаблицаЗначений - Таблица с рассчитанными скидками.
//   * ПараметрыРасчета - Структура - Структура параметров расчета.
//
Функция РассчитатьДеревоСкидок(ДеревоСкидок, ПараметрыРасчета)
	
	РассчитатьСкидкиРекурсивно(ДеревоСкидок, ПараметрыРасчета);
	
	// На верхнем уровне только одна корневая группа.
	ЕстьРезультат = ДеревоСкидок.Строки.Количество() > 0;
	
	Если ЕстьРезультат Тогда
		РезультатРасчета = ДеревоСкидок.Строки[0].РезультатРасчета;
	КонецЕсли;
	
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("КлючСвязи",     Новый ОписаниеТипов("Число"));
	ТЗ.Колонки.Добавить("СкидкаНаценка", Новый ОписаниеТипов("СправочникСсылка.СкидкиНаценки"));
	ТЗ.Колонки.Добавить("Сумма",         Новый ОписаниеТипов("Число"));
	ТЗ.Колонки.Добавить("ЭтоСообщение",  Новый ОписаниеТипов("Булево"));
	
	Если ЕстьРезультат Тогда
		Для Каждого СтрокаТаблицы Из РезультатРасчета Цикл
			Для Каждого СтрокаСкидкиНаценки Из СтрокаТаблицы.Расшифровка Цикл
				НоваяСтрока               = ТЗ.Добавить();
				НоваяСтрока.КлючСвязи     = СтрокаТаблицы.КлючСвязи;
				НоваяСтрока.СкидкаНаценка = СтрокаСкидкиНаценки.СкидкаНаценка;
				НоваяСтрока.Сумма         = СтрокаСкидкиНаценки.Сумма;
				НоваяСтрока.ЭтоСообщение  = СтрокаСкидкиНаценки.ЭтоСообщение;
			КонецЦикла;
		КонецЦикла;
		ТЗ.Свернуть("КлючСвязи, СкидкаНаценка, ЭтоСообщение", "Сумма");
	КонецЕсли;
	
	ТЗ.Колонки.Добавить("НапомнитьПозже", Новый ОписаниеТипов("Булево"));
	ТЗ.Колонки.Добавить("СпособПримененияСкидки", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыПримененияСкидокНаценок"));
	
	Если ЕстьРезультат Тогда
		Для Каждого СтрокаТЧ Из ТЗ Цикл
			СтрокаСкидкаНаценка = ДеревоСкидок.Строки.Найти(СтрокаТЧ.СкидкаНаценка, "Ссылка", Истина);
			СтрокаТЧ.СпособПримененияСкидки = СтрокаСкидкаНаценка.СпособПримененияСкидки;
		КонецЦикла;
	КонецЕсли;
	
	СохранитьДанныеОВыбранныхУправляемыхСкидках(ТЗ, ПараметрыРасчета);
	
	ВозвращаемыеДанные = Новый Структура;
	ВозвращаемыеДанные.Вставить("ДеревоСкидок",         ДеревоСкидок);
	ВозвращаемыеДанные.Вставить("ТаблицаСкидкиНаценки", ТЗ);
	ВозвращаемыеДанные.Вставить("ПараметрыРасчета",     ПараметрыРасчета);
	
	Возврат ВозвращаемыеДанные;
	
КонецФункции

// Функция выполняет расчет скидок (наценок) по переданным параметрам.
//
// Параметры:
//  ПараметрыРасчета - см. СкидкиНаценкиЗаполнениеСервер.НовыйПараметрыРассчитать
//  ВходныеПараметры - Структура - Входные параметры.
//
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * ДеревоСкидок - см. СформироватьДеревоСкидок
//   * ТаблицаСкидкиНаценки - см. ПустаяТаблицаСкидкиНаценки
//   * ПараметрыРасчета - см. СкидкиНаценкиЗаполнениеСервер.НовыйПараметрыРассчитать
//
Функция РассчитатьДеревоСкидокНаценок(ПараметрыРасчета, ВходныеПараметры) Экспорт
	
	ИспользоватьЦеновыеГруппы = ПолучитьФункциональнуюОпцию("ИспользоватьЦеновыеГруппы");
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ОсновныеТаблицы = ПакетЗапросовСоздать();
	ОсновныеТаблицы.Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	УдалятьВременныеТаблицы = Ложь;
	ПреобразовыватьПараметры = Ложь;
	Для Каждого КлючИЗначение Из ПараметрыРасчета Цикл
		ОсновныеТаблицы.Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
	ПакетЗапросовВставитьЗапросВПакет(ЗапросВременнаяТаблицаТовары(ПараметрыРасчета, ОсновныеТаблицы),
		ОсновныеТаблицы,
		Неопределено,
		УдалятьВременныеТаблицы,
		ПреобразовыватьПараметры);
		
	ПакетЗапросовВставитьЗапросВПакет(ЗапросКурсыВалют(ПараметрыРасчета),              ОсновныеТаблицы, "КурсыВалют",              УдалятьВременныеТаблицы, ПреобразовыватьПараметры);
	ПакетЗапросовВставитьЗапросВПакет(ЗапросСкидкиНаценки(ПараметрыРасчета),           ОсновныеТаблицы, "СкидкиНаценки",           УдалятьВременныеТаблицы, ПреобразовыватьПараметры);
	ПакетЗапросовВставитьЗапросВПакет(ЗапросТЧУсловияПредоставления(ПараметрыРасчета), ОсновныеТаблицы, "ТЧУсловияПредоставления", УдалятьВременныеТаблицы, ПреобразовыватьПараметры);
	ПакетЗапросовВставитьЗапросВПакет(ЗапросУсловияПредоставления(ПараметрыРасчета),   ОсновныеТаблицы, "УсловияПредоставления",   УдалятьВременныеТаблицы, ПреобразовыватьПараметры);
	// Для Подарков
	ПакетЗапросовВставитьЗапросВПакет(ЗапросТоварыПоСегментам(ПараметрыРасчета),       ОсновныеТаблицы, "ТоварыПоСегментам",       УдалятьВременныеТаблицы, ПреобразовыватьПараметры);
	// Для Специальной цены
	ПакетЗапросовВставитьЗапросВПакет(ЗапросЦеныНоменклатуры(ПараметрыРасчета),        ОсновныеТаблицы, "ЦеныНоменклатуры",        УдалятьВременныеТаблицы, ПреобразовыватьПараметры);
	Если ИспользоватьЦеновыеГруппы Тогда
		ПакетЗапросовВставитьЗапросВПакет(
			ЗапросСкидкиНаценкиПоЦеновымГруппам(ПараметрыРасчета),
			ОсновныеТаблицы,
			"УточненияЗначенияСкидкиНаценкиПоЦеновымГруппам",
			УдалятьВременныеТаблицы, ПреобразовыватьПараметры);
	КонецЕсли;
	
	ПакетЗапросовВыполнить(ОсновныеТаблицы);
	
	КурсыВалют = ПакетЗапросовРезультатПоИмени("КурсыВалют", ОсновныеТаблицы).Выгрузить();
	Для Каждого СтрокаТЧ Из КурсыВалют Цикл
		Если СтрокаТЧ.КурсЧислитель = 0 Тогда
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Для валюты %1 не установлен курс.
				           |Перед расчетом скидок (наценок) курсы валют должны быть установлены.'"), СтрокаТЧ.Валюта);
		КонецЕсли;
	КонецЦикла;
	
	Условия               = ПакетЗапросовРезультатПоИмени("ТЧУсловияПредоставления", ОсновныеТаблицы).Выгрузить();
	УсловияПредоставления = ПакетЗапросовРезультатПоИмени("УсловияПредоставления", ОсновныеТаблицы).Выгрузить();
	ДеревоСкидок          = СформироватьДеревоСкидок(ОсновныеТаблицы);
	
	ДеревоСкидок.Колонки.Добавить("Рассчитано", Новый ОписаниеТипов("Булево"));
	
	Скидки = ДеревоСкидок.Строки.НайтиСтроки(Новый Структура("ЭтоГруппа", Ложь), Истина);
	
	ТаблицаВыполненныеУсловия = ВыполнитьЗапросыУсловийПредоставления(
		УсловияПредоставления,
		Скидки,
		ПараметрыРасчета,
		МенеджерВременныхТаблиц);
	ПроверитьУсловияРекурсивно(ДеревоСкидок, Скидки, Условия, ТаблицаВыполненныеУсловия);
	
	Если ВходныеПараметры.ТолькоПредварительныйРасчет Тогда
		
		ВозвращаемыеДанные = Новый Структура;
		ВозвращаемыеДанные.Вставить("ДеревоСкидок",         ДеревоСкидок);
		ВозвращаемыеДанные.Вставить("ТаблицаСкидкиНаценки", ПустаяТаблицаСкидкиНаценки());
		ВозвращаемыеДанные.Вставить("ПараметрыРасчета",     ПараметрыРасчета);
		
		Возврат ВозвращаемыеДанные;
		
	КонецЕсли;
	
	ПараметрыРасчета.Вставить("ИспользоватьЦеновыеГруппы", ИспользоватьЦеновыеГруппы);
	ПараметрыРасчета.Вставить("ЦеныНоменклатуры",      ПакетЗапросовРезультатПоИмени("ЦеныНоменклатуры",  ОсновныеТаблицы).Выгрузить());
	ПараметрыРасчета.Вставить("ТоварыПоСегментам",     ПакетЗапросовРезультатПоИмени("ТоварыПоСегментам", ОсновныеТаблицы).Выгрузить());
	ПараметрыРасчета.Вставить("УсловияПредоставления", ПакетЗапросовРезультатПоИмени("УсловияПредоставления",         ОсновныеТаблицы).Выгрузить());
	
	ПараметрыРасчета.Вставить("ПримененныеСкидкиУмножение", ПустаяТаблицаСкидкиНаценки());
	ПараметрыРасчета.Вставить("ВыполненныеУсловия",         ТаблицаВыполненныеУсловия);
	ПараметрыРасчета.Вставить("КурсыВалют",                 КурсыВалют);
	
	ПараметрыРасчета.Вставить("ПустаяТаблицаСкидокСРасшифровкой", ПустаяТаблицаСкидокСРасшифровкой());
	ПараметрыРасчета.Вставить("ПустаяТаблицаРасшифровка",         ПустаяТаблицаРасшифровка());
	ПараметрыРасчета.Вставить("ПустаяТаблицаСкидкиНаценки",       ПустаяТаблицаСкидкиНаценки());
	
	ПараметрыРасчета.Вставить("КешУмножениеГруппы", Новый Соответствие);
	ПараметрыРасчета.Вставить("КешУмножениеСуммы",  Новый Соответствие);
	
	Если ИспользоватьЦеновыеГруппы Тогда
		
		ПараметрыРасчета.Вставить("УточненияЗначенияСкидкиНаценки", Новый Соответствие);
		
		УточненияЗначенияСкидкиНаценкиПоЦеновымГруппам = ПакетЗапросовРезультатПоИмени(
			"УточненияЗначенияСкидкиНаценкиПоЦеновымГруппам", ОсновныеТаблицы).Выгрузить();
		
		ТекущаяСкидка = Неопределено;
		Уточнения = Неопределено;
		
		Для Каждого СтрокаТЧ Из УточненияЗначенияСкидкиНаценкиПоЦеновымГруппам Цикл
			Если ТекущаяСкидка <> СтрокаТЧ.СкидкаНаценка Тогда
				Если ТекущаяСкидка <> Неопределено Тогда
					ПараметрыРасчета.УточненияЗначенияСкидкиНаценки.Вставить(ТекущаяСкидка, Уточнения);
				КонецЕсли;
				Уточнения = ПустаяТаблицаУточненияЗначенияСкидкиНаценки();
				ТекущаяСкидка = СтрокаТЧ.СкидкаНаценка;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(Уточнения.Добавить(), СтрокаТЧ);
		КонецЦикла;
		
		Если ТекущаяСкидка <> Неопределено Тогда
			ПараметрыРасчета.УточненияЗначенияСкидкиНаценки.Вставить(ТекущаяСкидка, Уточнения);
		КонецЕсли;
		
	КонецЕсли;
	
	РезультатРасчета = РассчитатьДеревоСкидок(ДеревоСкидок, ПараметрыРасчета);
	
	Возврат РезультатРасчета;
	
КонецФункции

// Процедура применяет результат расчета скидок (наценок) к объекту.
// Вызывается из форм документов.
// Параметры:
//  Объект - ДокументОбъект, ДанныеФормыСтруктура - Объект, в котором требуется рассчитать скидки (наценки).
//  ИмяТЧ  - Строка - имя табличной части для расчета скидок
//  ПримененныеСкидкиНаценки - Структура - Структура со свойствами:
//    * ДеревоСкидок - ДеревоЗначений - Дерево скидок (наценок).
//    * ТаблицаСкидкиНаценки - ТаблицаЗначений - Таблица с рассчитанными скидками.
//    * ПараметрыРасчета - Структура - Структура параметров расчета.
//  РассчитыватьСуммуСНДС - Булево - 
//  РассчитыватьСуммуВзаиморасчетов - Булево -
//  РеализацияСверхЗаказа - Булево - 
//  ДополнительныеПараметры - Структура - Структура с дополнительными параметрами
Процедура ПрименитьРезультатРасчетаКОбъекту(Объект,
                                            ИмяТЧ,
                                            ПримененныеСкидкиНаценки,
                                            РассчитыватьСуммуСНДС, 
                                            РассчитыватьСуммуВзаиморасчетов=Ложь,
                                            РеализацияСверхЗаказа = Ложь,
                                            ДополнительныеПараметры = Неопределено) Экспорт
	
	Если РеализацияСверхЗаказа Тогда
		Для каждого СтрокаСкидок Из ПримененныеСкидкиНаценки.ТаблицаСкидкиНаценки Цикл
			СтрокаТЧ = Объект.СкидкиНаценки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтрокаСкидок);
		КонецЦикла; 
	Иначе	
		Объект.СкидкиНаценки.Загрузить(ПримененныеСкидкиНаценки.ТаблицаСкидкиНаценки);
	КонецЕсли;
	АвтоматическиеСкидкиНаценки = ПримененныеСкидкиНаценки.ТаблицаСкидкиНаценки.Скопировать(Новый Структура("СпособПримененияСкидки", Перечисления.СпособыПримененияСкидокНаценок.ПрименитьВМоментРасчетаСкидокНаценок));
	
	// Заполнение скидок в табличной части "Товары"
	АвтоматическиеСкидкиНаценки.Свернуть("КлючСвязи", "Сумма");
	АвтоматическиеСкидкиНаценки.Индексы.Добавить("КлючСвязи");
	
	Если ИмяТЧ="Услуги" Тогда
		ИмяКоличества = "Количество";
	ИначеЕсли ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("ИмяКолонкиКоличество") Тогда
		ИмяКоличества = ДополнительныеПараметры.ИмяКолонкиКоличество;
	Иначе
		ИмяКоличества = "КоличествоУпаковок";
	КонецЕсли;
	
	Для Каждого СтрокаТЧ Из Объект[ИмяТЧ] Цикл
		
		Если РеализацияСверхЗаказа И СтрокаТЧ.КодСтроки > 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		СтрокаТаблицы = АвтоматическиеСкидкиНаценки.Найти(СтрокаТЧ.КлючСвязи, "КлючСвязи");
		Если СтрокаТаблицы = Неопределено Тогда
			СуммаАвтоматическойСкидки = 0;
		Иначе
			СуммаАвтоматическойСкидки = Окр(СтрокаТаблицы.Сумма, 2);
		КонецЕсли;
		
		РассчитыватьСуммуВзаиморасчетовПоСтроке = (РассчитыватьСуммуВзаиморасчетов И СтрокаТЧ.СуммаАвтоматическойСкидки <> СуммаАвтоматическойСкидки);
		
		СтрокаТЧ.СуммаАвтоматическойСкидки = СуммаАвтоматическойСкидки;
		
		// Применение автоматической скидки.
		СуммаБезСкидки = Окр(СтрокаТЧ[ИмяКоличества] * СтрокаТЧ.Цена,2);
		
		Если СуммаБезСкидки <> 0 Тогда
			СтрокаТЧ.ПроцентРучнойСкидки = 100 * СтрокаТЧ.СуммаРучнойСкидки / СуммаБезСкидки;
		КонецЕсли;
		
		СуммаСкидки = СуммаАвтоматическойСкидки + СтрокаТЧ.СуммаРучнойСкидки;
		
		ИспользоватьОплатуБонуснымиБаллами = Ложь;
		
		Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
			И ДополнительныеПараметры.Свойство("ИспользоватьОплатуБонуснымиБаллами", ИспользоватьОплатуБонуснымиБаллами)
			И ИспользоватьОплатуБонуснымиБаллами Тогда
			СуммаСкидки = СуммаСкидки + СтрокаТЧ.СуммаБонусныхБалловКСписаниюВВалюте;
		КонецЕсли;
		
		СтрокаТЧ.ПроцентАвтоматическойСкидки = ?(СуммаБезСкидки = 0, 0 , 100 * СуммаАвтоматическойСкидки / СуммаБезСкидки);
		
		СтрокаТЧ.Сумма    = СуммаБезСкидки - ?(СуммаСкидки > СуммаБезСкидки, СуммаБезСкидки, СуммаСкидки);
		СтрокаТЧ.СуммаНДС = РассчитатьСуммуНДС(СтрокаТЧ.Сумма, СтрокаТЧ.СтавкаНДС, Объект);
		
		Если РассчитыватьСуммуСНДС Тогда
			СтрокаТЧ.СуммаСНДС = СтрокаТЧ.Сумма + ?(Объект.ЦенаВключаетНДС, 0, СтрокаТЧ.СуммаНДС);
		КонецЕсли;
		
		Если РассчитыватьСуммуВзаиморасчетовПоСтроке Тогда
			СтрокаТЧ.СуммаВзаиморасчетов = 0;
		КонецЕсли;
		
	КонецЦикла;
	
	Объект.СуммаДокумента = ЦенообразованиеКлиентСервер.ПолучитьСуммуДокумента(Объект[ИмяТЧ], Объект.ЦенаВключаетНДС);
	Объект.СкидкиРассчитаны = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ЗапросыПодготовкиДанныхДляРасчета

// Функция формирует запрос для таблицы значений скидок (наценок) по ценовым группам.
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ЗапросКурсыВалют(ПараметрыРасчета)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОтносительныеКурсыВСрезПоследних.Валюта    КАК Валюта,
	|	ОтносительныеКурсыВСрезПоследних.КурсЧислитель КАК КурсЧислитель,
	|	ОтносительныеКурсыВСрезПоследних.КурсЗнаменатель КАК КурсЗнаменатель
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&ТекущаяДата, БазоваяВалюта = &БазоваяВалюта) КАК ОтносительныеКурсыВСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|////#123456789#//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалют.Валюта    КАК Валюта,
	|	КурсыВалют.КурсЧислитель КАК КурсЧислитель,
	|	КурсыВалют.КурсЗнаменатель КАК КурсЗнаменатель
	|ИЗ
	|	КурсыВалют
	|");
	
	Запрос.Параметры.Вставить("ТекущаяДата", ПараметрыРасчета.ТекущаяДата);
	Запрос.Параметры.Вставить("БазоваяВалюта", ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию());
	
	Возврат Запрос;
	
КонецФункции

// Функция формирует запрос для таблицы действующих скидок (наценок).
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ЗапросСкидкиНаценки(ПараметрыРасчета, РасширенныйВариант = Истина)
	
	Запрос = Новый Запрос(
	"////#123456789#//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СкидкиНаценки.Ссылка                    КАК Ссылка,
	|	СкидкиНаценки.РеквизитДопУпорядочивания КАК РеквизитДопУпорядочивания,
	|	СкидкиНаценки.ВариантСовместногоПрименения                  КАК ВариантСовместногоПрименения,
	|	СкидкиНаценки.ВариантРасчетаРезультатаСовместногоПрименения КАК ВариантРасчетаРезультатаСовместногоПрименения,
	|	СкидкиНаценки.ПометкаУдаления        КАК ПометкаУдаления,
	|	СкидкиНаценки.СпособПредоставления   КАК СпособПредоставления,
	|	СкидкиНаценки.СпособПримененияСкидки КАК СпособПримененияСкидки,
	|	СкидкиНаценки.ПрименятьУмножениеВРамкахВышестоящейГруппы КАК ПрименятьУмножениеВРамкахВышестоящейГруппы,
	|	&РасширенныйВариант1,
	|	СкидкиНаценки.ТочностьОкругления        КАК ТочностьОкругления,
	|	СкидкиНаценки.ВариантОкругления         КАК ВариантОкругления,
	|	СкидкиНаценки.ПсихологическоеОкругление КАК ПсихологическоеОкругление,
	|	СкидкиНаценки.ЭтоГруппа                    КАК ЭтоГруппа,
	|	СкидкиНаценки.Управляемая                  КАК Управляемая,
	|	ВЫБОР
	|		КОГДА СкидкиНаценки.ССЫЛКА В(&УправляемыеСкидки) ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ КАК НазначенаПользователем,
	|	СкидкиНаценки.ВидЦены                           КАК ВидЦены,
	|	СкидкиНаценки.СегментПодарков                   КАК СегментПодарков,
	|	СкидкиНаценки.СегментНоменклатурыОграничения    КАК СегментНоменклатурыОграничения,
	|	СкидкиНаценки.ИспользоватьКратность             КАК ИспользоватьКратность,
	|	СкидкиНаценки.УсловиеДляСкидкиКоличеством       КАК УсловиеДляСкидкиКоличеством,
	|	СкидкиНаценки.ВариантОтбораНоменклатуры         КАК ВариантОтбораНоменклатуры,
	|	&РасширенныйВариант2,
	|	СкидкиНаценки.ВалютаПредоставления КАК ВалютаПредоставления
	|ИЗ
	|	Справочник.СкидкиНаценки КАК СкидкиНаценки
	|		,&РасширенныйВариант3
	|	
	|ГДЕ
	|	СкидкиНаценки.Ссылка В(&СкидкиНаценки)
	|
	|УПОРЯДОЧИТЬ ПО
	|	СкидкиНаценки.РеквизитДопУпорядочивания
	|ИТОГИ ПО
	|	СкидкиНаценки.Ссылка ИЕРАРХИЯ");
	
	Запрос.Параметры.Вставить("СкидкиНаценки",     ПараметрыРасчета.СкидкиНаценки);
	Запрос.Параметры.Вставить("УправляемыеСкидки", ПараметрыРасчета.УправляемыеСкидки);
	
	Если РасширенныйВариант Тогда
		
		Запрос.Параметры.Вставить("ВалютаДокумента", ПараметрыРасчета.ВалютаДокумента);
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&РасширенныйВариант1,", 
			"ВЫБОР
			|		КОГДА СкидкиНаценки.СпособПримененияСкидки = ЗНАЧЕНИЕ(Перечисление.СпособыПримененияСкидокНаценок.ПрименитьВМоментРасчетаСкидокНаценок)
			|			И (СкидкиНаценки.СпособПредоставления = ЗНАЧЕНИЕ(Перечисление.СпособыПредоставленияСкидокНаценок.Сумма) ИЛИ СкидкиНаценки.СпособПредоставления = ЗНАЧЕНИЕ(Перечисление.СпособыПредоставленияСкидокНаценок.СуммаДляКаждойСтроки))
			|			ТОГДА СкидкиНаценки.ЗначениеСкидкиНаценки * ЕСТЬNULL(КурсыВалютПредоставления.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютДокумента.КурсЗнаменатель, 1) / (ЕСТЬNULL(КурсыВалютДокумента.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютПредоставления.КурсЗнаменатель, 1))
			|		ИНАЧЕ СкидкиНаценки.ЗначениеСкидкиНаценки
			|	КОНЕЦ КАК ЗначениеСкидкиНаценки,");
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&РасширенныйВариант2,", 
			"СкидкиНаценки.ПараметрыВнешнейОбработки         КАК ПараметрыВнешнейОбработки,
			|	СкидкиНаценки.УстановленДополнительныйОтбор     КАК УстановленДополнительныйОтбор,
			|	СкидкиНаценки.ХранилищеНастроекКомпоновкиДанных КАК ХранилищеНастроекКомпоновкиДанных,
			|	СкидкиНаценки.УчитыватьХарактеристики КАК УчитыватьХарактеристики,
			|	БонусныеПрограммыЛояльности.ВалютаКонвертацииБонусов      КАК ВалютаКонвертацииБонусов,
			|	БонусныеПрограммыЛояльности.КурсКонвертацииБонусовВВалюту КАК КурсКонвертацииБонусовВВалюту,");
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ",&РасширенныйВариант3", 
			"ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютПредоставления
			|		ПО (КурсыВалютПредоставления.Валюта = СкидкиНаценки.ВалютаПредоставления)
			|	
			|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютДокумента
			|		ПО (КурсыВалютДокумента.Валюта = &ВалютаДокумента)
			|	
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.БонусныеПрограммыЛояльности КАК БонусныеПрограммыЛояльности
			|		ПО БонусныеПрограммыЛояльности.Ссылка = СкидкиНаценки.БонуснаяПрограммаЛояльности");
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&РасширенныйВариант1,", 
			"СкидкиНаценки.ЗначениеСкидкиНаценки,");
			
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&РасширенныйВариант2,", "");
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ",&РасширенныйВариант3", "");
		
	КонецЕсли;
	
	Возврат Запрос;
	
КонецФункции

// Функция формирует запрос для таблицы действующих скидок (наценок).
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ЗапросТЧУсловияПредоставления(ПараметрыРасчета)
	
	Запрос = Новый Запрос(
	"////#123456789#//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Ссылка КАК СкидкаНаценка,
	|	Т.УсловиеПредоставления КАК УсловиеПредоставления
	|ИЗ
	|	Справочник.СкидкиНаценки.УсловияПредоставления КАК Т
	|ГДЕ
	|	Т.Ссылка В(&СкидкиНаценки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Ссылка КАК СкидкаНаценка,
	|	Т.Ссылка КАК УсловиеПредоставления
	|ИЗ
	|	Справочник.СкидкиНаценки КАК Т
	|ГДЕ
	|	Т.Ссылка В(&СкидкиНаценки)
	|	И (Т.ВариантОтбораНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВариантыОтбораНоменклатурыДляРасчетаСкидокНаценок.БезОграничений)
	|	ИЛИ Т.УстановленДополнительныйОтбор)
	|");
	
	Запрос.Параметры.Вставить("СкидкиНаценки", ПараметрыРасчета.СкидкиНаценки);
	
	Возврат Запрос;
	
КонецФункции

// Функция формирует запрос для таблицы действующих скидок (наценок).
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ЗапросУсловияПредоставления(ПараметрыРасчета)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Ссылка КАК Ссылка,
	|	
	|	ВЫБОР
	|		КОГДА (
	|				Т.УсловиеПредоставления = ЗНАЧЕНИЕ(Перечисление.УсловияПредоставленияСкидокНаценок.ЗаРазовыйОбъемПродаж)
	|				ИЛИ Т.УсловиеПредоставления = ЗНАЧЕНИЕ(Перечисление.УсловияПредоставленияСкидокНаценок.ЗаНакопленныйОбъемПродаж)
	|				)
	|				И Т.КритерийОграниченияПримененияЗаОбъемПродаж = ЗНАЧЕНИЕ(Перечисление.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.Сумма)
	|			ТОГДА Т.ЗначениеУсловияОграничения * ЕСТЬNULL(КурсыВалютОграничения.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютДокумента.КурсЗнаменатель, 1) / (ЕСТЬNULL(КурсыВалютДокумента.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютОграничения.КурсЗнаменатель, 1))
	|		ИНАЧЕ Т.ЗначениеУсловияОграничения
	|	КОНЕЦ КАК ЗначениеУсловияОграничения
	|	
	|ПОМЕСТИТЬ УсловияПредоставления
	|ИЗ
	|	Справочник.СкидкиНаценки.УсловияПредоставления КАК Условия
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УсловияПредоставленияСкидокНаценок КАК Т
	|		ПО Т.Ссылка = Условия.УсловиеПредоставления
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютОграничения
	|		ПО (КурсыВалютОграничения.Валюта = Условия.УсловиеПредоставления.ВалютаОграничения)
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютДокумента
	|		ПО (КурсыВалютДокумента.Валюта = &ВалютаДокумента)
	|ГДЕ
	|	Условия.Ссылка В (&СкидкиНаценки)
	|;
	|
	|////#123456789#//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Ссылка                                         КАК Ссылка,
	|	УсловияПредоставления.ЗначениеУсловияОграничения КАК ЗначениеУсловияОграничения,
	|	Т.УсловиеПредоставления                          КАК УсловиеПредоставления,
	|	Т.ВариантОпределенияПериодаНакопительнойСкидки   КАК ВариантОпределенияПериодаНакопительнойСкидки,
	|	Т.ВариантНакопления                              КАК ВариантНакопления,
	|	Т.ВалютаОграничения                              КАК ВалютаОграничения,
	|	Т.ВалютаОграничения.Код                          КАК ЦифровойКодВалютыОграничения,
	|	Т.ТипСравнения                                   КАК ТипСравнения,
	|	Т.КритерийОграниченияПримененияЗаОбъемПродаж     КАК КритерийОграниченияПримененияЗаОбъемПродаж,
	|	Т.ГрафикОплаты                                   КАК ГрафикОплаты,
	|	Т.ФормаОплаты                                    КАК ФормаОплаты,
	|	Т.СегментНоменклатурыОграничения                 КАК СегментНоменклатурыОграничения,
	|	Т.ПериодНакопления                               КАК ПериодНакопления,
	|	Т.ГруппаПользователей                            КАК ГруппаПользователей,
	|	Т.ВидКартыЛояльности                             КАК ВидКартыЛояльности,
	|	Т.КоличествоПериодовНакопления                   КАК КоличествоПериодовНакопления,
	|	Т.ВариантОтбораНоменклатуры                      КАК ВариантОтбораНоменклатуры,
	|	Т.ХранилищеНастроекКомпоновкиДанных              КАК ХранилищеНастроекКомпоновкиДанных,
	|	Т.ПараметрыВнешнейОбработки                      КАК ПараметрыВнешнейОбработки,
	|	Т.КоличествоДнейДоДняРождения                    КАК КоличествоДнейДоДняРождения,
	|	Т.КоличествоДнейПослеДняРождения                 КАК КоличествоДнейПослеДняРождения,
	|	Т.ВключатьТекущуюПродажуВНакопленныйОбъемПродаж  КАК ВключатьТекущуюПродажуВНакопленныйОбъемПродаж,
	|	Т.УчитыватьХарактеристики                        КАК УчитыватьХарактеристики,
	|	Т.СегментПартнеров                               КАК СегментПартнеров
	|ИЗ
	|	УсловияПредоставления КАК УсловияПредоставления
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УсловияПредоставленияСкидокНаценок КАК Т
	|		ПО Т.Ссылка = УсловияПредоставления.Ссылка
	|");
	
	Запрос.Параметры.Вставить("СкидкиНаценки",   ПараметрыРасчета.СкидкиНаценки);
	Запрос.Параметры.Вставить("ВалютаДокумента", ПараметрыРасчета.ВалютаДокумента);
	
	Возврат Запрос;
	
КонецФункции

// Функция формирует запрос для таблицы товаров по сегментам.
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ЗапросВременнаяТаблицаТовары(ПараметрыРасчета, ОсновныеТаблицы)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Товары.КлючСвязи          КАК КлючСвязи,
	|	Товары.Номенклатура       КАК Номенклатура,
	|	Товары.Характеристика     КАК Характеристика,
	|	Товары.Упаковка           КАК Упаковка,
	|	Товары.Серия              КАК Серия,
	|	Товары.Склад              КАК Склад,
	|
	|	ВЫБОР
	|		КОГДА Товары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) ТОГДА
	|			9999999999
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ(РАЗНОСТЬДАТ(&ТекущаяДата, Товары.СрокГодности, ДЕНЬ) КАК Число(10))
	|	КОНЕЦ КАК КоличествоДнейДоОкончанияСрокаГодности,
	|
	|	Товары.ВидЦены            КАК ВидЦены,
	|	Товары.Количество         КАК Количество,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Товары.Цена                             КАК ЦенаЗаУпаковку,
	|	Товары.КоличествоУпаковок * Товары.Цена КАК Сумма
	|ПОМЕСТИТЬ ВременнаяТаблицаТовары
	|ИЗ
	|	&ТоварыСоСрокомГодности КАК Товары
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|");
	
	ТоварыСоСрокомГодности = ПараметрыРасчета.Товары.Скопировать(); // ТаблицаЗначений
	ТоварыСоСрокомГодности.Колонки.Добавить("СрокГодности", Новый ОписаниеТипов("Дата"));
	Для каждого СтрокаТоваров Из ТоварыСоСрокомГодности Цикл
		Если ЗначениеЗаполнено(СтрокаТоваров.Серия) Тогда
			СтрокаТоваров.СрокГодности = СтрокаТоваров.Серия.ГоденДо;
		КонецЕсли;
	КонецЦикла;
	
	Запрос.Параметры.Вставить("ТоварыСоСрокомГодности", ТоварыСоСрокомГодности);
	Запрос.Параметры.Вставить("ТекущаяДата",            ПараметрыРасчета.ТекущаяДата);
	
	ОсновныеТаблицы.Запрос.Параметры.Вставить("ТоварыСоСрокомГодности", ТоварыСоСрокомГодности);
	
	Возврат Запрос;
	
КонецФункции

// Функция формирует запрос для таблицы товаров по сегментам.
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ЗапросТоварыПоСегментам(ПараметрыРасчета)
	
	Запрос = Новый Запрос(
	"////#123456789#//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.КлючСвязи             КАК КлючСвязи,
	|	Товары.Номенклатура          КАК Номенклатура,
	|	Товары.Характеристика        КАК Характеристика,
	|	Товары.Упаковка              КАК Упаковка,
	|	Товары.Количество            КАК Количество,
	|	Товары.КоличествоУпаковок    КАК КоличествоУпаковок,
	|	Товары.Серия                 КАК Серия,
	|	Товары.ВидЦены               КАК ВидЦены,
	|	НоменклатураСегмента.Сегмент КАК СегментНоменклатуры,
	|	Т.ЦеноваяГруппа              КАК ЦеноваяГруппа,
	|	Товары.Сумма                 КАК Сумма
	|ИЗ
	|	ВременнаяТаблицаТовары КАК Товары
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураСегмента КАК НоменклатураСегмента
	|		ПО    Товары.Номенклатура   = НоменклатураСегмента.Номенклатура
	|			И Товары.Характеристика = НоменклатураСегмента.Характеристика
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Т
	|		ПО Т.Ссылка = Товары.Номенклатура
	|");
	
	Возврат Запрос;
	
КонецФункции

// Функция формирует запрос для таблицы значений скидок (наценок) по ценовым группам.
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ЗапросСкидкиНаценкиПоЦеновымГруппам(ПараметрыРасчета)
	
	Запрос = Новый Запрос(
	"////#123456789#//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УточненияПоЦеновымГруппам.Ссылка        КАК СкидкаНаценка,
	|	УточненияПоЦеновымГруппам.ЦеноваяГруппа КАК Ключ,
	|	ВЫБОР
	|		КОГДА (СкидкиНаценки.СпособПредоставления = ЗНАЧЕНИЕ(Перечисление.СпособыПредоставленияСкидокНаценок.Сумма) ИЛИ СкидкиНаценки.СпособПредоставления = ЗНАЧЕНИЕ(Перечисление.СпособыПредоставленияСкидокНаценок.СуммаДляКаждойСтроки))
	|			ТОГДА УточненияПоЦеновымГруппам.ЗначениеСкидкиНаценки * ЕСТЬNULL(КурсыВалютПредоставления.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютДокумента.КурсЗнаменатель, 1) / (ЕСТЬNULL(КурсыВалютДокумента.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютПредоставления.КурсЗнаменатель, 1))
	|		ИНАЧЕ УточненияПоЦеновымГруппам.ЗначениеСкидкиНаценки
	|	КОНЕЦ КАК Значение
	|ИЗ
	|	Справочник.СкидкиНаценки КАК СкидкиНаценки
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СкидкиНаценки.ЦеновыеГруппы КАК УточненияПоЦеновымГруппам
	|		ПО СкидкиНаценки.Ссылка = УточненияПоЦеновымГруппам.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютПредоставления
	|		ПО (КурсыВалютПредоставления.Валюта = СкидкиНаценки.ВалютаПредоставления)
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютДокумента
	|		ПО (КурсыВалютДокумента.Валюта = &ВалютаДокумента)
	|ГДЕ
	|	СкидкиНаценки.Ссылка В (&СкидкиНаценки)
	|УПОРЯДОЧИТЬ ПО
	|	УточненияПоЦеновымГруппам.Ссылка
	|");
	
	Запрос.Параметры.Вставить("СкидкиНаценки", ПараметрыРасчета.СкидкиНаценки);
	Запрос.Параметры.Вставить("ВалютаДокумента", ПараметрыРасчета.ВалютаДокумента);
	
	Возврат Запрос;
	
КонецФункции

// Функция формирует запрос для таблицы цены номенклатуры (цена пересчитывается в валюту документа за единицу хранения).
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ЗапросЦеныНоменклатуры(ПараметрыРасчета)

	Если ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25(ПараметрыРасчета.ТекущаяДата) Тогда
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВременнаяТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ВременнаяТаблицаТовары.Характеристика КАК Характеристика,
		|	ВременнаяТаблицаТовары.Упаковка КАК Упаковка,
		|	ВЫБОР
		|		КОГДА ВидыНоменклатуры.НастройкиКлючаЦенПоХарактеристике = ЗНАЧЕНИЕ(Перечисление.ВариантОтбораДляКлючаЦен.НеИспользовать)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатурыДляЦенообразования.ПустаяСсылка)
		|		ИНАЧЕ ВременнаяТаблицаТовары.Характеристика.ХарактеристикаНоменклатурыДляЦенообразования
		|	КОНЕЦ КАК ХарактеристикаЦО,
		|	ЕСТЬNULL(ВЫБОР
		|			КОГДА ВидыНоменклатуры.НастройкиКлючаЦенПоСерии = ЗНАЧЕНИЕ(Перечисление.ВариантОтбораДляКлючаЦен.НеИспользовать)
		|				ТОГДА ЗНАЧЕНИЕ(Справочник.СерииНоменклатурыДляЦенообразования.ПустаяСсылка)
		|			ИНАЧЕ ВременнаяТаблицаТовары.Серия.СерияНоменклатурыДляЦенообразования
		|		КОНЕЦ, ЗНАЧЕНИЕ(Справочник.СерииНоменклатурыДляЦенообразования.ПустаяСсылка)) КАК СерияЦО,
		|	ВЫБОР
		|		КОГДА ВидыНоменклатуры.НастройкиКлючаЦенПоУпаковке = ЗНАЧЕНИЕ(Перечисление.ВариантОтбораДляКлючаЦен.НеИспользовать)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|		ИНАЧЕ ВременнаяТаблицаТовары.Упаковка
		|	КОНЕЦ КАК УпаковкаЦО
		|ПОМЕСТИТЬ ВременнаяТаблицаТовары25
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
		|		ПО (ВидыНоменклатуры.Ссылка = ВременнаяТаблицаТовары.Номенклатура.ВидНоменклатуры)
		|;
		|
		|////#123456789#//////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЦеныНоменклатурыСрезПоследних.ВидЦены КАК ВидЦены,
		|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
		|	ВременнаяТаблицаТовары.Характеристика КАК Характеристика,
		|	ЦеныНоменклатурыСрезПоследних.Упаковка КАК Упаковка,
		|	ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК УпаковкаКоэффициент,
		|	ВЫРАЗИТЬ(ЦеныНоменклатурыСрезПоследних.Цена 
		|		* ( ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютДокумента.КурсЗнаменатель, 1) )
		|		/ ( ЕСТЬNULL(КурсыВалютДокумента.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) )
		|	КАК ЧИСЛО(31, 2)) КАК Цена
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры25.СрезПоследних(
		|			КОНЕЦПЕРИОДА(&ТекущаяДата, ДЕНЬ),
		|			ВидЦены В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						Т.ВидЦены
		|					ИЗ
		|						Справочник.СкидкиНаценки КАК Т
		|					ГДЕ
		|						Т.Ссылка В (&СкидкиНаценки)
		|						И Т.СпособПредоставления = ЗНАЧЕНИЕ(Перечисление.СпособыПредоставленияСкидокНаценок.ВидЦены))
		|				И (Номенклатура, ХарактеристикаЦО, СерияЦО, УпаковкаЦО) В
		|					(ВЫБРАТЬ
		|						Т.Номенклатура,
		|						Т.ХарактеристикаЦО,
		|						Т.СерияЦО,
		|						Т.УпаковкаЦО
		|					ИЗ
		|						ВременнаяТаблицаТовары25 КАК Т)) КАК ЦеныНоменклатурыСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаТовары25 КАК ВременнаяТаблицаТовары
		|		ПО ЦеныНоменклатурыСрезПоследних.Номенклатура = ВременнаяТаблицаТовары.Номенклатура
		|			И ЦеныНоменклатурыСрезПоследних.ХарактеристикаЦО = ВременнаяТаблицаТовары.ХарактеристикаЦО
		|			И ЦеныНоменклатурыСрезПоследних.СерияЦО = ВременнаяТаблицаТовары.СерияЦО
		|			И ЦеныНоменклатурыСрезПоследних.УпаковкаЦО = ВременнаяТаблицаТовары.УпаковкаЦО
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
		|		ПО (КурсыВалют.Валюта = ЦеныНоменклатурыСрезПоследних.Валюта)
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютДокумента
		|		ПО (КурсыВалютДокумента.Валюта = &ВалютаДокумента)");
	Иначе
		Запрос = Новый Запрос(
		"////#123456789#//////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЦеныНоменклатурыСрезПоследних.ВидЦены        КАК ВидЦены,
		|	ЦеныНоменклатурыСрезПоследних.Номенклатура   КАК Номенклатура,
		|	ЦеныНоменклатурыСрезПоследних.Характеристика КАК Характеристика,
		|	ЦеныНоменклатурыСрезПоследних.Упаковка       КАК Упаковка,
		|	
		|	ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК УпаковкаКоэффициент,
		|	
		|	ВЫРАЗИТЬ(ЦеныНоменклатурыСрезПоследних.Цена 
		|		* ( ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютДокумента.КурсЗнаменатель, 1) )
		|		/ ( ЕСТЬNULL(КурсыВалютДокумента.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) )
		|	КАК ЧИСЛО(31, 2)) КАК Цена
		|	
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(КОНЕЦПЕРИОДА(&ТекущаяДата, ДЕНЬ),
		|			ВидЦены В (ВЫБРАТЬ РАЗЛИЧНЫЕ
		|							Т.ВидЦены
		|						ИЗ
		|							Справочник.СкидкиНаценки КАК Т
		|						ГДЕ
		|							Т.Ссылка В (&СкидкиНаценки) И Т.СпособПредоставления = ЗНАЧЕНИЕ(Перечисление.СпособыПредоставленияСкидокНаценок.ВидЦены))
		|				И (Номенклатура, Характеристика) В (ВЫБРАТЬ Т.Номенклатура, Т.Характеристика ИЗ ВременнаяТаблицаТовары КАК Т)
		|	
		|	) КАК ЦеныНоменклатурыСрезПоследних
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
		|		ПО (КурсыВалют.Валюта = ЦеныНоменклатурыСрезПоследних.Валюта)
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютДокумента
		|		ПО (КурсыВалютДокумента.Валюта = &ВалютаДокумента)");
	КонецЕсли;	
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ЦеныНоменклатурыСрезПоследних.Упаковка",
		"ЦеныНоменклатурыСрезПоследних.Номенклатура"));
	
	Запрос.Параметры.Вставить("СкидкиНаценки",   ПараметрыРасчета.СкидкиНаценки);
	Запрос.Параметры.Вставить("ВалютаДокумента", ПараметрыРасчета.ВалютаДокумента);
	Запрос.Параметры.Вставить("ТекущаяДата",     ПараметрыРасчета.ТекущаяДата);
	
	Возврат Запрос;
	
КонецФункции
#КонецОбласти

#Область ЗапросыПроверкиУсловийПредоставленияСкидок

// Параметры:
// 	ИсточникДанных - СтрокаТаблицыЗначений - Содержит в том числе:
// 		* Ссылка - СправочникСсылка.УсловияПредоставленияСкидокНаценок - Ссылка на элемент справочника
// 	СтрокаЗамены - Строка
// 
// Возвращаемое значение:
// 	Структура:
// 		* ТекстЗапроса - Строка
// 		* Параметры - Структура
// 
Функция ДанныеФильтраПоНоменклатуре(ИсточникДанных, СтрокаЗамены) Экспорт
	
	Если ТипЗнч(ИсточникДанных.Ссылка) = Тип("СправочникСсылка.УсловияПредоставленияСкидокНаценок") Тогда
		Если ИсточникДанных.КритерийОграниченияПримененияЗаОбъемПродаж = Перечисления.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.КоличествоОдинаковых Тогда
			СхемаКомпоновкиДанных = Справочники.СкидкиНаценки.ПолучитьМакет("ОтборУсловияПредоставленияКоличествоОдинаковых");
		ИначеЕсли ИсточникДанных.КритерийОграниченияПримененияЗаОбъемПродаж = Перечисления.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.КоличествоРазличных Тогда
			СхемаКомпоновкиДанных = Справочники.СкидкиНаценки.ПолучитьМакет("ОтборУсловияПредоставленияКоличествоРазличных");
		Иначе
			СхемаКомпоновкиДанных = Справочники.СкидкиНаценки.ПолучитьМакет("ОтборУсловияПредоставленияСуммаКоличество");
		КонецЕсли;
	Иначе
		СхемаКомпоновкиДанных = Справочники.СкидкиНаценки.ПолучитьМакет("ОтборСтрок");
		
		Набор = СхемаКомпоновкиДанных.НаборыДанных.Найти("НаборДанных");
		ОбеспечениеВДокументахСервер.СкидкиНаценкиПодменитьОстатокНаСкладеПереопределяемый(Набор.Запрос);
		Поля = "ОстатокНаСкладе.Номенклатура,ОстатокНаСкладе.Характеристика,ОстатокНаСкладе.Склад";
		Набор.Запрос = РегистрыСведений.ТоварныеОграничения.ПодставитьСоединение(Набор.Запрос, "ПодстановкаТоварногоОграничения", Поля);
		
	КонецЕсли;
	
	Компоновщик = Новый КомпоновщикНастроекКомпоновкиДанных;
	Компоновщик.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	Компоновщик.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	Если ТипЗнч(ИсточникДанных.Ссылка) = Тип("СправочникСсылка.УсловияПредоставленияСкидокНаценок") Тогда
		УстановленДополнительныйОтбор = Ложь;
	Иначе
		УстановленДополнительныйОтбор = ИсточникДанных.УстановленДополнительныйОтбор;
	КонецЕсли;
	
	Если ИсточникДанных.ВариантОтбораНоменклатуры = Перечисления.ВариантыОтбораНоменклатурыДляРасчетаСкидокНаценок.ОтборКомпоновкиДанных
		ИЛИ УстановленДополнительныйОтбор Тогда
		ХранилищеНастроекКомпоновкиДанных = ИсточникДанных.ХранилищеНастроекКомпоновкиДанных; // ХранилищеЗначения
		Настройки = ХранилищеНастроекКомпоновкиДанных.Получить();
	КонецЕсли;
	
	Если ИсточникДанных.ВариантОтбораНоменклатуры = Перечисления.ВариантыОтбораНоменклатурыДляРасчетаСкидокНаценок.ОтборКомпоновкиДанных Тогда
		
		Если Настройки <> Неопределено И ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(Настройки, "ОтборПоНоменклатуре") Тогда
			КомпоновкаДанныхКлиентСервер.СкопироватьЭлементы(Компоновщик.Настройки.Отбор, Настройки.ОтборПоНоменклатуре.Отбор, Ложь);
		КонецЕсли;
		
		СегментыСервер.ВключитьОтборПоСегментуНоменклатурыВСКД(Компоновщик);
		
	ИначеЕсли ИсточникДанных.ВариантОтбораНоменклатуры = Перечисления.ВариантыОтбораНоменклатурыДляРасчетаСкидокНаценок.СегментНоменклатуры Тогда
		
		ЭлементОтбора = Компоновщик.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СегментНоменклатуры");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = ИсточникДанных.СегментНоменклатурыОграничения;
		ЭлементОтбора.Использование = Истина;
		
		СегментыСервер.ВключитьОтборПоСегментуНоменклатурыВСКД(Компоновщик);
		
	ИначеЕсли ИсточникДанных.ВариантОтбораНоменклатуры = Перечисления.ВариантыОтбораНоменклатурыДляРасчетаСкидокНаценок.НеСегментНоменклатуры Тогда
		
		ЭлементОтбора = Компоновщик.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НеСегментНоменклатуры");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = ИсточникДанных.СегментНоменклатурыОграничения;
		ЭлементОтбора.Использование = Истина;
		
		СегментыСервер.ВключитьОтборПоНеСегментуНоменклатурыВСКД(Компоновщик);
		
	ИначеЕсли ИсточникДанных.ВариантОтбораНоменклатуры = Перечисления.ВариантыОтбораНоменклатурыДляРасчетаСкидокНаценок.СписокНоменклатуры Тогда
		
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(Компоновщик, "ИспользуетсяОтборПоТоварамИзСписка", Истина, Истина);
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(Компоновщик, "Источник", ИсточникДанных.Ссылка, Истина);
		
	КонецЕсли;
	
	Если УстановленДополнительныйОтбор Тогда
		Если Настройки <> Неопределено И ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(Настройки, "ДополнительныеУсловия") Тогда
			КомпоновкаДанныхКлиентСервер.СкопироватьЭлементы(Компоновщик.Настройки.Отбор, Настройки.ДополнительныеУсловия.Отбор, Ложь);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(ИсточникДанных.Ссылка) = Тип("СправочникСсылка.УсловияПредоставленияСкидокНаценок") Тогда
		Если ИсточникДанных.КритерийОграниченияПримененияЗаОбъемПродаж = Перечисления.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.КоличествоОдинаковых Тогда
			КомпоновкаДанныхКлиентСервер.УстановитьПараметр(Компоновщик, "УчитыватьХарактеристики", ИсточникДанных.УчитыватьХарактеристики, Истина);
		ИначеЕсли ИсточникДанных.КритерийОграниченияПримененияЗаОбъемПродаж = Перечисления.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.КоличествоРазличных Тогда
			КомпоновкаДанныхКлиентСервер.УстановитьПараметр(Компоновщик, "УчитыватьХарактеристики", ИсточникДанных.УчитыватьХарактеристики, Истина);
		КонецЕсли;
	КонецЕсли;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(
		СхемаКомпоновкиДанных,
			Компоновщик.ПолучитьНастройки(),,,
			Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	ТекстЗапроса = МакетКомпоновкиДанных.НаборыДанных.НаборДанных.Запрос;
	
	КомментарийРазделителя = "////////////////////////////////////////////////////////////////////////////////";
	Позиция = СтрНайти(ТекстЗапроса, КомментарийРазделителя);
	ТекстЗапроса = Сред(ТекстЗапроса, Позиция + СтрДлина(КомментарийРазделителя) + 1);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаШаблон", СтрокаЗамены);
	
	Тексты = СтрРазделить(ТекстЗапроса, ";");
	Тексты[Тексты.Количество() - 1] = СтрЗаменить(Тексты[Тексты.Количество() - 1], Символы.ПС + "ИЗ", Символы.ПС + "ПОМЕСТИТЬ ФильтрПоНоменклатуре" + Символы.ПС + "ИЗ");
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТекстЗапроса", СтрСоединить(Тексты, ";") + Символы.ПС);
	ВозвращаемоеЗначение.Вставить("Параметры", Новый Структура);
	Для Каждого ЗначениеПараметра Из МакетКомпоновкиДанных.ЗначенияПараметров Цикл
		ВозвращаемоеЗначение.Параметры.Вставить(ЗначениеПараметра.Имя, ЗначениеПараметра.Значение);
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ПодготовитьФильтрПоНоменклатуре(Запрос, ИсточникДанных, СтрокаЗамены, ПараметрыРасчета)
	
	ДанныеФильтраПоНоменклатуре = ДанныеФильтраПоНоменклатуре(ИсточникДанных, СтрокаЗамены);
	
	Постфиксы = ПакетЗапросовПостфиксы();
	Для Каждого КлючЗначение Из ДанныеФильтраПоНоменклатуре.Параметры Цикл
		ИмяПараметра = КлючЗначение.Ключ;
		Если Не ПараметрыРасчета.Свойство(ИмяПараметра) Тогда
			НовоеИмяПараметра = ИмяПараметра + "Кэшируется";
			ЗаменитьИмяПараметраВЗапросе(ДанныеФильтраПоНоменклатуре.ТекстЗапроса, ИмяПараметра, НовоеИмяПараметра, Постфиксы);
		Иначе
			НовоеИмяПараметра = ИмяПараметра;
		КонецЕсли;
		Запрос.УстановитьПараметр(НовоеИмяПараметра, КлючЗначение.Значение);
	КонецЦикла;
	
	Возврат ДанныеФильтраПоНоменклатуре.ТекстЗапроса;
	
КонецФункции

// Функция формирует запрос для таблицы рассчитанных скидок за разовую продажу с условием по строке.
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ЗапросОпределенияПозицийНоменклатурыНаКоторыеПредоставляетсяСкидка(СкидкаНаценка, ПараметрыРасчета, ЗаполняемыеПараметры)
	
	Запрос = Новый Запрос;
	
	ИмяВременнойТаблицы = "ВременнаяТаблицаТовары";
	
	ТекстЗапроса = ПодготовитьФильтрПоНоменклатуре(Запрос, СкидкаНаценка, ИмяВременнойТаблицы, ПараметрыРасчета);
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов();
	
	ТекстЗапроса = ТекстЗапроса +
	"//// #123456789#
	|ВЫБРАТЬ
	|	Товары.КлючСвязи КАК КлючСвязи,
	|	Неопределено     КАК ЗначениеПоказателя,
	|	-1               КАК КратностьВыполнения
	|ИЗ
	|	ФильтрПоНоменклатуре КАК Товары
	|";
	
	Запрос.Текст = ТекстЗапроса;
	
	ЗаполнитьПараметрыЗапросОпределенияПозицийНоменклатурыНаКоторыеПредоставляетсяСкидка(Запрос, ПараметрыРасчета, ЗаполняемыеПараметры);
	
	Возврат Запрос;
	
КонецФункции

// Функция формирует запрос для таблицы рассчитанных скидок за разовую продажу с условием по документу.
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ЗапросУсловияЗаРазовуюПродажуСУсловиемПоДокументу(УсловиеПредоставления, ПараметрыРасчета, ЗаполняемыеПараметры)
	
	Запрос = Новый Запрос;
	
	ИмяВременнойТаблицы = "ВременнаяТаблицаТовары";
	
	ТекстЗапроса = ПодготовитьФильтрПоНоменклатуре(Запрос, УсловиеПредоставления, ИмяВременнойТаблицы, ПараметрыРасчета);
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов();
	
	ТекстЗапроса = ТекстЗапроса +
	"//// #123456789#
	|ВЫБРАТЬ
	|	&ТекстЗначениеПоказателя КАК ЗначениеПоказателя,
	|	&ТекстКратности          КАК КратностьВыполнения
	|ИЗ
	|	ФильтрПоНоменклатуре КАК Товары
	|";
	
	ПодготовитьТекстЗапросаДляПроверкиУсловияИРасчетаКратности(ТекстЗапроса, УсловиеПредоставления);
	
	Запрос.Текст = ТекстЗапроса;
	
	ЗаполнитьПараметрыЗапросУсловияЗаРазовуюПродажуСУсловиемПоДокументу(
		Запрос,
		УсловиеПредоставления,
		ПараметрыРасчета, ЗаполняемыеПараметры);
	
	Возврат Запрос;
	
КонецФункции

// Функция формирует запрос для получения временной таблицы накопленного объем продаж по партнеру.
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ЗапросНакопленныйОбъемПродажПоПартнеруЗаПериод(УсловиеПредоставления, Интервал, ПараметрыРасчета)
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура    КАК Номенклатура,
	|	ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Характеристика  КАК Характеристика,
	|	ЕСТЬNULL(ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНаборов.НоменклатураНабора,
	|		ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))                           КАК НоменклатураНабора,
	|	ЕСТЬNULL(ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНаборов.ХарактеристикаНабора,
	|		ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))             КАК ХарактеристикаНабора,
	|	ВыручкаИСебестоимостьПродажОбороты.КоличествоОборот                           КАК Количество,
	|	ВыручкаИСебестоимостьПродажОбороты.СуммаВыручкиОборот
	|	* ЕСТЬNULL(КурсыВалютИсточник.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютУсловиеПредоставления.КурсЗнаменатель, 1)
	|	 / (ЕСТЬNULL(КурсыВалютУсловиеПредоставления.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютИсточник.КурсЗнаменатель, 1)) КАК Сумма
	|	
	|ПОМЕСТИТЬ ИсходныеДанные
	|	
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(
	|			&ДатаНачала,
	|			&ДатаОкончания,
	|			Авто,
	|			АналитикаУчетаПоПартнерам В
	|				(ВЫБРАТЬ
	|					АналитикаУчетаПоПартнерам.КлючАналитики
	|				ИЗ
	|					РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|				ГДЕ
	|					АналитикаУчетаПоПартнерам.Партнер = &Партнер И АналитикаУчетаПоПартнерам.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.РозничныйПокупатель))
	|	) КАК ВыручкаИСебестоимостьПродажОбороты
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютИсточник
	|		ПО (КурсыВалютИсточник.Валюта = &ВалютаУправленческогоУчета)
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУсловиеПредоставления
	|		ПО (КурсыВалютУсловиеПредоставления.Валюта = &ВалютаУсловия)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура    КАК Номенклатура,
	|	ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Характеристика  КАК Характеристика,
	|	ЕСТЬNULL(ВыручкаИСебестоимостьПродаж.АналитикаУчетаНаборов.НоменклатураНабора,
	|		ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))                    КАК НоменклатураНабора,
	|	ЕСТЬNULL(ВыручкаИСебестоимостьПродаж.АналитикаУчетаНаборов.ХарактеристикаНабора,
	|		ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))      КАК ХарактеристикаНабора,
	|	-ВыручкаИСебестоимостьПродаж.Количество                                КАК Количество,
	|	-ВыручкаИСебестоимостьПродаж.СуммаВыручки
	|	* ЕСТЬNULL(КурсыВалютИсточник.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютУсловиеПредоставления.КурсЗнаменатель, 1)
	|	 / (ЕСТЬNULL(КурсыВалютУсловиеПредоставления.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютИсточник.КурсЗнаменатель, 1)) КАК Сумма
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютИсточник
	|		ПО (КурсыВалютИсточник.Валюта = &ВалютаУправленческогоУчета)
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУсловиеПредоставления
	|		ПО (КурсыВалютУсловиеПредоставления.Валюта = &ВалютаУсловия)
	|	
	|ГДЕ
	|	ВыручкаИСебестоимостьПродаж.Регистратор = &Регистратор
	|	И ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &ДатаНачалаЧекККМ И &ДатаОкончанияЧекККМ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ПроданныеТовары.Номенклатура   КАК Номенклатура,
	|	ПроданныеТовары.Характеристика КАК Характеристика,
	|	ПроданныеТовары.НоменклатураНабора   КАК НоменклатураНабора,
	|	ПроданныеТовары.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ПроданныеТовары.Количество     КАК Количество,
	|	ПроданныеТовары.Сумма
	|	* ЕСТЬNULL(КурсыВалютИсточник.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютУсловиеПредоставления.КурсЗнаменатель, 1)
	|	 / (ЕСТЬNULL(КурсыВалютУсловиеПредоставления.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютИсточник.КурсЗнаменатель, 1)) КАК Сумма
	|ИЗ
	|	Документ.ЧекККМ.Товары КАК ПроданныеТовары
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютИсточник
	|		ПО (КурсыВалютИсточник.Валюта = ПроданныеТовары.Ссылка.Валюта)
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУсловиеПредоставления
	|		ПО (КурсыВалютУсловиеПредоставления.Валюта = &ВалютаУсловия)
	|	
	|ГДЕ
	|	ПроданныеТовары.Ссылка В (
	|		ВЫБРАТЬ
	|			Т.Ссылка
	|		ИЗ
	|			Документ.ЧекККМ КАК Т
	|		ГДЕ
	|			Т.Дата МЕЖДУ &ДатаНачалаЧекККМ И &ДатаОкончанияЧекККМ
	|			И Т.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит)
	|			И Т.КассоваяСмена.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыКассовойСмены.Открыта)
	|			И Т.Партнер = &Партнер
	|			И Т.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.РозничныйПокупатель)
	|)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ПроданныеТовары.Номенклатура   КАК Номенклатура,
	|	ПроданныеТовары.Характеристика КАК Характеристика,
	|	ПроданныеТовары.НоменклатураНабора   КАК НоменклатураНабора,
	|	ПроданныеТовары.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	-ПроданныеТовары.Количество     КАК Количество,
	|	-ПроданныеТовары.Сумма
	|	* ЕСТЬNULL(КурсыВалютИсточник.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютУсловиеПредоставления.КурсЗнаменатель, 1)
	|	 / (ЕСТЬNULL(КурсыВалютУсловиеПредоставления.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютИсточник.КурсЗнаменатель, 1)) КАК Сумма
	|ИЗ
	|	Документ.ЧекККМВозврат.Товары КАК ПроданныеТовары
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютИсточник
	|		ПО (КурсыВалютИсточник.Валюта = ПроданныеТовары.Ссылка.Валюта)
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУсловиеПредоставления
	|		ПО (КурсыВалютУсловиеПредоставления.Валюта = &ВалютаУсловия)
	|	
	|ГДЕ
	|	ПроданныеТовары.Ссылка В (
	|		ВЫБРАТЬ
	|			Т.Ссылка
	|		ИЗ
	|			Документ.ЧекККМВозврат КАК Т
	|		ГДЕ
	|			Т.Дата МЕЖДУ &ДатаНачалаЧекККМ И &ДатаОкончанияЧекККМ
	|			И Т.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит)
	|			И Т.КассоваяСмена.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыКассовойСмены.Открыта)
	|			И Т.Партнер = &Партнер
	|			И Т.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.РозничныйПокупатель)
	|	И ПроданныеТовары.КоличествоУпаковок <> 0
	|)";
	
	Если УсловиеПредоставления.ВключатьТекущуюПродажуВНакопленныйОбъемПродаж Тогда
		ТекстЗапроса = ТекстЗапроса + Символы.ПС +
		"ОБЪЕДИНИТЬ ВСЕ
		|";
		
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ПроданныеТовары.Номенклатура   КАК Номенклатура,
		|	ПроданныеТовары.Характеристика КАК Характеристика,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)               КАК НоменклатураНабора,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаНабора,
		|	ПроданныеТовары.Количество КАК Количество,
		|	ПроданныеТовары.Сумма
		|	* ЕСТЬNULL(КурсыВалютИсточник.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютУсловиеПредоставления.КурсЗнаменатель, 1)
		|	 / (ЕСТЬNULL(КурсыВалютУсловиеПредоставления.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютИсточник.КурсЗнаменатель, 1)) КАК Сумма
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ПроданныеТовары
		|	
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютИсточник
		|		ПО (КурсыВалютИсточник.Валюта = &ВалютаДокумента)
		|	
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУсловиеПредоставления
		|		ПО (КурсыВалютУсловиеПредоставления.Валюта = &ВалютаУсловия)
		|";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	ЗаполнитьПараметрыЗапросНакопленныйОбъемПродажПоПартнеруЗаПериод(
		Запрос,
		УсловиеПредоставления,
		Интервал,
		ПараметрыРасчета);
	
	ИмяВременнойТаблицы = ИмяВременнойТаблицыНакопленныйОбъемПродаж(УсловиеПредоставления, Интервал);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПОМЕСТИТЬ ИсходныеДанные", "ПОМЕСТИТЬ" + " " + ИмяВременнойТаблицы);
	
	Возврат Запрос;
	
КонецФункции

// Функция формирует запрос для получения временной таблицы накопленного объем продаж по соглашению.
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ЗапросНакопленныйОбъемПродажПоТорговомуСоглашениюЗаПериод(УсловиеПредоставления, Интервал, ПараметрыРасчета)
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура    КАК Номенклатура,
	|	ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Характеристика  КАК Характеристика,
	|	ЕСТЬNULL(ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНаборов.НоменклатураНабора,
	|		ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))                           КАК НоменклатураНабора,
	|	ЕСТЬNULL(ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНаборов.ХарактеристикаНабора,
	|		ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))             КАК ХарактеристикаНабора,
	|	ВыручкаИСебестоимостьПродажОбороты.КоличествоОборот                           КАК Количество,
	|	ВыручкаИСебестоимостьПродажОбороты.СуммаВыручкиОборот
	|	* ЕСТЬNULL(КурсыВалютИсточник.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютУсловиеПредоставления.КурсЗнаменатель, 1)
	|	 / (ЕСТЬNULL(КурсыВалютУсловиеПредоставления.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютИсточник.КурсЗнаменатель, 1)) КАК Сумма
	|	
	|ПОМЕСТИТЬ ИсходныеДанные
	|	
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(
	|			&ДатаНачала,
	|			&ДатаОкончания,
	|			Авто,
	|			Соглашение = &Соглашение
	|			И АналитикаУчетаПоПартнерам В
	|				(ВЫБРАТЬ
	|					АналитикаУчетаПоПартнерам.КлючАналитики
	|				ИЗ
	|					РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|				ГДЕ
	|					АналитикаУчетаПоПартнерам.Партнер = &Партнер И АналитикаУчетаПоПартнерам.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.РозничныйПокупатель))
	|	) КАК ВыручкаИСебестоимостьПродажОбороты
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютИсточник
	|		ПО (КурсыВалютИсточник.Валюта = &ВалютаУправленческогоУчета)
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУсловиеПредоставления
	|		ПО (КурсыВалютУсловиеПредоставления.Валюта = &ВалютаУсловия)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура    КАК Номенклатура,
	|	ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Характеристика  КАК Характеристика,
	|	ЕСТЬNULL(ВыручкаИСебестоимостьПродаж.АналитикаУчетаНаборов.НоменклатураНабора,
	|		ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))                    КАК НоменклатураНабора,
	|	ЕСТЬNULL(ВыручкаИСебестоимостьПродаж.АналитикаУчетаНаборов.ХарактеристикаНабора,
	|		ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))      КАК ХарактеристикаНабора,
	|	-ВыручкаИСебестоимостьПродаж.Количество                                КАК Количество,
	|	-ВыручкаИСебестоимостьПродаж.СуммаВыручки
	|	* ЕСТЬNULL(КурсыВалютИсточник.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютУсловиеПредоставления.КурсЗнаменатель, 1)
	|	 / (ЕСТЬNULL(КурсыВалютУсловиеПредоставления.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютИсточник.КурсЗнаменатель, 1)) КАК Сумма
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютИсточник
	|		ПО (КурсыВалютИсточник.Валюта = &ВалютаУправленческогоУчета)
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУсловиеПредоставления
	|		ПО (КурсыВалютУсловиеПредоставления.Валюта = &ВалютаУсловия)
	|	
	|ГДЕ
	|	ВыручкаИСебестоимостьПродаж.Регистратор = &Регистратор
	|	И ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &ДатаНачалаПС И &ДатаОкончанияПС
	|";
	
	Если УсловиеПредоставления.ВключатьТекущуюПродажуВНакопленныйОбъемПродаж Тогда
		ТекстЗапроса = ТекстЗапроса + Символы.ПС +
		"ОБЪЕДИНИТЬ ВСЕ
		|";
		
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ПроданныеТовары.Номенклатура   КАК Номенклатура,
		|	ПроданныеТовары.Характеристика КАК Характеристика,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)               КАК НоменклатураНабора,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаНабора,
		|	ПроданныеТовары.Количество КАК Количество,
		|	ПроданныеТовары.Сумма
		|	* ЕСТЬNULL(КурсыВалютИсточник.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютУсловиеПредоставления.КурсЗнаменатель, 1)
		|	 / (ЕСТЬNULL(КурсыВалютУсловиеПредоставления.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютИсточник.КурсЗнаменатель, 1)) КАК Сумма
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ПроданныеТовары
		|	
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютИсточник
		|		ПО (КурсыВалютИсточник.Валюта = &ВалютаДокумента)
		|	
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУсловиеПредоставления
		|		ПО (КурсыВалютУсловиеПредоставления.Валюта = &ВалютаУсловия)
		|";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	ЗаполнитьПараметрыЗапросНакопленныйОбъемПродажПоТорговомуСоглашениюЗаПериод(
		Запрос,
		УсловиеПредоставления,
		Интервал,
		ПараметрыРасчета);
	
	ИмяВременнойТаблицы = ИмяВременнойТаблицыНакопленныйОбъемПродаж(УсловиеПредоставления, Интервал);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПОМЕСТИТЬ ИсходныеДанные", "ПОМЕСТИТЬ" + " " + ИмяВременнойТаблицы);
	
	Возврат Запрос;
	
КонецФункции

// Функция формирует запрос для таблицы рассчитанных скидок за накопленный объем продаж по партнеру.
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ЗапросУсловияЗаНакопленныйОбъемПродажПоПартнеруЗаПериод(УсловиеПредоставления, Интервал, ПараметрыРасчета, ЗаполняемыеПараметры)
	
	Запрос = Новый Запрос;
	
	Если ЗаполняемыеПараметры = "Кэшируемые" Тогда
		ИмяВременнойТаблицы = "ИсходныеДанные";
	Иначе
		ИмяВременнойТаблицы = ИмяВременнойТаблицыНакопленныйОбъемПродаж(УсловиеПредоставления, Интервал);
	КонецЕсли;
	
	ТекстЗапроса = ПодготовитьФильтрПоНоменклатуре(Запрос, УсловиеПредоставления, ИмяВременнойТаблицы, ПараметрыРасчета);
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов();
	
	ТекстЗапроса = ТекстЗапроса +
	"//// #123456789#
	|ВЫБРАТЬ
	|	&ТекстЗначениеПоказателя КАК ЗначениеПоказателя,
	|	&ТекстКратности          КАК КратностьВыполнения
	|ИЗ
	|	ФильтрПоНоменклатуре КАК Товары
	|";
	
	ПодготовитьТекстЗапросаДляПроверкиУсловияИРасчетаКратности(ТекстЗапроса, УсловиеПредоставления);
	
	Запрос.Текст = ТекстЗапроса;
	
	ЗаполнитьПараметрыЗапросУсловияЗаНакопленныйОбъемПродажПоПартнеруЗаПериод(
		Запрос,
		УсловиеПредоставления,
		Интервал,
		ПараметрыРасчета, ЗаполняемыеПараметры);
	
	Возврат Запрос;
	
КонецФункции

// Функция формирует запрос для таблицы рассчитанных скидок за накопленный объем продаж по торговому соглашению.
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ЗапросУсловияЗаНакопленныйОбъемПродажПоТорговомуСоглашениюЗаПериод(УсловиеПредоставления, Интервал, ПараметрыРасчета, ЗаполняемыеПараметры)
	
	Запрос = Новый Запрос;
	
	Если ЗаполняемыеПараметры = "Кэшируемые" Тогда
		ИмяВременнойТаблицы = "ИсходныеДанные";
	Иначе
		ИмяВременнойТаблицы = ИмяВременнойТаблицыНакопленныйОбъемПродаж(УсловиеПредоставления, Интервал);
	КонецЕсли;
	
	ТекстЗапроса = ПодготовитьФильтрПоНоменклатуре(Запрос, УсловиеПредоставления, ИмяВременнойТаблицы, ПараметрыРасчета);
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов();
	
	ТекстЗапроса = ТекстЗапроса +
	"//// #123456789#
	|ВЫБРАТЬ
	|	&ТекстЗначениеПоказателя КАК ЗначениеПоказателя,
	|	&ТекстКратности          КАК КратностьВыполнения
	|ИЗ
	|	ФильтрПоНоменклатуре КАК Товары
	|";
	
	ПодготовитьТекстЗапросаДляПроверкиУсловияИРасчетаКратности(ТекстЗапроса, УсловиеПредоставления);
	
	Запрос.Текст = ТекстЗапроса;
	
	ЗаполнитьПараметрыЗапросУсловияЗаНакопленныйОбъемПродажПоТорговомуСоглашениюЗаПериод(
		Запрос,
		УсловиеПредоставления,
		Интервал,
		ПараметрыРасчета, ЗаполняемыеПараметры);
	
	Возврат Запрос;
	
КонецФункции

// Функция формирует запрос для таблицы рассчитанных скидок за соблюдение графика оплаты.
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ПроверитьУсловиеЗаГрафикОплаты(УсловиеПредоставления, Контекст)
	
	КратностьВыполнения = 0;
	
	Если (Контекст.ГрафикОплаты = УсловиеПредоставления.ГрафикОплаты) Тогда
		КратностьВыполнения = -1;
	КонецЕсли;
	
	Возврат КратностьВыполнения;
	
КонецФункции

// Функция формирует запрос для таблицы рассчитанных скидок за форму оплаты.
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ПроверитьУсловиеЗаФормуОплаты(УсловиеПредоставления, Контекст)
	
	КратностьВыполнения = 0;
	
	Если (Контекст.ФормаОплаты = УсловиеПредоставления.ФормаОплаты) Тогда
		КратностьВыполнения = -1;
	КонецЕсли;
	
	Возврат КратностьВыполнения;
	
КонецФункции

// Функция формирует запрос для проверки, что карта лояльности не зарегистрирована.
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ЗапросУсловияКартаЛояльностиНеЗарегистрирована(УсловиеПредоставления, ПараметрыРасчета)

	Запрос = Новый Запрос(
	"////#123456789#//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	-1 КАК КратностьВыполнения
	|ГДЕ
	|	НЕ &ВидКартыЛояльностиУсловия В
	|		(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			Т.Владелец
	|		ИЗ
	|			Справочник.КартыЛояльности КАК Т
	|		ГДЕ
	|			Т.Партнер = &Партнер
	|			И Т.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка))");
	
	Запрос.УстановитьПараметр("ВидКартыЛояльностиУсловия", УсловиеПредоставления.ВидКартыЛояльности);
	Запрос.УстановитьПараметр("Партнер", ПараметрыРасчета.Партнер);
	
	Возврат Запрос;
	
КонецФункции

// Функция формирует запрос для проверки, что партнер является владельцем карты лояльности.
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ЗапросУсловияЗаНаличиеКартыЛояльности(УсловиеПредоставления, ПараметрыРасчета)

	Запрос = Новый Запрос(
	"////#123456789#//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	-1 КАК КратностьВыполнения
	|ГДЕ
	|	&ВидКартыЛояльностиУсловия В
	|		(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			Т.Владелец
	|		ИЗ
	|			Справочник.КартыЛояльности КАК Т
	|		ГДЕ
	|			Т.Партнер = &Партнер
	|			И Т.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|			И Т.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыКартЛояльности.Действует))");
	
	Запрос.УстановитьПараметр("ВидКартыЛояльностиУсловия", УсловиеПредоставления.ВидКартыЛояльности);
	Запрос.УстановитьПараметр("Партнер", ПараметрыРасчета.Партнер);
	
	Возврат Запрос;
	
КонецФункции

// Функция формирует запрос для таблицы рассчитанных скидок с ограничением по группе пользователей.
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ЗапросУсловияОграничениеПоГруппеПользователей(УсловиеПредоставления, ПараметрыРасчета)

	Запрос = Новый Запрос(
	"////#123456789#//////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	-1 КАК КратностьВыполнения
	|ИЗ
	|	Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	|ГДЕ
	|	ГруппыПользователейСостав.Ссылка = &ГруппаПользователейУсловия
	|	И ГруппыПользователейСостав.Пользователь = &Пользователь");
	
	Запрос.УстановитьПараметр("ГруппаПользователейУсловия", УсловиеПредоставления.ГруппаПользователей);
	Запрос.УстановитьПараметр("Пользователь", ПараметрыРасчета.Пользователь);
	
	Возврат Запрос;
	
КонецФункции

// Функция формирует запрос для таблицы рассчитанных скидок за время продажи.
//
// Параметры:
// 	УсловиеПредоставления - СтрокаТаблицыЗначений - Содержит в том числе:
// 		* Ссылка - СправочникСсылка.УсловияПредоставленияСкидокНаценок - Ссылка на элемент справочника
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ЗапросУсловияЗаВремяПродажи(УсловиеПредоставления, ПараметрыРасчета)

	Запрос = Новый Запрос(
	"////#123456789#//////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	-1 КАК КратностьВыполнения
	|ИЗ
	|	Справочник.УсловияПредоставленияСкидокНаценок.ВремяДействия КАК ВремяДействия
	|ГДЕ
	|	ВремяДействия.Ссылка = &Условие
	|	И ВремяДействия.ДеньНедели = &ДеньНедели
	|	И (ВремяДействия.ВремяНачала <= &ТекущееВремя И ВремяДействия.ВремяОкончания >= &ТекущееВремя)");
	
	Запрос.УстановитьПараметр("Условие", УсловиеПредоставления.Ссылка);
	Запрос.УстановитьПараметр("ТекущееВремя", ПараметрыРасчета.ТекущееВремя);
	Запрос.УстановитьПараметр("ДеньНедели", ПараметрыРасчета.ДеньНедели);
	
	Возврат Запрос;
	
КонецФункции

// Функция формирует запрос для таблицы рассчитанных скидок за вхождение партнера в сегмент.
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ЗапросУсловияВхождениеПартнераВСегмент(УсловиеПредоставления, ПараметрыРасчета)

	Запрос = Новый Запрос(
	"////#123456789#//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	-1 КАК КратностьВыполнения
	|ГДЕ
	|	&СегментПартнеровУсловия В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				Т.Сегмент КАК Сегмент
	|			ИЗ
	|				РегистрСведений.ПартнерыСегмента КАК Т
	|			ГДЕ
	|				Т.Партнер = &Партнер)");
	
	Запрос.УстановитьПараметр("СегментПартнеровУсловия", УсловиеПредоставления.СегментПартнеров);
	Запрос.УстановитьПараметр("Партнер", ПараметрыРасчета.Партнер);
	
	Возврат Запрос;
	
КонецФункции

// Функция формирует запрос для проверки, что партнер является владельцем карты лояльности.
//
// Возвращаемое значение:
//	Запрос - Запрос
//
Функция ЗапросУсловияЗаДеньРождения(УсловиеПредоставления, ПараметрыРасчета)

	Запрос = Новый Запрос(
	"////#123456789#//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	-1 КАК КратностьВыполнения
	|ИЗ
	|	Справочник.Партнеры КАК Партнеры
	|ГДЕ
	|	Партнеры.Ссылка = &Партнер
	|	И Партнеры.ДатаРождения <> ДАТАВРЕМЯ(1, 1, 1)
	|	И ДобавитьКДате(ДОБАВИТЬКДАТЕ(Партнеры.ДатаРождения, ДЕНЬ, &СдвигДней), ГОД, Год(&ДатаНачала) - Год(Партнеры.ДатаРождения)) МЕЖДУ &ДатаНачала И &ДатаОкончания
	|");
	
	ДатаОкончания 	= КонецДня(ПараметрыРасчета.ТекущаяДата) + УсловиеПредоставления.КоличествоДнейПослеДняРождения * 24*60*60;
	ДатаНачала 		= НачалоДня(ПараметрыРасчета.ТекущаяДата) - УсловиеПредоставления.КоличествоДнейДоДняРождения * 24*60*60;
	Если Год(ДатаНачала) = Год(ДатаОкончания) Тогда
		СдвигДней 		= 0;
	иначе
		СдвигДней 		= ДеньГода(КонецГода(ДатаНачала)) - ДеньГода(ДатаНачала)+1;
		ДатаОкончания 	= ДатаОкончания + СдвигДней * 24*60*60;
		ДатаНачала 		= ДатаНачала + СдвигДней * 24*60*60;
	КонецЕсли;		
	
	Запрос.УстановитьПараметр("ДатаОкончания", 	ДатаОкончания);
	Запрос.УстановитьПараметр("ДатаНачала", 	ДатаНачала);
	Запрос.УстановитьПараметр("СдвигДней", 		СдвигДней);
	Запрос.УстановитьПараметр("Партнер", 		ПараметрыРасчета.Партнер);
	
	Возврат Запрос;
	
КонецФункции


Процедура ЗаполнитьПараметрыЗапросНакопленныйОбъемПродажПоПартнеруЗаПериод(Запрос, УсловиеПредоставления, Интервал, ПараметрыРасчета)
	
	Запрос.УстановитьПараметр("ВалютаУсловия",              УсловиеПредоставления.ВалютаОграничения);
	
	Запрос.УстановитьПараметр("Партнер",                    ПараметрыРасчета.Партнер);
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета", ПараметрыРасчета.ВалютаУправленческогоУчета);
	Запрос.УстановитьПараметр("ВалютаДокумента",            ПараметрыРасчета.ВалютаДокумента);
	Запрос.УстановитьПараметр("ТекущаяДата",                ПараметрыРасчета.ТекущаяДата);
	Запрос.УстановитьПараметр("Регистратор",                ПараметрыРасчета.Регистратор);
	
	Если Интервал.ДатаНачала <> Неопределено Тогда
		Запрос.УстановитьПараметр("ДатаНачалаЧекККМ", Интервал.ДатаНачала);
		Запрос.УстановитьПараметр("ДатаНачала",       Интервал.ДатаНачала);
	Иначе
		Запрос.УстановитьПараметр("ДатаНачалаЧекККМ", Дата(1,1,1));
		Запрос.УстановитьПараметр("ДатаНачала", "");
	КонецЕсли;
	
	Если Интервал.ДатаОкончания <> Неопределено Тогда
		Запрос.УстановитьПараметр("ДатаОкончанияЧекККМ", Интервал.ДатаОкончания);
		Запрос.УстановитьПараметр("ДатаОкончания",       Интервал.ДатаОкончания);
	Иначе
		Запрос.УстановитьПараметр("ДатаОкончанияЧекККМ", КонецДня(ТекущаяДатаСеанса()));
		Запрос.УстановитьПараметр("ДатаОкончания", "");
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьПараметрыЗапросНакопленныйОбъемПродажПоТорговомуСоглашениюЗаПериод(Запрос, УсловиеПредоставления, Интервал, ПараметрыРасчета)
	
	Запрос.УстановитьПараметр("ВалютаУсловия",              УсловиеПредоставления.ВалютаОграничения);
	
	Запрос.УстановитьПараметр("Соглашение",                 ПараметрыРасчета.Соглашение);
	Запрос.УстановитьПараметр("Партнер",                    ПараметрыРасчета.Партнер);
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета", ПараметрыРасчета.ВалютаУправленческогоУчета);
	Запрос.УстановитьПараметр("ВалютаДокумента",            ПараметрыРасчета.ВалютаДокумента);
	Запрос.УстановитьПараметр("ТекущаяДата",                ПараметрыРасчета.ТекущаяДата);
	Запрос.УстановитьПараметр("Регистратор",                ПараметрыРасчета.Регистратор);
	
	Если Интервал.ДатаНачала <> Неопределено Тогда
		Запрос.УстановитьПараметр("ДатаНачалаПС", Интервал.ДатаНачала);
		Запрос.УстановитьПараметр("ДатаНачала",   Интервал.ДатаНачала);
	Иначе
		Запрос.УстановитьПараметр("ДатаНачалаПС", Дата(1,1,1));
		Запрос.УстановитьПараметр("ДатаНачала",   "");
	КонецЕсли;
	
	Если Интервал.ДатаОкончания <> Неопределено Тогда
		Запрос.УстановитьПараметр("ДатаОкончанияПС", Интервал.ДатаОкончания);
		Запрос.УстановитьПараметр("ДатаОкончания",   Интервал.ДатаОкончания);
	Иначе
		Запрос.УстановитьПараметр("ДатаОкончанияПС", ТекущаяДатаСеанса());
		Запрос.УстановитьПараметр("ДатаОкончания",   "");
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыЗапросОпределенияПозицийНоменклатурыНаКоторыеПредоставляетсяСкидка(Запрос, ПараметрыРасчета, ЗаполняемыеПараметры)
	
	Если ЗаполняемыеПараметры = "Все" ИЛИ ЗаполняемыеПараметры = "ПараметрыРасчета" Тогда
		Запрос.УстановитьПараметр("ТекущаяДата", ПараметрыРасчета.ТекущаяДата);
		Запрос.УстановитьПараметр("Партнер", ПараметрыРасчета.Партнер);
		Запрос.УстановитьПараметр("Соглашение", ПараметрыРасчета.Соглашение);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыЗапросУсловияЗаРазовуюПродажуСУсловиемПоДокументу(Запрос, УсловиеПредоставления, ПараметрыРасчета, ЗаполняемыеПараметры)

	Если ЗаполняемыеПараметры = "Все" ИЛИ ЗаполняемыеПараметры = "Кэшируемые" Тогда
		Запрос.УстановитьПараметр("ЗначениеУсловияОграничения"+"Кэшируется", УсловиеПредоставления.ЗначениеУсловияОграничения);
	КонецЕсли;
	
	Если ЗаполняемыеПараметры = "Все" ИЛИ ЗаполняемыеПараметры = "ПараметрыРасчета" Тогда
		Запрос.УстановитьПараметр("ТекущаяДата", ПараметрыРасчета.ТекущаяДата);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыЗапросУсловияЗаНакопленныйОбъемПродажПоПартнеруЗаПериод(Запрос, УсловиеПредоставления, Интервал, ПараметрыРасчета, ЗаполняемыеПараметры)
	
	Если ЗаполняемыеПараметры = "Все" ИЛИ ЗаполняемыеПараметры = "Кэшируемые" Тогда
		Запрос.УстановитьПараметр("ЗначениеУсловияОграничения"+"Кэшируется", УсловиеПредоставления.ЗначениеУсловияОграничения);
	КонецЕсли;
	
	Если ЗаполняемыеПараметры = "Все" ИЛИ ЗаполняемыеПараметры = "ПараметрыРасчета" Тогда
		Запрос.УстановитьПараметр("ТекущаяДата", ПараметрыРасчета.ТекущаяДата);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыЗапросУсловияЗаНакопленныйОбъемПродажПоТорговомуСоглашениюЗаПериод(Запрос, УсловиеПредоставления, Интервал, ПараметрыРасчета, ЗаполняемыеПараметры)

	Если ЗаполняемыеПараметры = "Все" ИЛИ ЗаполняемыеПараметры = "Кэшируемые" Тогда
		Запрос.УстановитьПараметр("ЗначениеУсловияОграничения"+"Кэшируется", УсловиеПредоставления.ЗначениеУсловияОграничения);
	КонецЕсли;
	
	Если ЗаполняемыеПараметры = "Все" ИЛИ ЗаполняемыеПараметры = "ПараметрыРасчета" Тогда
		Запрос.УстановитьПараметр("ТекущаяДата", ПараметрыРасчета.ТекущаяДата);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаУсловийСкидокНаценок

Функция ИнтервалНакопленияОбъемаПродаж(СтрокаТЧ, ПараметрыРасчета)
	
	ДатаНачала    = Неопределено;
	ДатаОкончания = КонецДня(ПараметрыРасчета.ТекущаяДата);
	
	Если СтрокаТЧ.ВариантОпределенияПериодаНакопительнойСкидки = Перечисления.ВариантыОпределенияПериодаНакопительнойСкидки.ПрошлыйПериод Тогда
		
		Если СтрокаТЧ.ПериодНакопления = Перечисления.Периодичность.Полугодие Тогда
			ДатаНачала = ОбщегоНазначенияУТКлиентСервер.РассчитатьДатуОкончанияПериода(
				НачалоПолугодия(ПараметрыРасчета.ТекущаяДата),
				Перечисления.Периодичность.Полугодие,
				-1 * СтрокаТЧ.КоличествоПериодовНакопления);
			ДатаОкончания = НачалоПолугодия(ПараметрыРасчета.ТекущаяДата) - 1;
		ИначеЕсли СтрокаТЧ.ПериодНакопления = Перечисления.Периодичность.Декада Тогда
			ДатаНачала = ОбщегоНазначенияУТКлиентСервер.РассчитатьДатуОкончанияПериода(
				НачалоДекады(ПараметрыРасчета.ТекущаяДата),
				Перечисления.Периодичность.Декада,
				-1 * СтрокаТЧ.КоличествоПериодовНакопления);
			ДатаОкончания = НачалоДекады(ПараметрыРасчета.ТекущаяДата) - 1;
		ИначеЕсли СтрокаТЧ.ПериодНакопления = Перечисления.Периодичность.Год Тогда
			ДатаНачала = ОбщегоНазначенияУТКлиентСервер.РассчитатьДатуОкончанияПериода(
				НачалоГода(ПараметрыРасчета.ТекущаяДата),
				Перечисления.Периодичность.Год,
				-1 * СтрокаТЧ.КоличествоПериодовНакопления);
			ДатаОкончания = НачалоГода(ПараметрыРасчета.ТекущаяДата) - 1;
		ИначеЕсли СтрокаТЧ.ПериодНакопления = Перечисления.Периодичность.Квартал Тогда
			ДатаНачала = ОбщегоНазначенияУТКлиентСервер.РассчитатьДатуОкончанияПериода(
				НачалоКвартала(ПараметрыРасчета.ТекущаяДата),
				Перечисления.Периодичность.Квартал,
				-1 * СтрокаТЧ.КоличествоПериодовНакопления);
			ДатаОкончания = НачалоКвартала(ПараметрыРасчета.ТекущаяДата) - 1;
		ИначеЕсли СтрокаТЧ.ПериодНакопления = Перечисления.Периодичность.Месяц Тогда
			ДатаНачала = ОбщегоНазначенияУТКлиентСервер.РассчитатьДатуОкончанияПериода(
				НачалоМесяца(ПараметрыРасчета.ТекущаяДата),
				Перечисления.Периодичность.Месяц,
				-1 * СтрокаТЧ.КоличествоПериодовНакопления);
			ДатаОкончания = НачалоМесяца(ПараметрыРасчета.ТекущаяДата) - 1;
		ИначеЕсли СтрокаТЧ.ПериодНакопления = Перечисления.Периодичность.Неделя Тогда
			ДатаНачала = ОбщегоНазначенияУТКлиентСервер.РассчитатьДатуОкончанияПериода(
				НачалоНедели(ПараметрыРасчета.ТекущаяДата),
				Перечисления.Периодичность.Неделя,
				-1 * СтрокаТЧ.КоличествоПериодовНакопления);
			ДатаОкончания = НачалоНедели(ПараметрыРасчета.ТекущаяДата) - 1;
		ИначеЕсли СтрокаТЧ.ПериодНакопления = Перечисления.Периодичность.День Тогда
			ДатаНачала = ОбщегоНазначенияУТКлиентСервер.РассчитатьДатуОкончанияПериода(
				НачалоДня(ПараметрыРасчета.ТекущаяДата),
				Перечисления.Периодичность.День,
				-1  *СтрокаТЧ.КоличествоПериодовНакопления);
			ДатаОкончания = НачалоДня(ПараметрыРасчета.ТекущаяДата) - 1;
		КонецЕсли;
		
	ИначеЕсли СтрокаТЧ.ВариантОпределенияПериодаНакопительнойСкидки = Перечисления.ВариантыОпределенияПериодаНакопительнойСкидки.ПрошлыйСкользящийПериод Тогда
		
		ДатаНачала = ОбщегоНазначенияУТКлиентСервер.РассчитатьДатуОкончанияПериода(
			ПараметрыРасчета.ТекущаяДата,
			СтрокаТЧ.ПериодНакопления,
			-1 * СтрокаТЧ.КоличествоПериодовНакопления);
		
	ИначеЕсли СтрокаТЧ.ВариантОпределенияПериодаНакопительнойСкидки = Перечисления.ВариантыОпределенияПериодаНакопительнойСкидки.СНачалаТекущегоПериода Тогда
		
		Если СтрокаТЧ.ПериодНакопления = Перечисления.Периодичность.Год Тогда
			ДатаНачала = НачалоГода(ПараметрыРасчета.ТекущаяДата);
		ИначеЕсли СтрокаТЧ.ПериодНакопления = Перечисления.Периодичность.Декада Тогда
			ДатаНачала = НачалоДекады(ПараметрыРасчета.ТекущаяДата);
		ИначеЕсли СтрокаТЧ.ПериодНакопления = Перечисления.Периодичность.Полугодие Тогда
			ДатаНачала = НачалоПолугодия(ПараметрыРасчета.ТекущаяДата);
		ИначеЕсли СтрокаТЧ.ПериодНакопления = Перечисления.Периодичность.Квартал Тогда
			ДатаНачала = НачалоКвартала(ПараметрыРасчета.ТекущаяДата);
		ИначеЕсли СтрокаТЧ.ПериодНакопления = Перечисления.Периодичность.Месяц Тогда
			ДатаНачала = НачалоМесяца(ПараметрыРасчета.ТекущаяДата);
		ИначеЕсли СтрокаТЧ.ПериодНакопления = Перечисления.Периодичность.Неделя Тогда
			ДатаНачала = НачалоНедели(ПараметрыРасчета.ТекущаяДата);
		ИначеЕсли СтрокаТЧ.ПериодНакопления = Перечисления.Периодичность.День Тогда
			ДатаНачала = НачалоДня(ПараметрыРасчета.ТекущаяДата);
		КонецЕсли;
		
	КонецЕсли;
	
	Интервал = Новый Структура;
	Интервал.Вставить("ДатаНачала", ДатаНачала);
	Интервал.Вставить("ДатаОкончания", ДатаОкончания);
	
	Возврат Интервал;
	
КонецФункции

Функция ЗапросПроверкиУсловияПредоставления(СтрокаТЧ, ПараметрыРасчета, ЗаполняемыеПараметры)
	
	Запрос = Неопределено;
	
	Если СтрокаТЧ.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ЗаРазовыйОбъемПродаж Тогда
		
		Запрос = ЗапросУсловияЗаРазовуюПродажуСУсловиемПоДокументу(СтрокаТЧ, ПараметрыРасчета, ЗаполняемыеПараметры);
		
	ИначеЕсли СтрокаТЧ.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ЗаНакопленныйОбъемПродаж Тогда
		
		Интервал = ИнтервалНакопленияОбъемаПродаж(СтрокаТЧ, ПараметрыРасчета);
		
		Если СтрокаТЧ.ВариантНакопления = Перечисления.ВариантыНакопленияКумулятивнойСкидкиНаценки.ПоПартнеру Тогда
			Запрос = ЗапросУсловияЗаНакопленныйОбъемПродажПоПартнеруЗаПериод(СтрокаТЧ, Интервал, ПараметрыРасчета, ЗаполняемыеПараметры);
		ИначеЕсли СтрокаТЧ.ВариантНакопления = Перечисления.ВариантыНакопленияКумулятивнойСкидкиНаценки.ПоТорговомуСоглашению Тогда
			Запрос = ЗапросУсловияЗаНакопленныйОбъемПродажПоТорговомуСоглашениюЗаПериод(СтрокаТЧ, Интервал, ПараметрыРасчета, ЗаполняемыеПараметры);
		КонецЕсли;
		
	ИначеЕсли СтрокаТЧ.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ОграничениеПоГруппеПользователей Тогда
		
		Запрос = ЗапросУсловияОграничениеПоГруппеПользователей(СтрокаТЧ, ПараметрыРасчета);
		
	ИначеЕсли СтрокаТЧ.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ЗаВремяПродажи Тогда
		
		Запрос = ЗапросУсловияЗаВремяПродажи(СтрокаТЧ, ПараметрыРасчета);
		
	ИначеЕсли СтрокаТЧ.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.КартаЛояльностиНеЗарегистрирована Тогда
		
		Запрос = ЗапросУсловияКартаЛояльностиНеЗарегистрирована(СтрокаТЧ, ПараметрыРасчета);
		
	ИначеЕсли СтрокаТЧ.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ЗаНаличиеКартыЛояльности Тогда
		
		Запрос = ЗапросУсловияЗаНаличиеКартыЛояльности(СтрокаТЧ, ПараметрыРасчета);
		
	ИначеЕсли СтрокаТЧ.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ВхождениеПартнераВСегмент Тогда
		
		Запрос = ЗапросУсловияВхождениеПартнераВСегмент(СтрокаТЧ, ПараметрыРасчета);
		
	ИначеЕсли СтрокаТЧ.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ЗаДеньРожденияКлиента Тогда
		
		Запрос = ЗапросУсловияЗаДеньРождения(СтрокаТЧ, ПараметрыРасчета);
		
	ИначеЕсли СтрокаТЧ.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ЗаГрафикОплаты Тогда
		
		// Обработка второй очереди
		
	ИначеЕсли СтрокаТЧ.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ЗаФормуОплаты Тогда
		
		// Обработка второй очереди
		
	ИначеЕсли ТипЗнч(СтрокаТЧ.УсловиеПредоставления) = Тип("СправочникСсылка.ДополнительныеОтчетыИОбработки") Тогда
		
		ВнешняяОбработка = СкидкиНаценкиПовтИсп.ОбъектВнешнейОбработки(СтрокаТЧ.УсловиеПредоставления);
		УстановитьБезопасныйРежим(Истина);
		Запрос = ВнешняяОбработка.Запрос(СтрокаТЧ.ПараметрыВнешнейОбработки.Получить(), ПараметрыРасчета);
		УстановитьБезопасныйРежим(Ложь);
		
	КонецЕсли;
	
	Возврат Запрос;
	
КонецФункции

// Параметры:
// 	УсловияПредоставления - ТаблицаЗначений - Состоит из:
// 		* Ссылка - СправочникСсылка.УсловияПредоставленияСкидокНаценок 
// 		* ЗначениеУсловияОграничения - Число - Значение условия ограничения
// 		* УсловиеПредоставления - СправочникСсылка.ДополнительныеОтчетыИОбработки, ПеречислениеСсылка.УсловияПредоставленияСкидокНаценок - Условие предоставления
// 		* ВариантОпределенияПериодаНакопительнойСкидки - ПеречислениеСсылка.ВариантыОпределенияПериодаНакопительнойСкидки
// 		* ВариантНакопления - ПеречислениеСсылка.ВариантыНакопленияКумулятивнойСкидкиНаценки - Вариант накопления
// 		* ВалютаОграничения - СправочникСсылка.Валюты - Валюта ограничения
// 		* ЦифровойКодВалютыОграничения - Строка - Код валюты
// 		* ТипСравнения - ПеречислениеСсылка.ТипыСравненияЗначенийСкидокНаценок - Тип сравнения
// 		* КритерийОграниченияПримененияЗаОбъемПродаж - ПеречислениеСсылка.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж 
// 		* ГрафикОплаты - СправочникСсылка.ГрафикиОплаты - График оплаты
// 		* ФормаОплаты - ПеречислениеСсылка.ФормыОплаты - Форма оплаты
// 		* СегментНоменклатурыОграничения - СправочникСсылка.СегментыНоменклатуры - Сегмент номенклатуры ограничения
// 		* ПериодНакопления - ПеречислениеСсылка.Периодичность - Периодичность, с которой осуществляется накопление кумулятивной скидки (наценки)
// 		* ГруппаПользователей - СправочникСсылка.ГруппыПользователей - Группа пользователей
// 		* ВидКартыЛояльности - СправочникСсылка.ВидыКартЛояльности - Вид карты лояльности
// 		* КоличествоПериодовНакопления - Число - Количество периодов накопления
// 		* ВариантОтбораНоменклатуры - ПеречислениеСсылка.ВариантыОтбораНоменклатурыДляРасчетаСкидокНаценок - Вариант отбора номенклатуры
// 		* ХранилищеНастроекКомпоновкиДанных - ХранилищеЗначения - Хранилище настроек компоновки данных
// 		* ПараметрыВнешнейОбработки - ХранилищеЗначения - Параметры внешней обработки
// 		* КоличествоДнейДоДняРождения - Число - Количество дней до дня рождения
// 		* КоличествоДнейПослеДняРождения - Число - Количество дней после дня рождения
// 		* ВключатьТекущуюПродажуВНакопленныйОбъемПродаж - Булево - Включить текущую продажу в накопленный объем продаж
// 		* УчитыватьХарактеристики - Булево - Учитывать характеристики
// 		* СегментПартнеров - СправочникСсылка.СегментыПартнеров - Скидка (наценка) действует на номенклатурные позиции из данного сегмента.
// 	СкидкиНаценки - Массив из СтрокаДереваЗначений - см. СформироватьДеревоСкидок
// 	ПараметрыРасчета - см. ПараметрыРасчета
// 	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц
// 	
// 	Возвращаемое значение:
// 		см. ПустаяТаблицаРезультатПроверкиУсловия
//
Функция ВыполнитьЗапросыУсловийПредоставления(УсловияПредоставления, СкидкиНаценки, ПараметрыРасчета, МенеджерВременныхТаблиц)
	
	Пакет = ПакетЗапросовСоздать();
	Пакет.Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ОбработкаВторойОчереди = Новый Массив; // см. ВыполнитьЗапросыУсловийПредоставления.УсловияПредоставления
	
	// Подготовка таблиц для сбора данных за накопленный объем продаж
	ДанныеОТаблицах = Новый Соответствие;
	Для Каждого СтрокаТЧ Из УсловияПредоставления Цикл
		
		Если Не СтрокаТЧ.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ЗаНакопленныйОбъемПродаж Тогда
			Продолжить;
		КонецЕсли;
		
		Интервал = ИнтервалНакопленияОбъемаПродаж(СтрокаТЧ, ПараметрыРасчета);
		
		ИмяВременнойТаблицы = ИмяВременнойТаблицыНакопленныйОбъемПродаж(СтрокаТЧ, Интервал);
		
		Если ДанныеОТаблицах.Получить(ИмяВременнойТаблицы) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеОТаблицах.Вставить(ИмяВременнойТаблицы, Истина);
		
		Если СтрокаТЧ.ВариантНакопления = Перечисления.ВариантыНакопленияКумулятивнойСкидкиНаценки.ПоПартнеру Тогда
			Запрос = ЗапросНакопленныйОбъемПродажПоПартнеруЗаПериод(СтрокаТЧ, Интервал, ПараметрыРасчета);
		ИначеЕсли СтрокаТЧ.ВариантНакопления = Перечисления.ВариантыНакопленияКумулятивнойСкидкиНаценки.ПоТорговомуСоглашению Тогда
			Запрос = ЗапросНакопленныйОбъемПродажПоТорговомуСоглашениюЗаПериод(СтрокаТЧ, Интервал, ПараметрыРасчета);
		КонецЕсли;
		
		Ключ = Неопределено;
		УдалятьВременныеТаблицы = Ложь;
		ПреобразовыватьПараметры = Истина;
		
		ПакетЗапросовВставитьЗапросВПакет(
			Запрос, Пакет, Ключ,
			УдалятьВременныеТаблицы, ПреобразовыватьПараметры);
		
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из УсловияПредоставления Цикл
		
		Ключ = СтрокаТЧ.Ссылка;
		
		ИспользоватьКэшированныйЗапрос = Ложь;
		Если СтрокаТЧ.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ЗаРазовыйОбъемПродаж Тогда
			ИспользоватьКэшированныйЗапрос = Истина;
		ИначеЕсли СтрокаТЧ.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ЗаНакопленныйОбъемПродаж Тогда
			ИспользоватьКэшированныйЗапрос = Истина;
		КонецЕсли;
		
		Если ИспользоватьКэшированныйЗапрос Тогда
			
			Если СтрокаТЧ.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ЗаРазовыйОбъемПродаж
				И СтрокаТЧ.ВалютаОграничения <> ПараметрыРасчета.ВалютаДокумента Тогда
				
				Настройки = Неопределено;
			Иначе
				Настройки = СтрокаТЧ.ХранилищеНастроекКомпоновкиДанных.Получить();
			КонецЕсли;
			
			Если Настройки <> Неопределено И Настройки.Свойство("Запрос") Тогда
			
				КоличествоЗапросовВПакете = Настройки.Запрос.КоличествоЗапросовВПакете;
				ИндексЗапросаРезультата   = Настройки.Запрос.ИндексЗапросаРезультата;
				ТекстЗапроса              = Настройки.Запрос.ТекстЗапроса;
				
				Интервал = ИнтервалНакопленияОбъемаПродаж(СтрокаТЧ, ПараметрыРасчета);
				
				Если СтрокаТЧ.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ЗаНакопленныйОбъемПродаж Тогда
					ИмяВременнойТаблицы = ИмяВременнойТаблицыНакопленныйОбъемПродаж(СтрокаТЧ, Интервал);
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИсходныеДанные", ИмяВременнойТаблицы);
				КонецЕсли;
				
				Запрос = Новый Запрос(ТекстЗапроса);
				Для Каждого Параметр Из Настройки.Запрос.Параметры Цикл
					Запрос.УстановитьПараметр(Параметр.Ключ, Параметр.Значение);
				КонецЦикла;
				
				Если СтрокаТЧ.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ЗаНакопленныйОбъемПродаж Тогда
					Если СтрокаТЧ.ВариантНакопления = Перечисления.ВариантыНакопленияКумулятивнойСкидкиНаценки.ПоПартнеру Тогда
						ЗаполнитьПараметрыЗапросУсловияЗаНакопленныйОбъемПродажПоПартнеруЗаПериод(Запрос, СтрокаТЧ, Интервал, ПараметрыРасчета, "ПараметрыРасчета");
					ИначеЕсли СтрокаТЧ.ВариантНакопления = Перечисления.ВариантыНакопленияКумулятивнойСкидкиНаценки.ПоТорговомуСоглашению Тогда
						ЗаполнитьПараметрыЗапросУсловияЗаНакопленныйОбъемПродажПоТорговомуСоглашениюЗаПериод(Запрос, СтрокаТЧ, Интервал, ПараметрыРасчета, "ПараметрыРасчета");
					КонецЕсли;
				ИначеЕсли СтрокаТЧ.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ЗаРазовыйОбъемПродаж Тогда
					ЗаполнитьПараметрыЗапросУсловияЗаРазовуюПродажуСУсловиемПоДокументу(Запрос, СтрокаТЧ, ПараметрыРасчета, "ПараметрыРасчета");
				КонецЕсли;
				
				УдалятьВременныеТаблицы = Ложь;
				ПреобразовыватьПараметры = Истина;
				ПакетЗапросовВставитьЗапросВПакет(
					Запрос, Пакет, Ключ,
					УдалятьВременныеТаблицы, ПреобразовыватьПараметры,
					КоличествоЗапросовВПакете,
					ИндексЗапросаРезультата);
					
			Иначе
				
				УдалятьВременныеТаблицы = Истина;
				ПреобразовыватьПараметры = Истина;
				
				Запрос = ЗапросПроверкиУсловияПредоставления(СтрокаТЧ, ПараметрыРасчета, "Все");
				ПакетЗапросовВставитьЗапросВПакет(
					Запрос, Пакет, Ключ,
					УдалятьВременныеТаблицы, ПреобразовыватьПараметры);
				
			КонецЕсли;
			
		Иначе
			
			Запрос = ЗапросПроверкиУсловияПредоставления(СтрокаТЧ, ПараметрыРасчета, "Все");
			
			Если Запрос = Неопределено Тогда
				ОбработкаВторойОчереди.Добавить(СтрокаТЧ);
			Иначе
				
				Если ТипЗнч(СтрокаТЧ.УсловиеПредоставления) = Тип("СправочникСсылка.ДополнительныеОтчетыИОбработки") Тогда
					УдалятьВременныеТаблицы = Истина;
				Иначе
					УдалятьВременныеТаблицы = Ложь;
				КонецЕсли;
				
				ПреобразовыватьПараметры = Истина;
				
				ПакетЗапросовВставитьЗапросВПакет(
					Запрос, Пакет, Ключ,
					УдалятьВременныеТаблицы, ПреобразовыватьПараметры);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаДерева Из СкидкиНаценки Цикл
		
		Если СтрокаДерева.ВариантОтбораНоменклатуры = Перечисления.ВариантыОтбораНоменклатурыДляРасчетаСкидокНаценок.БезОграничений
			И Не СтрокаДерева.УстановленДополнительныйОтбор Тогда
			Продолжить;
		КонецЕсли;
		
		Ключ = СтрокаДерева.Ссылка;
		
		Настройки = СтрокаДерева.ХранилищеНастроекКомпоновкиДанных.Получить();
		
		Если Настройки <> Неопределено И Настройки.Свойство("Запрос") Тогда
		
			КоличествоЗапросовВПакете = Настройки.Запрос.КоличествоЗапросовВПакете;
			ИндексЗапросаРезультата   = Настройки.Запрос.ИндексЗапросаРезультата;
			ТекстЗапроса              = Настройки.Запрос.ТекстЗапроса;
			
			Запрос = Новый Запрос(ТекстЗапроса);
			Для Каждого Параметр Из Настройки.Запрос.Параметры Цикл
				Запрос.УстановитьПараметр(Параметр.Ключ, Параметр.Значение);
			КонецЦикла;
			
			УдалятьВременныеТаблицы = Ложь;
			ПреобразовыватьПараметры = Истина;
			ЗаполнитьПараметрыЗапросОпределенияПозицийНоменклатурыНаКоторыеПредоставляетсяСкидка(Запрос, ПараметрыРасчета, "ПараметрыРасчета");
			ПакетЗапросовВставитьЗапросВПакет(
				Запрос, Пакет, Ключ,
				УдалятьВременныеТаблицы, ПреобразовыватьПараметры,
				КоличествоЗапросовВПакете,
				ИндексЗапросаРезультата);
			
		Иначе
			
			УдалятьВременныеТаблицы = Истина;
			ПреобразовыватьПараметры = Истина;
			
			Запрос = ЗапросОпределенияПозицийНоменклатурыНаКоторыеПредоставляетсяСкидка(СтрокаДерева, ПараметрыРасчета, "Все");
			ПакетЗапросовВставитьЗапросВПакет(
				Запрос, Пакет, Ключ,
				УдалятьВременныеТаблицы, ПреобразовыватьПараметры);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПакетЗапросовВыполнить(Пакет);
	
	ТЗ = ПустаяТаблицаРезультатПроверкиУсловия();
	
	Для Каждого СтрокаТЧ Из ОбработкаВторойОчереди Цикл
		
		Если СтрокаТЧ.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ЗаГрафикОплаты Тогда
			
			КратностьВыполнения = ПроверитьУсловиеЗаГрафикОплаты(СтрокаТЧ, ПараметрыРасчета);
			
		ИначеЕсли СтрокаТЧ.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ЗаФормуОплаты Тогда
			
			КратностьВыполнения = ПроверитьУсловиеЗаФормуОплаты(СтрокаТЧ, ПараметрыРасчета);
			
		ИначеЕсли ТипЗнч(СтрокаТЧ.УсловиеПредоставления) = Тип("СправочникСсылка.ДополнительныеОтчетыИОбработки") Тогда
			
			ВнешняяОбработка = СкидкиНаценкиПовтИсп.ОбъектВнешнейОбработки(СтрокаТЧ.УсловиеПредоставления);
			УстановитьБезопасныйРежим(Истина);
			КратностьВыполнения = ВнешняяОбработка.ПроверитьУсловие(СтрокаТЧ.ПараметрыВнешнейОбработки.Получить(), ПараметрыРасчета);
			УстановитьБезопасныйРежим(Ложь);
			
		КонецЕсли;
		
		Если КратностьВыполнения = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТЗ.Добавить();
		НоваяСтрока.Ссылка              = СтрокаТЧ.Ссылка; 
		НоваяСтрока.КлючСвязи           = -1;
		НоваяСтрока.КратностьВыполнения = КратностьВыполнения;
		
	КонецЦикла;
	
	ТаблицаВыполненныеУсловия = ОбъединитьРезультаты(Пакет, ТЗ);
	
	Возврат ТаблицаВыполненныеУсловия;
	
КонецФункции

Функция ОбъединитьРезультаты(ПакетЗапросов, Таблица)
	
	ТЗ = ПустаяТаблицаРезультатПроверкиУсловия();
	
	ТипСкидкиНаценки = Тип("СправочникСсылка.СкидкиНаценки");
	
	Для Каждого КлючИЗначение Из ПакетЗапросов.СоответствиеЗапросаИНомераТаблицыРезультата Цикл
		
		ЕстьКолонкаЗначениеПоказателя = Неопределено;
		
		РезультатЗапроса = ПакетЗапросов.РезультатЗапроса[КлючИЗначение.Значение-1]; // РезультатЗапроса
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если ЕстьКолонкаЗначениеПоказателя = Неопределено Тогда
				ЕстьКолонкаЗначениеПоказателя = ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(Выборка, "ЗначениеПоказателя");
			КонецЕсли;
			
			НоваяСтрока = ТЗ.Добавить();
			НоваяСтрока.Ссылка = КлючИЗначение.Ключ;
			Если ТипЗнч(КлючИЗначение.Ключ) = ТипСкидкиНаценки Тогда
				НоваяСтрока.КлючСвязи = Выборка.КлючСвязи;
			Иначе
				НоваяСтрока.КлючСвязи = -1;
			КонецЕсли;
			НоваяСтрока.КратностьВыполнения = Выборка.КратностьВыполнения;
			
			Если ЕстьКолонкаЗначениеПоказателя Тогда
				НоваяСтрока.ЗначениеПоказателя = Выборка.ЗначениеПоказателя;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из Таблица Цикл
		ЗаполнитьЗначенияСвойств(ТЗ.Добавить(), СтрокаТЧ);
	КонецЦикла;
	
	Возврат ТЗ;
	
КонецФункции

// Параметры:
// 	СтрокаДерева - СтрокаДереваЗначений - см. СформироватьДеревоСкидок. Содержит в том числе:
// 		* ПараметрыУсловий - Структура - Содержит в том числе:
// 			** УсловияВыполнены - Булево
// 			** КодыСтрок - Массив
// 			** УсловияПоСтроке - Структура - Содержит в том числе:
// 				*** ТаблицаПроверкиУсловий - ТаблицаЗначений
// 				*** СоответствиеУсловийКолонкамТаблицыПроверкиУсловий - Соответствие
// 			** ЕстьУсловияПоСтроке - Булево
// 			** ТаблицаУсловий - ТаблицаЗначений
// 	ВыполненныеУсловия - см. ПустаяТаблицаРезультатПроверкиУсловия
// 
Функция ПроверитьУсловия(СтрокаДерева, ВыполненныеУсловия, Условия)
	
	СтрокаДерева.ПараметрыУсловий.Вставить("УсловияВыполнены",    Истина);
	СтрокаДерева.ПараметрыУсловий.Вставить("КодыСтрок",           Новый Массив);
	СтрокаДерева.ПараметрыУсловий.Вставить("УсловияПоСтроке",     Новый Структура);
	СтрокаДерева.ПараметрыУсловий.Вставить("ЕстьУсловияПоСтроке", Ложь);
	СтрокаДерева.ПараметрыУсловий.Вставить("ТаблицаУсловий",      Новый ТаблицаЗначений);
	
	// Служебная таблица для временного хранения результатов проверки условий предоставления.
	СтрокаДерева.ПараметрыУсловий.ТаблицаУсловий.Колонки.Добавить("УсловиеПредоставления", Новый ОписаниеТипов("СправочникСсылка.УсловияПредоставленияСкидокНаценок,СправочникСсылка.СкидкиНаценки"));
	СтрокаДерева.ПараметрыУсловий.ТаблицаУсловий.Колонки.Добавить("ОбластьОграничения",    Новый ОписаниеТипов("ПеречислениеСсылка.ВариантыОбластейОграниченияСкидокНаценок"));
	СтрокаДерева.ПараметрыУсловий.ТаблицаУсловий.Колонки.Добавить("Выполнено");
	СтрокаДерева.ПараметрыУсловий.ТаблицаУсловий.Колонки.Добавить("ЗначениеПоказателя");
	СтрокаДерева.ПараметрыУсловий.ТаблицаУсловий.Колонки.Добавить("КратностьВыполнения",   Новый ОписаниеТипов("Число"));
	
	// Таблица применяется для проверки выполнения условий по строке.
	// Если у скидки существуют условия по строке то для этих условий в таблице будет создана колонка.
	СтрокаДерева.ПараметрыУсловий.УсловияПоСтроке.Вставить("ТаблицаПроверкиУсловий", Новый ТаблицаЗначений);
	СтрокаДерева.ПараметрыУсловий.УсловияПоСтроке.ТаблицаПроверкиУсловий.Колонки.Добавить("КлючСвязи");
	СтрокаДерева.ПараметрыУсловий.УсловияПоСтроке.ТаблицаПроверкиУсловий.Индексы.Добавить("КлючСвязи");
	
	СтрокаДерева.ПараметрыУсловий.УсловияПоСтроке.Вставить("СоответствиеУсловийКолонкамТаблицыПроверкиУсловий", Новый Соответствие);
	
	// Служебные параметры
	ИспользуетсяТаблицаПроверкиУсловий      = Ложь;
	ЭтоПервоеУсловиеТаблицыПроверкиУсловий  = Истина;
	КоличествоКолонокТаблицыПроверкиУсловий = 0;
	
	Для Каждого Условие Из Условия.НайтиСтроки(Новый Структура("СкидкаНаценка", СтрокаДерева.Ссылка)) Цикл
		
		СтрокаТаблицаУсловий = СтрокаДерева.ПараметрыУсловий.ТаблицаУсловий.Добавить();
		СтрокаТаблицаУсловий.УсловиеПредоставления = Условие.УсловиеПредоставления;
		Если ТипЗнч(Условие.УсловиеПредоставления) = Тип("СправочникСсылка.УсловияПредоставленияСкидокНаценок") Тогда
			СтрокаТаблицаУсловий.ОбластьОграничения = Перечисления.ВариантыОбластейОграниченияСкидокНаценок.ВДокументе;
		Иначе
			СтрокаДерева.ПараметрыУсловий.ЕстьУсловияПоСтроке = Истина;
			СтрокаТаблицаУсловий.ОбластьОграничения = Перечисления.ВариантыОбластейОграниченияСкидокНаценок.ВСтроке;
		КонецЕсли;
		
		НайденныеСтроки = ВыполненныеУсловия.НайтиСтроки(Новый Структура("Ссылка", Условие.УсловиеПредоставления));
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			
			// Условие не выполнено.
			СтрокаТаблицаУсловий.Выполнено = Ложь;
			
			СтрокаДерева.ПараметрыУсловий.УсловияВыполнены = Ложь;
			
			СтрокаТаблицаУсловий.КратностьВыполнения = 0;
			
		ИначеЕсли НайденныеСтроки.Количество() = 1
			И НайденныеСтроки[0].КлючСвязи = -1 Тогда
			
			НайденнаяСтрока = НайденныеСтроки[0];
			Если НайденнаяСтрока.КратностьВыполнения = 0 Тогда
				
				СтрокаТаблицаУсловий.Выполнено = Ложь;
				// Условие не выполнено.
				СтрокаТаблицаУсловий.ЗначениеПоказателя = НайденнаяСтрока.ЗначениеПоказателя;
				
				СтрокаДерева.ПараметрыУсловий.УсловияВыполнены = Ложь;
				
			Иначе
				
				СтрокаТаблицаУсловий.Выполнено = Истина;
				// Условие выполнено. Условие не зависит от конкретных строк.
				СтрокаТаблицаУсловий.ЗначениеПоказателя = НайденнаяСтрока.ЗначениеПоказателя;
				СтрокаТаблицаУсловий.КратностьВыполнения = НайденнаяСтрока.КратностьВыполнения;
				
			КонецЕсли;
			
		Иначе
			
			КоличествоКолонокТаблицыПроверкиУсловий = КоличествоКолонокТаблицыПроверкиУсловий + 1;
			ЗаголовокКолонки = "Условие" + КоличествоКолонокТаблицыПроверкиУсловий;
			
			СтрокаДерева.ПараметрыУсловий.УсловияПоСтроке.СоответствиеУсловийКолонкамТаблицыПроверкиУсловий.Вставить(Условие.УсловиеПредоставления, ЗаголовокКолонки);
			СтрокаДерева.ПараметрыУсловий.УсловияПоСтроке.ТаблицаПроверкиУсловий.Колонки.Добавить(ЗаголовокКолонки, Новый ОписаниеТипов("Булево"));
			
			Счетчик = 0;
			СуммарнаяКратностьВыполнения = 0;
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				
				Если НайденнаяСтрока.КратностьВыполнения <> 0 Тогда
					Счетчик = Счетчик + 1;
				КонецЕсли;
				
				ИспользуетсяТаблицаПроверкиУсловий = Истина;
				
				НайденныеСтрокиТаблицыПроверкиУсловий = СтрокаДерева.ПараметрыУсловий.УсловияПоСтроке.ТаблицаПроверкиУсловий.Найти(НайденнаяСтрока.КлючСвязи, "КлючСвязи");
				Если НайденныеСтрокиТаблицыПроверкиУсловий <> Неопределено Тогда
					НайденныеСтрокиТаблицыПроверкиУсловий[ЗаголовокКолонки] = Истина;
				Иначе
					Если ЭтоПервоеУсловиеТаблицыПроверкиУсловий Тогда
						НоваяСтрока1 = СтрокаДерева.ПараметрыУсловий.УсловияПоСтроке.ТаблицаПроверкиУсловий.Добавить();
						НоваяСтрока1.КлючСвязи = НайденнаяСтрока.КлючСвязи;
						НоваяСтрока1[ЗаголовокКолонки] = Истина;
					КонецЕсли;
				КонецЕсли;
				
				Если НайденнаяСтрока.КратностьВыполнения > 0 Тогда
					СуммарнаяКратностьВыполнения = СуммарнаяКратностьВыполнения + НайденнаяСтрока.КратностьВыполнения;
				КонецЕсли;
				
			КонецЦикла;
			
			СтрокаТаблицаУсловий.Выполнено = (Счетчик > 0);
			// Условие выполнено если найдено несколько строк, прошедших проверку условий.
			
			Если Не СтрокаТаблицаУсловий.Выполнено Тогда
				СтрокаДерева.ПараметрыУсловий.УсловияВыполнены = Ложь;
			КонецЕсли;
			
			СтрокаТаблицаУсловий.КратностьВыполнения = СуммарнаяКратностьВыполнения;
			
		КонецЕсли;
		
		Если ИспользуетсяТаблицаПроверкиУсловий Тогда
			ЭтоПервоеУсловиеТаблицыПроверкиУсловий = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	// Заполним коды строк...
	Если СтрокаДерева.ПараметрыУсловий.УсловияВыполнены Тогда
		
		Если СтрокаДерева.ПараметрыУсловий.УсловияПоСтроке.СоответствиеУсловийКолонкамТаблицыПроверкиУсловий.Количество() > 0 Тогда
			
			Отбор = Новый Структура;
			Для Каждого КлючИЗначение Из СтрокаДерева.ПараметрыУсловий.УсловияПоСтроке.СоответствиеУсловийКолонкамТаблицыПроверкиУсловий Цикл
				Отбор.Вставить(КлючИЗначение.Значение, Истина);
			КонецЦикла;
			
			НайденныеСтроки = СтрокаДерева.ПараметрыУсловий.УсловияПоСтроке.ТаблицаПроверкиУсловий.НайтиСтроки(Отбор);
			Для Каждого СтрокаТЗ Из НайденныеСтроки Цикл
				СтрокаДерева.ПараметрыУсловий.КодыСтрок.Добавить(СтрокаТЗ.КлючСвязи);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;

КонецФункции

Процедура ПроверитьУсловияРекурсивно(ДеревоСкидок, СтрокиДереваСкидок, Условия, ВыполненныеУсловия)
	
	ДеревоСкидок.Колонки.Добавить("УсловияПредоставления", Новый ОписаниеТипов("ТаблицаЗначений"));
	ДеревоСкидок.Колонки.Добавить("РезультатРасчета",      Новый ОписаниеТипов("ТаблицаЗначений"));
	ДеревоСкидок.Колонки.Добавить("ПараметрыУсловий",      Новый ОписаниеТипов("Структура"));
	
	Для Каждого СтрокаДерева Из СтрокиДереваСкидок Цикл
		
		ПроверитьУсловия(СтрокаДерева, ВыполненныеУсловия, Условия);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПодготовитьТекстЗапросаДляПроверкиУсловияИРасчетаКратности(ТекстЗапроса, УсловиеПредоставления)
	
	ТекстУсловия = "";
	ТекстПоказателя = "";
	Если УсловиеПредоставления.КритерийОграниченияПримененияЗаОбъемПродаж = Перечисления.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.Сумма Тогда
		Если УсловиеПредоставления.ТипСравнения = Перечисления.ТипыСравненияЗначенийСкидокНаценок.НеМенее Тогда
			ТекстУсловия = "ЕСТЬNULL(Товары.Сумма, 0) >= &ЗначениеУсловияОграничения"+"Кэшируется";
		Иначе
			ТекстУсловия = "ЕСТЬNULL(Товары.Сумма, 0) <= &ЗначениеУсловияОграничения"+"Кэшируется";
		КонецЕсли;
		ТекстПоказателя = "ЕСТЬNULL(Товары.Сумма, 0)";
	Иначе
		Если УсловиеПредоставления.ТипСравнения = Перечисления.ТипыСравненияЗначенийСкидокНаценок.НеМенее Тогда
			ТекстУсловия = "ЕСТЬNULL(Товары.Количество, 0) >= &ЗначениеУсловияОграничения"+"Кэшируется"
		Иначе
			ТекстУсловия = "ЕСТЬNULL(Товары.Количество, 0) <= &ЗначениеУсловияОграничения"+"Кэшируется"
		КонецЕсли;
		ТекстПоказателя = "ЕСТЬNULL(Товары.Количество, 0)";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстУсловия", ТекстУсловия);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗначениеПоказателя", ТекстПоказателя);
	
	ТекстКратности = "";
	Если УсловиеПредоставления.КритерийОграниченияПримененияЗаОбъемПродаж = Перечисления.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.Сумма Тогда
		
		Если УсловиеПредоставления.ТипСравнения = Перечисления.ТипыСравненияЗначенийСкидокНаценок.НеМенее Тогда
			Если УсловиеПредоставления.ЗначениеУсловияОграничения <> 0 Тогда
				ТекстКратности = "ВЫБОР КОГДА ЕСТЬNULL(Товары.Сумма, 0) > 0 ТОГДА ВЫРАЗИТЬ((ЕСТЬNULL(Товары.Сумма, 0) / &ЗначениеУсловияОграниченияКэшируется - 0.5) КАК Число(10,0)) ИНАЧЕ 0 КОНЕЦ";
			Иначе
				ТекстКратности = "
				|ВЫБОР
				|	КОГДА ЕСТЬNULL(Товары.Сумма, 0) > 0 ТОГДА
				|		1
				|	ИНАЧЕ
				|		0
				|КОНЕЦ";
			КонецЕсли;
		Иначе
			ТекстКратности = "
			|ВЫБОР
			|	КОГДА ЕСТЬNULL(Товары.Сумма, 0) <= &ЗначениеУсловияОграниченияКэшируется ТОГДА
			|		1
			|	ИНАЧЕ
			|		0
			|КОНЕЦ";
		КонецЕсли;
		ТекстКратности = СтрЗаменить(ТекстКратности, "&ЗначениеУсловияОграниченияКэшируется", "&ЗначениеУсловияОграничения"+"Кэшируется");
	ИначеЕсли УсловиеПредоставления.КритерийОграниченияПримененияЗаОбъемПродаж = Перечисления.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.Количество
		ИЛИ УсловиеПредоставления.КритерийОграниченияПримененияЗаОбъемПродаж = Перечисления.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.КоличествоОдинаковых
		ИЛИ УсловиеПредоставления.КритерийОграниченияПримененияЗаОбъемПродаж = Перечисления.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.КоличествоРазличных Тогда
		
		Если УсловиеПредоставления.ТипСравнения = Перечисления.ТипыСравненияЗначенийСкидокНаценок.НеМенее Тогда
			Если УсловиеПредоставления.ЗначениеУсловияОграничения <> 0 Тогда
				ТекстКратности = "ВЫБОР КОГДА ЕСТЬNULL(Товары.Количество, 0) > 0 ТОГДА ВЫРАЗИТЬ((ЕСТЬNULL(Товары.Количество, 0) / &ЗначениеУсловияОграниченияКэшируется - 0.5) КАК Число(10,0)) ИНАЧЕ 0 КОНЕЦ";
			Иначе
				ТекстКратности = "
				|ВЫБОР
				|	КОГДА ЕСТЬNULL(Товары.Количество, 0) > 0 ТОГДА
				|		1
				|	ИНАЧЕ
				|		0
				|КОНЕЦ";
			КонецЕсли
		Иначе
			ТекстКратности = "
			|ВЫБОР
			|	КОГДА ЕСТЬNULL(Товары.Количество, 0) <= &ЗначениеУсловияОграниченияКэшируется ТОГДА
			|		1
			|	ИНАЧЕ
			|		0
			|КОНЕЦ";
		КонецЕсли;
		ТекстКратности = СтрЗаменить(ТекстКратности, "&ЗначениеУсловияОграниченияКэшируется", "&ЗначениеУсловияОграничения"+"Кэшируется");
	Иначе
		ТекстКратности = "0";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстКратности", ТекстКратности);
	
КонецПроцедуры

#КонецОбласти

#Область Наборы

Функция ДанныеПоТоварамИНаборам(ТекстЗапроса, ПараметрыЗапроса, ИспользоватьНаборы) Экспорт
	
	ПоляНабора =
	"	Товары.НоменклатураНабора   КАК НоменклатураНабора,
	|	Товары.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|";
	
	Поля =
	"	ВариантыКомплектацииНоменклатуры.Ссылка  КАК ВариантКомплектацииНоменклатуры,
	|	ВариантыКомплектацииНоменклатуры.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ВариантыКомплектацииНоменклатуры.ВариантРасчетаЦеныНабора                  КАК ВариантРасчетаЦеныНабора,
	|	Таблица.НоменклатураНабора   КАК НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|";
	
	Соединение =
	"	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
	|		ПО ВариантыКомплектацииНоменклатуры.Владелец = Таблица.НоменклатураНабора
	|		И ВариантыКомплектацииНоменклатуры.Характеристика = Таблица.ХарактеристикаНабора
	|		И ВариантыКомплектацииНоменклатуры.Основной
	|";
	
	ЗапросНаборы =
	";
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.НоменклатураНабора   КАК НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	МИНИМУМ(Таблица.КлючСвязи)   КАК КлючСвязи,
	|	СУММА(Таблица.Цена * Таблица.КоличествоУпаковок) КАК Сумма
	|ПОМЕСТИТЬ ВременнаяТаблицаНаборыПодготовка
	|ИЗ
	|	Товары КАК Таблица
	|
	|ГДЕ
	|	Таблица.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И Таблица.ВариантРасчетаЦеныНабора В (ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ЦенаЗадаетсяЗаНаборРаспределяетсяПоЦенам),
	|	                                      ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ЦенаЗадаетсяЗаНаборРаспределяетсяПоДолям))
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НоменклатураНабора,
	|	Товары.ХарактеристикаНабора,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	ВЫБОР КОГДА Товары.ВариантКомплектацииНоменклатуры.НоменклатураОсновногоКомпонента = Товары.Номенклатура
	|		И Товары.ВариантКомплектацииНоменклатуры.ХарактеристикаОсновногоКомпонента = Товары.Характеристика ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОсновнаяКомплектующая,
	|	0 КАК КоличествоПоУмолчанию,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ ВременнаяТаблицаНаборыДополнительноЧастьПервая
	|ИЗ
	|	Товары КАК Товары
	|
	|ГДЕ
	|	Товары.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка.Владелец                                  КАК НоменклатураНабора,
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка.Характеристика                            КАК ХарактеристикаНабора,
	|	ВариантыКомплектацииНоменклатурыТовары.Номенклатура   КАК Номенклатура,
	|	ВариантыКомплектацииНоменклатурыТовары.Характеристика КАК Характеристика,
	|	ЛОЖЬ КАК ОсновнаяКомплектующая,
	|	СУММА(ВариантыКомплектацииНоменклатурыТовары.Количество) КАК КоличествоПоУмолчанию,
	|	0 КАК Количество
	|ИЗ
	|	Справочник.ВариантыКомплектацииНоменклатуры.Товары КАК ВариантыКомплектацииНоменклатурыТовары
	|ГДЕ
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка В (ВЫБРАТЬ РАЗЛИЧНЫЕ Т.ВариантКомплектацииНоменклатуры ИЗ Товары КАК Т)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка.Владелец,
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка.Характеристика,
	|	ВариантыКомплектацииНоменклатурыТовары.Номенклатура,
	|	ВариантыКомплектацииНоменклатурыТовары.Характеристика,
	|	ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	МАКСИМУМ(Таблица.ОсновнаяКомплектующая) КАК ОсновнаяКомплектующая,
	|	СУММА(Таблица.КоличествоПоУмолчанию) КАК КоличествоПоУмолчанию,
	|	СУММА(Таблица.Количество) КАК Количество
	|ПОМЕСТИТЬ ВременнаяТаблицаНаборыДополнительноЧастьВторая
	|ИЗ
	|	ВременнаяТаблицаНаборыДополнительноЧастьПервая КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Результат.НоменклатураНабора,
	|	Результат.ХарактеристикаНабора,
	|	ВЫРАЗИТЬ(МИНИМУМ(ВЫБОР
	|			КОГДА Результат.КоличествоПоУмолчанию <> 0 И Результат.ОсновнаяКомплектующая
	|				ТОГДА Результат.Количество / Результат.КоличествоПоУмолчанию
	|			ИНАЧЕ null
	|		КОНЕЦ) + 0.5 КАК Число(10,0)) - 1 КАК Количество
	|ПОМЕСТИТЬ ВременнаяТаблицаНаборыДополнительно
	|ИЗ
	|	ВременнаяТаблицаНаборыДополнительноЧастьВторая КАК Результат
	|СГРУППИРОВАТЬ ПО
	|	Результат.НоменклатураНабора,
	|	Результат.ХарактеристикаНабора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.НоменклатураНабора                КАК НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора              КАК ХарактеристикаНабора,
	|	Таблица.КлючСвязи                         КАК КлючСвязи,
	|	ЕСТЬNULL(ВременнаяТаблицаНаборыДополнительно.Количество, 1) КАК КоличествоУпаковок,
	|	ЕСТЬNULL(ВременнаяТаблицаНаборыДополнительно.Количество, 1) КАК Количество,
	|	Таблица.Сумма                             КАК Сумма
	|ПОМЕСТИТЬ ВременнаяТаблицаНаборы
	|ИЗ
	|	ВременнаяТаблицаНаборыПодготовка КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНаборыДополнительно КАК ВременнаяТаблицаНаборыДополнительно
	|		ПО Таблица.НоменклатураНабора = ВременнаяТаблицаНаборыДополнительно.НоменклатураНабора
	|		И Таблица.ХарактеристикаНабора = ВременнаяТаблицаНаборыДополнительно.ХарактеристикаНабора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.КлючСвязи,
	|	Таблица.НоменклатураНабора КАК Номенклатура,
	|	Таблица.ХарактеристикаНабора КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка) КАК ВидЦены,
	|	Таблица.НоменклатураНабора.ЦеноваяГруппа КАК ЦеноваяГруппа,
	|	Таблица.Количество,
	|	Таблица.КоличествоУпаковок,
	|	ВЫБОР КОГДА Таблица.КоличествоУпаковок <> 0 ТОГДА
	|		Таблица.Сумма / Таблица.КоличествоУпаковок
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК Цена,
	|	Таблица.Сумма
	|ИЗ
	|	ВременнаяТаблицаНаборы КАК Таблица
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.КлючСвязи,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	Таблица.Упаковка,
	|	Таблица.Серия,
	|	Таблица.Склад,
	|	Таблица.ВидЦены,
	|	Таблица.ЦеноваяГруппа,
	|	Таблица.Количество,
	|	Таблица.КоличествоУпаковок,
	|	Таблица.Цена,
	|	Таблица.Сумма
	|ИЗ
	|	Товары КАК Таблица
	|ГДЕ
	|	Таблица.НоменклатураНабора = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	ИЛИ (Таблица.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	   И Таблица.ВариантРасчетаЦеныНабора = ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.РассчитываетсяИзЦенКомплектующих))
	|;
	|
	|ВЫБРАТЬ
	|	Таблица.КлючСвязи   КАК КлючСвязиКомплектующих,
	|	ВременнаяТаблицаНаборы.КлючСвязи КАК КлючСвязиНабора
	|ИЗ
	|	Товары КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНаборы КАК ВременнаяТаблицаНаборы
	|		ПО ВременнаяТаблицаНаборы.НоменклатураНабора = Таблица.НоменклатураНабора
	|		И ВременнаяТаблицаНаборы.ХарактеристикаНабора = Таблица.ХарактеристикаНабора
	|ГДЕ
	|	Таблица.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И Таблица.ВариантРасчетаЦеныНабора В (ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ЦенаЗадаетсяЗаНаборРаспределяетсяПоЦенам),
	|	                                      ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ЦенаЗадаетсяЗаНаборРаспределяетсяПоДолям))
	|ИТОГИ ПО
	|	ВременнаяТаблицаНаборы.КлючСвязи
	|";
	
	Если ИспользоватьНаборы Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&ИмяВременнойТаблицы",	"ПОМЕСТИТЬ Товары");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляНабора,",			ПоляНабора);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляТаблицы,",			Поля);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&Соединение",			Соединение);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "; ВЫБРАТЬ &ЗапросНаборы",	ЗапросНаборы);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&ИмяВременнойТаблицы",	"");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляНабора,",			"");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляТаблицы,",			"");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&Соединение",			"");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "; ВЫБРАТЬ &ЗапросНаборы",	"");
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Для Каждого КлючЗначение Из ПараметрыЗапроса Цикл
		Запрос.УстановитьПараметр(КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("Товары");
	ВозвращаемоеЗначение.Вставить("СоответствиеКлючей");
	Если ИспользоватьНаборы Тогда
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		ВозвращаемоеЗначение.Товары             = РезультатЗапроса[РезультатЗапроса.Количество() - 2].Выгрузить();
		ВозвращаемоеЗначение.СоответствиеКлючей = РезультатЗапроса[РезультатЗапроса.Количество() - 1].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	Иначе
		ВозвращаемоеЗначение.Товары = Запрос.Выполнить().Выгрузить();
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Процедура РаспределитьСкидкиНабораПоКомплектующим(ПримененныеСкидкиНаценки, СоответствиеКлючей, ТаблицаТовары)
	
	Для Каждого СтрокаТЧ Из ПримененныеСкидкиНаценки.ДеревоСкидок.Строки Цикл
		РаспределитьТаблицыДанныхПоКомплектующим(СтрокаТЧ, СоответствиеКлючей, ТаблицаТовары);
	КонецЦикла;
	
	Для Каждого СтрокаНабор Из СоответствиеКлючей.Строки Цикл
		
		КУдалению = Новый Массив;
		
		НайденныеСтроки = ПримененныеСкидкиНаценки.ТаблицаСкидкиНаценки.НайтиСтроки(Новый Структура("КлючСвязи", СтрокаНабор.КлючСвязиНабора));
		Для Каждого СтрокаСкидка Из НайденныеСтроки Цикл
			
			СуммаКРаспределению = СтрокаСкидка.Сумма;
			
			Сумма = 0;
			Для Каждого СтрокаКомплектующие Из СтрокаНабор.Строки Цикл
				СтрокаТЧ = ТаблицаТовары.Найти(СтрокаКомплектующие.КлючСвязиКомплектующих, "КлючСвязи");
				Сумма = Сумма + СтрокаТЧ.КоличествоУпаковок*СтрокаТЧ.Цена;
			КонецЦикла;
			
			НоваяСтрока = Неопределено;
			
			Для Каждого СтрокаКомплектующие Из СтрокаНабор.Строки Цикл
				
				СтрокаТЧ = ТаблицаТовары.Найти(СтрокаКомплектующие.КлючСвязиКомплектующих, "КлючСвязи");
				
				НоваяСтрока = ПримененныеСкидкиНаценки.ТаблицаСкидкиНаценки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСкидка,,"КлючСвязи,Сумма");
				
				НоваяСтрока.КлючСвязи = СтрокаКомплектующие.КлючСвязиКомплектующих;
				Если Сумма <> 0 Тогда
					НоваяСтрока.Сумма = Окр((СтрокаТЧ.КоличествоУпаковок*СтрокаТЧ.Цена)/Сумма*СуммаКРаспределению, 2);
				КонецЕсли;
				СуммаКРаспределению = СуммаКРаспределению - НоваяСтрока.Сумма;
				Сумма = Сумма - СтрокаТЧ.КоличествоУпаковок*СтрокаТЧ.Цена;
				
				Если СуммаКРаспределению = 0 Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;

			Если СуммаКРаспределению <> 0 И Не НоваяСтрока = Неопределено Тогда
				НоваяСтрока.Сумма = НоваяСтрока.Сумма + СуммаКРаспределению;
			КонецЕсли;
			
			КУдалению.Добавить(СтрокаСкидка);
			
		КонецЦикла;
		
		Для Каждого СтрокаТЧ Из КУдалению Цикл
			ПримененныеСкидкиНаценки.ТаблицаСкидкиНаценки.Удалить(СтрокаТЧ);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура РаспределитьТаблицыДанныхПоКомплектующим(СтрокаТаблицы, СоответствиеКлючей, ТаблицаТовары)
	
	Для Каждого СтрокаНабор Из СоответствиеКлючей.Строки Цикл
		
		КУдалению = Новый Массив;
		
		НайденныеСтроки = СтрокаТаблицы.РезультатРасчета.НайтиСтроки(Новый Структура("КлючСвязи", СтрокаНабор.КлючСвязиНабора));
		Для Каждого СтрокаСкидка Из НайденныеСтроки Цикл
			
			СуммаКРаспределению = СтрокаСкидка.Сумма;
			
			Сумма = 0;
			Для Каждого СтрокаКомплектующие Из СтрокаНабор.Строки Цикл
				СтрокаТЧ = ТаблицаТовары.Найти(СтрокаКомплектующие.КлючСвязиКомплектующих, "КлючСвязи");
				Сумма = Сумма + СтрокаТЧ.КоличествоУпаковок*СтрокаТЧ.Цена;
			КонецЦикла;
			
			Для Каждого СтрокаКомплектующие Из СтрокаНабор.Строки Цикл
				
				СтрокаТЧ = ТаблицаТовары.Найти(СтрокаКомплектующие.КлючСвязиКомплектующих, "КлючСвязи");
				
				НоваяСтрока = СтрокаТаблицы.РезультатРасчета.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСкидка,,"КлючСвязи,Сумма,Расшифровка");
				
				НоваяСтрока.КлючСвязи = СтрокаКомплектующие.КлючСвязиКомплектующих;
				Если Сумма <> 0 Тогда
					НоваяСтрока.Сумма = (СтрокаТЧ.КоличествоУпаковок*СтрокаТЧ.Цена)/Сумма*СуммаКРаспределению;
				КонецЕсли;
				СуммаКРаспределению = СуммаКРаспределению - НоваяСтрока.Сумма;
				Сумма = Сумма - СтрокаТЧ.КоличествоУпаковок*СтрокаТЧ.Цена;
				
				НоваяСтрока.Расшифровка = СтрокаСкидка.Расшифровка.СкопироватьКолонки();
				Для Каждого СтрокаТЧ Из СтрокаСкидка.Расшифровка Цикл
					НоваяСтрокаРасшифровки = НоваяСтрока.Расшифровка.Добавить();
					НоваяСтрокаРасшифровки.СкидкаНаценка = СтрокаТЧ.СкидкаНаценка;
					Если СтрокаСкидка.Сумма <> 0 Тогда
						НоваяСтрокаРасшифровки.Сумма = СтрокаТЧ.Сумма * НоваяСтрока.Сумма / СтрокаСкидка.Сумма;
					КонецЕсли;
				КонецЦикла;
				
			КонецЦикла;
			
			КУдалению.Добавить(СтрокаСкидка);
			
		КонецЦикла;
		
		Для Каждого СтрокаТЧ Из КУдалению Цикл
			СтрокаТаблицы.РезультатРасчета.Удалить(СтрокаТЧ);
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из СтрокаТаблицы.Строки Цикл
		РаспределитьТаблицыДанныхПоКомплектующим(СтрокаТЧ, СоответствиеКлючей, ТаблицаТовары);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбщиеПроцедурыФункцииФормУсловийПримененияСкидок

// Проверка необходимости регистрации на обновление.
// 
// Параметры:
//  СсылкаДляПроверки  - СправочникОбъект.СкидкиНаценки, СправочникОбъект.УсловияПредоставленияСкидокНаценок - Ссылка для проверки
//  ТекстПроверки  - Строка - Текс вхождения в запрос
// 
// Возвращаемое значение:
//  Булево - Истина, Необходима регистрация
Функция НеобходимаРегистрация(СсылкаДляПроверки, ТекстПроверки = "") Экспорт
	
	// проверим корректность сохраненного фильтра, если он есть
	Попытка
		ДанныеФильтраПоНоменклатуре(СсылкаДляПроверки, "");
	Исключение
		Возврат Ложь;
	КонецПопытки;
	ХранилищеНастроекКомпоновкиДанных = СсылкаДляПроверки.ПолучитьОбъект().ХранилищеНастроекКомпоновкиДанных; // ХранилищеЗначения
	НастройкиКомпоновкиДанных = ХранилищеНастроекКомпоновкиДанных.Получить();
	Если ЗначениеЗаполнено(НастройкиКомпоновкиДанных) 
		И ТипЗнч(НастройкиКомпоновкиДанных) = Тип("Структура") 
		И НастройкиКомпоновкиДанных.Свойство("Запрос") 
		И НастройкиКомпоновкиДанных.Запрос.Свойство("ТекстЗапроса") Тогда
		
		Если ПустаяСтрока(ТекстПроверки) Тогда
			Возврат Не ПустаяСтрока(НастройкиКомпоновкиДанных.Запрос.ТекстЗапроса);
		Иначе
			Возврат (СтрНайти(НастройкиКомпоновкиДанных.Запрос.ТекстЗапроса, ТекстПроверки) = 0);
		КонецЕсли;
		
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Обновить кэшированный запрос.
// 
// Параметры:
//  ТекущийОбъект - СправочникОбъект.СкидкиНаценки, СправочникОбъект.УсловияПредоставленияСкидокНаценок - Объект изменения
Процедура ОбновитьКэшированныйЗапрос(ТекущийОбъект) Экспорт

	ФормаСтруктура = Новый Структура;
	ФормаСтруктура.Вставить("АдресНастроекВнешнейОбработки","");
	ФормаСтруктура.Вставить("КомпоновщикНастроекОтборПоНоменклатуре",Новый КомпоновщикНастроекКомпоновкиДанных);
	ФормаСтруктура.Вставить("КомпоновщикНастроекДополнительныеУсловия",Новый КомпоновщикНастроекКомпоновкиДанных);
	
	НастройкиКомпоновкиДанных = ТекущийОбъект.ХранилищеНастроекКомпоновкиДанных.Получить();
	Если ЗначениеЗаполнено(НастройкиКомпоновкиДанных) Тогда
		Если ТипЗнч(НастройкиКомпоновкиДанных) = Тип("Структура") И НастройкиКомпоновкиДанных.Свойство("Запрос") Тогда
			НастройкиКомпоновкиДанных.Удалить("Запрос");
		КонецЕсли;
	Иначе
		НастройкиКомпоновкиДанных = Новый Структура();
	КонецЕсли;
	
	// Поместить настройки без кэшированного запроса.
	ТекущийОбъект.ХранилищеНастроекКомпоновкиДанных = Новый ХранилищеЗначения(НастройкиКомпоновкиДанных);
	
	ВставитьКэшированныйЗапрос(НастройкиКомпоновкиДанных, ТекущийОбъект);
	
	ТекущийОбъект.ХранилищеНастроекКомпоновкиДанных = Новый ХранилищеЗначения(НастройкиКомпоновкиДанных);

КонецПроцедуры

Процедура ВставитьКэшированныйЗапрос(НастройкиКомпоновкиДанных, ТекущийОбъект)
	
	ПараметрыРасчета = ПараметрыРасчета();
	Если ТипЗнч(ТекущийОбъект.Ссылка) = Тип("СправочникСсылка.УсловияПредоставленияСкидокНаценок") Тогда
		ИспользоватьКэшированныйЗапрос = Ложь;
		Если ТекущийОбъект.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ЗаРазовыйОбъемПродаж Тогда
			ИспользоватьКэшированныйЗапрос = Истина;
		ИначеЕсли ТекущийОбъект.УсловиеПредоставления = Перечисления.УсловияПредоставленияСкидокНаценок.ЗаНакопленныйОбъемПродаж Тогда
			ИспользоватьКэшированныйЗапрос = Истина;
		КонецЕсли;
		Если ИспользоватьКэшированныйЗапрос Тогда
			Запрос = ЗапросПроверкиУсловияПредоставления(ТекущийОбъект, ПараметрыРасчета, "Кэшируемые");
		КонецЕсли;
	Иначе
		Запрос = ЗапросОпределенияПозицийНоменклатурыНаКоторыеПредоставляетсяСкидка(ТекущийОбъект, ПараметрыРасчета, "Кэшируемые");
	КонецЕсли;
	
	Если Запрос <> Неопределено Тогда
		
		УдалитьВременныеТаблицы(Запрос);
		
		Подзапросы = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Запрос.Текст, ";");
		КоличествоЗапросовВПакете = Подзапросы.Количество();
		ИндексЗапросаРезультата = 0;
		Для Индекс = -(КоличествоЗапросовВПакете - 1) По 0 Цикл
			Если Найти(Подзапросы[-Индекс], "#123456789#") > 0 Тогда
				ИндексЗапросаРезультата = -Индекс + 1;
			КонецЕсли;
		КонецЦикла;
		
		НастройкиКомпоновкиДанных.Вставить("Запрос", Новый Структура);
		НастройкиКомпоновкиДанных.Запрос.Вставить("Параметры", Новый Структура);
		Для Каждого Параметр Из Запрос.Параметры Цикл
			Если СтрНайти(Параметр.Ключ, "Кэшируется") > 0 Тогда
				ИмяПараметра = ПакетЗапросовПреобразоватьПараметр(Запрос.Текст, Параметр.Ключ, Параметр.Значение, Неопределено);
				Если ИмяПараметра <> Неопределено Тогда
					НастройкиКомпоновкиДанных.Запрос.Параметры.Вставить(ИмяПараметра, Параметр.Значение);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		НастройкиКомпоновкиДанных.Запрос.Вставить("ТекстЗапроса", Запрос.Текст);
		НастройкиКомпоновкиДанных.Запрос.Вставить("ИндексЗапросаРезультата",   ИндексЗапросаРезультата);
		НастройкиКомпоновкиДанных.Запрос.Вставить("КоличествоЗапросовВПакете", КоличествоЗапросовВПакете);
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события ПередЗаписьюНаСервере.
//
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Форма
// 	ТекущийОбъект - СправочникОбъект.СкидкиНаценки, СправочникОбъект.УсловияПредоставленияСкидокНаценок - 
// 	ИмяПоля - Строка - Имя поля
// 
Процедура ПередЗаписьюНаСервере(Форма, ТекущийОбъект, ИмяПоля) Экспорт
	
	Если ТипЗнч(ТекущийОбъект[ИмяПоля]) = Тип("СправочникСсылка.ДополнительныеОтчетыИОбработки") Тогда
		Если ЗначениеЗаполнено(Форма.АдресНастроекВнешнейОбработки) Тогда
			ТекущийОбъект.ПараметрыВнешнейОбработки = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(Форма.АдресНастроекВнешнейОбработки));
		Иначе
			ТекущийОбъект.ПараметрыВнешнейОбработки = Новый ХранилищеЗначения(Неопределено);
		КонецЕсли;
	Иначе
		ТекущийОбъект.ПараметрыВнешнейОбработки = Новый ХранилищеЗначения(Неопределено);
	КонецЕсли;
	
	НастройкиКомпоновкиДанных = Новый Структура;
	НастройкиКомпоновкиДанных.Вставить("ОтборПоНоменклатуре", Форма.КомпоновщикНастроекОтборПоНоменклатуре.ПолучитьНастройки());
	Если ТипЗнч(ТекущийОбъект.Ссылка) = Тип("СправочникСсылка.СкидкиНаценки") Тогда
		ТекущийОбъект.УстановленДополнительныйОтбор = ЗначениеЗаполнено(Строка(Форма.КомпоновщикНастроекДополнительныеУсловия.Настройки.Отбор));
		НастройкиКомпоновкиДанных.Вставить("ДополнительныеУсловия", Форма.КомпоновщикНастроекДополнительныеУсловия.ПолучитьНастройки());
	КонецЕсли;
	
	// Поместить настройки без кэшированного запроса.
	// Функция ДанныеФильтраПоНоменклатуре считывает настройки из объекта для подготовки кэшированного запроса.
	ТекущийОбъект.ХранилищеНастроекКомпоновкиДанных = Новый ХранилищеЗначения(НастройкиКомпоновкиДанных);
	
	ВставитьКэшированныйЗапрос(НастройкиКомпоновкиДанных, ТекущийОбъект);
	
	ТекущийОбъект.ХранилищеНастроекКомпоновкиДанных = Новый ХранилищеЗначения(НастройкиКомпоновкиДанных);
	
КонецПроцедуры

// Обработчик события ПередЗаписью.
//
// Параметры:
// 	ТекущийОбъект - СправочникОбъект.СкидкиНаценки, СправочникОбъект.УсловияПредоставленияСкидокНаценок - 
// 	Отказ - Булево - Истина, если возникла ошибка
// 
Процедура ПередЗаписью(ТекущийОбъект, Отказ) Экспорт
	
	Если ТекущийОбъект.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиКомпоновкиДанных = ТекущийОбъект.ХранилищеНастроекКомпоновкиДанных.Получить();
	
	Если ТипЗнч(НастройкиКомпоновкиДанных) = Тип("Структура") Тогда
		ТекстИсключения = "";
		ЗапросНастройкиКомпоновкиДанных = Неопределено;
		Если НастройкиКомпоновкиДанных.Свойство("Запрос", ЗапросНастройкиКомпоновкиДанных) Тогда
			Если ТипЗнч(ЗапросНастройкиКомпоновкиДанных) = Тип("Структура") Тогда
				ТекстЗапросаНастройкиКомпоновкиДанных = Неопределено;
				ПараметрыЗапросаНастройкиКомпоновкиДанных = Неопределено;
				Если ЗапросНастройкиКомпоновкиДанных.Свойство("ТекстЗапроса", ТекстЗапросаНастройкиКомпоновкиДанных) И 
					 ЗапросНастройкиКомпоновкиДанных.Свойство("Параметры", ПараметрыЗапросаНастройкиКомпоновкиДанных) Тогда
					 
					 ЗапросДляПроверкиПараметров = Новый Запрос(ТекстЗапросаНастройкиКомпоновкиДанных);
					 Для каждого ПараметрЗапроса из ЗапросДляПроверкиПараметров.Параметры Цикл
						 Если Не ПараметрыЗапросаНастройкиКомпоновкиДанных.Свойство(ПараметрЗапроса.Ключ) Тогда
							Отказ = Истина;
							ТекстИсключения = НСтр("ru = 'В реквизите %1 коллекция параметров не соответствует параметрам текста запроса'");
							ТекстИсключения = СтрШаблон(ТекстИсключения, "ХранилищеНастроекКомпоновкиДанных");
							Прервать;
						 КонецЕсли;
					 КонецЦикла;
					 
				КонецЕсли;
			Иначе	
				Отказ = Истина;
				ТекстИсключения = НСтр("ru = 'В реквизите %1 нет данных о запросе'");
				ТекстИсключения = СтрШаблон(ТекстИсключения, "ХранилищеНастроекКомпоновкиДанных");
			КонецЕсли;
		КонецЕсли;
		
		Если Отказ Тогда
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Форма, содержит в том числе:
// 		* Объект - СправочникОбъект.СкидкиНаценки, СправочникОбъект.УсловияПредоставленияСкидокНаценок - 
// 	ТекущийОбъект - СправочникОбъект.СкидкиНаценки, СправочникОбъект.УсловияПредоставленияСкидокНаценок - 
// 
Процедура ПриСозданииЧтенииНаСервере(Форма, ТекущийОбъект = Неопределено) Экспорт
	
	Если ТекущийОбъект <> Неопределено Тогда
		Форма.АдресНастроекВнешнейОбработки = ПоместитьВоВременноеХранилище(ТекущийОбъект.ПараметрыВнешнейОбработки.Получить(), Форма.УникальныйИдентификатор);
		Настройки = ТекущийОбъект.ХранилищеНастроекКомпоновкиДанных.Получить();
	Иначе
		Если Форма.Параметры.Свойство("ЗначениеКопирования") И ЗначениеЗаполнено(Форма.Параметры.ЗначениеКопирования) Тогда
			ОбъектКопирования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Форма.Параметры.ЗначениеКопирования, "ПараметрыВнешнейОбработки,ХранилищеНастроекКомпоновкиДанных");
			ПараметрыВнешнейОбработки = ОбъектКопирования.ПараметрыВнешнейОбработки; // ХранилищеЗначения
			Форма.АдресНастроекВнешнейОбработки = ПоместитьВоВременноеХранилище(ПараметрыВнешнейОбработки.Получить(), Форма.УникальныйИдентификатор);
			ХранилищеНастроекКомпоновкиДанных = ОбъектКопирования.ХранилищеНастроекКомпоновкиДанных; // ХранилищеЗначения
			Настройки = ХранилищеНастроекКомпоновкиДанных.Получить();
		Иначе
			Форма.АдресНастроекВнешнейОбработки = ПоместитьВоВременноеХранилище(Неопределено, Форма.УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Форма.Объект.Ссылка) = Тип("СправочникСсылка.УсловияПредоставленияСкидокНаценок") Тогда
		
		Для Каждого РеквизитСправочника Из Метаданные.Справочники.УсловияПредоставленияСкидокНаценок.Реквизиты Цикл
			
			ЗначениеЗаполнения = РеквизитСправочника.ЗначениеЗаполнения;
			Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(Форма.Объект, РеквизитСправочника.Имя)
				И Не ЗначениеЗаполнено(Форма.Объект[РеквизитСправочника.Имя]) Тогда
				Форма.Объект[РеквизитСправочника.Имя] = ЗначениеЗаполнения;
			КонецЕсли;
			
			Если РеквизитСправочника = Метаданные.Справочники.УсловияПредоставленияСкидокНаценок.Реквизиты.ВалютаОграничения
				И Не ЗначениеЗаполнено(Форма.Объект.ВалютаОграничения) Тогда
				Форма.Объект.ВалютаОграничения = ДоходыИРасходыСервер.ПолучитьВалютуУправленческогоУчета();
			КонецЕсли;
			
		КонецЦикла;
		
		СкидкиНаценкиКлиентСервер.УсловиеПредоставленияПриИзмененииНаСервере(Форма);
		СкидкиНаценкиКлиентСервер.КритерийПримененияЗаОбъемПродажПриИзмененииНаСервере(Форма);
		СкидкиНаценкиКлиентСервер.ВариантОпределенияПериодаНакопительнойСкидкиПриИзмененииНаСервере(Форма);
		
		Если Не ЗначениеЗаполнено(Форма.Объект.Ссылка) Тогда
			Форма.Объект.Наименование = СкидкиНаценкиКлиентСервер.АвтоНаименованиеУсловияПредоставленияСкидки(Форма);
		Иначе
			СкидкиНаценкиКлиентСервер.АвтоНаименованиеУсловияПредоставленияСкидки(Форма);
		КонецЕсли;
		СкидкиНаценкиКлиентСервер.ОбновитьРеквизитыАвтонаименования(Форма);
		
		ЭлементУсловияПредоставления = Форма.Элементы.УсловиеПредоставления; // РасширениеПоляФормыДляПоляВвода
		
		Для Каждого ЭлементСписка Из ДоступныеУсловияПредоставленияСкидокНаценок() Цикл
			ЭлементУсловияПредоставления.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
		КонецЦикла;
		
		ИспользоватьСегментыНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСегментыНоменклатуры");
		ЭлементыВариантОтбораНоменклатуры = Новый Массив;
		ЭлементыВариантОтбораНоменклатуры.Добавить("ГруппаВариантОтбораНоменклатурыЗаНакопленныйОбъемПродажСегментНоменклатуры");
		ЭлементыВариантОтбораНоменклатуры.Добавить("ГруппаВариантОтбораНоменклатурыСегментНоменклатуры");
		Для Каждого ИмяЭлемента Из ЭлементыВариантОтбораНоменклатуры Цикл
			Форма.Элементы[ИмяЭлемента].Видимость = ИспользоватьСегментыНоменклатуры;
		КонецЦикла;
		
		Форма.Элементы.ФормаОплаты.СписокВыбора.Очистить();
		Форма.Элементы.ФормаОплаты.СписокВыбора.Добавить(Перечисления.ФормыОплаты.Наличная);
		Форма.Элементы.ФормаОплаты.СписокВыбора.Добавить(Перечисления.ФормыОплаты.Безналичная);
		Если ПолучитьФункциональнуюОпцию("ИспользоватьОплатуПлатежнымиКартами") Тогда
			Форма.Элементы.ФормаОплаты.СписокВыбора.Добавить(Перечисления.ФормыОплаты.ПлатежнаяКарта);
		КонецЕсли;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьБонусныеПрограммыЛояльности") Тогда
			Форма.Элементы.ФормаОплаты.СписокВыбора.Добавить(Перечисления.ФормыОплаты.БонусныеБаллы);
		КонецЕсли;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьПодарочныеСертификаты") Тогда
			Форма.Элементы.ФормаОплаты.СписокВыбора.Добавить(Перечисления.ФормыОплаты.ПодарочныйСертификат);
		КонецЕсли;
	
	КонецЕсли;
	
	Если ТипЗнч(Форма.Объект.Ссылка) = Тип("СправочникСсылка.СкидкиНаценки") Тогда
		
		Форма.Элементы.ВариантОтбораНоменклатуры.СписокВыбора.Очистить();
		Форма.Элементы.ВариантОтбораНоменклатуры.СписокВыбора.Добавить(
			Перечисления.ВариантыОтбораНоменклатурыДляРасчетаСкидокНаценок.БезОграничений,
			НСтр("ru = 'Любую номенклатуру'"));
		Форма.Элементы.ВариантОтбораНоменклатуры.СписокВыбора.Добавить(
			Перечисления.ВариантыОтбораНоменклатурыДляРасчетаСкидокНаценок.СписокНоменклатуры,
			НСтр("ru = 'Номенклатуру из списка'"));
		Если ПолучитьФункциональнуюОпцию("ИспользоватьСегментыНоменклатуры") Тогда
			Форма.Элементы.ВариантОтбораНоменклатуры.СписокВыбора.Добавить(
				Перечисления.ВариантыОтбораНоменклатурыДляРасчетаСкидокНаценок.СегментНоменклатуры,
				НСтр("ru = 'Номенклатуру из сегмента'"));
			Форма.Элементы.ВариантОтбораНоменклатуры.СписокВыбора.Добавить(
				Перечисления.ВариантыОтбораНоменклатурыДляРасчетаСкидокНаценок.НеСегментНоменклатуры,
				НСтр("ru = 'Номенклатуру не из сегмента'"));
		КонецЕсли;
		Форма.Элементы.ВариантОтбораНоменклатуры.СписокВыбора.Добавить(
			Перечисления.ВариантыОтбораНоменклатурыДляРасчетаСкидокНаценок.ОтборКомпоновкиДанных,
			НСтр("ru = 'Номенклатуру по отбору'"));
		
	КонецЕсли;
	
	Если ТипЗнч(Форма.Объект.Ссылка) = Тип("СправочникСсылка.СкидкиНаценки") Тогда
		СхемаКомпоновкиДанных = Справочники.СкидкиНаценки.ПолучитьМакет("ОтборСтрокНоменклатура");
	Иначе
		СхемаКомпоновкиДанных = Справочники.СкидкиНаценки.ПолучитьМакет("ОтборУсловияПредоставленияСуммаКоличество");
	КонецЕсли;

	Форма.КомпоновщикНастроекОтборПоНоменклатуре.Инициализировать(
		Новый ИсточникДоступныхНастроекКомпоновкиДанных(
			ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, Форма.УникальныйИдентификатор)));
	Если ЗначениеЗаполнено(Настройки) И ТипЗнч(Настройки) = Тип("Структура") И Настройки.Свойство("ОтборПоНоменклатуре") Тогда
		Форма.КомпоновщикНастроекОтборПоНоменклатуре.ЗагрузитьНастройки(Настройки.ОтборПоНоменклатуре);
	Иначе
		Форма.КомпоновщикНастроекОтборПоНоменклатуре.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	КонецЕсли;
	Форма.КомпоновщикНастроекОтборПоНоменклатуре.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
	
	Если ТипЗнч(Форма.Объект.Ссылка) = Тип("СправочникСсылка.СкидкиНаценки") Тогда
		
		СхемаКомпоновкиДанных = Справочники.СкидкиНаценки.ПолучитьМакет("ОтборСтрокДополнительныеУсловия");
		Форма.КомпоновщикНастроекДополнительныеУсловия.Инициализировать(
			Новый ИсточникДоступныхНастроекКомпоновкиДанных(
				ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, Форма.УникальныйИдентификатор)));
		Если ЗначениеЗаполнено(Настройки) И ТипЗнч(Настройки) = Тип("Структура") И Настройки.Свойство("ДополнительныеУсловия") Тогда
			Форма.КомпоновщикНастроекДополнительныеУсловия.ЗагрузитьНастройки(Настройки.ДополнительныеУсловия);
		Иначе
			Форма.КомпоновщикНастроекДополнительныеУсловия.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
		КонецЕсли;
		Форма.КомпоновщикНастроекДополнительныеУсловия.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
		
	КонецЕсли;
	
	СкидкиНаценкиКлиентСервер.НастроитьГиперссылкуНаСписокНоменклатурныхПозиций(Форма);
	
	СкидкиНаценкиКлиентСервер.НастроитьГиперссылкиОтборов(Форма);
	
КонецПроцедуры

// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Форма, содержит в том числе:
// 		* КомандыУсловияПредоставления - ТаблицаЗначений - Таблица содержит:
// 			** ИмяКоманды - Строка - 
// 			** Значение - Произвольный - 
// 	ГруппаСоздать - ГруппаФормы 
// 
Процедура ДобавитьКомандыСозданияНовыхУсловийПредоставленияСкидокНаценок(Форма, ГруппаСоздать) Экспорт
	
	КомандыФормы = ДоступныеУсловияПредоставленияСкидокНаценок();
	
	Для Каждого ЭлементСписка Из КомандыФормы Цикл
		
		ИмяКоманды = "ПодключаемаяКоманда" + СтрЗаменить(Новый УникальныйИдентификатор, "-","");
		
		НоваяСтрока = Форма.КомандыУсловияПредоставления.Добавить();
		НоваяСтрока.ИмяКоманды = ИмяКоманды;
		НоваяСтрока.Значение = ЭлементСписка.Значение;
		
		КомандаФормы = Форма.Команды.Добавить(ИмяКоманды);
		КомандаФормы.Действие = "Подключаемый_ВыполнитьКоманду";
		КомандаФормы.Заголовок = ЭлементСписка.Представление;
		КомандаФормы.ИзменяетСохраняемыеДанные = Ложь;
		КомандаФормы.Отображение = ОтображениеКнопки.Авто;
		
		НовыйЭлемент = Форма.Элементы.Добавить("Кнопка"+ИмяКоманды, Тип("КнопкаФормы"), ГруппаСоздать);
		НовыйЭлемент.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
		НовыйЭлемент.ИмяКоманды = ИмяКоманды;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьКомандыСозданияНовыхСкидокНаценок(Форма, ГруппаСоздать) Экспорт
	
	Варианты = ДоступныеСпособыПредоставления();
	
	Для Каждого ЭлементСписка Из Варианты.СкидкиНаценки Цикл
		ДобавитьКомандуВГруппу(ЭлементСписка, Форма, ГруппаСоздать, "СкидкиНаценки");
	КонецЦикла;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьБонусныеПрограммыЛояльности") Тогда
		Для Каждого ЭлементСписка Из Варианты.БонусныеБаллы Цикл
			ДобавитьКомандуВГруппу(ЭлементСписка, Форма, ГруппаСоздать, "БонусныеБаллы");
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ФункцииПакетаЗапросов

// Конструктор пакета запросов.
//
// Возвращаемое значение:
//	Структура - пакет запросов. Состоит из:
//		* ОбщееКоличествоТаблиц - Число
//		* СоответствиеЗапросаИНомераТаблицыРезультата - Соответствие
//		* Запрос - Запрос
//		* Параметры - Соответствие
//		* РезультатЗапроса - РезультатЗапроса
//		* МассивИменЗапросов - Массив
//
Функция ПакетЗапросовСоздать()
	
	ПакетЗапросов = Новый Структура;
	ПакетЗапросов.Вставить("ОбщееКоличествоТаблиц", 0);
	ПакетЗапросов.Вставить("СоответствиеЗапросаИНомераТаблицыРезультата", Новый Соответствие);
	ПакетЗапросов.Вставить("Запрос", Новый Запрос);
	ПакетЗапросов.Вставить("Параметры", Новый Соответствие);
	ПакетЗапросов.Вставить("РезультатЗапроса", Неопределено);
	ПакетЗапросов.Вставить("МассивИменЗапросов", Новый Массив);
	
	Возврат ПакетЗапросов;
	
КонецФункции

Процедура УдалитьВременныеТаблицы(Запрос)
	
	УстановитьПривилегированныйРежим(Истина);
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
	ТаблицыДляУничтожения = Новый Массив;
	Для Индекс = 0 По СхемаЗапроса.ПакетЗапросов.Количество() - 1 Цикл
		ЗапросСхемы = СхемаЗапроса.ПакетЗапросов.Получить(Индекс);
		Если ТипЗнч(ЗапросСхемы) = Тип("ЗапросВыбораСхемыЗапроса") Тогда
			Если ЗначениеЗаполнено(ЗапросСхемы.ТаблицаДляПомещения) Тогда
				ТаблицыДляУничтожения.Добавить(ЗапросСхемы.ТаблицаДляПомещения);
			КонецЕсли;
		ИначеЕсли ТипЗнч(ЗапросСхемы) = Тип("ЗапросУничтоженияТаблицыСхемыЗапроса") Тогда
			Индекс = ТаблицыДляУничтожения.Найти(ЗапросСхемы.ИмяТаблицы);
			Если Индекс <> Неопределено Тогда
				ТаблицыДляУничтожения.Удалить(Индекс);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Для Каждого ИмяТаблицы Из ТаблицыДляУничтожения Цикл
		Запрос.Текст = Запрос.Текст + СтрЗаменить(
			ОбщегоНазначения.РазделительПакетаЗапросов() + "УНИЧТОЖИТЬ ИмяТаблицы", "ИмяТаблицы", ИмяТаблицы) + Символы.ПС;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Добавляет запрос в пакет запросов.
//
Процедура ПакетЗапросовВставитьЗапросВПакет(Запрос,
	                                        ПакетЗапросов,
	                                        Ключ,
	                                        УдалятьВременныеТаблицы,
	                                        ПреобразовыватьПараметры,
	                                        КоличествоЗапросовВПакете = Неопределено,
	                                        ИндексЗапросаРезультата = Неопределено)
	
	// Проверка на дубли запросов.
	Если Ключ <> Неопределено
		И ПакетЗапросов.МассивИменЗапросов.Найти(Ключ) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПреобразовыватьПараметры Тогда
		Для Каждого Параметр Из Запрос.Параметры Цикл
			НовоеИмя = ПакетЗапросовПреобразоватьПараметр(Запрос.Текст, Параметр.Ключ, Параметр.Значение, ПакетЗапросов);
			Если НовоеИмя <> Неопределено Тогда
				ПакетЗапросов.Запрос.УстановитьПараметр(НовоеИмя, Параметр.Значение);
				ПакетЗапросов.Параметры.Вставить(Параметр.Значение, НовоеИмя);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если УдалятьВременныеТаблицы Тогда
		УдалитьВременныеТаблицы(Запрос);
	КонецЕсли;
	
	Если КоличествоЗапросовВПакете = Неопределено ИЛИ ИндексЗапросаРезультата = Неопределено Тогда
		Если СтрЧислоВхождений(Запрос.Текст, ";") > 0 Тогда
			Подзапросы = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Запрос.Текст, ";");
			КоличествоЗапросовВПакете = Подзапросы.Количество();
			ИндексЗапросаРезультата = 0;
			Для Индекс = -(КоличествоЗапросовВПакете - 1) По 0 Цикл
				Если Найти(Подзапросы[-Индекс], "#123456789#") > 0 Тогда
					ИндексЗапросаРезультата = -Индекс + 1;
				КонецЕсли;
			КонецЦикла;
		Иначе
			КоличествоЗапросовВПакете = 1;
			ИндексЗапросаРезультата = 1;
		КонецЕсли;
	КонецЕсли;
	
	ПакетЗапросов.ОбщееКоличествоТаблиц = ПакетЗапросов.ОбщееКоличествоТаблиц + КоличествоЗапросовВПакете;
	
	Если ПакетЗапросов.ОбщееКоличествоТаблиц = КоличествоЗапросовВПакете Тогда
		ПакетЗапросов.Запрос.Текст = Запрос.Текст;
	Иначе
		ПакетЗапросов.Запрос.Текст = ПакетЗапросов.Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() + Запрос.Текст;
	КонецЕсли;
	
	Если Ключ <> Неопределено Тогда
		НомерТаблицы = ПакетЗапросов.ОбщееКоличествоТаблиц - КоличествоЗапросовВПакете + ИндексЗапросаРезультата;
		ПакетЗапросов.СоответствиеЗапросаИНомераТаблицыРезультата.Вставить(Ключ, НомерТаблицы);
		ПакетЗапросов.МассивИменЗапросов.Добавить(Ключ);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет пакет запросов.
//
// Возвращаемое значение:
//	Булево - Истина, если запрос выполнен успешно.
//
Функция ПакетЗапросовВыполнить(ПакетЗапросов)
	
	Если ЗначениеЗаполнено(ПакетЗапросов.Запрос.Текст) Тогда
		ПакетЗапросов.РезультатЗапроса = ПакетЗапросов.Запрос.ВыполнитьПакет();
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Получает результат запроса по имени из пакета запросов.
//
// Возвращаемое значение:
//	РезультатЗапроса - Результат запроса входящего в пакет.
//
Функция ПакетЗапросовРезультатПоИмени(ИмяЗапроса, ПакетЗапросов)
	
	Возврат ПакетЗапросов.РезультатЗапроса[ПакетЗапросов.СоответствиеЗапросаИНомераТаблицыРезультата[ИмяЗапроса] - 1];
	
КонецФункции

Функция ПакетЗапросовПостфиксы()
	
	Постфиксы = Новый Массив;
	Постфиксы.Добавить(Символы.ПС);
	Постфиксы.Добавить(" ");
	Постфиксы.Добавить(",");
	Постфиксы.Добавить(".");
	Постфиксы.Добавить(")");
	Постфиксы.Добавить(Символы.Таб);
	Постфиксы.Добавить("(");
	Постфиксы.Добавить("+");
	Постфиксы.Добавить("-");
	Постфиксы.Добавить("*");
	Постфиксы.Добавить("/");
	
	Возврат Постфиксы;
	
КонецФункции

Функция ЗаменитьИмяПараметраВЗапросе(ТекстЗапроса, ИсходноеИмя, НовоеИмя, Постфиксы)
	
	Для Каждого Постфикс Из Постфиксы Цикл
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&" + ИсходноеИмя + Постфикс, "&" + НовоеИмя + Постфикс);
	КонецЦикла;

КонецФункции

// Преобразует параметр запроса (Изменяет имя или устраняет необходимость использования параметра).
// Используется для объединения запросов с одинаковыми параметрами.
//
// Параметры:
// 	ТекстЗапроса - Строка - 
// 	ИсходноеИмя - Строка - 
// 	ЗначениеПараметра - ПеречислениеСсылка - 
// 	ПакетЗапросов - ПакетЗапросовСхемыЗапроса -
// 	 
// Возвращаемое значение:
//	Строка - Новое имя параметра.
//
Функция ПакетЗапросовПреобразоватьПараметр(ТекстЗапроса, ИсходноеИмя, ЗначениеПараметра, ПакетЗапросов = Неопределено)
	
	Постфиксы = ПакетЗапросовПостфиксы();
	
	ТипЗнч = ТипЗнч(ЗначениеПараметра);
	
	НовоеИмя = Неопределено;
	
	Если ТипЗнч = Тип("Дата") Тогда
		Текст = "ДатаВремя("+Формат(Год(ЗначениеПараметра),"ЧН=0; ЧГ=0")+","+Месяц(ЗначениеПараметра)+","+День(ЗначениеПараметра)+","+Час(ЗначениеПараметра)+","+Минута(ЗначениеПараметра)+","+Секунда(ЗначениеПараметра)+")";
		Для Каждого Постфикс Из Постфиксы Цикл
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&" + ИсходноеИмя + Постфикс, Текст + Постфикс);
		КонецЦикла;
	ИначеЕсли ТипЗнч = Тип("Число") Тогда
		Текст = Формат(ЗначениеПараметра, "ЧРД=.; ЧН=; ЧГ=0");
		Для Каждого Постфикс Из Постфиксы Цикл
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&" + ИсходноеИмя + Постфикс, Текст + Постфикс);
		КонецЦикла;
	ИначеЕсли ТипЗнч = Тип("Булево") Тогда
		Текст = ?(ЗначениеПараметра, "ИСТИНА", "ЛОЖЬ");
		Для Каждого Постфикс Из Постфиксы Цикл
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&" + ИсходноеИмя + Постфикс, Текст + Постфикс);
		КонецЦикла;
	ИначеЕсли ЗначениеПараметра = "" Тогда
		Для Каждого Постфикс Из Постфиксы Цикл
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&" + ИсходноеИмя + Постфикс, Постфикс);
		КонецЦикла;
	ИначеЕсли ЗначениеЗаполнено(ЗначениеПараметра) И Перечисления.ТипВсеСсылки().СодержитТип(ТипЗнч) Тогда
		
		МетаданныеПараметра = ЗначениеПараметра.Метаданные(); // ОбъектМетаданных
		ИмяПеречисления = МетаданныеПараметра.Имя;
		ИмяЗначения = Метаданные.Перечисления[ИмяПеречисления].ЗначенияПеречисления[Перечисления[ИмяПеречисления].Индекс(ЗначениеПараметра)].Имя;
		
		Текст = "ЗНАЧЕНИЕ(Перечисление." + ИмяПеречисления + "." + ИмяЗначения + ")";
		Для Каждого Постфикс Из Постфиксы Цикл
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&" + ИсходноеИмя + Постфикс, Текст + Постфикс);
		КонецЦикла;
		
	Иначе
		
		Если ПакетЗапросов <> Неопределено Тогда
			Ключ = ПакетЗапросов.Параметры.Получить(ЗначениеПараметра);
		КонецЕсли;
		Если Ключ = Неопределено Тогда
			
			Если ПакетЗапросов <> Неопределено И ПакетЗапросов.Запрос.Параметры.Свойство(ИсходноеИмя) Тогда
			
				НовоеИмя = "П" + СтрЗаменить(Новый УникальныйИдентификатор,"-","_");
				ЗаменитьИмяПараметраВЗапросе(ТекстЗапроса, ИсходноеИмя, НовоеИмя, Постфиксы);
				
			Иначе
				
				НовоеИмя = ИсходноеИмя;
				
			КонецЕсли;
			
		Иначе
			
			ЗаменитьИмяПараметраВЗапросе(ТекстЗапроса, ИсходноеИмя, Ключ, Постфиксы);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат НовоеИмя;
	
КонецФункции

#КонецОбласти

#Область Прочее

// Возвращает пустую таблицу значений для хранения результата рассчитанных скидок (наценок).
//
// Возвращаемое значение:
//	ТаблицаЗначений - Таблица, содержит:
//		* КлючСвязи   - Число
//		* Расшифровка - ТаблицаЗначений
//		* Сумма       - Число
//		* Действует   - Булево
//
Функция ПустаяТаблицаСкидокСРасшифровкой(ПараметрыРасчета = Неопределено) Экспорт
	
	Если ПараметрыРасчета = Неопределено Тогда
		Таблица = Новый ТаблицаЗначений;
		Таблица.Колонки.Добавить("КлючСвязи",   Новый ОписаниеТипов("Число"));
		Таблица.Колонки.Добавить("Расшифровка", Новый ОписаниеТипов("ТаблицаЗначений"));
		Таблица.Колонки.Добавить("Сумма",       Новый ОписаниеТипов("Число"));
		Таблица.Колонки.Добавить("Действует",   Новый ОписаниеТипов("Булево"));
		Возврат Таблица;
	Иначе
		Возврат ПараметрыРасчета.ПустаяТаблицаСкидокСРасшифровкой.СкопироватьКолонки();
	КонецЕсли;
	
КонецФункции

// Возвращает пустую таблицу значений для хранения расшифровок.
// 
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблица, содержит:
// * СкидкаНаценка - СправочникСсылка.СкидкиНаценки
// * Сумма - Число
// * ЭтоСообщение - Булево
// 
Функция ПустаяТаблицаРасшифровка(ПараметрыРасчета = Неопределено)
	
	Если ПараметрыРасчета = Неопределено Тогда
		Таблица = Новый ТаблицаЗначений;
		Таблица.Колонки.Добавить("СкидкаНаценка", Новый ОписаниеТипов("СправочникСсылка.СкидкиНаценки"));
		Таблица.Колонки.Добавить("Сумма",         Новый ОписаниеТипов("Число"));
		Таблица.Колонки.Добавить("ЭтоСообщение",  Новый ОписаниеТипов("Булево"));
		Возврат Таблица;
	Иначе
		Возврат ПараметрыРасчета.ПустаяТаблицаРасшифровка.СкопироватьКолонки();
	КонецЕсли;
	
КонецФункции

// Возвращает пустую таблицу значений для хранения расшифровок.
// 
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблица, содержит:
// * КлючСвязи - Число
// * СкидкаНаценка - СправочникСсылка.СкидкиНаценки
// * Сумма - Число
//
Функция ПустаяТаблицаСкидкиНаценки(ПараметрыРасчета = Неопределено)
	
	Если ПараметрыРасчета = Неопределено Тогда
		Таблица = Новый ТаблицаЗначений;
		Таблица.Колонки.Добавить("КлючСвязи",     Новый ОписаниеТипов("Число"));
		Таблица.Колонки.Добавить("СкидкаНаценка", Новый ОписаниеТипов("СправочникСсылка.СкидкиНаценки"));
		Таблица.Колонки.Добавить("Сумма",         Новый ОписаниеТипов("Число"));
		Возврат Таблица;
	Иначе
		Возврат ПараметрыРасчета.ПустаяТаблицаСкидкиНаценки.СкопироватьКолонки();
	КонецЕсли;
	
КонецФункции

// Возвращает пустую таблицу значений для результатов проверки условий.
// 
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблица, содержит:
// * Ссылка - СправочникСсылка.УсловияПредоставленияСкидокНаценок, СправочникСсылка.СкидкиНаценки -
// * КлючСвязи - Число
// * КратностьВыполнения - Число
// * ЗначениеПоказателя - 
// 
Функция ПустаяТаблицаРезультатПроверкиУсловия()
	
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Ссылка",              Новый ОписаниеТипов("СправочникСсылка.УсловияПредоставленияСкидокНаценок,СправочникСсылка.СкидкиНаценки"));
	ТЗ.Колонки.Добавить("КлючСвязи",           Новый ОписаниеТипов("Число"));
	ТЗ.Колонки.Добавить("КратностьВыполнения", Новый ОписаниеТипов("Число"));
	ТЗ.Колонки.Добавить("ЗначениеПоказателя");
	
	Возврат ТЗ;
	
КонецФункции

// Возвращает пустую таблицу уточнений для скидок/наценок.
// 
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблица, содержит:
// * Ключ - Число - 
// * Значение - Число -
// 
Функция ПустаяТаблицаУточненияЗначенияСкидкиНаценки()
	
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Ключ");
	ТЗ.Колонки.Добавить("Значение", Новый ОписаниеТипов("Число"));
	
	Возврат ТЗ;
	
КонецФункции


Функция УдалитьЛишниеСтрокиДереваСкидокРекурсивно(ДеревоСкидок)
	
	Для Каждого СтрокаДерева Из ДеревоСкидок.Строки Цикл
		
		Если СтрокаДерева.ЭтоГруппа Тогда
			
			УдалитьЛишниеСтрокиДереваСкидокРекурсивно(СтрокаДерева);
			
		Иначе
			
			Если СтрокаДерева.Строки.Количество() > 0 Тогда
				ЗаполнитьЗначенияСвойств(СтрокаДерева, СтрокаДерева.Строки[0]);
				СтрокаДерева.Строки.Удалить(СтрокаДерева.Строки[0]);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

// Формирует дерево скидок/наценок
// 
// Возвращаемое значение:
// 	ДеревоЗначений - содержит:
// 		* Ссылка - СправочникСсылка.СкидкиНаценки
// 		* РеквизитДопУпорядочивания - Число - Порядок применения
// 		* ВариантСовместногоПрименения - ПеречислениеСсылка.ВариантыСовместногоПримененияСкидокНаценок - Вариант совместного применения
// 		* ВариантРасчетаРезультатаСовместногоПрименения - ПеречислениеСсылка.ВариантыРасчетаРезультатаСовместногоПрименения
// 			Вариант расчета результата совместного применения
// 		* ПометкаУдаления - Булево - Пометка на удаление элемента
// 		* СпособПредоставления - СправочникСсылка.ДополнительныеОтчетыИОбработки, ПеречислениеСсылка.СпособыПредоставленияСкидокНаценок - 
// 			Определяет способ, в соответствии с которым предоставляется скидка (наценка)
// 		* СпособПримененияСкидки - ПеречислениеСсылка.СпособыПримененияСкидокНаценок - Способ применения скидки
// 		* ПрименятьУмножениеВРамкахВышестоящейГруппы - Булево - Применять умножение в рамках вышестоящей группы
// 		* ЗначениеСкидкиНаценки - ОпределяемыйТип.ДенежнаяСуммаЛюбогоЗнака - Значение скидки наценки
// 		* ТочностьОкругления - ОпределяемыйТип.ДенежнаяСуммаНеотрицательная - Точность округления
// 		* ВариантОкругления - ПеречислениеСсылка.ВариантыОкругления - Вариант округления
// 		* ПсихологическоеОкругление - ОпределяемыйТип.ДенежнаяСуммаНеотрицательная - Психологическое округление
// 		* ЭтоГруппа - Булево
// 		* Управляемая - Булево - Назначается вручную
// 		* НазначенаПользователем - Булево - Назначена пользователем
// 		* ВидЦены - СправочникСсылка.ВидыЦен - Вид цены
// 		* СегментПодарков - СправочникСсылка.СегментыНоменклатуры - Сегмент подарков
// 		* СегментНоменклатурыОграничения - СправочникСсылка.СегментыНоменклатуры - Сегмент номенклатуры ограничения
// 		* ИспользоватьКратность - Булево - Использовать кратность
// 		* УсловиеДляСкидкиКоличеством - Число - Условие для скидки количеством
// 		* ВариантОтбораНоменклатуры - ПеречислениеСсылка.ВариантыОтбораНоменклатурыДляРасчетаСкидокНаценок
// 			Вариант отбора номенклатуры
// 		* ПараметрыВнешнейОбработки - ХранилищеЗначения - Параметры внешней обработки
// 		* УстановленДополнительныйОтбор - Булево - Установлен дополнительный отбор
// 		* ХранилищеНастроекКомпоновкиДанных - ХранилищеЗначения - Хранилище настроек компоновки данных
// 		* УчитыватьХарактеристики - Булево - Учитывать характеристики
// 		* ВалютаКонвертацииБонусов - СправочникСсылка.Валюты - Валюта конвертации бонусов
// 		* КурсКонвертацииБонусовВВалюту - Число - Курс конвертации бонусов в валюту
// 		* ВалютаПредоставления - СправочникСсылка.Валюты - Валюта предоставления
// 		* УсловияПредоставления - ТаблицаЗначений
// 		* РезультатРасчета - см. ПустаяТаблицаСкидокСРасшифровкой
// 		* ПараметрыУсловий - Структура - Содержит в том числе:
// 			** УсловияВыполнены - Булево
// 			** КодыСтрок - Массив
// 			** УсловияПоСтроке - Структура - Содержит в том числе:
// 				*** ТаблицаПроверкиУсловий - ТаблицаЗначений
// 				*** СоответствиеУсловийКолонкамТаблицыПроверкиУсловий - Соответствие
// 			** ЕстьУсловияПоСтроке - Булево
// 			** ТаблицаУсловий - ТаблицаЗначений
// 
Функция СформироватьДеревоСкидок(Данные)
	
	Если ТипЗнч(Данные) = Тип("Структура") Тогда
		
		ДеревоСкидок = ПакетЗапросовРезультатПоИмени("СкидкиНаценки", Данные).Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		
	Иначе
		
		ПараметрыРасчета = Новый Структура;
		ПараметрыРасчета.Вставить("СкидкиНаценки", Данные);
		ПараметрыРасчета.Вставить("УправляемыеСкидки", Новый Массив);
		
		Запрос = ЗапросСкидкиНаценки(ПараметрыРасчета, Ложь);
		
		ДеревоСкидок = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		
	КонецЕсли;
	
	УдалитьЛишниеСтрокиДереваСкидокРекурсивно(ДеревоСкидок);
	
	Возврат ДеревоСкидок;
	
КонецФункции

// Возвращаемое значение:
// 	Структура - содержит:
// * Объект - Неопределено
// * Пользователь - СправочникСсылка.Пользователи
// * ВалютаУправленческогоУчета - СправочникСсылка.Валюты
// * ВалютаДокумента - СправочникСсылка.Валюты
// * Товары - Массив, ТаблицаЗначений -
// * КартаЛояльности - СправочникСсылка.КартыЛояльности
// * ГрафикОплаты - СправочникСсылка.ГрафикиОплаты
// * ТекущаяДата - Дата
// * ТекущееВремя - Дата
// * ДеньНедели - ПеречислениеСсылка.ДниНедели
// * ФормаОплаты - ПеречислениеСсылка.ФормыОплаты
// * Регистратор - ДокументСсылка.РеализацияТоваровУслуг
// * Соглашение - СправочникСсылка.СоглашенияСКлиентами
// * Партнер - СправочникСсылка.Партнеры
// * СкидкиНаценки - Массив из СправочникСсылка.СкидкиНаценки
// 
Функция ПараметрыРасчета() Экспорт
	
	ПараметрыРасчета = Новый Структура;
	ПараметрыРасчета.Вставить("СкидкиНаценки", Новый Массив);
	ПараметрыРасчета.Вставить("Партнер", Справочники.Партнеры.ПустаяСсылка());
	ПараметрыРасчета.Вставить("Соглашение", Справочники.СоглашенияСКлиентами.ПустаяСсылка());
	ПараметрыРасчета.Вставить("Регистратор", Документы.РеализацияТоваровУслуг.ПустаяСсылка());
	ПараметрыРасчета.Вставить("БазоваяВалюта", Справочники.Валюты.ПустаяСсылка());
	
	// Для скидки "За форму оплаты".
	ПараметрыРасчета.Вставить("ФормаОплаты", Перечисления.ФормыОплаты.ПустаяСсылка());
	
	// Для скидки "За время продажи".
	ПараметрыРасчета.Вставить("ДеньНедели", Перечисления.ДниНедели.ПустаяСсылка());
	ПараметрыРасчета.Вставить("ТекущееВремя", '00010101');
	ПараметрыРасчета.Вставить("ТекущаяДата", '00010101');
	
	// Для скидки "За соблюдение графика оплаты".
	ПараметрыРасчета.Вставить("ГрафикОплаты", Справочники.ГрафикиОплаты.ПустаяСсылка());
	
	// Карты лояльности
	ПараметрыРасчета.Вставить("КартаЛояльности", Справочники.КартыЛояльности.ПустаяСсылка());
	
	ПараметрыРасчета.Вставить("Товары", Новый Массив);
	ПараметрыРасчета.Вставить("ВалютаДокумента", Справочники.Валюты.ПустаяСсылка());
	ПараметрыРасчета.Вставить("ВалютаУправленческогоУчета", Справочники.Валюты.ПустаяСсылка());
	ПараметрыРасчета.Вставить("Пользователь", Справочники.Пользователи.ПустаяСсылка());
	ПараметрыРасчета.Вставить("Объект", Неопределено);
	
	Возврат ПараметрыРасчета;
	
КонецФункции

Функция НачалоДекады(Дата)
	
	Если День(Дата) < 11 Тогда
		
		Возврат НачалоМесяца(Дата);
		
	ИначеЕсли День(Дата) < 21 Тогда
		
		Возврат НачалоМесяца(Дата) + 86400 * 10;
		
	Иначе
		
		Возврат НачалоМесяца(Дата) + 86400 * 20;
		
	КонецЕсли;
	
КонецФункции

Функция НачалоПолугодия(Дата)
	
	Если Месяц(Дата) < 6 Тогда
		Возврат НачалоГода(Дата)
	Иначе
		Возврат ОбщегоНазначенияУТКлиентСервер.РассчитатьДатуОкончанияПериода(
			НачалоГода(Дата),
			Перечисления.Периодичность.Полугодие,
			1) + 1;
	КонецЕсли;
	
КонецФункции

// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Форма, содержит в том числе:
// 		* КомандыУсловияПредоставления - ТаблицаЗначений - Таблица содержит:
// 			** ИмяКоманды - Строка - 
// 			** Значение - Произвольный -
// 
Процедура ДобавитьКомандуВГруппу(ЭлементСписка, Форма, ГруппаСоздать, Ключ)
	
	ИмяКоманды = "ПодключаемаяКоманда" + Ключ + (ГруппаСоздать.ПодчиненныеЭлементы.Количество() + 1);
	
	НоваяСтрока = Форма.КомандыУсловияПредоставления.Добавить();
	НоваяСтрока.ИмяКоманды = ИмяКоманды;
	НоваяСтрока.Значение = ЭлементСписка.Значение;
	
	КомандаФормы = Форма.Команды.Добавить(ИмяКоманды);
	КомандаФормы.Действие = "Подключаемый_ВыполнитьКомандуДобавленияСкидкиНаценки";
	КомандаФормы.Заголовок = ЭлементСписка.Представление;
	КомандаФормы.ИзменяетСохраняемыеДанные = Ложь;
	КомандаФормы.Отображение = ОтображениеКнопки.Авто;
	
	НовыйЭлемент = Форма.Элементы.Добавить("Кнопка"+ИмяКоманды, Тип("КнопкаФормы"), ГруппаСоздать);
	НовыйЭлемент.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	НовыйЭлемент.ИмяКоманды = ИмяКоманды;
	
КонецПроцедуры

Функция ИмяВременнойТаблицыНакопленныйОбъемПродаж(УсловиеПредоставления, Интервал)
	
	Если УсловиеПредоставления.ВариантНакопления = Перечисления.ВариантыНакопленияКумулятивнойСкидкиНаценки.ПоПартнеру Тогда
		ИмяВременнойТаблицы = "НакопленныйОбъемПродажПоПартнеруЗаПериод";
	ИначеЕсли УсловиеПредоставления.ВариантНакопления = Перечисления.ВариантыНакопленияКумулятивнойСкидкиНаценки.ПоТорговомуСоглашению Тогда
		ИмяВременнойТаблицы = "НакопленныйОбъемПродажПоТорговомуСоглашениюЗаПериод";
	КонецЕсли;
	
	ПредставлениеДатаНачала    = Формат(Интервал.ДатаНачала, "ДФ=ddMMyyyy; ДП=");
	Если ПустаяСтрока(ПредставлениеДатаНачала) Тогда
		ПредставлениеДатаНачала = "01010001";
	КонецЕсли;
	ПредставлениеДатаОкончания = Формат(Интервал.ДатаОкончания, "ДФ=ddMMyyyy; ДП=");
	Если ПустаяСтрока(ПредставлениеДатаОкончания) Тогда
		ПредставлениеДатаОкончания = "01010001";
	КонецЕсли;
	
	ИмяВременнойТаблицы = ИмяВременнойТаблицы
	                    + ПредставлениеДатаНачала
	                    + ПредставлениеДатаОкончания
	                    + СокрЛП(УсловиеПредоставления.ЦифровойКодВалютыОграничения)
	                    + ?(УсловиеПредоставления.ВключатьТекущуюПродажуВНакопленныйОбъемПродаж, "ВключаяТекущуюПродажу", "ИсключаяТекущуюПродажу");
	
	Возврат ИмяВременнойТаблицы;
	
КонецФункции

#КонецОбласти

#КонецОбласти