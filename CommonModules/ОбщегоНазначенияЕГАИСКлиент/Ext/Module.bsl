#Область ПрограммныйИнтерфейс

//см. ОбщегоНазначенияКлиентПереопределяемый.ПередПериодическойОтправкойДанныхКлиентаНаСервер
Процедура ПередПериодическойОтправкойДанныхКлиентаНаСервер(Параметры) Экспорт
	
	Если Не СтандартныеПодсистемыКлиент.ПараметрКлиента("ДоступноИспользованиеРазделенныхДанных") Тогда
		Возврат;
	КонецЕсли;
	
	ИменаПараметров    = ОбщегоНазначенияЕГАИСКлиентСервер.ИменаПараметровПериодическогоПолученияДанных();
	ПараметрПриложения = ПараметрыПриложения[ИменаПараметров.ИмяПараметраПриложения]; // - см. ОбщегоНазначенияЕГАИСКлиентСервер.ИменаПараметровПериодическогоПолученияДанных
	
	Если ПараметрПриложения = Неопределено
		Или (Не ПараметрПриложения.ОбновитьПараметрыНастроек И Не ПараметрПриложения.НастроенАвтоматическийОбмен) Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеПараметра = ОбщегоНазначенияЕГАИСКлиентСервер.НовоеЗначениеПараметраПериодическойОтправкиДанныхНаСерверЗапрос();
	ЗначениеПараметра.ДатыПоследнегоЗапускаОбменаНаКлиентеПоРасписанию = ПараметрПриложения.ДатаПоследнейОбработкиОтветов;
	ЗначениеПараметра.НастроенАвтоматическийОбмен                      = ПараметрПриложения.НастроенАвтоматическийОбмен;
	
	Если ПараметрПриложения.ОбновитьПараметрыНастроек Тогда
		Параметры.Вставить(ИменаПараметров.ОбновитьПараметрыНастроек, ЗначениеПараметра);
	КонецЕсли;
	
	Если ПараметрПриложения.НастроенАвтоматическийОбмен
		И Не ПараметрПриложения.ЭтоВебКлиент
		И МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда
		Параметры.Вставить(ИменаПараметров.ВыполнитьОбменДанными, ЗначениеПараметра);
	КонецЕсли;
	
КонецПроцедуры

//см. ОбщегоНазначенияКлиентПереопределяемый.ПослеПериодическогоПолученияДанныхКлиентаНаСервере
Процедура ПослеПериодическогоПолученияДанныхКлиентаНаСервере(Результаты) Экспорт
	
	Если Не СтандартныеПодсистемыКлиент.ПараметрКлиента("ДоступноИспользованиеРазделенныхДанных") Тогда
		Возврат;
	КонецЕсли;
	
	ИменаПараметров                            = ОбщегоНазначенияЕГАИСКлиентСервер.ИменаПараметровПериодическогоПолученияДанных();
	ЗначениеПараметраОбновитьПараметрыНастроек = Результаты[ИменаПараметров.ОбновитьПараметрыНастроек]; // см. ОбщегоНазначенияЕГАИСКлиентСервер.НовоеЗначениеПараметраПериодическойОтправкиДанныхНаСерверОтветИнтеграцияЕГАИСКлиентСервер.НовоеЗначениеПараметраПериодическойОтправкиДанныхНаСерверОтвет
	ЗначениеПараметраВыполнитьОбменДанными     = Результаты[ИменаПараметров.ВыполнитьОбменДанными]; // см. ОбщегоНазначенияЕГАИСКлиентСервер.НовоеЗначениеПараметраПериодическойОтправкиДанныхНаСерверОтветИнтеграцияЕГАИСКлиентСервер.НовоеЗначениеПараметраПериодическойОтправкиДанныхНаСерверОтвет
	
	Если ЗначениеПараметраОбновитьПараметрыНастроек = Неопределено
		И ЗначениеПараметраВыполнитьОбменДанными = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеПараметраОбновитьПараметрыНастроек <> Неопределено Тогда
		
		ПараметрПриложения      = ПараметрыПриложения[ИменаПараметров.ИмяПараметраПриложения]; // - см. ОбщегоНазначенияЕГАИСКлиентСервер.ИменаПараметровПериодическогоПолученияДанных
		НовыйПараметрПриложения = ЗначениеПараметраОбновитьПараметрыНастроек.ПараметрПриложения;
		
		ПараметрПриложения.ВедетсяУчетАлкогольнойПродукции = НовыйПараметрПриложения.ВедетсяУчетАлкогольнойПродукции;
		ПараметрПриложения.НастроенАвтоматическийОбмен     = НовыйПараметрПриложения.НастроенАвтоматическийОбмен;
		ПараметрПриложения.ОбновитьПараметрыНастроек       = Ложь;
		ПараметрПриложения.ЭтоВебКлиент                    = НовыйПараметрПриложения.ЭтоВебКлиент;
		
		Если ЗначениеПараметраОбновитьПараметрыНастроек.ВыполнитьОбменНаКлиенте Тогда
			ОбменДаннымиЕГАИСКлиент.ВыполнитьОбменНаКлиентеПоРасписанию(ЗначениеПараметраОбновитьПараметрыНастроек.ДанныеДляВыполненияОбменаНаКлиенте);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеПараметраВыполнитьОбменДанными <> Неопределено Тогда
		
		Если ЗначениеПараметраВыполнитьОбменДанными.ВыполнитьОбменНаКлиенте Тогда
			ОбменДаннымиЕГАИСКлиент.ВыполнитьОбменНаКлиентеПоРасписанию(ЗначениеПараметраВыполнитьОбменДанными.ДанныеДляВыполненияОбменаНаКлиенте);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

//см. ОбщегоНазначенияКлиентПереопределяемый.ПослеНачалаРаботыСистемы
Процедура ПослеНачалаРаботыСистемы() Экспорт
	
	Если Не СтандартныеПодсистемыКлиент.ПараметрКлиента("ДоступноИспользованиеРазделенныхДанных") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрПриложения = ОбщегоНазначенияЕГАИСКлиентСервер.ПараметрПриложенияОбменНаКлиентеПоРасписанию();
	ИменаПараметров = ОбщегоНазначенияЕГАИСКлиентСервер.ИменаПараметровПериодическогоПолученияДанных();
	ПараметрыПриложения[ИменаПараметров.ИмяПараметраПриложения] = ПараметрПриложения;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЭтоРасширеннаяВерсияГосИС() Экспорт
	
	Возврат ОбщегоНазначенияИСКлиент.ЭтоРасширеннаяВерсияГосИС("ЕГАИС");
	
КонецФункции

#КонецОбласти