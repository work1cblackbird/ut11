///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.СверкаВзаиморасчетовСБПc2b".
// ОбщийМодуль.СверкаВзаиморасчетовСБПc2b.
//
// Клиентские процедуры сверки взаиморасчетов с участниками СБП:
//  - обработка событий отчетов.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Открывает форму списка документов "Сверка взаиморасчетов СБП (c2b)".
//
// Параметры:
//  Владелец - ФормаКлиентскогоПриложения - форма которая будет установлена в качестве владельца.
//
Процедура ОткрытьСверкиВзаиморасчетов(Владелец) Экспорт
	
	ОткрытьФорму(
		"Документ.СверкаВзаиморасчетовСБПc2b.ФормаСписка",
		,
		Владелец);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// См. ОтчетыКлиентПереопределяемый.ОбработкаВыбораТабличногоДокумента.
//
Процедура ПриОбработкеВыбораТабличногоДокумента(
		ФормаОтчета,
		Элемент,
		Область,
		СтандартнаяОбработка) Экспорт
	
	Если ТипЗнч(Область) = Тип("РисунокТабличногоДокумента")
		Или Область.ТипОбласти <> ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
		Возврат;
	КонецЕсли;
	
	Если Область.Расшифровка = "ПодключитьИнтернетПоддержкуПользователейСверкаОперацийСБПc2b" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ФормаОтчета", ФормаОтчета);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПослеПодключенияИнтернетПоддержкиПользователей",
			ЭтотОбъект,
			ДополнительныеПараметры);
		
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(
			ОписаниеОповещения,
			ФормаОтчета);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет формирование отчета после ввода данных аутентификации.
//
// Параметры:
//  Результат - Строка - результат ввода логина и пароля;
//  ДополнительныеПараметры - Структура - дополнительные параметры.
//
Процедура ПослеПодключенияИнтернетПоддержкиПользователей(
		Результат,
		ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ОтчетыКлиент.СформироватьОтчет(ДополнительныеПараметры.ФормаОтчета);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти