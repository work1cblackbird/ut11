//@strict-types

#Область СлужебныеПроцедурыИФункции

#Область ДляВызоваИзМодуляРегламентыЭДО

// Возвращает состояние входящего электронного документа.
// 
// Параметры:
//  ПараметрыДокумента - См. РегламентыЭДО.НовыеПараметрыДокументаДляОпределенияСостояния
//  СостоянияЭлементовРегламента - См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияДокументовЭДО
//
Функция СостояниеВходящегоДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента) Экспорт
	
	Состояние = Перечисления.СостоянияДокументовЭДО.ПустаяСсылка();
	
	Если ЗаполнитьСостояниеПоВходящейИнформацииОтправителя(ПараметрыДокумента, СостоянияЭлементовРегламента, Состояние)
		ИЛИ ЗначениеЗаполнено(Состояние) Тогда
		
		Возврат Состояние;
		
	ИначеЕсли ПараметрыДокумента.Исправлен Тогда
		
		Возврат Перечисления.СостоянияДокументовЭДО.ОбменЗавершенСИсправлением;
		
	КонецЕсли;
	
	Возврат Перечисления.СостоянияДокументовЭДО.ОбменЗавершен;
	
КонецФункции

// Возвращает состояние исходящего электронного документа.
// 
// Параметры:
//  ПараметрыДокумента - См. РегламентыЭДО.НовыеПараметрыДокументаДляОпределенияСостояния
//  СостоянияЭлементовРегламента - См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияДокументовЭДО
//
Функция СостояниеИсходящегоДокумента(ПараметрыДокумента, СостоянияЭлементовРегламента) Экспорт
	
	Состояние = Перечисления.СостоянияДокументовЭДО.ПустаяСсылка();
	
	Если ЗаполнитьСостояниеПоИсходящейИнформацииОтправителя(ПараметрыДокумента, СостоянияЭлементовРегламента, Состояние)
		Тогда
		
		Возврат Состояние;
		
	ИначеЕсли ПараметрыДокумента.Исправлен Тогда
		
		Возврат Перечисления.СостоянияДокументовЭДО.ОбменЗавершенСИсправлением;
		
	КонецЕсли;
	
	Возврат Перечисления.СостоянияДокументовЭДО.ОбменЗавершен;
	
КонецФункции

// Возвращает состояние сообщения.
//
// Параметры:
//  ПараметрыСообщения - См. РегламентыЭДО.НовыеПараметрыСообщенияДляОпределенияСостояния
//  ПараметрыДокумента - См. РегламентыЭДО.НовыеПараметрыДокументаДляОпределенияСостоянияСообщения
//  ИспользоватьУтверждение  - Булево
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияСообщенийЭДО
//
Функция СостояниеСообщения(ПараметрыСообщения, ПараметрыДокумента, ИспользоватьУтверждение) Экспорт
	
	Состояние = Перечисления.СостоянияСообщенийЭДО.ПустаяСсылка();
	
	Статус = ПараметрыСообщения.Статус;
	
	Если Статус = Перечисления.СтатусыСообщенийЭДО.Получен Тогда
		
		Состояние = Перечисления.СостоянияСообщенийЭДО.Хранение;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.Утвержден Тогда
		
		Состояние = Перечисления.СостоянияСообщенийЭДО.Хранение;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.Сформирован Тогда
		
		Если ПараметрыДокумента.ОбменБезПодписи Тогда
			Состояние = Перечисления.СостоянияСообщенийЭДО.ПодготовкаКОтправке;
		Иначе
			Состояние = Перечисления.СостоянияСообщенийЭДО.Подписание;
		КонецЕсли;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.ЧастичноПодписан Тогда
		
		Состояние = Перечисления.СостоянияСообщенийЭДО.Подписание;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.Подписан Тогда
		
		Состояние = Перечисления.СостоянияСообщенийЭДО.ПодготовкаКОтправке;
				
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.ПодготовленКОтправке Тогда
		
		Состояние = Перечисления.СостоянияСообщенийЭДО.Отправка;
		
	ИначеЕсли Статус = Перечисления.СтатусыСообщенийЭДО.Отправлен Тогда
		
		Состояние = Перечисления.СостоянияСообщенийЭДО.Хранение; 

	КонецЕсли;
	
	Возврат Состояние;
	
КонецФункции

// Возвращает коллекцию добавленных элементов схемы регламента.
// 
// Параметры:
//  СхемаРегламента - См. РегламентыЭДО.НоваяСхемаРегламента
//  НастройкиСхемыРегламента - См. РегламентыЭДО.НовыеНастройкиСхемыРегламента
//
// Возвращаемое значение:
//  См. РегламентыЭДО.НоваяКоллекцияЭлементовСхемыРегламента
//
Функция ДобавитьЭлементыСхемыРегламента(СхемаРегламента, НастройкиСхемыРегламента) Экспорт
	
	ЭлементыСхемы = РегламентыЭДО.НоваяКоллекцияЭлементовСхемыРегламента();
	
	ДобавитьЭлементыРегламентаОтправителя(СхемаРегламента, НастройкиСхемыРегламента, ЭлементыСхемы);
	
	Возврат ЭлементыСхемы;
	
КонецФункции

// Параметры:
//  СхемаРегламента - см. РегламентыЭДО.НоваяСхемаРегламента
//  НастройкиСхемыРегламента - см. РегламентыЭДО.НовыеНастройкиСхемыРегламента
//  ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
// 
// Возвращаемое значение:
//  См. РегламентыЭДО.НоваяКоллекцияЭлементовСхемыРегламента
Функция ДобавитьЭлементыСхемыВложенногоРегламента(СхемаРегламента, НастройкиСхемыРегламента, ТипЭлементаРегламента) Экспорт
	
	Возврат РегламентыЭДО.НоваяКоллекцияЭлементовСхемыРегламента();
	
КонецФункции

// Возвращает тип извещения для элемента входящего документа.
// 
// Параметры:
//  ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//
Функция ТипИзвещенияДляЭлементаВходящегоДокумента(ТипЭлементаРегламента) Экспорт
	

	Результат = Перечисления.ТипыЭлементовРегламентаЭДО.ПустаяСсылка();
	
	Возврат Результат;
	
КонецФункции

// Возвращает тип извещения для элемента исходящего документа.
// 
// Параметры:
//  ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//
Функция ТипИзвещенияДляЭлементаИсходящегоДокумента(ТипЭлементаРегламента) Экспорт
	
	Результат = Перечисления.ТипыЭлементовРегламентаЭДО.ПустаяСсылка();
	
	Возврат Результат;
	
КонецФункции

// Возвращает признак наличия информации получателя в регламенте.
// 
// Возвращаемое значение:
//  Булево
//
Функция ЕстьИнформацияПолучателя() Экспорт
	Возврат Истина;
КонецФункции

#КонецОбласти

#Область ДобавитьЭлементыСхемыРегламента

// Добавляет элементы по регламенту отправителя.
// 
// Параметры:
//  СхемаРегламента - См. РегламентыЭДО.НоваяСхемаРегламента
//  НастройкиСхемыРегламента - См. РегламентыЭДО.НовыеНастройкиСхемыРегламента
//  ЭлементыСхемы - См. РегламентыЭДО.НоваяКоллекцияЭлементовСхемыРегламента
//
Процедура ДобавитьЭлементыРегламентаОтправителя(СхемаРегламента, НастройкиСхемыРегламента, ЭлементыСхемы)
	
	ЭПЛ_Титул1 = РегламентыЭДО.ВставитьЭлементСхемыРегламента(ЭлементыСхемы, СхемаРегламента,
		Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул1);
		
		РегламентыЭДО.ВставитьЭлементСхемыРегламента(ЭлементыСхемы, ЭПЛ_Титул1,
			Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул2);
			
		РегламентыЭДО.ВставитьЭлементСхемыРегламента(ЭлементыСхемы, ЭПЛ_Титул1,
			Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_ОткМ);
			
		РегламентыЭДО.ВставитьЭлементСхемыРегламента(ЭлементыСхемы, ЭПЛ_Титул1,
			Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул3);
			
		РегламентыЭДО.ВставитьЭлементСхемыРегламента(ЭлементыСхемы, ЭПЛ_Титул1,
			Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул4);
			
		РегламентыЭДО.ВставитьЭлементСхемыРегламента(ЭлементыСхемы, ЭПЛ_Титул1,
			Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул5);
			
		РегламентыЭДО.ВставитьЭлементСхемыРегламента(ЭлементыСхемы, ЭПЛ_Титул1,
			Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул6);
	
КонецПроцедуры

#КонецОбласти

#Область СостояниеДокумента

// Возвращает признак успешности заполнения состояния по входящей информации отправителя.
// 
// Параметры:
// 	ПараметрыДокумента - См. РегламентыЭДО.НовыеПараметрыДокументаДляОпределенияСостояния
//  СостоянияЭлементовРегламента - См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
//  Состояние - ПеречислениеСсылка.СостоянияДокументовЭДО
//
// Возвращаемое значение:
//  Булево
//
Функция ЗаполнитьСостояниеПоВходящейИнформацииОтправителя(ПараметрыДокумента, СостоянияЭлементовРегламента, Состояние)
	
	Результат = Истина;

	Результат = Истина;
	ЭлементРегламента = Неопределено; // Неопределено,СтрокаТаблицыЗначений: См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
	
	ОбменЗавершен = Ложь;
	
	Если ЗаполнитьСостояниеПоОтклонению(ПараметрыДокумента, 
									СостоянияЭлементовРегламента, Состояние) Тогда
										
		Возврат Истина;
		
	ИначеЕсли РегламентыЭДО.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул6, ЭлементРегламента) Тогда
			
			ОбменЗавершен = Истина;
		
	ИначеЕсли РегламентыЭДО.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул5, ЭлементРегламента) Тогда
		
		РольУчастника = РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
			
		Если РольУчастника = 8 Тогда
			
				ОбменЗавершен = Истина;
				
		Иначе
			
	        //@skip-check invocation-parameter-type-intersect
	        //@skip-check property-return-type
	        МассивДокументов = ИнтеграцияЭДО.ОбъектыУчетаАктуальногоЭлектронногоДокумента(ПараметрыДокумента.Ссылка);
			Если МассивДокументов.Количество() > 0 Тогда
				Если  МассивДокументов[0].ТитулОформлениеОбязательностьМедОсмотраПосле = "2" Тогда	
					ОбменЗавершен = Истина;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
			
	ИначеЕсли РегламентыЭДО.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул4, ЭлементРегламента) Тогда
		
	ИначеЕсли РегламентыЭДО.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул3, ЭлементРегламента) Тогда
		
		РольУчастника = РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
		
		Если РольУчастника = 4 Тогда
			
				ОбменЗавершен = Истина;
				
		Иначе
			
			//@skip-check invocation-parameter-type-intersect
			//@skip-check property-return-type
			МассивДокументов = ИнтеграцияЭДО.ОбъектыУчетаАктуальногоЭлектронногоДокумента(ПараметрыДокумента.Ссылка);
			Если МассивДокументов.Количество() > 0 Тогда
				Если  МассивДокументов[0].ТитулВыпускРезультатПроведенияПредсменногоКонтроля = "2" Тогда	
					ОбменЗавершен = Истина;
				КонецЕсли;
			КонецЕсли;
				
		КонецЕсли;
		
	ИначеЕсли РегламентыЭДО.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул2, ЭлементРегламента) Тогда
		
		РольУчастника = РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
		
		Если РольУчастника = 2 Тогда
			
				ОбменЗавершен = Истина;
				
		КонецЕсли;

	ИначеЕсли РегламентыЭДО.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул1, ЭлементРегламента) Тогда
		
	Иначе
		
		Состояние = РегламентыЭДО.НачальноеСостояниеДокумента();
		Возврат Ложь;
		
	КонецЕсли;	
	
	Если ОбменЗавершен = Истина 
		И (ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.Хранение
			Или ЭтоВходящийЭлементРегламента(ЭлементРегламента)) Тогда
		Состояние = Перечисления.СостоянияДокументовЭДО.ОбменЗавершен;
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.Отправка Тогда
		Состояние = Перечисления.СостоянияДокументовЭДО.ТребуетсяОтправка;
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.ПодготовкаКОтправке Тогда
		Состояние = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодготовкаКОтправке;
	Иначе
		Если СостоянияЭлементовРегламента.Колонки.Найти("Ссылка") = Неопределено Тогда
			// Новый документ
			ТекущийДоступныйТитул = Неопределено;
		Иначе	
			ТекущийДоступныйТитул = ТекущийДоступныйТитул(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
		КонецЕсли;
		
		Если ЗаполнитьСостояниеДляИзвещения(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента, Состояние) Тогда
			Возврат Истина;		
		ИначеЕсли ЗначениеЗаполнено(ТекущийДоступныйТитул) Тогда
			Состояние = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписание;
		Иначе	
			Состояние = Перечисления.СостоянияДокументовЭДО.ОжидаетсяПодтверждение;	
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


Функция ЗаполнитьСостояниеДляИзвещения(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента, Состояние)
	
	Возврат Ложь;
	
КонецФункции


Функция РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента)
	
	Если ТипЗнч(ЭлементРегламента) = Тип("СтрокаТаблицыЗначений") Тогда
		//@skip-check property-return-type
		Если ЭлементРегламента.Владелец().Колонки.Найти("Ссылка") <> Неопределено
			И ТипЗнч(ЭлементРегламента.Ссылка) = Тип("ДокументСсылка.СообщениеЭДО") Тогда
				//@skip-check invocation-parameter-type-intersect
				//@skip-check wrong-string-literal-content
				ИдФайл = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭлементРегламента.Ссылка, "ОсновнойФайл.ПолноеИмяФайла");
				Если ТипЗнч(ИдФайл) = Тип("Строка") Тогда		
					Адресаты = ОбменСГИСЭПДКлиентСервер.АдресатыПоИдФайл(ИдФайл);			
					ИдентификаторОрганизации = Неопределено;
					Если ТипЗнч(ПараметрыДокумента) = Тип("Структура") Тогда
						ПараметрыДокумента.Свойство("ИдентификаторОрганизации", ИдентификаторОрганизации);
					ИначеЕсли ТипЗнч(ПараметрыДокумента) = Тип("ВыборкаИзРезультатаЗапроса") Тогда
						Если ПараметрыДокумента.Владелец().Колонки.Найти("ИдентификаторОрганизации") <> Неопределено Тогда
							//@skip-check statement-type-change
							ИдентификаторОрганизации = ПараметрыДокумента.ИдентификаторОрганизации;
						КонецЕсли;
					КонецЕсли;		
					РольУчастника = 0;
					Если ИдентификаторОрганизации = Адресаты.Оформитель Тогда
						РольУчастника = РольУчастника + 1;
					КонецЕсли;
					Если ИдентификаторОрганизации = Адресаты.Медосмотр Тогда 
						РольУчастника = РольУчастника + 2;
					КонецЕсли;
					Если ИдентификаторОрганизации = Адресаты.Техконтроль Тогда 
						РольУчастника = РольУчастника + 4;
					КонецЕсли;
					Если ИдентификаторОрганизации = Адресаты.Одометр Тогда
						РольУчастника = РольУчастника + 8;
					КонецЕсли;
				КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РольУчастника;
	
КонецФункции


Функция ТекущийДоступныйТитул(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента)
	
	РольУчастника = РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
	
	Результат = Неопределено;
	// Одометр
	Если ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 8) Тогда	
		Если ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул4") Тогда
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул5");	
		ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул3") Тогда
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул4");
		ИначеЕсли (ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул4")
				Или ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул5"))
			И (ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.Формирование
				Или ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.ПодготовкаКОтправке
				Или ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.Подписание) Тогда
				Результат = ЭлементРегламента.ТипЭлементаРегламента;
		КонецЕсли;			
	// Техконтроль
	ИначеЕсли ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 4) Тогда
		Если ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул2") Тогда
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул3");
		ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул3")
			И (ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.Формирование
				Или ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.ПодготовкаКОтправке
				Или ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.Подписание) Тогда	
			Результат = ЭлементРегламента.ТипЭлементаРегламента;		
		КонецЕсли;
	// Медосмотр
	ИначеЕсли ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 2) Тогда	
		Если ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул5") Тогда	
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул6");
		ИначеЕсли ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул1") Тогда	
			Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул2");
		ИначеЕсли (ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул2")
				Или ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул6"))
			И (ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.Формирование
				Или ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.ПодготовкаКОтправке
				Или ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.Подписание) Тогда
			Результат = ЭлементРегламента.ТипЭлементаРегламента;
		КонецЕсли;
	// Оформление
	ИначеЕсли ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 1) Тогда
		Если ЭлементРегламента.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул1")
			И (ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.Формирование
				Или ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.ПодготовкаКОтправке
				Или ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.Подписание) Тогда	
			Результат = ЭлементРегламента.ТипЭлементаРегламента;		
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


// Возвращает признак успешности заполнения состояния по исходящей информации отправителя.
// 
// Параметры:
//  ПараметрыДокумента - См. РегламентыЭДО.НовыеПараметрыДокументаДляОпределенияСостояния
//  СостоянияЭлементовРегламента - См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
//  Состояние - ПеречислениеСсылка.СостоянияДокументовЭДО
//
// Возвращаемое значение:
//  Булево
//
Функция ЗаполнитьСостояниеПоИсходящейИнформацииОтправителя(ПараметрыДокумента, СостоянияЭлементовРегламента, Состояние)
	
	Результат = Истина;
	ЭлементРегламента = Неопределено; // Неопределено,СтрокаТаблицыЗначений: См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
	
	ОбменЗавершен = Ложь;
	
	Если ЗаполнитьСостояниеПоОтклонению(ПараметрыДокумента, 
									СостоянияЭлементовРегламента, Состояние) Тогда
										
		Возврат Истина;
		
	ИначеЕсли РегламентыЭДО.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул6, ЭлементРегламента) Тогда
			
			ОбменЗавершен = Истина;
		
	ИначеЕсли РегламентыЭДО.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул5, ЭлементРегламента) Тогда
		
		РольУчастника = РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
			
		Если РольУчастника = 8 Тогда
			
				ОбменЗавершен = Истина;
				
		Иначе
				
			//@skip-check invocation-parameter-type-intersect
			//@skip-check property-return-type
			МассивДокументов = ИнтеграцияЭДО.ОбъектыУчетаАктуальногоЭлектронногоДокумента(ПараметрыДокумента.Ссылка);
			Если МассивДокументов.Количество() > 0 Тогда
				Если  МассивДокументов[0].ТитулОформлениеОбязательностьМедОсмотраПосле = "2" Тогда	
					ОбменЗавершен = Истина;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
			
	ИначеЕсли РегламентыЭДО.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул4, ЭлементРегламента) Тогда
		
	ИначеЕсли РегламентыЭДО.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул3, ЭлементРегламента) Тогда
		
		РольУчастника = РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
		
		Если РольУчастника = 4 Тогда
			
				ОбменЗавершен = Истина;
				
		КонецЕсли;
		
	ИначеЕсли РегламентыЭДО.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул2, ЭлементРегламента) Тогда
		
		РольУчастника = РольУчастникаЭлементаРегламента(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
		
		Если РольУчастника = 2 Тогда
			
				ОбменЗавершен = Истина;
				
		КонецЕсли;
		
	ИначеЕсли РегламентыЭДО.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул1, ЭлементРегламента) Тогда
		
	Иначе
		
		Состояние = РегламентыЭДО.НачальноеСостояниеДокумента();
		Возврат Ложь;
		
	КонецЕсли;
	
	Если ОбменЗавершен = Истина 
		И (ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.Хранение
			Или ЭтоВходящийЭлементРегламента(ЭлементРегламента)) Тогда
		Состояние = Перечисления.СостоянияДокументовЭДО.ОбменЗавершен;
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.Отправка Тогда
		Состояние = Перечисления.СостоянияДокументовЭДО.ТребуетсяОтправка;
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.ПодготовкаКОтправке Тогда
		Состояние = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодготовкаКОтправке;
	Иначе
		Если СостоянияЭлементовРегламента.Колонки.Найти("Ссылка") = Неопределено Тогда
			// Новый документ
			ТекущийДоступныйТитул = Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул1;
		Иначе	
			ТекущийДоступныйТитул = ТекущийДоступныйТитул(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента);
		КонецЕсли;
		
		Если ЗаполнитьСостояниеДляИзвещения(ПараметрыДокумента, ЭлементРегламента, СостоянияЭлементовРегламента, Состояние) Тогда
			Возврат Истина;		
		ИначеЕсли ЗначениеЗаполнено(ТекущийДоступныйТитул) Тогда
			Состояние = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписание;
		Иначе
			Состояние = Перечисления.СостоянияДокументовЭДО.ОжидаетсяПодтверждение;	
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ЭтоВходящийЭлементРегламента(ЭлементРегламента)
	
	Если ЭлементРегламента.Владелец().Колонки.Найти("Ссылка") = Неопределено Тогда
		Результат = Ложь;
	Иначе
		//@skip-check property-return-type
		//@skip-check variable-value-type
		СообщениеСсылка = ЭлементРегламента.Ссылка; 
		Если ТипЗнч(СообщениеСсылка) = Тип("ДокументСсылка.СообщениеЭДО") Тогда
			Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СообщениеСсылка, "Направление") = Перечисления.НаправленияЭДО.Входящий;	
		Иначе
			Результат = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает признак успешности заполнения состояния по исходящему отклонению.
// 
// Параметры:
//  ПараметрыДокумента - См. РегламентыЭДО.НовыеПараметрыДокументаДляОпределенияСостояния
//  СостоянияЭлементовРегламента - См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
//  Состояние - ПеречислениеСсылка.СостоянияДокументовЭДО
//
// Возвращаемое значение:
//  Булево
//
Функция ЗаполнитьСостояниеПоОтклонению(ПараметрыДокумента, СостоянияЭлементовРегламента, Состояние)
	
	Результат = Истина;
	ЭлементРегламента = Неопределено; // Неопределено,СтрокаТаблицыЗначений: См. РегламентыЭДО.НовыеСостоянияЭлементовРегламента
	
	Если ПараметрыДокумента.Исправлен = Истина Тогда
		Результат = Ложь;	
	КонецЕсли;
	
	Если Не РегламентыЭДО.ЕстьЭлементРегламента(СостоянияЭлементовРегламента,
		Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_ОткМ, ЭлементРегламента) Тогда
		
		Результат = Ложь;
		
	ИначеЕсли ЭтоВходящийЭлементРегламента(ЭлементРегламента) Тогда
		
		Состояние = Перечисления.СостоянияДокументовЭДО.ЗакрытСОтклонением;			
		
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.Подписание Тогда 
		
		Состояние = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеОтклонения;
		
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.ПодготовкаКОтправке Тогда 
		
		Состояние = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодготовкаКОтправкеОтклонения;
		
	ИначеЕсли ЭлементРегламента.Состояние = Перечисления.СостоянияСообщенийЭДО.Отправка Тогда 
		
		Состояние = Перечисления.СостоянияДокументовЭДО.ТребуетсяОтправкаОтклонения;
		
	ИначеЕсли Не ПараметрыДокумента.Исправлен Тогда
		
		Состояние = Перечисления.СостоянияДокументовЭДО.ЗакрытСОтклонением;		
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти