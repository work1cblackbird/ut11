// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает признак, будет ли превышено ограничение оператора ЭДО по количеству документов в пакете при добавлении
// определенного количества документов в пакет и превышающие ограничения оператора
// 
// Параметры:
//  ОграниченияОбъемаПакетаДокументов - см. ПакетыДокументовЭДО.НовыеОграниченияОбъемаПакетаДокументовУчастников
//  ОбъемПакетаДокументов - см. НовыйОбъемПакетаДокументов
//  ДобавляемоеКоличествоДокументов - Число
//
// Возвращаемое значение:
//  см. НовыйРезультатПроверкиПревышения
Функция ПроверкаПревышенияМаксимальногоКоличестваДокументовВПакете(ОграниченияОбъемаПакетаДокументов, ОбъемПакетаДокументов,
	ДобавляемоеКоличествоДокументов) Экспорт
	
	Результат = НовыйРезультатПроверкиПревышения();
	
	Если ПревышеноМаксимальноеКоличествоДокументов(ОграниченияОбъемаПакетаДокументов.ПоКонтрагенту,
		ОбъемПакетаДокументов, ДобавляемоеКоличествоДокументов) Тогда
			
		Результат.Превышено = Истина;
		Результат.ПревышаемыеОграничения = ОграниченияОбъемаПакетаДокументов.ПоКонтрагенту;
		
	ИначеЕсли ПревышеноМаксимальноеКоличествоДокументов(ОграниченияОбъемаПакетаДокументов.ПоОрганизации,
		ОбъемПакетаДокументов, ДобавляемоеКоличествоДокументов) Тогда
			
		Результат.Превышено = Истина;
		Результат.ПревышаемыеОграничения = ОграниченияОбъемаПакетаДокументов.ПоОрганизации;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает признак, будет ли превышено ограничение оператора ЭДО по размеру пакета при добавлении документов в пакет
// 
// Параметры:
//  ОграниченияОбъемаПакетаДокументов - см. ПакетыДокументовЭДО.НовыеОграниченияОбъемаПакетаДокументовУчастников
//  ОбъемПакетаДокументов - см. НовыйОбъемПакетаДокументов
//  РазмерДокументов - Число
//
// Возвращаемое значение:
//  см. НовыйРезультатПроверкиПревышения
Функция ПроверкаПревышенияМаксимальногоРазмераПакета(ОграниченияОбъемаПакетаДокументов, ОбъемПакетаДокументов, РазмерДокументов) Экспорт
	
	Результат = НовыйРезультатПроверкиПревышения();
	Если ПревышенМаксимальныйРамер(ОграниченияОбъемаПакетаДокументов.ПоКонтрагенту, ОбъемПакетаДокументов,
			РазмерДокументов) Тогда
				
		Результат.Превышено = Истина;
		Результат.ПревышаемыеОграничения = ОграниченияОбъемаПакетаДокументов.ПоКонтрагенту;
		
	ИначеЕсли ПревышенМаксимальныйРамер(ОграниченияОбъемаПакетаДокументов.ПоОрганизации, ОбъемПакетаДокументов,
			РазмерДокументов) Тогда
				
		Результат.Превышено = Истина;
		Результат.ПревышаемыеОграничения = ОграниченияОбъемаПакетаДокументов.ПоОрганизации;
		
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
// Структура:
//	* КоличествоДокументов - Число
//	* РазмерПакета - Число
Функция НовыйОбъемПакетаДокументов() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("КоличествоДокументов", 0);
	Результат.Вставить("РазмерПакета", 0);
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
// Структура:
//	* Превышено - Булево
//	* ПревышаемыеОграничения - Неопределено
//							 - см. СервисНастроекЭДО.НовыеОграниченияОбъемаПакетаДокументов
Функция НовыйРезультатПроверкиПревышения() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Превышено", Ложь);
	Результат.Вставить("ПревышаемыеОграничения", Неопределено);
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Параметры:
//  ОграниченияОбъемаПакетаДокументов - см. СервисНастроекЭДО.НовыеОграниченияОбъемаПакетаДокументов
//  ОбъемПакетаДокументов - см. НовыйОбъемПакетаДокументов
//  ДобавляемоеКоличествоДокументов - Число
//
// Возвращаемое значение:
//  Булево
Функция ПревышеноМаксимальноеКоличествоДокументов(ОграниченияОбъемаПакетаДокументов, ОбъемПакетаДокументов,
	ДобавляемоеКоличествоДокументов)
	
	Возврат ОграниченияОбъемаПакетаДокументов.МаксимальноеКоличествоЭДВПакете > 0
		И ОбъемПакетаДокументов.КоличествоДокументов + ДобавляемоеКоличествоДокументов > ОграниченияОбъемаПакетаДокументов.МаксимальноеКоличествоЭДВПакете;
	
КонецФункции

// Параметры:
//  ОграниченияОбъемаПакетаДокументов - см. СервисНастроекЭДО.НовыеОграниченияОбъемаПакетаДокументов
//  ОбъемПакетаДокументов - см. НовыйОбъемПакетаДокументов
//  РазмерДокументов - Число
//
// Возвращаемое значение:
//  Булево
Функция ПревышенМаксимальныйРамер(ОграниченияОбъемаПакетаДокументов, ОбъемПакетаДокументов, РазмерДокументов)
	
	МаксимальныйРазмерПакетаВБайтах = ОграниченияОбъемаПакетаДокументов.МаксимальныйРазмерПакетаЭД * 1024;
	
	Возврат ОграниченияОбъемаПакетаДокументов.МаксимальныйРазмерПакетаЭД > 0
		И ОбъемПакетаДокументов.РазмерПакета + РазмерДокументов > МаксимальныйРазмерПакетаВБайтах;
	
КонецФункции

#КонецОбласти