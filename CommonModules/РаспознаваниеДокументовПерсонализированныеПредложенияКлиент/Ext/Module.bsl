#Область СлужебныйПрограммныйИнтерфейс

// Вызывает форму, которую нужно открыть при нажатии на гиперссылку в баннере.
// Параметры:
// 		НавигационнаяСсылка - Строка
// 		СтандартнаяОбработка - Булево
// 		Форма - Форма, в которой произошло нажатие на навигационную ссылку
//
Процедура ПерейтиПоСсылкеБаннера(НавигационнаяСсылка, СтандартнаяОбработка, Форма) Экспорт
	
	Страница = Форма.КарусельБаннеров.Баннер.Страницы.Получить(НавигационнаяСсылка);
	Если Страница <> Неопределено Тогда
		АдресСтраницыHTML = ПоместитьВоВременноеХранилище(Страница.СтраницаHTML, Форма.УникальныйИдентификатор);
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Заголовок", Страница.Заголовок);
		ПараметрыОткрытия.Вставить("ВременноеХранилищеHTML", АдресСтраницыHTML);
		ОткрытьФорму("Обработка.РаспознаваниеДокументов.Форма.ФормаПросмотраHTML", ПараметрыОткрытия);
		
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьРежимОжиданияНаБаннере(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	
	Элементы.ТекстБаннера.Видимость = Ложь;
	Элементы.КартинкаБаннера.Видимость = Ложь;
	Элементы.ГруппаБаннерОжидание.Видимость = Истина;
	
	Элементы.КартинкаЗакрытьБаннер.Доступность = Ложь;
	Элементы.КартинкаПредыдущийБаннер.Доступность = Ложь;
	Элементы.КартинкаСледующийБаннер.Доступность = Ложь;
	
КонецПроцедуры

// Процедура закрывает баннер на форме.
// Параметры:
//		Форма - ФормаКлиентскогоПриложения - Форма на которой нужно закрыть баннер.
//
Процедура ОткрытьБаннер(Форма) Экспорт
	
	РаспознаваниеДокументовПерсонализированныеПредложенияВызовСервера.СохранитьОткрытиеБаннера(Форма.КарусельБаннеров);
	Форма.КарусельБаннеров = Неопределено;
	Форма.Элементы.Баннер.Видимость = Истина;
	УстановитьРежимОжиданияНаБаннере(Форма);
	Форма.Элементы.ДекорацияПоказатьБаннер.Доступность = Ложь;
	
КонецПроцедуры

// Процедура закрывает баннер на форме.
// Параметры:
//		Форма - ФормаКлиентскогоПриложения - Форма на которой нужно закрыть баннер.
//
Процедура ЗакрытьБаннер(Форма) Экспорт
	
	РаспознаваниеДокументовПерсонализированныеПредложенияВызовСервера.СохранитьЗакрытиеБаннера(Форма.КарусельБаннеров);
	Форма.КарусельБаннеров = Неопределено;
	Форма.Элементы.Баннер.Видимость = Истина;
	УстановитьРежимОжиданияНаБаннере(Форма);
	Форма.Элементы.ДекорацияПоказатьБаннер.Доступность = Ложь;
	
КонецПроцедуры

#КонецОбласти

