#Область ПрограммныйИнтерфейс

#Область ФормированиеОбщихПараметровЧека

// Инициализирует параметры фискального чека
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ, по которому требуется распечатать чек 
// 	Организация - СправочникСсылка.Организации - Организация по документу
// Возвращаемое значение:
// 	Структура - Описание: Параметры фискального чека
Функция ПараметрыОперацииФискализацииЧека(ДокументСсылка, Организация, Кассир = Неопределено) Экспорт
	
	ПараметрыОперацииФискализацииЧека = Неопределено;
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ПараметрыОперацииФискализацииЧека(ПараметрыОперацииФискализацииЧека, ДокументСсылка, Организация);
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ПараметрыОперацииФискализацииЧекаПоДокументу(ПараметрыОперацииФискализацииЧека, ДокументСсылка, Организация, Кассир);
	
	Возврат ПараметрыОперацииФискализацииЧека;
	
КонецФункции

// Обновляет параметр "ДокументОснование" в параметрах фискального чека 
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	ДокументОснование - ДокументСсылка - новое значение параметра "ДокументОснование"
Процедура ОбновитьПараметрыФискальногоЧекаДокументОснование(ПараметрыФискальногоЧека, ДокументОснование) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ОбновитьПараметрыФискальногоЧекаДокументОснование(ПараметрыФискальногоЧека, ДокументОснование);
	
КонецПроцедуры

// Обновляет параметр "ЕдиныйЧек" в параметрах фискального чека 
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	ФлагЕдиныйЧек - Булево - новое значение параметра "ЕдиныйЧек"
Процедура ОбновитьПараметрыФискальногоЧекаЕдиныйЧек(ПараметрыФискальногоЧека, ФлагЕдиныйЧек) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ОбновитьПараметрыФискальногоЧекаЕдиныйЧек(ПараметрыФискальногоЧека, ФлагЕдиныйЧек);
	
КонецПроцедуры

// Обновляет параметр "ТипРасчета" в параметрах фискального чека 
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	ТипРасчета - ПеречислениеСсылка.ТипыРасчетаДенежнымиСредствами - новое значение параметра "ТипРасчета"
Процедура ПараметрыФискальногоЧекаЗаполнитьТипРасчета(ПараметрыФискальногоЧека, ТипРасчета) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ПараметрыФискальногоЧекаЗаполнитьТипРасчета(ПараметрыФискальногоЧека, ТипРасчета);
	
КонецПроцедуры

// Обновляет параметр "СерийныйНомер" в параметрах фискального чека 
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	СерийныйНомерОборудования - Строка - новое значение параметра "СерийныйНомер"
Процедура ПараметрыФискальногоЧекаЗаполнитьСерийныйНомерОборудования(ПараметрыФискальногоЧека, СерийныйНомерОборудования) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ПараметрыФискальногоЧекаЗаполнитьСерийныйНомерОборудования(ПараметрыФискальногоЧека, СерийныйНомерОборудования);
	
КонецПроцедуры

// Обновляет параметры получателя в параметрах фискального чека
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	СведенияОЮрФизЛице - Структура - Сведения о покупателе юр. лице/ИП
Процедура ПараметрыФискальногоЧекаЗаполнитьПараметрыПокупателя(ПараметрыФискальногоЧека, СведенияОЮрФизЛице) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ПараметрыФискальногоЧекаЗаполнитьПараметрыПокупателя(ПараметрыФискальногоЧека, СведенияОЮрФизЛице);
	
КонецПроцедуры

// Обновляет параметр "ТорговыйОбъект" в параметрах фискального чека 
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	МестоРасчетов - Структура - содержит структуру значений по торговому объекту
Процедура ПараметрыФискальногоЧекаЗаполнитьМестоРасчетов(ПараметрыФискальногоЧека, МестоРасчетов) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ПараметрыФискальногоЧекаЗаполнитьМестоРасчетов(ПараметрыФискальногоЧека, МестоРасчетов);
	
КонецПроцедуры

// Обновляет параметры отправки электронного чека в параметрах фискального чека
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	ВариантОтправки - ПеречислениеСсылка.ВариантыОтправкиЭлектронногоЧекаПокупателю - вариант отправки электронного чека (email, sms)
// 	КонтактПолучателя - Строка - email или номер телефона
Процедура ПараметрыФискальногоЧекаЗаполнитьПараметрыОтправкиЭлектронногоЧека(ПараметрыФискальногоЧека, ВариантОтправки, КонтактПолучателя) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ПараметрыФискальногоЧекаЗаполнитьПараметрыОтправкиЭлектронногоЧека(ПараметрыФискальногоЧека, ВариантОтправки, КонтактПолучателя);
	
КонецПроцедуры

// Обновляет персональные данные в параметрах фискального чека
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	ЕстьПерсональныеДанные - Булево - Параметры фискального чека содержат/не содержат персональные данные
// 	Получатель - Строка - ФИО получателя и паспортные данные в случае отсутствия ИНН
// 	ПолучательИНН - Строка - ИНН получателя
Процедура ПараметрыФискальногоЧекаЗаполнитьПерсональныеДанные(ПараметрыФискальногоЧека, ЕстьПерсональныеДанные, Получатель, ПолучательИНН) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ПараметрыФискальногоЧекаЗаполнитьПерсональныеДанные(
		ПараметрыФискальногоЧека,
		ЕстьПерсональныеДанные,
		Получатель,
		ПолучательИНН);
	
КонецПроцедуры
	
// Обновляет персональные данные в параметрах фискального чека версии ФФД 1.2
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	ЕстьПерсональныеДанные - Булево - Параметры фискального чека содержат/не содержат персональные данные
// 	ТипПерсональныхДанных - ПеречислениеСсылка.ТипыПерсональныхДанныхККТ - Тип персональных данных, указанных при пробитии чека
//	СубъектПерсональныхДанных - СправочникСсылка.ФизическиеЛица - Розничный покупатель
// 	СведенияОПокупателе - Структура - Сведения о покупателе
Процедура ПараметрыФискальногоЧекаЗаполнитьПерсональныеДанныеФФД_1_2(ПараметрыФискальногоЧека, ЕстьПерсональныеДанные, ТипПерсональныхДанных, СубъектПерсональныхДанных, СведенияОПокупателе) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ПараметрыФискальногоЧекаЗаполнитьПерсональныеДанныеФФД_1_2(
		ПараметрыФискальногоЧека,
		ЕстьПерсональныеДанные,
		ТипПерсональныхДанных,
		СубъектПерсональныхДанных,
		СведенияОПокупателе);
	
КонецПроцедуры
	

// Обновляет параметр "ПозицииЧека" в параметрах фискального чека
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	ПозицииЧека - Массив из Структура - новое значение параметра "ПозицииЧека"
Процедура ПараметрыФискальногоЧекаЗаполнитьПозицииЧека(ПараметрыФискальногоЧека, ПозицииЧека) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ПараметрыФискальногоЧекаЗаполнитьПозицииЧека(ПараметрыФискальногоЧека, ПозицииЧека);
	
КонецПроцедуры

// Обновляет параметр "ТаблицаОплат" в параметрах фискального чека 
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	ТаблицаОплат - Массив из Структура - новое значение параметра "ТаблицаОплат"
Процедура ПараметрыФискальногоЧекаЗаполнитьТаблицуОплат(ПараметрыФискальногоЧека, ТаблицаОплат) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ПараметрыФискальногоЧекаЗаполнитьТаблицуОплат(ПараметрыФискальногоЧека, ТаблицаОплат);
	
КонецПроцедуры

// Обновляет параметры чека коррекции в параметрах фискального чека
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	КорректируемыйДокумент - ДокументСсылка - Основание, по которому введен документ печати чека
// 	ВидКоррекции - ПеречислениеСсылка.ВидыЧековКоррекции - вид чека коррекции
// 	ДанныеКоррекции - Массив - Дополнительные данные коррекции
Процедура ПараметрыФискальногоЧекаЗаполнитьПараметрыКоррекции(ПараметрыФискальногоЧека, КорректируемыйДокумент, ВидКоррекции, ДанныеКоррекции) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ПараметрыФискальногоЧекаЗаполнитьПараметрыКоррекции(ПараметрыФискальногоЧека, КорректируемыйДокумент, ВидКоррекции, ДанныеКоррекции);
	
КонецПроцедуры

// Очищает параметры чека коррекции в параметрах фискального чека 
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
Процедура ПараметрыФискальногоЧекаУдалитьПараметрыКоррекции(ПараметрыФискальногоЧека) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ПараметрыФискальногоЧекаУдалитьПараметрыКоррекции(ПараметрыФискальногоЧека);
	
КонецПроцедуры

// Добавляет дополнительные данные к параметрам фискального чека в случае оплаты подарочным сертификатом
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	ПодарочныеСертификаты - ТаблицаЗначений - Список подарочных сертификатов с суммовыми данными
Процедура ДобавитьДанныеПоПодарочнымСертификатам(ПараметрыФискальногоЧека, ПодарочныеСертификаты) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ДобавитьДанныеПоПодарочнымСертификатам(ПараметрыФискальногоЧека, ПодарочныеСертификаты);
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеПараметровЧекаККМ

// Обновляет данные торгового объекта в параметрах чека
//
// Параметры:
// 	ПараметрыОперацииФискализацииЧека - Структура - Параметры фискального чека
//  Организация - СправочникСсылка.Организации - Ссылка на организацию
// 	ТорговыйОбъект - СправочникСсылка.Склады - Ссылка на склад, к которому привязана ККТ
//  ОборудованиеККТ - СправочникСсылка.ПодключаемоеОборудование - Ссылка на ККТ
Процедура ОбновитьПараметрыФискальногоЧекаМестоРасчетов(ПараметрыОперацииФискализацииЧека, Организация, ТорговыйОбъект, ОборудованиеККТ) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ОбновитьПараметрыФискальногоЧекаМестоРасчетов(
		ПараметрыОперацииФискализацииЧека, Организация, ТорговыйОбъект, ОборудованиеККТ);
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеПозицийЧекаККМ

// Рассчитывает и обновляет параметр позиций чека ККМ (ЧекККМ, ЧекККМВозврат, ЧекККМКоррекции)
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	ДокументСсылка - ДокументСсылка - Ссылка на чек ККМ
Процедура ОбновитьПараметрыФискальногоЧекаПозицийЧекаККМ(ПараметрыФискальногоЧека, ДокументСсылка) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ОбновитьПараметрыФискальногоЧекаПозицийЧекаККМ(ПараметрыФискальногоЧека, ДокументСсылка);
	
КонецПроцедуры

// Рассчитывает и обновляет параметр таблицы оплат чека ККМ (ЧекККМ, ЧекККМВозврат, ЧекККМКоррекции)
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	ИнформацияОбОплате - Структура - Суммы оплат по видам оплат
Процедура ОбновитьПараметрыФискальногоЧекаТаблицаОплатЧекаККМ(ПараметрыФискальногоЧека, ИнформацияОбОплате) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ОбновитьПараметрыФискальногоЧекаТаблицаОплатЧекаККМ(ПараметрыФискальногоЧека, ИнформацияОбОплате);
	
КонецПроцедуры

// Обновить параметры фискального чека по сертификатам ФСС.
// 
// Параметры:
//  ПараметрыФискальногоЧека - Структура - Параметры фискального чека
//  ОплатыПлатежнымиКартами - ТаблицаЗначений -  Оплаты платежными картами
Процедура ОбновитьПараметрыФискальногоЧекаСертификатыФСС(ПараметрыФискальногоЧека, ОплатыПлатежнымиКартами) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ОбновитьПараметрыФискальногоЧекаСертификатыФСС(
		ПараметрыФискальногоЧека,
		ОплатыПлатежнымиКартами);
	
КонецПроцедуры

// Обновить параметры фискального чека по СБП.
// 
// Параметры:
//  ПараметрыФискальногоЧека - Структура - Параметры фискального чека
//  ОплатыСБП - ТаблицаЗначений -  Оплаты СБП
Процедура ОбновитьПараметрыФискальногоЧекаСБП(ПараметрыФискальногоЧека, ОплатыСБП) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ОбновитьПараметрыФискальногоЧекаСБП(
		ПараметрыФискальногоЧека,
		ОплатыСБП);
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеПозицийЕдиногоЧека

// Рассчитывает и обновляет параметр позиций единого чека
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	ДокументыЕдиногоЧека - Массив - Документы, входящие в единый чек
Процедура ОбновитьПараметрыФискальногоЧекаПозицийЧекаЕдиныйЧек(ПараметрыФискальногоЧека, ДокументыЕдиногоЧека) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ОбновитьПараметрыФискальногоЧекаПозицийЧекаЕдиныйЧек(ПараметрыФискальногоЧека, ДокументыЕдиногоЧека);
	
КонецПроцедуры

// Рассчитывает и обновляет параметр таблицы оплат единого чека
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	ДокументыЕдиногоЧека - Массив - Документы, входящие в единый чек
Процедура ОбновитьПараметрыФискальногоЧекаТаблицаОплатЕдиныйЧек(ПараметрыФискальногоЧека, ДокументыЕдиногоЧека) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ОбновитьПараметрыФискальногоЧекаТаблицаОплатЕдиныйЧек(ПараметрыФискальногоЧека, ДокументыЕдиногоЧека);
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеПозицийДокументаПоставки

// Рассчитывает и обновляет параметр позиций чека для документа поставки
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	ОбъектыРасчетовСПризнакамиСпособаРасчетов - ТаблицаЗначений - таблица сумм поставки по объектам расчетов документа печати чека с детализацией по признакам способа расчета
Процедура ОбновитьПараметрыФискальногоЧекаПозицийЧекаПоДокументуПоставки(ПараметрыФискальногоЧека, ОбъектыРасчетовСПризнакамиСпособаРасчетов) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ОбновитьПараметрыФискальногоЧекаПозицийЧекаПоДокументуПоставки(ПараметрыФискальногоЧека, ОбъектыРасчетовСПризнакамиСпособаРасчетов);
	
КонецПроцедуры

// Рассчитывает и обновляет параметр таблицы оплат чека для документа поставки
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	СтруктуруОплатДокументаПоставки - Структура - Суммы оплат по видам оплат
Процедура ОбновитьПараметрыФискальногоЧекаТаблицыОплатПоДокументуПоставки(ПараметрыФискальногоЧека, СтруктуруОплатДокументаПоставки) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ОбновитьПараметрыФискальногоЧекаТаблицыОплатПоДокументуПоставки(ПараметрыФискальногоЧека, СтруктуруОплатДокументаПоставки);
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеПозицийДокументаОплаты

// Рассчитывает и обновляет параметр позиций чека для документа оплаты
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	ОбъектыРасчетовСПризнакамиСпособаРасчетов - ТаблицаЗначений - таблица сумм поставки по объектам расчетов документа печати чека с детализацией по признакам способа расчета
// 	ИмяКомандыПробитияЧека - Строка - Имя команды пробития чека
Процедура ОбновитьПараметрыФискальногоЧекаПозицийЧекаПоДокументуОплаты(ПараметрыФискальногоЧека, ОбъектыРасчетовСПризнакамиСпособаРасчетов, ИмяКомандыПробитияЧека) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ОбновитьПараметрыФискальногоЧекаПозицийЧекаПоДокументуОплаты(ПараметрыФискальногоЧека, ОбъектыРасчетовСПризнакамиСпособаРасчетов, ИмяКомандыПробитияЧека);
	
КонецПроцедуры

// Рассчитывает и обновляет параметр таблицы оплат чека для документа оплаты
// 
// Параметры:
// 	ПараметрыФискальногоЧека - Структура - Параметры фискального чека
// 	СуммаДокумента - Структура - Суммы оплат по видам оплат
Процедура ОбновитьПараметрыФискальногоЧекаТаблицыОплатПоДокументуОплаты(ПараметрыФискальногоЧека, СуммаДокумента) Экспорт
	
	ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ОбновитьПараметрыФискальногоЧекаТаблицыОплатПоДокументуОплаты(ПараметрыФискальногоЧека, СуммаДокумента);
	
КонецПроцедуры

#КонецОбласти

#Область ПакетноеФормирование

// Возвращает параметры операции для фискализации чека по документу
//
// Параметры:
// 	ДокументСсылка - ДокументСсылка - ссылка на документ, который поддерживает фискализацию
// 	ОборудованиеККТ - СправочникСсылка.ПодключаемоеОборудование
// 
// Возвращаемое значение:
// 	Структура, Неопределено - 
//
Функция ПараметрыОперацииФискализацииЧекаПакетныйРежим(ДокументСсылка, ОборудованиеККТ = Неопределено) Экспорт
	
	Если НЕ ФормированиеФискальныхЧековВызовСервера.РазрешеноПробитиеФискальныхЧековПоДокументу(ДокументСсылка) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ФормированиеФискальныхЧековВызовСервера.ПробитФискальныйЧекПоДокументу(ДокументСсылка) Тогда
		Возврат Неопределено;
	КонецЕсли; 

	ВерсияФФД = "";
	Если ОборудованиеККТ = Неопределено Тогда
		ОборудованиеККТ = Справочники.ПодключаемоеОборудование.ПустаяСсылка();
		ТорговыйОбъект = Неопределено;
	Иначе 
		ТорговыйОбъект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Касса");
		ВерсияФФД = ФормированиеФискальныхЧековСерверПереопределяемый.ВерсияФФДОборудования(ОборудованиеККТ);
	КонецЕсли;
	
	ПараметрыОперации = Новый Структура();
	
	ПараметрыОперации.Вставить("ДокументСсылка", ДокументСсылка);
	ПараметрыОперации.Вставить("Валюта");
	ПараметрыОперации.Вставить("ВалютаВзаиморасчетов");
	ПараметрыОперации.Вставить("Контрагент");
	ПараметрыОперации.Вставить("Организация");
	ПараметрыОперации.Вставить("Партнер");
	ПараметрыОперации.Вставить("СуммаДокумента");
	
	ЗаполнитьЗначенияСвойств(ПараметрыОперации, ДокументСсылка);
	
	ПараметрыОперации.Вставить("ИмяКомандыПробитияЧека",			"ПробитьЧек");
	ПараметрыОперации.Вставить("ИмяРеквизитаГиперссылкиНаФорме",	Неопределено);
	ПараметрыОперации.Вставить("ОплатаВыполнена",					Неопределено);
	ПараметрыОперации.Вставить("ПараметрыЭквайринговойОперации",	Неопределено);
	ПараметрыОперации.Вставить("ТорговыйОбъект",					ТорговыйОбъект);
	
	ПараметрыПредпросмотраЧека = ФормированиеФискальныхЧековСервер.ПараметрыПредпросмотраЧека(ПараметрыОперации);
	
	СтруктураСостояниеКассовойСмены = РозничныеПродажиВызовСервера.СостояниеКассовойСмены(ОборудованиеККТ);
	
	ТипРасчетаДенежнымиСредствами = ПредопределенноеЗначение("Перечисление.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств");
	ПризнакиСпособаРасчетаАвто = ОбщегоНазначенияУТ.МассивВТаблицуЗначений(ПараметрыПредпросмотраЧека.ПризнакиСпособаРасчетаАвто);
	
	ВозможныеПризнакиСпособаРасчетаПоДокументу = ФормированиеФискальныхЧековСервер.ВидыВозможныхПризнаковСпособаРасчетаПоДокументу(
		ДокументСсылка,
		ПараметрыПредпросмотраЧека.ОбъектыРасчетовПоДокументу,
		ТипРасчетаДенежнымиСредствами,
		ПараметрыОперации.СуммаДокумента);
	
	ОбъектыРасчетовСПризнакамиСпособаРасчетов = ФормированиеФискальныхЧековСерверПереопределяемый.ОбъектыРасчетовСПризнакамиСпособаРасчетов(
		ДокументСсылка,
		ВозможныеПризнакиСпособаРасчетаПоДокументу,
		ПризнакиСпособаРасчетаАвто,
		"<Авто>",
		ПараметрыПредпросмотраЧека.ОбъектыРасчетовПоДокументу,
		ПараметрыОперации.СуммаДокумента,
		0);
	
	ПараметрыОперацииФискализацииЧека = ПараметрыПредпросмотраЧека.ПараметрыОперацииФискализацииЧека;
	
	ПараметрыОперацииФискализацииЧека.Вставить("ДокументСсылка", 			ДокументСсылка);
	ПараметрыОперацииФискализацииЧека.Вставить("Организация", 				ПараметрыОперации.Организация);
	ПараметрыОперацииФискализацииЧека.Вставить("Контрагент", 				ПараметрыОперации.Контрагент);
	ПараметрыОперацииФискализацииЧека.Вставить("СуммаДокумента", 			ПараметрыОперации.СуммаДокумента);
	
	Если ПараметрыПредпросмотраЧека.ДокументыЕдиногоЧека.Количество() > 0 Тогда
		ПараметрыОперацииФискализацииЧека.Вставить("ЕдиныйЧек", 			Истина);
		ПараметрыОперацииФискализацииЧека.Вставить("ДокументыЕдиногоЧека", 	ПараметрыПредпросмотраЧека.ДокументыЕдиногоЧека);
	КонецЕсли; 
	
	Если Не ФормированиеФискальныхЧековСерверПереопределяемый.ДокументОплатыСВозможностьюПробитияЧеков(ДокументСсылка)
		И ЗначениеЗаполнено(СтруктураСостояниеКассовойСмены)
		И СтруктураСостояниеКассовойСмены.Свойство("Склад") Тогда
		ПараметрыОперацииФискализацииЧека.Вставить("Склад", СтруктураСостояниеКассовойСмены.Склад);
	Иначе
		ПараметрыОперацииФискализацииЧека.Вставить("Склад", Неопределено);
	КонецЕсли;
	
	ПараметрыОперацииФискализацииЧека.Вставить("ТипФискальногоДокумента", ПредопределенноеЗначение("Перечисление.ТипыФискальныхДокументовККТ.КассовыйЧек"));
	ПараметрыОперацииФискализацииЧека.Вставить("ТипРасчетаДенежнымиСредствами", ТипРасчетаДенежнымиСредствами);
	ПараметрыОперацииФискализацииЧека.Вставить("ВидЧекаКоррекции", Неопределено);
	ПараметрыОперацииФискализацииЧека.Вставить("ОборудованиеККТ", ОборудованиеККТ);
	ПараметрыОперацииФискализацииЧека.Вставить("СерийныйНомерОборудования", ОборудованиеККТ.СерийныйНомер);
	
	ПараметрыОтправкиЭлектронногоЧека = РозничныеПродажи.ПолучитьПараметрыОтправкиЭлектронногоЧекаПоПартнеру(ПараметрыОперации.Партнер); 
	ПараметрыОперацииФискализацииЧека.Вставить("ВариантОтправкиЭлектронногоЧека", ПараметрыОтправкиЭлектронногоЧека.ВариантОтправкиЭлектронногоЧека);
	ПараметрыОперацииФискализацииЧека.Вставить("КонтактПолучателяЭлектронногоЧека", ПараметрыОтправкиЭлектронногоЧека.КонтактПолучателяЭлектронногоЧека);
	
	ПараметрыОперацииФискализацииЧека.Вставить("ОбъектыРасчетовСПризнакамиСпособаРасчетов", ОбъектыРасчетовСПризнакамиСпособаРасчетов);
	ПараметрыОперацииФискализацииЧека.Вставить("ПризнакиСпособаРасчетаАвто", ПризнакиСпособаРасчетаАвто);
	
	ПараметрыОперацииФискализацииЧека.Вставить("ОперацияПредусматриваетВводПерсональныхДанныхПокупателя", Ложь);
	
	ПараметрыОперацииФискализацииЧека.Вставить("ИмяКомандыПробитияЧека", ПараметрыОперации.ИмяКомандыПробитияЧека);
	
	ПараметрыОперацииФискализацииЧека = ФормированиеФискальныхЧековСервер.ОбновитьПараметрыФискальногоЧека(ПараметрыОперацииФискализацииЧека, ВерсияФФД);

	Возврат ПараметрыОперацииФискализацииЧека;
	
КонецФункции

#КонецОбласти 

// Возвращает таблицу товаров для заполнения позиций строк в параметрах чека
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ для получения товарных позиций
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблицу с товарными позициями с количественными и суммовыми показателями
Функция ПозицииНоменклатурыПоДокументу(ДокументСсылка) Экспорт
	
	Возврат ФормированиеПараметровФискальногоЧекаСерверПереопределяемый.ПозицииНоменклатурыПоДокументу(ДокументСсылка);
	
КонецФункции

#КонецОбласти

