#Область ПрограммныйИнтерфейс

#Область РаботаСФормойЭкземпляраОфлайнОборудования

// Дополнительные переопределяемые действия с управляемой формой в Экземпляре оборудования
// при событии "ПриСозданииНаСервере".
//
// Параметры:
//  Объект - СправочникОбъект.ОфлайнОборудование - 
//  ЭтаФорма - ФормаКлиентскогоПриложения - 
//  Отказ - Булево - Признак отказа от создания формы
//  Параметры - ДанныеФормыСтруктура - параметры формы
//  СтандартнаяОбработка - Булево - признак выполнения стандартной обработки
//
Процедура ЭкземплярОфлайнОборудованияПриСозданииНаСервере(Объект, ЭтаФорма, Отказ, Параметры, СтандартнаяОбработка) Экспорт
	
	//++ НЕ ГОСИС
	Элемент = ЭтаФорма.Элементы.Добавить("ПравилоОбмена", Тип("ПолеФормы"), );
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	Элемент.ПутьКДанным = "Объект.ПравилоОбмена";
	
	// Доступ к узлу есть только для соответствующего оборудования.
	Если Объект.ТипОфлайнОборудования = Перечисления.ТипыОфлайнОборудования.ККМ Тогда
		
		ПараметрыВыбора = Новый Массив;
		НовыйПараметр = Новый ПараметрВыбора("Отбор.ТипПодключаемогоОборудования", Перечисления.ТипыОфлайнОборудования.ККМ);
		ПараметрыВыбора.Добавить(НовыйПараметр);
		
		ПараметрыВыбораПравилаОбмена = Новый ФиксированныйМассив(ПараметрыВыбора);
		
		ЭтаФорма.Элементы.ПравилоОбмена.Видимость = Истина;
		Если ЗначениеЗаполнено(ПараметрыВыбораПравилаОбмена) Тогда
			ЭтаФорма.Элементы.ПравилоОбмена.ПараметрыВыбора = ПараметрыВыбораПравилаОбмена;
		КонецЕсли;
	Иначе
		ЭтаФорма.Элементы.ПравилоОбмена.Видимость = Ложь;
	КонецЕсли;
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Дополнительные переопределяемые действия с управляемой формой в Экземпляре оборудования
// при событии "ПриЧтенииНаСервере".
//
// Параметры:
//  ТекущийОбъект - СправочникОбъект.ОфлайнОборудование - Объект, который будет прочитан.
//  ЭтаФорма - ФормаКлиентскогоПриложения
//
Процедура ЭкземплярОфлайнОборудованияПриЧтенииНаСервере(ТекущийОбъект, ЭтаФорма) Экспорт

КонецПроцедуры

// Дополнительные переопределяемые действия с управляемой формой в Экземпляре оборудования
// при событии "ПередЗаписьюНаСервере".
//
// Параметры:
//  Отказ - Булево - Признак отказа от записи
//  ТекущийОбъект - СправочникОбъект.ОфлайнОборудование - Записываемый объект
//  ПараметрыЗаписи - Структура - Структура, содержащая параметры записи.
//
Процедура ЭкземплярОфлайнОборудованияПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи) Экспорт

КонецПроцедуры

// Дополнительные переопределяемые действия с управляемой формой в Экземпляре оборудования
// при событии "ПриЗаписиНаСервере".
//
// Параметры:
//  Отказ - Булево - Признак отказа от записи
//  ТекущийОбъект - СправочникОбъект.ОфлайнОборудование - Записываемый объект
//  ПараметрыЗаписи - Структура - Структура, содержащая параметры записи.
//
Процедура ЭкземплярОфлайнОборудованияПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи) Экспорт

КонецПроцедуры

// Дополнительные переопределяемые действия с управляемой формой в Экземпляре оборудования
// при событии "ПослеЗаписиНаСервере".
//
// Параметры:
//  ТекущийОбъект - СправочникОбъект.ОфлайнОборудование - Записанный объект
//  ПараметрыЗаписи - Структура - Структура, содержащая параметры записи.
//
Процедура ЭкземплярОфлайнОборудованияПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи) Экспорт

КонецПроцедуры

// Дополнительные переопределяемые действия с управляемой формой в Экземпляре оборудования
// при событии "ОбработкаПроверкиЗаполненияНаСервере".
//
// Параметры:
//  Объект - СправочникОбъект.ОфлайнОборудование
//  ЭтаФорма - ФормаКлиентскогоПриложения
//  Отказ - Булево - Признак отказа от создания формы
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ЭкземплярОфлайнОборудованияОбработкаПроверкиЗаполненияНаСервере(Объект, ЭтаФорма, Отказ, ПроверяемыеРеквизиты) Экспорт

КонецПроцедуры

// Дополнительные переопределяемые действия при переносе элемента офлайн оборудования из справочника ПодключаемоеОборудование
// в справочник ОфлайнОборудование.
//
// Параметры:
//  СсылкаНаПодключаемоеОборудование - СправочникСсылка.ПодключаемоеОборудование - ссылка на перемещаемый элемент
//  СсылкаНаОфлайнОборудование - СправочникСсылка.ОфлайнОборудование - ссылка на аналогичный объект в справочнике ОфлайнОборудование
//
Процедура ОбработатьПереходСправочникаПодключаемоеОборудованиеНаОфлайнОборудование(СсылкаНаПодключаемоеОборудование, СсылкаНаОфлайнОборудование) Экспорт
	
	Блокировка = Новый БлокировкаДанных();
	
	ЭлементБлокировки = Блокировка.Добавить("Справочник.ОфлайнОборудование");
	ЭлементБлокировки.УстановитьЗначение("Ссылка", СсылкаНаОфлайнОборудование);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	ЭлементБлокировки = Блокировка.Добавить("Справочник.НастройкиРМК");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	
	Блокировка.Заблокировать();
	
	// Обновить данные по спец. полям
	ПодключаемоеОборудованиеРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		СсылкаНаОфлайнОборудование, 
		"УзелИнформационнойБазы, ПравилоОбмена");
	ОфлайнОборудованиеОбъект = СсылкаНаОфлайнОборудование.ПолучитьОбъект();
	ОфлайнОборудованиеОбъект.УзелИнформационнойБазы = ПодключаемоеОборудованиеРеквизиты.УзелИнформационнойБазы;
	ОфлайнОборудованиеОбъект.ПравилоОбмена = ПодключаемоеОборудованиеРеквизиты.ПравилоОбмена;
	ОфлайнОборудованиеОбъект.Записать();
	
	// Обновить Настройки ККМ по оборудованию
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НастройкиРМККассыККМ.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НастройкиРМК.КассыККМ КАК НастройкиРМККассыККМ
	|ГДЕ
	|	НастройкиРМККассыККМ.ПодключаемоеОборудование = &ПодключаемоеОборудование
	|";
	Запрос.УстановитьПараметр("ПодключаемоеОборудование", СсылкаНаПодключаемоеОборудование);
	
	ВыборкаЗапроса = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаЗапроса.Следующий() Цикл
		НастройкаРМКОбъект = ВыборкаЗапроса.Ссылка.ПолучитьОБъект(); // - СправочникОбъект.НастройкиРМК
		
		Для Каждого НастройкаКассыККМ Из НастройкаРМКОбъект.КассыККМ Цикл
			Если НастройкаКассыККМ.ПодключаемоеОборудование = СсылкаНаПодключаемоеОборудование Тогда
				НастройкаКассыККМ.ПодключаемоеОборудование = СсылкаНаОфлайнОборудование;
			КонецЕсли;
		КонецЦикла;
		НастройкаРМКОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Дополнительные переопределяемые действия для обработки объектов прикладной конфигурации при переходе на новый
// справочник "ОфлайнОборудование".
//
Процедура ДополнительнаяОбработкаОбъектовПриПереходеНаОфлайнОборудование() Экспорт
	
	НачатьТранзакцию();
	
	Блокировка = Новый БлокировкаДанных();
	ЭлементБлокировки = Блокировка.Добавить("Справочник.ПравилаОбменаСПодключаемымОборудованиемOffline");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	
	Блокировка.Заблокировать();
	
	// Обновить ТипПодключаемогоОборудования в ПравилаОбменаСПодключаемымОборудованиемOffline
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПравилаОбменаСПодключаемымОборудованиемOffline.Ссылка
	|ИЗ
	|	Справочник.ПравилаОбменаСПодключаемымОборудованиемOffline КАК ПравилаОбменаСПодключаемымОборудованиемOffline
	|ГДЕ
	|	ПравилаОбменаСПодключаемымОборудованиемOffline.ТипПодключаемогоОборудования = &ТипПодключаемогоОборудования";
	Запрос.УстановитьПараметр("ТипПодключаемогоОборудования", Перечисления.ТипыПодключаемогоОборудования.УдалитьККМОфлайн);
	
	ВыборкаЗапроса = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаЗапроса.Следующий() Цикл
		ПравилоОбменаОбъект = ВыборкаЗапроса.Ссылка.ПолучитьОбъект(); // СправочникОбъект.ПравилаОбменаСПодключаемымОборудованиемOffline
		ПравилоОбменаОбъект.ТипПодключаемогоОборудования = Перечисления.ТипыОфлайнОборудования.ККМ;
		ПравилоОбменаОбъект.Записать();
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти