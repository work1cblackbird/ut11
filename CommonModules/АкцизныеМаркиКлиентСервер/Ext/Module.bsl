#Область ПрограммныйИнтерфейс

// Заполнить индекс акцизной марки.
// 
// Параметры:
//  ТекущаяСтрока - Структура, ДанныеФормыЭлементКоллекции - Текущая строка:
//   * МаркируемаяПродукция - Булево - признак маркируемой продукции
//   * ИндексАкцизнойМарки  - Число  - индекс картинки статуса указания акцизных марок
//  ИмяКолонкиКоличество - Строка - Имя колонки количество
Процедура ЗаполнитьИндексАкцизнойМарки(ТекущаяСтрока, ИмяКолонкиКоличество = "Количество") Экспорт
	
	Если ТекущаяСтрока.МаркируемаяПродукция = Истина Тогда
		
		Если ТекущаяСтрока[ИмяКолонкиКоличество] = ТекущаяСтрока.КоличествоПродукцииПоАкцизнымМаркам
			И ТекущаяСтрока[ИмяКолонкиКоличество] <> 0 Тогда
			ТекущаяСтрока.ИндексАкцизнойМарки = 1;
		Иначе
			ТекущаяСтрока.ИндексАкцизнойМарки = 2;
		КонецЕсли;
		
	Иначе
		
		ТекущаяСтрока.ИндексАкцизнойМарки = 0;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Заполняет поле индекса акцизной марки в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//
Процедура ЗаполнитьИндексАкцизнойМаркиДляСтрокиТабличнойЧасти(ТекущаяСтрока, СтруктураДействий) Экспорт
	
	ПараметрыДействия = Неопределено;
	Если СтруктураДействий.Свойство("ЗаполнитьИндексАкцизнойМарки", ПараметрыДействия) Тогда
		
		Если ПараметрыДействия <> Неопределено И ПараметрыДействия.Свойство("ИмяКолонкиКоличество") Тогда
			ИмяКолонкиКоличество = ПараметрыДействия.ИмяКолонкиКоличество;
		Иначе
			ИмяКолонкиКоличество = "Количество";
		КонецЕсли;
		
		ЗаполнитьИндексАкцизнойМарки(ТекущаяСтрока, ИмяКолонкиКоличество);
		
	КонецЕсли;
	
КонецПроцедуры

Функция СтруктураДанныхШтрихкода(Штрихкод) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("АлкогольнаяПродукция",    Неопределено);
	Результат.Вставить("ВложенныеШтрихкоды",      Новый Соответствие);
	Результат.Вставить("КодАлкогольнойПродукции", "");
	Результат.Вставить("Количество",              0);
	Результат.Вставить("МаркированныеТовары",     Новый Массив);
	Результат.Вставить("Номенклатура",            Неопределено);
	Результат.Вставить("НомерМарки",              "");
	Результат.Вставить("Серия",                   Неопределено);
	Результат.Вставить("СерияМарки",              "");
	Результат.Вставить("Справка2",                Неопределено);
	Результат.Вставить("Справки2",                Новый Массив);
	Результат.Вставить("Статус",                  Неопределено);
	Результат.Вставить("ТекстОшибки",             "");
	Результат.Вставить("ТипМарки",                "");
	Результат.Вставить("ТипУпаковки",             Неопределено);
	Результат.Вставить("ТипШтрихкода",            Неопределено);
	Результат.Вставить("Упаковка",                Неопределено);
	Результат.Вставить("Характеристика",          Неопределено);
	Результат.Вставить("Штрихкод",                Штрихкод);
	Результат.Вставить("ШтрихкодУпаковки",        Неопределено);
	Результат.Вставить("НазначениеШтрихкода",     "");
	Результат.Вставить("МаркируемаяПродукция",    Неопределено);
	Результат.Вставить("ДополнительныеПараметры", Неопределено);
	
	// Частичное выбытие
	Результат.Вставить("ЧастичноеВыбытие",                      Ложь);
	Результат.Вставить("ЕмкостьПотребительскойУпаковки",        Неопределено);
	Результат.Вставить("ЧастичноеВыбытиеВариантУчета",          Неопределено);
	Результат.Вставить("ЧастичноеВыбытиеНоменклатура",          Неопределено);
	Результат.Вставить("ЧастичноеВыбытиеХарактеристика",        Неопределено);
	
	Возврат Результат;
	
КонецФункции

Процедура ОбработатьСохраненныйВыборДанныхПоАлкогольнойПродукции(Форма, ДанныеШтрихкода) Экспорт

	Если ДанныеШтрихкода = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = ДанныеШтрихкода.ДополнительныеПараметры;
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("ЗапомнитьВыбор") 
		И ДополнительныеПараметры.ЗапомнитьВыбор Тогда
		
		Если ДополнительныеПараметры.Свойство("ДанныеВыбора")
			И ТипЗнч(ДополнительныеПараметры.ДанныеВыбора) = Тип("Структура") 
			И ДополнительныеПараметры.ДанныеВыбора.Количество() > 0 Тогда
			
			Форма.ДанныеВыбораПоМаркируемойПродукции = ДополнительныеПараметры.ДанныеВыбора;
			Форма.ДанныеВыбораПоМаркируемойПродукции.Вставить("АлкогольнаяПродукция", ДанныеШтрихКода.АлкогольнаяПродукция);
			Форма.ДанныеВыбораПоМаркируемойПродукции.Вставить("ТипШтрихКода", ДанныеШтрихКода.ТипШтрихКода);
			Форма.СохраненВыборПоМаркируемойПродукции = Истина;
			
			ШтрихкодированиеИСКлиентСервер.ОтобразитьСохраненныйВыборПоМаркируемойПродукции(Форма);
			
		КонецЕсли;
		
	ИначеЕсли Форма.ДанныеВыбораПоМаркируемойПродукции <> Неопределено Тогда
		
		ТребуетсяСбросВыбора = Ложь;
		
		Если Форма.ДанныеВыбораПоМаркируемойПродукции.ТипШтрихкода <> ДанныеШтрихкода.ТипШтрихкода Тогда
			
			ТребуетсяСбросВыбора = Истина;
			
		ИначеЕсли ДанныеШтрихкода.ТипШтрихкода = ПредопределенноеЗначение("Перечисление.ТипыШтрихкодов.PDF417")
		          И ЗначениеЗаполнено(ДанныеШтрихкода.АлкогольнаяПродукция)
		          И ДанныеШтрихкода.АлкогольнаяПродукция <> Форма.ДанныеВыбораПоМаркируемойПродукции.АлкогольнаяПродукция Тогда
			
			ТребуетсяСбросВыбора = Истина;
			
		КонецЕсли;
		
		Если ТребуетсяСбросВыбора Тогда
			
			Форма.ДанныеВыбораПоМаркируемойПродукции  = Неопределено;
			Форма.СохраненВыборПоМаркируемойПродукции = Ложь;
			ШтрихкодированиеИСКлиентСервер.ОтобразитьСохраненныйВыборПоМаркируемойПродукции(Форма);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция КодКлассификатораНоменклатурыЕГАИС(ШтрихкодАкцизнойМарки, КэшКодовАлкогольнойПродукции = Неопределено) Экспорт
	
	Если Сред(ШтрихкодАкцизнойМарки, 4, 5) = "00000" Тогда
		
		Значение = Сред(ШтрихкодАкцизнойМарки, 9, 11);
		КоличествоИтераций = 11;
		
	Иначе
		
		Значение = Сред(ШтрихкодАкцизнойМарки, 8, 12);
		КоличествоИтераций = 12;
		
	КонецЕсли;
	
	Если КэшКодовАлкогольнойПродукции <> Неопределено Тогда
		КодАлкогольнойПродукции = КэшКодовАлкогольнойПродукции[Значение];
	КонецЕсли;
	
	Если КодАлкогольнойПродукции = Неопределено Тогда
		
		Результат = 0;
		
		Для Итерация = 1 По КоличествоИтераций Цикл
			
			Сумматор = 1;
			Для Индекс = 1 По КоличествоИтераций - Итерация Цикл
				Сумматор = Сумматор * 36;
			КонецЦикла;
			
			Результат = Результат + Сумматор * (Найти("0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ", Сред(Значение, Итерация, 1)) - 1);
			
		КонецЦикла;
		
		КодАлкогольнойПродукции = Формат(Результат, "ЧЦ=19; ЧВН=; ЧГ=0");
		
		Если КэшКодовАлкогольнойПродукции <> Неопределено Тогда
			КэшКодовАлкогольнойПродукции.Вставить(Значение, КодАлкогольнойПродукции);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат КодАлкогольнойПродукции;
	
КонецФункции

#КонецОбласти
