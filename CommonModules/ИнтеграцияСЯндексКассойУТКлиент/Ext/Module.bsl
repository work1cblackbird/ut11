
#Область ЗагрузкаОпераций

Процедура НачатьЗагрузкуОперацийПоЯндексКассе(Знач ПараметрыЗагрузки, Знач ОписаниеОповещения = Неопределено, ВыводитьОкноОжидания = Истина) Экспорт
	
	ДлительнаяОперация = ИнтеграцияСЯндексКассойУТВызовСервера.НачатьЗагрузкуОперацийПоЯндексКассе(ПараметрыЗагрузки);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Загрузка операций по Яндекс.Кассе'");
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Ложь;
	ПараметрыОжидания.ВыводитьОкноОжидания = ВыводитьОкноОжидания;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Ложь;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ЭлементФормыНастроекПриИзменении(Контекст, Элемент) Экспорт
	
	Форма = Контекст.Форма;
	Префикс = Контекст.Префикс;
	ДополнительныеПараметрыФормы = Форма[Префикс + "ДополнительныеПараметрыФормы"];
	
	Если Элемент.Имя = Префикс + "СтатьяРасходов" Тогда
		
		ДоходыИРасходыКлиентСервер.СтатьяПриИзменении(Форма, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЭлементФормыНастроекНачалоВыбора(Контекст, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	Форма = Контекст.Форма;
	Префикс = Контекст.Префикс;
	ДополнительныеПараметрыФормы = Форма[Префикс + "ДополнительныеПараметрыФормы"];
	
	Если Элемент.Имя = Префикс + "СтатьяДвиженияДенежныхСредствОплаты"
		Или Элемент.Имя = Префикс + "СтатьяДвиженияДенежныхСредствВозвраты" Тогда 
		
		Если Элемент.Имя = Префикс + "СтатьяДвиженияДенежныхСредствОплаты" Тогда 
			ХозяйственнаяОперация = 
			ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента");
		ИначеЕсли Элемент.Имя = Префикс + "СтатьяДвиженияДенежныхСредствВозвраты" Тогда 
			ХозяйственнаяОперация = 
			ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту");
		Иначе
			Возврат;
		КонецЕсли;
		
		СтандартнаяОбработка = Ложь;
		
		Отбор = Новый Структура();
		Отбор.Вставить("ХозяйственнаяОперация", ХозяйственнаяОперация);
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Отбор",         Отбор);
		ПараметрыФормы.Вставить("ТекущаяСтрока", Форма[Элемент.Имя]);
		
		ОткрытьФорму("Справочник.СтатьиДвиженияДенежныхСредств.ФормаВыбора", 
		ПараметрыФормы, Элемент, Форма.УникальныйИдентификатор);
		
	ИначеЕсли Элемент.Имя = Префикс + "СтатьяРасходов" Тогда 
		
		ДоходыИРасходыКлиент.НачалоВыбораСтатьи(Форма, Элемент, СтандартнаяОбработка);
		
	ИначеЕсли Элемент.Имя = Префикс + "АналитикаРасходов" Тогда 
		
		ДоходыИРасходыКлиент.НачалоВыбораАналитикиРасходов(Форма, Элемент, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЭлементФормыНастроекОбработкаВыбора(Контекст, Элемент, ВыбранноеЗначение, СтандартнаяОбработка) Экспорт
	
	Форма = Контекст.Форма;
	Префикс = Контекст.Префикс;

КонецПроцедуры

Процедура ЭлементФормыНастроекСоздание(Контекст, Элемент, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Основание", ИнтеграцияСЯндексКассойУТВызовСервера.ДанныеЭквайераПоУмолчанию());
	
	ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта", ПараметрыФормы, Элемент, Контекст.Форма.УникальныйИдентификатор); 
	
КонецПроцедуры

#КонецОбласти

