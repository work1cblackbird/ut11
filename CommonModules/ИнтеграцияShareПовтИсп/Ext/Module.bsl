
#Область СлужебныйПрограммныйИнтерфейс

// Параметры соединения к сервисам.
//
// Параметры:
//  ИмяСервиса - Строка - имя сервиса "share".
// 
// Возвращаемое значение:
//  см. НовыйПараметрыСоединения
//
Функция ПараметрыСоединения(Знач ИмяСервиса, Знач АдресСервиса = Неопределено) Экспорт
	
	Если ИмяСервиса <> ИнтеграцияShareКлиентСервер.ИмяСервиса() Тогда
		ВызватьИсключение НСтр("ru = 'Передано не поддерживаемое имя сервиса'", ОбщегоНазначения.КодОсновногоЯзыка());
	КонецЕсли;
		
	Если Не ЗначениеЗаполнено(АдресСервиса) Тогда
		АдресСервиса = ИнтеграцияShareКлиентСервер.АдресСервиса();
	КонецЕсли;
		
	Результат = НовыйПараметрыСоединения(АдресСервиса, 443, Истина);
	Результат.Аутентификация = Ложь;
	Результат.Таймаут = 30;

	Возврат Результат;

КонецФункции

// Возвращает служебные данные заголовков запроса к сервису.
//
// Возвращаемое значение:
//  Соответствие из Строка - набор служебных заголовков для запроса в сервис.
//
Функция СлужебныеДанныеЗаголовковЗапроса() Экспорт
	
	ДанныеЗаголовков = Новый Соответствие;
	ДанныеЗаголовков.Вставить(ИмяЗаголовкаИдентификаторВнешнейСистемы(),
		СтандартныеПодсистемыСервер.ИдентификаторИнформационнойБазы());
	ДанныеЗаголовков.Вставить(ИмяЗаголовкаНаименованиеВнешнейСистемы(),
		СтроковыеФункции.СтрокаЛатиницей(Метаданные.Имя));
	ДанныеЗаголовков.Вставить(ИмяЗаголовкаВерсияВнешнейСистемы(), Метаданные.Версия);
	
	СистемнаяИнформация = Новый СистемнаяИнформация();
	ДанныеЗаголовков.Вставить(ИмяЗаголовкаВерсияПлатформыВнешнейСистемы(), СистемнаяИнформация.ВерсияПриложения);
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеАбонентаИТС = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ДанныеАбонентаИТС <> Неопределено Тогда
		ДанныеЗаголовков.Вставить(ИмяЗаголовкаАбонентИТС(), ДанныеАбонентаИТС.Логин);
	КонецЕсли;
	
	ДанныеЗаголовков.Вставить(ИмяЗаголовкаПользователь(), Строка(Пользователи.АвторизованныйПользователь()));
	
	Возврат ДанныеЗаголовков;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Конструкторы

// Возвращает параметры соединения.
// 
// Возвращаемое значение:
//  Структура - параметры соединения:
//   * Сервер - Строка - имя сервера.
//   * Порт - Число - порт соединения.
//   * Аутентификация - Булево - требуется аутентификация по токену.
//   * Таймаут - Число - длительность ожидания ответа в секундах.
//   * ЗащищенноеСоединение - ЗащищенноеСоединениеOpenSSL - объект защищенного соединения OpenSSL.
//                          - Неопределено - объект защищенного соединения не установлен.
//   * Прокси - ИнтернетПрокси - параметры прокси-сервера.
//
Функция НовыйПараметрыСоединения(Сервер = "", Порт = 80, ЗащищенноеСоединение = Ложь)

	Результат = Новый Структура;
	Результат.Вставить("Сервер"              , Сервер);
	Результат.Вставить("Порт"                , Порт);
	Результат.Вставить("Аутентификация"      , Ложь);
	Результат.Вставить("Таймаут"             , 30);
	Результат.Вставить("Прокси"              , Новый ИнтернетПрокси);
	Результат.Вставить("ЗащищенноеСоединение", Неопределено);
	
	Если ЗащищенноеСоединение Тогда
		Результат.ЗащищенноеСоединение = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение(,
			Новый СертификатыУдостоверяющихЦентровОС);
		Результат.Прокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
	Иначе
		Результат.Прокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси("http");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеЗаголовки

Функция ИмяЗаголовкаИдентификаторВнешнейСистемы()
	
	Возврат "x-vnd-1cshare-endpointid";
	
КонецФункции

Функция ИмяЗаголовкаВерсияВнешнейСистемы()
	
	Возврат "x-vnd-1cshare-app-version";
	
КонецФункции

Функция ИмяЗаголовкаНаименованиеВнешнейСистемы()
	
	Возврат "x-vnd-1cshare-app-name";
	
КонецФункции

Функция ИмяЗаголовкаВерсияПлатформыВнешнейСистемы()
	
	Возврат "x-vnd-1cshare-platform-version";
	
КонецФункции

Функция ИмяЗаголовкаАбонентИТС()
	
	Возврат "x-abonent-its";
	
КонецФункции

Функция ИмяЗаголовкаПользователь()
	
	Возврат "x-user";
	
КонецФункции

#КонецОбласти

#КонецОбласти