#Область ПрограммныйИнтерфейс

#Область ПараметрыВыбора

// Устанавливает параметры выбора номенклатуры.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой нужно установить параметры выбора
//  ВидыПродукции - ПеречислениеСсылка.ВидыПродукцииИС, Массив Из ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции.
//  ИмяПоляВвода - Строка - имя поля ввода номенклатуры.
Процедура УстановитьПараметрыВыбораНоменклатуры(Форма, ВидыПродукции = Неопределено, ИмяПоляВвода = "ТоварыНоменклатура") Экспорт
	
	//++ НЕ ГОСИС
	ПараметрыВыбора = Новый Массив;
	
	Для Каждого ПараметрВыбора Из Форма.Элементы[ИмяПоляВвода].ПараметрыВыбора Цикл
		Если ПараметрВыбора.Имя <> "Отбор.ОсобенностьУчета"
				И ПараметрВыбора.Имя <> "Отбор.АлкогольнаяПродукцияВоВскрытойТаре"
				И ПараметрВыбора.Имя <> "Отбор.ТипНоменклатуры" Тогда
			ПараметрыВыбора.Добавить(ПараметрВыбора);
		КонецЕсли;
	КонецЦикла;
	
	Если ТипЗнч(ВидыПродукции) = Тип("Массив") Или ТипЗнч(ВидыПродукции) = Тип("ФиксированныйМассив") Тогда
		ОсобенностиУчета = Новый Массив;
		Для Каждого ВидПродукции Из ВидыПродукции Цикл
			ОсобенностьУчета = ИнтеграцияИСУТКлиентСервер.ОсобенностьУчетаПоВидуПродукции(ВидПродукции);
			Если ЗначениеЗаполнено(ОсобенностьУчета) Тогда
				ОсобенностиУчета.Добавить(ОсобенностьУчета);
			КонецЕсли;
		КонецЦикла;
	Иначе
		ОсобенностиУчета = ИнтеграцияИСУТКлиентСервер.ОсобенностьУчетаПоВидуПродукции(ВидыПродукции);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОсобенностиУчета) Тогда
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ТипНоменклатуры",  ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар")));
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ОсобенностьУчета", ОсобенностиУчета));
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.АлкогольнаяПродукцияВоВскрытойТаре", Ложь));
	КонецЕсли;
	
	Форма.Элементы[ИмяПоляВвода].ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	//-- НЕ ГОСИС
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
