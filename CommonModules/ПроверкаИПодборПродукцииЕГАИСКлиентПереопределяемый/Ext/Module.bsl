#Область СлужебныйПрограммныйИнтерфейс

// Заполняет специфичные параметры открытия формы проверки и подбора алкогольной продукции в зависимости от точки вызова
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма из которой происходит открытие формы проверки и подбора:
//  * Объект - ДанныеФормыСтруктураСКоллекцией - основной объект формы:
//  ** Ссылка - ДокументСсылка - ссылка на объект БД.
//  Параметры - (См. ПроверкаИПодборПродукцииЕГАИСКлиент.ПараметрыОткрытияФормыПроверкиИПодбора)
Процедура  ПриУстановкеПараметровОткрытияФормыПроверкиИПодбора(Форма, Параметры) Экспорт
	
	Если Форма.ИмяФормы = "Документ.ЧекККМ.Форма.ФормаДокументаРМК"
		Или Форма.ИмяФормы = "Документ.ЧекККМВозврат.Форма.ФормаДокументаРМК" Тогда
	
		Параметры.ОрганизацияЕГАИС         = Форма.Объект.ОрганизацияЕГАИС;
		Параметры.НачальныйСтатусПроверки  = ПредопределенноеЗначение("Перечисление.СтатусыПроверкиНаличияПродукцииИС.ВНаличии");
		Параметры.СообщениеПротоколаОбмена = ИнтеграцияЕГАИСВызовСервера.ПоследнееСообщение(Форма.Объект.Ссылка);
		Параметры.ЧастичноеВыбытиеДоступно = Истина;
		
		Параметры.ОписаниеОповещенияПриЗакрытии = Новый ОписаниеОповещения(
			"ПриЗакрытииФормыПроверкиИПодбора",
			СобытияФормЕГАИСУТКлиент,
			Новый Структура("Форма", Форма));
		
		Объект = Новый Структура;
		Объект.Вставить("ОрганизацияЕГАИС", Форма.Объект.ОрганизацияЕГАИС);
		Объект.Вставить("Ссылка", Форма.Объект.Ссылка);
		Объект.Вставить("АкцизныеМарки", Форма.Объект.АкцизныеМарки);
		Объект.Вставить("Товары", Форма.Объект.Товары);
		
		Если Форма.Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЧековККМ.Пробит") Тогда
			Параметры.РежимПросмотра = Истина;
			Параметры.ПроверятьНеобходимостьПеремаркировки = Ложь;
		КонецЕсли;
		
		Параметры.АдресДанныхПроверяемойАлкогольнойПродукции = ПроверкаИПодборПродукцииЕГАИСУТВызовСервера.АдресДанныхПроверкиАлкогольнойПродукции(
			Неопределено,
			Объект,
			Форма.УникальныйИдентификатор);
		
	КонецЕсли;
	
	Если Форма.ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ВозвратТоваровПоставщику.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ПеремещениеТоваров.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.СборкаТоваров.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ВнутреннееПотребление.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ПередачаТоваровХранителю.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.РасходныйОрдерНаТовары.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ОтборРазмещениеТоваров.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ПередачаТоваровМеждуОрганизациями.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ВозвратТоваровМеждуОрганизациями.Форма.ФормаДокумента"



	Тогда
		Параметры.БезУчетаОрганизацииЕГАИС = Истина;
		Параметры.ПроверкаНеПоДокументу = Ложь;
		Параметры.ПроверяемыйДокумент = Форма.Объект.Ссылка;
		Параметры.ОписаниеОповещенияПриЗакрытии = Новый ОписаниеОповещения(
			"ПриЗакрытииФормыПроверкиИПодбора",
			СобытияФормЕГАИСУТКлиент,
			Новый Структура("Форма", Форма));
	КонецЕсли;
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти