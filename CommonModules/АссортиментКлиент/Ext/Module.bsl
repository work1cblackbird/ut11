
#Область СлужебныеПроцедурыИФункции

Процедура ОбработкаРасшифровкиИсполненияАссортимента(ЭтаФорма, Расшифровка, СтандартнаяОбработка) Экспорт
	
	ТипРасшифровки = ТипЗнч(Расшифровка);
	Если ТипРасшифровки = Тип("Структура") Тогда
		СтандартнаяОбработка = Ложь;
		Если Расшифровка.ИмяГруппировки = "ВидНоменклатуры"
			ИЛИ Расшифровка.ИмяГруппировки = "ТоварнаяКатегория" Тогда
			
			НовыеНастройки = Неопределено; // НастройкиКомпоновкиДанных - 
			АссортиментВызовСервера.КопироватьИДополнитьНастройки(ЭтаФорма.Отчет.КомпоновщикНастроек, НовыеНастройки, Расшифровка);
			
			НовыеНастройки.Отбор.Элементы.Очистить();
			Для Каждого ЭлементНастроек Из НовыеНастройки.ПараметрыДанных.Элементы Цикл
				ЭлементНастроек.Использование = Ложь;
			КонецЦикла;
			
			Отбор = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(Расшифровка);
			
			ПараметрыОткрытия = Новый Структура;
			Расшифровка.Вставить("ПрименяемыеНастройки", НовыеНастройки);
			ПараметрыОткрытия.Вставить("Расшифровка", Расшифровка);
			ПараметрыОткрытия.Вставить("ПериодРасшифровки", Расшифровка.Период);
			ПараметрыОткрытия.Вставить("ФиксированныеНастройки", НовыеНастройки);
			ПараметрыОткрытия.Вставить("ЭтоРасшифровка", Истина);
			ПараметрыОткрытия.Вставить("ИмяГруппировкиРасшифровки", Расшифровка.ИмяГруппировки);
			ПараметрыОткрытия.Вставить("КлючВарианта", "ПоКатегорииКонтекст");
			ПараметрыОткрытия.Вставить("СформироватьПриОткрытии", Истина);
			ПараметрыОткрытия.Вставить("Отбор", Отбор);
			ОткрытьФорму(
				"Отчет.АнализИсполненияАссортимента.ФормаОбъекта",
				ПараметрыОткрытия,
				ЭтаФорма,
				Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
// 	ЭтаФорма - ФормаКлиентскогоПриложения - в форме обязательный реквизит:
//  * Отчет - ОтчетОбъект - 
// 	Расшифровка - Структура -
// 	СтандартнаяОбработка - Булево -
Процедура ОбработкаРасшифровкиСравнительногоАнализаАссортимента(ЭтаФорма, Расшифровка, СтандартнаяОбработка) Экспорт
	
	ТипРасшифровки = ТипЗнч(Расшифровка);
	Если ТипРасшифровки = Тип("Структура") Тогда
		СтандартнаяОбработка = Ложь;
		Если Расшифровка.ИмяГруппировки = "ВидНоменклатуры"
			ИЛИ Расшифровка.ИмяГруппировки = "ТоварнаяКатегория" Тогда
			
			ТекущаяОбласть = ЭтаФорма.ТекущийЭлемент.ТекущаяОбласть; // ОбластьЯчеекТабличногоДокумента
			НомерКолонкиРасшифровки = ТекущаяОбласть.Лево;
			
			Расшифровка.Вставить("АнализируемыйПериод", Неопределено);
			Расшифровка.Вставить("ПериодДляСравнения", Неопределено);
			Расшифровка.Вставить("НомерКолонкиРасшифровки", НомерКолонкиРасшифровки);
			
			Для Каждого Элемент Из ЭтаФорма.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
				Если ТипЗнч(Элемент) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
					Если СокрЛП(Элемент.Параметр) = "АнализируемыйПериод" Тогда
						Расшифровка.АнализируемыйПериод = Элемент.Значение;
					ИначеЕсли СокрЛП(Элемент.Параметр) = "ПериодДляСравнения" Тогда
						Расшифровка.ПериодДляСравнения = Элемент.Значение;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			НовыеНастройки = Неопределено;
			АссортиментВызовСервера.КопироватьИДополнитьНастройки(ЭтаФорма.Отчет.КомпоновщикНастроек, НовыеНастройки, Расшифровка);
			
			ПараметрыОткрытия = Новый Структура;
			ПараметрыОткрытия.Вставить("ФиксированныеНастройки", НовыеНастройки);
			ПараметрыОткрытия.Вставить("ЭтоРасшифровка", Истина);
			ПараметрыОткрытия.Вставить("КлючВарианта", "ПоНоменклатуреКонтекст");
			ПараметрыОткрытия.Вставить("СформироватьПриОткрытии", Истина);
			ОткрытьФорму(
				"Отчет.СравнительныйАнализДинамикиАссортиментаИПродаж.ФормаОбъекта",
				ПараметрыОткрытия,
				ЭтаФорма,
				Истина,);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

