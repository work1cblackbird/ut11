
#Область ПрограммныйИнтерфейс

#Область ПроцедурыПроверкиКорректностиНомераТаможеннойДекларации

// Возвращает структуру регистрационного номера таможенной декларации по переданному полному номеру декларации на товары,
// а также проверяет корректность ввода номера таможенной декларации.
//
// Параметры:
//	НомерТаможеннойДекларации - Строка - номер таможенной декларации или регистрационный номер таможенной декларации.
//	НачалоКорректногоПериода - Дата - дата начала периода проверки.
//	КонецКорректногоПериода - Дата - дата окончания периода проверки.
//
// Возвращаемое значение:
//	Структура:
//		* РегистрационныйНомер - Строка - регистрационный номер таможенной декларации либо пустая строка, если его
//											не удалось определить.
//		* ПорядковыйНомерТовара - Строка - порядковый номер товара.
//		* КодОшибки - Число - код ошибки.
//
Функция ПроверитьКорректностьНомераТаможеннойДекларации(НомерТаможеннойДекларации,
														НачалоКорректногоПериода = Неопределено,
														КонецКорректногоПериода = Неопределено) Экспорт
	
	СтруктураНомера = Новый Структура("РегистрационныйНомер, ПорядковыйНомерТовара, КодОшибки", "", "", 0);
	
	//++ Локализация
	СтруктураНомера = УчетНДСРФКлиентСервер.ПроверитьКорректностьНомераТаможеннойДекларации(НомерТаможеннойДекларации,
																							НачалоКорректногоПериода,
																							КонецКорректногоПериода);
	//-- Локализация
	
	Возврат СтруктураНомера;
	
КонецФункции

// Возвращает текстовое описание ошибки при вводе номера таможенной декларации.
//
// Параметры:
//	КодОшибки - Число - код ошибки
//
// Возвращаемое значение:
//	Строка - текстовое описание ошибки в номере таможенной декларации.
//
Функция ТекстОшибкиВНомереТаможеннойДекларации(КодОшибки) Экспорт
	
	ТекстОшибки = "";
	
	//++ Локализация
	ТекстОшибки = УчетНДСРФКлиентСервер.ТекстОшибкиВНомереТаможеннойДекларации(КодОшибки);
	//-- Локализация
	
	Возврат ТекстОшибки;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Дополняет список типов налогообложения НДС, для которых в документах скрываются реквизиты ставки и суммы НДС.
//
// Параметры:
//  Массив - см. УчетНДСУПКлиентСервер.ТипыНалогообложенияСкрывающиеРеквизитыНДС
//
Процедура ДополнитьТипыНалогообложенияСкрывающиеРеквизитыНДС(Массив) Экспорт
	
	//++ Локализация
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.НалоговыйАгентПоНДС"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.РеализацияРаботУслугНаЭкспорт"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДСВСтранеЕАЭС"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаПоПатенту"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДССМежценовойРазницы"));
	//-- Локализация
	
КонецПроцедуры

// Рассчитывает сумму НДС исходя из суммы и флагов налогообложения.
//
// Параметры:
//  Сумма            - Число - сумма от которой надо рассчитывать налоги;
//  СтавкаНДС        - Число, СправочникСсылка.СтавкиНДС - Значение или ссылка на ставку НДС.
//  СуммаВключаетНДС - Булево - признак включения НДС в сумму ("внутри" или "сверху");
//  НалогообложениеНДС - ПеречислениеСсылка.ТипыНалогообложенияНДС - налогообложение документа
//
// Возвращаемое значение:
//  Число - полученная сумма НДС.
//
Функция РассчитатьСуммуНДС(Сумма, СтавкаНДС, СуммаВключаетНДС = Истина, НалогообложениеНДС = Неопределено) Экспорт

	СуммаНДС = Неопределено;
	
	//++ Локализация
	Если НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ЭлектронныеУслуги") Тогда
		Если ТипЗнч(СтавкаНДС) = Тип("СправочникСсылка.СтавкиНДС") Тогда
			ЗначениеСтавкиНДС = УчетНДСУПКлиентСервер.ЗначениеСтавкиНДС(СтавкаНДС);
		Иначе
			ЗначениеСтавкиНДС = СтавкаНДС; 
		КонецЕсли;
	
		СуммаНДС = Окр(Сумма * ЗначениеСтавкиНДС / 100, 2, РежимОкругления.Окр15как20);
	КонецЕсли;
	//-- Локализация
	
	Возврат СуммаНДС;
	
КонецФункции

#КонецОбласти
