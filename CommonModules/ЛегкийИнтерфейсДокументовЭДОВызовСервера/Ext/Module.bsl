// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

// Параметры:
//  ПодписанныйОбъект - СправочникСсылка.СообщениеЭДОПрисоединенныеФайлы
//  ПорядковыйНомерПодписи - Число
// 
// Возвращаемое значение:
//  Структура:
// * СообщениеЭДО - ДокументСсылка.СообщениеЭДО 
// * ДанныеПодписи - Неопределено
//                 - См. ЭлектронныеДокументыЭДОКлиентСервер.НовыеДанныеПодписиСУчетомДоверенности
Функция ДанныеПодписиОбъектаДляОткрытияРезультатаПроверки(Знач ПодписанныйОбъект, Знач ПорядковыйНомерПодписи) Экспорт

	Результат = Новый Структура("СообщениеЭДО, ДанныеПодписи", Документы.СообщениеЭДО.ПустаяСсылка(), Неопределено);
	
	Результат.СообщениеЭДО = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПодписанныйОбъект, "ВладелецФайла");
	
	ПодписиОбъекта = ЭлектронныеДокументыЭДО.УстановленныеПодписиФайлаСУчетомДоверенностей(
		ПодписанныйОбъект, ПорядковыйНомерПодписи);
		
	Если ПодписиОбъекта.Количество() Тогда
		Результат.ДанныеПодписи = ПодписиОбъекта[0];
		ИнтерфейсДокументовЭДО.СкорректироватьДатыПодписиНаЧасовойПоясКлиента(Результат.ДанныеПодписи);
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Параметры:
//  ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  ЭтоОблачныйЭДО - Булево
//  ИдентификаторФормы - УникальныйИдентификатор
//
// Возвращаемое значение:
//  Строка - результат получения данных файла во временном хранилище См. ЛегкийИнтерфейсДокументовЭДО.ДанныеФайлаИнформацииОтправителяДокумента
Функция ДанныеФайлаИнформацииОтправителяДокумента(Знач ЭлектронныйДокумент, Знач ЭтоОблачныйЭДО, 
	Знач ИдентификаторФормы) Экспорт
	
	ДанныеФайла = ЛегкийИнтерфейсДокументовЭДО.ДанныеФайлаИнформацииОтправителяДокумента(ЭлектронныйДокумент,
		ЭтоОблачныйЭДО, ИдентификаторФормы);
	Возврат ПоместитьВоВременноеХранилище(ДанныеФайла, ИдентификаторФормы); 

КонецФункции

#Область ОтражениеВУчете

// Параметры:
//  ЭлектронныеДокументы - Массив Из ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
// 
// Возвращаемое значение:
//  Массив Из См. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента
Функция НоменклатураЭлектронныхДокументовДляСопоставления(Знач ЭлектронныеДокументы) Экспорт
	
	ДанныеДляОтраженияВУчете =
		ЭлектронныеДокументыЭДО.ДанныеДокументовДляОтраженияВУчете(ЭлектронныеДокументы)[0];
	
	Если ИнтеграцияЭДО.ОтключенКонтрольОтраженияВУчетеВНастройках(ДанныеДляОтраженияВУчете) Тогда
		НаборНоменклатурыКонтрагентов = 
			ИнтеграцияЭДО.НоменклатураЭлектронногоДокументаБезСопоставления(ДанныеДляОтраженияВУчете);
	Иначе
		ОтборПоДокументам = ИнтеграцияЭДО.ОтборНоменклатурыКонтрагентовНаКонтроле(ЭлектронныеДокументы);
		НаборНоменклатурыКонтрагентов = ИнтеграцияЭДО.НоменклатураКонтрагентовНаКонтроле(ОтборПоДокументам);
	КонецЕсли;
	
	Возврат НаборНоменклатурыКонтрагентов;
	
КонецФункции

// Параметры:
//  СпособОбработки - Строка - один из способов обработки входящего электронного документа.
// 
// Возвращаемое значение:
//  - Неопределено
//  - Структура:
//    * ИмяОбъектаМетаданных - Строка
//    * ИмяТипаСсылки - Строка
Функция ПараметрыОбъектаУчетаПоСпособуОбработки(Знач СпособОбработки) Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("ИмяОбъектаМетаданных", "");
	Параметры.Вставить("ИмяТипаСсылки", "");
	
	ИмяТипа = ИнтеграцияЭДО.ИмяДокументаПоСпособуОбработки(СпособОбработки);
	
	МетаданныеОбъектаДокумент = Метаданные.Документы.Найти(ИмяТипа);
	Если МетаданныеОбъектаДокумент <> Неопределено Тогда
		Параметры.ИмяОбъектаМетаданных = МетаданныеОбъектаДокумент.ПолноеИмя();
		Параметры.ИмяТипаСсылки = "ДокументСсылка." + ИмяТипа;
		Возврат Параметры;
	КонецЕсли;
	
	МетаданныеОбъектаСправочник = Метаданные.Справочники.Найти(ИмяТипа);
	Если МетаданныеОбъектаСправочник <> Неопределено Тогда
		Параметры.ИмяОбъектаМетаданных = МетаданныеОбъектаСправочник.ПолноеИмя();
		Параметры.ИмяТипаСсылки = "СправочникСсылка." + ИмяТипа;
		Возврат Параметры;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Параметры:
//  СпособыОбработкиДокументов - Соответствие Из КлючИЗначение:
//  * Ключ - ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * Значение - Строка - Способ обработки
//  ИдентификаторФормы - УникальныйИдентификатор
// 
// Возвращаемое значение:
//  См. ДлительныеОперации.ВыполнитьФункцию
Функция СоздатьОбъектыУчетаПоДокументамЭДОВФоне(Знач СпособыОбработкиДокументов, Знач ИдентификаторФормы) Экспорт
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	
	// См. ЛегкийИнтерфейсДокументовЭДО.СоздатьОбъектыУчетаПоДокументамЭДО
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполненияВФоне,
		"ЛегкийИнтерфейсДокументовЭДО.СоздатьОбъектыУчетаПоДокументамЭДО", СпособыОбработкиДокументов);
	
КонецФункции

// Параметры:
//  АдресРезультата - Строка
//
// Возвращаемое значение:
//   Структура:
//   * ЕстьОшибка - Булево
//   * КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//   * СозданныеОбъектыУчета - Массив Из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
Функция ОбработатьРезультатСозданияОбъектовУчета(Знач АдресРезультата) Экспорт
	
	РезультатОбработки = Новый Структура;
	РезультатОбработки.Вставить("ЕстьОшибка", Ложь);
	РезультатОбработки.Вставить("КонтекстДиагностики", Новый Структура);
	РезультатОбработки.Вставить("СозданныеОбъектыУчета", Новый Массив);
	
	РезультатСозданияОбъектовУчета = ПолучитьИзВременногоХранилища(АдресРезультата); // См. ЛегкийИнтерфейсДокументовЭДО.СоздатьОбъектыУчетаПоДокументамЭДО
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	РезультатОбработки.КонтекстДиагностики = РезультатСозданияОбъектовУчета.КонтекстДиагностики;
	РезультатОбработки.СозданныеОбъектыУчета = 
		РезультатСозданияОбъектовУчета.ОбъектыУчетаДокументов.ВыгрузитьКолонку("ОбъектУчета");
	Если Не ЗначениеЗаполнено(РезультатОбработки.СозданныеОбъектыУчета) Тогда
		РезультатОбработки.ЕстьОшибка = Истина;
	КонецЕсли;
	
	Возврат РезультатОбработки;
	
КонецФункции

#КонецОбласти // ОтражениеВУчете

#КонецОбласти
