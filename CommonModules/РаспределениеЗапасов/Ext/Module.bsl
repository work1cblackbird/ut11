////////////////////////////////////////////////////////////////////////////////
// Модуль "Распределение запасов", содержит процедуры и функции
// чтения и записи в регистр сведений "Распределение запасов".
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Процедура АктуализацияПотребностейПоГраницеОбеспеченияРегламентноеЗадание() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.АктуализацияПотребностейПоГраницеОбеспечения);
		
	АктуализацияПотребностейПоГраницеОбеспеченияФоновоеЗадание(Неопределено, Неопределено);
	
КонецПроцедуры

Процедура ЗапуститьФоновоеЗаданиеАктуализацииПотребностейПоГраницеОбеспечения() Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыХотяБыОдногоВида") Тогда
		Возврат;
	КонецЕсли;
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне();
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Актуализация потребностей по границе обеспечения'");
	
	ДлительныеОперации.ВыполнитьВФоне("РаспределениеЗапасов.АктуализацияПотребностейПоГраницеОбеспеченияФоновоеЗадание",
		Новый Структура, ПараметрыВыполнения);
	
КонецПроцедуры

Процедура АктуализацияПотребностейПоГраницеОбеспеченияФоновоеЗадание(ПараметрыПроцедуры, АдресРезультата) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Текст =
		"ВЫБРАТЬ
		|	ДанныеИБ.Номенклатура КАК Номенклатура,
		|	ДанныеИБ.Характеристика КАК Характеристика,
		|	ДанныеИБ.Склад КАК Склад,
		|	ДанныеИБ.Назначение КАК Назначение,
		|	ДанныеИБ.Заказ КАК Заказ,
		|	ДанныеИБ.ДатаСобытия КАК ДатаСобытия,
		|	ДанныеИБ.РезервироватьПоМереПоступленияОстаток КАК РезервироватьПоМереПоступления,
		|	ДанныеИБ.ОтложитьРезервированиеОстаток КАК ОтложитьРезервирование,
		|	НЕ УпорядочиваниеПоДатеДокумента.Значение ЕСТЬ NULL
		|		ИЛИ НЕ КонтрольРезервироватьПоМере.Значение ЕСТЬ NULL КАК ОтложенноеОбеспечениеЗапрещено
		|ПОМЕСТИТЬ ДанныеЗаказов
		|ИЗ
		|	РегистрНакопления.ЗапасыИПотребности.Остатки(, ДатаСобытия > ДАТАВРЕМЯ(1,1,1)) КАК ДанныеИБ
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ВариантУпорядочиванияПотребностейПоЗаказамДляОбеспечения КАК УпорядочиваниеПоДатеДокумента
		|		ПО УпорядочиваниеПоДатеДокумента.Значение = ЗНАЧЕНИЕ(Перечисление.ВариантыУпорядочиванияПотребностейПоЗаказамДляОбеспечения.ПриоритетИДатаДокумента)
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.КонтролироватьЗапасыТоваровПодлежащихРезервированиюПоМереПоступления КАК КонтрольРезервироватьПоМере
		|		ПО КонтрольРезервироватьПоМере.Значение
		|ГДЕ
		|	ДанныеИБ.РезервироватьПоМереПоступленияОстаток <> 0
		|		И УпорядочиваниеПоДатеДокумента.Значение ЕСТЬ NULL
		|		И КонтрольРезервироватьПоМере.Значение ЕСТЬ NULL // Допустимо отложенное обеспечение
		|		ИЛИ ДанныеИБ.ОтложитьРезервированиеОстаток <> 0
		|ИНДЕКСИРОВАТЬ ПО
		|	ОтложенноеОбеспечениеЗапрещено
		|;
		|
		|/////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.Склад КАК Склад
		|ПОМЕСТИТЬ РазличныеТовары
		|ИЗ
		|	ДанныеЗаказов КАК Товары
		|ГДЕ
		|	НЕ Товары.ОтложенноеОбеспечениеЗапрещено
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад
		|;
		|
		|/////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РазличныеТовары.Номенклатура КАК Номенклатура,
		|	РазличныеТовары.Характеристика КАК Характеристика,
		|	РазличныеТовары.Склад КАК Склад,
		|	РасчетПереопределяемый.ГраницаПериода КАК Дата
		|ПОМЕСТИТЬ ГраницыПериодаОбеспечения
		|ИЗ
		|	РазличныеТовары КАК РазличныеТовары
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РасчетПереопределяемый ПО ИСТИНА
		|ГДЕ
		|	ЕСТЬNULL(РасчетПереопределяемый.ГраницаПериода, ДАТАВРЕМЯ(1, 1, 1)) > &НачалоТекущегоДня
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад
		|;
		|
		|/////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Движения.Регистратор КАК Регистратор
		|ИЗ
		|	ДанныеЗаказов КАК ДанныеЗаказов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ГраницыПериодаОбеспечения КАК ГраницыПериода
		|		ПО ГраницыПериода.Номенклатура = ДанныеЗаказов.Номенклатура
		|		 И ГраницыПериода.Характеристика = ДанныеЗаказов.Характеристика
		|		 И ГраницыПериода.Склад = ДанныеЗаказов.Склад
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыИПотребности КАК Движения
		|		ПО Движения.Заказ = ДанныеЗаказов.Заказ
		|ГДЕ
		|	ДанныеЗаказов.ОтложитьРезервирование <> 0
		|			И (ГраницыПериода.Дата ЕСТЬ NULL ИЛИ ГраницыПериода.Дата >= ДанныеЗаказов.ДатаСобытия)
		|		ИЛИ ДанныеЗаказов.РезервироватьПоМереПоступления <> 0
		|			И ГраницыПериода.Дата < ДанныеЗаказов.ДатаСобытия";
	
	Подстановки = ОбеспечениеВДокументахСервер.ПодстановкиГраницыОбеспечиваемогоПериода(
		"РазличныеТовары.Номенклатура", "РазличныеТовары.Характеристика", "РазличныеТовары.Склад", "&НачалоТекущегоДня", "ИСТИНА");
	
	Текст = СтрЗаменить(Текст, "РасчетПереопределяемый.ГраницаПериода", Подстановки.Поле);
	Текст = СтрЗаменить(Текст, "ЛЕВОЕ СОЕДИНЕНИЕ РасчетПереопределяемый ПО ИСТИНА", Подстановки.Соединения);
	
	Запрос = Новый Запрос(Текст);
	Запрос.УстановитьПараметр("НачалоТекущегоДня", НачалоДня(ТекущаяДатаСеанса()));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Набор = РегистрыНакопления.ЗапасыИПотребности.СоздатьНаборЗаписей();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			МетаданныеДокумента = Выборка.Регистратор.Метаданные(); // ОбъектМетаданныхДокумент -
			БлокировкаДанных = Новый БлокировкаДанных();
			ЭлементБлокировки = БлокировкаДанных.Добавить(МетаданныеДокумента.ПолноеИмя());
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Регистратор);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			БлокировкаДанных.Заблокировать();
			
			ЗаписатьДвижения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.Регистратор, "Проведен");
			Если ТипЗнч(ЗаписатьДвижения) <> Тип("Булево") Тогда
				ЗаписатьДвижения = Ложь;
			КонецЕсли;
			Если ЗаписатьДвижения Тогда
				НаборЗаписей = РегистрыНакопления.ЗапасыИПотребности.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
				Набор.Отбор.Регистратор.Значение = Выборка.Регистратор;
				Набор.Прочитать();
				ВсегоСтрок = Набор.Количество();
				Для Счетчик = 1 По ВсегоСтрок Цикл
					СтрокаНабора = Набор[ВсегоСтрок - Счетчик];
					Если СтрокаНабора.ТипЗаписи <> Перечисления.ТипыЗаписейЗапасыИПотребности.ПервичнаяЗапись Тогда
						Набор.Удалить(СтрокаНабора);
					КонецЕсли;
				КонецЦикла;
				МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
				Набор.ДополнительныеСвойства.Вставить("РассчитыватьИзменения", Истина);
				Набор.ДополнительныеСвойства.Вставить("СвойстваДокумента", Новый Структура("ЭтоНовый", Ложь));
				Набор.ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
				Набор.ДополнительныеСвойства.Вставить("ТаблицыКонтроля", Новый Структура);
				Набор.ДополнительныеСвойства.Вставить("АктуализацияОтложенногоОбеспечения");
				Набор.Записать();
				ЭмуляцияДокумента = ПроведениеДокументов.ЭмуляцияДокумента(Неопределено, Неопределено);
				РаспределениеЗапасовДвижения.ОтразитьЗаданияКРаспределениюЗапасов(ЭмуляцияДокумента, МенеджерВременныхТаблиц);
			КонецЕсли;
		ЗафиксироватьТранзакцию();
		Исключение
		
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru = 'Регламентное задание ""Актуализация потребностей по границе обеспечения"" завершилось неудачно по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			СобытиеЖурнала = НСтр("ru = 'Распределение запасов'", ОбщегоНазначения.КодОсновногоЯзыка());
			ЗаписьЖурналаРегистрации(СобытиеЖурнала,
			                         УровеньЖурналаРегистрации.Ошибка,
			                         Метаданные.РегламентныеЗадания.АктуализацияПотребностейПоГраницеОбеспечения,
			                         ,
			                         ТекстСообщения);
			
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

// Выполняет распределение в фоне
//
// Параметры:
//  Задания - ТаблицаЗначений - колонки соответствуют измерениям, ресурсам и реквизитам регистра сведений
//                              "ЗаданияКРаспределениюЗапасов", есть и служебные колонки:
//             * Номенклатура         - СправочникСсылка.Номенклатура -
//             * Характеристика       - СправочникСсылка.ХарактеристикиНоменклатуры -
//             * Склад                - СправочникСсылка.СтруктураПредприятия, СправочникСсылка.Склады  -
//             * Назначение           - СправочникСсылка.Назначения   -
//             * РазделительЗаписи    - УникальныйИдентификатор -
//             * ЗаказНаОтгрузку      - ОпределяемыйТип.ОжидаемаяОтгрузка -
//             * ИдентификаторЗаписи  - УникальныйИдентификатор - 
//             * КСнятиюРезерва       - Число - 
//             * ДатаЗаписи           - Дата - 
//             * ИдентификаторЗадания - УникальныйИдентификатор - служебная
//             * ДатаЗадания          - Дата                    - служебная
//  ИдентификаторыНеОбработанныхЗаписей - Соответствие из УникальныйИдентификатор - ключ, это идентификатор 
//                                                                                   не выполненных (выдающих ошибку при
//                                                                                   выполнении) записей регистра
//                                                                                   сведений очереди заданий
//                                                                                   (измерение ИдентификаторЗаписи
//                                                                                   регистра сведений очереди заданий),
//                                                                                  значение, это представление ошибки.
//  ДополнительныеСвойства - Неопределено, Структура - дополнительные свойства выполнения заданий.
//
Процедура ВыполнитьРаспределениеВФоне(
			Задания,
			ИдентификаторыНеОбработанныхЗаписей,
			ДополнительныеСвойства = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Если БлокироватьРаспределениеЗапасов() Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапроса = Новый Массив;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Задания.Номенклатура        КАК Номенклатура,
		|	Задания.Характеристика      КАК Характеристика,
		|	Задания.Склад               КАК Склад,
		|	Задания.Назначение          КАК Назначение,
		|	Задания.ЗаказНаОтгрузку     КАК ЗаказНаОтгрузку,
		|	Задания.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
		|	Задания.КСнятиюРезерва      КАК КСнятиюРезерва
		|ПОМЕСТИТЬ ЗаданияКРаспределениюЗапасов
		|ИЗ
		|	&Задания КАК Задания
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, ЗаказНаОтгрузку
		|;
		|
		|////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Задания.Номенклатура   КАК Номенклатура,
		|	Задания.Характеристика КАК Характеристика,
		|	Задания.Склад          КАК Склад,
		|	Задания.Назначение     КАК Назначение
		|ПОМЕСТИТЬ ТоварыДляРаспределенияЗапасов
		|ИЗ
		|	ЗаданияКРаспределениюЗапасов КАК Задания
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|//////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Задания.Номенклатура КАК Номенклатура,
		|	Задания.Характеристика КАК Характеристика,
		|	Задания.Склад КАК Склад,
		|	Задания.Назначение КАК Назначение,
		|	Задания.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|	СУММА(Задания.КСнятиюРезерва) КАК Отгрузка
		|ПОМЕСТИТЬ ИзменениеОстаткаПоТоварамДляКонтроляОстатков
		|ИЗ
		|	ЗаданияКРаспределениюЗапасов КАК Задания
		|ГДЕ
		|	Задания.КСнятиюРезерва > 0
		|СГРУППИРОВАТЬ ПО
		|	Задания.Номенклатура,
		|	Задания.Характеристика,
		|	Задания.Склад,
		|	Задания.Назначение,
		|	Задания.ЗаказНаОтгрузку
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, ЗаказНаОтгрузку";
		
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
		
	Запрос.УстановитьПараметр("Задания", Задания);
	
	Запрос.Выполнить();
	
	Набор = РегистрыСведений.РаспределениеЗапасов.СоздатьНаборЗаписей();
	ТаблицаРезультат = Набор.ВыгрузитьКолонки();
	ТаблицаРезультат.Колонки.Добавить("Пустая", Новый ОписаниеТипов("Булево"));
	Запрос.УстановитьПараметр("НачалоТекущегоДня", НачалоДня(ТекущаяДатаСеанса()));
	
	Отбор = Новый Структура();
	Отбор.Вставить("Номенклатура");
	Отбор.Вставить("Характеристика");
	Отбор.Вставить("Склад");
	Отбор.Вставить("Назначение");
	НаборЗаписанЦеликом = Ложь;
	
	
	НачатьТранзакцию();
	
	Попытка
		
		Если Не ОбновлениеИнформационнойБазы.ОбъектОбработан("РегистрСведений.РаспределениеЗапасов").Обработан Тогда
			
			Блокировка = Новый БлокировкаДанных();
			
			ТаблицаПакета = Запрос.МенеджерВременныхТаблиц.Таблицы["ТоварыДляРаспределенияЗапасов"];
			ИсточникДанныхБлокировки = ТаблицаПакета.ПолучитьДанные().Выгрузить();
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РаспределениеЗапасов");
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика", "Характеристика");
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Склад", "Склад");
			ЭлементБлокировки.ИсточникДанных = ИсточникДанныхБлокировки;
			
			Блокировка.Заблокировать();
			
		КонецЕсли;
	
		
		ТаблицаДляОбновленияРесурсаВНаличии = ВременныеТаблицыДанныхРегистра(Запрос, Истина);
		ВременнаяТаблицаПотребностиРезервироватьПоМере(Запрос);
		ВременнаяТаблицаЗапасыИДоступностьПоДатам(Запрос);
		ВременнаяТаблицаРезультатРаспределенияЗапасовНаПотребности(Запрос);
		ТаблицаРезультат = ДанныеДляЗаписиТоваровВПроизводительномРежиме(Запрос);
		
		Для Индекс = 0 По ТаблицаРезультат.Количество() - 1 Цикл
			
			Строка = ТаблицаРезультат[Индекс];
			
			Если Отбор.Номенклатура <> Строка.Номенклатура
					Или Отбор.Характеристика <> Строка.Характеристика
					Или Отбор.Склад <> Строка.Склад
					Или Отбор.Назначение <> Строка.Назначение Тогда
						
						Если Отбор.Номенклатура <> Неопределено Тогда
							Набор.Записать();
						КонецЕсли;
						
						ЗаполнитьЗначенияСвойств(Отбор, Строка);
						
						Набор.Очистить();
						Набор.Отбор.Номенклатура.Установить(Отбор.Номенклатура);
						Набор.Отбор.Характеристика.Установить(Отбор.Характеристика);
						Набор.Отбор.Склад.Установить(Отбор.Склад);
						Набор.Отбор.Назначение.Установить(Отбор.Назначение);
						
			КонецЕсли;
			
			Если Не Строка.Пустая Тогда
				ЗаполнитьЗначенияСвойств(Набор.Добавить(), Строка);
			КонецЕсли;
			
		КонецЦикла;
		
		Если ТаблицаРезультат.Количество() > 0 Тогда
			Набор.Записать();
		КонецЕсли;
		
		Для Индекс = 0 По ТаблицаДляОбновленияРесурсаВНаличии.Количество() - 1 Цикл
			
			Строка = ТаблицаДляОбновленияРесурсаВНаличии[Индекс];
			Набор.Очистить();
			Набор.Отбор.Номенклатура.Установить(Строка.Номенклатура);
			Набор.Отбор.Характеристика.Установить(Строка.Характеристика);
			Набор.Отбор.Склад.Установить(Строка.Склад);
			Набор.Отбор.Назначение.Установить(Строка.Назначение);
			Набор.Отбор.Состояние.Установить(Строка.Состояние);
			
			Если Не Строка.Пустая Тогда
				ЗаполнитьЗначенияСвойств(Набор.Добавить(), Строка);
			КонецЕсли;
			Набор.Записать();
			
		КонецЦикла;
		
		НаборЗаписанЦеликом = Истина;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки = "";
		СообщитьОНеудачнойОбработкеНоменклатуры(ИнформацияОбОшибке(), ТекстОшибки);
		
		Для каждого СтрокаТаблицыЗадания Из Задания Цикл
			ИдентификаторыНеОбработанныхЗаписей.Вставить(СтрокаТаблицыЗадания.ИдентификаторЗаписи, ТекстОшибки);
		КонецЦикла;
		
		НаборЗаписанЦеликом = Ложь;
		
	КонецПопытки;
	
	Если НаборЗаписанЦеликом Тогда
		
		ТекстыЗапроса.Очистить();
		
		
		ТекстЗапроса =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Набор.Ссылка КАК Ссылка
			|ПОМЕСТИТЬ ВсеЗаказы
			|ИЗ(
			|	ВЫБРАТЬ
			|		Задания.ЗаказНаОтгрузку КАК Ссылка
			|	ИЗ
			|		ЗаданияКРаспределениюЗапасов КАК Задания
			|	ГДЕ
			|		Задания.ЗаказНаОтгрузку <> НЕОПРЕДЕЛЕНО
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ОжидаемаяОтгрузка.ЗаказНаОтгрузку КАК Ссылка
			|	ИЗ
			|		ЗаданияКРаспределениюЗапасов КАК Заказы
			|			
			|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК ОжидаемаяОтгрузка
			|			ПО ОжидаемаяОтгрузка.Номенклатура = Заказы.Номенклатура
			|			 И ОжидаемаяОтгрузка.Характеристика = Заказы.Характеристика
			|			 И ОжидаемаяОтгрузка.Склад = Заказы.Склад
			|			 И ОжидаемаяОтгрузка.Назначение = Заказы.Назначение
			|	ГДЕ
			|		ОжидаемаяОтгрузка.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка)) КАК Набор
			|ИНДЕКСИРОВАТЬ ПО
			|	Ссылка
			|;
			|
			|/////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВсеЗаказы.Ссылка КАК Ссылка
			|ИЗ
			|	ВсеЗаказы КАК ВсеЗаказы
			|ГДЕ
			|	НЕ ИСТИНА В(
			|		ВЫБРАТЬ ПЕРВЫЕ 1
			|			ИСТИНА КАК ЗаказЕстьВНеобработанныхЗаданиях
			|		ИЗ
			|			РегистрСведений.ЗаданияКРаспределениюЗапасов КАК ВсеЗадания
			|		ГДЕ
			|			ВсеЗадания.ЗаказНаОтгрузку = ВсеЗаказы.Ссылка
			|				И НЕ ИСТИНА В(
			|					ВЫБРАТЬ ПЕРВЫЕ 1
			|						ИСТИНА КАК ЭтаНоменклатураОтработана
			|					ИЗ
			|						ЗаданияКРаспределениюЗапасов КАК ОтработанныеЗадания
			|					ГДЕ
			|						ОтработанныеЗадания.Номенклатура = ВсеЗадания.Номенклатура
			|							И ОтработанныеЗадания.Характеристика = ВсеЗадания.Характеристика
			|							И ОтработанныеЗадания.Склад = ВсеЗадания.Склад
			|							И ОтработанныеЗадания.Назначение = ВсеЗадания.Назначение))
			|	И НЕ ИСТИНА В(
			|		ВЫБРАТЬ ПЕРВЫЕ 1
			|			ИСТИНА КАК НоменклатураЗаказаЕстьВНеотработанныхЗаданиях
			|		ИЗ
			|			РегистрСведений.РаспределениеЗапасов КАК ОжидаемаяОтгрузка
			|		ГДЕ
			|			ОжидаемаяОтгрузка.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка)
			|				И ОжидаемаяОтгрузка.ЗаказНаОтгрузку = ВсеЗаказы.Ссылка
			|				И ИСТИНА В(
			|					ВЫБРАТЬ ПЕРВЫЕ 1
			|						ИСТИНА КАК ЭтаНоменклатураЕстьВЗаданиях
			|					ИЗ
			|						РегистрСведений.ЗаданияКРаспределениюЗапасов КАК ВсеЗадания
			|					ГДЕ
			|						ВсеЗадания.Номенклатура = ОжидаемаяОтгрузка.Номенклатура
			|							И ВсеЗадания.Характеристика = ОжидаемаяОтгрузка.Характеристика
			|							И ВсеЗадания.Склад = ОжидаемаяОтгрузка.Склад
			|							И ВсеЗадания.Назначение = ОжидаемаяОтгрузка.Назначение)
			|					
			|				И НЕ ИСТИНА В(
			|					ВЫБРАТЬ ПЕРВЫЕ 1
			|						ИСТИНА КАК ЭтаНоменклатураОтработана
			|					ИЗ
			|						ЗаданияКРаспределениюЗапасов КАК ОтработанныеЗадания
			|					ГДЕ
			|						ОтработанныеЗадания.Номенклатура = ОжидаемаяОтгрузка.Номенклатура
			|							И ОтработанныеЗадания.Характеристика = ОжидаемаяОтгрузка.Характеристика
			|							И ОтработанныеЗадания.Склад = ОжидаемаяОтгрузка.Склад
			|							И ОтработанныеЗадания.Назначение = ОжидаемаяОтгрузка.Назначение))";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса);
		
		Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
		
		Результаты = Запрос.ВыполнитьПакет();
		
		Заказы = Результаты[Результаты.ВГраница()].Выгрузить().ВыгрузитьКолонку("Ссылка");
		ИсключитьСсылкиТребующиеОтложенногоОбновления(Заказы);
		
		РегистрыКОтражению = СостоянияДокументов.РегистрыКОтражениюСостоянияЗаказов();
		РегистрыКОтражению.СостоянияЗаказовКлиентов    = Истина;
		РегистрыКОтражению.СостоянияВнутреннихЗаказов  = Истина;
		
		СостоянияДокументов.ДобавитьЗаданияКОтражениюСостоянияЗаказов(Неопределено, Заказы,, РегистрыКОтражению);
		
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьРаспределениеЗапасовВТранзакцииПроведения(Запрос) Экспорт
	
	ТаблицаДляОбновленияРесурсаВНаличии = ВременныеТаблицыДанныхРегистра(Запрос, Истина);
	ВременнаяТаблицаПотребностиРезервироватьПоМере(Запрос);
	ВременнаяТаблицаЗапасыИДоступностьПоДатам(Запрос);
	ВременнаяТаблицаРезультатРаспределенияЗапасовНаПотребности(Запрос);
	Результат = ДанныеДляЗаписиТоваровВПроизводительномРежиме(Запрос);
	
	ВсегоСтрок = Результат.Количество();
	
	Номенклатура   = Неопределено;
	Характеристика = Неопределено;
	Склад          = Неопределено;
	Назначение     = Неопределено;
	
	Набор = РегистрыСведений.РаспределениеЗапасов.СоздатьНаборЗаписей();
	
	Для Индекс = 0 По ВсегоСтрок - 1 Цикл
		
		ТекСтрока = Результат[Индекс];
		Если Номенклатура <> ТекСтрока.Номенклатура
				Или Характеристика <> ТекСтрока.Характеристика
				Или Склад <> ТекСтрока.Склад
				Или Назначение <> ТекСтрока.Назначение Тогда
					
					Если Номенклатура <> Неопределено Тогда
						Набор.Записать();
						Набор.Очистить();
					КонецЕсли;
					
					Номенклатура   = ТекСтрока.Номенклатура;
					Характеристика = ТекСтрока.Характеристика;
					Склад          = ТекСтрока.Склад;
					Назначение     = ТекСтрока.Назначение;
					
					Набор.Отбор.Номенклатура.Установить(Номенклатура);
					Набор.Отбор.Характеристика.Установить(Характеристика);
					Набор.Отбор.Склад.Установить(Склад);
					Набор.Отбор.Назначение.Установить(Назначение);
		КонецЕсли;
		
		Если Не ТекСтрока.Пустая Тогда
			ЗаполнитьЗначенияСвойств(Набор.Добавить(), ТекСтрока);
		КонецЕсли;
		
	КонецЦикла;
	
	Если Номенклатура <> Неопределено Тогда
		Набор.Записать();
	КонецЕсли;
	
	Для Индекс = 0 По ТаблицаДляОбновленияРесурсаВНаличии.Количество() - 1 Цикл
		
		Строка = ТаблицаДляОбновленияРесурсаВНаличии[Индекс];
		Набор.Очистить();
		Набор.Отбор.Номенклатура.Установить(Строка.Номенклатура);
		Набор.Отбор.Характеристика.Установить(Строка.Характеристика);
		Набор.Отбор.Склад.Установить(Строка.Склад);
		Набор.Отбор.Назначение.Установить(Строка.Назначение);
		Набор.Отбор.Состояние.Установить(Строка.Состояние);
		
		Если Не Строка.Пустая Тогда
			ЗаполнитьЗначенияСвойств(Набор.Добавить(), Строка);
		КонецЕсли;
		Набор.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВременныеТаблицыДанныхРегистра(Запрос, ВозвращатьТаблицуВНаличии, ИмяТаблицыТовары = "ТоварыДляРаспределенияЗапасов", ИмяТаблицыКорректировки = "")
	
	Тексты = Новый Массив();
	Текст =
		"ВЫБРАТЬ
		|	Потребности.Номенклатура КАК Номенклатура,
		|	Потребности.Характеристика КАК Характеристика,
		|	Потребности.Склад КАК Склад,
		|	Потребности.Назначение КАК Назначение,
		|	Потребности.Заказ КАК Заказ,
		|	Потребности.ДатаСобытия КАК ДатаСобытия,
		|	ВЫБОР
		|		КОГДА Потребности.РезервироватьНаСкладеОстаток > 0
		|			ТОГДА Потребности.РезервироватьНаСкладеОстаток
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК РезервироватьНаСкладе,
		|	ВЫБОР
		|		КОГДА Потребности.РезервироватьПоМереПоступленияОстаток > 0
		|			ТОГДА Потребности.РезервироватьПоМереПоступленияОстаток
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК РезервироватьПоМереПоступления,
		|	ВЫБОР
		|		КОГДА Потребности.ОтложитьРезервированиеОстаток > 0
		|			ТОГДА Потребности.ОтложитьРезервированиеОстаток
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ОтложитьРезервирование,
		|	ВЫБОР
		|		КОГДА Потребности.КОбеспечениюОстаток > 0
		|			ТОГДА Потребности.КОбеспечениюОстаток
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КОбеспечению,
		|	ВЫБОР
		|		КОГДА Потребности.НеОбеспечиватьОстаток > 0
		|			ТОГДА Потребности.НеОбеспечиватьОстаток
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК НеОбеспечивать
		|ПОМЕСТИТЬ ВсеПотребности
		|ИЗ
		|	РегистрНакопления.ЗапасыИПотребности.Остатки(,
		|		(Номенклатура, Характеристика, Склад, Назначение) В(
		|			ВЫБРАТЬ
		|				Товары.Номенклатура КАК Номенклатура,
		|				Товары.Характеристика КАК Характеристика,
		|				Товары.Склад КАК Склад,
		|				Товары.Назначение КАК Назначение
		|			ИЗ
		|				ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары)) КАК Потребности
		|ГДЕ
		|	Потребности.РезервироватьНаСкладеОстаток > 0
		|		ИЛИ Потребности.РезервироватьПоМереПоступленияОстаток > 0
		|		ИЛИ Потребности.ОтложитьРезервированиеОстаток > 0
		|		ИЛИ Потребности.КОбеспечениюОстаток > 0
		|		ИЛИ Потребности.НеОбеспечиватьОстаток > 0";
	
	Если ИмяТаблицыКорректировки <> "" Тогда
		
		ЗаменяемыйТекст = "ПОМЕСТИТЬ ВсеПотребности"; //@Query-part
		Текст = СтрЗаменить(Текст, ЗаменяемыйТекст, "");
		
		ТекстыНабора = Новый Массив();
		ТекстыНабора.Добавить(Текст);
		Текст =
			"ВЫБРАТЬ
			|	Корректировка.Номенклатура КАК Номенклатура,
			|	Корректировка.Характеристика КАК Характеристика,
			|	Корректировка.Склад КАК Склад,
			|	Корректировка.Назначение КАК Назначение,
			|	Корректировка.Заказ КАК Заказ,
			|	Корректировка.ДатаСобытия КАК ДатаСобытия,
			|	Корректировка.РезервироватьНаСкладе КАК РезервироватьНаСкладе,
			|	Корректировка.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
			|	Корректировка.ОтложитьРезервирование КАК ОтложитьРезервирование,
			|	Корректировка.КОбеспечению КАК КОбеспечению,
			|	Корректировка.НеОбеспечивать КАК НеОбеспечивать
			|ИЗ
			|	КорректировкаДвиженийПереопределяемый КАК Корректировка
			|ГДЕ
			|	Корректировка.РезервироватьНаСкладе <> 0
			|		ИЛИ Корректировка.РезервироватьПоМереПоступления <> 0
			|		ИЛИ Корректировка.ОтложитьРезервирование <> 0
			|		ИЛИ Корректировка.КОбеспечению <> 0
			|		ИЛИ Корректировка.НеОбеспечивать <> 0";
		ТекстыНабора.Добавить(Текст);
		ТекстНабора = СтрСоединить(ТекстыНабора, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
		Текст =
			"ВЫБРАТЬ
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	Набор.Назначение КАК Назначение,
			|	Набор.Заказ КАК Заказ,
			|	Набор.ДатаСобытия КАК ДатаСобытия,
			|	СУММА(Набор.РезервироватьНаСкладе) КАК РезервироватьНаСкладе,
			|	СУММА(Набор.РезервироватьПоМереПоступления) КАК РезервироватьПоМереПоступления,
			|	СУММА(Набор.ОтложитьРезервирование) КАК ОтложитьРезервирование,
			|	СУММА(Набор.КОбеспечению) КАК КОбеспечению,
			|	СУММА(Набор.НеОбеспечивать) КАК НеОбеспечивать
			|ПОМЕСТИТЬ ВсеПотребности
			|ИЗ
			|	НаборПереопределяемый КАК Набор
			|СГРУППИРОВАТЬ ПО
			|	Набор.Номенклатура,
			|	Набор.Характеристика,
			|	Набор.Склад,
			|	Набор.Назначение,
			|	Набор.Заказ,
			|	Набор.ДатаСобытия
			|ИМЕЮЩИЕ
			|	СУММА(Набор.РезервироватьНаСкладе) > 0
			|		ИЛИ СУММА(Набор.РезервироватьПоМереПоступления) > 0
			|		ИЛИ СУММА(Набор.ОтложитьРезервирование) > 0
			|		ИЛИ СУММА(Набор.КОбеспечению) > 0
			|		ИЛИ СУММА(Набор.НеОбеспечивать) > 0";
		Текст = СтрЗаменить(Текст, "НаборПереопределяемый", СтрШаблон("(%1)", ТекстНабора));
		
	КонецЕсли;
	Тексты.Добавить(Текст);
	
	Текст =
		"ВЫБРАТЬ
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.Склад КАК Склад,
		|	Запасы.ВНаличииОстаток КАК ВНаличии
		|ПОМЕСТИТЬ ЗапасыВНаличии
		|ИЗ
		|	РегистрНакопления.ЗапасыИПотребности.Остатки(,
		|		(Номенклатура, Характеристика, Склад) В(
		|			ВЫБРАТЬ
		|				Товары.Номенклатура КАК Номенклатура,
		|				Товары.Характеристика КАК Характеристика,
		|				Товары.Склад КАК Склад
		|			ИЗ
		|				ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары)) КАК Запасы
		|ГДЕ
		|	Запасы.ВНаличииОстаток <> 0
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад";
	Тексты.Добавить(Текст);
	
	Текст =
		"ВЫБРАТЬ
		|	0 КАК Раздел,
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.Склад КАК Склад,
		|	Запасы.Назначение КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК Заказ,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаСобытия,
		|	Запасы.ВНаличииОстаток КАК Запас,
		|	Запасы.РезервироватьНаСкладеОстаток КАК Резерв
		|ПОМЕСТИТЬ ВсеЗапасы
		|ИЗ
		|	РегистрНакопления.ЗапасыИПотребности.Остатки(,
		|		(Номенклатура, Характеристика, Склад, Назначение) В(
		|			ВЫБРАТЬ
		|				Товары.Номенклатура КАК Номенклатура,
		|				Товары.Характеристика КАК Характеристика,
		|				Товары.Склад КАК Склад,
		|				Товары.Назначение КАК Назначение
		|			ИЗ
		|				ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары)) КАК Запасы
		|ГДЕ
		|	Запасы.ВНаличииОстаток <> 0
		|		ИЛИ Запасы.РезервироватьНаСкладеОстаток > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	1 КАК Раздел,
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.Склад КАК Склад,
		|	Запасы.Назначение КАК Назначение,
		|	Запасы.Заказ КАК Заказ,
		|	Запасы.ДатаСобытия КАК ДатаСобытия,
		|	Запасы.ПоступитОстаток КАК Запас,
		|	0 КАК Резерв
		|ИЗ
		|	РегистрНакопления.ЗапасыИПотребности.Остатки(,
		|		(Номенклатура, Характеристика, Склад, Назначение) В(
		|			ВЫБРАТЬ
		|				Товары.Номенклатура КАК Номенклатура,
		|				Товары.Характеристика КАК Характеристика,
		|				Товары.Склад КАК Склад,
		|				Товары.Назначение КАК Назначение
		|			ИЗ
		|				ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары)) КАК Запасы
		|ГДЕ
		|	Запасы.ПоступитОстаток > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2 КАК Раздел,
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.Склад КАК Склад,
		|	Запасы.Назначение КАК Назначение,
		|	Запасы.Заказ КАК Заказ,
		|	Запасы.ДатаСобытия КАК ДатаСобытия,
		|	Запасы.ЗаказаноОстаток - Запасы.ПоступитОстаток КАК Запас,
		|	0 КАК Резерв
		|ИЗ
		|	РегистрНакопления.ЗапасыИПотребности.Остатки(,
		|		(Номенклатура, Характеристика, Склад, Назначение) В(
		|			ВЫБРАТЬ
		|				Товары.Номенклатура КАК Номенклатура,
		|				Товары.Характеристика КАК Характеристика,
		|				Товары.Склад КАК Склад,
		|				Товары.Назначение КАК Назначение
		|			ИЗ
		|				ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары)) КАК Запасы
		|ГДЕ
		|	Запасы.ЗаказаноОстаток - Запасы.ПоступитОстаток > 0";
	Если ИмяТаблицыКорректировки <> "" Тогда
		
		ЗаменяемыйТекст = "ПОМЕСТИТЬ ВсеЗапасы"; //@Query-part
		Текст = СтрЗаменить(Текст, ЗаменяемыйТекст, "");
		
		ТекстыНабора = Новый Массив();
		ТекстыНабора.Добавить(Текст);
		Текст =
			"ВЫБРАТЬ
			|	0 КАК Раздел,
			|	Корректировка.Номенклатура КАК Номенклатура,
			|	Корректировка.Характеристика КАК Характеристика,
			|	Корректировка.Склад КАК Склад,
			|	Корректировка.Назначение КАК Назначение,
			|	НЕОПРЕДЕЛЕНО КАК Заказ,
			|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаСобытия,
			|	Корректировка.ВНаличии КАК Запас,
			|	Корректировка.РезервироватьНаСкладе КАК Резерв
			|ИЗ
			|	КорректировкаДвиженийПереопределяемый КАК Корректировка
			|ГДЕ
			|	Корректировка.ВНаличии <> 0
			|		ИЛИ Корректировка.РезервироватьНаСкладе <> 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	1 КАК Раздел,
			|	Корректировка.Номенклатура КАК Номенклатура,
			|	Корректировка.Характеристика КАК Характеристика,
			|	Корректировка.Склад КАК Склад,
			|	Корректировка.Назначение КАК Назначение,
			|	Корректировка.Заказ КАК Заказ,
			|	Корректировка.ДатаСобытия КАК ДатаСобытия,
			|	Корректировка.Поступит КАК Запас,
			|	0 КАК Резерв
			|ИЗ
			|	КорректировкаДвиженийПереопределяемый КАК Корректировка
			|ГДЕ
			|	Корректировка.Поступит <> 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	2 КАК Раздел,
			|	Корректировка.Номенклатура КАК Номенклатура,
			|	Корректировка.Характеристика КАК Характеристика,
			|	Корректировка.Склад КАК Склад,
			|	Корректировка.Назначение КАК Назначение,
			|	Корректировка.Заказ КАК Заказ,
			|	Корректировка.ДатаСобытия КАК ДатаСобытия,
			|	Корректировка.Заказано - Корректировка.Поступит КАК Запас,
			|	0 КАК Резерв
			|ИЗ
			|	КорректировкаДвиженийПереопределяемый КАК Корректировка
			|ГДЕ
			|	Корректировка.Заказано - Корректировка.Поступит <> 0";
		ТекстыНабора.Добавить(Текст);
		ТекстНабора = СтрСоединить(ТекстыНабора, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
		Текст =
			"ВЫБРАТЬ
			|	Набор.Раздел КАК Раздел,
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	Набор.Назначение КАК Назначение,
			|	Набор.Заказ КАК Заказ,
			|	Набор.ДатаСобытия КАК ДатаСобытия,
			|	СУММА(Набор.Запас) КАК Запас,
			|	СУММА(Набор.Резерв) КАК Резерв
			|ПОМЕСТИТЬ ВсеЗапасы
			|ИЗ
			|	НаборПереопределяемый КАК Набор
			|СГРУППИРОВАТЬ ПО
			|	Набор.Раздел,
			|	Набор.Номенклатура,
			|	Набор.Характеристика,
			|	Набор.Склад,
			|	Набор.Назначение,
			|	Набор.Заказ,
			|	Набор.ДатаСобытия
			|ИМЕЮЩИЕ
			|	СУММА(Набор.Запас) <> 0
			|			ИЛИ СУММА(Набор.Резерв) > 0";
		Текст = СтрЗаменить(Текст, "НаборПереопределяемый", СтрШаблон("(%1)", ТекстНабора));
		
	КонецЕсли;
	
	ТекстыСИндексированием = Новый Массив();
	ТекстыСИндексированием.Добавить(Текст);
	Текст =
		"ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, Раздел"; //@Query-part
	ТекстыСИндексированием.Добавить(Текст);
	Текст = СтрСоединить(ТекстыСИндексированием, Символы.ПС);
	Тексты.Добавить(Текст);
	
	Текст =
		"ВЫБРАТЬ
		|	ЗапасыВНаличии.Номенклатура КАК Номенклатура,
		|	ЗапасыВНаличии.Характеристика КАК Характеристика,
		|	ЗапасыВНаличии.Склад КАК Склад,
		|	ЗапасыВНаличии.ВНаличии КАК ВНаличии
		|ПОМЕСТИТЬ ЕстьВНаличииЕстьДвиженияНетЗапаса
		|ИЗ
		|	ЗапасыВНаличии КАК ЗапасыВНаличии
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВсеЗапасы КАК Запасы
		|		ПО Запасы.Номенклатура = ЗапасыВНаличии.Номенклатура
		|		 И Запасы.Характеристика = ЗапасыВНаличии.Характеристика
		|		 И Запасы.Склад = ЗапасыВНаличии.Склад
		|		 И Запасы.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		 И Запасы.Заказ = НЕОПРЕДЕЛЕНО
		|		 И Запасы.ДатаСобытия = ДАТАВРЕМЯ(1, 1, 1)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары
		|		ПО Товары.Номенклатура = ЗапасыВНаличии.Номенклатура
		|		 И Товары.Характеристика = ЗапасыВНаличии.Характеристика
		|		 И Товары.Склад = ЗапасыВНаличии.Склад
		|		 И Товары.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|ГДЕ
		|	Запасы.Номенклатура ЕСТЬ NULL
		|		И НЕ Товары.Номенклатура ЕСТЬ NULL
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад";
	Тексты.Добавить(Текст);
	
	Текст =
		"ВЫБРАТЬ
		|	Сведения.Номенклатура КАК Номенклатура,
		|	Сведения.Характеристика КАК Характеристика,
		|	Сведения.Склад КАК Склад,
		|	Сведения.Назначение КАК Назначение,
		|	Сведения.ЗаказНаОтгрузку КАК Заказ,
		|	ВЫБОР
		|		КОГДА СУММА(Сведения.Зарезервировано) - МАКСИМУМ(ЕСТЬNULL(ОтгрузкаИзСобственногоРезерва.Отгрузка, 0)) > 0
		|			ТОГДА СУММА(Сведения.Зарезервировано) - МАКСИМУМ(ЕСТЬNULL(ОтгрузкаИзСобственногоРезерва.Отгрузка, 0))
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Зарезервировано
		|ПОМЕСТИТЬ ЗапасыРаспределенныеНаЗаказыРанее
		|ИЗ
		|	ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаКонтроляОбеспеченияХарактеристика
		|		ПО НастройкаКонтроляОбеспеченияХарактеристика.Склад = Товары.Склад
		|		 И НастройкаКонтроляОбеспеченияХарактеристика.Номенклатура = Товары.Номенклатура
		|		 И НастройкаКонтроляОбеспеченияХарактеристика.Характеристика = Товары.Характеристика
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаКонтроляОбеспеченияНоменклатура
		|		ПО НастройкаКонтроляОбеспеченияНоменклатура.Склад = Товары.Склад
		|		 И НастройкаКонтроляОбеспеченияНоменклатура.Номенклатура = Товары.Номенклатура
		|		 И НастройкаКонтроляОбеспеченияНоменклатура.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		 И НастройкаКонтроляОбеспеченияХарактеристика.Номенклатура ЕСТЬ NULL
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаКонтроляОбеспечения
		|		ПО НастройкаКонтроляОбеспечения.Склад = Товары.Склад
		|		 И НастройкаКонтроляОбеспечения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|		 И НастройкаКонтроляОбеспечения.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		 И НастройкаКонтроляОбеспеченияХарактеристика.Номенклатура ЕСТЬ NULL
		|		 И НастройкаКонтроляОбеспеченияНоменклатура.Номенклатура ЕСТЬ NULL
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК Сведения
		|		ПО Сведения.Номенклатура = Товары.Номенклатура
		|		 И Сведения.Характеристика = Товары.Характеристика
		|		 И Сведения.Склад = Товары.Склад
		|		 И Сведения.Назначение = Товары.Назначение
		|		 И Сведения.Состояние В(
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченНаСкладе),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ЗарезервированПриПоступлении))
		|		 И ЕСТЬNULL(НастройкаКонтроляОбеспеченияХарактеристика.КонтролироватьСвободныеОстатки,
		|				ЕСТЬNULL(НастройкаКонтроляОбеспеченияНоменклатура.КонтролироватьСвободныеОстатки,
		|					ЕСТЬNULL(НастройкаКонтроляОбеспечения.КонтролироватьСвободныеОстатки, ЛОЖЬ)))
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ИзменениеОстаткаПоТоварамДляКонтроляОстатков КАК ОтгрузкаИзСобственногоРезерва
		|		ПО ОтгрузкаИзСобственногоРезерва.Номенклатура = Сведения.Номенклатура
		|		 И ОтгрузкаИзСобственногоРезерва.Характеристика = Сведения.Характеристика
		|		 И ОтгрузкаИзСобственногоРезерва.Склад = Сведения.Склад
		|		 И ОтгрузкаИзСобственногоРезерва.Назначение = Сведения.Назначение
		|		 И ОтгрузкаИзСобственногоРезерва.ЗаказНаОтгрузку = Сведения.ЗаказНаОтгрузку
		|
		|ГДЕ
		|	НЕ Сведения.Номенклатура ЕСТЬ NULL
 		|СГРУППИРОВАТЬ ПО
		|	Сведения.Номенклатура,
		|	Сведения.Характеристика,
		|	Сведения.Склад,
		|	Сведения.Назначение,
		|	Сведения.ЗаказНаОтгрузку
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение";
	Тексты.Добавить(Текст);
	
	Если ВозвращатьТаблицуВНаличии Тогда
		
		Текст =
			"ВЫБРАТЬ
			|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе) КАК Состояние,
			|	1 КАК ТипЗаписиРаспределенияЗапасов,
			|	ЗапасыВНаличии.Номенклатура КАК Номенклатура,
			|	ЗапасыВНаличии.Характеристика КАК Характеристика,
			|	ЗапасыВНаличии.Склад КАК Склад,
			|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
			|	ЗапасыВНаличии.ВНаличии КАК ВНаличии,
			|	ЕСТЬNULL(Сведения.Запас, 0) КАК Запас,
			|	ЕСТЬNULL(Сведения.Резерв, 0) КАК Резерв,
			|	ЕСТЬNULL(Сведения.Свободно, 0) КАК Свободно,
			|	ЕСТЬNULL(Сведения.Излишек, 0) КАК Излишек,
			|	ЛОЖЬ КАК Пустая
			|ИЗ
			|	ЗапасыВНаличии КАК ЗапасыВНаличии
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК Сведения
			|		ПО Сведения.Номенклатура = ЗапасыВНаличии.Номенклатура
			|		 И Сведения.Характеристика = ЗапасыВНаличии.Характеристика
			|		 И Сведения.Склад = ЗапасыВНаличии.Склад
			|		 И Сведения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|		 И Сведения.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары
			|		ПО ЗапасыВНаличии.Номенклатура = Товары.Номенклатура
			|		 И ЗапасыВНаличии.Характеристика = Товары.Характеристика
			|		 И ЗапасыВНаличии.Склад = Товары.Склад
			|		 И Товары.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|		
			|ГДЕ
			|	ЗапасыВНаличии.ВНаличии <> ЕСТЬNULL(Сведения.ВНаличии, 0)
					// Наличие не будет пересчитано по товару автоматически, так как в проводимом документе нет аналитики с пустым назначением.
			|		И Товары.Номенклатура ЕСТЬ NULL
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе) КАК Состояние,
			|	1 КАК ТипЗаписиРаспределенияЗапасов,
			|	Сведения.Номенклатура КАК Номенклатура,
			|	Сведения.Характеристика КАК Характеристика,
			|	Сведения.Склад КАК Склад,
			|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
			|	0 КАК ВНаличии,
			|	Сведения.Запас КАК Запас,
			|	Сведения.Резерв КАК Резерв,
			|	Сведения.Свободно КАК Свободно,
			|	Сведения.Излишек КАК Излишек,
			|	Сведения.Запас = 0 И Сведения.Резерв = 0 И Сведения.Свободно = 0 И Сведения.Излишек = 0 КАК Пустая
			|ИЗ
			|	РегистрСведений.РаспределениеЗапасов КАК Сведения
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары
			|		ПО Сведения.Номенклатура = Товары.Номенклатура
			|		 И Сведения.Характеристика = Товары.Характеристика
			|		 И Сведения.Склад = Товары.Склад
			|		 И Сведения.Назначение = Товары.Назначение
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ ЗапасыВНаличии КАК ЗапасыВНаличии
			|		ПО ЗапасыВНаличии.Номенклатура = Сведения.Номенклатура
			|		 И ЗапасыВНаличии.Характеристика = Сведения.Характеристика
			|		 И ЗапасыВНаличии.Склад = Сведения.Склад
			|		
			|ГДЕ
			|	Сведения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|		И Сведения.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)
					// Строка не попала в предыдущий блок объединения.
			|		И ЗапасыВНаличии.Номенклатура ЕСТЬ NULL
			|		И Сведения.ВНаличии <> 0
					// Затронуты товары проводимого документа.
			|		И (Сведения.Номенклатура, Сведения.Характеристика, Сведения.Склад, Сведения.Назначение) В(
			|			ВЫБРАТЬ
			|				Фильтр.Номенклатура КАК Номенклатура,
			|				Фильтр.Характеристика КАК Характеристика,
			|				Фильтр.Склад КАК Склад,
			|				ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение
			|			ИЗ
			|				ТоварыДляРаспределенияЗапасовПереопределяемый КАК Фильтр)
					// Наличие не будет пересчитано по товару автоматически, так как в проводимом документе нет аналитики с пустым назначением.
			|		И Товары.Номенклатура ЕСТЬ NULL";
		Тексты.Добавить(Текст);
	
	КонецЕсли;
	
	Текст = СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());
	Текст = СтрЗаменить(Текст, "ТоварыДляРаспределенияЗапасовПереопределяемый", ИмяТаблицыТовары);
	Запрос.Текст = СтрЗаменить(Текст, "КорректировкаДвиженийПереопределяемый", ИмяТаблицыКорректировки);
	РезультатЗапроса = Запрос.Выполнить();
	Результат = Неопределено;
	Если ВозвращатьТаблицуВНаличии Тогда
		Результат = РезультатЗапроса.Выгрузить();
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

Процедура ВременнаяТаблицаПотребностиРезервироватьПоМере(Запрос)
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Потребности.Номенклатура КАК Номенклатура,
		|	Потребности.Характеристика КАК Характеристика,
		|	Потребности.Склад КАК Склад,
		|	Потребности.Назначение КАК Назначение,
		|	Потребности.Заказ КАК Заказ,
		|	Потребности.ДатаСобытия КАК ДатаСобытия,
		|	Потребности.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|	0 КАК РезервироватьПоМереПоступленияИтогНаДату
		|ИЗ
		|	ВсеПотребности КАК Потребности
		|ГДЕ
		|	Потребности.РезервироватьПоМереПоступления > 0
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, Заказ, ДатаСобытия";
	
	ПотребностиРезервироватьПоМере = Запрос.Выполнить().Выгрузить();
	Заказ = Неопределено;
	Номенклатура = Неопределено;
	Характеристика = Неопределено;
	Склад = Неопределено;
	Назначение = Неопределено;
	
	Итог = 0;
	Для Индекс = 0 По ПотребностиРезервироватьПоМере.Количество() - 1 Цикл
		
		СтрокаТаблицы = ПотребностиРезервироватьПоМере[Индекс];
		
		Если Заказ <> СтрокаТаблицы.Заказ
				Или Номенклатура <> СтрокаТаблицы.Номенклатура
				Или Характеристика <> СтрокаТаблицы.Характеристика
				Или Склад <> СтрокаТаблицы.Склад
				Или Назначение <> СтрокаТаблицы.Назначение Тогда
					
					Итог = 0;
					Заказ = СтрокаТаблицы.Заказ;
					Номенклатура = СтрокаТаблицы.Номенклатура;
					Характеристика = СтрокаТаблицы.Характеристика;
					Склад = СтрокаТаблицы.Склад;
					Назначение = СтрокаТаблицы.Назначение;
					
		КонецЕсли;
		Итог = Итог + СтрокаТаблицы.РезервироватьПоМереПоступления;
		СтрокаТаблицы.РезервироватьПоМереПоступленияИтогНаДату = Итог;
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ПотребностиРезервироватьПоМере", ПотребностиРезервироватьПоМере);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПотребностиРезервироватьПоМере.Номенклатура КАК Номенклатура,
		|	ПотребностиРезервироватьПоМере.Характеристика КАК Характеристика,
		|	ПотребностиРезервироватьПоМере.Склад КАК Склад,
		|	ПотребностиРезервироватьПоМере.Назначение КАК Назначение,
		|	ПотребностиРезервироватьПоМере.Заказ КАК Заказ,
		|	ПотребностиРезервироватьПоМере.ДатаСобытия КАК ДатаСобытия,
		|	ПотребностиРезервироватьПоМере.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|	ПотребностиРезервироватьПоМере.РезервироватьПоМереПоступленияИтогНаДату КАК РезервироватьПоМереПоступленияИтогНаДату
		|ПОМЕСТИТЬ ПотребностиРезервироватьПоМере
		|ИЗ
		|	&ПотребностиРезервироватьПоМере КАК ПотребностиРезервироватьПоМере";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ВременнаяТаблицаЗапасыИДоступностьПоДатам(Запрос)
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Потребности.Номенклатура КАК Номенклатура,
		|	Потребности.Характеристика КАК Характеристика,
		|	Потребности.Склад КАК Склад,
		|	Потребности.Назначение КАК Назначение,
		|	СУММА(Потребности.РезервироватьПоМереПоступления) КАК РезервироватьПоМереПоступления,
		|	СУММА(Потребности.КОбеспечению + Потребности.ОтложитьРезервирование) КАК КОбеспечению
		|ПОМЕСТИТЬ ПотребностиПоТовару
		|ИЗ
		|	ВсеПотребности КАК Потребности
		|ГДЕ
		|	Потребности.РезервироватьПоМереПоступления > 0
		|		ИЛИ Потребности.КОбеспечению + Потребности.ОтложитьРезервирование > 0
		|СГРУППИРОВАТЬ ПО
		|	Потребности.Номенклатура,
		|	Потребности.Характеристика,
		|	Потребности.Склад,
		|	Потребности.Назначение
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|/////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Запасы.Раздел КАК Раздел,
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.Склад КАК Склад,
		|	Запасы.Назначение КАК Назначение,
		|	Запасы.Заказ КАК Заказ,
		|	Запасы.ДатаСобытия КАК ДатаСобытия,
		|	ЕСТЬNULL(ЗапасыВНаличии.ВНаличии, 0) КАК ВНаличии,
		|	Запасы.Запас КАК Запас,
		|	Запасы.Резерв КАК Резерв,
		|	ЕСТЬNULL(Потребности.РезервироватьПоМереПоступления, 0) КАК РезервироватьПоМереПоступления,
		|	ЕСТЬNULL(Потребности.КОбеспечению, 0) КАК КОбеспечению,
		|	0 КАК Свободно,
		|	0 КАК Излишек
		|ИЗ
		|	ВсеЗапасы КАК Запасы
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЗапасыВНаличии КАК ЗапасыВНаличии
		|		ПО ЗапасыВНаличии.Номенклатура = Запасы.Номенклатура
		|		 И ЗапасыВНаличии.Характеристика = Запасы.Характеристика
		|		 И ЗапасыВНаличии.Склад = Запасы.Склад
		|		 И Запасы.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		 И Запасы.Раздел = 0
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПотребностиПоТовару КАК Потребности
		|		ПО Потребности.Номенклатура = Запасы.Номенклатура
		|		 И Потребности.Характеристика = Запасы.Характеристика
		|		 И Потребности.Склад = Запасы.Склад
		|		 И Потребности.Назначение = Запасы.Назначение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0 КАК Раздел,
		|	ЗапасыВНаличии.Номенклатура КАК Номенклатура,
		|	ЗапасыВНаличии.Характеристика КАК Характеристика,
		|	ЗапасыВНаличии.Склад КАК Склад,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК Заказ,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаСобытия,
		|	ЗапасыВНаличии.ВНаличии КАК ВНаличии,
		|	0 КАК Запас,
		|	0 КАК Резерв,
		|	ЕСТЬNULL(Потребности.РезервироватьПоМереПоступления, 0) КАК РезервироватьПоМереПоступления,
		|	ЕСТЬNULL(Потребности.КОбеспечению, 0) КАК КОбеспечению,
		|	0 КАК Свободно,
		|	0 КАК Излишек
		|ИЗ
		|	ЕстьВНаличииЕстьДвиженияНетЗапаса КАК ЗапасыВНаличии
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПотребностиПоТовару КАК Потребности
		|		ПО Потребности.Номенклатура = ЗапасыВНаличии.Номенклатура
		|		 И Потребности.Характеристика = ЗапасыВНаличии.Характеристика
		|		 И Потребности.Склад = ЗапасыВНаличии.Склад
		|		 И Потребности.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, Раздел, ДатаСобытия, Заказ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВсеЗапасы;
		|УНИЧТОЖИТЬ ЗапасыВНаличии;
		|УНИЧТОЖИТЬ ПотребностиПоТовару";
		
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ЗапасыИДоступностьПоДатам = ПакетЗапросов[ПакетЗапросов.ВГраница() - 3].Выгрузить();
	
	Номенклатура = Неопределено; Характеристика = Неопределено; Склад = Неопределено; Назначение = Неопределено;
	
	Для Индекс = 0 По ЗапасыИДоступностьПоДатам.Количество() - 1 Цикл
		
		СтрокаТаблицы = ЗапасыИДоступностьПоДатам[Индекс];
		Если Номенклатура <> СтрокаТаблицы.Номенклатура
				Или Характеристика <> СтрокаТаблицы.Характеристика
				Или Склад <> СтрокаТаблицы.Склад
				Или Назначение <> СтрокаТаблицы.Назначение Тогда
					
					Номенклатура = СтрокаТаблицы.Номенклатура;
					Характеристика = СтрокаТаблицы.Характеристика;
					Склад = СтрокаТаблицы.Склад;
					Назначение = СтрокаТаблицы.Назначение;
					
					РаспределитьВсего = СтрокаТаблицы.РезервироватьПоМереПоступления;
					РаспределитьИзлишекВсего = СтрокаТаблицы.КОбеспечению;
				
		КонецЕсли;
		
		РаспределитьНаСтроку = Мин(РаспределитьВсего, Макс(СтрокаТаблицы.Запас - СтрокаТаблицы.Резерв, 0));
		СтрокаТаблицы.Свободно = СтрокаТаблицы.Запас - СтрокаТаблицы.Резерв - РаспределитьНаСтроку;
		РаспределитьВсего = РаспределитьВсего - РаспределитьНаСтроку;
		
		РаспределитьИзлишекНаСтроку = Мин(РаспределитьИзлишекВсего, Макс(СтрокаТаблицы.Свободно, 0));
		СтрокаТаблицы.Излишек = СтрокаТаблицы.Свободно - РаспределитьИзлишекНаСтроку;
		РаспределитьИзлишекВсего = РаспределитьИзлишекВсего - РаспределитьИзлишекНаСтроку;
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ЗапасыИДоступностьПоДатам", ЗапасыИДоступностьПоДатам);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗапасыИДоступностьПоДатам.Раздел КАК Раздел,
		|	ЗапасыИДоступностьПоДатам.Номенклатура КАК Номенклатура,
		|	ЗапасыИДоступностьПоДатам.Характеристика КАК Характеристика,
		|	ЗапасыИДоступностьПоДатам.Склад КАК Склад,
		|	ЗапасыИДоступностьПоДатам.Назначение КАК Назначение,
		|	ЗапасыИДоступностьПоДатам.Заказ КАК Заказ,
		|	ЗапасыИДоступностьПоДатам.ДатаСобытия КАК ДатаСобытия,
		|	ЗапасыИДоступностьПоДатам.ВНаличии КАК ВНаличии,
		|	ЗапасыИДоступностьПоДатам.Запас КАК Запас,
		|	ЗапасыИДоступностьПоДатам.Резерв КАК Резерв,
		|	ЗапасыИДоступностьПоДатам.Свободно КАК Свободно,
		|	ЗапасыИДоступностьПоДатам.Излишек КАК Излишек,
		|	ВЫБОР
		|		КОГДА ЗапасыИДоступностьПоДатам.Раздел = 0 ТОГДА
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)
		|		КОГДА ЗапасыИДоступностьПоДатам.Раздел = 1 ТОГДА
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеподтвержденное)
		|	КОНЕЦ КАК Состояние
		|ПОМЕСТИТЬ ЗапасыИДоступностьПоДатам
		|ИЗ
		|	&ЗапасыИДоступностьПоДатам КАК ЗапасыИДоступностьПоДатам
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ВременнаяТаблицаРезультатРаспределенияЗапасовНаПотребности(Запрос, ИмяТаблицыТовары = "ТоварыДляРаспределенияЗапасов", ИмяТаблицыРезультат = "РаспределениеЗапасовНаПотребности", ИмяТаблицыРеестрДокументов = "РегистрСведений.РеестрДокументов") Экспорт
	
	Текст =
		// Потребности.
		"ВЫБРАТЬ
		|	Набор.Номенклатура КАК Номенклатура,
		|	Набор.Характеристика КАК Характеристика,
		|	Набор.Склад КАК Склад,
		|	Набор.Назначение КАК Назначение,
		|	Набор.Заказ КАК Заказ,
		|	Набор.ДатаСобытия КАК ДатаСобытия,
		|	СУММА(Набор.РезервироватьПоМереПоступления) КАК РезервироватьПоМереПоступления,
		|	СУММА(Набор.РезервироватьПоМереПоступленияИтогНаДату) КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	СУММА(Набор.КОбеспечению) КАК КОбеспечению
		|ПОМЕСТИТЬ ПотребностиОстатки
		|ИЗ(
		|	ВЫБРАТЬ
		|		Потребности.Номенклатура КАК Номенклатура,
		|		Потребности.Характеристика КАК Характеристика,
		|		Потребности.Склад КАК Склад,
		|		Потребности.Назначение КАК Назначение,
		|		Потребности.Заказ КАК Заказ,
		|		Потребности.ДатаСобытия КАК ДатаСобытия,
		|		Потребности.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|		Потребности.РезервироватьПоМереПоступленияИтогНаДату КАК РезервироватьПоМереПоступленияИтогНаДату,
		|		0 КАК КОбеспечению
		|	ИЗ
		|		ПотребностиРезервироватьПоМере КАК Потребности
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Потребности.Номенклатура КАК Номенклатура,
		|		Потребности.Характеристика КАК Характеристика,
		|		Потребности.Склад КАК Склад,
		|		Потребности.Назначение КАК Назначение,
		|		Потребности.Заказ КАК Заказ,
		|		Потребности.ДатаСобытия КАК ДатаСобытия,
		|		0 КАК РезервироватьПоМереПоступления,
		|		0 КАК РезервироватьПоМереПоступленияИтогНаДату,
		|		Потребности.КОбеспечению + Потребности.ОтложитьРезервирование КАК КОбеспечению
		|	ИЗ
		|		ВсеПотребности КАК Потребности
		|	ГДЕ
		|		Потребности.КОбеспечению + Потребности.ОтложитьРезервирование > 0) КАК Набор
		|СГРУППИРОВАТЬ ПО
		|	Набор.Номенклатура,
		|	Набор.Характеристика,
		|	Набор.Склад,
		|	Набор.Назначение,
		|	Набор.Заказ,
		|	Набор.ДатаСобытия
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Потребности.Заказ КАК Заказ
		|ПОМЕСТИТЬ РазличныеЗаказыПотребностей
		|ИЗ
		|	ПотребностиОстатки КАК Потребности
		|ИНДЕКСИРОВАТЬ ПО
		|	Заказ
		|;
		|
		|///////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказыРеестра.Ссылка КАК Ссылка,
		|	МИНИМУМ(ЗаказыРеестра.Приоритет) КАК Приоритет,
		|	МИНИМУМ(ЗаказыРеестра.ДатаДокументаИБ) КАК ДатаДокументаИБ
		|ПОМЕСТИТЬ ТаблицаЗаказыРеестра
		|ИЗ
		|	РазличныеЗаказыПотребностей КАК РазличныеЗаказыПотребностей
		|		ЛЕВОЕ СОЕДИНЕНИЕ РеестрДокументовПереопределяемый КАК ЗаказыРеестра
		|		ПО ЗаказыРеестра.Ссылка = РазличныеЗаказыПотребностей.Заказ
		|		И НЕ ЗаказыРеестра.ДополнительнаяЗапись
		|ГДЕ
		|	НЕ ЗаказыРеестра.Ссылка ЕСТЬ NULL
		|СГРУППИРОВАТЬ ПО
		|	ЗаказыРеестра.Ссылка
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Потребности.Номенклатура КАК Номенклатура,
		|	Потребности.Характеристика КАК Характеристика,
		|	Потребности.Склад КАК Склад,
		|	Потребности.Назначение КАК Назначение,
		|	Потребности.Заказ КАК Заказ,
		|	СУММА(Потребности.РезервироватьПоМереПоступления) КАК РезервироватьПоМереПоступления,
		|	СУММА(Потребности.КОбеспечению) КАК КОбеспечению
		|ПОМЕСТИТЬ ПотребностиОстаткиГруппировкаПоЗаказам
		|ИЗ
		|	ПотребностиОстатки КАК Потребности
		|СГРУППИРОВАТЬ ПО
		|	Потребности.Номенклатура,
		|	Потребности.Характеристика,
		|	Потребности.Склад,
		|	Потребности.Назначение,
		|	Потребности.Заказ
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Потребности.Номенклатура КАК Номенклатура,
		|	Потребности.Характеристика КАК Характеристика,
		|	Потребности.Склад КАК Склад,
		|	Потребности.Назначение КАК Назначение,
		|	СУММА(Потребности.РезервироватьПоМереПоступления) КАК РезервироватьПоМереПоступления,
		|	СУММА(Потребности.КОбеспечению) КАК КОбеспечению
		|ПОМЕСТИТЬ ПотребностиОстаткиГруппировкаПоТоварам
		|ИЗ
		|	ПотребностиОстаткиГруппировкаПоЗаказам КАК Потребности
		|СГРУППИРОВАТЬ ПО
		|	Потребности.Номенклатура,
		|	Потребности.Характеристика,
		|	Потребности.Склад,
		|	Потребности.Назначение
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗапасыРаспределенныеРанее.Номенклатура КАК Номенклатура,
		|	ЗапасыРаспределенныеРанее.Характеристика КАК Характеристика,
		|	ЗапасыРаспределенныеРанее.Склад КАК Склад,
		|	ЗапасыРаспределенныеРанее.Назначение КАК Назначение,
		|	СУММА(ВЫБОР
		|		КОГДА ЗапасыРаспределенныеРанее.Зарезервировано > Потребности.РезервироватьПоМереПоступления
		|			ТОГДА Потребности.РезервироватьПоМереПоступления
		|		ИНАЧЕ ЗапасыРаспределенныеРанее.Зарезервировано
		|		КОНЕЦ) КАК Зарезервировано
		|ПОМЕСТИТЬ ЗапасыРаспределенныеНаЗаказыРанееГруппировкаПоТоварам
		|ИЗ
		|	ЗапасыРаспределенныеНаЗаказыРанее КАК ЗапасыРаспределенныеРанее
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПотребностиОстаткиГруппировкаПоЗаказам КАК Потребности
		|		ПО Потребности.Номенклатура = ЗапасыРаспределенныеРанее.Номенклатура
		|		 И Потребности.Характеристика = ЗапасыРаспределенныеРанее.Характеристика
		|		 И Потребности.Склад = ЗапасыРаспределенныеРанее.Склад
		|		 И Потребности.Назначение = ЗапасыРаспределенныеРанее.Назначение
		|		 И Потребности.Заказ = ЗапасыРаспределенныеРанее.Заказ
		|СГРУППИРОВАТЬ ПО
		|	ЗапасыРаспределенныеРанее.Номенклатура,
		|	ЗапасыРаспределенныеРанее.Характеристика,
		|	ЗапасыРаспределенныеРанее.Склад,
		|	ЗапасыРаспределенныеРанее.Назначение
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.Склад КАК Склад,
		|	Запасы.Назначение КАК Назначение,
		|	СУММА(ВЫБОР
		|		КОГДА Запасы.Заказ = НЕОПРЕДЕЛЕНО ТОГДА
		|			Запасы.Запас
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК ЗапасНаСкладе,
		|	СУММА(ВЫБОР
		|		КОГДА Запасы.Заказ = НЕОПРЕДЕЛЕНО ТОГДА
		|			Запасы.Резерв
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК Резерв,
		|	СУММА(ВЫБОР
		|		КОГДА Запасы.Заказ = НЕОПРЕДЕЛЕНО ТОГДА
		|			Запасы.Свободно
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК СвободноНаСкладе,
		|	СУММА(ВЫБОР
		|		КОГДА Запасы.Заказ <> НЕОПРЕДЕЛЕНО ТОГДА
		|			Запасы.Запас
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК ЗапасВГрафике,
		|	СУММА(ВЫБОР
		|		КОГДА Запасы.Заказ <> НЕОПРЕДЕЛЕНО ТОГДА
		|			Запасы.Свободно
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК СвободноВГрафике
		|ПОМЕСТИТЬ ЗапасыИДоступностьПоДатамГруппировкаПоТоварам
		|ИЗ
		|	ЗапасыИДоступностьПоДатам КАК Запасы
		|СГРУППИРОВАТЬ ПО
		|	Запасы.Номенклатура,
		|	Запасы.Характеристика,
		|	Запасы.Склад,
		|	Запасы.Назначение
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|///////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.Склад КАК Склад,
		|	Товары.Назначение КАК Назначение,
		// Потребности по товару.
		|	ЕСТЬNULL(ПотребностиПоТоварам.РезервироватьПоМереПоступления, 0) КАК РезервироватьПоМереПоступленияПоТовару,
		|	ЕСТЬNULL(ПотребностиПоТоварам.КОбеспечению, 0) КАК КОбеспечениюПоТовару,
		// Потребности по заказу.
		|	ЕСТЬNULL(ПотребностиПоЗаказам.РезервироватьПоМереПоступления, 0) КАК РезервироватьПоМереПоступленияПоЗаказу,
		|	ЕСТЬNULL(ПотребностиПоЗаказам.КОбеспечению, 0) КАК КОбеспечениюПоЗаказу,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ЗапасыРаспределенныеНаЗаказыРанее.Зарезервировано, 0) > ЕСТЬNULL(ПотребностиПоЗаказам.РезервироватьПоМереПоступления, 0)
		|			ТОГДА ЕСТЬNULL(ПотребностиПоЗаказам.РезервироватьПоМереПоступления, 0)
		|		ИНАЧЕ ЕСТЬNULL(ЗапасыРаспределенныеНаЗаказыРанее.Зарезервировано, 0)
		|	КОНЕЦ КАК РаспределеноПоЗаказу,
		// Потребности - строки заказов.
		|	Потребности.Заказ КАК ЗаказНаОтгрузку,
		|	Потребности.ДатаСобытия КАК ЖелаемаяДатаОтгрузки,
		|	ЕСТЬNULL(Потребности.РезервироватьПоМереПоступления, 0) КАК РезервироватьПоМереПоступления,
		|	ЕСТЬNULL(Потребности.РезервироватьПоМереПоступленияИтогНаДату, 0) КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	ЕСТЬNULL(Потребности.КОбеспечению, 0) КАК КОбеспечению,
		// Запасы для распределения по товару.
		|	ЕСТЬNULL(ЗапасыПоТоварам.ЗапасНаСкладе - ЗапасыПоТоварам.Резерв, 0) КАК РаспределитьНаВсеСтроки,
		|	ЕСТЬNULL(ЗапасыПоТоварам.ЗапасНаСкладе, 0)
		|		- ЕСТЬNULL(ЗапасыПоТоварам.Резерв, 0)
		|		- ЕСТЬNULL(ЗапасыРаспределенныеНаЗаказыПоТоварам.Зарезервировано, 0) КАК СвободноПоТовару,
		|	ЕСТЬNULL(ЗапасыПоТоварам.ЗапасНаСкладе, 0)
		|		- ЕСТЬNULL(ЗапасыПоТоварам.Резерв, 0)
		|		- ЕСТЬNULL(ПотребностиПоТоварам.РезервироватьПоМереПоступления, 0) КАК СвободноПоТоваруНаСтрокиКОбеспечению,
		|	ЕСТЬNULL(ЗапасыПоТоварам.ЗапасВГрафике, 0) КАК ВсегоВГрафике,
		|	ЕСТЬNULL(ЗапасыПоТоварам.СвободноВГрафике, 0) КАК ВсегоВГрафикеНаСтрокиКОбеспечению,
		// Данные заполнения.
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.Обеспечить) КАК Состояние,
		|	0 КАК НеОбеспечено,
		// Порядок обхода заказов.
		|	ВЫРАЗИТЬ(ЗаказыРеестра.Приоритет КАК Справочник.Приоритеты).РеквизитДопУпорядочивания КАК Приоритет,
		|	
		|	ВЫБОР
		|		КОГДА НЕ УпорядочиваниеПоДатеДокумента.Значение ЕСТЬ NULL
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА Потребности.ДатаСобытия > &КонецТекущегоДня
		|			ТОГДА Потребности.ДатаСобытия
		|		КОГДА ИСТИНА
		|			ТОГДА ЗаказыРеестра.ДатаДокументаИБ
		|	КОНЕЦ КАК ПорядокОсновной,
		|	ВЫБОР
		|		КОГДА ИСТИНА
		|			ТОГДА ЗаказыРеестра.ДатаДокументаИБ
		|	КОНЕЦ КАК ПорядокДополнительный,
		|	ВЫБОР
		|		КОГДА Потребности.ДатаСобытия > &КонецТекущегоДня
		|			ТОГДА Потребности.ДатаСобытия
		|		КОГДА ИСТИНА
		|			ТОГДА ЗаказыРеестра.ДатаДокументаИБ
		|	КОНЕЦ КАК ПорядокПоДатеОтгрузкиПослеДатыДокумента
		|ИЗ
		|	ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПотребностиОстатки КАК Потребности
		|		ПО Потребности.Номенклатура = Товары.Номенклатура
		|		 И Потребности.Характеристика = Товары.Характеристика
		|		 И Потребности.Склад = Товары.Склад
		|		 И Потребности.Назначение = Товары.Назначение
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЗапасыРаспределенныеНаЗаказыРанее КАК ЗапасыРаспределенныеНаЗаказыРанее
		|		ПО ЗапасыРаспределенныеНаЗаказыРанее.Номенклатура = Товары.Номенклатура
		|		 И ЗапасыРаспределенныеНаЗаказыРанее.Характеристика = Товары.Характеристика
		|		 И ЗапасыРаспределенныеНаЗаказыРанее.Склад = Товары.Склад
		|		 И ЗапасыРаспределенныеНаЗаказыРанее.Назначение = Товары.Назначение
		|		 И ЗапасыРаспределенныеНаЗаказыРанее.Заказ = Потребности.Заказ
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЗапасыРаспределенныеНаЗаказыРанееГруппировкаПоТоварам КАК ЗапасыРаспределенныеНаЗаказыПоТоварам
		|		ПО ЗапасыРаспределенныеНаЗаказыПоТоварам.Номенклатура = Товары.Номенклатура
		|		 И ЗапасыРаспределенныеНаЗаказыПоТоварам.Характеристика = Товары.Характеристика
		|		 И ЗапасыРаспределенныеНаЗаказыПоТоварам.Склад = Товары.Склад
		|		 И ЗапасыРаспределенныеНаЗаказыПоТоварам.Назначение = Товары.Назначение
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПотребностиОстаткиГруппировкаПоЗаказам КАК ПотребностиПоЗаказам
		|		ПО ПотребностиПоЗаказам.Номенклатура = Товары.Номенклатура
		|		 И ПотребностиПоЗаказам.Характеристика = Товары.Характеристика
		|		 И ПотребностиПоЗаказам.Склад = Товары.Склад
		|		 И ПотребностиПоЗаказам.Назначение = Товары.Назначение
		|		 И ПотребностиПоЗаказам.Заказ = Потребности.Заказ
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПотребностиОстаткиГруппировкаПоТоварам КАК ПотребностиПоТоварам
		|		ПО ПотребностиПоТоварам.Номенклатура = Товары.Номенклатура
		|		 И ПотребностиПоТоварам.Характеристика = Товары.Характеристика
		|		 И ПотребностиПоТоварам.Склад = Товары.Склад
		|		 И ПотребностиПоТоварам.Назначение = Товары.Назначение
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЗапасыИДоступностьПоДатамГруппировкаПоТоварам КАК ЗапасыПоТоварам
		|		ПО ЗапасыПоТоварам.Номенклатура = Товары.Номенклатура
		|		 И ЗапасыПоТоварам.Характеристика = Товары.Характеристика
		|		 И ЗапасыПоТоварам.Склад = Товары.Склад
		|		 И ЗапасыПоТоварам.Назначение = Товары.Назначение
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЗаказыРеестра КАК ЗаказыРеестра
		|		ПО ЗаказыРеестра.Ссылка = Потребности.Заказ
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ВариантУпорядочиванияПотребностейПоЗаказамДляОбеспечения КАК УпорядочиваниеПоДатеДокумента
		|		ПО УпорядочиваниеПоДатеДокумента.Значение = ЗНАЧЕНИЕ(Перечисление.ВариантыУпорядочиванияПотребностейПоЗаказамДляОбеспечения.ПриоритетИДатаДокумента)
		|		
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	Склад,
		|	Назначение,
		|	Приоритет,
		|	ПорядокОсновной,
		|	ПорядокДополнительный,
		|	ПорядокПоДатеОтгрузкиПослеДатыДокумента,
		|	ЗаказНаОтгрузку
		|;
		|
		|//////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГрафикПоступления.Номенклатура КАК Номенклатура,
		|	ГрафикПоступления.Характеристика КАК Характеристика,
		|	ГрафикПоступления.Склад КАК Склад,
		|	ГрафикПоступления.Назначение КАК Назначение,
		|	ГрафикПоступления.Раздел КАК Раздел,
		|	ВЫБОР
		|		КОГДА ГрафикПоступления.Раздел = 1 ТОГДА
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченКДате)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаетсяПоНеподтвержденномуЗаказу)
		|	КОНЕЦ КАК Состояние,
		|	ГрафикПоступления.Заказ КАК ЗаказНаПоступление,
		|	ГрафикПоступления.ДатаСобытия КАК ДатаПоступления,
		|	ЕСТЬNULL(ГрафикПоступления.Запас - ГрафикПоступления.Свободно, 0) КАК Распределить,
		|	ЕСТЬNULL(ГрафикПоступления.Свободно, 0) КАК РаспределитьНаСтрокиКОбеспечению
		|
		|ИЗ
		|	ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЗапасыИДоступностьПоДатам КАК ГрафикПоступления
		|		ПО ГрафикПоступления.Номенклатура = Товары.Номенклатура
		|		 И ГрафикПоступления.Характеристика = Товары.Характеристика
		|		 И ГрафикПоступления.Склад = Товары.Склад
		|		 И ГрафикПоступления.Назначение = Товары.Назначение
		|		 И ГрафикПоступления.Заказ <> НЕОПРЕДЕЛЕНО
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, Раздел, ДатаПоступления, ЗаказНаПоступление
		|;
		|
		|////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПотребностиОстатки;
		|УНИЧТОЖИТЬ РазличныеЗаказыПотребностей;
		|УНИЧТОЖИТЬ ТаблицаЗаказыРеестра;
		|УНИЧТОЖИТЬ ПотребностиОстаткиГруппировкаПоЗаказам;
		|УНИЧТОЖИТЬ ПотребностиОстаткиГруппировкаПоТоварам;
		|УНИЧТОЖИТЬ ЗапасыИДоступностьПоДатамГруппировкаПоТоварам;
		|УНИЧТОЖИТЬ ЗапасыРаспределенныеНаЗаказыРанее;
		|УНИЧТОЖИТЬ ЗапасыРаспределенныеНаЗаказыРанееГруппировкаПоТоварам";
	
	Текст = СтрЗаменить(Текст, "РеестрДокументовПереопределяемый", ИмяТаблицыРеестрДокументов);
	Запрос.Текст = СтрЗаменить(Текст, "ТоварыДляРаспределенияЗапасовПереопределяемый", ИмяТаблицыТовары);
	Запрос.УстановитьПараметр("КонецТекущегоДня", КонецДня(ТекущаяДатаСеанса()));
	Пакет = Запрос.ВыполнитьПакет();

	Таблица = Пакет[Пакет.Вграница() - 9].Выгрузить();
	График = Пакет[Пакет.Вграница() - 8].Выгрузить();
	
	РезультатРаспределенияЗапасовНаПотребности = РегистрыСведений.РаспределениеЗапасов.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	РаспределитьЗапасыВТаблицу(РезультатРаспределенияЗапасовНаПотребности, Таблица, График, Ложь);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Назначение КАК Назначение,
		|	Таблица.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|	Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	Таблица.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|	Таблица.ДатаПоступления КАК ДатаПоступления,
		|	Таблица.Состояние КАК Состояние,
		|	Таблица.Зарезервировано КАК Зарезервировано,
		|	Таблица.Обеспечено КАК Обеспечено,
		|	Таблица.НеОбеспечено КАК НеОбеспечено,
		|	Таблица.НеОбеспеченоКОбеспечению КАК НеОбеспеченоКОбеспечению
		|ПОМЕСТИТЬ РезультатРаспределенияЗапасовНаПотребности
		|ИЗ
		|	&РезультатРаспределенияЗапасовНаПотребности КАК Таблица";
		
	Запрос.УстановитьПараметр("РезультатРаспределенияЗапасовНаПотребности", РезультатРаспределенияЗапасовНаПотребности);
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ДанныеДляЗаписиТоваровВПроизводительномРежиме(Запрос, ИмяТаблицыТовары = "ТоварыДляРаспределенияЗапасов", ИмяТаблицыРезультат = "", ПолучатьПустые = Истина)
	
	Текст =
		"ВЫБРАТЬ
		|	ЛОЖЬ КАК Пустая,
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Назначение КАК Назначение,
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка) Состояние,
		|	Таблица.Заказ КАК ЗаказНаОтгрузку,
		|	Таблица.ДатаСобытия КАК ЖелаемаяДатаОтгрузки,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПоступление,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаПоступления,
		|	1 КАК ТипЗаписиРаспределенияЗапасов,
		|	Таблица.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|	Таблица.КОбеспечению КАК КОбеспечениюБезРезерва,
		|	Таблица.ОтложитьРезервирование КАК ОтложитьРезервирование,
		|	Таблица.РезервироватьНаСкладе КАК Резервировать,
		|	Таблица.НеОбеспечивать КАК НеОбеспечивать,
		|	0 КАК Зарезервировано,
		|	0 КАК Обеспечено,
		|	0 КАК НеОбеспечено,
		|	0 КАК Запас,
		|	0 КАК Резерв,
		|	0 КАК Свободно,
		|	0 КАК Излишек,
		|	0 КАК ВНаличии,
		|	ПотребностиРезервироватьПоМере.РезервироватьПоМереПоступленияИтогНаДату КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	0 КАК НеОбеспеченоКОбеспечению
		|ПОМЕСТИТЬ ИмяТаблицыРезультатПереопределяемый
		|ИЗ
		|	ВсеПотребности КАК Таблица
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПотребностиРезервироватьПоМере КАК ПотребностиРезервироватьПоМере
		|		ПО ПотребностиРезервироватьПоМере.Номенклатура = Таблица.Номенклатура
		|		 И ПотребностиРезервироватьПоМере.Характеристика = Таблица.Характеристика
		|		 И ПотребностиРезервироватьПоМере.Склад = Таблица.Склад
		|		 И ПотребностиРезервироватьПоМере.Назначение = Таблица.Назначение
		|		 И ПотребностиРезервироватьПоМере.Заказ = Таблица.Заказ
		|		 И ПотребностиРезервироватьПоМере.ДатаСобытия = Таблица.ДатаСобытия
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ КАК Пустая,
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Назначение КАК Назначение,
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.НеОбеспечивать) Состояние,
		|	Таблица.Заказ КАК ЗаказНаОтгрузку,
		|	Таблица.ДатаСобытия КАК ЖелаемаяДатаОтгрузки,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПоступление,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаПоступления,
		|	1 КАК ТипЗаписиРаспределенияЗапасов,
		|	0 КАК РезервироватьПоМереПоступления,
		|	0 КАК КОбеспечениюБезРезерва,
		|	0 КАК ОтложитьРезервирование,
		|	0 КАК Резервировать,
		|	0 КАК НеОбеспечивать,
		|	0 КАК Зарезервировано,
		|	0 КАК Обеспечено,
		|	Таблица.НеОбеспечивать КАК НеОбеспечено,
		|	0 КАК Запас,
		|	0 КАК Резерв,
		|	0 КАК Свободно,
		|	0 КАК Излишек,
		|	0 КАК ВНаличии,
		|	0 КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	0 КАК НеОбеспеченоКОбеспечению
		|ИЗ
		|	ВсеПотребности КАК Таблица
		|ГДЕ
		|	Таблица.НеОбеспечивать > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ КАК Пустая,
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Назначение КАК Назначение,
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ВРезерве) Состояние,
		|	Таблица.Заказ КАК ЗаказНаОтгрузку,
		|	Таблица.ДатаСобытия КАК ЖелаемаяДатаОтгрузки,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПоступление,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаПоступления,
		|	1 КАК ТипЗаписиРаспределенияЗапасов,
		|	0 КАК РезервироватьПоМереПоступления,
		|	0 КАК КОбеспечениюБезРезерва,
		|	0 КАК ОтложитьРезервирование,
		|	0 КАК Резервировать,
		|	0 КАК НеОбеспечивать,
		|	Таблица.РезервироватьНаСкладе КАК Зарезервировано,
		|	0 КАК Обеспечено,
		|	0 КАК НеОбеспечено,
		|	0 КАК Запас,
		|	0 КАК Резерв,
		|	0 КАК Свободно,
		|	0 КАК Излишек,
		|	0 КАК ВНаличии,
		|	0 КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	0 КАК НеОбеспеченоКОбеспечению
		|ИЗ
		|	ВсеПотребности КАК Таблица
		|ГДЕ
		|	Таблица.РезервироватьНаСкладе > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ КАК Пустая,
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Назначение КАК Назначение,
		|	Таблица.Состояние Состояние,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаОтгрузку,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ЖелаемаяДатаОтгрузки,
		|	Таблица.Заказ КАК ЗаказНаПоступление,
		|	Таблица.ДатаСобытия КАК ДатаПоступления,
		|	1 КАК ТипЗаписиРаспределенияЗапасов,
		|	0 КАК РезервироватьПоМереПоступления,
		|	0 КАК КОбеспечениюБезРезерва,
		|	0 КАК ОтложитьРезервирование,
		|	0 КАК Резервировать,
		|	0 КАК НеОбеспечивать,
		|	0 КАК Зарезервировано,
		|	0 КАК Обеспечено,
		|	0 КАК НеОбеспечено,
		|	Таблица.Запас КАК Запас,
		|	Таблица.Резерв КАК Резерв,
		|	Таблица.Свободно КАК Свободно,
		|	Таблица.Излишек КАК Излишек,
		|	Таблица.ВНаличии КАК ВНаличии,
		|	0 КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	0 КАК НеОбеспеченоКОбеспечению
		|ИЗ
		|	ЗапасыИДоступностьПоДатам КАК Таблица
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ КАК Пустая,
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Назначение КАК Назначение,
		|	Таблица.Состояние Состояние,
		|	Таблица.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|	Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	Таблица.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|	Таблица.ДатаПоступления КАК ДатаПоступления,
		|	0 КАК ТипЗаписиРаспределенияЗапасов,
		|	0 КАК РезервироватьПоМереПоступления,
		|	0 КАК КОбеспечениюБезРезерва,
		|	0 КАК ОтложитьРезервирование,
		|	0 КАК Резервировать,
		|	0 КАК НеОбеспечивать,
		|	Таблица.Зарезервировано КАК Зарезервировано,
		|	Таблица.Обеспечено КАК Обеспечено,
		|	Таблица.НеОбеспечено КАК НеОбеспечено,
		|	0 КАК Запас,
		|	0 КАК Резерв,
		|	0 КАК Свободно,
		|	0 КАК Излишек,
		|	0 КАК ВНаличии,
		|	0 КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	Таблица.НеОбеспеченоКОбеспечению КАК НеОбеспеченоКОбеспечению
		|ИЗ
		|	РезультатРаспределенияЗапасовНаПотребности КАК Таблица";
		
	Если ПолучатьПустые Тогда
		
		Тексты = Новый Массив();
		Тексты.Добавить(Текст);
		Текст =
		"ВЫБРАТЬ
		|	ИСТИНА КАК Пустая,
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Назначение КАК Назначение,
		|	НЕОПРЕДЕЛЕНО Состояние,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаОтгрузку,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ЖелаемаяДатаОтгрузки,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПоступление,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаПоступления,
		|	0 КАК ТипЗаписиРаспределенияЗапасов,
		|	0 КАК РезервироватьПоМереПоступления,
		|	0 КАК КОбеспечениюБезРезерва,
		|	0 КАК ОтложитьРезервирование,
		|	0 КАК Резервировать,
		|	0 КАК НеОбеспечивать,
		|	0 КАК Зарезервировано,
		|	0 КАК Обеспечено,
		|	0 КАК НеОбеспечено,
		|	0 КАК Запас,
		|	0 КАК Резерв,
		|	0 КАК Свободно,
		|	0 КАК Излишек,
		|	0 КАК ВНаличии,
		|	0 КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	0 КАК НеОбеспеченоКОбеспечению
		|ИЗ
		|	ТоварыДляРаспределенияЗапасовПереопределяемый КАК Таблица";
		Тексты.Добавить(Текст);
		Текст = СтрСоединить(Тексты, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	КонецЕсли;
	
	ТекстУпорядочивания =
		"УПОРЯДОЧИТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение"; //@Query-part
	
	ТекстУдаленияТаблиц =
		"УНИЧТОЖИТЬ ВсеПотребности;
		|УНИЧТОЖИТЬ ПотребностиРезервироватьПоМере;
		|УНИЧТОЖИТЬ ЗапасыИДоступностьПоДатам;
		|УНИЧТОЖИТЬ РезультатРаспределенияЗапасовНаПотребности";
	
	Если ИмяТаблицыРезультат = "" Тогда
		Тексты = Новый Массив();
		Тексты.Добавить(Текст);
		Тексты.Добавить(ТекстУпорядочивания);
		Текст = СтрСоединить(Тексты, Символы.ПС);
	КонецЕсли;
	Тексты = Новый Массив();
	Тексты.Добавить(Текст);
	Тексты.Добавить(ТекстУдаленияТаблиц);
	Текст = СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Текст = СтрЗаменить(Текст, "ТоварыДляРаспределенияЗапасовПереопределяемый", ИмяТаблицыТовары);
	Если ИмяТаблицыРезультат = "" Тогда
		ЗаменяемыйТекст = "ПОМЕСТИТЬ ИмяТаблицыРезультатПереопределяемый"; //@Query-part
		Текст = СтрЗаменить(Текст, ЗаменяемыйТекст, "");
		Запрос.Текст = Текст;
		Результат = Запрос.ВыполнитьПакет()[0].Выгрузить();
	Иначе
		Запрос.Текст = СтрЗаменить(Текст, "ИмяТаблицыРезультатПереопределяемый", ИмяТаблицыРезультат);
		Результат = Запрос.ВыполнитьПакет()[0];
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура РаспределитьЗапасыВТаблицу(Набор, Таблица, График, ДобавлятьПустуюЗаписьПоТовару) Экспорт
	
	Номенклатура   = Неопределено;
	Характеристика = Неопределено;
	Склад          = Неопределено;
	Назначение     = Неопределено;
	ВариантОбеспеченияКОбеспечению = Перечисления.ВариантыОбеспечения.КОбеспечению;
	ИндексВГрафике = 0;
	ИндексВГрафикеОбеспечить = 0;
	
	Свободно                = 0;
	РаспределитьНаВсеСтроки = 0;
	КОбеспечению            = 0;
	СвободноВГрафике        = 0;
	РезервироватьПоМереПоступления = 0;
	
	ВсегоСтрок = Таблица.Количество();
	Для Индекс = 0 По ВсегоСтрок - 1 Цикл
		
		ТекСтрока = Таблица[Индекс];
		
		Если Номенклатура <> ТекСтрока.Номенклатура
				Или Характеристика <> ТекСтрока.Характеристика
				Или Склад <> ТекСтрока.Склад
				Или Назначение <> ТекСтрока.Назначение Тогда
					
					Номенклатура   = ТекСтрока.Номенклатура;
					Характеристика = ТекСтрока.Характеристика;
					Склад          = ТекСтрока.Склад;
					Назначение     = ТекСтрока.Назначение;
					
					РаспределитьНаВсеСтроки = Макс(ТекСтрока.РаспределитьНаВсеСтроки, 0);
					Свободно                = Макс(ТекСтрока.СвободноПоТовару, 0);
					КОбеспечению            = ТекСтрока.КОбеспечениюПоТовару;
					СвободноНаСтрокиКОбеспечению = Макс(ТекСтрока.СвободноПоТоваруНаСтрокиКОбеспечению, 0);
					
					СвободноВГрафике = Мин(
						Макс(ТекСтрока.РезервироватьПоМереПоступленияПоТовару - РаспределитьНаВсеСтроки, 0),
						ТекСтрока.ВсегоВГрафике);
					СвободноВГрафикеНаСтрокиКОбеспечению = ТекСтрока.ВсегоВГрафикеНаСтрокиКОбеспечению;
					
					Если ДобавлятьПустуюЗаписьПоТовару Тогда
						НоваяЗапись = Набор.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока, "Номенклатура,Характеристика,Склад,Назначение");
						НоваяЗапись.Пустая = Истина;
					КонецЕсли;
					
					
		КонецЕсли;
		
		// Распределяем то, что уже было распределено на заказ ранее.
		РаспределитьНаСтроку = Мин(
			Макс(ТекСтрока.РаспределеноПоЗаказу - ТекСтрока.РезервироватьПоМереПоступленияИтогНаДату + ТекСтрока.РезервироватьПоМереПоступления, 0),
			ТекСтрока.РезервироватьПоМереПоступления);
		
		// Распределяем запас на строки Резервировать по мере поступления.
		Если РаспределитьНаСтроку < ТекСтрока.РезервироватьПоМереПоступления Тогда
			СвободноНаСтроку = Мин(Свободно, ТекСтрока.РезервироватьПоМереПоступления - РаспределитьНаСтроку);
			РаспределитьНаСтроку = РаспределитьНаСтроку + СвободноНаСтроку;
			Свободно = Свободно - СвободноНаСтроку;
		КонецЕсли;
		
		РаспределитьНаВсеСтроки = РаспределитьНаВсеСтроки - РаспределитьНаСтроку;
		СнятьРаспределение = Макс(0, -РаспределитьНаВсеСтроки);
		РаспределитьНаСтроку = РаспределитьНаСтроку - СнятьРаспределение;
		РаспределитьНаВсеСтроки = РаспределитьНаВсеСтроки + СнятьРаспределение;
		
		РаспределитьНаСтрокуВГрафике = 0;
		Если РаспределитьНаСтроку < ТекСтрока.РезервироватьПоМереПоступления Тогда
			РаспределитьНаСтрокуВГрафике = Мин(ТекСтрока.РезервироватьПоМереПоступления - РаспределитьНаСтроку, СвободноВГрафике);
			СвободноВГрафике = СвободноВГрафике - РаспределитьНаСтрокуВГрафике;
		КонецЕсли;
		
		НеОбеспечено = ТекСтрока.РезервироватьПоМереПоступления - РаспределитьНаСтроку - РаспределитьНаСтрокуВГрафике;
		
		// Распределяем свободный остаток на строки К обеспечению.
		ОбеспеченоНаСкладе = Мин(СвободноНаСтрокиКОбеспечению, ТекСтрока.КОбеспечению);
		СвободноНаСтрокиКОбеспечению = СвободноНаСтрокиКОбеспечению - ОбеспеченоНаСкладе;
		
		ОбеспеченоВГрафике = 0;
		Если ОбеспеченоНаСкладе < ТекСтрока.КОбеспечению Тогда
			ОбеспеченоВГрафике = Мин(ТекСтрока.КОбеспечению - ОбеспеченоНаСкладе, СвободноВГрафикеНаСтрокиКОбеспечению);
			СвободноВГрафикеНаСтрокиКОбеспечению = СвободноВГрафикеНаСтрокиКОбеспечению - ОбеспеченоВГрафике;
		КонецЕсли;
		
		НеОбеспеченоКОбеспечению = ТекСтрока.КОбеспечению - ОбеспеченоНаСкладе - ОбеспеченоВГрафике;
		НеОбеспечено = НеОбеспечено + НеОбеспеченоКОбеспечению;
		
		Если РаспределитьНаСтроку > 0 Тогда
			
			НоваяЗапись = Набор.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока,
				"Номенклатура,Характеристика,Склад,Назначение,ЗаказНаОтгрузку,ЖелаемаяДатаОтгрузки");
			НоваяЗапись.Состояние = Перечисления.РаспределениеЗапасовСостояния.ЗарезервированПриПоступлении;
			НоваяЗапись.Зарезервировано = РаспределитьНаСтроку;
			
		КонецЕсли;
		
		Если ОбеспеченоНаСкладе > 0 Тогда
			
			НоваяЗапись = Набор.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока,
				"Номенклатура,Характеристика,Склад,Назначение,ЗаказНаОтгрузку,ЖелаемаяДатаОтгрузки");
			НоваяЗапись.Состояние = Перечисления.РаспределениеЗапасовСостояния.ОбеспеченНаСкладе;
			НоваяЗапись.Обеспечено = ОбеспеченоНаСкладе;
			
		КонецЕсли;
		
		// Позиционирование на поступлениях текущей номенклатуры.
		Если РаспределитьНаСтрокуВГрафике > 0 Или ОбеспеченоВГрафике > 0 Тогда
			
			Пока Номенклатура <> График[ИндексВГрафике].Номенклатура
					Или Характеристика <> График[ИндексВГрафике].Характеристика
					Или Склад <> График[ИндексВГрафике].Склад
					Или Назначение <> График[ИндексВГрафике].Назначение Цикл
				ИндексВГрафике = ИндексВГрафике + 1;
				ИндексВГрафикеОбеспечить = ИндексВГрафике;
				Продолжить;
			КонецЦикла;
			
		КонецЕсли;
		
		НоваяЗапись = Неопределено;
		Пока РаспределитьНаСтрокуВГрафике > 0 Цикл
			
			Если График[ИндексВГрафике].Распределить = 0 Тогда
				ИндексВГрафике = ИндексВГрафике + 1;
				Продолжить;
			КонецЕсли;
			
			РаспределитьНаСтроку = Мин(График[ИндексВГрафике].Распределить, РаспределитьНаСтрокуВГрафике);
			График[ИндексВГрафике].Распределить = График[ИндексВГрафике].Распределить - РаспределитьНаСтроку;
			РаспределитьНаСтрокуВГрафике = РаспределитьНаСтрокуВГрафике - РаспределитьНаСтроку;
			
			НоваяЗапись = Набор.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока,
				"Номенклатура,Характеристика,Склад,Назначение,ЗаказНаОтгрузку,ЖелаемаяДатаОтгрузки");
			ЗаполнитьЗначенияСвойств(НоваяЗапись, График[ИндексВГрафике], "ЗаказНаПоступление,ДатаПоступления,Состояние");
			НоваяЗапись.Зарезервировано = РаспределитьНаСтроку;
			
		КонецЦикла;
		
		Пока ОбеспеченоВГрафике > 0 Цикл
			
			Если График[ИндексВГрафикеОбеспечить].РаспределитьНаСтрокиКОбеспечению = 0 Тогда
				ИндексВГрафикеОбеспечить = ИндексВГрафикеОбеспечить + 1;
				Продолжить;
			КонецЕсли;
			
			РаспределитьНаСтроку = Мин(График[ИндексВГрафикеОбеспечить].РаспределитьНаСтрокиКОбеспечению, ОбеспеченоВГрафике);
			График[ИндексВГрафикеОбеспечить].РаспределитьНаСтрокиКОбеспечению = График[ИндексВГрафикеОбеспечить].РаспределитьНаСтрокиКОбеспечению - РаспределитьНаСтроку;
			ОбеспеченоВГрафике = ОбеспеченоВГрафике - РаспределитьНаСтроку;
			
			Если НоваяЗапись = Неопределено
				Или НоваяЗапись.ЗаказНаПоступление <> График[ИндексВГрафикеОбеспечить].ЗаказНаПоступление
				Или НоваяЗапись.ДатаПоступления <> График[ИндексВГрафикеОбеспечить].ДатаПоступления
				Или НоваяЗапись.Состояние <> График[ИндексВГрафикеОбеспечить].Состояние Тогда
					НоваяЗапись = Набор.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока,
						"Номенклатура,Характеристика,Склад,Назначение,ЗаказНаОтгрузку,ЖелаемаяДатаОтгрузки");
					ЗаполнитьЗначенияСвойств(НоваяЗапись, График[ИндексВГрафикеОбеспечить], "ЗаказНаПоступление,ДатаПоступления,Состояние");
			КонецЕсли;
			НоваяЗапись.Обеспечено = РаспределитьНаСтроку;
			
		КонецЦикла;
		
		Если НеОбеспечено > 0 Тогда
			
			НоваяЗапись = Набор.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока,
				"Номенклатура,Характеристика,Склад,Назначение,ЗаказНаОтгрузку,ЖелаемаяДатаОтгрузки,Состояние");
			НоваяЗапись.НеОбеспечено = НеОбеспечено;
			НоваяЗапись.НеОбеспеченоКОбеспечению = НеОбеспеченоКОбеспечению;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СостояниеСУчетомОтраженияИзменений(Заказ, ТаблицаДвижений, МенеджерВременныхТаблиц, ТаблицаИмитацияРеестра) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	УстановитьПривилегированныйРежим(Истина);
	
	//
	
	Запрос.УстановитьПараметр("ТаблицаИмитацияРеестра", ТаблицаИмитацияРеестра);
	Запрос.УстановитьПараметр("Заказ", Заказ);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаказыРеестра.ЗаказНаОтгрузку КАК Ссылка,
		|	ЗаказыРеестра.Приоритет КАК Приоритет,
		|	ЗаказыРеестра.ДатаДокумента КАК ДатаДокументаИБ
		|ПОМЕСТИТЬ ТаблицаИмитацияРеестра
		|ИЗ
		|	&ТаблицаИмитацияРеестра КАК ЗаказыРеестра
		|;
		|
		|///////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказыРеестра.Ссылка КАК Ссылка,
		|	ЗаказыРеестра.Приоритет КАК Приоритет,
		|	ЗаказыРеестра.ДатаДокументаИБ КАК ДатаДокументаИБ,
		|	ЛОЖЬ КАК ДополнительнаяЗапись
		|ПОМЕСТИТЬ РеестрДокументовСУчетомИзменений
		|ИЗ
		|	РегистрСведений.РеестрДокументов КАК ЗаказыРеестра
		|ГДЕ
		|	НЕ Ссылка В(
		|		ВЫБРАТЬ
		|			ЗаказыРеестра.Ссылка КАК Ссылка
		|		ИЗ
		|			ТаблицаИмитацияРеестра КАК ЗаказыРеестра)
		|		И НЕ ЗаказыРеестра.ДополнительнаяЗапись
		|		И ИСТИНА В(
		|			ВЫБРАТЬ ПЕРВЫЕ 1
		|				ИСТИНА
		|			ИЗ
		|				РегистрСведений.РаспределениеЗапасов КАК Сведения
		|			ГДЕ
		|				Сведения.ЗаказНаОтгрузку = ЗаказыРеестра.Ссылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗаказыРеестра.Ссылка КАК Ссылка,
		|	ЗаказыРеестра.Приоритет КАК Приоритет,
		|	ЗаказыРеестра.ДатаДокументаИБ КАК ДатаДокументаИБ,
		|	ЛОЖЬ КАК ДополнительнаяЗапись
		|ИЗ
		|	ТаблицаИмитацияРеестра КАК ЗаказыРеестра";
	
	Запрос.Выполнить();
	
	Запрос.УстановитьПараметр("НаборЗаписейЗапасыИПотребности", ТаблицаДвижений);
	Запрос.УстановитьПараметр("Регистратор", Заказ);
	
	РаспределениеЗапасовДвижения.ЗапасыИПотребностиДвиженияПриЗаписиВоВременнуюТаблицу(Запрос, Ложь);
	РаспределениеЗапасовДвижения.ЗапасыИПотребностиИзменениеВоВременнуюТаблицу(Запрос);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Назначение КАК Назначение
		|ПОМЕСТИТЬ ТоварыДляРаспределенияЗапасов
		|ИЗ
		|	ДвиженияПриЗаписи КАК Таблица";
	Запрос.Выполнить();
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Набор.Номенклатура КАК Номенклатура,
		|	Набор.Характеристика КАК Характеристика,
		|	Набор.Склад КАК Склад,
		|	Набор.Назначение КАК Назначение,
		|	Набор.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|	СУММА(Набор.Отгрузка) КАК Отгрузка
		|ПОМЕСТИТЬ ИзменениеОстаткаПоТоварамДляКонтроляОстатков
		|ИЗ(
		|	ВЫБРАТЬ
		|		ИзмененияДвижений.Номенклатура КАК Номенклатура,
		|		ИзмененияДвижений.Характеристика КАК Характеристика,
		|		ИзмененияДвижений.Склад КАК Склад,
		|		ИзмененияДвижений.Назначение КАК Назначение,
		|		ИзмененияДвижений.Заказ КАК ЗаказНаОтгрузку,
		|		СУММА(ИзмененияДвижений.ВНаличииРасход + ИзмененияДвижений.РезервироватьНаСкладе) КАК Отгрузка
		|	ИЗ
		|		ЗапасыИПотребностиИзменение КАК ИзмененияДвижений
		|	СГРУППИРОВАТЬ ПО
		|		ИзмененияДвижений.Номенклатура,
		|		ИзмененияДвижений.Характеристика,
		|		ИзмененияДвижений.Склад,
		|		ИзмененияДвижений.Назначение,
		|		ИзмененияДвижений.Заказ
		|	ИМЕЮЩИЕ
		|		СУММА(ИзмененияДвижений.ВНаличииРасход + ИзмененияДвижений.РезервироватьНаСкладе) > 0
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Задания.Номенклатура КАК Номенклатура,
		|		Задания.Характеристика КАК Характеристика,
		|		Задания.Склад КАК Склад,
		|		Задания.Назначение КАК Назначение,
		|		Задания.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|		Задания.КСнятиюРезерва КАК Отгрузка
		|	ИЗ
		|		ТоварыДляРаспределенияЗапасов КАК Товары
		|			
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРаспределениюЗапасов КАК Задания
		|			ПО Задания.Номенклатура = Товары.Номенклатура
		|			 И Задания.Характеристика = Товары.Характеристика
		|			 И Задания.Склад = Товары.Склад
		|			 И Задания.Назначение = Товары.Назначение
		|	ГДЕ
		|		Задания.КСнятиюРезерва > 0) КАК Набор
		|СГРУППИРОВАТЬ ПО
		|	Набор.Номенклатура,
		|	Набор.Характеристика,
		|	Набор.Склад,
		|	Набор.Назначение,
		|	Набор.ЗаказНаОтгрузку
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение";
	Запрос.Выполнить();
	
	ВременныеТаблицыДанныхРегистра(Запрос, Ложь, "ТоварыДляРаспределенияЗапасов", "ЗапасыИПотребностиИзменение");
	ВременнаяТаблицаПотребностиРезервироватьПоМере(Запрос);
	ВременнаяТаблицаЗапасыИДоступностьПоДатам(Запрос);
	ВременнаяТаблицаРезультатРаспределенияЗапасовНаПотребности(Запрос, "ТоварыДляРаспределенияЗапасов", "РаспределениеЗапасовНаПотребности", "РеестрДокументовСУчетомИзменений");
	ДанныеДляЗаписиТоваровВПроизводительномРежиме(Запрос, "ТоварыДляРаспределенияЗапасов", "ИмитацияОтражения");
	
КонецПроцедуры


Процедура ОбновитьИнформациюОДоступностиТоваровДляВнешнихПользователей() Экспорт

	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ОбновлениеДанныхОДоступностиТоваровДляВнешнихПользователей);
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоДня", НачалоДня(ТекущаяДатаСеанса()));
	
	Тексты = Новый Массив();
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	&НачалоДня КАК Период,
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.Склад КАК Склад,
		|	ВЫБОР
		|		КОГДА Запасы.ВНаличииОстаток - Запасы.РезервироватьНаСкладеОстаток < 0
		|			ТОГДА
		|				Запасы.ВНаличииОстаток - Запасы.РезервироватьНаСкладеОстаток
		|		КОГДА Запасы.ВНаличииОстаток - Запасы.РезервироватьНаСкладеОстаток - Запасы.РезервироватьПоМереПоступленияОстаток > 0
		|			ТОГДА
		|				Запасы.ВНаличииОстаток - Запасы.РезервироватьНаСкладеОстаток - Запасы.РезервироватьПоМереПоступленияОстаток
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Доступно
		|ИЗ
		|	РегистрНакопления.ЗапасыИПотребности.Остатки(,
		|		Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|			И Номенклатура.ТипНоменклатуры В(
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))) КАК Запасы";
	Тексты.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВЫБОР КОГДА РаспределениеЗапасов.ДатаПоступления > &НачалоДня ТОГДА
		|			РаспределениеЗапасов.ДатаПоступления
		|		ИНАЧЕ
		|			&НачалоДня
		|		КОНЕЦ КАК Период,
		|	РаспределениеЗапасов.Номенклатура КАК Номенклатура,
		|	РаспределениеЗапасов.Характеристика Характеристика,
		|	РаспределениеЗапасов.Склад КАК Склад,
		|	РаспределениеЗапасов.Свободно КАК Доступно
		|ИЗ
		|	РегистрСведений.РаспределениеЗапасов КАК РаспределениеЗапасов
		|ГДЕ
		|	РаспределениеЗапасов.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		И РаспределениеЗапасов.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление)
		|		И РаспределениеЗапасов.Номенклатура.ТипНоменклатуры В(
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|		И РаспределениеЗапасов.Свободно <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоДня КАК Период,
		|	СтарыеДанные.Номенклатура КАК Номенклатура,
		|	СтарыеДанные.Характеристика КАК Характеристика,
		|	СтарыеДанные.Склад КАК Склад,
		|	0 КАК Доступно
		|ИЗ
		|	РегистрСведений.ДоступностьТоваровДляВнешнихПользователей КАК СтарыеДанные";
	Тексты.Добавить(ТекстЗапроса);
	ТекстЗапроса = СтрСоединить(Тексты, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	Текст =
		"ВЫБРАТЬ
		|	ВложенныйЗапрос.Период КАК Период,
		|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
		|	ВложенныйЗапрос.Характеристика КАК Характеристика,
		|	ВложенныйЗапрос.Склад КАК Склад,
		|	СУММА(ВложенныйЗапрос.Доступно) КАК Доступно
		|ИЗ
		|	ВложенныйЗапросПереопределяемый КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Номенклатура,
		|	ВложенныйЗапрос.Характеристика,
		|	ВложенныйЗапрос.Склад,
		|	ВложенныйЗапрос.Период
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	Склад,
		|	Период";
	Запрос.Текст = СтрЗаменить(Текст, "ВложенныйЗапросПереопределяемый", СтрШаблон("(%1)", ТекстЗапроса));
	
	СтрокаОстатка = Запрос.Выполнить().Выбрать();
	
	Ключ = Новый Структура("Номенклатура,Характеристика,Склад");
	Итог = 0;
	НаборЗаписей = РегистрыСведений.ДоступностьТоваровДляВнешнихПользователей.СоздатьНаборЗаписей();
	Пока СтрокаОстатка.Следующий() Цикл
		
		Если СтрокаОстатка.Номенклатура <> Ключ.Номенклатура
			Или СтрокаОстатка.Характеристика <> Ключ.Характеристика
			Или СтрокаОстатка.Склад <> Ключ.Склад Тогда
			
			Если Ключ.Номенклатура <> Неопределено Тогда
				НаборЗаписей.Записать();
				НаборЗаписей.Очистить();
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(Ключ, СтрокаОстатка);
			Итог = 0;
			НаборЗаписей.Отбор.Номенклатура.Установить(Ключ.Номенклатура, Истина);
			НаборЗаписей.Отбор.Характеристика.Установить(Ключ.Характеристика, Истина);
			НаборЗаписей.Отбор.Склад.Установить(Ключ.Склад, Истина);
			
		КонецЕсли;
		Итог = СтрокаОстатка.Доступно + Итог;
		
		Если Итог > 0 Тогда
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, СтрокаОстатка);
			Запись.Доступно = Итог;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Ключ.Номенклатура <> Неопределено Тогда
		НаборЗаписей.Записать();
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  Товар - Структура - структура с полями:
//   * Номенклатура - СправочникСсылка.Номенклатура - номенклатура,
//   * Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика,
//   * Склад - СправочникСсылка.Склады, СправочникСсылка.СтруктураПредприятия - склад или подразделение,
//   * Назначение - СправочникСсылка.Назначения - азначение
// Возвращаемое значение:
//  РегистрСведенийНаборЗаписей.РаспределениеЗапасов - набор данных для записи при обновлении. 
Функция ОбновлениеИБПоТовару(Товар) Экспорт
	
	Набор = РегистрыСведений.РаспределениеЗапасов.СоздатьНаборЗаписей();
	Набор.Отбор.Номенклатура.Установить(Товар.Номенклатура);
	Набор.Отбор.Характеристика.Установить(Товар.Характеристика);
	Набор.Отбор.Склад.Установить(Товар.Склад);
	Набор.Отбор.Назначение.Установить(Товар.Назначение);
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Номенклатура", Товар.Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Товар.Характеристика);
	Запрос.УстановитьПараметр("Склад", Товар.Склад);
	Запрос.УстановитьПараметр("Назначение", Товар.Назначение);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	&Номенклатура КАК Номенклатура,
		|	&Характеристика КАК Характеристика,
		|	&Склад КАК Склад,
		|	&Назначение КАК Назначение
		|ПОМЕСТИТЬ ТоварыДляРаспределенияЗапасов
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|//////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Задания.Номенклатура КАК Номенклатура,
		|	Задания.Характеристика КАК Характеристика,
		|	Задания.Склад КАК Склад,
		|	Задания.Назначение КАК Назначение,
		|	Задания.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|	СУММА(Задания.КСнятиюРезерва) КАК Отгрузка
		|ПОМЕСТИТЬ ИзменениеОстаткаПоТоварамДляКонтроляОстатков
		|ИЗ
		|	РегистрСведений.ЗаданияКРаспределениюЗапасов КАК Задания
		|ГДЕ
		|	Задания.КСнятиюРезерва > 0
		|		И &Номенклатура = Задания.Номенклатура
		|		И &Характеристика = Задания.Номенклатура
		|		И &Склад = Задания.Номенклатура
		|		И &Назначение = Задания.Номенклатура
		|СГРУППИРОВАТЬ ПО
		|	Задания.Номенклатура,
		|	Задания.Характеристика,
		|	Задания.Склад,
		|	Задания.Назначение,
		|	Задания.ЗаказНаОтгрузку
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, ЗаказНаОтгрузку";
	Запрос.Выполнить();
	ВременныеТаблицыДанныхРегистра(Запрос, Ложь);
	ВременнаяТаблицаПотребностиРезервироватьПоМере(Запрос);
	ВременнаяТаблицаЗапасыИДоступностьПоДатам(Запрос);
	ВременнаяТаблицаРезультатРаспределенияЗапасовНаПотребности(Запрос);
	Результат = ДанныеДляЗаписиТоваровВПроизводительномРежиме(Запрос, "ТоварыДляРаспределенияЗапасов", "", Ложь);
	Набор.Загрузить(Результат);
	
	Возврат Набор;
	
КонецФункции

// Параметры:
//  Запрос - Запрос - запрос, хранящий параметры используемые в списке запросов
//  ТекстыЗапроса - СписокЗначений Из Строка - список текстов запросов и их имен.
//  Документ - ДокументОбъект - записываемый документ.
Процедура ДобавитьТекстЗапросаКонтроль(Запрос, ТекстыЗапроса, Документ) Экспорт
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИзмененияДвижений.Номенклатура КАК Номенклатура,
		|	ИзмененияДвижений.Характеристика КАК Характеристика,
		|	ИзмененияДвижений.Склад КАК Склад,
		|	ИзмененияДвижений.Назначение КАК Назначение,
		|	СУММА(ИзмененияДвижений.ВНаличииРасход) КАК Отгрузка,
		|	СУММА(ИзмененияДвижений.РезервироватьНаСкладе - ИзмененияДвижений.ВНаличии) КАК Расход,
		|	СУММА(ИзмененияДвижений.РезервироватьНаСкладе
		|			+ ИзмененияДвижений.РезервироватьПоМереПоступления
		|			- ИзмененияДвижений.ВНаличии) КАК РасходСкладПлюсПоставка
		|ПОМЕСТИТЬ ИзмененияДляКонтроляОбеспечения
		|ИЗ
		|	ЗапасыИПотребностиИзменение КАК ИзмененияДвижений
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаХарактеристика
		|		ПО НастройкаХарактеристика.Склад          = ИзмененияДвижений.Склад
		|		 И НастройкаХарактеристика.Номенклатура   = ИзмененияДвижений.Номенклатура
		|		 И НастройкаХарактеристика.Характеристика = ИзмененияДвижений.Характеристика
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаНоменклатура
		|		ПО НастройкаНоменклатура.Склад        = ИзмененияДвижений.Склад
		|		 И НастройкаНоменклатура.Номенклатура = ИзмененияДвижений.Номенклатура
		|		 И НастройкаНоменклатура.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		 И НастройкаХарактеристика.Склад ЕСТЬ NULL
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаСклад
		|		ПО НастройкаСклад.Склад          = ИзмененияДвижений.Склад
		|		 И НастройкаСклад.Номенклатура   = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|		 И НастройкаСклад.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		 И НастройкаХарактеристика.Склад ЕСТЬ NULL
		|		 И НастройкаНоменклатура.Склад ЕСТЬ NULL
		|ГДЕ
		|	ЕСТЬNULL(НастройкаХарактеристика.КонтролироватьСвободныеОстатки,
		|		ЕСТЬNULL(НастройкаНоменклатура.КонтролироватьСвободныеОстатки,
		|			НастройкаСклад.КонтролироватьСвободныеОстатки))
		|		И ВЫРАЗИТЬ(ИзмененияДвижений.Номенклатура КАК Справочник.Номенклатура).ТипНоменклатуры
		|			<> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|СГРУППИРОВАТЬ ПО
		|	ИзмененияДвижений.Номенклатура,
		|	ИзмененияДвижений.Характеристика,
		|	ИзмененияДвижений.Склад,
		|	ИзмененияДвижений.Назначение
		|ИМЕЮЩИЕ
		|	СУММА(ИзмененияДвижений.РезервироватьНаСкладе + ИзмененияДвижений.ВНаличииРасход) > 0
		|		ИЛИ СУММА(ИзмененияДвижений.ВНаличииРасход) > 0
		|		ИЛИ СУММА(ИзмененияДвижений.РезервироватьНаСкладе
		|					+ ИзмененияДвижений.РезервироватьПоМереПоступления
		|					+ ИзмененияДвижений.ВНаличииРасход) > 0
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ИзмененияДляКонтроляОбеспечения");
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Набор.Номенклатура КАК Номенклатура,
		|	Набор.Характеристика КАК Характеристика,
		|	Набор.Склад КАК Склад,
		|	Набор.Назначение КАК Назначение,
		|	СУММА(Набор.Зарезервировано) КАК Зарезервировано
		|ПОМЕСТИТЬ РезервыПоЗаказамДляКонтроляОбеспечения
		|ИЗ(
		|	ВЫБРАТЬ
		|		НаборПоЗаказам.Номенклатура КАК Номенклатура,
		|		НаборПоЗаказам.Характеристика КАК Характеристика,
		|		НаборПоЗаказам.Склад КАК Склад,
		|		НаборПоЗаказам.Назначение КАК Назначение,
		|		НаборПоЗаказам.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|		СУММА(НаборПоЗаказам.Зарезервировано) КАК Зарезервировано
		|	ИЗ(
		|		ВЫБРАТЬ
		|			РаспределениеЗапасов.Номенклатура КАК Номенклатура,
		|			РаспределениеЗапасов.Характеристика КАК Характеристика,
		|			РаспределениеЗапасов.Склад КАК Склад,
		|			РаспределениеЗапасов.Назначение КАК Назначение,
		|			РаспределениеЗапасов.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|			РаспределениеЗапасов.Зарезервировано КАК Зарезервировано
		|		ИЗ
		|			ИзмененияДляКонтроляОбеспечения КАК ИзмененияДвижений
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК РаспределениеЗапасов
		|				ПО РаспределениеЗапасов.Номенклатура = ИзмененияДвижений.Номенклатура
		|				 И РаспределениеЗапасов.Характеристика = ИзмененияДвижений.Характеристика
		|				 И РаспределениеЗапасов.Склад = ИзмененияДвижений.Склад
		|				 И РаспределениеЗапасов.Назначение = ИзмененияДвижений.Назначение
		|		ГДЕ
		|			РаспределениеЗапасов.Состояние В(
		|					ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченНаСкладе),
		|					ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ЗарезервированПриПоступлении))
		|				И РаспределениеЗапасов.Зарезервировано > 0
		|				И ИСТИНА В(
		|					ВЫБРАТЬ ПЕРВЫЕ 1
		|						ИСТИНА КАК ЕстьЗаписи
		|					ИЗ
		|						ЗапасыИПотребностиПередЗаписью КАК Заказы
		|					ГДЕ
		|						Заказы.Номенклатура = РаспределениеЗапасов.Номенклатура
		|						 И Заказы.Характеристика = РаспределениеЗапасов.Характеристика
		|						 И Заказы.Склад = РаспределениеЗапасов.Склад
		|						 И Заказы.Назначение = РаспределениеЗапасов.Назначение
		|						 И Заказы.Заказ = РаспределениеЗапасов.ЗаказНаОтгрузку)
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ЗаданияПоЗаказам.Номенклатура КАК Номенклатура,
		|			ЗаданияПоЗаказам.Характеристика КАК Характеристика,
		|			ЗаданияПоЗаказам.Склад КАК Склад,
		|			ЗаданияПоЗаказам.Назначение КАК Назначение,
		|			ЗаданияПоЗаказам.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|			-ЗаданияПоЗаказам.КСнятиюРезерва КАК Зарезервировано
		|		ИЗ
		|			ИзмененияДляКонтроляОбеспечения КАК ИзмененияДвижений
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРаспределениюЗапасов КАК ЗаданияПоЗаказам
		|				ПО ЗаданияПоЗаказам.Номенклатура = ИзмененияДвижений.Номенклатура
		|				 И ЗаданияПоЗаказам.Характеристика = ИзмененияДвижений.Характеристика
		|				 И ЗаданияПоЗаказам.Склад = ИзмененияДвижений.Склад
		|				 И ЗаданияПоЗаказам.Назначение = ИзмененияДвижений.Назначение
		|		ГДЕ
		|			ЗаданияПоЗаказам.КСнятиюРезерва > 0
		|				И ИСТИНА В(
		|					ВЫБРАТЬ ПЕРВЫЕ 1
		|						ИСТИНА КАК ЕстьЗаписи
		|					ИЗ
		|						ЗапасыИПотребностиПередЗаписью КАК Заказы
		|					ГДЕ
		|						Заказы.Номенклатура = ЗаданияПоЗаказам.Номенклатура
		|						 И Заказы.Характеристика = ЗаданияПоЗаказам.Характеристика
		|						 И Заказы.Склад = ЗаданияПоЗаказам.Склад
		|						 И Заказы.Назначение = ЗаданияПоЗаказам.Назначение
		|						 И Заказы.Заказ = ЗаданияПоЗаказам.ЗаказНаОтгрузку)) КАК НаборПоЗаказам
		|	СГРУППИРОВАТЬ ПО
		|		НаборПоЗаказам.Номенклатура,
		|		НаборПоЗаказам.Характеристика,
		|		НаборПоЗаказам.Склад,
		|		НаборПоЗаказам.Назначение,
		|		НаборПоЗаказам.ЗаказНаОтгрузку
		|	ИМЕЮЩИЕ
		|		СУММА(НаборПоЗаказам.Зарезервировано) > 0) КАК Набор
		|СГРУППИРОВАТЬ ПО
		|	Набор.Номенклатура,
		|	Набор.Характеристика,
		|	Набор.Склад,
		|	Набор.Назначение";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "РезервыПоЗаказамДляКонтроляОбеспечения");
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИзмененияДвижений.Номенклатура КАК Номенклатура,
		|	ИзмененияДвижений.Характеристика КАК Характеристика,
		|	ИзмененияДвижений.Склад КАК Склад,
		|	ИзмененияДвижений.Назначение КАК Назначение,
		|	ИзмененияДвижений.РасходСкладПлюсПоставка КАК РасходСкладПлюсПоставка,
		|	ИзмененияДвижений.Расход - ЕСТЬNULL(РезервыПоЗаказам.Зарезервировано, 0) КАК Расход,
		|	ИзмененияДвижений.Отгрузка КАК Отгрузка,
		|	ЕСТЬNULL(РезервыПоЗаказам.Зарезервировано, 0) КАК Зарезервировано
		|ПОМЕСТИТЬ ИзмененияИРезервыДляКонтроляОбеспечения
		|ИЗ
		|	ИзмененияДляКонтроляОбеспечения КАК ИзмененияДвижений
		|		ЛЕВОЕ СОЕДИНЕНИЕ РезервыПоЗаказамДляКонтроляОбеспечения КАК РезервыПоЗаказам
		|		ПО РезервыПоЗаказам.Номенклатура = ИзмененияДвижений.Номенклатура
		|		 И РезервыПоЗаказам.Характеристика = ИзмененияДвижений.Характеристика
		|		 И РезервыПоЗаказам.Склад = ИзмененияДвижений.Склад
		|		 И РезервыПоЗаказам.Назначение = ИзмененияДвижений.Назначение
		|ГДЕ
		|	ИзмененияДвижений.Расход - ЕСТЬNULL(РезервыПоЗаказам.Зарезервировано, 0) > 0
		|		ИЛИ ИзмененияДвижений.Отгрузка > 0
		|		ИЛИ &КонтролироватьЗапасыТоваровПодлежащихРезервированиюПоМереПоступления
		|				И ИзмененияДвижений.РасходСкладПлюсПоставка > 0
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ИзмененияИРезервыДляКонтроляОбеспечения");
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Набор.Номенклатура КАК Номенклатура,
		|	Набор.Характеристика КАК Характеристика,
		|	Набор.Склад КАК Склад,
		|	Набор.Назначение КАК Назначение,
		|	СУММА(Набор.РасходСкладПлюсПоставка) КАК РасходСкладПлюсПоставка,
		|	СУММА(Набор.Расход) КАК Расход,
		|	СУММА(Набор.Отгрузка) КАК Отгрузка,
		|	СУММА(Набор.ДоступноСкладПлюсПоставка) КАК ДоступноСкладПлюсПоставка,
		|	&КонтролироватьЗапасыТоваровПодлежащихРезервированиюПоМереПоступления
		|		И МАКСИМУМ(Набор.ЕстьРезервироватьПоМереПоступления) КАК НуженКонтрольДоступногоОстатка,
		|	СУММА(Набор.Свободно) КАК Свободно,
		|	СУММА(Набор.ОстатокДляКонтроляОтгрузки) КАК ОстатокДляКонтроляОтгрузки,
		|	СУММА(Набор.ФизическийОстаток) КАК ФизическийОстаток
		|ПОМЕСТИТЬ ОшибкиКонтроляДляКонтроляОбеспечения
		|ИЗ(
		|	ВЫБРАТЬ
		|		Остатки.Номенклатура КАК Номенклатура,
		|		Остатки.Характеристика КАК Характеристика,
		|		Остатки.Склад КАК Склад,
		|		Остатки.Назначение КАК Назначение,
		|		0 КАК РасходСкладПлюсПоставка,
		|		0 КАК Расход,
		|		0 КАК Отгрузка,
		|		Остатки.ВНаличииОстаток
		|			+ Остатки.ПоступитОстаток
		|			- Остатки.РезервироватьНаСкладеОстаток
		|			- Остатки.РезервироватьПоМереПоступленияОстаток КАК ДоступноСкладПлюсПоставка,
		|		Остатки.РезервироватьПоМереПоступленияОстаток > 0 ЕстьРезервироватьПоМереПоступления,
		|		Остатки.ВНаличииОстаток
		|			- Остатки.РезервироватьНаСкладеОстаток
		|			- Остатки.РезервироватьПоМереПоступленияОстаток КАК Свободно,
		|		Остатки.ВНаличииОстаток
		|			- Остатки.РезервироватьНаСкладеОстаток КАК ОстатокДляКонтроляОтгрузки,
		|		Остатки.ВНаличииОстаток КАК ФизическийОстаток
		|	ИЗ
		|		РегистрНакопления.ЗапасыИПотребности.Остатки(,
		|			(Номенклатура, Характеристика, Склад, Назначение) В(
		|				ВЫБРАТЬ
		|					Таблица.Номенклатура КАК Номенклатура,
		|					Таблица.Характеристика КАК Характеристика,
		|					Таблица.Склад КАК Склад,
		|					Таблица.Назначение КАК Назначение
		|				ИЗ
		|					ИзмененияИРезервыДляКонтроляОбеспечения КАК Таблица)) КАК Остатки
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Остатки.Номенклатура КАК Номенклатура,
		|		Остатки.Характеристика КАК Характеристика,
		|		Остатки.Склад КАК Склад,
		|		Остатки.Назначение КАК Назначение,
		|		0 КАК РасходСкладПлюсПоставка,
		|		0 КАК Расход,
		|		0 КАК Отгрузка,
		|		0 КАК ДоступноСкладПлюсПоставка,
		|		ЛОЖЬ ЕстьРезервироватьПоМереПоступления,
		|		Остатки.РезервироватьПоМереПоступленияОстаток КАК Свободно,
		|		0 КАК ОстатокДляКонтроляОтгрузки,
		|		-Остатки.РезервироватьНаСкладеОстаток КАК ФизическийОстаток
		|	ИЗ
		|		РегистрНакопления.ЗапасыИПотребности.Остатки(,
		|			(Номенклатура, Характеристика, Склад, Назначение, Заказ) В(
		|				ВЫБРАТЬ
		|					Таблица.Номенклатура КАК Номенклатура,
		|					Таблица.Характеристика КАК Характеристика,
		|					Таблица.Склад КАК Склад,
		|					Таблица.Назначение КАК Назначение,
		|					Таблица.Заказ КАК Заказ
		|				ИЗ
		|					ЗапасыИПотребностиЗатронутыеЗаказы КАК Таблица)
		|			И (Номенклатура, Характеристика, Склад, Назначение) В(
		|				ВЫБРАТЬ
		|					Таблица.Номенклатура КАК Номенклатура,
		|					Таблица.Характеристика КАК Характеристика,
		|					Таблица.Склад КАК Склад,
		|					Таблица.Назначение КАК Назначение
		|				ИЗ
		|					ИзмененияИРезервыДляКонтроляОбеспечения КАК Таблица)) КАК Остатки
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ИзмененияИРезервы.Номенклатура КАК Номенклатура,
		|		ИзмененияИРезервы.Характеристика КАК Характеристика,
		|		ИзмененияИРезервы.Склад КАК Склад,
		|		ИзмененияИРезервы.Назначение КАК Назначение,
		|		ИзмененияИРезервы.РасходСкладПлюсПоставка КАК РасходСкладПлюсПоставка,
		|		ИзмененияИРезервы.Расход КАК Расход,
		|		ИзмененияИРезервы.Отгрузка КАК Отгрузка,
		|		0 КАК ДоступноСкладПлюсПоставка,
		|		ЛОЖЬ ЕстьРезервироватьПоМереПоступления,
		|		0 КАК Свободно,
		|		0 КАК ОстатокДляКонтроляОтгрузки,
		|		0 КАК ФизическийОстаток
		|	ИЗ
		|		ИзмененияИРезервыДляКонтроляОбеспечения КАК ИзмененияИРезервы) КАК Набор
		|СГРУППИРОВАТЬ ПО
		|	Набор.Номенклатура,
		|	Набор.Характеристика,
		|	Набор.Склад,
		|	Набор.Назначение
		|ИМЕЮЩИЕ
		|	СУММА(Набор.Расход) > 0 И СУММА(Набор.Свободно) < 0
		|		ИЛИ СУММА(Набор.Отгрузка) > 0 И СУММА(Набор.ОстатокДляКонтроляОтгрузки) < 0
		|		ИЛИ СУММА(Набор.РасходСкладПлюсПоставка) > 0
		|				И СУММА(Набор.ДоступноСкладПлюсПоставка) < 0
		|				И &КонтролироватьЗапасыТоваровПодлежащихРезервированиюПоМереПоступления
		|				И МАКСИМУМ(Набор.ЕстьРезервироватьПоМереПоступления)";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ОшибкиКонтроляДляКонтроляОбеспечения");
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ОшибкиКонтроля.Номенклатура КАК Номенклатура,
		|	ОшибкиКонтроля.Характеристика КАК Характеристика,
		|	ОшибкиКонтроля.Склад КАК Склад,
		|	ОшибкиКонтроля.Назначение КАК Назначение,
		|	ОшибкиКонтроля.НуженКонтрольДоступногоОстатка КАК НуженКонтрольДоступногоОстатка,
		|	ВЫРАЗИТЬ(ОшибкиКонтроля.Номенклатура КАК Справочник.Номенклатура).ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВЫБОР
		|		КОГДА
		|			ВЫБОР
		|				КОГДА -ОшибкиКонтроля.Свободно > ОшибкиКонтроля.Расход
		|					ТОГДА ОшибкиКонтроля.Расход
		|				ИНАЧЕ -ОшибкиКонтроля.Свободно
		|			КОНЕЦ
		|			> ВЫБОР
		|					КОГДА -ОшибкиКонтроля.ОстатокДляКонтроляОтгрузки > ОшибкиКонтроля.Отгрузка
		|						ТОГДА ОшибкиКонтроля.Отгрузка
		|					ИНАЧЕ -ОшибкиКонтроля.ОстатокДляКонтроляОтгрузки
		|				КОНЕЦ
		|			ТОГДА
		|				ВЫБОР
		|					КОГДА -ОшибкиКонтроля.Свободно > ОшибкиКонтроля.Расход
		|						ТОГДА ОшибкиКонтроля.Расход
		|					ИНАЧЕ -ОшибкиКонтроля.Свободно
		|				КОНЕЦ
		|		ИНАЧЕ
		|			ВЫБОР
		|				КОГДА -ОшибкиКонтроля.ОстатокДляКонтроляОтгрузки > ОшибкиКонтроля.Отгрузка
		|					ТОГДА ОшибкиКонтроля.Отгрузка
		|				ИНАЧЕ -ОшибкиКонтроля.ОстатокДляКонтроляОтгрузки
		|			КОНЕЦ
		|	КОНЕЦ КАК КоличествоСвободно,
		|	ВЫБОР
		|		КОГДА -ОшибкиКонтроля.ДоступноСкладПлюсПоставка > ОшибкиКонтроля.РасходСкладПлюсПоставка
		|			ТОГДА ОшибкиКонтроля.РасходСкладПлюсПоставка
		|		ИНАЧЕ -ОшибкиКонтроля.ДоступноСкладПлюсПоставка
		|	КОНЕЦ КАК КоличествоДоступно,
		|	ВЫБОР
		|		КОГДА -ОшибкиКонтроля.ФизическийОстаток >
		|				ВЫБОР
		|					КОГДА ОшибкиКонтроля.Расход > ОшибкиКонтроля.Отгрузка
		|						ТОГДА ОшибкиКонтроля.Расход
		|					ИНАЧЕ ОшибкиКонтроля.Отгрузка
		|				КОНЕЦ
		|			ТОГДА ВЫБОР
		|					КОГДА ОшибкиКонтроля.Расход > ОшибкиКонтроля.Отгрузка
		|						ТОГДА ОшибкиКонтроля.Расход
		|					ИНАЧЕ ОшибкиКонтроля.Отгрузка
		|				КОНЕЦ
		|		ИНАЧЕ -ОшибкиКонтроля.ФизическийОстаток
		|	КОНЕЦ КАК КоличествоФизическийОстаток
		|ИЗ
		|	ОшибкиКонтроляДляКонтроляОбеспечения КАК ОшибкиКонтроля";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ОбеспечениеРезультатыКонтроля");
	
	ТипДокумента = ТипЗнч(Документ.Ссылка);
	Если ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровОтКлиента")
			Или ТипДокумента = Тип("ДокументСсылка.КорректировкаРеализации")
			
			Или ТипДокумента = Тип("ДокументСсылка.ПоступлениеТоваровОтХранителя") Тогда
				
				Возврат;
	КонецЕсли;
	
	Если Не Запрос.Параметры.Свойство("МерныеТипыЕдиницИзмерений") Тогда
		Запрос.УстановитьПараметр("МерныеТипыЕдиницИзмерений",
			Справочники.УпаковкиЕдиницыИзмерения.МерныеТипыЕдиницИзмерений());
	КонецЕсли;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИзмененияДвижений.Номенклатура КАК Номенклатура,
		|	ИзмененияДвижений.Характеристика КАК Характеристика,
		|	ИзмененияДвижений.Склад КАК Склад,
		|	ИзмененияДвижений.Назначение КАК Назначение,
		|	МАКСИМУМ(ИзмененияДвижений.ВНаличииПриход > 0) КАК ЭтоУвеличениеПрихода
		|ПОМЕСТИТЬ АналитикиДляКонтроляПревышенияОбособленногоОбеспечения
		|ИЗ
		|	ЗапасыИПотребностиИзменение КАК ИзмененияДвижений
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаХарактеристика
		|		ПО НастройкаХарактеристика.Склад          = ИзмененияДвижений.Склад
		|		 И НастройкаХарактеристика.Номенклатура   = ИзмененияДвижений.Номенклатура
		|		 И НастройкаХарактеристика.Характеристика = ИзмененияДвижений.Характеристика
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаНоменклатура
		|		ПО НастройкаНоменклатура.Склад        = ИзмененияДвижений.Склад
		|		 И НастройкаНоменклатура.Номенклатура = ИзмененияДвижений.Номенклатура
		|		 И НастройкаНоменклатура.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		 И НастройкаХарактеристика.Склад ЕСТЬ NULL
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаСклад
		|		ПО НастройкаСклад.Склад          = ИзмененияДвижений.Склад
		|		 И НастройкаСклад.Номенклатура   = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|		 И НастройкаСклад.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		 И НастройкаХарактеристика.Склад ЕСТЬ NULL
		|		 И НастройкаНоменклатура.Склад ЕСТЬ NULL
		|		
		|ГДЕ
		|	ИзмененияДвижений.КонтролироватьПревышениеОбособленногоОбеспечения
		|		И (ЕСТЬNULL(НастройкаХарактеристика.КонтролироватьСвободныеОстатки,
		|				ЕСТЬNULL(НастройкаНоменклатура.КонтролироватьСвободныеОстатки,
		|					НастройкаСклад.КонтролироватьСвободныеОстатки))
		|			ИЛИ ИзмененияДвижений.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
		|СГРУППИРОВАТЬ ПО
		|	ИзмененияДвижений.Номенклатура,
		|	ИзмененияДвижений.Характеристика,
		|	ИзмененияДвижений.Склад,
		|	ИзмененияДвижений.Назначение
		|ИМЕЮЩИЕ
		|	СУММА(ИзмененияДвижений.ВНаличииПриход) > 0
		|		ИЛИ СУММА(
		|				ВЫБОР
		|					КОГДА &КонтролироватьПодтвержденныйГрафикПоступления
		|						ТОГДА ИзмененияДвижений.Поступит
		|					ИНАЧЕ ИзмененияДвижений.Заказано
		|				КОНЕЦ) > 0
		|		ИЛИ СУММА(ИзмененияДвижений.ВНаличииРасход
		|					+ ИзмененияДвижений.РезервироватьНаСкладе
		|					+ ИзмененияДвижений.РезервироватьПоМереПоступления
		|					+ ИзмененияДвижений.ОтложитьРезервирование
		|					+ ИзмененияДвижений.КОбеспечению) < 0
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "АналитикиДляКонтроляПревышенияОбособленногоОбеспечения");
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Остатки.Номенклатура КАК Номенклатура,
		|	Остатки.Характеристика КАК Характеристика,
		|	Остатки.Склад КАК Склад,
		|	Остатки.Назначение КАК Назначение,
		|	Остатки.ВНаличииОстаток КАК ВНаличииОстаток,
		|	ВЫБОР
		|		КОГДА &КонтролироватьПодтвержденныйГрафикПоступления
		|			ТОГДА Остатки.ПоступитОстаток
		|		ИНАЧЕ Остатки.ЗаказаноОстаток
		|	КОНЕЦ КАК ЗаказаноОстаток,
		|	Остатки.РезервироватьНаСкладеОстаток
		|		+ Остатки.РезервироватьПоМереПоступленияОстаток
		|		+ Остатки.ОтложитьРезервированиеОстаток
		|		+ Остатки.КОбеспечениюОстаток КАК ВсеПотребностиОстаток
		|ПОМЕСТИТЬ ЗапасыИПотребностиДляКонтроляОбособленногоОбеспечения
		|ИЗ
		|	РегистрНакопления.ЗапасыИПотребности.Остатки(,
		|		(Номенклатура, Характеристика, Склад, Назначение) В(
		|			ВЫБРАТЬ
		|				Аналитики.Номенклатура КАК Номенклатура,
		|				Аналитики.Характеристика КАК Характеристика,
		|				Аналитики.Склад КАК Склад,
		|				Аналитики.Назначение КАК Назначение
		|			ИЗ
		|				АналитикиДляКонтроляПревышенияОбособленногоОбеспечения КАК Аналитики)) КАК Остатки
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ЗапасыИПотребностиДляКонтроляОбособленногоОбеспечения");
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Остатки.Номенклатура КАК Номенклатура,
		|	Остатки.Характеристика КАК Характеристика,
		|	Остатки.Склад КАК Склад,
		|	Остатки.Назначение КАК Назначение,
		|	Остатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВЫБОР
		|		КОГДА Аналитики.ЭтоУвеличениеПрихода
		|				И Остатки.ВНаличииОстаток - Остатки.ВсеПотребностиОстаток
		|					> ВЫБОР
		|						КОГДА НЕ Остатки.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины
		|									В (&МерныеТипыЕдиницИзмерений)
		|							ТОГДА 0
		|						ИНАЧЕ Остатки.ВсеПотребностиОстаток * ДопустимоеОтклонение.Значение / 100
		|					КОНЕЦ
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ОшибкаНаличиеБольшеПотребности,
		|	Остатки.ВНаличииОстаток - Остатки.ВсеПотребностиОстаток КАК ВеличинаНаличиеБольшеПотребности,
		|	Остатки.ВНаличииОстаток + Остатки.ЗаказаноОстаток - Остатки.ВсеПотребностиОстаток КАК ВеличинаЗаказатьМеньшеНуля
		|ИЗ
		|	ЗапасыИПотребностиДляКонтроляОбособленногоОбеспечения КАК Остатки
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиДляКонтроляПревышенияОбособленногоОбеспечения КАК Аналитики
		|		ПО Аналитики.Номенклатура = Остатки.Номенклатура
		|		 И Аналитики.Характеристика = Остатки.Характеристика
		|		 И Аналитики.Склад = Остатки.Склад
		|		 И Аналитики.Назначение = Остатки.Назначение
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров КАК ДопустимоеОтклонение
		|		ПО ИСТИНА
		|ГДЕ
		|	Остатки.ВНаличииОстаток + Остатки.ЗаказаноОстаток - Остатки.ВсеПотребностиОстаток
		|		> ВЫБОР
		|			КОГДА НЕ Остатки.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины
		|						В (&МерныеТипыЕдиницИзмерений)
		|				ТОГДА 0
		|			ИНАЧЕ
		|				Остатки.ВсеПотребностиОстаток * ДопустимоеОтклонение.Значение / 100
		|		КОНЕЦ";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ОбеспечениеРезультатыКонтроляПревышенияОбособленногоОбеспечения");
	
КонецПроцедуры

Процедура СообщитьОбОшибкахПроведения(Объект, Отказ, РезультатыКонтроля) Экспорт
	
	ОтключитьКонтроль = ПараметрыСеанса.ПроводитьБезКонтроляРезервовТоваровПоЗаказам;
	
	Для Каждого ОшибкаКонтроля Из РезультатыКонтроля Цикл
		
		ЕдиницаИзмерения = ОшибкаКонтроля.ЕдиницаИзмерения;
		
		Количество = ОшибкаКонтроля.КоличествоФизическийОстаток;
		Если Количество > 0 И ОтключитьКонтроль Тогда
			
			ТекстНазначениеХарактеристика = НСтр("ru = 'Для отгрузки/резервирования товара ""%1"",""%2"" на складе ""%3"" по назначению ""%4"" недостаточно остатка в наличии в количестве %5 %6.'");
			ТекстНазначение = НСтр("ru = 'Для отгрузки/резервирования обособленного товара ""%1"" на складе ""%2"" по назначению ""%3"" недостаточно остатка в наличии количестве %4 %5'");
			ТекстБезНазначенияХарактеристика = НСтр("ru = 'Для отгрузки/резервирования необособленного товара ""%1"",""%2"" на складе ""%3"" недостаточно остатка в наличии в количестве %4 %5.'");
			ТекстБезНазначения = НСтр("ru = 'Для отгрузки/резервирования необособленного товара ""%1"" на складе ""%2"" недостаточно остатка в наличии в количестве %3 %4'");
			
			Если ЗначениеЗаполнено(ОшибкаКонтроля.Назначение) Тогда
				
				Если ЗначениеЗаполнено(ОшибкаКонтроля.Характеристика) Тогда
					ТекстСообщения = СтрШаблон(ТекстНазначениеХарактеристика,
						ОшибкаКонтроля.Номенклатура,
						ОшибкаКонтроля.Характеристика,
						ОшибкаКонтроля.Склад,
						ОшибкаКонтроля.Назначение,
						Количество,
						ЕдиницаИзмерения);
				Иначе
					ТекстСообщения = СтрШаблон(ТекстНазначение,
						ОшибкаКонтроля.Номенклатура,
						ОшибкаКонтроля.Склад,
						ОшибкаКонтроля.Назначение,
						Количество,
						ЕдиницаИзмерения);
				КонецЕсли;
				
			Иначе
				
				Если ЗначениеЗаполнено(ОшибкаКонтроля.Характеристика) Тогда
					ТекстСообщения = СтрШаблон(ТекстБезНазначенияХарактеристика,
						ОшибкаКонтроля.Номенклатура,
						ОшибкаКонтроля.Характеристика,
						ОшибкаКонтроля.Склад,
						Количество,
						ЕдиницаИзмерения);
				Иначе
					ТекстСообщения = СтрШаблон(ТекстБезНазначения,
						ОшибкаКонтроля.Номенклатура,
						ОшибкаКонтроля.Склад,
						Количество,
						ЕдиницаИзмерения);
				КонецЕсли;
				
			КонецЕсли;
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Неопределено, Неопределено, Отказ);
			
		КонецЕсли;
		
		Количество = ОшибкаКонтроля.КоличествоСвободно;
		Если Количество > 0 Тогда
			
			ТекстНазначениеХарактеристика = НСтр("ru = 'Для отгрузки/резервирования товара ""%1"",""%2"" на складе ""%3"" по назначению ""%4"" недостаточно свободного остатка в количестве %5 %6.'");
			ТекстНазначение = НСтр("ru = 'Для отгрузки/резервирования обособленного товара ""%1"" на складе ""%2"" по назначению ""%3"" недостаточно свободного остатка в количестве %4 %5'");
			ТекстБезНазначенияХарактеристика = НСтр("ru = 'Для отгрузки/резервирования необособленного товара ""%1"",""%2"" на складе ""%3"" недостаточно свободного остатка в количестве %4 %5.'");
			ТекстБезНазначения = НСтр("ru = 'Для отгрузки/резервирования необособленного товара ""%1"" на складе ""%2"" недостаточно свободного остатка в количестве %3 %4'");
			
			Если ЗначениеЗаполнено(ОшибкаКонтроля.Назначение) Тогда
				
				Если ЗначениеЗаполнено(ОшибкаКонтроля.Характеристика) Тогда
					ТекстСообщения = СтрШаблон(ТекстНазначениеХарактеристика,
						ОшибкаКонтроля.Номенклатура,
						ОшибкаКонтроля.Характеристика,
						ОшибкаКонтроля.Склад,
						ОшибкаКонтроля.Назначение,
						Количество,
						ЕдиницаИзмерения);
				Иначе
					ТекстСообщения = СтрШаблон(ТекстНазначение,
						ОшибкаКонтроля.Номенклатура,
						ОшибкаКонтроля.Склад,
						ОшибкаКонтроля.Назначение,
						Количество,
						ЕдиницаИзмерения);
				КонецЕсли;
				
			Иначе
				
				Если ЗначениеЗаполнено(ОшибкаКонтроля.Характеристика) Тогда
					ТекстСообщения = СтрШаблон(ТекстБезНазначенияХарактеристика,
						ОшибкаКонтроля.Номенклатура,
						ОшибкаКонтроля.Характеристика,
						ОшибкаКонтроля.Склад,
						Количество,
						ЕдиницаИзмерения);
				Иначе
					ТекстСообщения = СтрШаблон(ТекстБезНазначения,
						ОшибкаКонтроля.Номенклатура,
						ОшибкаКонтроля.Склад,
						Количество,
						ЕдиницаИзмерения);
				КонецЕсли;
				
			КонецЕсли;
			
			Если ОтключитьКонтроль Тогда
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Неопределено, Неопределено, Неопределено);
			Иначе
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Неопределено, Неопределено, Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
		Количество = ОшибкаКонтроля.КоличествоДоступно;
		
		Если ОшибкаКонтроля.НуженКонтрольДоступногоОстатка И Количество > 0 Тогда
			
			ТекстНазначениеХарактеристика = НСтр("ru = 'Для обеспечения заказов недостаточно запасов товара ""%1"",""%2"" в наличии и в ожидаемых поступлениях на склад ""%3"" по назначению ""%4"" в количестве %5 %6.'");
			ТекстНазначение = НСтр("ru = 'Для обеспечения заказов недостаточно запасов товара ""%1"" в наличии и в ожидаемых поступлениях на склад ""%2"" по назначению ""%3"" в количестве %4 %5'");
			ТекстБезНазначенияХарактеристика = НСтр("ru = 'Для обеспечения заказов недостаточно запасов товара ""%1"",""%2"" в наличии и в ожидаемых поступлениях на склад ""%3"" в количестве %4 %5.'");
			ТекстБезНазначения = НСтр("ru = 'Для обеспечения заказов недостаточно запасов товара ""%1"" в наличии и в ожидаемых поступлениях на склад ""%2"" в количестве %3 %4'");
			
			Если ЗначениеЗаполнено(ОшибкаКонтроля.Назначение) Тогда
				
				Если ЗначениеЗаполнено(ОшибкаКонтроля.Характеристика) Тогда
					ТекстСообщения = СтрШаблон(ТекстНазначениеХарактеристика,
						ОшибкаКонтроля.Номенклатура,
						ОшибкаКонтроля.Характеристика,
						ОшибкаКонтроля.Склад,
						ОшибкаКонтроля.Назначение,
						Количество,
						ЕдиницаИзмерения);
				Иначе
					ТекстСообщения = СтрШаблон(ТекстНазначение,
						ОшибкаКонтроля.Номенклатура,
						ОшибкаКонтроля.Склад,
						ОшибкаКонтроля.Назначение,
						Количество,
						ЕдиницаИзмерения);
				КонецЕсли;
				
			Иначе
				
				Если ЗначениеЗаполнено(ОшибкаКонтроля.Характеристика) Тогда
					ТекстСообщения = СтрШаблон(ТекстБезНазначенияХарактеристика,
						ОшибкаКонтроля.Номенклатура,
						ОшибкаКонтроля.Характеристика,
						ОшибкаКонтроля.Склад,
						Количество,
						ЕдиницаИзмерения);
				Иначе
					ТекстСообщения = СтрШаблон(ТекстБезНазначения,
						ОшибкаКонтроля.Номенклатура,
						ОшибкаКонтроля.Склад,
						Количество,
						ЕдиницаИзмерения);
				КонецЕсли;
				
			КонецЕсли;
			
			Если ОтключитьКонтроль Тогда
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Неопределено, Неопределено, Неопределено);
			Иначе
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Неопределено, Неопределено, Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СообщитьОбОшибкахПроведенияПревышениеОбособленногоОбеспечения(Объект, Отказ, РезультатыКонтроля) Экспорт
	
	Для Каждого ОшибкаКонтроля Из РезультатыКонтроля Цикл
		
		ТекстНазначениеХарактеристика = "";
		ТекстНазначение = "";
		Количество = 0;
		ЕдиницаИзмерения = ОшибкаКонтроля.ЕдиницаИзмерения;
		
		Если ОшибкаКонтроля.ОшибкаНаличиеБольшеПотребности Тогда
			Количество = ОшибкаКонтроля.ВеличинаНаличиеБольшеПотребности;
			Если ТипЗнч(ОшибкаКонтроля.Склад) = Тип("СправочникСсылка.Склады") Тогда
				ТекстНазначениеХарактеристика = НСтр("ru = 'Номенклатура: %1, Характеристика: %2, Склад: %3, Назначение: %4. 
					|Приходуется для обеспечения больше чем требуется на %5 %6.'");
				ТекстНазначение = НСтр("ru = 'Номенклатура: %1, Склад: %2, Назначение: %3. 
					|Приходуется для обеспечения больше чем требуется на %4 %5.'");
			Иначе
				ТекстНазначениеХарактеристика = НСтр("ru = 'Номенклатура: %1, Характеристика: %2, Подразделение: %3, Назначение: %4. 
					|Приходуется для обеспечения больше чем требуется на %5 %6.'");
				ТекстНазначение = НСтр("ru = 'Номенклатура: %1, Подразделение: %2, Назначение: %3. 
					|Приходуется для обеспечения больше чем требуется на %4 %5.'");
			КонецЕсли;
		Иначе
			Количество = ОшибкаКонтроля.ВеличинаЗаказатьМеньшеНуля;
			Если ТипЗнч(ОшибкаКонтроля.Склад) = Тип("СправочникСсылка.Склады") Тогда
				ТекстНазначениеХарактеристика = НСтр("ru = 'Номенклатура: %1, Характеристика: %2, Склад: %3, Назначение: %4. 
					|Заказано для обеспечения больше чем требуется на %5 %6.'");
				ТекстНазначение = НСтр("ru = 'Номенклатура: %1, Склад: %2, Назначение: %3. 
					|Заказано для обеспечения больше чем требуется на %4 %5.'");
			Иначе
				ТекстНазначениеХарактеристика = НСтр("ru = 'Номенклатура: %1, Характеристика: %2, Подразделение: %3, Назначение: %4. 
					|Заказано для обеспечения больше чем требуется на %5 %6.'");
				ТекстНазначение = НСтр("ru = 'Номенклатура: %1, Подразделение: %2, Назначение: %3. 
					|Заказано для обеспечения больше чем требуется на %4 %5.'");
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОшибкаКонтроля.Характеристика) Тогда
			ТекстСообщения = СтрШаблон(ТекстНазначениеХарактеристика,
				ОшибкаКонтроля.Номенклатура,
				ОшибкаКонтроля.Характеристика,
				ОшибкаКонтроля.Склад,
				ОшибкаКонтроля.Назначение,
				Количество,
				ЕдиницаИзмерения);
		Иначе
			ТекстСообщения = СтрШаблон(ТекстНазначение,
				ОшибкаКонтроля.Номенклатура,
				ОшибкаКонтроля.Склад,
				ОшибкаКонтроля.Назначение,
				Количество,
				ЕдиницаИзмерения);
		КонецЕсли;
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Неопределено, Неопределено, Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////////
// Обслуживание регистров состояний заказов.

Процедура ДобавитьВременныеТаблицыАктуализацииСостояний(Запрос, ФильтрПереопределяемый, Постфикс) Экспорт
	
	ИменаРегистров = Новый Массив();
	ИменаРегистров.Добавить("РегистрСведений.СостоянияЗаказовКлиентов");
	ИменаРегистров.Добавить("РегистрСведений.СостоянияВнутреннихЗаказов");
	
	Тексты = Новый Массив();
	
	Для Каждого ИмяРегистра Из ИменаРегистров Цикл
		
		ИмяРегистра = СтрЗаменить(ИмяРегистра, "РегистрСведений.", "");
		Параметры = РегистрыСведений[ИмяРегистра].ПараметрыАктуализацииСостоянийЗаказов();
		Текст = Параметры.ПравилоОтбораЗаписей;
		Текст = СтрЗаменить(Текст, "ФильтрПереопределяемый",  ФильтрПереопределяемый);
		Текст = СтрЗаменить(Текст, "ТаблицаПереопределяемый", ИмяРегистра + Постфикс);
		Тексты.Добавить(Текст);
		
	КонецЦикла;
	
	Запрос.Текст = СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ТаблицаИзмененийРаспределениеЗапасовДляРасчетаСостояний(Запрос) Экспорт
	
	ИменаРегистров = Новый Массив();
	ИменаРегистров.Добавить("РегистрСведений.СостоянияЗаказовКлиентов");
	ИменаРегистров.Добавить("РегистрСведений.СостоянияВнутреннихЗаказов");
	
	Тексты = Новый Массив();
	
	Для Каждого ИмяРегистра Из ИменаРегистров Цикл
		
		ИмяРегистра = СтрЗаменить(ИмяРегистра, "РегистрСведений.", "");
		Параметры = РегистрыСведений[ИмяРегистра].ПараметрыАктуализацииСостоянийЗаказов();
		Текст = Параметры.ФункцияСравненияЗаписейВоВременнуюТаблицу;
		Текст = СтрЗаменить(Текст, "ПередЗаписьюПереопределяемый", ИмяРегистра + "ПередЗаписью");
		Текст = СтрЗаменить(Текст, "ПриЗаписиПереопределяемый",    ИмяРегистра + "ПриЗаписи");
		Текст = СтрЗаменить(Текст, "ИзменениеПереопределяемый",    ИмяРегистра + "Изменение");
		Тексты.Добавить(Текст);
		
	КонецЦикла;
	
	Для Каждого ИмяРегистра Из ИменаРегистров Цикл
		
		ИмяРегистра = СтрЗаменить(ИмяРегистра, "РегистрСведений.", "");
		
		Текст = "УНИЧТОЖИТЬ " + ИмяРегистра + "ПередЗаписью";
		Тексты.Добавить(Текст);
		
		Текст = "УНИЧТОЖИТЬ " + ИмяРегистра + "ПриЗаписи";
		Тексты.Добавить(Текст);
		
	КонецЦикла;
	
	Запрос.Текст = СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.ВыполнитьПакет();
	
КонецПроцедуры

Процедура ИсключитьСсылкиТребующиеОтложенногоОбновления(Ссылки) Экспорт
	
	Если ОбновлениеИнформационнойБазы.ОтложенноеОбновлениеЗавершено() Тогда
		Возврат;
	КонецЕсли;
	
	НаборСостоянияЗаказовКлиентов = РегистрыСведений.СостоянияЗаказовКлиентов.СоздатьНаборЗаписей();
	НаборСостоянияВнутреннихЗаказов = РегистрыСведений.СостоянияВнутреннихЗаказов.СоздатьНаборЗаписей();
	НаборСостоянияЗаказовКлиентов.Отбор.Заказ.Использование = Истина;
	НаборСостоянияВнутреннихЗаказов.Отбор.Заказ.Использование = Истина;
	
	ВсегоЭлементов = Ссылки.Количество();
	Для Счетчик = 1 По ВсегоЭлементов Цикл
		Индекс = ВсегоЭлементов - Счетчик;
		Ссылка = Ссылки[Индекс];
		
		Набор = Неопределено;
		Если НаборСостоянияЗаказовКлиентов.Отбор.Заказ.ТипЗначения.СодержитТип(ТипЗнч(Ссылка)) Тогда
			Набор = НаборСостоянияЗаказовКлиентов;
			Набор.Отбор.Заказ.Значение = Ссылка;
		ИначеЕсли НаборСостоянияВнутреннихЗаказов.Отбор.Заказ.ТипЗначения.СодержитТип(ТипЗнч(Ссылка)) Тогда
			Набор = НаборСостоянияВнутреннихЗаказов;
			Набор.Отбор.Заказ.Значение = Ссылка;
		КонецЕсли;
		
		Если Набор <> Неопределено Тогда
			РезультатПроверки = ОбновлениеИнформационнойБазы.ОбъектОбработан(Набор);
			Если Не РезультатПроверки.Обработан Тогда
				Ссылки.Удалить(Индекс);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ДосчитыватьРегистрРегламентнымЗаданием() Экспорт
	Возврат Не ОбщегоНазначения.ИнформационнаяБазаФайловая() Или БлокироватьРаспределениеЗапасов();
КонецФункции

Функция БлокироватьРаспределениеЗапасов() Экспорт
	ПараметрЗапускаПриложения = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("ПараметрЗапуска");
	Возврат СтрНайти(ПараметрЗапускаПриложения, "БлокироватьРаспределениеЗапасов") > 0;
КонецФункции

Функция ОшибкиПроверкиКорректностиЗаписейРегистраСведенийРаспределениеЗапасов() Экспорт
	
	Текст =
		// ресурсы ВНаличии - Запас на складе
		"ВЫБРАТЬ
		|	Набор.Номенклатура КАК Номенклатура,
		|	Набор.Характеристика КАК Характеристика,
		|	Набор.Склад КАК Склад,
		|	Набор.Назначение КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПоступление,
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе) КАК Состояние,
		|	СУММА(Набор.ЗапасДвижения) КАК ЗапасДвижения,
		|	СУММА(Набор.ЗапасСведения) КАК ЗапасСведения
		|ИЗ(
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		Таблица.ВНаличииОстаток КАК ЗапасДвижения,
		|		0 КАК ЗапасСведения
		|	ИЗ
		|		РегистрНакопления.ЗапасыИПотребности.Остатки КАК Таблица
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		0 КАК ЗапасДвижения,
		|		Таблица.Запас КАК ЗапасСведения
		|	ИЗ
		|		РегистрСведений.РаспределениеЗапасов КАК Таблица
		|	ГДЕ
		|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)) КАК Набор
		|	СГРУППИРОВАТЬ ПО
		|		Набор.Номенклатура,
		|		Набор.Характеристика,
		|		Набор.Склад,
		|		Набор.Назначение
		|ИМЕЮЩИЕ
		|	СУММА(Набор.ЗапасДвижения) <> СУММА(Набор.ЗапасСведения)
		|;
		|
		|/////////////////////////////////////////////////////////////
		// ресурсы Заказано - Запас в графике поступления
		|ВЫБРАТЬ
		|	Набор.Номенклатура КАК Номенклатура,
		|	Набор.Характеристика КАК Характеристика,
		|	Набор.Склад КАК Склад,
		|	Набор.Назначение КАК Назначение,
		|	Набор.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|	Набор.ДатаПоступления КАК ДатаПоступления,
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление) КАК Состояние,
		|	СУММА(Набор.ЗапасДвижения) КАК ЗапасДвижения,
		|	СУММА(Набор.ЗапасСведения) КАК ЗапасСведения
		|ИЗ(
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		Таблица.Заказ КАК ЗаказНаПоступление,
		|		Таблица.ДатаСобытия КАК ДатаПоступления,
		|		Таблица.ЗаказаноОстаток КАК ЗапасДвижения,
		|		0 КАК ЗапасСведения
		|	ИЗ
		|		РегистрНакопления.ЗапасыИПотребности.Остатки КАК Таблица
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		Таблица.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|		Таблица.ДатаПоступления КАК ДатаПоступления,
		|		0 КАК ЗапасДвижения,
		|		Таблица.Запас КАК ЗапасСведения
		|	ИЗ
		|		РегистрСведений.РаспределениеЗапасов КАК Таблица
		|	ГДЕ
		|		Таблица.Состояние В(
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеподтвержденное))) КАК Набор
		|	СГРУППИРОВАТЬ ПО
		|		Набор.Номенклатура,
		|		Набор.Характеристика,
		|		Набор.Склад,
		|		Набор.Назначение,
		|		Набор.ЗаказНаПоступление,
		|		Набор.ДатаПоступления
		|	ИМЕЮЩИЕ
		|		СУММА(Набор.ЗапасДвижения) <> СУММА(Набор.ЗапасСведения)
		|
		|;
		|/////////////////////////////////////////////////////////////
		// ресурсы Резервировать на складе, Резервировать по мере, Не обеспечивать, Отложить резервирование, К обеспечению в графике отгрузки
		|ВЫБРАТЬ
		|	Набор.Номенклатура КАК Номенклатура,
		|	Набор.Характеристика КАК Характеристика,
		|	Набор.Склад КАК Склад,
		|	Набор.Назначение КАК Назначение,
		|	Набор.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|	Набор.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка) КАК Состояние,
		|	СУММА(Набор.РезервироватьДвижения) КАК РезервироватьДвижения,
		|	СУММА(Набор.НеОбеспечиватьДвижения) КАК НеОбеспечиватьДвижения,
		|	СУММА(Набор.КОбеспечениюДвижения) КАК КОбеспечениюДвижения,
		|	СУММА(Набор.РезервироватьПоМереПоступленияДвижения) КАК РезервироватьПоМереПоступленияДвижения,
		|	СУММА(Набор.ОтложитьРезервированиеДвижения) КАК ОтложитьРезервированиеДвижения,
		|	СУММА(Набор.РезервироватьСведения) КАК РезервироватьСведения,
		|	СУММА(Набор.НеОбеспечиватьСведения) КАК НеОбеспечиватьСведения,
		|	СУММА(Набор.РезервироватьПоМереПоступленияСведения) КАК РезервироватьПоМереПоступленияСведения,
		|	СУММА(Набор.ОтложитьРезервированиеСведения) КАК ОтложитьРезервированиеСведения,
		|	СУММА(Набор.КОбеспечениюСведения) КАК КОбеспечениюСведения
		|ИЗ(
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		Таблица.Заказ КАК ЗаказНаОтгрузку,
		|		Таблица.ДатаСобытия КАК ЖелаемаяДатаОтгрузки,
		|		Таблица.РезервироватьНаСкладеОстаток КАК РезервироватьДвижения,
		|		Таблица.НеОбеспечиватьОстаток КАК НеОбеспечиватьДвижения,
		|		Таблица.РезервироватьПоМереПоступленияОстаток КАК РезервироватьПоМереПоступленияДвижения,
		|		Таблица.ОтложитьРезервированиеОстаток КАК ОтложитьРезервированиеДвижения,
		|		Таблица.КОбеспечениюОстаток КАК КОбеспечениюДвижения,
		|		0 КАК РезервироватьСведения,
		|		0 КАК НеОбеспечиватьСведения,
		|		0 КАК РезервироватьПоМереПоступленияСведения,
		|		0 КАК ОтложитьРезервированиеСведения,
		|		0 КАК КОбеспечениюСведения
		|	ИЗ
		|		РегистрНакопления.ЗапасыИПотребности.Остатки КАК Таблица
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		Таблица.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|		Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|		0 КАК РезервироватьДвижения,
		|		0 КАК НеОбеспечиватьДвижения,
		|		0 КАК РезервироватьПоМереПоступленияДвижения,
		|		0 КАК ОтложитьРезервированиеДвижения,
		|		0 КАК КОбеспечениюДвижения,
		|		Таблица.Резервировать КАК РезервироватьСведения,
		|		Таблица.НеОбеспечивать КАК НеОбеспечиватьСведения,
		|		Таблица.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступленияСведения,
		|		Таблица.ОтложитьРезервирование КАК ОтложитьРезервированиеСведения,
		|		Таблица.КОбеспечениюБезРезерва КАК КОбеспечениюСведения
		|	ИЗ
		|		РегистрСведений.РаспределениеЗапасов КАК Таблица
		|	ГДЕ
		|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка)) КАК Набор
		|СГРУППИРОВАТЬ ПО
		|	Набор.Номенклатура,
		|	Набор.Характеристика,
		|	Набор.Склад,
		|	Набор.Назначение,
		|	Набор.ЗаказНаОтгрузку,
		|	Набор.ЖелаемаяДатаОтгрузки
		|ИМЕЮЩИЕ
		|	СУММА(Набор.РезервироватьДвижения) <> СУММА(Набор.РезервироватьСведения)
		|		ИЛИ СУММА(Набор.НеОбеспечиватьДвижения) <> СУММА(Набор.НеОбеспечиватьСведения)
		|		ИЛИ СУММА(Набор.РезервироватьПоМереПоступленияДвижения) <> СУММА(Набор.РезервироватьПоМереПоступленияСведения)
		|		ИЛИ СУММА(Набор.ОтложитьРезервированиеДвижения) <> СУММА(Набор.ОтложитьРезервированиеСведения)
		|		ИЛИ СУММА(Набор.КОбеспечениюДвижения) <> СУММА(Набор.КОбеспечениюСведения)
		|;
		|
		|/////////////////////////////////////////////////////////////
		// ресурсы РезервироватьНаСкладе - Зарезервировано - распределение запаса на заказ
		|ВЫБРАТЬ
		|	Набор.Номенклатура КАК Номенклатура,
		|	Набор.Характеристика КАК Характеристика,
		|	Набор.Склад КАК Склад,
		|	Набор.Назначение КАК Назначение,
		|	Набор.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|	Набор.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ВРезерве) КАК Состояние,
		|	СУММА(Набор.РезервироватьДвижения) КАК РезервироватьДвижения,
		|	СУММА(Набор.РезервироватьСведения) КАК РезервироватьСведения
		|ИЗ(
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		Таблица.Заказ КАК ЗаказНаОтгрузку,
		|		Таблица.ДатаСобытия КАК ЖелаемаяДатаОтгрузки,
		|		Таблица.РезервироватьНаСкладеОстаток КАК РезервироватьДвижения,
		|		0 КАК РезервироватьСведения
		|	ИЗ
		|		РегистрНакопления.ЗапасыИПотребности.Остатки КАК Таблица
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		Таблица.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|		Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|		0 КАК РезервироватьДвижения,
		|		Таблица.Зарезервировано КАК РезервироватьСведения
		|	ИЗ
		|		РегистрСведений.РаспределениеЗапасов КАК Таблица
		|	ГДЕ
		|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ВРезерве)) КАК Набор
		|СГРУППИРОВАТЬ ПО
		|	Набор.Номенклатура,
		|	Набор.Характеристика,
		|	Набор.Склад,
		|	Набор.Назначение,
		|	Набор.ЗаказНаОтгрузку,
		|	Набор.ЖелаемаяДатаОтгрузки
		|ИМЕЮЩИЕ
		|	СУММА(Набор.РезервироватьДвижения) <> СУММА(Набор.РезервироватьСведения)
		|;
		|
		|/////////////////////////////////////////////////////////////
		// ресурсы РезервироватьНаСкладе - Резерв - распределение запаса на все заказы
		|ВЫБРАТЬ
		|	Набор.Номенклатура КАК Номенклатура,
		|	Набор.Характеристика КАК Характеристика,
		|	Набор.Склад КАК Склад,
		|	Набор.Назначение КАК Назначение,
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ВРезерве) КАК Состояние,
		|	СУММА(Набор.РезервироватьДвижения) КАК РезервироватьДвижения,
		|	СУММА(Набор.РезервСведения) КАК РезервСведения
		|ИЗ(
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		Таблица.РезервироватьНаСкладеОстаток КАК РезервироватьДвижения,
		|		0 КАК РезервСведения
		|	ИЗ
		|		РегистрНакопления.ЗапасыИПотребности.Остатки КАК Таблица
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		0 КАК РезервироватьДвижения,
		|		Таблица.Резерв КАК РезервСведения
		|	ИЗ
		|		РегистрСведений.РаспределениеЗапасов КАК Таблица
		|	ГДЕ
		|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)) КАК Набор
		|СГРУППИРОВАТЬ ПО
		|	Набор.Номенклатура,
		|	Набор.Характеристика,
		|	Набор.Склад,
		|	Набор.Назначение
		|ИМЕЮЩИЕ
		|	СУММА(Набор.РезервироватьДвижения) <> СУММА(Набор.РезервСведения)
		|;
		|
		|/////////////////////////////////////////////////////////////
		// ресурсы Не обеспечивать - Не обеспечивать - не обеспечиваемая потребность заказа
		|ВЫБРАТЬ
		|	Набор.Номенклатура КАК Номенклатура,
		|	Набор.Характеристика КАК Характеристика,
		|	Набор.Склад КАК Склад,
		|	Набор.Назначение КАК Назначение,
		|	Набор.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|	Набор.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.НеОбеспечивать) КАК Состояние,
		|	СУММА(Набор.НеОбеспечиватьДвижения) КАК НеОбеспечиватьДвижения,
		|	СУММА(Набор.НеОбеспечиватьСведения) КАК НеОбеспечиватьСведения
		|ИЗ(
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		Таблица.Заказ КАК ЗаказНаОтгрузку,
		|		Таблица.ДатаСобытия КАК ЖелаемаяДатаОтгрузки,
		|		Таблица.НеОбеспечиватьОстаток КАК НеОбеспечиватьДвижения,
		|		0 КАК НеОбеспечиватьСведения
		|	ИЗ
		|		РегистрНакопления.ЗапасыИПотребности.Остатки КАК Таблица
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		Таблица.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|		Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|		0 КАК НеОбеспечиватьДвижения,
		|		Таблица.НеОбеспечено КАК НеОбеспечиватьСведения
		|	ИЗ
		|		РегистрСведений.РаспределениеЗапасов КАК Таблица
		|	ГДЕ
		|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.НеОбеспечивать)) КАК Набор
		|СГРУППИРОВАТЬ ПО
		|	Набор.Номенклатура,
		|	Набор.Характеристика,
		|	Набор.Склад,
		|	Набор.Назначение,
		|	Набор.ЗаказНаОтгрузку,
		|	Набор.ЖелаемаяДатаОтгрузки
		|ИМЕЮЩИЕ
		|	СУММА(Набор.НеОбеспечиватьДвижения) <> СУММА(Набор.НеОбеспечиватьСведения)
		|
		|;
		|/////////////////////////////////////////////////////////////
		// ресурсы В наличии - В наличии - запас по всем назначениям на складе
		|ВЫБРАТЬ
		|	Набор.Номенклатура КАК Номенклатура,
		|	Набор.Характеристика КАК Характеристика,
		|	Набор.Склад КАК Склад,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	&ТекстВНаличии КАК Состояние,
		|	СУММА(Набор.ВНаличииДвижения) КАК ВНаличииДвижения,
		|	СУММА(Набор.ВНаличииСведения) КАК ВНаличииСведения
		|ИЗ(
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.ВНаличииОстаток КАК ВНаличииДвижения,
		|		0 КАК ВНаличииСведения
		|	ИЗ
		|		РегистрНакопления.ЗапасыИПотребности.Остатки КАК Таблица
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		0 КАК ВНаличииДвижения,
		|		Таблица.ВНаличии КАК ВНаличииСведения
		|	ИЗ
		|		РегистрСведений.РаспределениеЗапасов КАК Таблица
		|	ГДЕ
		|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)) КАК Набор
		|СГРУППИРОВАТЬ ПО
		|	Набор.Номенклатура,
		|	Набор.Характеристика,
		|	Набор.Склад
		|ИМЕЮЩИЕ
		|	СУММА(Набор.ВНаличииДвижения) <> СУММА(Набор.ВНаличииСведения)
		|;
		|
		|/////////////////////////////////////////////////////////////
		// ресурсы Свободно, Запас, Резерв и Резервировать по мере поступления в регистре сведений
		|ВЫБРАТЬ
		|	Набор.Номенклатура КАК Номенклатура,
		|	Набор.Характеристика КАК Характеристика,
		|	Набор.Склад КАК Склад,
		|	Набор.Назначение КАК Назначение,
		|	&ТекстСвободно КАК Состояние,
		|	СУММА(Набор.РезервироватьПоМереПоступленияСведения) КАК КОбеспечениюСведения,
		|	СУММА(Набор.ЗапасСведения) КАК ЗапасСведения,
		|	СУММА(Набор.ЗапасНаСкладеСведения) КАК ЗапасНаСкладеСведения,
		|	СУММА(Набор.СвободноСведения) КАК СвободноСведения
		|ИЗ(
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		Таблица.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступленияСведения,
		|		0 КАК ЗапасНаСкладеСведения,
		|		0 КАК ЗапасСведения,
		|		0 КАК СвободноСведения
		|	ИЗ
		|		РегистрСведений.РаспределениеЗапасов КАК Таблица
		|	ГДЕ
		|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		0 КАК РезервироватьПоМереПоступленияСведения,
		|		ВЫБОР КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе) ТОГДА
		|					Таблица.Запас - Таблица.Резерв
		|				ИНАЧЕ
		|					0
		|			КОНЕЦ КАК ЗапасНаСкладеСведения,
		|		ВЫБОР КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе) ТОГДА
		|					0
		|				ИНАЧЕ
		|					Таблица.Запас
		|			КОНЕЦ КАК ЗапасСведения,
		|		Таблица.Свободно КАК СвободноСведения
		|	ИЗ
		|		РегистрСведений.РаспределениеЗапасов КАК Таблица
		|	ГДЕ
		|		Таблица.Состояние В(
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное))) КАК Набор
		|СГРУППИРОВАТЬ ПО
		|	Набор.Номенклатура,
		|	Набор.Характеристика,
		|	Набор.Склад,
		|	Набор.Назначение
		|ИМЕЮЩИЕ
		|	ВЫБОР КОГДА СУММА(Набор.ЗапасНаСкладеСведения) < 0 ТОГДА
		|				0
		|			КОГДА СУММА(Набор.РезервироватьПоМереПоступленияСведения) > СУММА(Набор.ЗапасНаСкладеСведения) ТОГДА
		|				СУММА(Набор.ЗапасНаСкладеСведения)
		|			ИНАЧЕ
		|				СУММА(Набор.РезервироватьПоМереПоступленияСведения)
		|		КОНЕЦ // Распределено на складе
		|		+ ВЫБОР КОГДА СУММА(Набор.РезервироватьПоМереПоступленияСведения)
		|					- ВЫБОР КОГДА СУММА(Набор.ЗапасНаСкладеСведения) < 0 ТОГДА
		|								0
		|								КОГДА СУММА(Набор.РезервироватьПоМереПоступленияСведения) > СУММА(Набор.ЗапасНаСкладеСведения) ТОГДА
		|									СУММА(Набор.ЗапасНаСкладеСведения)
		|							ИНАЧЕ
		|								СУММА(Набор.РезервироватьПоМереПоступленияСведения)
		|						КОНЕЦ > СУММА(Набор.ЗапасСведения) ТОГДА
		|				СУММА(Набор.ЗапасСведения)
		|			ИНАЧЕ
		|				СУММА(Набор.РезервироватьПоМереПоступленияСведения)
		|					- ВЫБОР КОГДА СУММА(Набор.ЗапасНаСкладеСведения) < 0 ТОГДА
		|								0
		|								КОГДА СУММА(Набор.РезервироватьПоМереПоступленияСведения) > СУММА(Набор.ЗапасНаСкладеСведения) ТОГДА
		|									СУММА(Набор.ЗапасНаСкладеСведения)
		|							ИНАЧЕ
		|								СУММА(Набор.РезервироватьПоМереПоступленияСведения)
		|						КОНЕЦ
		|		КОНЕЦ // Распределено в заказах
		|		<> СУММА(Набор.ЗапасСведения) + СУММА(Набор.ЗапасНаСкладеСведения) - СУММА(Набор.СвободноСведения)
		|;
		|
		|/////////////////////////////////////////////////////////////
		// Порядок распределения запасов.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Назначение КАК Назначение,
		|	Таблица.Состояние КАК СостояниеБлиже,
		|	Таблица.ЗаказНаПоступление КАК ЗаказНаПоступлениеБлиже,
		|	Таблица.ДатаПоступления КАК ДатаПоступленияБлиже,
		|	ВсеЧтоДальше.Состояние КАК СостояниеДальше,
		|	ВсеЧтоДальше.ЗаказНаПоступление КАК ЗаказНаПоступлениеДальше,
		|	ВсеЧтоДальше.ДатаПоступления КАК ДатаПоступленияДальше,
		|	&ТекстНарушенПорядокРаспределенияЗапаса КАК Состояние
		|ИЗ
		|	РегистрСведений.РаспределениеЗапасов КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК ВсеЧтоДальше
		|		ПО ВсеЧтоДальше.Номенклатура = Таблица.Номенклатура
		|		 И ВсеЧтоДальше.Характеристика = Таблица.Характеристика
		|		 И ВсеЧтоДальше.Склад = Таблица.Склад
		|		 И ВсеЧтоДальше.Назначение = Таблица.Назначение
		|		 И ВсеЧтоДальше.Состояние В(
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное))
		|		 И ВЫБОР КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе) ТОГДА
		|				ВсеЧтоДальше.Состояние В(
		|					ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление),
		|					ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное))
		|			КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление) ТОГДА
		|				ВсеЧтоДальше.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное)
		|					ИЛИ ВсеЧтоДальше.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление)
		|						И ВсеЧтоДальше.ДатаПоступления > Таблица.ДатаПоступления
		|							ИЛИ ВсеЧтоДальше.ДатаПоступления = Таблица.ДатаПоступления
		|								И ВсеЧтоДальше.ЗаказНаПоступление > Таблица.ЗаказНаПоступление
		|			КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное) ТОГДА
		|				ВсеЧтоДальше.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное)
		|					И ВсеЧтоДальше.ЗаказНаПоступление > Таблица.ЗаказНаПоступление
		|			КОНЕЦ
		|
		|ГДЕ
		|	Таблица.Состояние В(
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное))
		|		И Таблица.Свободно > 0 И ВсеЧтоДальше.Запас - ВсеЧтоДальше.Резерв - ВсеЧтоДальше.Свободно > 0
		|;
		|
		|/////////////////////////////////////////////////////////////
		// Распределение запасов
		|ВЫБРАТЬ
		|	Таблица.Состояние КАК Состояние,
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Назначение КАК Назначение,
		|	Таблица.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|	Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	Таблица.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|	Таблица.ДатаПоступления КАК ДатаПоступления,
		|	Таблица.ТипЗаписиРаспределенияЗапасов КАК ТипЗаписиРаспределенияЗапасов,
		|	Таблица.КОбеспечениюБезРезерва КАК КОбеспечению,
		|	Таблица.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|	Таблица.Резервировать КАК Резервировать,
		|	Таблица.НеОбеспечивать КАК НеОбеспечивать,
		|	Таблица.Зарезервировано КАК Зарезервировано,
		|	Таблица.Обеспечено КАК Обеспечено,
		|	Таблица.НеОбеспечено КАК НеОбеспечено,
		|	Таблица.Запас КАК Запас,
		|	Таблица.Резерв КАК Резерв,
		|	Таблица.Свободно КАК Свободно,
		|	Таблица.ВНаличии КАК ВНаличии,
		|	Таблица.ОтложитьРезервирование КАК ОтложитьРезервирование
		|ИЗ
		|	РегистрСведений.РаспределениеЗапасов КАК Таблица
		|ГДЕ
		|	ВЫБОР КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка) ТОГДА
		|			Таблица.РезервироватьПоМереПоступления = 0 И Таблица.КОбеспечениюБезРезерва = 0 И Таблица.Резервировать = 0 И Таблица.НеОбеспечивать = 0 И Таблица.ОтложитьРезервирование = 0
		|				ИЛИ Таблица.Зарезервировано <> 0 ИЛИ Таблица.Обеспечено <> 0 ИЛИ Таблица.НеОбеспечено <> 0 ИЛИ Таблица.Запас <> 0 ИЛИ Таблица.Резерв <> 0 ИЛИ Таблица.Свободно <> 0 ИЛИ Таблица.ВНаличии <> 0
		|				ИЛИ Таблица.ЗаказНаОтгрузку = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ЗаказНаПоступление <> НЕОПРЕДЕЛЕНО ИЛИ Таблица.ДатаПоступления <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ТипЗаписиРаспределенияЗапасов <> 1
		|		КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе) ТОГДА
		|			Таблица.Запас = 0 И Таблица.Свободно = 0 И Таблица.Резерв = 0 И ВЫБОР КОГДА Таблица.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) ТОГДА Таблица.ВНаличии = 0 ИНАЧЕ ИСТИНА КОНЕЦ
		|				ИЛИ Таблица.Зарезервировано <> 0 ИЛИ Таблица.Обеспечено <> 0 ИЛИ Таблица.КОбеспечениюБезРезерва <> 0 ИЛИ Таблица.РезервироватьПоМереПоступления <> 0 ИЛИ Таблица.Резервировать <> 0 ИЛИ Таблица.НеОбеспечивать <> 0 ИЛИ Таблица.НеОбеспечено <> 0 ИЛИ Таблица.ОтложитьРезервирование <> 0
		|				ИЛИ Таблица.ЗаказНаОтгрузку <> НЕОПРЕДЕЛЕНО ИЛИ Таблица.ЖелаемаяДатаОтгрузки <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ЗаказНаПоступление <> НЕОПРЕДЕЛЕНО ИЛИ Таблица.ДатаПоступления <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ТипЗаписиРаспределенияЗапасов <> 1
		|		КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление) ТОГДА
		|			Таблица.Запас = 0 И Таблица.Свободно = 0
		|				ИЛИ Таблица.ВНаличии <> 0 ИЛИ Таблица.Резерв <> 0 ИЛИ Таблица.Зарезервировано <> 0 ИЛИ Таблица.Обеспечено <> 0 ИЛИ Таблица.РезервироватьПоМереПоступления <> 0 ИЛИ Таблица.КОбеспечениюБезРезерва <> 0 ИЛИ Таблица.Резервировать <> 0 ИЛИ Таблица.НеОбеспечивать <> 0 ИЛИ Таблица.НеОбеспечено <> 0 ИЛИ Таблица.ОтложитьРезервирование <> 0
		|				ИЛИ Таблица.ЗаказНаОтгрузку <> НЕОПРЕДЕЛЕНО ИЛИ Таблица.ЖелаемаяДатаОтгрузки <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ЗаказНаПоступление = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ДатаПоступления = ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ТипЗаписиРаспределенияЗапасов <> 1
		|		КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное) ТОГДА
		|			Таблица.Запас = 0 И Таблица.Свободно = 0
		|				ИЛИ Таблица.ВНаличии <> 0 ИЛИ Таблица.Резерв <> 0 ИЛИ Таблица.Зарезервировано <> 0 ИЛИ Таблица.Обеспечено <> 0 ИЛИ Таблица.КОбеспечениюБезРезерва <> 0 ИЛИ Таблица.РезервироватьПоМереПоступления <> 0 ИЛИ Таблица.Резервировать <> 0 ИЛИ Таблица.НеОбеспечивать <> 0 ИЛИ Таблица.НеОбеспечено <> 0 ИЛИ Таблица.ОтложитьРезервирование <> 0
		|				ИЛИ Таблица.ЗаказНаОтгрузку <> НЕОПРЕДЕЛЕНО ИЛИ Таблица.ЖелаемаяДатаОтгрузки <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ЗаказНаПоступление = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ДатаПоступления <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ТипЗаписиРаспределенияЗапасов <> 1
		|		КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ВРезерве) ТОГДА
		|			Таблица.Зарезервировано = 0
		|				ИЛИ Таблица.Обеспечено <> 0 ИЛИ Таблица.Запас <> 0 ИЛИ Таблица.Свободно <> 0 ИЛИ Таблица.ВНаличии <> 0 ИЛИ Таблица.Резерв <> 0 ИЛИ Таблица.РезервироватьПоМереПоступления <> 0 ИЛИ Таблица.КОбеспечениюБезРезерва <> 0 ИЛИ Таблица.Резервировать <> 0 ИЛИ Таблица.НеОбеспечивать <> 0 ИЛИ Таблица.НеОбеспечено <> 0 ИЛИ Таблица.ОтложитьРезервирование <> 0
		|				ИЛИ Таблица.ЗаказНаОтгрузку = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ЗаказНаПоступление <> НЕОПРЕДЕЛЕНО ИЛИ Таблица.ДатаПоступления <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ТипЗаписиРаспределенияЗапасов <> 1
		|		КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.НеОбеспечивать) ТОГДА
		|			Таблица.НеОбеспечено = 0
		|				ИЛИ Таблица.Запас <> 0 ИЛИ Таблица.Свободно <> 0 ИЛИ Таблица.ВНаличии <> 0 ИЛИ Таблица.Резерв <> 0 ИЛИ Таблица.Зарезервировано <> 0 ИЛИ Таблица.Обеспечено <> 0 ИЛИ Таблица.РезервироватьПоМереПоступления <> 0 ИЛИ Таблица.КОбеспечениюБезРезерва <> 0 ИЛИ Таблица.Резервировать <> 0 ИЛИ Таблица.НеОбеспечивать <> 0 ИЛИ Таблица.ОтложитьРезервирование <> 0
		|				ИЛИ Таблица.ЗаказНаОтгрузку = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ЗаказНаПоступление <> НЕОПРЕДЕЛЕНО ИЛИ Таблица.ДатаПоступления <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ТипЗаписиРаспределенияЗапасов <> 1
		|		КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченКДате) ТОГДА
		|			Таблица.Зарезервировано = 0 И Таблица.Обеспечено = 0
		|				ИЛИ Таблица.Запас <> 0 ИЛИ Таблица.Свободно <> 0 ИЛИ Таблица.ВНаличии <> 0 ИЛИ Таблица.Резерв <> 0 ИЛИ Таблица.КОбеспечениюБезРезерва <> 0 ИЛИ Таблица.Резервировать <> 0 ИЛИ Таблица.НеОбеспечивать <> 0 ИЛИ Таблица.НеОбеспечено <> 0 ИЛИ Таблица.ОтложитьРезервирование <> 0
		|				ИЛИ Таблица.ЗаказНаОтгрузку = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ЗаказНаПоступление = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ДатаПоступления = ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ТипЗаписиРаспределенияЗапасов <> 0
		|		КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.Обеспечить) ТОГДА
		|			Таблица.НеОбеспечено = 0
		|				ИЛИ Таблица.Запас <> 0 ИЛИ Таблица.Свободно <> 0 ИЛИ Таблица.ВНаличии <> 0 ИЛИ Таблица.Резерв <> 0 ИЛИ Таблица.РезервироватьПоМереПоступления <> 0 ИЛИ Таблица.КОбеспечениюБезРезерва <> 0 ИЛИ Таблица.Резервировать <> 0 ИЛИ Таблица.НеОбеспечивать <> 0 ИЛИ Таблица.Зарезервировано <> 0 ИЛИ Таблица.Обеспечено <> 0 ИЛИ Таблица.ОтложитьРезервирование <> 0
		|				ИЛИ Таблица.ЗаказНаОтгрузку = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ЗаказНаПоступление <> НЕОПРЕДЕЛЕНО ИЛИ Таблица.ДатаПоступления <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ТипЗаписиРаспределенияЗапасов <> 0
		|		КОГДА Таблица.Состояние В(
		|				ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченНаСкладе),
		|				ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ЗарезервированПриПоступлении)) ТОГДА
		|			Таблица.Зарезервировано = 0 И Таблица.Обеспечено = 0
		|				ИЛИ Таблица.Запас <> 0 ИЛИ Таблица.Свободно <> 0 ИЛИ Таблица.ВНаличии <> 0 ИЛИ Таблица.Резерв <> 0 ИЛИ Таблица.РезервироватьПоМереПоступления <> 0 ИЛИ Таблица.КОбеспечениюБезРезерва <> 0 ИЛИ Таблица.Резервировать <> 0 ИЛИ Таблица.НеОбеспечивать <> 0 ИЛИ Таблица.НеОбеспечено <> 0 ИЛИ Таблица.ОтложитьРезервирование <> 0
		|				ИЛИ Таблица.ЗаказНаОтгрузку = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ЗаказНаПоступление <> НЕОПРЕДЕЛЕНО ИЛИ Таблица.ДатаПоступления <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ТипЗаписиРаспределенияЗапасов <> 0
		|		КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаетсяПоНеподтвержденномуЗаказу) ТОГДА
		|			Таблица.Зарезервировано = 0 И Таблица.Обеспечено = 0
		|				ИЛИ Таблица.Запас <> 0 ИЛИ Таблица.Свободно <> 0 ИЛИ Таблица.ВНаличии <> 0 ИЛИ Таблица.Резерв <> 0 ИЛИ Таблица.РезервироватьПоМереПоступления <> 0 ИЛИ Таблица.КОбеспечениюБезРезерва <> 0 ИЛИ Таблица.Резервировать <> 0 ИЛИ Таблица.НеОбеспечивать <> 0 ИЛИ Таблица.НеОбеспечено <> 0 ИЛИ Таблица.ОтложитьРезервирование <> 0
		|				ИЛИ Таблица.ЗаказНаОтгрузку = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ЗаказНаПоступление = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ДатаПоступления <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ТипЗаписиРаспределенияЗапасов <> 0
		|		ИНАЧЕ
		|			ИСТИНА
		|		КОНЕЦ
		|
		|;
		// Проверки внутри РС Распределение запасов.
		|////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Набор.Номенклатура КАК Номенклатура,
		|	Набор.Характеристика КАК Характеристика,
		|	Набор.Склад КАК Склад,
		|	Набор.Назначение КАК Назначение,
		|	Набор.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|	Набор.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.Обеспечить) КАК Состояние,
		|	СУММА(Набор.КОбеспечениюДвижения) КАК КОбеспечениюДвижения,
		|	СУММА(Набор.КОбеспечениюСведения) КАК КОбеспечениюСведения
		|ИЗ(
		|ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		Таблица.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|		Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|		Таблица.КОбеспечениюБезРезерва + Таблица.РезервироватьПоМереПоступления + Таблица.ОтложитьРезервирование КАК КОбеспечениюДвижения,
		|		0 КАК КОбеспечениюСведения
		|	ИЗ
		|		РегистрСведений.РаспределениеЗапасов КАК Таблица
		|	ГДЕ
		|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		Таблица.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|		Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|		0 КАК КОбеспечениюДвижения,
		|		Таблица.НеОбеспечено + Таблица.Зарезервировано + Таблица.Обеспечено КАК КОбеспечениюСведения
		|	ИЗ
		|		РегистрСведений.РаспределениеЗапасов КАК Таблица
		|	ГДЕ
		|		Таблица.Состояние В(
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.Обеспечить),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченКДате),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаетсяПоНеподтвержденномуЗаказу),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченНаСкладе),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ЗарезервированПриПоступлении))) КАК Набор
		|СГРУППИРОВАТЬ ПО
		|	Набор.Номенклатура,
		|	Набор.Характеристика,
		|	Набор.Склад,
		|	Набор.Назначение,
		|	Набор.ЗаказНаОтгрузку,
		|	Набор.ЖелаемаяДатаОтгрузки
		|ИМЕЮЩИЕ
		|	СУММА(Набор.КОбеспечениюДвижения) <> СУММА(Набор.КОбеспечениюСведения)
		|;
		|/////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ
		|	Набор.Номенклатура КАК Номенклатура,
		|	Набор.Характеристика КАК Характеристика,
		|	Набор.Склад КАК Склад,
		|	Набор.Назначение КАК Назначение,
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ЗарезервированПриПоступлении) КАК Состояние,
		|	СУММА(Набор.РаспределеноДвижения) КАК РаспределеноДвижения,
		|	СУММА(Набор.РаспределеноСведения) КАК РаспределеноСведения
		|ИЗ(
		|ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		Таблица.Запас - Таблица.Свободно - Таблица.Резерв КАК РаспределеноДвижения,
		|		0 КАК РаспределеноСведения
		|	ИЗ
		|		РегистрСведений.РаспределениеЗапасов КАК Таблица
		|	ГДЕ
		|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		0 КАК РаспределеноДвижения,
		|		Таблица.Зарезервировано КАК РаспределеноСведения
		|	ИЗ
		|		РегистрСведений.РаспределениеЗапасов КАК Таблица
		|	ГДЕ
		|		Таблица.Состояние В(
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченНаСкладе),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ЗарезервированПриПоступлении))) КАК Набор
		|СГРУППИРОВАТЬ ПО
		|	Набор.Номенклатура,
		|	Набор.Характеристика,
		|	Набор.Склад,
		|	Набор.Назначение
		|ИМЕЮЩИЕ
		|	СУММА(Набор.РаспределеноДвижения) <> СУММА(Набор.РаспределеноСведения)
		|;
		|/////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ
		|	Набор.Номенклатура КАК Номенклатура,
		|	Набор.Характеристика КАК Характеристика,
		|	Набор.Склад КАК Склад,
		|	Набор.Назначение КАК Назначение,
		|	Набор.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|	Набор.ДатаПоступления КАК ДатаПоступления,
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченКДате) КАК Состояние,
		|	СУММА(Набор.РаспределеноДвижения) КАК РаспределеноДвижения,
		|	СУММА(Набор.РаспределеноСведения) КАК РаспределеноСведения
		|ИЗ(
		|ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		Таблица.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|		Таблица.ДатаПоступления КАК ДатаПоступления,
		|		Таблица.Запас - Таблица.Свободно КАК РаспределеноДвижения,
		|		0 КАК РаспределеноСведения
		|	ИЗ
		|		РегистрСведений.РаспределениеЗапасов КАК Таблица
		|	ГДЕ
		|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		Таблица.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|		Таблица.ДатаПоступления КАК ДатаПоступления,
		|		0 КАК РаспределеноДвижения,
		|		Таблица.Зарезервировано КАК РаспределеноСведения
		|	ИЗ
		|		РегистрСведений.РаспределениеЗапасов КАК Таблица
		|	ГДЕ
		|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченКДате)) КАК Набор
		|СГРУППИРОВАТЬ ПО
		|	Набор.Номенклатура,
		|	Набор.Характеристика,
		|	Набор.Склад,
		|	Набор.Назначение,
		|	Набор.ЗаказНаПоступление,
		|	Набор.ДатаПоступления
		|ИМЕЮЩИЕ
		|	СУММА(Набор.РаспределеноДвижения) <> СУММА(Набор.РаспределеноСведения)
		|;
		|/////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ
		|	Набор.Номенклатура КАК Номенклатура,
		|	Набор.Характеристика КАК Характеристика,
		|	Набор.Склад КАК Склад,
		|	Набор.Назначение КАК Назначение,
		|	Набор.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|	Набор.ДатаПоступления КАК ДатаПоступления,
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаетсяПоНеподтвержденномуЗаказу) КАК Состояние,
		|	СУММА(Набор.РаспределеноДвижения) КАК РаспределеноДвижения,
		|	СУММА(Набор.РаспределеноСведения) КАК РаспределеноСведения
		|ИЗ(
		|ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		Таблица.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|		Таблица.ДатаПоступления КАК ДатаПоступления,
		|		Таблица.Запас - Таблица.Свободно КАК РаспределеноДвижения,
		|		0 КАК РаспределеноСведения
		|	ИЗ
		|		РегистрСведений.РаспределениеЗапасов КАК Таблица
		|	ГДЕ
		|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Назначение КАК Назначение,
		|		Таблица.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|		Таблица.ДатаПоступления КАК ДатаПоступления,
		|		0 КАК РаспределеноДвижения,
		|		Таблица.Зарезервировано КАК РаспределеноСведения
		|	ИЗ
		|		РегистрСведений.РаспределениеЗапасов КАК Таблица
		|	ГДЕ
		|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаетсяПоНеподтвержденномуЗаказу)) КАК Набор
		|СГРУППИРОВАТЬ ПО
		|	Набор.Номенклатура,
		|	Набор.Характеристика,
		|	Набор.Склад,
		|	Набор.Назначение,
		|	Набор.ЗаказНаПоступление,
		|	Набор.ДатаПоступления
		|ИМЕЮЩИЕ
		|	СУММА(Набор.РаспределеноДвижения) <> СУММА(Набор.РаспределеноСведения)";
	
	Запрос = Новый Запрос();
	Запрос.Текст = Текст;
	Запрос.УстановитьПараметр("ТекстСвободно", НСтр("ru = 'Свободный остаток'"));
	Запрос.УстановитьПараметр("ТекстНарушенПорядокРаспределенияЗапаса", НСтр("ru = 'Нарушен порядок распределения запаса'"));
	Запрос.УстановитьПараметр("ТекстВНаличии", НСтр("ru = 'В наличии'"));
	Запрос.УстановитьПараметр("ТекстНарушенПорядокРасчетаГрафикаПоступленийПоДатам", НСтр("ru = 'Нарушен порядок расчета графика поступления по датам'"));
	Пакет = Запрос.ВыполнитьПакет();
	Возврат Пакет;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СообщитьОНеудачнойОбработкеНоменклатуры(ИнформацияОбОшибке, ТекстСообщения)
	
	ТекстСообщения = НСтр("ru = 'Задание распределения запасов завершилось неудачно по причине: %Причина%'");
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
	СобытиеЖурнала = НСтр("ru = 'Распределение запасов'", ОбщегоНазначения.КодОсновногоЯзыка());
	ЗаписьЖурналаРегистрации(СобытиеЖурнала,
	УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.РаспределениеЗапасов,, ТекстСообщения);
	
КонецПроцедуры

#КонецОбласти
