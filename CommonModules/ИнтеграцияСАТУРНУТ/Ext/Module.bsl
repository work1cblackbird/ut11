#Область ПрограммныйИнтерфейс

// Возвращает массив особенностей учета номенклатуры подсистемы
//
// Возвращаемое значение:
//   Массив из ПеречислениеСсылка.ОсобенностиУчетаНоменклатуры
Функция ОсобенностиУчетаНоменклатуры() Экспорт
	
	Массив = Новый Массив;
	
	Массив.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН);
	
	Возврат Массив;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Серии

// Параметры указания серий акт инвентаризации САТУРН.
// 
// Параметры:
//  Объект - ДокументОбъект - Объект
// 
// Возвращаемое значение:
//  См. НоменклатураКлиентСервер.ПараметрыУказанияСерий
Функция ПараметрыУказанияСерийАктИнвентаризацииСАТУРН(Объект) Экспорт
	
	ПараметрыУказанияСерий        = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.АктИнвентаризацииСАТУРН";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	ПараметрыУказанияСерий.ПодготовкаОрдера               = Истина;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Параметры указания серий акт применения САТУРН.
// 
// Параметры:
//  Объект - ДокументОбъект - Объект
// 
// Возвращаемое значение:
//  См. НоменклатураКлиентСервер.ПараметрыУказанияСерий
Функция ПараметрыУказанияСерийАктПримененияСАТУРН(Объект) Экспорт
	
	ПараметрыУказанияСерий        = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.АктПримененияСАТУРН";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	ПараметрыУказанияСерий.ПодготовкаОрдера               = Истина;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Параметры указания серий запрос остатков партий САТУРН.
// 
// Параметры:
//  Объект - ДокументОбъект - Объект
// 
// Возвращаемое значение:
//  См. НоменклатураКлиентСервер.ПараметрыУказанияСерий
Функция ПараметрыУказанияСерийЗапросОстатковПартийСАТУРН(Объект) Экспорт
	
	ПараметрыУказанияСерий        = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.ЗапросОстатковПартийСАТУРН";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧТовары                    = "ОстаткиПоДаннымСАТУРН";
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "ОстаткиПоДаннымСАТУРН";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	ПараметрыУказанияСерий.ПодготовкаОрдера               = Истина;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Параметры указания серий импорт продукции САТУРН.
// 
// Параметры:
//  Объект - ДокументОбъект - Объект
// 
// Возвращаемое значение:
//  См. НоменклатураКлиентСервер.ПараметрыУказанияСерий
Функция ПараметрыУказанияСерийИмпортПродукцииСАТУРН(Объект) Экспорт
	
	ПараметрыУказанияСерий        = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.ИмпортПродукцииСАТУРН";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	ПараметрыУказанияСерий.ПодготовкаОрдера               = Истина;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Параметры указания серий накладная САТУРН.
// 
// Параметры:
//  Объект - ДокументОбъект - Объект
// 
// Возвращаемое значение:
//  См. НоменклатураКлиентСервер.ПараметрыУказанияСерий
Функция ПараметрыУказанияСерийНакладнаяСАТУРН(Объект) Экспорт
	
	ПараметрыУказанияСерий        = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.НакладнаяСАТУРН";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	ПараметрыУказанияСерий.ПодготовкаОрдера               = Истина;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Параметры указания серий план применения САТУРН.
// 
// Параметры:
//  Объект - ДокументОбъект - Объект
// 
// Возвращаемое значение:
//  См. НоменклатураКлиентСервер.ПараметрыУказанияСерий
Функция ПараметрыУказанияСерийПланПримененияСАТУРН(Объект) Экспорт
	
	ПараметрыУказанияСерий        = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	ПараметрыУказанияСерий.ПолноеИмяОбъекта               = "Документ.ПланПримененияСАТУРН";
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = Неопределено;
	ПараметрыУказанияСерий.ПодготовкаОрдера               = Истина;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Параметры указания серий производственная операция САТУРН.
// 
// Параметры:
//  Объект - ДокументОбъект - Объект
// 
// Возвращаемое значение:
//  Структура - параметры указания серий объектов:
//  * Товары - См. НоменклатураКлиентСервер.ПараметрыУказанияСерий,
//  * Сырье - См. НоменклатураКлиентСервер.ПараметрыУказанияСерий.
Функция ПараметрыУказанияСерийПроизводственнаяОперацияСАТУРН(Объект) Экспорт
	
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	
	Товары        = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	Товары.ПолноеИмяОбъекта               = "Документ.ПроизводственнаяОперацияСАТУРН";
	Товары.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	Товары.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	Товары.ИмяТЧСерии                     = "Товары";
	Товары.ИмяПоляСклад                   = Неопределено;
	Товары.ПодготовкаОрдера               = Истина;
	
	Сырье        = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	Сырье.ПолноеИмяОбъекта               = "Документ.ПроизводственнаяОперацияСАТУРН";
	Сырье.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	Сырье.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	Сырье.ИмяТЧТовары                    = "Сырье";
	Сырье.ИмяТЧСерии                     = "Сырье";
	Сырье.ИмяПоляСклад                   = Неопределено;
	Сырье.ПодготовкаОрдера               = Истина;
	
	ПараметрыУказанияСерий = Новый Структура("Товары,Сырье", Товары, Сырье);
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Параметры указания серий соответствие номенклатуры САТУРН.
// 
// Параметры:
//  Объект - ДокументОбъект - Объект
// 
// Возвращаемое значение:
//  См. НоменклатураКлиентСервер.ПараметрыУказанияСерий
Функция ПараметрыУказанияСерийСоответствиеНоменклатурыСАТУРН(Объект) Экспорт
	
	ПараметрыУказанияСерий        = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "РегистрСведений.СоответствиеНоменклатурыСАТУРН";
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	ПараметрыУказанияСерий.ТоварВШапке = Истина;
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии = "";
	ПараметрыУказанияСерий.ИмяТЧТовары = "";
	ПараметрыУказанияСерий.ИмяПоляКоличество = Неопределено;
	ПараметрыУказанияСерий.ИмяПоляСклад = Неопределено;
	ПараметрыУказанияСерий.ИмяИсточникаЗначенийВФормеОбъекта = "ЭтаФорма";
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

#КонецОбласти

#Область ОбработкаЗаполнения

#Область ОбщиеПроцедуры

Процедура ЗаполнитьТабличнуюЧастьДокумента(ТабличнаяЧасть, РезультатЗапроса, ДанныеЗаполнения, ДоступнаПустаяТаблица = Ложь, ДополнительныеПараметры = Неопределено)
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Количество() = 0 Тогда
		
		Если ДоступнаПустаяТаблица Тогда
			Возврат;
		КонецЕсли;
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'В %1 отсутствует продукция для заполнения.'"),
			ДанныеЗаполнения);
		
	КонецЕсли;
	
	КолонкиТабличнойЧасти = ТабличнаяЧасть.Выгрузить(Новый Массив);
	ЕстьКолонкаСерий = КолонкиТабличнойЧасти.Колонки.Найти("Серия") <> Неопределено;
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТабличнаяЧасть.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		Если ЕстьКолонкаСерий И ЗначениеЗаполнено(НоваяСтрока.Серия) Тогда
			НоваяСтрока.СтатусУказанияСерий = 2;
		КонецЕсли;
		
		Если ДополнительныеПараметры <> Неопределено
			И ДополнительныеПараметры.Свойство("ЗаполнитьСкладИзТабличнойЧасти")
			И НоваяСтрока.НомерСтроки = 1 Тогда
			
			ПараметрыЗаполнения = Новый Структура("Организация, Склад, Подразделение",
				ДополнительныеПараметры.Организация,
				Выборка.Склад,
				Выборка.Подразделение);
			
			ЗаполнитьОрганизациюМестоХраненияСАТУРНПоРеквизитамДокумента(ДополнительныеПараметры.ДокументОбъект,
				ПараметрыЗаполнения);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаполнитьКоличественныеПоказатели(ТабличнаяЧасть);
	
КонецПроцедуры

Функция ДокументОснованиеИзДанныхЗаполнения(ДанныеЗаполнения)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Основание") Тогда
		Возврат ДанныеЗаполнения.Основание;
	Иначе
		Возврат ДанныеЗаполнения;
	КонецЕсли;
	
КонецФункции

#Область Сопоставления

Функция СкладПодразделениеИзМестаХранения(Параметры)
	
	СтруктураВозврата = Новый Структура("Склад, Подразделение");
	
	ПараметрыПолученияСопоставлений = Новый Массив;
	ПараметрПредставления = ИнтеграцияСАТУРН.НовыеПараметрыФормированияПредставленияСопоставлений();
	ПараметрПредставления.ОрганизацияСАТУРН = Параметры.ОрганизацияСАТУРН;
	ПараметрПредставления.МестоХранения     = Параметры.МестоХранения;
	ПараметрыПолученияСопоставлений.Добавить(ПараметрПредставления);
	ДанныеПредставлений = ИнтеграцияСАТУРН.ПредставленияСопоставлений(ПараметрыПолученияСопоставлений);
	
	Для Каждого КлючИЗначение Из ДанныеПредставлений Цикл
		
		СтруктураВозврата.Склад         = КлючИЗначение.Значение.ТорговыйОбъект.Значение;
		СтруктураВозврата.Подразделение = КлючИЗначение.Значение.ПроизводственныйОбъект.Значение;
		
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции

Процедура ЗаполнитьОрганизациюМестоХраненияСАТУРНПоРеквизитамДокумента(ДокументОбъект, РеквизитыДокумента, ИспользоватьПроизводственныйОбъект = Ложь)
	
	ТаблицаСопоставлений = ИнтеграцияСАТУРН.НоваяТаблицаОрганизацияКонтрагент();
	
	СтрокаСопоставленияОрганизация = ИнтеграцияСАТУРН.ДобавитьВТаблицуОтбораСопоставлений(
		ТаблицаСопоставлений,
		РеквизитыДокумента.Организация,
		РеквизитыДокумента.Склад,
		РеквизитыДокумента.Подразделение);
	
	Справочники.КлассификаторОрганизацийСАТУРН.ЗаполнитьТаблицуСопоставленийКлассификаторамиСАТУРН(ТаблицаСопоставлений);
	
	Если ЗначениеЗаполнено(СтрокаСопоставленияОрганизация.ОрганизацияСАТУРН) Тогда
		ДокументОбъект.ОрганизацияСАТУРН = СтрокаСопоставленияОрганизация.ОрганизацияСАТУРН;
		Если ИспользоватьПроизводственныйОбъект Тогда
			ДокументОбъект.МестоХранения = СтрокаСопоставленияОрганизация.МестоХраненияПроизводственныйОбъект;
		Иначе
			ДокументОбъект.МестоХранения = СтрокаСопоставленияОрганизация.МестоХраненияТорговыйОбъект;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьКоличественныеПоказатели(ТабличнаяЧасть)
	
	СтруктураДействий = Новый Структура;
	Колонки = ТабличнаяЧасть.ВыгрузитьКолонки().Колонки;
	
	Если Колонки.Найти("Номенклатура") <> Неопределено
		И Колонки.Найти("КоличествоВУпаковкеСАТУРН") <> Неопределено Тогда
		
		ПараметрыЗаполнения = ИнтеграцияСАТУРНКлиентСервер.ПараметрыЗаполненияТабличнойЧасти();
		ПараметрыЗаполнения.ПересчитатьКоличествоСАТУРН = Колонки.Найти("КоличествоСАТУРН") <> Неопределено;
		ПараметрыЗаполнения.ПересчитатьКоличествоВУпаковкеСАТУРН = Истина;
		
		ПараметрыВыполнения = Новый Структура;
		ПараметрыВыполнения.Вставить("ПараметрыЗаполнения",       ПараметрыЗаполнения);
		ПараметрыВыполнения.Вставить("ЕстьТипИзмеряемойВеличины", Колонки.Найти("ТипИзмеряемойВеличиныСАТУРН") <> Неопределено);
		
		СтруктураДействий.Вставить("ПересчитатьТипИзмеряемойВеличиныСАТУРН", ПараметрыВыполнения);
		
	КонецЕсли;
	
	Если СтруктураДействий.Количество() > 0 Тогда
		КэшированныеЗначения = Новый Структура;
		КэшированныеЗначения.Вставить("КоэффициентВесОбъемУпаковки",
			ИнтеграцияИСВызовСервера.КоэффициентВесОбъемУпаковок(ТабличнаяЧасть));
		Для Каждого ТекущаяСтрока Из ТабличнаяЧасть Цикл
			ОбработкаТабличнойЧастиКлиентСерверЛокализация.ПересчитатьТипИзмеряемойВеличиныСАТУРН(
				ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Шаблоны

Функция ТекстТоварыДокументаБезСерий(ИмяДокумента, ИмяТЧТовары = ".Товары")
	
	Шаблон = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|	ТаблицаТовары.Характеристика    КАК Характеристика,
	|	ТаблицаТовары.Серия             КАК Серия,
	|	СУММА(ТаблицаТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН)
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия
	|;
	|";
	
	Шаблон = СтрЗаменить(Шаблон, "РеализацияТоваровУслуг", ИмяДокумента);
	Шаблон = СтрЗаменить(Шаблон, ".Товары", ИмяТЧТовары);
	Возврат Шаблон;
	
КонецФункции

Функция ТекстТоварыДокументаССериями(ИмяДокумента, ИмяТЧТовары = ".Товары", ИмяТЧСерии = ".Серии")
	
	Шаблон = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|	ТаблицаТовары.Характеристика    КАК Характеристика,
	|	ТаблицаТовары.Серия             КАК Серия,
	|	СУММА(ТаблицаТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &ДокументОснование
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСерии.Номенклатура      КАК Номенклатура,
	|	ТаблицаСерии.Характеристика    КАК Характеристика,
	|	ТаблицаСерии.Серия             КАК Серия,
	|	СУММА(ТаблицаСерии.Количество) КАК Количество
	|ПОМЕСТИТЬ ТаблицаСерии
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Серии КАК ТаблицаСерии
	|ГДЕ
	|	ТаблицаСерии.Ссылка = &ДокументОснование
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСерии.Номенклатура,
	|	ТаблицаСерии.Характеристика,
	|	ТаблицаСерии.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура                                         КАК Номенклатура,
	|	ТаблицаТовары.Характеристика                                       КАК Характеристика,
	|	ЕСТЬNULL(ТаблицаСерии.Серия, ТаблицаТовары.Серия)                  КАК Серия,
	|	СУММА(ЕСТЬNULL(ТаблицаСерии.Количество, ТаблицаТовары.Количество)) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСерии КАК ТаблицаСерии
	|		ПО ТаблицаТовары.Номенклатура = ТаблицаСерии.Номенклатура
	|		И ТаблицаТовары.Характеристика = ТаблицаСерии.Характеристика
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН)
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ЕСТЬNULL(ТаблицаСерии.Серия, ТаблицаТовары.Серия)
	|;
	|";
	Шаблон = СтрЗаменить(Шаблон, "РеализацияТоваровУслуг", ИмяДокумента);
	Шаблон = СтрЗаменить(Шаблон, ".Товары", ИмяТЧТовары);
	Шаблон = СтрЗаменить(Шаблон, ".Серии", ИмяТЧСерии);
	Возврат Шаблон;
	
КонецФункции


#КонецОбласти

#КонецОбласти

#Область ПроизводственнаяОперацияСАТУРН

Процедура ОбработкаЗаполненияДокументаПроизводственнаяОперацияСАТУРН(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПрочееОприходованиеТоваров")Тогда
		ЗаполнитьПроизводственнуюОперациюСАТУРННаОснованииПрочегоОприходованияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.СборкаТоваров")Тогда
		ЗаполнитьПроизводственнуюОперациюСАТУРННаОснованииСборкиТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьШапкуПроизводственнойОперации(ДокументОбъект, ДанныеЗаполнения, РеквизитыДокумента, ИспользоватьПроизводственныйОбъект = Истина)
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение
			И Не ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		
		ИнтеграцияИСУТ.ПроверитьВозможностьВводаНаОсновании(РеквизитыДокумента);
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
		ЗаполнитьОрганизациюМестоХраненияСАТУРНПоРеквизитамДокумента(ДокументОбъект, РеквизитыДокумента, ИспользоватьПроизводственныйОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаЗаполненияПродукцииПроизводстваИзПрикладногоДокумента(ТекстТоварыДокумента)
	
	Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ПроизводственнаяОперацияСАТУРН КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК СтатусыДокументовСАТУРН
	|		ПО СтатусыДокументовСАТУРН.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовСАТУРН.Статус НЕ В(&КонечныеСтатусы)
	|;
	|"
	+
	ТекстТоварыДокумента
	+
	"
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыСерии.Номенклатура         КАК Номенклатура,
	|	ТоварыСерии.Характеристика       КАК Характеристика,
	|	ТоварыСерии.Серия                КАК Серия,
	|	ТоварыСерии.Количество           КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыСерии КАК ТоварыСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	-ОформленныеТовары.Количество    КАК Количество
	|ИЗ
	|	Документ.ПроизводственнаяОперацияСАТУРН.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка ИЗ ОформленныеДокументы КАК Т)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыСАТУРН.ПАТ,СоответствиеНоменклатурыСАТУРН2.ПАТ)) КАК ПАТ,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленныеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыСАТУРН КАК СоответствиеНоменклатурыСАТУРН
	|		ПО СоответствиеНоменклатурыСАТУРН.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыСАТУРН.Характеристика = ТабличнаяЧасть.Характеристика
	|			И СоответствиеНоменклатурыСАТУРН.Серия = ТабличнаяЧасть.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыСАТУРН КАК СоответствиеНоменклатурыСАТУРН2
	|		ПО СоответствиеНоменклатурыСАТУРН2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыСАТУРН2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыСАТУРН2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыСАТУРН.ПАТ,СоответствиеНоменклатурыСАТУРН2.ПАТ)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура                    КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика                  КАК Характеристика,
	|	ТоварыКОформлению.Серия                           КАК Серия,
	|	СопоставленныеПозиции.ПАТ                         КАК ПАТ,
	|	СУММА(ТоварыКОформлению.Количество)               КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество)               КАК КоличествоУпаковок,
	|	МАКСИМУМ(ТоварыКОформлению.Номенклатура.Артикул)            КАК АртикулПроизводителя,
	|	МАКСИМУМ(ТоварыКОформлению.Номенклатура.НаименованиеПолное) КАК ТорговоеНаименование,
	|	МАКСИМУМ(ТоварыКОформлению.Серия.ГоденДо)                   КАК СрокГодности,
	|	МАКСИМУМ(ТоварыКОформлению.Серия.Номер)                     КАК НомерПартии
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленныеПозиции КАК СопоставленныеПозиции
	|		ПО СопоставленныеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленныеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленныеПозиции.Серия = ТоварыКОформлению.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|			ПО ТоварыКОформлению.Номенклатура = СправочникНоменклатура.Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленныеПозиции.ПАТ
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|";
	
	Возврат Текст;
	
КонецФункции

Процедура ЗаполнитьТабличнуюЧастьТоварыПроизводственнойОперации(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента)
	
	ДокументОбъект.Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаЗаполненияПродукцииПроизводстваИзПрикладногоДокумента(ТекстТоварыДокумента);
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы", Документы.ПроизводственнаяОперацияСАТУРН.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия",       Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Тип.ПривестиЗначение());
	
	ЗаполнитьТабличнуюЧастьДокумента(ДокументОбъект.Товары, Запрос.Выполнить(), ДанныеЗаполнения);
	
	РеквизитыСерийВТЧТовары = Новый Структура;
	РеквизитыСерийВТЧТовары.Вставить("Номер", "НомерПартии");
	РеквизитыСерийВТЧТовары.Вставить("ГоденДо", "СрокГодности");
	
КонецПроцедуры

Функция ТекстЗапросаЗаполненияСырьяПроизводстваИзПрикладногоДокумента(ТекстТоварыДокумента)

	Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ПроизводственнаяОперацияСАТУРН КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК СтатусыДокументовСАТУРН
	|		ПО СтатусыДокументовСАТУРН.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовСАТУРН.Статус НЕ В(&КонечныеСтатусы)
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеАкты
	|ИЗ
	|	Документ.АктПримененияСАТУРН КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК СтатусыДокументовСАТУРН
	|		ПО СтатусыДокументовСАТУРН.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовСАТУРН.Статус НЕ В(&КонечныеСтатусыАкты)
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеНакладные
	|ИЗ
	|	Документ.НакладнаяСАТУРН КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК СтатусыДокументовСАТУРН
	|		ПО СтатусыДокументовСАТУРН.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовСАТУРН.Статус НЕ В(&КонечныеСтатусыНакладные)
	|;
	|"
	+
	ТекстТоварыДокумента
	+
	"
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыСерии.Номенклатура         КАК Номенклатура,
	|	ТоварыСерии.Характеристика       КАК Характеристика,
	|	ТоварыСерии.Серия                КАК Серия,
	|	ТоварыСерии.Количество           КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыСерии КАК ТоварыСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	-ОформленныеТовары.Количество    КАК Количество
	|ИЗ
	|	Документ.НакладнаяСАТУРН.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка ИЗ ОформленныеНакладные КАК Т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	-ОформленныеТовары.Количество    КАК Количество
	|ИЗ
	|	Документ.АктПримененияСАТУРН.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка ИЗ ОформленныеАкты КАК Т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	-ОформленныеТовары.Количество    КАК Количество
	|ИЗ
	|	Документ.ПроизводственнаяОперацияСАТУРН.Сырье КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка ИЗ ОформленныеДокументы КАК Т)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыСАТУРН.ПАТ,СоответствиеНоменклатурыСАТУРН2.ПАТ)) КАК ПАТ,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленныеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыСАТУРН КАК СоответствиеНоменклатурыСАТУРН
	|		ПО СоответствиеНоменклатурыСАТУРН.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыСАТУРН.Характеристика = ТабличнаяЧасть.Характеристика
	|			И СоответствиеНоменклатурыСАТУРН.Серия = ТабличнаяЧасть.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыСАТУРН КАК СоответствиеНоменклатурыСАТУРН2
	|		ПО СоответствиеНоменклатурыСАТУРН2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыСАТУРН2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыСАТУРН2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыСАТУРН.ПАТ,СоответствиеНоменклатурыСАТУРН2.ПАТ)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура                    КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика                  КАК Характеристика,
	|	ТоварыКОформлению.Серия                           КАК Серия,
	|	СопоставленныеПозиции.ПАТ                         КАК ПАТ,
	|	СУММА(ТоварыКОформлению.Количество)               КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество)               КАК КоличествоУпаковок
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленныеПозиции КАК СопоставленныеПозиции
	|		ПО СопоставленныеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленныеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленныеПозиции.Серия = ТоварыКОформлению.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленныеПозиции.ПАТ
	|ИМЕЮЩИЕ СУММА(ТоварыКОформлению.Количество)>0
	|";
	
	Возврат Текст;
	
КонецФункции

Процедура ЗаполнитьТабличнуюЧастьСырьеПроизводственнойОперации(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента)
	
	ДокументОбъект.Сырье.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаЗаполненияСырьяПроизводстваИзПрикладногоДокумента(ТекстТоварыДокумента);
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("Документ",          ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.ПроизводственнаяОперацияСАТУРН.КонечныеСтатусы());
	Запрос.УстановитьПараметр("КонечныеСтатусыАкты", Документы.АктПримененияСАТУРН.КонечныеСтатусы());
	Запрос.УстановитьПараметр("КонечныеСтатусыНакладные", Документы.НакладнаяСАТУРН.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия",       Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Тип.ПривестиЗначение());
	Запрос.УстановитьПараметр("ОсобенностьУчета",  Перечисления.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН);
	
	
	ЗаполнитьТабличнуюЧастьДокумента(ДокументОбъект.Сырье, Запрос.Выполнить(), ДанныеЗаполнения, Истина);
	
	Документы.ПроизводственнаяОперацияСАТУРН.ЗаполнитьПартии(ДокументОбъект);
	
КонецПроцедуры

Процедура ЗаполнитьПроизводственнуюОперациюСАТУРННаОснованииПрочегоОприходованияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	ДокументСсылка.Дата          КАК ДатаПервичногоДокумента,
	|	ДокументСсылка.Дата          КАК ДатаПроизводства,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный КАК Ответственный,
	|	ДокументСсылка.Организация   КАК Организация,
	|	ДокументСсылка.Подразделение КАК Подразделение,
	|	ДокументСсылка.Склад         КАК Склад,
	|	Неопределено                 КАК Контрагент,
	|	Неопределено                 КАК Партнер,
	|	ДокументСсылка.Номер         КАК НомерПервичногоДокумента,
	|	""Оприходование произведенных товаров"" КАК НаименованиеПервичногоДокумента
	|ИЗ
	|	Документ.ПрочееОприходованиеТоваров КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	РеквизитыДокумента = Запрос.Выполнить().Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЗаполнитьШапкуПроизводственнойОперации(ДокументОбъект, ДанныеЗаполнения, РеквизитыДокумента);
	
	ТекстТоварыДокумента = ТекстТоварыДокументаССериями("ПрочееОприходованиеТоваров");
	ЗаполнитьТабличнуюЧастьТоварыПроизводственнойОперации(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	
КонецПроцедуры



Процедура ЗаполнитьПроизводственнуюОперациюСАТУРННаОснованииСборкиТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	ДокументСсылка.Дата          КАК ДатаПервичногоДокумента,
	|	ДокументСсылка.Дата          КАК ДатаПроизводства,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный КАК Ответственный,
	|	ДокументСсылка.Организация   КАК Организация,
	|	ДокументСсылка.Подразделение КАК Подразделение,
	|	ДокументСсылка.Склад         КАК Склад,
	|	Неопределено                 КАК Контрагент,
	|	Неопределено                 КАК Партнер,
	|	ДокументСсылка.Номер         КАК НомерПервичногоДокумента,
	|	""Сборка (разборка) товаров"" КАК НаименованиеПервичногоДокумента,
	|	ДокументСсылка.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.СборкаТоваров КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	РеквизитыДокумента = Запрос.Выполнить().Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЗаполнитьШапкуПроизводственнойОперации(ДокументОбъект, ДанныеЗаполнения, РеквизитыДокумента, Ложь);
	
	Если РеквизитыДокумента.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СборкаТоваров Тогда
		ТекстТоварыДокумента = ТекстТоварыДокументаССериями("СборкаТоваров", "");
	Иначе
		ТекстТоварыДокумента = ТекстТоварыДокументаССериями("СборкаТоваров");
	КонецЕсли;
	ЗаполнитьТабличнуюЧастьТоварыПроизводственнойОперации(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	
	ТекстТоварыДокумента = ИнтеграцияИСУТ.ТекстЗапросаСборкаТоваров() +
	"
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|	ТаблицаТовары.Характеристика    КАК Характеристика,
	|	ТаблицаТовары.Серия             КАК Серия,
	|	СУММА(ТаблицаТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ ТаблицаТоварыСборкаТоваров КАК ТаблицаТовары
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия
	|;
	|";
	ЗаполнитьТабличнуюЧастьСырьеПроизводственнойОперации(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	
КонецПроцедуры

#КонецОбласти

#Область АктИнвентаризацииСАТУРН

Функция ДанныеПрикладныхДокументовИзАктИнвентаризацииСАТУРН(ДокументСсылка, ПолучитьШапку = Истина, ПолучитьТовары = Истина, Расход = Ложь)

	СписокЗапросов = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Если ПолучитьШапку Тогда
		
		СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	АктИнвентаризацииСАТУРН.ОрганизацияСАТУРН.Контрагент КАК Организация,
		|	АктИнвентаризацииСАТУРН.Дата                         КАК Дата,
		|	АктИнвентаризацииСАТУРН.Комментарий                  КАК Комментарий,
		|	АктИнвентаризацииСАТУРН.ОрганизацияСАТУРН            КАК ОрганизацияСАТУРН,
		|	АктИнвентаризацииСАТУРН.Ответственный                КАК Ответственный,
		|	АктИнвентаризацииСАТУРН.МестоХранения                КАК МестоХранения
		|ИЗ
		|	Документ.АктИнвентаризацииСАТУРН КАК АктИнвентаризацииСАТУРН
		|ГДЕ
		|	АктИнвентаризацииСАТУРН.Ссылка = &Ссылка", "Шапка");
		
	КонецЕсли;
	
	Если ПолучитьТовары Тогда
		
		Запрос.УстановитьПараметр("СтранаРегистрации", ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана());
		
		СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	СтавкиНДСНоменклатуры.Номенклатура КАК Номенклатура,
		|	СтавкиНДСНоменклатуры.СтавкаНДС    КАК СтавкаНДС
		|ПОМЕСТИТЬ СтавкиНДСНоменклатуры
		|ИЗ
		|	(ВЫБРАТЬ
		|		СтавкиНДСНоменклатуры.Номенклатура КАК Номенклатура,
		|		СтавкиНДСНоменклатуры.Страна КАК Страна,
		|		МАКСИМУМ(СтавкиНДСНоменклатуры.Период) КАК Период
		|		
		|	ИЗ
		|		РегистрСведений.СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктИнвентаризацииСАТУРН.Товары КАК АктИнвентаризацииСАТУРНТовары
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктИнвентаризацииСАТУРН КАК АктИнвентаризацииСАТУРН
		|				ПО АктИнвентаризацииСАТУРНТовары.Ссылка = АктИнвентаризацииСАТУРН.Ссылка
		|			ПО СтавкиНДСНоменклатуры.Номенклатура = АктИнвентаризацииСАТУРНТовары.Номенклатура
		|				И (СтавкиНДСНоменклатуры.Страна = &СтранаРегистрации
		|					ИЛИ СтавкиНДСНоменклатуры.Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка))
		|				И СтавкиНДСНоменклатуры.Период <= АктИнвентаризацииСАТУРН.Дата
		|	ГДЕ
		|		АктИнвентаризацииСАТУРНТовары.Ссылка = &Ссылка
		|	СГРУППИРОВАТЬ ПО
		|		СтавкиНДСНоменклатуры.Номенклатура,
		|		СтавкиНДСНоменклатуры.Страна) КАК АктуальныеСтавкиНДС
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
		|	ПО АктуальныеСтавкиНДС.Период = СтавкиНДСНоменклатуры.Период
		|		И АктуальныеСтавкиНДС.Номенклатура = СтавкиНДСНоменклатуры.Номенклатура
		|		И АктуальныеСтавкиНДС.Страна = СтавкиНДСНоменклатуры.Страна
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОсновныеСтавкиНДС.СтавкаНДС КАК СтавкаНДС
		|ПОМЕСТИТЬ ОсновныеСтавкиНДС
		|ИЗ
		|	(ВЫБРАТЬ
		|		ОсновныеСтавкиНДС.Страна КАК Страна,
		|		МАКСИМУМ(ОсновныеСтавкиНДС.Период) КАК Период
		|		
		|	ИЗ
		|		РегистрСведений.ОсновныеСтавкиНДС КАК ОсновныеСтавкиНДС
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктИнвентаризацииСАТУРН КАК АктИнвентаризацииСАТУРН
		|			ПО (ОсновныеСтавкиНДС.Страна = &СтранаРегистрации
		|					ИЛИ ОсновныеСтавкиНДС.Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка))
		|				И ОсновныеСтавкиНДС.Период <= АктИнвентаризацииСАТУРН.Дата
		|	ГДЕ
		|		АктИнвентаризацииСАТУРН.Ссылка = &Ссылка
		|	СГРУППИРОВАТЬ ПО
		|		ОсновныеСтавкиНДС.Страна) КАК АктуальныеСтавкиНДС
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСтавкиНДС КАК ОсновныеСтавкиНДС
		|	ПО АктуальныеСтавкиНДС.Период = ОсновныеСтавкиНДС.Период
		|		И АктуальныеСтавкиНДС.Страна = ОсновныеСтавкиНДС.Страна
		|");
		
		ТекстТовары = "ВЫБРАТЬ
		|	АктИнвентаризацииСАТУРНТовары.Номенклатура                     КАК Номенклатура,
		|	ЕСТЬNULL(СтавкиНДСНоменклатуры.СтавкаНДС, ЕСТЬNULL(ОсновныеСтавкиНДС.СтавкаНДС,
		|		ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)))              КАК СтавкаНДС,
		|	АктИнвентаризацииСАТУРНТовары.Характеристика                   КАК Характеристика,
		|	АктИнвентаризацииСАТУРНТовары.Серия                            КАК Серия,
		|	АктИнвентаризацииСАТУРНТовары.Количество*&ДопустимыйЗнакСтроки КАК Количество,
		|	АктИнвентаризацииСАТУРНТовары.Количество*&ДопустимыйЗнакСтроки КАК КоличествоУпаковок
		|ИЗ
		|	Документ.АктИнвентаризацииСАТУРН.Товары КАК АктИнвентаризацииСАТУРНТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
		|		ПО АктИнвентаризацииСАТУРНТовары.Номенклатура = СтавкиНДСНоменклатуры.Номенклатура,
		|	ОсновныеСтавкиНДС КАК ОсновныеСтавкиНДС
		|ГДЕ
		|	АктИнвентаризацииСАТУРНТовары.Ссылка = &Ссылка
		|	И АктИнвентаризацииСАТУРНТовары.КоличествоУпаковок > 0";
		ДопустимыйЗнакСтроки = 1;
		Если Расход Тогда
			ТекстТовары = СтрЗаменить(ТекстТовары, "> 0", "< 0");
			ДопустимыйЗнакСтроки = -1;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("ДопустимыйЗнакСтроки", ДопустимыйЗнакСтроки);
		
		СписокЗапросов.Добавить(ТекстТовары, "Товары");
	КонецЕсли;
	
	Возврат ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов);

КонецФункции

Процедура ЗаполнитьШапкуПрикладногоДокументаНаОснованииАктИнвентаризацииСАТУРН(ДокументОбъект, ДанныеЗаполнения)
	
	Выборка = ДанныеЗаполнения.Выбрать();
	Выборка.Следующий();
	
	СкладПодразделение = СкладПодразделениеИзМестаХранения(Выборка);
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Организация",   Выборка.Организация);
	Реквизиты.Вставить("Дата",          Выборка.Дата);
	Реквизиты.Вставить("Комментарий",   Выборка.Комментарий);
	Реквизиты.Вставить("Склад",         СкладПодразделение.Склад);
	Реквизиты.Вставить("Подразделение", СкладПодразделение.Подразделение);
	
	ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты);
	
КонецПроцедуры

Функция СтруктураЗаполненияТабличныхЧастейПоАктИнвентаризацииСАТУРН(ДанныеЗаполнения, ЕстьСерии = Ложь, ЕстьСкладВТабличнойЧасти = Ложь)
	
	Результат = Новый Структура;
	Результат.Вставить("Товары", ДанныеЗаполнения.Выгрузить());
	Если ЕстьСерии Тогда
		Результат.Вставить("ЕстьСерии");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииАктИнвентаризацииСАТУРН(ДокументОбъект, ДанныеЗаполнения, ДокументОснование)
	
	ДокументОбъект.Товары.Очистить();
	
	Для Каждого СтрокаТовары Из ДанныеЗаполнения.Товары Цикл
		
		НоваяСтрокаТовары = ДокументОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары, СтрокаТовары);
		
	КонецЦикла;

	Если ДанныеЗаполнения.Свойство("ЕстьСерии") Тогда
		
		ИмяТЧСерии = ДанныеЗаполнения.ЕстьСерии;
		Если ИмяТЧСерии = Неопределено Тогда 
			ИмяТЧСерии = "Серии";
		КонецЕсли;
		
		ДокументОбъект[ИмяТЧСерии].Очистить();
		Для Каждого СтрокаТовары Из ДокументОбъект.Товары Цикл
			Если ЗначениеЗаполнено(СтрокаТовары.Серия) Тогда
				НоваяСтрокаСерия = ДокументОбъект[ИмяТЧСерии].Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаСерия, СтрокаТовары);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполненияДокументаАктИнвентаризацииСАТУРН(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОприходованиеИзлишковТоваров")Тогда
		ЗаполнитьАктИнвентаризацииСАТУРННаОснованииОприходованияИзлишковТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПересортицаТоваров")Тогда
		ЗаполнитьАктИнвентаризацииСАТУРННаОснованииПересортицыТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПорчаТоваров")Тогда
		ЗаполнитьАктИнвентаризацииСАТУРННаОснованииПорчиТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.СписаниеНедостачТоваров")Тогда
		ЗаполнитьАктИнвентаризацииСАТУРННаОснованииСписанияНедостачТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПрочееОприходованиеТоваров")Тогда
		ЗаполнитьАктИнвентаризацииСАТУРННаОснованииПрочегоОприходованияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОприходованиеИзлишковТоваровУХранителя")Тогда
		ЗаполнитьАктИнвентаризацииСАТУРННаОснованииОприходованияИзлишковТоваровУХранителя(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетОСписанииТоваровУХранителя")Тогда
		ЗаполнитьАктИнвентаризацииСАТУРННаОснованииОтчетаОСписанииТоваровУХранителя(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПорчаТоваровУХранителя")Тогда
		ЗаполнитьАктИнвентаризацииСАТУРННаОснованииПорчиТоваровУХранителя(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПересортицаТоваровУХранителя")Тогда
		ЗаполнитьАктИнвентаризацииСАТУРННаОснованииПересортицыТоваровУХранителя(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьШапкуАктаИнвентаризации(ДокументОбъект, ДанныеЗаполнения, Запрос)
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	РеквизитыДокумента = Запрос.Выполнить().Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение
			И Не ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		
		ИнтеграцияИСУТ.ПроверитьВозможностьВводаНаОсновании(РеквизитыДокумента);
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
		ЗаполнитьОрганизациюМестоХраненияСАТУРНПоРеквизитамДокумента(ДокументОбъект, РеквизитыДокумента);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаЗаполненияАктаИнвентаризацииИзПрикладногоДокумента(ТекстТоварыДокумента, ДопустимыйЗнак)
	Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.АктИнвентаризацииСАТУРН КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК СтатусыДокументовСАТУРН
	|		ПО СтатусыДокументовСАТУРН.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовСАТУРН.Статус НЕ В(&КонечныеСтатусы)
	|;
	|"
	+
	ТекстТоварыДокумента
	+
	"
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыСерии.Номенклатура         КАК Номенклатура,
	|	ТоварыСерии.Характеристика       КАК Характеристика,
	|	ТоварыСерии.Серия                КАК Серия,
	|	ТоварыСерии.Количество           КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыСерии КАК ТоварыСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	-ОформленныеТовары.Количество    КАК Количество
	|ИЗ
	|	Документ.АктИнвентаризацииСАТУРН.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка ИЗ ОформленныеДокументы КАК Т)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыСАТУРН.ПАТ,СоответствиеНоменклатурыСАТУРН2.ПАТ)) КАК ПАТ,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленныеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыСАТУРН КАК СоответствиеНоменклатурыСАТУРН
	|		ПО СоответствиеНоменклатурыСАТУРН.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыСАТУРН.Характеристика = ТабличнаяЧасть.Характеристика
	|			И СоответствиеНоменклатурыСАТУРН.Серия = ТабличнаяЧасть.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыСАТУРН КАК СоответствиеНоменклатурыСАТУРН2
	|		ПО СоответствиеНоменклатурыСАТУРН2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыСАТУРН2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыСАТУРН2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыСАТУРН.ПАТ,СоответствиеНоменклатурыСАТУРН2.ПАТ)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура                    КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика                  КАК Характеристика,
	|	ТоварыКОформлению.Серия                           КАК Серия,
	|	СопоставленныеПозиции.ПАТ                         КАК ПАТ,
	|	СУММА(ТоварыКОформлению.Количество)               КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество)               КАК КоличествоУпаковок,
//
	|	МАКСИМУМ(ТоварыКОформлению.Номенклатура.Артикул)            КАК АртикулПроизводителя,
	|	МАКСИМУМ(ТоварыКОформлению.Номенклатура.НаименованиеПолное) КАК ТорговоеНаименование,
	|	МАКСИМУМ(ТоварыКОформлению.Серия.ГоденДо)                   КАК СрокГодности,
	|	МАКСИМУМ(ТоварыКОформлению.Серия.ДатаПроизводства)          КАК ДатаПроизводства,
	|	МАКСИМУМ(ТоварыКОформлению.Серия.Номер)                     КАК НомерПартии
	|
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленныеПозиции КАК СопоставленныеПозиции
	|		ПО СопоставленныеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленныеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленныеПозиции.Серия = ТоварыКОформлению.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|			ПО ТоварыКОформлению.Номенклатура = СправочникНоменклатура.Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленныеПозиции.ПАТ
//	
	|ИМЕЮЩИЕ &ДопустимыйЗнак
	|";
	Если ДопустимыйЗнак = -1 Тогда
		Текст = СтрЗаменить(Текст, "&ДопустимыйЗнак", "СУММА(ТоварыКОформлению.Количество)<0");
	ИначеЕсли ДопустимыйЗнак = 1 Тогда
		Текст = СтрЗаменить(Текст, "&ДопустимыйЗнак", "СУММА(ТоварыКОформлению.Количество)>0");
	Иначе
		Текст = СтрЗаменить(Текст, "&ДопустимыйЗнак", "СУММА(ТоварыКОформлению.Количество)<>0");
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

Процедура ЗаполнитьТабличнуюЧастьАктаИнвентаризации(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, ДопустимыйЗнак)
	
	ДокументОбъект.Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаЗаполненияАктаИнвентаризацииИзПрикладногоДокумента(ТекстТоварыДокумента, ДопустимыйЗнак);
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.АктИнвентаризацииСАТУРН.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия",       Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Тип.ПривестиЗначение());
	
	ЗаполнитьТабличнуюЧастьДокумента(ДокументОбъект.Товары, Запрос.Выполнить(), ДанныеЗаполнения);
	Если ДопустимыйЗнак <> 1 Тогда
		Документы.АктИнвентаризацииСАТУРН.ЗаполнитьПартии(ДокументОбъект);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьАктИнвентаризацииСАТУРННаОснованииОприходованияИзлишковТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	ДокументСсылка.Дата          КАК ДатаРегистрации,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный КАК Ответственный,
	|	ДокументСсылка.Организация   КАК Организация,
	|	ДокументСсылка.Подразделение КАК Подразделение,
	|	ДокументСсылка.Склад         КАК Склад,
	|	Неопределено                 КАК Контрагент,
	|	Неопределено                 КАК Партнер,
	|	""Оприходование излишков товаров"" КАК ПричинаИнвентаризации
	|ИЗ
	|	Документ.ОприходованиеИзлишковТоваров КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуАктаИнвентаризации(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = ТекстТоварыДокументаБезСерий("ОприходованиеИзлишковТоваров");
	ЗаполнитьТабличнуюЧастьАктаИнвентаризации(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, 1);
	
КонецПроцедуры

Процедура ЗаполнитьАктИнвентаризацииСАТУРННаОснованииОприходованияИзлишковТоваровУХранителя(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	ДокументСсылка.Дата          КАК ДатаРегистрации,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный КАК Ответственный,
	|	ДокументСсылка.Организация   КАК Организация,
	|	ДокументСсылка.Подразделение КАК Подразделение,
	|	Неопределено                 КАК Склад,
	|	ДокументСсылка.Контрагент    КАК Контрагент,
	|	ДокументСсылка.Партнер       КАК Партнер,
	|	""Оприходование излишков товаров"" КАК ПричинаИнвентаризации
	|ИЗ
	|	Документ.ОприходованиеИзлишковТоваровУХранителя КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуАктаИнвентаризации(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = ТекстТоварыДокументаБезСерий("ОприходованиеИзлишковТоваровУХранителя");
	ЗаполнитьТабличнуюЧастьАктаИнвентаризации(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, 1);
	
КонецПроцедуры

Процедура ЗаполнитьАктИнвентаризацииСАТУРННаОснованииПрочегоОприходованияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	ДокументСсылка.Дата          КАК ДатаРегистрации,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный КАК Ответственный,
	|	ДокументСсылка.Организация   КАК Организация,
	|	ДокументСсылка.Подразделение КАК Подразделение,
	|	ДокументСсылка.Склад         КАК Склад,
	|	Неопределено                 КАК Контрагент,
	|	Неопределено                 КАК Партнер,
	|	""Прочее оприходование товаров"" КАК ПричинаИнвентаризации
	|ИЗ
	|	Документ.ПрочееОприходованиеТоваров КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуАктаИнвентаризации(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = ТекстТоварыДокументаССериями("ПрочееОприходованиеТоваров");
	ЗаполнитьТабличнуюЧастьАктаИнвентаризации(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, 1);
	
КонецПроцедуры

Процедура ЗаполнитьАктИнвентаризацииСАТУРННаОснованииСписанияНедостачТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка         КАК ДокументОснование,
	|	ДокументСсылка.Дата           КАК ДатаРегистрации,
	|	НЕ ДокументСсылка.Проведен    КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный  КАК Ответственный,
	|	ДокументСсылка.Организация    КАК Организация,
	|	ДокументСсылка.Подразделение  КАК Подразделение,
	|	ДокументСсылка.Склад          КАК Склад,
	|	Неопределено                  КАК Контрагент,
	|	Неопределено                  КАК Партнер,
	|	""Списание недостач товаров"" КАК ПричинаИнвентаризации
	|ИЗ
	|	Документ.СписаниеНедостачТоваров КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуАктаИнвентаризации(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура       КАК Номенклатура,
	|	Товары.Характеристика     КАК Характеристика,
	|	Товары.Серия              КАК Серия,
	|	-СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.СписаниеНедостачТоваров.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН)
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьАктаИнвентаризации(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, -1);
	
КонецПроцедуры

Процедура ЗаполнитьАктИнвентаризацииСАТУРННаОснованииОтчетаОСписанииТоваровУХранителя(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка         КАК ДокументОснование,
	|	ДокументСсылка.Дата           КАК ДатаРегистрации,
	|	НЕ ДокументСсылка.Проведен    КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный  КАК Ответственный,
	|	ДокументСсылка.Организация    КАК Организация,
	|	ДокументСсылка.Подразделение  КАК Подразделение,
	|	Неопределено                  КАК Склад,
	|	ДокументСсылка.Контрагент     КАК Контрагент,
	|	ДокументСсылка.Партнер        КАК Партнер,
	|	""Списание недостач товаров"" КАК ПричинаИнвентаризации
	|ИЗ
	|	Документ.ОтчетОСписанииТоваровУХранителя КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуАктаИнвентаризации(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура       КАК Номенклатура,
	|	Товары.Характеристика     КАК Характеристика,
	|	Товары.Серия              КАК Серия,
	|	-СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.ОтчетОСписанииТоваровУХранителя.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН)
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьАктаИнвентаризации(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, -1);
	
КонецПроцедуры

Процедура ЗаполнитьАктИнвентаризацииСАТУРННаОснованииПересортицыТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	ДокументСсылка.Дата          КАК ДатаРегистрации,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный КАК Ответственный,
	|	ДокументСсылка.Организация   КАК Организация,
	|	ДокументСсылка.Подразделение КАК Подразделение,
	|	ДокументСсылка.Склад         КАК Склад,
	|	Неопределено                 КАК Контрагент,
	|	Неопределено                 КАК Партнер,
	|	""Пересортица товаров""      КАК ПричинаИнвентаризации
	|ИЗ
	|	Документ.ПересортицаТоваров КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуАктаИнвентаризации(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура       КАК Номенклатура,
	|	Товары.Характеристика     КАК Характеристика,
	|	Товары.Серия              КАК Серия,
	|	-СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.ПересортицаТоваров.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН)
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.НоменклатураОприходование,
	|	Товары.ХарактеристикаОприходование,
	|	Товары.СерияОприходование,
	|	СУММА(Товары.Количество)
	|ИЗ
	|	Документ.ПересортицаТоваров.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.НоменклатураОприходование = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН)
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.НоменклатураОприходование,
	|	Товары.ХарактеристикаОприходование,
	|	Товары.СерияОприходование
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьАктаИнвентаризации(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, 0);
	
КонецПроцедуры

Процедура ЗаполнитьАктИнвентаризацииСАТУРННаОснованииПорчиТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	ДокументСсылка.Дата          КАК ДатаРегистрации,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный КАК Ответственный,
	|	ДокументСсылка.Организация   КАК Организация,
	|	ДокументСсылка.Подразделение КАК Подразделение,
	|	ДокументСсылка.Склад         КАК Склад,
	|	Неопределено                 КАК Контрагент,
	|	Неопределено                 КАК Партнер,
	|	""Порча товаров""            КАК ПричинаИнвентаризации
	|ИЗ
	|	Документ.ПорчаТоваров КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуАктаИнвентаризации(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура       КАК Номенклатура,
	|	Товары.Характеристика     КАК Характеристика,
	|	Товары.Серия              КАК Серия,
	|	-СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.ПорчаТоваров.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН)
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.НоменклатураОприходование,
	|	Товары.ХарактеристикаОприходование,
	|	Товары.Серия,
	|	СУММА(Товары.Количество)
	|ИЗ
	|	Документ.ПорчаТоваров.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.НоменклатураОприходование = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН)
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.НоменклатураОприходование,
	|	Товары.ХарактеристикаОприходование,
	|	Товары.Серия
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьАктаИнвентаризации(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, 0);
	
КонецПроцедуры

Процедура ЗаполнитьАктИнвентаризацииСАТУРННаОснованииПересортицыТоваровУХранителя(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	ДокументСсылка.Дата          КАК ДатаРегистрации,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный КАК Ответственный,
	|	ДокументСсылка.Организация   КАК Организация,
	|	ДокументСсылка.Подразделение КАК Подразделение,
	|	Неопределено                 КАК Склад,
	|	ДокументСсылка.Контрагент    КАК Контрагент,
	|	ДокументСсылка.Партнер       КАК Партнер,
	|	""Пересортица товаров""      КАК ПричинаИнвентаризации
	|ИЗ
	|	Документ.ПересортицаТоваровУХранителя КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуАктаИнвентаризации(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура       КАК Номенклатура,
	|	Товары.Характеристика     КАК Характеристика,
	|	Товары.Серия              КАК Серия,
	|	-СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.ПересортицаТоваровУХранителя.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН)
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.НоменклатураОприходование,
	|	Товары.ХарактеристикаОприходование,
	|	Товары.СерияОприходование,
	|	СУММА(Товары.Количество)
	|ИЗ
	|	Документ.ПересортицаТоваровУХранителя.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.НоменклатураОприходование = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН)
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.НоменклатураОприходование,
	|	Товары.ХарактеристикаОприходование,
	|	Товары.СерияОприходование
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьАктаИнвентаризации(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, 0);
	
КонецПроцедуры

Процедура ЗаполнитьАктИнвентаризацииСАТУРННаОснованииПорчиТоваровУХранителя(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	ДокументСсылка.Дата          КАК ДатаРегистрации,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный КАК Ответственный,
	|	ДокументСсылка.Организация   КАК Организация,
	|	ДокументСсылка.Подразделение КАК Подразделение,
	|	Неопределено                 КАК Склад,
	|	ДокументСсылка.Контрагент    КАК Контрагент,
	|	ДокументСсылка.Партнер       КАК Партнер,
	|	""Порча товаров""            КАК ПричинаИнвентаризации
	|ИЗ
	|	Документ.ПорчаТоваровУХранителя КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуАктаИнвентаризации(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура       КАК Номенклатура,
	|	Товары.Характеристика     КАК Характеристика,
	|	Товары.Серия              КАК Серия,
	|	-СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.ПорчаТоваровУХранителя.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН)
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.НоменклатураОприходование,
	|	Товары.ХарактеристикаОприходование,
	|	Товары.Серия,
	|	СУММА(Товары.Количество)
	|ИЗ
	|	Документ.ПорчаТоваровУХранителя.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.НоменклатураОприходование = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН)
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.НоменклатураОприходование,
	|	Товары.ХарактеристикаОприходование,
	|	Товары.Серия
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьАктаИнвентаризации(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, 0);
	
КонецПроцедуры

#КонецОбласти

#Область АктПримененияСАТУРН

Функция ДанныеПрикладныхДокументовИзАктПримененияСАТУРН(ДокументСсылка, ПолучитьШапку = Истина, ПолучитьТовары = Истина)

	СписокЗапросов = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Если ПолучитьШапку Тогда
		
		СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	АктПримененияСАТУРН.ОрганизацияСАТУРН.Контрагент КАК Организация,
		|	АктПримененияСАТУРН.Дата                         КАК Дата,
		|	АктПримененияСАТУРН.Комментарий                  КАК Комментарий,
		|	АктПримененияСАТУРН.ОрганизацияСАТУРН            КАК ОрганизацияСАТУРН,
		|	АктПримененияСАТУРН.Ответственный                КАК Ответственный,
		|	АктПримененияСАТУРН.МестоХранения                КАК МестоХранения
		|ИЗ
		|	Документ.АктПримененияСАТУРН КАК АктПримененияСАТУРН
		|ГДЕ
		|	АктПримененияСАТУРН.Ссылка = &Ссылка", "Шапка");
		
	КонецЕсли;
	
	Если ПолучитьТовары Тогда
		
		Запрос.УстановитьПараметр("СтранаРегистрации", ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана());
		
		СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	СтавкиНДСНоменклатуры.Номенклатура КАК Номенклатура,
		|	СтавкиНДСНоменклатуры.СтавкаНДС    КАК СтавкаНДС
		|ПОМЕСТИТЬ СтавкиНДСНоменклатуры
		|ИЗ
		|	(ВЫБРАТЬ
		|		СтавкиНДСНоменклатуры.Номенклатура КАК Номенклатура,
		|		СтавкиНДСНоменклатуры.Страна КАК Страна,
		|		МАКСИМУМ(СтавкиНДСНоменклатуры.Период) КАК Период
		|		
		|	ИЗ
		|		РегистрСведений.СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктПримененияСАТУРН.Товары КАК АктПримененияСАТУРНТовары
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктПримененияСАТУРН КАК АктПримененияСАТУРН
		|				ПО АктПримененияСАТУРНТовары.Ссылка = АктПримененияСАТУРН.Ссылка
		|			ПО СтавкиНДСНоменклатуры.Номенклатура = АктПримененияСАТУРНТовары.Номенклатура
		|				И (СтавкиНДСНоменклатуры.Страна = &СтранаРегистрации
		|					ИЛИ СтавкиНДСНоменклатуры.Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка))
		|				И СтавкиНДСНоменклатуры.Период <= АктПримененияСАТУРН.Дата
		|	ГДЕ
		|		АктПримененияСАТУРНТовары.Ссылка = &Ссылка
		|	СГРУППИРОВАТЬ ПО
		|		СтавкиНДСНоменклатуры.Номенклатура,
		|		СтавкиНДСНоменклатуры.Страна) КАК АктуальныеСтавкиНДС
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
		|	ПО АктуальныеСтавкиНДС.Период = СтавкиНДСНоменклатуры.Период
		|		И АктуальныеСтавкиНДС.Номенклатура = СтавкиНДСНоменклатуры.Номенклатура
		|		И АктуальныеСтавкиНДС.Страна = СтавкиНДСНоменклатуры.Страна
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОсновныеСтавкиНДС.СтавкаНДС КАК СтавкаНДС
		|ПОМЕСТИТЬ ОсновныеСтавкиНДС
		|ИЗ
		|	(ВЫБРАТЬ
		|		ОсновныеСтавкиНДС.Страна КАК Страна,
		|		МАКСИМУМ(ОсновныеСтавкиНДС.Период) КАК Период
		|		
		|	ИЗ
		|		РегистрСведений.ОсновныеСтавкиНДС КАК ОсновныеСтавкиНДС
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктПримененияСАТУРН КАК АктПримененияСАТУРН
		|			ПО (ОсновныеСтавкиНДС.Страна = &СтранаРегистрации
		|					ИЛИ ОсновныеСтавкиНДС.Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка))
		|				И ОсновныеСтавкиНДС.Период <= АктПримененияСАТУРН.Дата
		|	ГДЕ
		|		АктПримененияСАТУРН.Ссылка = &Ссылка
		|	СГРУППИРОВАТЬ ПО
		|		ОсновныеСтавкиНДС.Страна) КАК АктуальныеСтавкиНДС
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСтавкиНДС КАК ОсновныеСтавкиНДС
		|	ПО АктуальныеСтавкиНДС.Период = ОсновныеСтавкиНДС.Период
		|		И АктуальныеСтавкиНДС.Страна = ОсновныеСтавкиНДС.Страна
		|");
		
		ТекстТовары = "ВЫБРАТЬ
		|	АктПримененияСАТУРНТовары.Номенклатура                     КАК Номенклатура,
		|	ЕСТЬNULL(СтавкиНДСНоменклатуры.СтавкаНДС, ЕСТЬNULL(ОсновныеСтавкиНДС.СтавкаНДС,
		|		ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)))              КАК СтавкаНДС,
		|	АктПримененияСАТУРНТовары.Характеристика                   КАК Характеристика,
		|	АктПримененияСАТУРНТовары.Серия                            КАК Серия,
		|	АктПримененияСАТУРНТовары.Количество                       КАК Количество,
		|	АктПримененияСАТУРНТовары.Количество                       КАК КоличествоУпаковок
		|ИЗ
		|	Документ.АктПримененияСАТУРН.Товары КАК АктПримененияСАТУРНТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
		|		ПО АктПримененияСАТУРНТовары.Номенклатура = СтавкиНДСНоменклатуры.Номенклатура,
		|	ОсновныеСтавкиНДС КАК ОсновныеСтавкиНДС
		|ГДЕ
		|	АктПримененияСАТУРНТовары.Ссылка = &Ссылка";
		
		СписокЗапросов.Добавить(ТекстТовары, "Товары");
	КонецЕсли;
	
	Возврат ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов);

КонецФункции

Процедура ЗаполнитьШапкуПрикладногоДокументаНаОснованииАктПримененияСАТУРН(ДокументОбъект, ДанныеЗаполнения)
	
	Выборка = ДанныеЗаполнения.Выбрать();
	Выборка.Следующий();
	
	СкладПодразделение = СкладПодразделениеИзМестаХранения(Выборка);
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Организация",   Выборка.Организация);
	Реквизиты.Вставить("Дата",          Выборка.Дата);
	Реквизиты.Вставить("Комментарий",   Выборка.Комментарий);
	Реквизиты.Вставить("Склад",         СкладПодразделение.Склад);
	Реквизиты.Вставить("Подразделение", СкладПодразделение.Подразделение);
	
	ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты);
	
КонецПроцедуры

Функция СтруктураЗаполненияТабличныхЧастейПоАктПримененияСАТУРН(ДанныеЗаполнения, ЕстьСерии = Ложь, ЕстьСкладВТабличнойЧасти = Ложь)
	
	Результат = Новый Структура;
	Результат.Вставить("Товары", ДанныеЗаполнения.Выгрузить());
	Если ЕстьСерии Тогда
		Результат.Вставить("ЕстьСерии");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииАктПримененияСАТУРН(ДокументОбъект, ДанныеЗаполнения, ДокументОснование)
	
	ДокументОбъект.Товары.Очистить();
	
	Для Каждого СтрокаТовары Из ДанныеЗаполнения.Товары Цикл
		
		НоваяСтрокаТовары = ДокументОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары, СтрокаТовары);
		
	КонецЦикла;

	Если ДанныеЗаполнения.Свойство("ЕстьСерии") Тогда
		
		ИмяТЧСерии = ДанныеЗаполнения.ЕстьСерии;
		Если ИмяТЧСерии = Неопределено Тогда 
			ИмяТЧСерии = "Серии";
		КонецЕсли;
		
		ДокументОбъект[ИмяТЧСерии].Очистить();
		Для Каждого СтрокаТовары Из ДокументОбъект.Товары Цикл
			Если ЗначениеЗаполнено(СтрокаТовары.Серия) Тогда
				НоваяСтрокаСерия = ДокументОбъект[ИмяТЧСерии].Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаСерия, СтрокаТовары);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполненияДокументаАктПримененияСАТУРН(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РеализацияТоваровУслуг")Тогда
		ЗаполнитьАктПримененияСАТУРННаОснованииРеализацииТоваровУслуг(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВнутреннееПотребление")Тогда
		ЗаполнитьАктПримененияСАТУРННаОснованииВнутреннегоПотребления(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаЗаполненияАктаПримененияИзПрикладногоДокумента(ТекстТоварыДокумента, ДополнительныеПараметры = Неопределено)
	
	ДетализацияДоСклада = Ложь;
	
	Если ДополнительныеПараметры <> Неопределено
		И ДополнительныеПараметры.Свойство("ЗаполнитьСкладИзТабличнойЧасти") Тогда
		ДетализацияДоСклада = ДополнительныеПараметры.ЗаполнитьСкладИзТабличнойЧасти;
	КонецЕсли;
	
	Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.АктПримененияСАТУРН КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК СтатусыДокументовСАТУРН
	|		ПО СтатусыДокументовСАТУРН.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовСАТУРН.Статус НЕ В(&КонечныеСтатусы)
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеНакладные
	|ИЗ
	|	Документ.НакладнаяСАТУРН КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК СтатусыДокументовСАТУРН
	|		ПО СтатусыДокументовСАТУРН.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовСАТУРН.Статус НЕ В(&КонечныеСтатусыНакладная)
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеПроизводства
	|ИЗ
	|	Документ.ПроизводственнаяОперацияСАТУРН КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК СтатусыДокументовСАТУРН
	|		ПО СтатусыДокументовСАТУРН.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовСАТУРН.Статус НЕ В(&КонечныеСтатусыПроизводство)
	|;
	|"
	+
	ТекстТоварыДокумента
	+
	"
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОформленныеТовары.Ссылка.ОрганизацияСАТУРН КАК ОрганизацияСАТУРН,
	|	ОформленныеТовары.Ссылка.МестоХранения     КАК МестоХранения,
	|	ОформленныеТовары.Номенклатура             КАК Номенклатура,
	|	ОформленныеТовары.Характеристика           КАК Характеристика,
	|	ОформленныеТовары.Серия                    КАК Серия,
	|	-ОформленныеТовары.Количество              КАК Количество
	|ПОМЕСТИТЬ ОформленныеТоварыПоДокументамСАТУРН
	|ИЗ
	|	Документ.АктПримененияСАТУРН.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка ИЗ ОформленныеДокументы КАК Т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Ссылка.ГрузополучательОрганизацияСАТУРН КАК ОрганизацияСАТУРН,
	|	ОформленныеТовары.Ссылка.ГрузополучательМестоХранения     КАК МестоХранения,
	|	ОформленныеТовары.Номенклатура                            КАК Номенклатура,
	|	ОформленныеТовары.Характеристика                          КАК Характеристика,
	|	ОформленныеТовары.Серия                                   КАК Серия,
	|	-ОформленныеТовары.Количество                             КАК Количество
	|ИЗ
	|	Документ.НакладнаяСАТУРН.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка ИЗ ОформленныеНакладные КАК Т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Ссылка.ОрганизацияСАТУРН КАК ОрганизацияСАТУРН,
	|	ОформленныеТовары.Ссылка.МестоХранения     КАК МестоХранения,
	|	ОформленныеТовары.Номенклатура             КАК Номенклатура,
	|	ОформленныеТовары.Характеристика           КАК Характеристика,
	|	ОформленныеТовары.Серия                    КАК Серия,
	|	-ОформленныеТовары.Количество              КАК Количество
	|ИЗ
	|	Документ.ПроизводственнаяОперацияСАТУРН.Сырье КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка ИЗ ОформленныеПроизводства КАК Т)
	|;"
	
	+ ?(ДетализацияДоСклада, "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КлассификаторОрганизацийСАТУРНМестаХранения.ТорговыйОбъект         КАК Склад,
	|	КлассификаторОрганизацийСАТУРНМестаХранения.ПроизводственныйОбъект КАК Подразделение,
	|	ОформленныеТовары.Номенклатура                                     КАК Номенклатура,
	|	ОформленныеТовары.Характеристика                                   КАК Характеристика,
	|	ОформленныеТовары.Серия                                            КАК Серия,
	|	ОформленныеТовары.Количество                                       КАК Количество
	|ПОМЕСТИТЬ ОформленныеТовары
	|ИЗ
	|	ОформленныеТоварыПоДокументамСАТУРН КАК ОформленныеТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийСАТУРН.МестаХранения КАК
	|			КлассификаторОрганизацийСАТУРНМестаХранения
	|		ПО ОформленныеТовары.ОрганизацияСАТУРН = КлассификаторОрганизацийСАТУРНМестаХранения.Ссылка
	|		И ОформленныеТовары.МестоХранения = КлассификаторОрганизацийСАТУРНМестаХранения.МестоХранения
	|;",
	
	"////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Неопределено                                               КАК Склад,
	|	Неопределено                                               КАК Подразделение,
	|	ОформленныеТовары.Номенклатура                             КАК Номенклатура,
	|	ОформленныеТовары.Характеристика                           КАК Характеристика,
	|	ОформленныеТовары.Серия                                    КАК Серия,
	|	ОформленныеТовары.Количество                               КАК Количество
	|ПОМЕСТИТЬ ОформленныеТовары
	|ИЗ
	|	ОформленныеТоварыПоДокументамСАТУРН КАК ОформленныеТовары
	|;")
	
	+ "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&СтрокаСкладТоваровСерии         КАК Склад,
	|	&СтрокаПодразделениеТоваровСерии КАК Подразделение,
	|	ТоварыСерии.Номенклатура         КАК Номенклатура,
	|	ТоварыСерии.Характеристика       КАК Характеристика,
	|	ТоварыСерии.Серия                КАК Серия,
	|	ТоварыСерии.Количество           КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыСерии КАК ТоварыСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Склад          КАК Склад,
	|	ОформленныеТовары.Подразделение  КАК Подразделение,
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	ОформленныеТовары.Количество     КАК Количество
	|ИЗ
	|	ОформленныеТовары КАК ОформленныеТовары
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыСАТУРН.ПАТ,СоответствиеНоменклатурыСАТУРН2.ПАТ)) КАК ПАТ,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленныеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыСАТУРН КАК СоответствиеНоменклатурыСАТУРН
	|		ПО СоответствиеНоменклатурыСАТУРН.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыСАТУРН.Характеристика = ТабличнаяЧасть.Характеристика
	|			И СоответствиеНоменклатурыСАТУРН.Серия = ТабличнаяЧасть.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыСАТУРН КАК СоответствиеНоменклатурыСАТУРН2
	|		ПО СоответствиеНоменклатурыСАТУРН2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыСАТУРН2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыСАТУРН2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыСАТУРН.ПАТ,СоответствиеНоменклатурыСАТУРН2.ПАТ)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Склад КАК Склад,
	|	ТоварыКОформлению.Подразделение КАК Подразделение,
	|	ТоварыКОформлению.Номенклатура КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия КАК Серия,
	|	СопоставленныеПозиции.ПАТ КАК ПАТ,
	|	СУММА(ТоварыКОформлению.Количество) КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество) КАК КоличествоУпаковок
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленныеПозиции КАК СопоставленныеПозиции
	|		ПО СопоставленныеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленныеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленныеПозиции.Серия = ТоварыКОформлению.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Подразделение,
	|	ТоварыКОформлению.Склад,
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленныеПозиции.ПАТ
	|ИМЕЮЩИЕ СУММА(ТоварыКОформлению.Количество) > 0
	|";
	
	Если Не ДетализацияДоСклада Тогда
		
		МассивУдаляемыхСтрок = Новый Массив;
		МассивУдаляемыхСтрок.Добавить("ТоварыКОформлению.Склад,");
		МассивУдаляемыхСтрок.Добавить("ТоварыКОформлению.Склад КАК Склад,");
		МассивУдаляемыхСтрок.Добавить("ТоварыКОформлению.Подразделение,");
		МассивУдаляемыхСтрок.Добавить("ТоварыКОформлению.Подразделение КАК Подразделение,");
		
		Для Каждого УдаляемаяСтрока Из МассивУдаляемыхСтрок Цикл
			Текст = СтрЗаменить(Текст, УдаляемаяСтрока, "");
		КонецЦикла;
		
		Текст = СтрЗаменить(Текст, "&СтрокаСкладТоваровСерии", "Неопределено");
		Текст = СтрЗаменить(Текст, "&СтрокаПодразделениеТоваровСерии", "Неопределено");
		
	Иначе
		Текст = СтрЗаменить(Текст, "&СтрокаСкладТоваровСерии", "ТоварыСерии.Склад");
		Текст = СтрЗаменить(Текст, "&СтрокаПодразделениеТоваровСерии", "ТоварыСерии.Подразделение");
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

Процедура ЗаполнитьТабличнуюЧастьАктаПрименения(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, ДополнительныеПараметры = Неопределено)
	
	ДокументОбъект.Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаЗаполненияАктаПримененияИзПрикладногоДокумента(ТекстТоварыДокумента, ДополнительныеПараметры);
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("Документ",          ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.АктПримененияСАТУРН.КонечныеСтатусы());
	Запрос.УстановитьПараметр("КонечныеСтатусыНакладная",   Документы.НакладнаяСАТУРН.КонечныеСтатусы());
	Запрос.УстановитьПараметр("КонечныеСтатусыПроизводство",   Документы.ПроизводственнаяОперацияСАТУРН.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия",       Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Тип.ПривестиЗначение());
	Запрос.УстановитьПараметр("ОсобенностьУчета",  Перечисления.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН);
	
	ЗаполнитьТабличнуюЧастьДокумента(ДокументОбъект.Товары, Запрос.Выполнить(), ДанныеЗаполнения,, ДополнительныеПараметры);
	Документы.АктПримененияСАТУРН.ЗаполнитьПартии(ДокументОбъект);
	
КонецПроцедуры

Процедура ЗаполнитьШапкуАктаПрименения(ДокументОбъект, ДанныеЗаполнения, Запрос, ДополнительныеПараметры = Неопределено)
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	РеквизитыДокумента = Запрос.Выполнить().Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	ЗаполнитьСкладИзТабличнойЧасти = Ложь;
	Если ДополнительныеПараметры <> Неопределено
		И ДополнительныеПараметры.Свойство("ЗаполнитьСкладИзТабличнойЧасти") Тогда
		ЗаполнитьСкладИзТабличнойЧасти = ДополнительныеПараметры.ЗаполнитьСкладИзТабличнойЧасти;
		ДополнительныеПараметры.Вставить("Организация", РеквизитыДокумента.Организация);
	КонецЕсли;
	
	Если Не ЭтоПерезаполнение
			И Не ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		
		ИнтеграцияИСУТ.ПроверитьВозможностьВводаНаОсновании(РеквизитыДокумента);
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
		
		Если Не ЗаполнитьСкладИзТабличнойЧасти Тогда
			ЗаполнитьОрганизациюМестоХраненияСАТУРНПоРеквизитамДокумента(ДокументОбъект, РеквизитыДокумента);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьАктПримененияСАТУРННаОснованииРеализацииТоваровУслуг(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	ДокументСсылка.Дата          КАК ДатаПрименения,
	|	ДокументСсылка.Номер         КАК НомерАкта,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Менеджер      КАК Ответственный,
	|	ДокументСсылка.Организация   КАК Организация,
	|	ДокументСсылка.Подразделение КАК Подразделение,
	|	ДокументСсылка.Склад         КАК Склад
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуАктаПрименения(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = ИнтеграцияИСУТ.ТекстЗапросаРеализацияТоваровУслуг() +"
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура   КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Серия          КАК Серия,
	|	СУММА(ТаблицаТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	ТаблицаТоварыРеализацияТоваровУслуг КАК ТаблицаТовары
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия,
	|	СУММА(ТаблицаТовары.Количество)
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Расхождения КАК ТаблицаТовары
	|		ПО ТаблицаТовары.Ссылка = КорректировкаРеализации.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ВЫРАЗИТЬ(КорректировкаРеализации.ДокументОснование КАК Документ.РеализацияТоваровУслуг) = &ДокументОснование
	|	И КорректировкаРеализации.Проведен
	|	И СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН)
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия
	|;
	|";
	ЗаполнитьТабличнуюЧастьАктаПрименения(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	
КонецПроцедуры

Процедура ЗаполнитьАктПримененияСАТУРННаОснованииВнутреннегоПотребления(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	ДокументСсылка.Дата          КАК ДатаПрименения,
	|	ДокументСсылка.Номер         КАК НомерАкта,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный КАК Ответственный,
	|	ДокументСсылка.Организация   КАК Организация,
	|	ДокументСсылка.Подразделение КАК Подразделение,
	|	ДокументСсылка.Склад         КАК Склад
	|ИЗ
	|	Документ.ВнутреннееПотребление КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуАктаПрименения(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = ИнтеграцияИСУТ.ТекстЗапросаВнутреннееПотребление() +"
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура   КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Серия          КАК Серия,
	|	СУММА(ТаблицаТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	ТаблицаТоварыВнутреннееПотребление КАК ТаблицаТовары
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия
	|;
	|";
	ЗаполнитьТабличнуюЧастьАктаПрименения(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	
КонецПроцедуры



#КонецОбласти

#Область НакладнаяСАТУРН

Процедура ОбработкаЗаполненияДокументаНакладнаяСАТУРН(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если ДокументОбъект.ВходящаяНакладная Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	ТипОснования = ТипЗнч(ДанныеЗаполнения);
	Если ТипОснования = Тип("Структура") Тогда
		ТипОснования = ТипЗнч(ДанныеЗаполнения.Основание);
	КонецЕсли;
	
	Если ТипОснования = Тип("ДокументСсылка.РеализацияТоваровУслуг")Тогда
		ЗаполнитьНакладнуюСАТУРННаОснованииРеализацииТоваровУслуг(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ВозвратТоваровПоставщику")Тогда
		ЗаполнитьНакладнуюСАТУРННаОснованииВозвратаТоваровПоставщику(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")Тогда
		ЗаполнитьНакладнуюСАТУРННаОснованииПередачиТоваровМеждуОрганизациями(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями")Тогда
		ЗаполнитьНакладнуюСАТУРННаОснованииВозвратаТоваровМеждуОрганизациями(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ОтгрузкаТоваровСХранения")Тогда
		ЗаполнитьНакладнуюСАТУРННаОснованииОтгрузкиТоваровСХранения(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ОтчетОРозничныхПродажах")Тогда
		ЗаполнитьНакладнуюСАТУРННаОснованииОтчетаОРозничныхПродажах(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПеремещениеТоваров")Тогда
		ЗаполнитьНакладнуюСАТУРННаОснованииПеремещенияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ВнутреннееПотребление")Тогда
		ЗаполнитьНакладнуюСАТУРННаОснованииВнутреннегоПотребления(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПередачаТоваровХранителю")Тогда
		ЗаполнитьНакладнуюСАТУРННаОснованииПередачиТоваровХранителю(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПоступлениеТоваровОтХранителя")Тогда
		ЗаполнитьНакладнуюСАТУРННаОснованииПоступленияТоваровОтХранителя(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ВыкупТоваровХранителем")Тогда
		ЗаполнитьНакладнуюСАТУРННаОснованииВыкупаТоваровХранителем(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПоступлениеТоваровНаСклад")Тогда
		ЗаполнитьНакладнаяСАТУРННаОснованииПоступлениеТоваровНаСклад(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.КорректировкаПриобретения")Тогда
		ЗаполнитьНакладнаяСАТУРННаОснованииКорректировкаПриобретения(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")Тогда
		ЗаполнитьНакладнаяСАТУРННаОснованииПриобретениеТоваровУслуг(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ВозвратТоваровОтКлиента")Тогда
		ЗаполнитьНакладнаяСАТУРННаОснованииВозвратТоваровОтКлиента(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТранспортныеРеквизитыНакладной(ДокументОбъект, ДокументОснование, ТаблицаСопоставлений)
	
	ТранспортныеНакладные = Документы.ТранспортнаяНакладная.ТранспортныеНакладныеДокументовОснований(ДокументОснование);
	Если ТранспортныеНакладные.Количество() <> 1 Тогда
		Возврат;
	КонецЕсли;
	
	ТранспортнаяНакладная = ТранспортныеНакладные[0].ТранспортнаяНакладная;
	ДанныеПеревозки = ИнтеграцияИСУТ.ДанныеПеревозки(ДокументОснование, ТранспортнаяНакладная);
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ОрганизацияКонтрагент", ДанныеПеревозки.Перевозчик);
	СопоставленныеСтроки = ТаблицаСопоставлений.НайтиСтроки(ПараметрыОтбора);
	
	Если СопоставленныеСтроки.Количество() = 0 Тогда
		ТаблицаСопоставлений = ИнтеграцияСАТУРН.НоваяТаблицаОрганизацияКонтрагент();
		СтрокаСопоставленияПеревозчик = ИнтеграцияСАТУРН.ДобавитьВТаблицуОтбораСопоставлений(
			ТаблицаСопоставлений,
			ДанныеПеревозки.Перевозчик,
			Справочники.Партнеры.ПустаяСсылка());
		Справочники.КлассификаторОрганизацийСАТУРН.ЗаполнитьТаблицуСопоставленийКлассификаторамиСАТУРН(ТаблицаСопоставлений);
		ДокументОбъект.Перевозчик = СтрокаСопоставленияПеревозчик.ОрганизацияСАТУРН;
	Иначе
		Для Каждого СтрокаСопоставленияПеревозчик Из СопоставленныеСтроки Цикл
			Если ЗначениеЗаполнено(СтрокаСопоставленияПеревозчик.ОрганизацияСАТУРН) Тогда
				ДокументОбъект.Перевозчик = СтрокаСопоставленияПеревозчик.ОрганизацияСАТУРН;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ДокументОбъект.НомерТранспортногоСредства = ДанныеПеревозки.АвтомобильГосударственныйНомер;
	ДокументОбъект.ТранспортноеСредство = ДанныеПеревозки.ТранспортноеСредство;
	
КонецПроцедуры

Процедура ЗаполнитьШапкуНакладной(ДокументОбъект, ДанныеЗаполнения, Запрос)
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	Если ЭтоПерезаполнение Или ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоОформлениеПередачиМеждуОрганизациями = Ложь;
	ДокументОснование = ДанныеЗаполнения;
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ДокументОснование = ДанныеЗаполнения.Основание;
		ЭтоОформлениеПередачиМеждуОрганизациями = ДанныеЗаполнения.Свойство("Товары");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	РеквизитыДокумента = Запрос.Выполнить().Выбрать();
	РеквизитыДокумента.Следующий();
	
	Если Не ЭтоОформлениеПередачиМеждуОрганизациями Тогда
		ИнтеграцияИСУТ.ПроверитьВозможностьВводаНаОсновании(РеквизитыДокумента);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
	
	ТаблицаСопоставлений = ИнтеграцияСАТУРН.НоваяТаблицаОрганизацияКонтрагент();
	
	СтрокаСопоставленияОрганизация = ИнтеграцияСАТУРН.ДобавитьВТаблицуОтбораСопоставлений(
		ТаблицаСопоставлений,
		РеквизитыДокумента.Организация,
		РеквизитыДокумента.ГрузоотправительСклад,
		РеквизитыДокумента.Подразделение);
	СтрокаСопоставленияКонтрагент = ИнтеграцияСАТУРН.ДобавитьВТаблицуОтбораСопоставлений(
		ТаблицаСопоставлений,
		РеквизитыДокумента.Контрагент,
		?(ЗначениеЗаполнено(РеквизитыДокумента.ГрузополучательСклад), РеквизитыДокумента.ГрузополучательСклад, РеквизитыДокумента.Партнер),
		РеквизитыДокумента.ГрузополучательПодразделение);
		
	Если ЗначениеЗаполнено(РеквизитыДокумента.ГрузоотправительКонтрагент) Тогда
		СтрокаСопоставленияГрузоотправитель = ИнтеграцияСАТУРН.ДобавитьВТаблицуОтбораСопоставлений(
			ТаблицаСопоставлений,
			РеквизитыДокумента.ГрузоотправительКонтрагент,
			?(ЗначениеЗаполнено(РеквизитыДокумента.ГрузоотправительСклад), РеквизитыДокумента.ГрузоотправительСклад, РеквизитыДокумента.ГрузоотправительПартнер),
			РеквизитыДокумента.Подразделение);
	КонецЕсли;
	Если ЗначениеЗаполнено(РеквизитыДокумента.ГрузополучательКонтрагент) Тогда
		СтрокаСопоставленияГрузополучатель = ИнтеграцияСАТУРН.ДобавитьВТаблицуОтбораСопоставлений(
			ТаблицаСопоставлений,
			РеквизитыДокумента.ГрузополучательКонтрагент,
			?(ЗначениеЗаполнено(РеквизитыДокумента.ГрузополучательСклад), РеквизитыДокумента.ГрузополучательСклад, РеквизитыДокумента.ГрузополучательПартнер),
			РеквизитыДокумента.ГрузополучательПодразделение);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыДокумента.ПеревозчикПартнер) Тогда
		КонтрагентПартнера = ПартнерыИКонтрагенты.ПолучитьКонтрагентаПартнераПоУмолчанию(РеквизитыДокумента.ПеревозчикПартнер);
		Если ЗначениеЗаполнено(КонтрагентПартнера) Тогда
			СтрокаСопоставленияПеревозчик = ИнтеграцияСАТУРН.ДобавитьВТаблицуОтбораСопоставлений(
				ТаблицаСопоставлений,
				КонтрагентПартнера);
		КонецЕсли;
	КонецЕсли;
	
	Справочники.КлассификаторОрганизацийСАТУРН.ЗаполнитьТаблицуСопоставленийКлассификаторамиСАТУРН(ТаблицаСопоставлений);
	Если ЗначениеЗаполнено(СтрокаСопоставленияОрганизация.ОрганизацияСАТУРН) Тогда
		ДокументОбъект.ГрузоотправительОрганизацияСАТУРН = СтрокаСопоставленияОрганизация.ОрганизацияСАТУРН;
		Если ЗначениеЗаполнено(СтрокаСопоставленияОрганизация.МестоХраненияТорговыйОбъект) Тогда
			ДокументОбъект.ГрузоотправительМестоХранения = СтрокаСопоставленияОрганизация.МестоХраненияТорговыйОбъект;
		ИначеЕсли ЗначениеЗаполнено(СтрокаСопоставленияОрганизация.МестоХраненияПроизводственныйОбъект) Тогда
			ДокументОбъект.ГрузоотправительМестоХранения = СтрокаСопоставленияОрганизация.МестоХраненияПроизводственныйОбъект;
		КонецЕсли;
	КонецЕсли;
	Если СтрокаСопоставленияГрузоотправитель <> Неопределено Тогда
		Если ЗначениеЗаполнено(СтрокаСопоставленияГрузоотправитель.МестоХраненияТорговыйОбъект) Тогда
			ДокументОбъект.ГрузоотправительМестоХранения = СтрокаСопоставленияГрузоотправитель.МестоХраненияТорговыйОбъект;
		ИначеЕсли ЗначениеЗаполнено(СтрокаСопоставленияГрузоотправитель.МестоХраненияПроизводственныйОбъект) Тогда
			ДокументОбъект.ГрузоотправительМестоХранения = СтрокаСопоставленияГрузоотправитель.МестоХраненияПроизводственныйОбъект;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаСопоставленияКонтрагент.ОрганизацияСАТУРН) Тогда
		ДокументОбъект.ГрузополучательОрганизацияСАТУРН = СтрокаСопоставленияКонтрагент.ОрганизацияСАТУРН;
		ДокументОбъект.ГрузополучательМестоХранения     = СтрокаСопоставленияКонтрагент.МестоХраненияТорговыйОбъект;
	КонецЕсли;
	Если СтрокаСопоставленияГрузополучатель <> Неопределено
		И ЗначениеЗаполнено(СтрокаСопоставленияГрузополучатель.ОрганизацияСАТУРН) Тогда
		Если ЗначениеЗаполнено(СтрокаСопоставленияГрузополучатель.МестоХраненияТорговыйОбъект) Тогда
			ДокументОбъект.ГрузополучательМестоХранения = СтрокаСопоставленияГрузополучатель.МестоХраненияТорговыйОбъект;
		ИначеЕсли ЗначениеЗаполнено(СтрокаСопоставленияГрузополучатель.МестоХраненияПроизводственныйОбъект) Тогда
			ДокументОбъект.ГрузополучательМестоХранения = СтрокаСопоставленияГрузополучатель.МестоХраненияПроизводственныйОбъект;
		КонецЕсли;
	КонецЕсли;
	
	Если СтрокаСопоставленияПеревозчик <>Неопределено
		И ЗначениеЗаполнено(СтрокаСопоставленияПеревозчик.ОрганизацияСАТУРН) Тогда
		ДокументОбъект.Перевозчик = СтрокаСопоставленияПеревозчик.ОрганизацияСАТУРН;
	КонецЕсли;
	
	Если ДокументОбъект.Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспорт
		Или (ДокументОбъект.Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПеревозка
			И (ДокументОбъект.ГрузоотправительМестоХранения <> ДокументОбъект.ГрузополучательМестоХранения
				Или Не ЗначениеЗаполнено(ДокументОбъект.ГрузоотправительМестоХранения))) Тогда
		ЗаполнитьТранспортныеРеквизитыНакладной(ДокументОбъект, РеквизитыДокумента.ДокументОснование, ТаблицаСопоставлений);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыДокумента.ГосударственныйКонтракт) Тогда
		СоответствиеРеквизитовГосКонтракта = ИнтеграцияИСУТ.ДанныеГосударственногоКонтракта(РеквизитыДокумента.ГосударственныйКонтракт);
		ДанныеГосКонтракта = СоответствиеРеквизитовГосКонтракта[РеквизитыДокумента.ГосударственныйКонтракт];
		Если ДанныеГосКонтракта = Неопределено Тогда
			ДокументОбъект.ИдентификаторГосКонтракта  = "";
		Иначе
			ДокументОбъект.ИдентификаторГосКонтракта = СтрШаблон(
				"%1 от %2",
				ДанныеГосКонтракта.НомерГосКонтракта,
				ДанныеГосКонтракта.ДатаГосКонтракта);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьНакладной(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента)
	
	ДокументОбъект.Товары.Очистить();
	
	Запрос = Новый Запрос;
	Если ТипЗнч(ТекстТоварыДокумента) = Тип("ТаблицаЗначений") Тогда
		Запрос.Текст = ТекстЗапросаТоварыНакладнойИзТаблицы();
		Запрос.УстановитьПараметр("Таблица", ТекстТоварыДокумента);
	Иначе
		Запрос.Текст = ТекстЗапросаЗаполненияНакладнойИзПрикладногоДокумента(ТекстТоварыДокумента);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("Документ",          ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.НакладнаяСАТУРН.КонечныеСтатусы());
	Запрос.УстановитьПараметр("КонечныеСтатусыАкты", Документы.АктПримененияСАТУРН.КонечныеСтатусы());
	Запрос.УстановитьПараметр("КонечныеСтатусыПроизводство", Документы.ПроизводственнаяОперацияСАТУРН.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия",       Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Тип.ПривестиЗначение());
	Запрос.УстановитьПараметр("ОсобенностьУчета",  Перечисления.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН);
	
	ЗаполнитьТабличнуюЧастьДокумента(ДокументОбъект.Товары, Запрос.Выполнить(), ДанныеЗаполнения);
	
	Документы.НакладнаяСАТУРН.ЗаполнитьПартии(ДокументОбъект);
	
КонецПроцедуры

Функция ТекстЗапросаТоварыНакладнойИзТаблицы()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Таблица.Номенклатура     КАК Номенклатура,
	|	Таблица.Характеристика   КАК Характеристика,
	|	Таблица.Серия            КАК Серия,
	|	Таблица.Количество       КАК Количество,
	|	Таблица.КоличествоСАТУРН КАК КоличествоСАТУРН,
	|	Таблица.ПартияСАТУРН     КАК Партия
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	&Таблица КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура     КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика   КАК Характеристика,
	|	ТоварыКОформлению.Серия            КАК Серия,
	|	ТоварыКОформлению.Количество       КАК Количество,
	|	ТоварыКОформлению.Количество       КАК КоличествоУпаковок,
	|	ТоварыКОформлению.КоличествоСАТУРН КАК КоличествоСАТУРН,
	|	ТоварыКОформлению.Количество / ТоварыКОформлению.КоличествоСАТУРН КАК КоличествоВУпаковкеСАТУРН,
	|	ПартииСАТУРН.Ссылка                       КАК Партия,
	|	ПартииСАТУРН.ПАТ                          КАК ПАТ,
	|	ПартииСАТУРН.ТипИзмеряемойВеличиныСАТУРН  КАК ТипИзмеряемойВеличиныСАТУРН
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииСАТУРН КАК ПартииСАТУРН
	|		ПО ПартииСАТУРН.Ссылка = ТоварыКОформлению.Партия
	|ГДЕ
	|	ТоварыКОформлению.Количество > 0
	|	И ТоварыКОформлению.КоличествоСАТУРН > 0
	|";
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаЗаполненияНакладнойИзПрикладногоДокумента(ТекстТоварыДокумента)
	Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.НакладнаяСАТУРН КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК СтатусыДокументовСАТУРН
	|		ПО СтатусыДокументовСАТУРН.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовСАТУРН.Статус НЕ В(&КонечныеСтатусы)
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеАкты
	|ИЗ
	|	Документ.АктПримененияСАТУРН КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК СтатусыДокументовСАТУРН
	|		ПО СтатусыДокументовСАТУРН.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовСАТУРН.Статус НЕ В(&КонечныеСтатусыАкты)
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеПроизводства
	|ИЗ
	|	Документ.ПроизводственнаяОперацияСАТУРН КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК СтатусыДокументовСАТУРН
	|		ПО СтатусыДокументовСАТУРН.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовСАТУРН.Статус НЕ В(&КонечныеСтатусыПроизводство)
	|;
	|"
	+
	ТекстТоварыДокумента
	+
	"
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыСерии.Номенклатура         КАК Номенклатура,
	|	ТоварыСерии.Характеристика       КАК Характеристика,
	|	ТоварыСерии.Серия                КАК Серия,
	|	ТоварыСерии.Количество           КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыСерии КАК ТоварыСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	-ОформленныеТовары.Количество    КАК Количество
	|ИЗ
	|	Документ.НакладнаяСАТУРН.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка ИЗ ОформленныеДокументы КАК Т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	-ОформленныеТовары.Количество    КАК Количество
	|ИЗ
	|	Документ.АктПримененияСАТУРН.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка ИЗ ОформленныеАкты КАК Т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	-ОформленныеТовары.Количество    КАК Количество
	|ИЗ
	|	Документ.ПроизводственнаяОперацияСАТУРН.Сырье КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка ИЗ ОформленныеПроизводства КАК Т)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыСАТУРН.ПАТ,СоответствиеНоменклатурыСАТУРН2.ПАТ)) КАК ПАТ,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленныеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыСАТУРН КАК СоответствиеНоменклатурыСАТУРН
	|		ПО СоответствиеНоменклатурыСАТУРН.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыСАТУРН.Характеристика = ТабличнаяЧасть.Характеристика
	|			И СоответствиеНоменклатурыСАТУРН.Серия = ТабличнаяЧасть.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыСАТУРН КАК СоответствиеНоменклатурыСАТУРН2
	|		ПО СоответствиеНоменклатурыСАТУРН2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыСАТУРН2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыСАТУРН2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыСАТУРН.ПАТ,СоответствиеНоменклатурыСАТУРН2.ПАТ)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура                    КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика                  КАК Характеристика,
	|	ТоварыКОформлению.Серия                           КАК Серия,
	|	СопоставленныеПозиции.ПАТ                         КАК ПАТ,
	|	СУММА(ТоварыКОформлению.Количество)               КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество)               КАК КоличествоУпаковок
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленныеПозиции КАК СопоставленныеПозиции
	|		ПО СопоставленныеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленныеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленныеПозиции.Серия = ТоварыКОформлению.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленныеПозиции.ПАТ
	|ИМЕЮЩИЕ СУММА(ТоварыКОформлению.Количество)>0
	|";
	
	Возврат Текст;
	
КонецФункции

Процедура ЗаполнитьНакладнаяСАТУРННаОснованииПоступлениеТоваровНаСклад(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка                          КАК ДокументОснование,
	|	ДокументСсылка.Дата                            КАК ДатаПолучения,
	|	ДокументСсылка.Дата                            КАК ДатаРегистрации,
	|	НЕ ДокументСсылка.Проведен                     КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Автор                           КАК Ответственный,
	|	ДокументСсылка.Контрагент                      КАК Контрагент,
	|	ДокументСсылка.Организация                     КАК Организация,
	|	ДокументСсылка.Подразделение                   КАК Подразделение,
	|	ДокументСсылка.Склад                           КАК ГрузоотправительСклад,
	|	Неопределено                                   КАК ГрузоотправительКонтрагент,
	|	ДокументСсылка.Партнер                         КАК ГрузоотправительПартнер,
	|	ДокументСсылка.Склад                           КАК ГрузополучательСклад,
	|	Неопределено                                   КАК ГрузополучательКонтрагент,
	|	Неопределено                                   КАК ГрузополучательПартнер,
	|	ДокументСсылка.Подразделение                   КАК ГрузополучательПодразделение,
	|	Неопределено                                   КАК ПеревозчикПартнер,
	|	ДокументСсылка.Договор.ГосударственныйКонтракт КАК ГосударственныйКонтракт,
	|	""Поступление товаров на склад""               КАК НаименованиеПервичногоДокумента,
	|	ВЫБОР
	|		КОГДА ДокументСсылка.Контрагент.ЮрФизлицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяРозничнаяТорговля)
	|		КОГДА ДокументСсылка.Контрагент.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			И ДокументСсылка.Контрагент.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяЭкспорт)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяПеревозка)
	|	КОНЕЦ КАК Операция
	|ИЗ
	|	Документ.ПоступлениеТоваровНаСклад КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуНакладной(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура      КАК Номенклатура,
	|	Товары.Характеристика    КАК Характеристика,
	|	Товары.Серия             КАК Серия,
	|	СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.ПоступлениеТоваровНаСклад.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН)
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьНакладной(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	
КонецПроцедуры

Процедура ЗаполнитьНакладнаяСАТУРННаОснованииПриобретениеТоваровУслуг(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка                          КАК ДокументОснование,
	|	ДокументСсылка.Дата                            КАК ДатаПолучения,
	|	ДокументСсылка.Дата                            КАК ДатаРегистрации,
	|	НЕ ДокументСсылка.Проведен                     КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Автор                           КАК Ответственный,
	|	ДокументСсылка.Контрагент                      КАК Контрагент,
	|	ДокументСсылка.Организация                     КАК Организация,
	|	ДокументСсылка.Подразделение                   КАК Подразделение,
	|	ДокументСсылка.Склад                           КАК ГрузоотправительСклад,
	|	ДокументСсылка.Грузоотправитель                КАК ГрузоотправительКонтрагент,
	|	ДокументСсылка.Партнер                         КАК ГрузоотправительПартнер,
	|	ДокументСсылка.Склад                           КАК ГрузополучательСклад,
	|	Неопределено                                   КАК ГрузополучательКонтрагент,
	|	Неопределено                                   КАК ГрузополучательПартнер,
	|	ДокументСсылка.Подразделение                   КАК ГрузополучательПодразделение,
	|	ДокументСсылка.Договор.ГосударственныйКонтракт КАК ГосударственныйКонтракт,
	|	ДокументСсылка.ПеревозчикПартнер               КАК ПеревозчикПартнер,
	|	""Приобретение товаров услуг""                 КАК НаименованиеПервичногоДокумента,
	|	ВЫБОР
	|		КОГДА ДокументСсылка.Контрагент.ЮрФизлицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяРозничнаяТорговля)
	|		КОГДА ДокументСсылка.Контрагент.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			И ДокументСсылка.Контрагент.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяЭкспорт)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяПеревозка)
	|	КОНЕЦ КАК Операция
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуНакладной(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура      КАК Номенклатура,
	|	Товары.Характеристика    КАК Характеристика,
	|	Товары.Серия             КАК Серия,
	|	СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН)
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьНакладной(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	
КонецПроцедуры

Процедура ЗаполнитьНакладнаяСАТУРННаОснованииКорректировкаПриобретения(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка                          КАК ДокументОснование,
	|	ДокументСсылка.Дата                            КАК ДатаПолучения,
	|	ДокументСсылка.Дата                            КАК ДатаРегистрации,
	|	ДокументСсылка.Номер                           КАК НомерПервичногоДокумента,
	|	НЕ ДокументСсылка.Проведен                     КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Автор                           КАК Ответственный,
	|	ДокументСсылка.Контрагент                      КАК Контрагент,
	|	ДокументСсылка.Организация                     КАК Организация,
	|	ДокументСсылка.Подразделение                   КАК Подразделение,
	|	ДокументСсылка.Склад                           КАК ГрузоотправительСклад,
	|	ДокументСсылка.Контрагент                      КАК ГрузоотправительКонтрагент,
	|	ДокументСсылка.Партнер                         КАК ГрузоотправительПартнер,
	|	ДокументСсылка.Склад                           КАК ГрузополучательСклад,
	|	Неопределено                                   КАК ГрузополучательКонтрагент,
	|	Неопределено                                   КАК ГрузополучательПартнер,
	|	ДокументСсылка.Подразделение                   КАК ГрузополучательПодразделение,
	|	ДокументСсылка.Договор.ГосударственныйКонтракт КАК ГосударственныйКонтракт,
	|	Неопределено                                   КАК ПеревозчикПартнер,
	|	""Корректировка приобретения""                 КАК НаименованиеПервичногоДокумента,
	|	ВЫБОР
	|		КОГДА ДокументСсылка.Контрагент.ЮрФизлицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяРозничнаяТорговля)
	|		КОГДА ДокументСсылка.Контрагент.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			И ДокументСсылка.Контрагент.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяЭкспорт)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяПеревозка)
	|	КОНЕЦ КАК Операция
	|ИЗ
	|	Документ.КорректировкаПриобретения КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуНакладной(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура      КАК Номенклатура,
	|	Товары.Характеристика    КАК Характеристика,
	|	Товары.Серия             КАК Серия,
	|	СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.КорректировкаПриобретения.Расхождения КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И Товары.Количество > 0
	|	И СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН)
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьНакладной(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	
КонецПроцедуры

Процедура ЗаполнитьНакладнаяСАТУРННаОснованииВозвратТоваровОтКлиента(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка                          КАК ДокументОснование,
	|	ДокументСсылка.Дата                            КАК ДатаПолучения,
	|	ДокументСсылка.Дата                            КАК ДатаРегистрации,
	|	ДокументСсылка.Номер                           КАК НомерПервичногоДокумента,
	|	НЕ ДокументСсылка.Проведен                     КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Автор                           КАК Ответственный,
	|	ДокументСсылка.Контрагент                      КАК Контрагент,
	|	ДокументСсылка.Организация                     КАК Организация,
	|	ДокументСсылка.Подразделение                   КАК Подразделение,
	|	ДокументСсылка.Склад                           КАК ГрузоотправительСклад,
	|	ДокументСсылка.Контрагент                      КАК ГрузоотправительКонтрагент,
	|	ДокументСсылка.Партнер                         КАК ГрузоотправительПартнер,
	|	ДокументСсылка.Склад                           КАК ГрузополучательСклад,
	|	Неопределено                                   КАК ГрузополучательКонтрагент,
	|	Неопределено                                   КАК ГрузополучательПартнер,
	|	ДокументСсылка.Подразделение                   КАК ГрузополучательПодразделение,
	|	Неопределено                                   КАК ПеревозчикПартнер,
	|	ДокументСсылка.Договор.ГосударственныйКонтракт КАК ГосударственныйКонтракт,
	|	""Возврат товаров от клиента""                 КАК НаименованиеПервичногоДокумента,
	|	ВЫБОР
	|		КОГДА ДокументСсылка.Контрагент.ЮрФизлицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяРозничнаяТорговля)
	|		КОГДА ДокументСсылка.Контрагент.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			И ДокументСсылка.Контрагент.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяЭкспорт)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяПеревозка)
	|	КОНЕЦ КАК Операция
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуНакладной(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура      КАК Номенклатура,
	|	Товары.Характеристика    КАК Характеристика,
	|	Товары.Серия             КАК Серия,
	|	СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И Товары.Количество > 0
	|	И СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН)
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьНакладной(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	
КонецПроцедуры

Процедура ЗаполнитьНакладнуюСАТУРННаОснованииРеализацииТоваровУслуг(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка           КАК ДокументОснование,
	|	ДокументСсылка.Дата             КАК ДатаПрименения,
	|	ДокументСсылка.Номер            КАК НомерПервичногоДокумента,
	|	ДокументСсылка.Дата             КАК ДатаРегистрации,
	|	""Реализация товаров и услуг""  КАК НаименованиеПервичногоДокумента,
	|	НЕ ДокументСсылка.Проведен      КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Менеджер         КАК Ответственный,
	|	ДокументСсылка.Организация      КАК Организация,
	|	ДокументСсылка.Подразделение    КАК Подразделение,
	|	ДокументСсылка.Контрагент       КАК Контрагент,
	|	ДокументСсылка.Партнер          КАК Партнер,
	|	ДокументСсылка.Склад            КАК ГрузоотправительСклад,
	|	ДокументСсылка.Грузоотправитель КАК ГрузоотправительКонтрагент,
	|	Неопределено                    КАК ГрузоотправительПартнер,
	|	Неопределено                    КАК ГрузополучательСклад,
	|	ДокументСсылка.Грузополучатель  КАК ГрузополучательКонтрагент,
	|	ДокументСсылка.Партнер          КАК ГрузополучательПартнер,
	|	Неопределено                    КАК ГрузополучательПодразделение,
	|	ДокументСсылка.ПеревозчикПартнер КАК ПеревозчикПартнер,
	|	ДокументСсылка.Договор.ГосударственныйКонтракт КАК ГосударственныйКонтракт,
	|	ВЫБОР
	|		КОГДА ДокументСсылка.Контрагент.ЮрФизлицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяРозничнаяТорговля)
	|		КОГДА ДокументСсылка.Контрагент.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			И ДокументСсылка.Контрагент.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяЭкспортЧерезПунктДосмотра)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяПеревозка)
	|	КОНЕЦ КАК Операция
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуНакладной(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = ИнтеграцияИСУТ.ТекстЗапросаРеализацияТоваровУслуг() + "
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ Корректировки
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|ГДЕ
	|	ВЫРАЗИТЬ(КорректировкаРеализации.ДокументОснование КАК Документ.РеализацияТоваровУслуг) = &ДокументОснование
	|	И КорректировкаРеализации.Проведен
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|	ТаблицаТовары.Характеристика    КАК Характеристика,
	|	ТаблицаТовары.Серия             КАК Серия,
	|	СУММА(ТаблицаТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	ТаблицаТоварыРеализацияТоваровУслуг КАК ТаблицаТовары
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия,
	|	СУММА(ТаблицаТовары.Количество)
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТаблицаТовары.Ссылка В (ВЫБРАТЬ Ссылка ИЗ Корректировки)
	|	И СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияСАТУРН)
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия
	|;
	|";
	ЗаполнитьТабличнуюЧастьНакладной(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	
КонецПроцедуры

Процедура ЗаполнитьНакладнуюСАТУРННаОснованииВозвратаТоваровПоставщику(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка          КАК ДокументОснование,
	|	ДокументСсылка.Дата            КАК ДатаПрименения,
	|	ДокументСсылка.Номер           КАК НомерПервичногоДокумента,
	|	ДокументСсылка.Дата            КАК ДатаРегистрации,
	|	""Возврат товаров поставщику"" КАК НаименованиеПервичногоДокумента,
	|	НЕ ДокументСсылка.Проведен     КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Менеджер        КАК Ответственный,
	|	ДокументСсылка.Организация     КАК Организация,
	|	ДокументСсылка.Подразделение   КАК Подразделение,
	|	ДокументСсылка.Контрагент      КАК Контрагент,
	|	ДокументСсылка.Партнер         КАК Партнер,
	|	ДокументСсылка.Склад           КАК ГрузоотправительСклад,
	|	Неопределено                   КАК ГрузоотправительКонтрагент,
	|	Неопределено                   КАК ГрузоотправительПартнер,
	|	Неопределено                   КАК ГрузополучательСклад,
	|	Неопределено                   КАК ГрузополучательКонтрагент,
	|	ДокументСсылка.Партнер         КАК ГрузополучательПартнер,
	|	Неопределено                   КАК ГрузополучательПодразделение,
	|	ДокументСсылка.ПеревозчикПартнер КАК ПеревозчикПартнер,
	|	ДокументСсылка.Договор.ГосударственныйКонтракт КАК ГосударственныйКонтракт,
	|	ВЫБОР
	|		КОГДА ДокументСсылка.Контрагент.ЮрФизлицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяРозничнаяТорговля)
	|		КОГДА ДокументСсылка.Контрагент.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			И ДокументСсылка.Контрагент.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяЭкспорт)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяПеревозка)
	|	КОНЕЦ КАК Операция
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуНакладной(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = ИнтеграцияИСУТ.ТекстЗапросаВозвратТоваровПоставщику() + "
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|	ТаблицаТовары.Характеристика    КАК Характеристика,
	|	ТаблицаТовары.Серия             КАК Серия,
	|	СУММА(ТаблицаТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	ТаблицаТоварыВозвратТоваровПоставщику КАК ТаблицаТовары
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия
	|;
	|";
	ЗаполнитьТабличнуюЧастьНакладной(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	
КонецПроцедуры

Процедура ЗаполнитьНакладнуюСАТУРННаОснованииОтчетаОРозничныхПродажах(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка          КАК ДокументОснование,
	|	ДокументСсылка.Дата            КАК ДатаПрименения,
	|	ДокументСсылка.Номер           КАК НомерПервичногоДокумента,
	|	ДокументСсылка.Дата            КАК ДатаРегистрации,
	|	""Отчет о розничных продажах"" КАК НаименованиеПервичногоДокумента,
	|	НЕ ДокументСсылка.Проведен     КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный   КАК Ответственный,
	|	ДокументСсылка.Организация     КАК Организация,
	|	ДокументСсылка.Подразделение   КАК Подразделение,
	|	Неопределено                   КАК Контрагент,
	|	Неопределено                   КАК Партнер,
	|	ДокументСсылка.Склад           КАК ГрузоотправительСклад,
	|	Неопределено                   КАК ГрузоотправительКонтрагент,
	|	Неопределено                   КАК ГрузоотправительПартнер,
	|	Неопределено                   КАК ГрузополучательСклад,
	|	Неопределено                   КАК ГрузополучательКонтрагент,
	|	Неопределено                   КАК ГрузополучательПартнер,
	|	Неопределено                   КАК ГрузополучательПодразделение,
	|	Неопределено                   КАК ПеревозчикПартнер,
	|	Неопределено                   КАК ГосударственныйКонтракт,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяРозничнаяТорговля) КАК Операция
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуНакладной(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = ИнтеграцияИСУТ.ТекстЗапросаОтчетОРозничныхПродажах() + "
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|	ТаблицаТовары.Характеристика    КАК Характеристика,
	|	ТаблицаТовары.Серия             КАК Серия,
	|	СУММА(ТаблицаТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	ТаблицаТоварыОтчетОРозничныхПродажах КАК ТаблицаТовары
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия
	|;
	|";
	ЗаполнитьТабличнуюЧастьНакладной(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	
КонецПроцедуры

Процедура ЗаполнитьНакладнуюСАТУРННаОснованииПередачиТоваровМеждуОрганизациями(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка          КАК ДокументОснование,
	|	ДокументСсылка.Дата            КАК ДатаПрименения,
	|	ДокументСсылка.Номер           КАК НомерПервичногоДокумента,
	|	ДокументСсылка.Дата            КАК ДатаРегистрации,
	|	""Передача между организациями"" КАК НаименованиеПервичногоДокумента,
	|	НЕ ДокументСсылка.Проведен     КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Менеджер        КАК Ответственный,
	|	ДокументСсылка.Организация     КАК Организация,
	|	ДокументСсылка.Подразделение   КАК Подразделение,
	|	ДокументСсылка.ОрганизацияПолучатель КАК Контрагент,
	|	Неопределено                   КАК Партнер,
	|	ДокументСсылка.Склад           КАК ГрузоотправительСклад,
	|	Неопределено                   КАК ГрузоотправительКонтрагент,
	|	Неопределено                   КАК ГрузоотправительПартнер,
	|	ДокументСсылка.Склад           КАК ГрузополучательСклад,
	|	Неопределено                   КАК ГрузополучательКонтрагент,
	|	Неопределено                   КАК ГрузополучательПартнер,
	|	ДокументСсылка.Подразделение   КАК ГрузополучательПодразделение,
	|	Неопределено                   КАК ПеревозчикПартнер,
	|	ДокументСсылка.Договор.ГосударственныйКонтракт КАК ГосударственныйКонтракт,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяПеревозка) КАК Операция
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуНакладной(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Товары") Тогда
		ЗаполнитьТабличнуюЧастьНакладной(ДокументОбъект, ДанныеЗаполнения, ДанныеЗаполнения.Товары);
	Иначе
		ТекстТоварыДокумента = ТекстТоварыДокументаБезСерий("ПередачаТоваровМеждуОрганизациями");
		ЗаполнитьТабличнуюЧастьНакладной(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьНакладнуюСАТУРННаОснованииВозвратаТоваровМеждуОрганизациями(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка          КАК ДокументОснование,
	|	ДокументСсылка.Дата            КАК ДатаПрименения,
	|	ДокументСсылка.Номер           КАК НомерПервичногоДокумента,
	|	ДокументСсылка.Дата            КАК ДатаРегистрации,
	|	""Возврат между организациями"" КАК НаименованиеПервичногоДокумента,
	|	НЕ ДокументСсылка.Проведен     КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Менеджер        КАК Ответственный,
	|	ДокументСсылка.Организация     КАК Организация,
	|	ДокументСсылка.Подразделение   КАК Подразделение,
	|	ДокументСсылка.ОрганизацияПолучатель КАК Контрагент,
	|	Неопределено                   КАК Партнер,
	|	ДокументСсылка.Склад           КАК ГрузоотправительСклад,
	|	Неопределено                   КАК ГрузоотправительКонтрагент,
	|	Неопределено                   КАК ГрузоотправительПартнер,
	|	ДокументСсылка.Склад           КАК ГрузополучательСклад,
	|	Неопределено                   КАК ГрузополучательКонтрагент,
	|	Неопределено                   КАК ГрузополучательПартнер,
	|	ДокументСсылка.Подразделение   КАК ГрузополучательПодразделение,
	|	Неопределено                   КАК ПеревозчикПартнер,
	|	ДокументСсылка.Договор.ГосударственныйКонтракт КАК ГосударственныйКонтракт,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяПеревозка) КАК Операция
	|ИЗ
	|	Документ.ВозвратТоваровМеждуОрганизациями КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуНакладной(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = ТекстТоварыДокументаБезСерий("ВозвратТоваровМеждуОрганизациями");
	ЗаполнитьТабличнуюЧастьНакладной(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	
КонецПроцедуры

Процедура ЗаполнитьНакладнуюСАТУРННаОснованииПеремещенияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка           КАК ДокументОснование,
	|	ДокументСсылка.Дата             КАК ДатаПрименения,
	|	ДокументСсылка.Номер            КАК НомерПервичногоДокумента,
	|	ДокументСсылка.Дата             КАК ДатаРегистрации,
	|	""Перемещение товаров""         КАК НаименованиеПервичногоДокумента,
	|	НЕ ДокументСсылка.Проведен      КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный    КАК Ответственный,
	|	ДокументСсылка.Организация      КАК Организация,
	|	ДокументСсылка.Подразделение    КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ДокументСсылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеТоваровМеждуФилиалами)
	|			ТОГДА ДокументСсылка.ОрганизацияПолучатель
	|		ИНАЧЕ ДокументСсылка.Организация
	|	КОНЕЦ                           КАК Контрагент,
	|	Неопределено                    КАК Партнер,
	|	ДокументСсылка.СкладОтправитель КАК ГрузоотправительСклад,
	|	Неопределено                    КАК ГрузоотправительКонтрагент,
	|	Неопределено                    КАК ГрузоотправительПартнер,
	|	ДокументСсылка.СкладПолучатель  КАК ГрузополучательСклад,
	|	Неопределено                    КАК ГрузополучательКонтрагент,
	|	Неопределено                    КАК ГрузополучательПартнер,
	|	ДокументСсылка.Подразделение    КАК ГрузополучательПодразделение,
	|	ДокументСсылка.ПеревозчикПартнер КАК ПеревозчикПартнер,
	|	Неопределено                     КАК ГосударственныйКонтракт,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяПеревозка) КАК Операция
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуНакладной(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = ИнтеграцияИСУТ.ТекстЗапросаПеремещениеТоваров() + "
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|	ТаблицаТовары.Характеристика    КАК Характеристика,
	|	ТаблицаТовары.Серия             КАК Серия,
	|	СУММА(ТаблицаТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	ТаблицаТоварыПеремещениеТоваров КАК ТаблицаТовары
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия
	|;
	|";
	ЗаполнитьТабличнуюЧастьНакладной(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	
КонецПроцедуры



Процедура ЗаполнитьНакладнуюСАТУРННаОснованииВнутреннегоПотребления(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка          КАК ДокументОснование,
	|	ДокументСсылка.Дата            КАК ДатаПрименения,
	|	ДокументСсылка.Номер           КАК НомерПервичногоДокумента,
	|	ДокументСсылка.Дата            КАК ДатаРегистрации,
	|	""Внутреннее потребление""     КАК НаименованиеПервичногоДокумента,
	|	НЕ ДокументСсылка.Проведен     КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный   КАК Ответственный,
	|	ДокументСсылка.Организация     КАК Организация,
	|	ДокументСсылка.Подразделение   КАК Подразделение,
	|	Неопределено                   КАК Контрагент,
	|	Неопределено                   КАК Партнер,
	|	ДокументСсылка.Склад           КАК ГрузоотправительСклад,
	|	Неопределено                   КАК ГрузоотправительКонтрагент,
	|	Неопределено                   КАК ГрузоотправительПартнер,
	|	Неопределено                   КАК ГрузополучательСклад,
	|	Неопределено                   КАК ГрузополучательКонтрагент,
	|	Неопределено                   КАК ГрузополучательПартнер,
	|	Неопределено                   КАК ГрузополучательПодразделение,
	|	Неопределено                   КАК ПеревозчикПартнер,
	|	Неопределено                   КАК ГосударственныйКонтракт,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяСписаниеВПроизводство) КАК Операция
	|ИЗ
	|	Документ.ВнутреннееПотребление КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуНакладной(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = ИнтеграцияИСУТ.ТекстЗапросаВнутреннееПотребление() + "
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|	ТаблицаТовары.Характеристика    КАК Характеристика,
	|	ТаблицаТовары.Серия             КАК Серия,
	|	СУММА(ТаблицаТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	ТаблицаТоварыВнутреннееПотребление КАК ТаблицаТовары
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия
	|;
	|";
	ЗаполнитьТабличнуюЧастьНакладной(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	
КонецПроцедуры

Процедура ЗаполнитьНакладнуюСАТУРННаОснованииОтгрузкиТоваровСХранения(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка          КАК ДокументОснование,
	|	ДокументСсылка.Дата            КАК ДатаПрименения,
	|	ДокументСсылка.Номер           КАК НомерПервичногоДокумента,
	|	ДокументСсылка.Дата            КАК ДатаРегистрации,
	|	""Отгрузка товаров с хранения"" КАК НаименованиеПервичногоДокумента,
	|	НЕ ДокументСсылка.Проведен     КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Менеджер        КАК Ответственный,
	|	ДокументСсылка.Организация     КАК Организация,
	|	ДокументСсылка.Подразделение   КАК Подразделение,
	|	ДокументСсылка.Организация     КАК Контрагент,
	|	ДокументСсылка.Партнер         КАК Партнер,
	|	Неопределено                   КАК ГрузоотправительСклад,
	|	ДокументСсылка.Контрагент      КАК ГрузоотправительКонтрагент,
	|	ДокументСсылка.Партнер         КАК ГрузоотправительПартнер,
	|	ДокументСсылка.Склад           КАК ГрузополучательСклад,
	|	Неопределено                   КАК ГрузополучательКонтрагент,
	|	Неопределено                   КАК ГрузополучательПартнер,
	|	ДокументСсылка.Подразделение   КАК ГрузополучательПодразделение,
	|	ДокументСсылка.ПеревозчикПартнер КАК ПеревозчикПартнер,
	|	ДокументСсылка.Договор.ГосударственныйКонтракт КАК ГосударственныйКонтракт,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяПеревозка) КАК Операция
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуНакладной(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = ТекстТоварыДокументаССериями("ОтгрузкаТоваровСХранения");
	ЗаполнитьТабличнуюЧастьНакладной(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	
КонецПроцедуры

Процедура ЗаполнитьНакладнуюСАТУРННаОснованииПоступленияТоваровОтХранителя(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка          КАК ДокументОснование,
	|	ДокументСсылка.Дата            КАК ДатаПрименения,
	|	ДокументСсылка.Номер           КАК НомерПервичногоДокумента,
	|	ДокументСсылка.Дата            КАК ДатаРегистрации,
	|	""Поступление товаров от хранителя"" КАК НаименованиеПервичногоДокумента,
	|	НЕ ДокументСсылка.Проведен     КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Менеджер        КАК Ответственный,
	|	ДокументСсылка.Организация     КАК Организация,
	|	ДокументСсылка.Подразделение   КАК Подразделение,
	|	ДокументСсылка.Организация     КАК Контрагент,
	|	ДокументСсылка.Партнер         КАК Партнер,
	|	Неопределено                   КАК ГрузоотправительСклад,
	|	ДокументСсылка.Контрагент      КАК ГрузоотправительКонтрагент,
	|	ДокументСсылка.Партнер         КАК ГрузоотправительПартнер,
	|	ДокументСсылка.Склад           КАК ГрузополучательСклад,
	|	Неопределено                   КАК ГрузополучательКонтрагент,
	|	Неопределено                   КАК ГрузополучательПартнер,
	|	ДокументСсылка.Подразделение   КАК ГрузополучательПодразделение,
	|	Неопределено                   КАК ПеревозчикПартнер,
	|	ДокументСсылка.Договор.ГосударственныйКонтракт КАК ГосударственныйКонтракт,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяПеревозка) КАК Операция
	|ИЗ
	|	Документ.ПоступлениеТоваровОтХранителя КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуНакладной(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = ТекстТоварыДокументаССериями("ПоступлениеТоваровОтХранителя");
	ЗаполнитьТабличнуюЧастьНакладной(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	
КонецПроцедуры

Процедура ЗаполнитьНакладнуюСАТУРННаОснованииВыкупаТоваровХранителем(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка          КАК ДокументОснование,
	|	ДокументСсылка.Дата            КАК ДатаПрименения,
	|	ДокументСсылка.Номер           КАК НомерПервичногоДокумента,
	|	ДокументСсылка.Дата            КАК ДатаРегистрации,
	|	""Выкуп товаров хранителем""   КАК НаименованиеПервичногоДокумента,
	|	НЕ ДокументСсылка.Проведен     КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Менеджер        КАК Ответственный,
	|	ДокументСсылка.Организация     КАК Организация,
	|	ДокументСсылка.Подразделение   КАК Подразделение,
	|	ДокументСсылка.Контрагент      КАК Контрагент,
	|	ДокументСсылка.Партнер         КАК Партнер,
	|	Неопределено                   КАК ГрузоотправительСклад,
	|	ДокументСсылка.Контрагент      КАК ГрузоотправительКонтрагент,
	|	ДокументСсылка.Партнер         КАК ГрузоотправительПартнер,
	|	Неопределено                   КАК ГрузополучательСклад,
	|	Неопределено                   КАК ГрузополучательКонтрагент,
	|	ДокументСсылка.Партнер         КАК ГрузополучательПартнер,
	|	Неопределено                   КАК ГрузополучательПодразделение,
	|	Неопределено                   КАК ПеревозчикПартнер,
	|	ДокументСсылка.Договор.ГосударственныйКонтракт КАК ГосударственныйКонтракт,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяПеревозка) КАК Операция
	|ИЗ
	|	Документ.ВыкупТоваровХранителем КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуНакладной(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = ТекстТоварыДокументаБезСерий("ВыкупТоваровХранителем");
	ЗаполнитьТабличнуюЧастьНакладной(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	
КонецПроцедуры

Процедура ЗаполнитьНакладнуюСАТУРННаОснованииПередачиТоваровХранителю(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка          КАК ДокументОснование,
	|	ДокументСсылка.Дата            КАК ДатаПрименения,
	|	ДокументСсылка.Номер           КАК НомерПервичногоДокумента,
	|	ДокументСсылка.Дата            КАК ДатаРегистрации,
	|	""Передача товаров хранителю"" КАК НаименованиеПервичногоДокумента,
	|	НЕ ДокументСсылка.Проведен     КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Менеджер        КАК Ответственный,
	|	ДокументСсылка.Организация     КАК Организация,
	|	ДокументСсылка.Подразделение   КАК Подразделение,
	|	ДокументСсылка.Организация     КАК Контрагент,
	|	ДокументСсылка.Партнер         КАК Партнер,
	|	ДокументСсылка.Склад           КАК ГрузоотправительСклад,
	|	Неопределено                   КАК ГрузоотправительКонтрагент,
	|	Неопределено                   КАК ГрузоотправительПартнер,
	|	Неопределено                   КАК ГрузополучательСклад,
	|	ДокументСсылка.Контрагент      КАК ГрузополучательКонтрагент,
	|	ДокументСсылка.Партнер         КАК ГрузополучательПартнер,
	|	Неопределено                   КАК ГрузополучательПодразделение,
	|	ДокументСсылка.ПеревозчикПартнер КАК ПеревозчикПартнер,
	|	ДокументСсылка.Договор.ГосударственныйКонтракт КАК ГосударственныйКонтракт,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСАТУРН.НакладнаяПеревозка) КАК Операция
	|ИЗ
	|	Документ.ПередачаТоваровХранителю КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	
	ЗаполнитьШапкуНакладной(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = ТекстТоварыДокументаБезСерий("ПередачаТоваровХранителю");
	ЗаполнитьТабличнуюЧастьНакладной(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента);
	
КонецПроцедуры

Функция ДанныеПрикладныхДокументовИзНакладнаяСАТУРН(ДокументСсылка, ПолучитьШапку = Истина, ПолучитьТовары = Истина)

	СписокЗапросов = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Если ПолучитьШапку Тогда
		
		СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	НакладнаяСАТУРН.ГрузоотправительОрганизацияСАТУРН.Контрагент         КАК Контрагент,
		|	НакладнаяСАТУРН.Дата                                                 КАК Дата,
		|	НакладнаяСАТУРН.Комментарий                                          КАК Комментарий,
		|	НакладнаяСАТУРН.ГрузополучательОрганизацияСАТУРН.Контрагент          КАК Организация,
		|	НакладнаяСАТУРН.Ответственный                                        КАК Ответственный,
		|	НакладнаяСАТУРН.ГрузополучательМестоХранения                         КАК МестоХранения,
		|	НакладнаяСАТУРН.ГрузополучательОрганизацияСАТУРН                     КАК ОрганизацияСАТУРН,
		|	НакладнаяСАТУРН.ГрузоотправительОрганизацияСАТУРН.Контрагент.Партнер КАК Партнер
		|ИЗ
		|	Документ.НакладнаяСАТУРН КАК НакладнаяСАТУРН
		|ГДЕ
		|	НакладнаяСАТУРН.Ссылка = &Ссылка", "Шапка");
		
	КонецЕсли;
	
	Если ПолучитьТовары Тогда
		
		Запрос.УстановитьПараметр("СтранаРегистрации", ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана());
		
		СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	СтавкиНДСНоменклатуры.Номенклатура КАК Номенклатура,
		|	СтавкиНДСНоменклатуры.СтавкаНДС    КАК СтавкаНДС
		|ПОМЕСТИТЬ СтавкиНДСНоменклатуры
		|ИЗ
		|	(ВЫБРАТЬ
		|		СтавкиНДСНоменклатуры.Номенклатура КАК Номенклатура,
		|		СтавкиНДСНоменклатуры.Страна КАК Страна,
		|		МАКСИМУМ(СтавкиНДСНоменклатуры.Период) КАК Период
		|		
		|	ИЗ
		|		РегистрСведений.СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НакладнаяСАТУРН.Товары КАК НакладнаяСАТУРНТовары
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НакладнаяСАТУРН КАК НакладнаяСАТУРН
		|				ПО НакладнаяСАТУРНТовары.Ссылка = НакладнаяСАТУРН.Ссылка
		|			ПО СтавкиНДСНоменклатуры.Номенклатура = НакладнаяСАТУРНТовары.Номенклатура
		|				И (СтавкиНДСНоменклатуры.Страна = &СтранаРегистрации
		|					ИЛИ СтавкиНДСНоменклатуры.Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка))
		|				И СтавкиНДСНоменклатуры.Период <= НакладнаяСАТУРН.Дата
		|	ГДЕ
		|		НакладнаяСАТУРНТовары.Ссылка = &Ссылка
		|	СГРУППИРОВАТЬ ПО
		|		СтавкиНДСНоменклатуры.Номенклатура,
		|		СтавкиНДСНоменклатуры.Страна) КАК АктуальныеСтавкиНДС
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
		|	ПО АктуальныеСтавкиНДС.Период = СтавкиНДСНоменклатуры.Период
		|		И АктуальныеСтавкиНДС.Номенклатура = СтавкиНДСНоменклатуры.Номенклатура
		|		И АктуальныеСтавкиНДС.Страна = СтавкиНДСНоменклатуры.Страна
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОсновныеСтавкиНДС.СтавкаНДС КАК СтавкаНДС
		|ПОМЕСТИТЬ ОсновныеСтавкиНДС
		|ИЗ
		|	(ВЫБРАТЬ
		|		ОсновныеСтавкиНДС.Страна КАК Страна,
		|		МАКСИМУМ(ОсновныеСтавкиНДС.Период) КАК Период
		|		
		|	ИЗ
		|		РегистрСведений.ОсновныеСтавкиНДС КАК ОсновныеСтавкиНДС
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НакладнаяСАТУРН КАК НакладнаяСАТУРН
		|			ПО (ОсновныеСтавкиНДС.Страна = &СтранаРегистрации
		|					ИЛИ ОсновныеСтавкиНДС.Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка))
		|				И ОсновныеСтавкиНДС.Период <= НакладнаяСАТУРН.Дата
		|	ГДЕ
		|		НакладнаяСАТУРН.Ссылка = &Ссылка
		|	СГРУППИРОВАТЬ ПО
		|		ОсновныеСтавкиНДС.Страна) КАК АктуальныеСтавкиНДС
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСтавкиНДС КАК ОсновныеСтавкиНДС
		|	ПО АктуальныеСтавкиНДС.Период = ОсновныеСтавкиНДС.Период
		|		И АктуальныеСтавкиНДС.Страна = ОсновныеСтавкиНДС.Страна
		|");
		
		СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	НакладнаяСАТУРНТовары.Номенклатура                КАК Номенклатура,
		|	ЕСТЬNULL(СтавкиНДСНоменклатуры.СтавкаНДС, ЕСТЬNULL(ОсновныеСтавкиНДС.СтавкаНДС,
		|		ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка))) КАК СтавкаНДС,
		|	НакладнаяСАТУРНТовары.Характеристика              КАК Характеристика,
		|	НакладнаяСАТУРНТовары.Серия                       КАК Серия,
		|	НакладнаяСАТУРНТовары.Количество                  КАК Количество,
		|	НакладнаяСАТУРНТовары.Количество                  КАК КоличествоУпаковок
		|ИЗ
		|	Документ.НакладнаяСАТУРН.Товары КАК НакладнаяСАТУРНТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
		|		ПО НакладнаяСАТУРНТовары.Номенклатура = СтавкиНДСНоменклатуры.Номенклатура,
		|	ОсновныеСтавкиНДС КАК ОсновныеСтавкиНДС
		|ГДЕ
		|	НакладнаяСАТУРНТовары.Ссылка = &Ссылка", "Товары");
		
	КонецЕсли;
	
	Возврат ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов);

КонецФункции

Процедура ЗаполнитьШапкуПрикладногоДокументаНаОснованииНакладнаяСАТУРН(ДокументОбъект, ДанныеЗаполнения)
	
	Выборка = ДанныеЗаполнения.Выбрать();
	Выборка.Следующий();
	
	СкладПодразделение = СкладПодразделениеИзМестаХранения(Выборка);
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Организация",   Выборка.Организация);
	Реквизиты.Вставить("Дата",          Выборка.Дата);
	Реквизиты.Вставить("Комментарий",   Выборка.Комментарий);
	Реквизиты.Вставить("Контрагент",    Выборка.Контрагент);
	Реквизиты.Вставить("Партнер",       Выборка.Партнер);
	Реквизиты.Вставить("Склад",         СкладПодразделение.Склад);
	Реквизиты.Вставить("Подразделение", СкладПодразделение.Подразделение);
	
	ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты);
	
КонецПроцедуры

Функция СтруктураЗаполненияТабличныхЧастейПоНакладнаяСАТУРН(ДанныеЗаполнения, ЕстьСерии = Ложь, ЕстьСкладВТабличнойЧасти = Ложь)
	
	Результат = Новый Структура;
	Результат.Вставить("Товары", ДанныеЗаполнения.Выгрузить());
	Если ЕстьСерии Тогда
		Результат.Вставить("ЕстьСерии");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииНакладнаяСАТУРН(ДокументОбъект, ДанныеЗаполнения, ДокументОснование)
	
	ДокументОбъект.Товары.Очистить();
	
	Для Каждого СтрокаТовары Из ДанныеЗаполнения.Товары Цикл
		
		НоваяСтрокаТовары = ДокументОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары, СтрокаТовары);
		
	КонецЦикла;

	Если ДанныеЗаполнения.Свойство("ЕстьСерии") Тогда
		
		ИмяТЧСерии = ДанныеЗаполнения.ЕстьСерии;
		Если ИмяТЧСерии = Неопределено Тогда 
			ИмяТЧСерии = "Серии";
		КонецЕсли;
		
		ДокументОбъект[ИмяТЧСерии].Очистить();
		Для Каждого СтрокаТовары Из ДокументОбъект.Товары Цикл
			Если ЗначениеЗаполнено(СтрокаТовары.Серия) Тогда
				НоваяСтрокаСерия = ДокументОбъект[ИмяТЧСерии].Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаСерия, СтрокаТовары);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ИмпортПродукцииСАТУРН

Процедура ОбработкаЗаполненияДокументаИмпортПродукцииСАТУРН(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")Тогда
		ЗаполнитьИмпортПродукцииСАТУРННаОснованииПриобретениеТоваровУслуг(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьИмпортПродукцииСАТУРН(ДокументОбъект, ДанныеЗаполнения)
	
	ДокументОбъект.Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаЗаполненияИмпортПродукцииСАТУРНИзПрикладногоДокумента();
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	ЗаполнитьТабличнуюЧастьДокумента(ДокументОбъект.Товары, Запрос.Выполнить(), ДанныеЗаполнения);
	
КонецПроцедуры

Функция ТекстЗапросаЗаполненияИмпортПродукцииСАТУРНИзПрикладногоДокумента()
	Текст = 
	"ВЫБРАТЬ
	|	ИмпортПродукцииСАТУРНТовары.НомерСтроки КАК НомерСтроки,
	|	ИмпортПродукцииСАТУРНТовары.ПАТ КАК ПАТ,
	|	ИмпортПродукцииСАТУРНТовары.Идентификатор КАК Идентификатор,
	|	ИмпортПродукцииСАТУРНТовары.ИмпортируемаяПартия КАК ИмпортируемаяПартия,
	|	ИмпортПродукцииСАТУРНТовары.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ИмпортПродукцииСАТУРНТовары.Упаковка КАК Упаковка,
	|	ИмпортПродукцииСАТУРНТовары.Количество КАК Количество,
	|	ИмпортПродукцииСАТУРНТовары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ИмпортПродукцииСАТУРНТовары.ТипИзмеряемойВеличиныСАТУРН КАК ТипИзмеряемойВеличиныСАТУРН,
	|	ИмпортПродукцииСАТУРНТовары.Партия КАК Партия,
	|	ИмпортПродукцииСАТУРНТовары.КоличествоСАТУРН КАК КоличествоСАТУРН,
	|	ИмпортПродукцииСАТУРНТовары.Номенклатура КАК Номенклатура,
	|	ИмпортПродукцииСАТУРНТовары.Характеристика КАК Характеристика,
	|	ИмпортПродукцииСАТУРНТовары.Серия КАК Серия
	|ПОМЕСТИТЬ ДанныеДокумента
	|ИЗ
	|	Документ.ИмпортПродукцииСАТУРН.Товары КАК ИмпортПродукцииСАТУРНТовары
	|ГДЕ
	|	ИмпортПродукцииСАТУРНТовары.Ссылка = &ЭтаСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.НомерСтроки КАК НомерСтроки,
	|	МАКСИМУМ(СоответствиеНоменклатурыСАТУРН.Номенклатура) КАК Номенклатура,
	|	МАКСИМУМ(СоответствиеНоменклатурыСАТУРН.Характеристика) КАК Характеристика,
	|	МАКСИМУМ(СоответствиеНоменклатурыСАТУРН.Серия) КАК Серия
	|ПОМЕСТИТЬ ПоискНоменклатуры
	|ИЗ
	|	ДанныеДокумента КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыСАТУРН КАК СоответствиеНоменклатурыСАТУРН
	|		ПО (ДанныеДокумента.ПАТ = СоответствиеНоменклатурыСАТУРН.ПАТ)
	|ГДЕ
	|	ДанныеДокумента.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.НомерСтроки КАК НомерСтроки,
	|	ДанныеДокумента.ПАТ КАК ПАТ,
	|	ДанныеДокумента.Идентификатор КАК Идентификатор,
	|	ДанныеДокумента.ИмпортируемаяПартия КАК ИмпортируемаяПартия,
	|	ДанныеДокумента.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ДанныеДокумента.Упаковка КАК Упаковка,
	|	ДанныеДокумента.Количество КАК Количество,
	|	ДанныеДокумента.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ДанныеДокумента.ТипИзмеряемойВеличиныСАТУРН КАК ТипИзмеряемойВеличиныСАТУРН,
	|	ДанныеДокумента.Партия КАК Партия,
	|	ДанныеДокумента.КоличествоСАТУРН КАК КоличествоСАТУРН,
	|	ЕСТЬNULL(ПоискНоменклатуры.Номенклатура, ДанныеДокумента.Номенклатура) КАК Номенклатура,
	|	ЕСТЬNULL(ПоискНоменклатуры.Характеристика, ДанныеДокумента.Характеристика) КАК Характеристика,
	|	ЕСТЬNULL(ПоискНоменклатуры.Серия, ДанныеДокумента.Серия) КАК Серия
	|ИЗ
	|	ДанныеДокумента КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПоискНоменклатуры КАК ПоискНоменклатуры
	|		ПО (ДанныеДокумента.НомерСтроки = ПоискНоменклатуры.НомерСтроки)";
	
	Возврат Текст;
	
КонецФункции

Процедура ЗаполнитьИмпортПродукцииСАТУРННаОснованииПриобретениеТоваровУслуг(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ЗаполнитьТабличнуюЧастьИмпортПродукцииСАТУРН(ДокументОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

Функция ДанныеПрикладныхДокументовИзИмпортПродукцииСАТУРН(ДокументСсылка, ПолучитьШапку = Истина, ПолучитьТовары = Истина)

	СписокЗапросов = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Если ПолучитьШапку Тогда
		
		СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	ИмпортПродукцииСАТУРН.Дата                                    КАК Дата,
		|	ИмпортПродукцииСАТУРН.Комментарий                             КАК Комментарий,
		|	ИмпортПродукцииСАТУРН.ОрганизацияСАТУРН.Контрагент            КАК Организация,
		|	ИмпортПродукцииСАТУРН.Ответственный                           КАК Ответственный,
		|	ИмпортПродукцииСАТУРН.МестоХранения                           КАК МестоХранения,
		|	ИмпортПродукцииСАТУРН.ОрганизацияСАТУРН                       КАК ОрганизацияСАТУРН,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту) КАК ХозяйственнаяОперация
		|ИЗ
		|	Документ.ИмпортПродукцииСАТУРН КАК ИмпортПродукцииСАТУРН
		|ГДЕ
		|	ИмпортПродукцииСАТУРН.Ссылка = &Ссылка", "Шапка");
		
	КонецЕсли;
	
	Если ПолучитьТовары Тогда
		
		Запрос.УстановитьПараметр("СтранаРегистрации", ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана());
		
		СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	СтавкиНДСНоменклатуры.Номенклатура КАК Номенклатура,
		|	СтавкиНДСНоменклатуры.СтавкаНДС    КАК СтавкаНДС
		|ПОМЕСТИТЬ СтавкиНДСНоменклатуры
		|ИЗ
		|	(ВЫБРАТЬ
		|		СтавкиНДСНоменклатуры.Номенклатура КАК Номенклатура,
		|		СтавкиНДСНоменклатуры.Страна КАК Страна,
		|		МАКСИМУМ(СтавкиНДСНоменклатуры.Период) КАК Период
		|		
		|	ИЗ
		|		РегистрСведений.СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИмпортПродукцииСАТУРН.Товары КАК ИмпортПродукцииСАТУРНТовары
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИмпортПродукцииСАТУРН КАК ИмпортПродукцииСАТУРН
		|				ПО ИмпортПродукцииСАТУРНТовары.Ссылка = ИмпортПродукцииСАТУРН.Ссылка
		|			ПО СтавкиНДСНоменклатуры.Номенклатура = ИмпортПродукцииСАТУРНТовары.Номенклатура
		|				И (СтавкиНДСНоменклатуры.Страна = &СтранаРегистрации
		|					ИЛИ СтавкиНДСНоменклатуры.Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка))
		|				И СтавкиНДСНоменклатуры.Период <= ИмпортПродукцииСАТУРН.Дата
		|	ГДЕ
		|		ИмпортПродукцииСАТУРНТовары.Ссылка = &Ссылка
		|	СГРУППИРОВАТЬ ПО
		|		СтавкиНДСНоменклатуры.Номенклатура,
		|		СтавкиНДСНоменклатуры.Страна) КАК АктуальныеСтавкиНДС
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
		|	ПО АктуальныеСтавкиНДС.Период = СтавкиНДСНоменклатуры.Период
		|		И АктуальныеСтавкиНДС.Номенклатура = СтавкиНДСНоменклатуры.Номенклатура
		|		И АктуальныеСтавкиНДС.Страна = СтавкиНДСНоменклатуры.Страна
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОсновныеСтавкиНДС.СтавкаНДС КАК СтавкаНДС
		|ПОМЕСТИТЬ ОсновныеСтавкиНДС
		|ИЗ
		|	(ВЫБРАТЬ
		|		ОсновныеСтавкиНДС.Страна КАК Страна,
		|		МАКСИМУМ(ОсновныеСтавкиНДС.Период) КАК Период
		|		
		|	ИЗ
		|		РегистрСведений.ОсновныеСтавкиНДС КАК ОсновныеСтавкиНДС
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИмпортПродукцииСАТУРН КАК ИмпортПродукцииСАТУРН
		|			ПО (ОсновныеСтавкиНДС.Страна = &СтранаРегистрации
		|					ИЛИ ОсновныеСтавкиНДС.Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка))
		|				И ОсновныеСтавкиНДС.Период <= ИмпортПродукцииСАТУРН.Дата
		|	ГДЕ
		|		ИмпортПродукцииСАТУРН.Ссылка = &Ссылка
		|	СГРУППИРОВАТЬ ПО
		|		ОсновныеСтавкиНДС.Страна) КАК АктуальныеСтавкиНДС
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСтавкиНДС КАК ОсновныеСтавкиНДС
		|	ПО АктуальныеСтавкиНДС.Период = ОсновныеСтавкиНДС.Период
		|		И АктуальныеСтавкиНДС.Страна = ОсновныеСтавкиНДС.Страна
		|");
		
		СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	ИмпортПродукцииСАТУРНТовары.Номенклатура                КАК Номенклатура,
		|	ЕСТЬNULL(СтавкиНДСНоменклатуры.СтавкаНДС, ЕСТЬNULL(ОсновныеСтавкиНДС.СтавкаНДС,
		|		ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка))) КАК СтавкаНДС,
		|	ИмпортПродукцииСАТУРНТовары.Характеристика              КАК Характеристика,
		|	ИмпортПродукцииСАТУРНТовары.Серия                       КАК Серия,
		|	ИмпортПродукцииСАТУРНТовары.Количество                  КАК Количество,
		|	ИмпортПродукцииСАТУРНТовары.Количество                  КАК КоличествоУпаковок
		|ИЗ
		|	Документ.ИмпортПродукцииСАТУРН.Товары КАК ИмпортПродукцииСАТУРНТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
		|		ПО ИмпортПродукцииСАТУРНТовары.Номенклатура = СтавкиНДСНоменклатуры.Номенклатура,
		|	ОсновныеСтавкиНДС КАК ОсновныеСтавкиНДС
		|ГДЕ
		|	ИмпортПродукцииСАТУРНТовары.Ссылка = &Ссылка", "Товары");
		
	КонецЕсли;
	
	Возврат ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов);

КонецФункции

Процедура ЗаполнитьШапкуПрикладногоДокументаНаОснованииИмпортПродукцииСАТУРН(ДокументОбъект, ДанныеЗаполнения)
	
	Выборка = ДанныеЗаполнения.Выбрать();
	Выборка.Следующий();
	
	СкладПодразделение = СкладПодразделениеИзМестаХранения(Выборка);
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Организация",           Выборка.Организация);
	Реквизиты.Вставить("Дата",                  Выборка.Дата);
	Реквизиты.Вставить("Комментарий",           Выборка.Комментарий);
	Реквизиты.Вставить("Склад",                 СкладПодразделение.Склад);
	Реквизиты.Вставить("Подразделение",         СкладПодразделение.Подразделение);
	Реквизиты.Вставить("ХозяйственнаяОперация", Выборка.ХозяйственнаяОперация);
	
	ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты);
	
КонецПроцедуры

Функция СтруктураЗаполненияТабличныхЧастейПоИмпортПродукцииСАТУРН(ДанныеЗаполнения, ЕстьСерии = Ложь, ЕстьСкладВТабличнойЧасти = Ложь)
	
	Результат = Новый Структура;
	Результат.Вставить("Товары", ДанныеЗаполнения.Выгрузить());
	Если ЕстьСерии Тогда
		Результат.Вставить("ЕстьСерии");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииИмпортПродукцииСАТУРН(ДокументОбъект, ДанныеЗаполнения, ДокументОснование)
	
	ДокументОбъект.Товары.Очистить();
	
	Для Каждого СтрокаТовары Из ДанныеЗаполнения.Товары Цикл
		
		НоваяСтрокаТовары = ДокументОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары, СтрокаТовары);
		
	КонецЦикла;

	Если ДанныеЗаполнения.Свойство("ЕстьСерии") Тогда
		
		ИмяТЧСерии = ДанныеЗаполнения.ЕстьСерии;
		Если ИмяТЧСерии = Неопределено Тогда 
			ИмяТЧСерии = "Серии";
		КонецЕсли;
		
		ДокументОбъект[ИмяТЧСерии].Очистить();
		Для Каждого СтрокаТовары Из ДокументОбъект.Товары Цикл
			
			Если ЗначениеЗаполнено(СтрокаТовары.Серия) Тогда
				НоваяСтрокаСерия = ДокументОбъект[ИмяТЧСерии].Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаСерия, СтрокаТовары);
				
				Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДокументОбъект, "Склад")
					И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(НоваяСтрокаСерия, "Склад") Тогда
					НоваяСтрокаСерия.Склад = ДокументОбъект.Склад;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрикладныеДокументы

#Область ВозвратТоваровОтКлиента

Процедура ЗаполнитьВозвратТоваровОтКлиентаНаОснованииНакладнаяСАТУРН(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	ДокументОснование = ДокументОснованиеИзДанныхЗаполнения(ДанныеЗаполнения);
	
	ЗаполнятьШапку = Не ЗначениеЗаполнено(ДокументОбъект.Ссылка)
		И Не ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку");
	
	РезультатЗапроса = ДанныеПрикладныхДокументовИзНакладнаяСАТУРН(ДокументОснование, ЗаполнятьШапку);
	
	Если ЗаполнятьШапку Тогда
		ЗаполнитьШапкуПрикладногоДокументаНаОснованииНакладнаяСАТУРН(ДокументОбъект, РезультатЗапроса.Шапка);
	КонецЕсли;
	
	ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииНакладнаяСАТУРН(
		ДокументОбъект,
		СтруктураЗаполненияТабличныхЧастейПоНакладнаяСАТУРН(РезультатЗапроса.Товары, Истина), ДанныеЗаполнения);
	
	ДокументОбъект.ЗаполнитьУсловияПродажПоУмолчанию();
	
КонецПроцедуры

#КонецОбласти

#Область ОприходованиеИзлишковТоваров

Процедура ЗаполнитьОприходованиеИзлишковТоваровНаОснованииАктИнвентаризацииСАТУРН(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	ДокументОснование = ДокументОснованиеИзДанныхЗаполнения(ДанныеЗаполнения);
	
	ЗаполнятьШапку = Не ЗначениеЗаполнено(ДокументОбъект.Ссылка)
		И Не ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку");
	
	РезультатЗапроса = ДанныеПрикладныхДокументовИзАктИнвентаризацииСАТУРН(ДокументОснование, ЗаполнятьШапку);
	
	Если ЗаполнятьШапку Тогда
		ЗаполнитьШапкуПрикладногоДокументаНаОснованииАктИнвентаризацииСАТУРН(ДокументОбъект, РезультатЗапроса.Шапка);
	КонецЕсли;
	
	ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииАктИнвентаризацииСАТУРН(
		ДокументОбъект,
		СтруктураЗаполненияТабличныхЧастейПоАктИнвентаризацииСАТУРН(РезультатЗапроса.Товары), ДанныеЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#Область СписаниеНедостачТоваров

Процедура ЗаполнитьСписаниеНедостачТоваровНаОснованииАктИнвентаризацииСАТУРН(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	ДокументОснование = ДокументОснованиеИзДанныхЗаполнения(ДанныеЗаполнения);
	
	ЗаполнятьШапку = Не ЗначениеЗаполнено(ДокументОбъект.Ссылка)
		И Не ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку");
	
	РезультатЗапроса = ДанныеПрикладныхДокументовИзАктИнвентаризацииСАТУРН(ДокументОснование, ЗаполнятьШапку,,Истина);
	
	Если ЗаполнятьШапку Тогда
		ЗаполнитьШапкуПрикладногоДокументаНаОснованииАктИнвентаризацииСАТУРН(ДокументОбъект, РезультатЗапроса.Шапка);
	КонецЕсли;
	
	ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииАктИнвентаризацииСАТУРН(
		ДокументОбъект,
		СтруктураЗаполненияТабличныхЧастейПоАктИнвентаризацииСАТУРН(РезультатЗапроса.Товары), ДанныеЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#Область ПриобретениеТоваровУслуг

Процедура ЗаполнитьПриобретениеТоваровУслугНаОснованииНакладнаяСАТУРН(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	ДокументОснование = ДокументОснованиеИзДанныхЗаполнения(ДанныеЗаполнения);
	
	ЗаполнятьШапку = Не ЗначениеЗаполнено(ДокументОбъект.Ссылка)
		И Не ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку");
	
	РезультатЗапроса = ДанныеПрикладныхДокументовИзНакладнаяСАТУРН(ДокументОснование, ЗаполнятьШапку);
	
	Если ЗаполнятьШапку Тогда
		ЗаполнитьШапкуПрикладногоДокументаНаОснованииНакладнаяСАТУРН(ДокументОбъект, РезультатЗапроса.Шапка);
	КонецЕсли;
	
	ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииНакладнаяСАТУРН(
		ДокументОбъект,
		СтруктураЗаполненияТабличныхЧастейПоНакладнаяСАТУРН(РезультатЗапроса.Товары, Истина), ДанныеЗаполнения);
	
	ДокументОбъект.ЗаполнитьУсловияЗакупокПоУмолчанию();
	
КонецПроцедуры

Процедура ЗаполнитьПриобретениеТоваровУслугНаОснованииИмпортПродукцииСАТУРН(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	ДокументОснование = ДокументОснованиеИзДанныхЗаполнения(ДанныеЗаполнения);
	
	ЗаполнятьШапку = Не ЗначениеЗаполнено(ДокументОбъект.Ссылка)
		И Не ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку");
	
	РезультатЗапроса = ДанныеПрикладныхДокументовИзИмпортПродукцииСАТУРН(ДокументОснование, ЗаполнятьШапку);
	
	Если ЗаполнятьШапку Тогда
		ЗаполнитьШапкуПрикладногоДокументаНаОснованииИмпортПродукцииСАТУРН(ДокументОбъект, РезультатЗапроса.Шапка);
	КонецЕсли;
	
	ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииИмпортПродукцииСАТУРН(
		ДокументОбъект,
		СтруктураЗаполненияТабличныхЧастейПоИмпортПродукцииСАТУРН(РезультатЗапроса.Товары, Истина), ДанныеЗаполнения);
	
	ДокументОбъект.ЗаполнитьУсловияЗакупокПоУмолчанию();
	
КонецПроцедуры

#КонецОбласти

#Область ВнутреннееПотребление

Процедура ЗаполнитьВнутреннееПотреблениеНаОснованииАктПримененияСАТУРН(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	ДокументОснование = ДокументОснованиеИзДанныхЗаполнения(ДанныеЗаполнения);
	
	ЗаполнятьШапку = Не ЗначениеЗаполнено(ДокументОбъект.Ссылка)
		И Не ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку");
	
	РезультатЗапроса = ДанныеПрикладныхДокументовИзАктПримененияСАТУРН(ДокументОснование, ЗаполнятьШапку);
	
	Если ЗаполнятьШапку Тогда
		ЗаполнитьШапкуПрикладногоДокументаНаОснованииАктПримененияСАТУРН(ДокументОбъект, РезультатЗапроса.Шапка);
	КонецЕсли;
	
	ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииАктПримененияСАТУРН(
		ДокументОбъект,
		СтруктураЗаполненияТабличныхЧастейПоАктПримененияСАТУРН(РезультатЗапроса.Товары), ДанныеЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти


#КонецОбласти
