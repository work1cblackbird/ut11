#Область ПрограммныйИнтерфейс

// Функция возвращает вес товара по сертификату, для расчета в единице прослеживаемости кг
//
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура.
// 
// Возвращаемое значение:
//  Число - Вес товара по сертификату
Функция ВесТовараПоСертификату(Номенклатура) Экспорт
	
	Возврат 1;
	
КонецФункции

// Функция вовзращает первое найденное распоряжение к оформлению уведомления о ввозе прослеживаемых товаров для заданного первичного документа.
//
// Параметры:
//	ПервичныйДокумент - ДокументСсылка - Документ, для которого необходимо найти уведомления о ввозе прослеживаемых товаров.
//	Проведен - Булево - Признак того, что необходимо получить проведенные документы.
//
// Возвращаемое значение:
//	Неопределено, Структура - Данные найденного распоряжения. Если не найдено, тогда Неопрелено
//
Функция РаспоряжениеКОформлениюУведомленияОВвозеПрослеживаемыхТоваровПоОснованию(ПервичныйДокумент) Экспорт
	
	Результат = Неопределено;
	Ссылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПервичныйДокумент, "Ссылка");
	ТаблицаРаспоряжений = Документы.УведомлениеОВвозеПрослеживаемыхТоваров.РаспоряженияКОформлениюУведомленийОВвозеПоОснованию(Ссылка);
	Если ТаблицаРаспоряжений.Количество() Тогда
		РаспоряжениеКОформлению = ТаблицаРаспоряжений[0];
		Результат = Новый Структура;
		Для Каждого Колонка Из ТаблицаРаспоряжений.Колонки Цикл
			Результат.Вставить(Колонка.Имя, РаспоряжениеКОформлению[Колонка.Имя]);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция вовзращает первое найденное уведомление о ввозе прослеживаемых товаров заданного первичного документа.
//
// Параметры:
//	ПервичныйДокумент - ДокументСсылка - Документ, для которого необходимо найти уведомления о ввозе прослеживаемых товаров.
//	Проведен - Булево - Признак того, что необходимо получить проведенные документы.
//
// Возвращаемое значение:
//	Неопределено, ДокументСсылка - Ссылка на документ уведомление о ввозе прослеживаемых товаров. Если не найден, тогда Неопрелено
//
Функция УведомлениеОВвозеПрослеживаемыхТоваровПоОснованию(ПервичныйДокумент, Проведен = Истина) Экспорт
	
	Результат = Неопределено;
	Ссылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПервичныйДокумент, "Ссылка");
	ТаблицаУведомленийОВвозе = Документы.УведомлениеОВвозеПрослеживаемыхТоваров.УведомленияОВвозеПрослеживаемыхТоваровПоОснованию(Ссылка, Проведен);
	Если ТаблицаУведомленийОВвозе.Количество() Тогда
		Результат = ТаблицаУведомленийОВвозе[0].Ссылка;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция возвращает первое найденное распоряжение к оформлению уведомления о перемещении прослеживаемых товаров для заданного сопроводительного документа.
//
// Параметры:
//	СопроводительныйДокумент - ДокументСсылка - Документ, для которого необходимо найти уведомления о перемещении прослеживаемых товаров.
//	Проведен - Булево - Признак того, что необходимо получить проведенные документы.
//
// Возвращаемое значение:
//	Неопределено, Структура - Данные найденного распоряжения. Если не найдено, тогда Неопределено
//
Функция РаспоряжениеКОформлениюУведомленияОПеремещенииПрослеживаемыхТоваровПоОснованию(СопроводительныйДокумент) Экспорт
	
	Результат = Неопределено;
	
	ТаблицаРаспоряжений = Документы.УведомлениеОПеремещенииПрослеживаемыхТоваров.РаспоряженияКОформлениюУведомленийОПеремещенииПоОснованию(СопроводительныйДокумент);
	Если ТаблицаРаспоряжений.Количество() Тогда
		РаспоряжениеКОформлению = ТаблицаРаспоряжений[0];
		Результат = Новый Структура;
		Для Каждого Колонка Из ТаблицаРаспоряжений.Колонки Цикл
			Результат.Вставить(Колонка.Имя, РаспоряжениеКОформлению[Колонка.Имя]);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция возвращает первое найденное уведомление о перемещении прослеживаемых товаров для заданного сопроводительного документа.
//
// Параметры:
//	СопроводительныйДокумент - ДокументСсылка - Документ, для которого необходимо найти уведомления о перемещении прослеживаемых товаров.
//	Проведен - Булево - Признак того, что необходимо получить проведенные документы.
//
// Возвращаемое значение:
//	Неопределено, ДокументСсылка - Ссылка на документ уведомление о перемещении прослеживаемых товаров. Если не найден, тогда Неопределено
//
Функция УведомлениеОПеремещенииПрослеживаемыхТоваровПоОснованию(СопроводительныйДокумент, Проведен = Истина) Экспорт
	
	Результат = Неопределено;
	
	ТаблицаУведомленийОПеремещении = Документы.УведомлениеОПеремещенииПрослеживаемыхТоваров.УведомленияОПеремещенииПрослеживаемыхТоваровПоОснованию(СопроводительныйДокумент, Проведен);
	Если ТаблицаУведомленийОПеремещении.Количество() Тогда
		Результат = ТаблицаУведомленийОПеремещении[0].Ссылка;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСведенияПрослеживаемогоТовара(Номенклатура, ДанныеОбъекта) Экспорт
	
	СведенияОНоменклатуре = Новый Структура("ЕдиницаИзмерения,ЕдиницаПрослеживаемости,КодОКПД2,СтранаПроисхождения");
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КлассификаторТНВЭД.ЕдиницаИзмерения КАК ЕдиницаПрослеживаемости,
	|	Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Номенклатура.КодОКПД2 КАК КодОКПД2,
	|	Номенклатура.СтранаПроисхождения КАК СтранаПроисхождения
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
	|		ПО Номенклатура.КодТНВЭД = КлассификаторТНВЭД.Ссылка
	|ГДЕ
	|	Номенклатура.Ссылка = &Ссылка
	|	И Номенклатура.КодТНВЭД = &КодТНВЭД";
	
	Запрос.УстановитьПараметр("Ссылка", Номенклатура);
	Запрос.УстановитьПараметр("КодТНВЭД", ДанныеОбъекта.КодТНВЭД);
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(СведенияОНоменклатуре, Результат);
		
	КонецЕсли;
	
	Возврат СведенияОНоменклатуре;
	
КонецФункции


// Возвращает структуру реквизитов для заполнения формы документов
// Параметры:
// КодТНВЭД - СправочникСсылка.КлассификаторТНВЭД - Выбранный код ТНВЭД
// 
// Возвращаемое значение:
// Структура - 
//				КодТНВЭД - Код ТН ВЭД
//				ЕдиницаИзмерения - СправочникСсылка.КлассификаторЕдиницИзмерения - Пустое значение
//				ЕдиницаПрослеживаемости - СправочникСсылка.КлассификаторЕдиницИзмерения - Единица прослеживаемости
//
Функция ПараметрыВыбранногоТНВЭД(КодТНВЭД) Экспорт
	
	Возврат Новый Структура("КодТНВЭД,ЕдиницаИзмерения,ЕдиницаПрослеживаемости", 
					КодТНВЭД,
					"",
					ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КодТНВЭД, "ЕдиницаИзмерения"));
	
КонецФункции

#КонецОбласти
