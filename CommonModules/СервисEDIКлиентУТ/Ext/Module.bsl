#Область ПрограммныйИнтерфейс

// См. СервисEDIКлиентПереопределяемый.ОткрытьФормуВыбораКонтрагентов
//
Процедура ОткрытьФормуВыбораКонтрагентов(Форма, ПараметрыОткрытия, ОписаниеОповещения) Экспорт
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ЕстьВБизнесСети", Истина);
	
	ПараметрыОткрытия.Вставить("Отбор", ПараметрыОтбора);
	
	ОткрытьФорму("Справочник.Контрагенты.ФормаВыбора", ПараметрыОткрытия, Форма, ,,,ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// См. СервисEDIКлиентПереопределяемый.ОбработатьСобытиеТекущихДелНастройкиСправочники
//
Процедура ОбработатьСобытиеТекущихДелНастройкиСправочники(Форма, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка) Экспорт
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "Настройки" Тогда
		
		Возврат;
		
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "Номенклатура" Тогда
		
		ОткрытьФорму("Справочник.Номенклатура.ФормаСписка");
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "Контрагенты" Тогда
		
		ОткрытьФорму("Справочник.Контрагенты.Форма.ФормаСписка");
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "НоменклатураКонтрагентов" Тогда
		
		СопоставлениеНоменклатурыКонтрагентовКлиент.ОткрытьФормуСпискаНоменклатурыКонтрагентов(
			Новый Структура(), , , РежимОткрытияОкнаФормы.Независимый);
		
	КонецЕсли;
	
КонецПроцедуры

// См. СервисEDIКлиентПереопределяемый.ЗаписатьДокумент
//
Процедура ЗаписатьДокумент(ФормаДокумента, ОписаниеОповещения) Экспорт
	
	ОбщегоНазначенияУТКлиент.Записать(ФормаДокумента, , ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти
