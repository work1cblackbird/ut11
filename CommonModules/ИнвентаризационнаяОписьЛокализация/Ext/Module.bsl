
#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
	Движения = Объект.Движения;
	ДополнительныеСвойства = Объект.ДополнительныеСвойства;
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	//++ Локализация
	Если ПравоДоступа("Изменение", Метаданные.Документы.ИнвентаризационнаяОпись) Тогда
		
		// Сличительная ведомость (ИНВ-19)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьИНВ19";
		КомандаПечати.Идентификатор = "ИНВ19";
		КомандаПечати.Представление = НСтр("ru = 'Сличительная ведомость (ИНВ-19)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		
		// Инвентаризационная опись (ИНВ-3)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьИНВ3";
		КомандаПечати.Идентификатор = "ИНВ3";
		КомандаПечати.Представление = НСтр("ru = 'Инвентаризационная опись (ИНВ-3)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		
	КонецЕсли;
	//-- Локализация
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	//++ Локализация
	//-- Локализация
КонецПроцедуры

//++ Локализация

// Функция получает данные для формирования печатной формы ИНВ19
//
// Параметры:
//	ПараметрыПечати - Структура
//	ДокументОснование - ДокументСсылка - Документ, по которому необходимо получить данные.
//
// Возвращаемое значение:
// 	Структура:
// 		* РезультатПоШапке - РезультатЗапроса
// 		* РезультатКурсВалюты - РезультатЗапроса
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса
// 		* РезультатСвязанныеТовары - РезультатЗапроса
//
Функция ПолучитьДанныеДляПечатнойФормыИНВ19(ПараметрыПечати, ДокументОснование) Экспорт
		
	УстановитьПривилегированныйРежим(Истина);
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
			
	ЗапросПредварительныхДанных = Новый Запрос;
	ЗапросПредварительныхДанных.Текст = 
	"ВЫБРАТЬ
	|	ИнвентаризационнаяОпись.Дата КАК Дата,
	|	ИнвентаризационнаяОпись.ДатаНачала,
	|	ИнвентаризационнаяОпись.ДатаОкончания,
	|	ИнвентаризационнаяОпись.ВидЦены,
	|	ИнвентаризационнаяОпись.Склад,
	|	ИнвентаризационнаяОпись.ИсточникИнформацииОЦенахДляПечати,
	|	ИнвентаризационнаяОпись.Организация,
	|	РасчетСебестоимостиТоваровОрганизации.Ссылка.ПредварительныйРасчет
	|ИЗ
	|	Документ.ИнвентаризационнаяОпись КАК ИнвентаризационнаяОпись
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК РасчетСебестоимостиТоваровОрганизации
	|		ПО (РасчетСебестоимостиТоваровОрганизации.Организация = ИнвентаризационнаяОпись.Организация)
	|			И (РасчетСебестоимостиТоваровОрганизации.Ссылка.Проведен)
	|			И (РасчетСебестоимостиТоваровОрганизации.Ссылка.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ИнвентаризационнаяОпись.ДатаНачала, МЕСЯЦ) И КОНЕЦПЕРИОДА(ИнвентаризационнаяОпись.ДатаОкончания, МЕСЯЦ))
	|ГДЕ
	|	ИнвентаризационнаяОпись.Ссылка = &ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	РасчетСебестоимостиТоваровОрганизации.Ссылка.Дата УБЫВ";
	
	ЗапросПредварительныхДанных.УстановитьПараметр("ДокументОснование", ДокументОснование);	
 	ПредварительныеДанныеРезультат = ЗапросПредварительныхДанных.Выполнить().Выбрать();
			
	Если НЕ ПредварительныеДанныеРезультат.Следующий() Тогда
		ТекстСообщения = НСтр("ru = 'Печать %Документ% не выполнена: в указанный инвентаризационный период не найдены пересчеты товаров.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", Строка(ДокументОснование));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ДокументОснование);
		Возврат Неопределено
	КонецЕсли;
	
	ПредварительныйРасчет 			  = ПредварительныеДанныеРезультат.ПредварительныйРасчет;
	ИсточникИнформацииОЦенахДляПечати = ПредварительныеДанныеРезультат.ИсточникИнформацииОЦенахДляПечати;
	ДатаНачала 						  = ПредварительныеДанныеРезультат.ДатаНачала;
	ДатаОкончания 					  = КонецДня(ПредварительныеДанныеРезультат.ДатаОкончания);
	Склад							  = ПредварительныеДанныеРезультат.Склад;	
	Организация 					  = ПредварительныеДанныеРезультат.Организация;
	ВидЦены 						  = ПредварительныеДанныеРезультат.ВидЦены;
				
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", 	   ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", 	   ДатаОкончания);
	Запрос.УстановитьПараметр("Склад", 			   Склад);
	Запрос.УстановитьПараметр("ДатаОстатков", 	   ДатаОкончания + 1);
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("Организация", 	   Организация);
	Запрос.УстановитьПараметр("ВидЦены", 		   ВидЦены);
		
	// Ответственные лица в печатной форме
	СтруктураОтветственных = ОтветственныеЛицаСервер.ПолучитьОтветственныеЛицаОрганизации(Организация, КонецДня(ДатаОкончания));
	Запрос.УстановитьПараметр("Руководитель", 		   СтруктураОтветственных.Руководитель);
	Запрос.УстановитьПараметр("ДолжностьРуководителя", СтруктураОтветственных.РуководительДолжность);
	Запрос.УстановитьПараметр("ГлавныйБухгалтер", 	   СтруктураОтветственных.ГлавныйБухгалтер);
	
	Если ПредварительныеДанныеРезультат.ИсточникИнформацииОЦенахДляПечати = Перечисления.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости Тогда
		
		Если ПредварительныеДанныеРезультат.ПредварительныйРасчет = Null Тогда
			
			ТекстСообщения = НСтр("ru = 'Не удалось получить цены по себестоимости для документа %Документ%: с %ПериодС% по %ПериодПо% не произведен расчет себестоимости.'");
			
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", ДокументОснование);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПериодС%", Формат(НачалоМесяца(ДатаНачала),"ДЛФ=DD"));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПериодПо%", Формат(КонецМесяца(ДатаОкончания),"ДЛФ=DD"));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
			Возврат Неопределено;
			
		КонецЕсли;
		
		Если ПредварительныеДанныеРезультат.ПредварительныйРасчет Тогда
			
			ТекстСообщения = НСтр("ru = 'При печати документа %Документ% использовались данные предварительного расчета себестоимости.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", ДокументОснование);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
			|	ВидыЗапасов.Количество КАК Количество,
			|	""1_Списание"" КАК Операция
			|ПОМЕСТИТЬ КоличествоНоменклатурыПоВидамЗапасов
			|ИЗ
			|	Документ.СписаниеНедостачТоваров.ВидыЗапасов КАК ВидыЗапасов
			|ГДЕ
			|	ВидыЗапасов.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ВидыЗапасов.Ссылка.Склад = &Склад
			|	И ВидыЗапасов.Ссылка.Организация = &Организация
			|	И ВидыЗапасов.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Товары.АналитикаУчетаНоменклатуры,
			|	Товары.ВидЗапасов,
			|	Товары.Количество,
			|	""2_Оприходование""
			|ИЗ
			|	Документ.ОприходованиеИзлишковТоваров.Товары КАК Товары
			|ГДЕ
			|	Товары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И Товары.Ссылка.Склад = &Склад
			|	И Товары.Ссылка.Организация = &Организация
			|	И Товары.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ВидыЗапасов.АналитикаУчетаНоменклатуры,
			|	ВидыЗапасов.ВидЗапасов,
			|	ВидыЗапасов.Количество,
			|	""3_ПересортицаСписание""
			|ИЗ
			|	Документ.ПересортицаТоваров.ВидыЗапасов КАК ВидыЗапасов
			|ГДЕ
			|	ВидыЗапасов.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ВидыЗапасов.Ссылка.Склад = &Склад
			|	И ВидыЗапасов.Ссылка.Организация = &Организация
			|	И ВидыЗапасов.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Ключи.КлючАналитики,
			|	Товары.ВидЗапасов,
			|	Товары.Количество,
			|	""4_ПересортицаОприходование""
			|ИЗ
			|	Документ.ПересортицаТоваров.Товары КАК Товары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Ключи
			|		ПО Товары.НоменклатураОприходование = Ключи.Номенклатура
			|			И Товары.ХарактеристикаОприходование = Ключи.Характеристика
			|			И Товары.СерияОприходование = Ключи.Серия
			|			И Товары.НазначениеОприходование = Ключи.Назначение
			|			И Товары.Ссылка.Склад = Ключи.МестоХранения
			|ГДЕ
			|	Товары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И Товары.Ссылка.Склад = &Склад
			|	И Товары.Ссылка.Организация = &Организация
			|	И Товары.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ВидыЗапасов.АналитикаУчетаНоменклатуры,
			|	ВидыЗапасов.ВидЗапасов,
			|	ВидыЗапасов.Количество,
			|	""1_Списание""
			|ИЗ
			|	Документ.ПорчаТоваров.ВидыЗапасов КАК ВидыЗапасов
			|ГДЕ
			|	ВидыЗапасов.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ВидыЗапасов.Ссылка.Склад = &Склад
			|	И ВидыЗапасов.Ссылка.Организация = &Организация
			|	И ВидыЗапасов.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Аналитика.КлючАналитики,
			|	Товары.ВидЗапасов,
			|	Товары.Количество,
			|	""2_Оприходование""
			|ИЗ
			|	Документ.ПорчаТоваров.Товары КАК Товары
			|
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
			|		ПО Товары.НоменклатураОприходование = Аналитика.Номенклатура
			|			И Товары.ХарактеристикаОприходование = Аналитика.Характеристика
			|			И Товары.Серия = Аналитика.Серия
			|			И Товары.АналитикаУчетаНоменклатуры.Назначение = Аналитика.Назначение
			|			И Товары.Ссылка.Склад = Аналитика.МестоХранения
			|ГДЕ
			|	Товары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И Товары.Ссылка.Склад = &Склад
			|	И Товары.Ссылка.Организация = &Организация
			|	И Товары.Ссылка.Проведен
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЦеныНоменклатуры.АналитикаУчетаНоменклатуры,
			|	ЦеныНоменклатуры.ВидЗапасов,
			|	ЦеныНоменклатуры.Организация,
			|	(ЦеныНоменклатуры.СтоимостьРегл
			|		+ ЦеныНоменклатуры.ДопРасходыРегл
			|		+ ЦеныНоменклатуры.ТрудозатратыРегл
			|		+ ЦеныНоменклатуры.ПостатейныеПостоянныеРегл
			|		+ ЦеныНоменклатуры.ПостатейныеПеременныеРегл) КАК СтоимостьРегл
			|ПОМЕСТИТЬ ЦеныНоменклатуры
			|ИЗ
			|	РегистрСведений.СтоимостьТоваров.СрезПоследних(
			|			&ДатаОкончания,
			|			(АналитикаУчетаНоменклатуры, Организация, ВидЗапасов, РазделУчета) В
			|				(ВЫБРАТЬ
			|					Таблица.АналитикаУчетаНоменклатуры,
			|					&Организация,
			|					Таблица.ВидЗапасов,
			|					ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
			|				ИЗ
			|					КоличествоНоменклатурыПоВидамЗапасов КАК Таблица)) КАК ЦеныНоменклатуры
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
			|	КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
			|	КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Серия КАК Серия,
			|	ЕСТЬNULL(ЦеныНоменклатуры.СтоимостьРегл, 0) КАК Стоимость,
			|	СУММА(КоличествоНоменклатурыПоВидамЗапасов.Количество) КАК Количество,
			|	КоличествоНоменклатурыПоВидамЗапасов.Операция КАК Операция,
			|	КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Характеристика.НаименованиеПолное КАК ХарактеристикаПредставление,
			|	КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Серия.Наименование КАК СерияПредставление,
			|	КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
			|	КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
			|	КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
			|	КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.Код КАК ТоварКод,
			|	ЛОЖЬ КАК ЭтоВозвратнаяТара
			|ИЗ
			|	КоличествоНоменклатурыПоВидамЗапасов КАК КоличествоНоменклатурыПоВидамЗапасов
			|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныНоменклатуры КАК ЦеныНоменклатуры
			|		ПО КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры = ЦеныНоменклатуры.АналитикаУчетаНоменклатуры
			|			И КоличествоНоменклатурыПоВидамЗапасов.ВидЗапасов = ЦеныНоменклатуры.ВидЗапасов
			|			И (ЦеныНоменклатуры.Организация = &Организация)
			|
			|СГРУППИРОВАТЬ ПО
			|	КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Номенклатура,
			|	КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Характеристика,
			|	КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Серия,
			|	КоличествоНоменклатурыПоВидамЗапасов.Операция,
			|	КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Характеристика.НаименованиеПолное,
			|	КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.НаименованиеПолное,
			|	КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Код,
			|	КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Наименование,
			|	КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.Код,
			|	ЕСТЬNULL(ЦеныНоменклатуры.СтоимостьРегл, 0),
			|	КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Серия.Наименование
			|
			|УПОРЯДОЧИТЬ ПО
			|	ТоварНаименование,
			|	ХарактеристикаПредставление,
			|	СерияПредставление,
			|	Операция
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	&Руководитель КАК Руководитель,
			|	&ДолжностьРуководителя КАК ДолжностьРуководителя,
			|	&ГлавныйБухгалтер КАК ГлавныйБухгалтер,
			|	&Организация КАК Организация,
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).КодПоОКПО КАК ОрганизацияПоОКПО,
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).Префикс КАК Префикс,
			|	ИнвентаризационнаяОпись.Склад.ТекущийОтветственный КАК Кладовщик,
			|	ИнвентаризационнаяОпись.Склад.ТекущаяДолжностьОтветственного КАК ДолжностьКладовщика,
			|	ИнвентаризационнаяОпись.Склад.Наименование КАК ПодразделениеПредставление,
			|	&ДатаНачала КАК ДатаНачала,
			|	&ДатаОкончания КАК ДатаОкончания,
			|	ИнвентаризационнаяОпись.Дата КАК ОснованиеДата,
			|	ИнвентаризационнаяОпись.Номер КАК ОснованиеНомер,
			|	ИнвентаризационнаяОпись.Номер КАК НомерДокумента,
			|	ИнвентаризационнаяОпись.Дата КАК ДатаДокумента,
			|	ИнвентаризационнаяОпись.Ссылка КАК Ссылка,
			|	&ДатаОкончания КАК ДатаСнятияОстатков
			|ИЗ
			|	Документ.ИнвентаризационнаяОпись КАК ИнвентаризационнаяОпись
			|ГДЕ
			|	ИнвентаризационнаяОпись.Ссылка = &ДокументОснование
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Товары.Номенклатура КАК НоменклатураСписание,
			|	Товары.Характеристика КАК ХарактеристикаСписание,
			|	Товары.Серия КАК СерияСписание,
			|	Товары.НоменклатураОприходование КАК НоменклатураОприходование,
			|	Товары.ХарактеристикаОприходование КАК ХарактеристикаОприходование,
			|	Товары.СерияОприходование КАК СерияОприходование,
			|	СУММА(Товары.Количество) КАК Количество
			|ИЗ
			|	Документ.ПересортицаТоваров.Товары КАК Товары
			|ГДЕ
			|	Товары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И Товары.Ссылка.Склад = &Склад
			|	И Товары.Ссылка.Организация = &Организация
			|	И Товары.Ссылка.Проведен
			|
			|СГРУППИРОВАТЬ ПО
			|	Товары.Номенклатура,
			|	Товары.Характеристика,
			|	Товары.Серия,
			|	Товары.НоменклатураОприходование,
			|	Товары.ХарактеристикаОприходование,
			|	Товары.СерияОприходование";
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Номенклатура.Код", "Номенклатура." + КолонкаКодов);
			
			Запрос.Текст = ТекстЗапроса;
			
			Результаты = Запрос.ВыполнитьПакет();
			РезультатПоШапке = Результаты[3];
			РезультатПоТабличнойЧасти = Результаты[2];
			РезультатКурсВалюты = Неопределено;
			РезультатСвязанныеТовары = Результаты[4];
			
		Иначе
			// Фактический расчет
						
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	СписаниеНедостачТоваровВидЗапасов.Ссылка КАК Документ,
			|	СписаниеНедостачТоваровВидЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	СписаниеНедостачТоваровВидЗапасов.ВидЗапасов КАК ВидЗапасов,
			|	СписаниеНедостачТоваровВидЗапасов.Количество КАК Количество,
			|	""1_Списание"" КАК Операция
			|ПОМЕСТИТЬ КоличествоНоменклатурыПоВидамЗапасов
			|ИЗ
			|	Документ.СписаниеНедостачТоваров.ВидыЗапасов КАК СписаниеНедостачТоваровВидЗапасов
			|ГДЕ
			|	СписаниеНедостачТоваровВидЗапасов.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И СписаниеНедостачТоваровВидЗапасов.Ссылка.Склад = &Склад
			|	И СписаниеНедостачТоваровВидЗапасов.Ссылка.Организация = &Организация
			|	И СписаниеНедостачТоваровВидЗапасов.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ОприходованиеИзлишковТоваровТовары.Ссылка,
			|	ОприходованиеИзлишковТоваровТовары.АналитикаУчетаНоменклатуры,
			|	ОприходованиеИзлишковТоваровТовары.ВидЗапасов,
			|	ОприходованиеИзлишковТоваровТовары.Количество,
			|	""2_Оприходование""
			|ИЗ
			|	Документ.ОприходованиеИзлишковТоваров.Товары КАК ОприходованиеИзлишковТоваровТовары
			|ГДЕ
			|	ОприходованиеИзлишковТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Склад = &Склад
			|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Организация = &Организация
			|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ПересортицаТоваровТовары.Ссылка,
			|	ПересортицаТоваровТовары.АналитикаУчетаНоменклатуры,
			|	ПересортицаТоваровТовары.ВидЗапасов,
			|	ПересортицаТоваровТовары.Количество,
			|	""3_ПересортицаСписание""
			|ИЗ
			|	Документ.ПересортицаТоваров.ВидыЗапасов КАК ПересортицаТоваровТовары
			|ГДЕ
			|	ПересортицаТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ПересортицаТоваровТовары.Ссылка.Склад = &Склад
			|	И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
			|	И ПересортицаТоваровТовары.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ПересортицаТоваровТовары.Ссылка,
			|	Ключи.КлючАналитики,
			|	ПересортицаТоваровТовары.ВидЗапасов,
			|	ПересортицаТоваровТовары.Количество,
			|	""4_ПересортицаОприходование""
			|ИЗ
			|	Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Ключи
			|		ПО ПересортицаТоваровТовары.НоменклатураОприходование = Ключи.Номенклатура
			|			И ПересортицаТоваровТовары.ХарактеристикаОприходование = Ключи.Характеристика
			|			И ПересортицаТоваровТовары.НазначениеОприходование = Ключи.Назначение
			|			И ПересортицаТоваровТовары.СерияОприходование = Ключи.Серия
			|			И ПересортицаТоваровТовары.Ссылка.Склад = Ключи.МестоХранения
			|ГДЕ
			|	ПересортицаТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ПересортицаТоваровТовары.Ссылка.Склад = &Склад
			|	И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
			|	И ПересортицаТоваровТовары.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ПорчаТоваровВидыЗапасов.Ссылка,
			|	ПорчаТоваровВидыЗапасов.АналитикаУчетаНоменклатуры,
			|	ПорчаТоваровВидыЗапасов.ВидЗапасов,
			|	ПорчаТоваровВидыЗапасов.Количество,
			|	""1_Списание""
			|ИЗ
			|	Документ.ПорчаТоваров.ВидыЗапасов КАК ПорчаТоваровВидыЗапасов
			|ГДЕ
			|	ПорчаТоваровВидыЗапасов.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ПорчаТоваровВидыЗапасов.Ссылка.Склад = &Склад
			|	И ПорчаТоваровВидыЗапасов.Ссылка.Организация = &Организация
			|	И ПорчаТоваровВидыЗапасов.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Товары.Ссылка,
			|	Аналитика.КлючАналитики,
			|	Товары.ВидЗапасов,
			|	Товары.Количество,
			|	""2_Оприходование""
			|ИЗ
			|	Документ.ПорчаТоваров.Товары КАК Товары
			|	
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
			|		ПО Товары.НоменклатураОприходование = Аналитика.Номенклатура
			|			И Товары.ХарактеристикаОприходование = Аналитика.Характеристика
			|			И Товары.АналитикаУчетаНоменклатуры.Назначение = Аналитика.Назначение
			|			И Товары.Серия = Аналитика.Серия
			|			И Товары.Ссылка.Склад = Аналитика.МестоХранения
			|ГДЕ
			|	Товары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И Товары.Ссылка.Склад = &Склад
			|	И Товары.Ссылка.Организация = &Организация
			|	И Товары.Ссылка.Проведен
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТоварыКОформлениюОтчетовКомитенту.ВидЗапасов КАК ВидЗапасов,
			|	ТоварыКОформлениюОтчетовКомитенту.КорВидЗапасов КАК НовыйВидЗапасов,
			|	ТоварыКОформлениюОтчетовКомитенту.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
			|ПОМЕСТИТЬ ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров
			|ИЗ
			|	КоличествоНоменклатурыПоВидамЗапасов КАК КоличествоНоменклатурыПоВидамЗапасов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту КАК ТоварыКОформлениюОтчетовКомитенту
			|		ПО (ТоварыКОформлениюОтчетовКомитенту.ВидЗапасов = КоличествоНоменклатурыПоВидамЗапасов.ВидЗапасов)
			|			И (ТоварыКОформлениюОтчетовКомитенту.АналитикаУчетаНоменклатуры.Номенклатура = КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Номенклатура)
			|			И (ТоварыКОформлениюОтчетовКомитенту.АналитикаУчетаНоменклатуры.Характеристика = КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Характеристика)
			|			И КоличествоНоменклатурыПоВидамЗапасов.АналитикаУчетаНоменклатуры.Серия = ТоварыКОформлениюОтчетовКомитенту.АналитикаУчетаНоменклатуры.Серия
			|ГДЕ
			|	КоличествоНоменклатурыПоВидамЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТОвар)
			|	И ТоварыКОформлениюОтчетовКомитенту.КорВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТоварыИнвентаризации.Документ КАК Документ,
			|	ВЫБОР
			|		КОГДА ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.ВидЗапасов ЕСТЬ NULL 
			|			ТОГДА ТоварыИнвентаризации.АналитикаУчетаНоменклатуры
			|		ИНАЧЕ ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.АналитикаУчетаНоменклатуры
			|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
			|	ВЫБОР
			|		КОГДА ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.ВидЗапасов ЕСТЬ NULL 
			|			ТОГДА ТоварыИнвентаризации.ВидЗапасов
			|		ИНАЧЕ ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.НовыйВидЗапасов
			|	КОНЕЦ КАК ВидЗапасов,
			|	ТоварыИнвентаризации.Количество КАК Количество,
			|	ТоварыИнвентаризации.Операция КАК Операция
			|ПОМЕСТИТЬ ТаблицаИнвентаризации
			|ИЗ
			|	КоличествоНоменклатурыПоВидамЗапасов КАК ТоварыИнвентаризации
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров КАК ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров
			|		ПО (ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.ВидЗапасов = ТоварыИнвентаризации.ВидЗапасов)
			|			И (ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.АналитикаУчетаНоменклатуры.Номенклатура = ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура)
			|			И (ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.АналитикаУчетаНоменклатуры.Характеристика = ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика)
			|			И (ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.АналитикаУчетаНоменклатуры.Серия = ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Серия)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаИнвентаризации.Документ,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ТаблицаИнвентаризации.ВидЗапасов КАК ВидЗапасов,
			|	ВЫБОР
			|		КОГДА СУММА(ЕСТЬNULL(СебестоимостьТоваровПоРегистратору.Количество, 0)) = 0
			|			ТОГДА 0
			|		ИНАЧЕ СУММА(ЕСТЬNULL(СебестоимостьТоваровПоРегистратору.СтоимостьРегл, 0)
			|				+ ЕСТЬNULL(СебестоимостьТоваровПоРегистратору.ДопРасходыРегл, 0)
			|				+ ЕСТЬNULL(СебестоимостьТоваровПоРегистратору.ТрудозатратыРегл, 0)
			|				+ ЕСТЬNULL(СебестоимостьТоваровПоРегистратору.ПостатейныеПостоянныеРегл, 0)
			|				+ ЕСТЬNULL(СебестоимостьТоваровПоРегистратору.ПостатейныеПеременныеРегл, 0))
			|			/ СУММА(СебестоимостьТоваровПоРегистратору.Количество)
			|	КОНЕЦ КАК Цена
			|ПОМЕСТИТЬ ТаблицаЦен
			|ИЗ
			|	ТаблицаИнвентаризации КАК ТаблицаИнвентаризации
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваровПоРегистратору
			|		ПО ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры = СебестоимостьТоваровПоРегистратору.АналитикаУчетаНоменклатуры
			|			И ТаблицаИнвентаризации.ВидЗапасов = СебестоимостьТоваровПоРегистратору.ВидЗапасов
			|			И (СебестоимостьТоваровПоРегистратору.Регистратор = ТаблицаИнвентаризации.Документ)
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаИнвентаризации.Документ,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры,
			|	СебестоимостьТоваровПоРегистратору.АналитикаУчетаНоменклатуры,
			|	ТаблицаИнвентаризации.ВидЗапасов
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Серия КАК Серия,
			|	ЕСТЬNULL(ТаблицаЦен.Цена, 0) КАК Стоимость,
			|	СУММА(ТаблицаИнвентаризации.Количество) КАК Количество,
			|	ТаблицаИнвентаризации.Операция КАК Операция,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика.НаименованиеПолное КАК ХарактеристикаПредставление,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Серия.Наименование КАК СерияПредставление,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.Код КАК ТоварКод,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
			|	ЛОЖЬ КАК ЭтоВозвратнаяТара
			|ИЗ
			|	ТаблицаИнвентаризации КАК ТаблицаИнвентаризации
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЦен КАК ТаблицаЦен
			|		ПО ТаблицаИнвентаризации.ВидЗапасов = ТаблицаЦен.ВидЗапасов
			|			И ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры = ТаблицаЦен.АналитикаУчетаНоменклатуры
			|			И ТаблицаИнвентаризации.Документ = ТаблицаЦен.Документ
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Серия,
			|	ТаблицаИнвентаризации.Операция,
			|	ЕСТЬNULL(ТаблицаЦен.Цена, 0),
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика.НаименованиеПолное,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.НаименованиеПолное,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Код,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Серия.Наименование,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.Код,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Наименование
			|
			|УПОРЯДОЧИТЬ ПО
			|	ТоварНаименование,
			|	ХарактеристикаПредставление,
			|	СерияПредставление,
			|	Операция
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	&Руководитель КАК Руководитель,
			|	&ДолжностьРуководителя КАК ДолжностьРуководителя,
			|	&ГлавныйБухгалтер КАК ГлавныйБухгалтер,
			|	&Организация КАК Организация,
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).КодПоОКПО КАК ОрганизацияПоОКПО,
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).Префикс КАК Префикс,
			|	ИнвентаризационнаяОпись.Склад.ТекущийОтветственный КАК Кладовщик,
			|	ИнвентаризационнаяОпись.Склад.ТекущаяДолжностьОтветственного КАК ДолжностьКладовщика,
			|	ИнвентаризационнаяОпись.Склад.Наименование КАК ПодразделениеПредставление,
			|	&ДатаНачала КАК ДатаНачала,
			|	&ДатаОкончания КАК ДатаОкончания,
			|	ИнвентаризационнаяОпись.Дата КАК ОснованиеДата,
			|	ИнвентаризационнаяОпись.Номер КАК ОснованиеНомер,
			|	ИнвентаризационнаяОпись.Номер КАК НомерДокумента,
			|	ИнвентаризационнаяОпись.Дата КАК ДатаДокумента,
			|	ИнвентаризационнаяОпись.Ссылка КАК Ссылка,
			|	&ДатаОкончания КАК ДатаСнятияОстатков
			|ИЗ
			|	Документ.ИнвентаризационнаяОпись КАК ИнвентаризационнаяОпись
			|ГДЕ
			|	ИнвентаризационнаяОпись.Ссылка = &ДокументОснование
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Товары.Номенклатура КАК НоменклатураСписание,
			|	Товары.Характеристика КАК ХарактеристикаСписание,
			|	Товары.Серия КАК СерияСписание,
			|	Товары.НоменклатураОприходование КАК НоменклатураОприходование,
			|	Товары.ХарактеристикаОприходование КАК ХарактеристикаОприходование,
			|	Товары.СерияОприходование КАК СерияОприходование,
			|	СУММА(Товары.Количество) КАК Количество
			|ИЗ
			|	Документ.ПересортицаТоваров.Товары КАК Товары
			|ГДЕ
			|	Товары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И Товары.Ссылка.Склад = &Склад
			|	И Товары.Ссылка.Организация = &Организация
			|	И Товары.Ссылка.Проведен
			|
			|СГРУППИРОВАТЬ ПО
			|	Товары.Номенклатура,
			|	Товары.Характеристика,
			|	Товары.Серия,
			|	Товары.НоменклатураОприходование,
			|	Товары.ХарактеристикаОприходование,
			|	Товары.СерияОприходование";			
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Номенклатура.Код", "Номенклатура." + КолонкаКодов);
			
			Запрос.Текст = ТекстЗапроса;
			
			Результаты = Запрос.ВыполнитьПакет();
			РезультатПоШапке = Результаты[5];
			РезультатПоТабличнойЧасти = Результаты[4];
			РезультатКурсВалюты = Неопределено;
			РезультатСвязанныеТовары = Результаты[6];
			
		КонецЕсли;
				
	ИначеЕсли ПредварительныеДанныеРезультат.ИсточникИнформацииОЦенахДляПечати = Перечисления.ИсточникиИнформацииОЦенахДляПечати.ПоВидуЦен Тогда 
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	СписаниеНедостачТоваровВидЗапасов.Номенклатура КАК Номенклатура,
		|	СписаниеНедостачТоваровВидЗапасов.Характеристика КАК Характеристика,
		|	СписаниеНедостачТоваровВидЗапасов.Серия КАК Серия,
		|	СписаниеНедостачТоваровВидЗапасов.Количество КАК Количество,
		|	""1_Списание"" КАК Операция,
		|	NULL КАК СвязаннаяНоменклатура,
		|	NULL КАК СвязаннаяХарактеристика,
		|	NULL КАК СвязаннаяСерия
		|ПОМЕСТИТЬ ВложенныйЗапрос
		|ИЗ
		|	Документ.СписаниеНедостачТоваров.Товары КАК СписаниеНедостачТоваровВидЗапасов
		|ГДЕ
		|	СписаниеНедостачТоваровВидЗапасов.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И СписаниеНедостачТоваровВидЗапасов.Ссылка.Склад = &Склад
		|	И СписаниеНедостачТоваровВидЗапасов.Ссылка.Организация = &Организация
		|	И СписаниеНедостачТоваровВидЗапасов.Ссылка.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОприходованиеИзлишковТоваровТовары.Номенклатура,
		|	ОприходованиеИзлишковТоваровТовары.Характеристика,
		|	ОприходованиеИзлишковТоваровТовары.Серия,
		|	ОприходованиеИзлишковТоваровТовары.Количество,
		|	""2_Оприходование"",
		|	NULL,
		|	NULL,
		|	NULL
		|ИЗ
		|	Документ.ОприходованиеИзлишковТоваров.Товары КАК ОприходованиеИзлишковТоваровТовары
		|ГДЕ
		|	ОприходованиеИзлишковТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Склад = &Склад
		|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Организация = &Организация
		|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПересортицаТоваровТовары.Номенклатура,
		|	ПересортицаТоваровТовары.Характеристика,
		|	ПересортицаТоваровТовары.Серия,
		|	ПересортицаТоваровТовары.Количество,
		|	""3_ПересортицаСписание"",
		|	ПересортицаТоваровТовары.НоменклатураОприходование,
		|	ПересортицаТоваровТовары.ХарактеристикаОприходование,
		|	ПересортицаТоваровТовары.СерияОприходование
		|ИЗ
		|	Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
		|ГДЕ
		|	ПересортицаТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ПересортицаТоваровТовары.Ссылка.Склад = &Склад
		|	И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
		|	И ПересортицаТоваровТовары.Ссылка.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПересортицаТоваровТовары.НоменклатураОприходование,
		|	ПересортицаТоваровТовары.ХарактеристикаОприходование,
		|	ПересортицаТоваровТовары.СерияОприходование,
		|	ПересортицаТоваровТовары.Количество,
		|	""4_ПересортицаОприходование"",
		|	ПересортицаТоваровТовары.Номенклатура,
		|	ПересортицаТоваровТовары.Характеристика,
		|	ПересортицаТоваровТовары.Серия
		|ИЗ
		|	Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
		|ГДЕ
		|	ПересортицаТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ПересортицаТоваровТовары.Ссылка.Склад = &Склад
		|	И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
		|	И ПересортицаТоваровТовары.Ссылка.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПорчаТоваровТовары.Номенклатура,
		|	ПорчаТоваровТовары.Характеристика,
		|	ПорчаТоваровТовары.Серия,
		|	ПорчаТоваровТовары.Количество,
		|	""1_Списание"",
		|	NULL,
		|	NULL,
		|	NULL
		|ИЗ
		|	Документ.ПорчаТоваров.Товары КАК ПорчаТоваровТовары
		|ГДЕ
		|	ПорчаТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ПорчаТоваровТовары.Ссылка.Склад = &Склад
		|	И ПорчаТоваровТовары.Ссылка.Организация = &Организация
		|	И ПорчаТоваровТовары.Ссылка.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПорчаТоваровТовары.НоменклатураОприходование,
		|	ПорчаТоваровТовары.ХарактеристикаОприходование,
		|	ПорчаТоваровТовары.Серия,
		|	ПорчаТоваровТовары.Количество,
		|	""2_Оприходование"",
		|	NULL,
		|	NULL,
		|	NULL
		|ИЗ
		|	Документ.ПорчаТоваров.Товары КАК ПорчаТоваровТовары
		|ГДЕ
		|	ПорчаТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ПорчаТоваровТовары.Ссылка.Склад = &Склад
		|	И ПорчаТоваровТовары.Ссылка.Организация = &Организация
		|	И ПорчаТоваровТовары.Ссылка.Проведен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Номенклатура,
		|	ВложенныйЗапрос.Характеристика,
		|	ВложенныйЗапрос.Серия,
		|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
		|	ВложенныйЗапрос.Операция,
		|	ВложенныйЗапрос.СвязаннаяНоменклатура,
		|	ВложенныйЗапрос.СвязаннаяХарактеристика,
		|	ВложенныйЗапрос.СвязаннаяСерия,
		|	&ПоляДляЦенообразованияВыборка,
		|	&ВидЦены КАК ВидЦен
		|ПОМЕСТИТЬ ТаблицаТовары
		|ИЗ
		|	ВложенныйЗапрос КАК ВложенныйЗапрос
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
		|		ПО ВидыНоменклатуры.Ссылка = ВложенныйЗапрос.Номенклатура.ВидНоменклатуры
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Операция,
		|	ВложенныйЗапрос.СвязаннаяНоменклатура,
		|	ВложенныйЗапрос.СвязаннаяХарактеристика,
		|	ВложенныйЗапрос.СвязаннаяСерия,
		|	ВложенныйЗапрос.Номенклатура,
		|	ВложенныйЗапрос.Характеристика,
		|	&ПоляДляЦенообразованияГруппировка,
		|	ВложенныйЗапрос.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаТовары.Характеристика КАК Характеристика,
		|	ТаблицаТовары.Серия КАК Серия,
		|	ТаблицаТовары.Характеристика.НаименованиеПолное КАК ХарактеристикаПредставление,
		|	ТаблицаТовары.Серия.Наименование КАК СерияПредставление,
		|	ТаблицаТовары.Количество,
		|	ВЫБОР
		|		КОГДА НЕ &ТекстЗапросаКоэффициентУпаковки ЕСТЬ NULL 
		|				И &ТекстЗапросаКоэффициентУпаковки <> 0
		|			ТОГДА ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) / &ТекстЗапросаКоэффициентУпаковки
		|		ИНАЧЕ ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
		|	КОНЕЦ КАК Стоимость,
		|	ТаблицаТовары.Операция,
		|	ТаблицаТовары.СвязаннаяНоменклатура,
		|	ТаблицаТовары.СвязаннаяХарактеристика,
		|	ТаблицаТовары.СвязаннаяСерия,
		|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
		|	ТаблицаТовары.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
		|	ТаблицаТовары.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
		|	ТаблицаТовары.Номенклатура.Код КАК ТоварКод,
		|	ЛОЖЬ КАК ЭтоВозвратнаяТара
		|ИЗ
		|	ТаблицаТовары КАК ТаблицаТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ &ЦеныНоменклатурыСрезПоследних КАК ЦеныНоменклатуры
		|		ПО 
		|			&УсловиеСоедиенияЦеныНоменклатурыСрезПоследних
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТоварНаименование,
		|	ХарактеристикаПредставление,
		|	СерияПредставление,
		|	ТаблицаТовары.Операция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Руководитель КАК Руководитель,
		|	&ДолжностьРуководителя КАК ДолжностьРуководителя,
		|	&ГлавныйБухгалтер КАК ГлавныйБухгалтер,
		|	&Организация КАК Организация,
		|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).КодПоОКПО КАК ОрганизацияПоОКПО,
		|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).Префикс КАК Префикс,
		|	ИнвентаризационнаяОпись.Склад.ТекущийОтветственный КАК Кладовщик,
		|	ИнвентаризационнаяОпись.Склад.ТекущаяДолжностьОтветственного КАК ДолжностьКладовщика,
		|	ИнвентаризационнаяОпись.Склад.Наименование КАК ПодразделениеПредставление,
		|	&ДатаНачала КАК ДатаНачала,
		|	&ДатаОкончания КАК ДатаОкончания,
		|	ИнвентаризационнаяОпись.Дата КАК ОснованиеДата,
		|	ИнвентаризационнаяОпись.Номер КАК ОснованиеНомер,
		|	ИнвентаризационнаяОпись.Номер КАК НомерДокумента,
		|	ИнвентаризационнаяОпись.Дата КАК ДатаДокумента,
		|	ИнвентаризационнаяОпись.Ссылка КАК Ссылка,
		|	&ДатаОкончания КАК ДатаСнятияОстатков
		|ИЗ
		|	Документ.ИнвентаризационнаяОпись КАК ИнвентаризационнаяОпись
		|ГДЕ
		|	ИнвентаризационнаяОпись.Ссылка = &ДокументОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	КурсыВалютСрезПоследних.КурсЧислитель / КурсыВалютСрезПоследних.КурсЗнаменатель КАК КоэффициентПересчета
		|ИЗ
		|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(
		|			&ДатаОкончания,
		|			Валюта В
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					ВЫРАЗИТЬ(Таблица.ВидЦен КАК Справочник.ВидыЦен).ВалютаЦены
		|				ИЗ
		|					ТаблицаТовары КАК Таблица)
		|				И БазоваяВалюта = &БазоваяВалюта) КАК КурсыВалютСрезПоследних";
		
		ИспользуетсяЦенообразование25 = ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25(ДатаОкончания);
		
		НастройкаЦенообразования = ЦенообразованиеКлиентСервер.НастройкиДляВременнойТаблицыСДополнениемДляЦенообразования();
		НастройкаЦенообразования.ИсточникТоваров = "ВложенныйЗапрос";
		НастройкаЦенообразования.ПолеУпаковка = "";
		
		ТекстЗамены = ЦенообразованиеКлиентСервер.ТекстПолейДляЦенообразования(НастройкаЦенообразования,
																				,
																				ИспользуетсяЦенообразование25);
		Если ЗначениеЗаполнено(ТекстЗамены) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляЦенообразованияВыборка", ТекстЗамены);
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляЦенообразованияВыборка,", "");
		КонецЕсли;
		
		ТекстЗамены = ЦенообразованиеКлиентСервер.ТекстПолейДляЦенообразования(НастройкаЦенообразования, 
																				Истина,
																				ИспользуетсяЦенообразование25);
		Если ЗначениеЗаполнено(ТекстЗамены) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляЦенообразованияГруппировка", ТекстЗамены);
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляЦенообразованияГруппировка,", "");
		КонецЕсли;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ЦеныНоменклатурыСрезПоследних",
			ЦенообразованиеКлиентСервер.ТекстЗапросаРегистрСведенийЦеныНоменклатуры(
								"ТаблицаТовары",
								"&ДатаОкончания",
								Новый Структура("ВТаблице", "ВидЦен"), ИспользуетсяЦенообразование25));
	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&УсловиеСоедиенияЦеныНоменклатурыСрезПоследних",
			ЦенообразованиеКлиентСервер.ТекстЗапросаРегистрСведенийЦеныНоменклатурыУсловиеСоединения(
			"ТаблицаТовары",
			"ЦеныНоменклатуры",,ИспользуетсяЦенообразование25));

		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"&ТекстЗапросаКоэффициентУпаковки",
				Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
				"ЦеныНоменклатуры.Упаковка",
				"ЦеныНоменклатуры.Номенклатура"));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Номенклатура.Код", "Номенклатура." + КолонкаКодов);
		
		Запрос.УстановитьПараметр("БазоваяВалюта", ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация));
		
		Запрос.Текст = ТекстЗапроса;
		
		Результаты = Запрос.ВыполнитьПакет();
		РезультатПоШапке = Результаты[3];
		РезультатПоТабличнойЧасти = Результаты[2];
		РезультатКурсВалюты = Результаты[4];
		РезультатСвязанныеТовары = Неопределено;
		
	КонецЕсли;
		
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("РезультатПоШапке",			РезультатПоШапке);
	СтруктураВозврата.Вставить("РезультатПоТабличнойЧасти", РезультатПоТабличнойЧасти);
	СтруктураВозврата.Вставить("РезультатКурсВалюты", 		РезультатКурсВалюты);
	СтруктураВозврата.Вставить("РезультатСвязанныеТовары",  РезультатСвязанныеТовары);
	
	Возврат СтруктураВозврата
	
КонецФункции

// Функция получает данные для формирования печатной формы ИНВ3
//
// Параметры:
//	ПараметрыПечати - Структура
//	ДокументОснование - ДокументСсылка - Документ, по которому необходимо получить данные.
//
// Возвращаемое значение:
// 	Структура:
// 		* РезультатПоШапке - РезультатЗапроса
// 		* РезультатКурсВалюты - РезультатЗапроса
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса
//
Функция ПолучитьДанныеДляПечатнойФормыИНВ3(ПараметрыПечати, ДокументОснование) Экспорт
	
	ДанныеДокумента = Документы.ИнвентаризационнаяОпись.ПолучитьДанныеДокумента(ПараметрыПечати, ДокументОснование);
	
	Если ДанныеДокумента.ОписаниеОшибки <> Неопределено Тогда
		Если ДанныеДокумента.ОписаниеОшибки = "НетИнвентаризацийВПериоде" Тогда
			ТекстСообщения = НСтр("ru = 'Печать %Документ% не выполнена: в указанный инвентаризационный период не найдены пересчеты товаров.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", Строка(ДокументОснование));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,ДокументОснование);
		ИначеЕсли ДанныеДокумента.ОписаниеОшибки = "НетЦенПоСебестоимости" Тогда
			ТекстСообщения = НСтр("ru = 'Не удалось получить цены по себестоимости для документа %Документ%: с %ПериодС% по %ПериодПо% не произведен расчет себестоимости.'");
			
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", ДокументОснование);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПериодС%", Формат(НачалоМесяца(ДанныеДокумента.РезультатПоШапке.ДатаНачала),"ДЛФ=DD"));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПериодПо%", Формат(КонецМесяца(ДанныеДокумента.РезультатПоШапке.ДатаОкончания),"ДЛФ=DD"));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		Возврат Неопределено;
	КонецЕсли;
	
	Если ДанныеДокумента.ПоПредварительномуРасчету Тогда
		ТекстСообщения = НСтр("ru = 'При печати документа %Документ% использовались данные предварительного расчета себестоимости.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", ДокументОснование);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Возврат Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти, РезультатКурсВалюты",
		ДанныеДокумента.РезультатПоШапке,
		ДанныеДокумента.РезультатПоТабличнойЧасти,
		ДанныеДокумента.РезультатКурсВалюты);
	
КонецФункции
//-- Локализация
#КонецОбласти


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
