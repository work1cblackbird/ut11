
#Область ПрограммныйИнтерфейс

#Область ПроцедурыИФункцииРаботыСВалютами

// Функция получает коэффициент пересчета из текущей валюты в новую валюту.
//
// Параметры:
//	ТекущаяВалюта - СправочникСсылка.Валюты - Текущая валюта документа
//	НоваяВалюта - СправочникСсылка.Валюты - Новая валюта документа
//	Дата - Дата - Дата документа.
//	БазоваяВалюта - СправочникСсылка.Валюты - Базовая валюта, относительно которой необходимо получить курс.
//											Если Неопределено, то курсы получаются относительно значения из константы БазоваяВалютаПоУмолчанию.
//	ПараметрыВариантаКурсаДоговора - См. РаботаСКурсамиВалютУТ.ПараметрыВариантаКурсаДоговора
//
// Возвращаемое значение:
//	Число - Коэффициент пересчета в новую валюту.
//
Функция ПолучитьКоэффициентПересчетаИзВалютыВВалюту(ТекущаяВалюта, НоваяВалюта, Дата, Знач БазоваяВалюта = Неопределено, ПараметрыВариантаКурсаДоговора = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(БазоваяВалюта) Тогда
		БазоваяВалюта = ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию();
	КонецЕсли;
	
	Если ПараметрыВариантаКурсаДоговора <> Неопределено 
		И ПараметрыВариантаКурсаДоговора.ВариантКурсаДоговора = Перечисления.ВариантыКурсаДоговора.УстановленныйВДоговоре 
		И (ПараметрыВариантаКурсаДоговора.ВалютаВзаиморасчетов = ТекущаяВалюта 
			ИЛИ ПараметрыВариантаКурсаДоговора.ВалютаВзаиморасчетов = НоваяВалюта) Тогда
			Если ТекущаяВалюта <> НоваяВалюта Тогда
				Если ТекущаяВалюта = ПараметрыВариантаКурсаДоговора.ВалютаВзаиморасчетов Тогда
					КурсТекущейВалюты = РегистрыСведений.КурсыВалютРасчетовПоДоговорам.ПолучитьЗначенияКурсаВалютыДоговора(ПараметрыВариантаКурсаДоговора.Договор, Дата);
				Иначе
					КурсТекущейВалюты = ПолучитьКурсВалюты(ТекущаяВалюта, Дата, БазоваяВалюта);
				КонецЕсли;
				Если НоваяВалюта = ПараметрыВариантаКурсаДоговора.ВалютаВзаиморасчетов Тогда
					КурсНовойВалюты = РегистрыСведений.КурсыВалютРасчетовПоДоговорам.ПолучитьЗначенияКурсаВалютыДоговора(ПараметрыВариантаКурсаДоговора.Договор, Дата);
				Иначе
					КурсНовойВалюты = ПолучитьКурсВалюты(НоваяВалюта, Дата, БазоваяВалюта);
				КонецЕсли;
				Если КурсНовойВалюты.КурсЧислитель * КурсТекущейВалюты.КурсЗнаменатель <> 0 Тогда
					КоэффициентПересчета = 
						(КурсТекущейВалюты.КурсЧислитель * КурсНовойВалюты.КурсЗнаменатель) 
						/ (КурсНовойВалюты.КурсЧислитель * КурсТекущейВалюты.КурсЗнаменатель);
				Иначе
					КоэффициентПересчета = 0;
				КонецЕсли;
			Иначе
				КоэффициентПересчета = 1;
			КонецЕсли;
	Иначе
		ОбъектРасчетов = Справочники.ОбъектыРасчетов.ПустаяСсылка();
		Если ПараметрыВариантаКурсаДоговора <> Неопределено Тогда
			ОбъектРасчетов = ПараметрыВариантаКурсаДоговора.ОбъектРасчетов;
		КонецЕсли;
		Если ТекущаяВалюта <> НоваяВалюта Тогда
			КурсТекущейВалюты = ПолучитьКурсВалюты(ТекущаяВалюта, Дата, БазоваяВалюта, ОбъектРасчетов);
			КурсНовойВалюты = ПолучитьКурсВалюты(НоваяВалюта, Дата, БазоваяВалюта, ОбъектРасчетов);
			Если КурсНовойВалюты.КурсЧислитель * КурсТекущейВалюты.КурсЗнаменатель <> 0 Тогда
				КоэффициентПересчета = 
					(КурсТекущейВалюты.КурсЧислитель * КурсНовойВалюты.КурсЗнаменатель) 
					/ (КурсНовойВалюты.КурсЧислитель * КурсТекущейВалюты.КурсЗнаменатель);
			Иначе
				КоэффициентПересчета = 0;
			КонецЕсли;
		Иначе
			КоэффициентПересчета = 1;
		КонецЕсли;
	КонецЕсли;
	
	Возврат КоэффициентПересчета;
	
КонецФункции // ПолучитьКоэффициентПересчетаИзВалютыВВалюту()

// Возвращает курс валюты на дату.
//
// Параметры:
//   Валюта    - СправочникСсылка.Валюты - валюта, для которой получается курс.
//   ДатаКурса - Дата - дата, на которую получается курс.
//   БазоваяВалюта - СправочникСсылка.Валюты - Валюта, относительно которой необходимо получить курс
//   ОбъектРасчетов - СправочникСсылка.ОбъектыРасчетов - Необязательный, объект расчетов, по которому определяется вариант курса договора
//   
// Возвращаемое значение: 
//   Структура:
//    * КурсЧислитель - Число - Числитель курса валюты на указанную дату.
//    * КурсЗнаменатель - Число - Знаменатель курса валюты на указанную дату.
//    * Валюта    - СправочникСсылка.Валюты - ссылка валюты.
//    * ДатаКурса - Дата - дата получения курса.
//    * БазоваяВалюта - СправочникСсылка.Валюты - Валюта, относительно которой получены  курс.
//
Функция ПолучитьКурсВалюты(Знач Валюта, Знач ДатаКурса, Знач БазоваяВалюта = Неопределено, ОбъектРасчетов = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(БазоваяВалюта) Тогда
		БазоваяВалюта = ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию();
	КонецЕсли;
	
	Если Валюта = БазоваяВалюта Тогда
		Результат = Новый Структура();
		Результат.Вставить("Валюта",    Валюта);
		Результат.Вставить("ДатаКурса", ДатаКурса);
		Результат.Вставить("БазоваяВалюта", БазоваяВалюта);
		Результат.Вставить("КурсЧислитель", 1);
		Результат.Вставить("КурсЗнаменатель", 1);
	ИначеЕсли ЗначениеЗаполнено(ОбъектРасчетов) Тогда
		КурсыВалют = ПолучитьКурсВалютыПоОбъектамРасчетов(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Валюта),
			ДатаКурса,
			БазоваяВалюта,
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОбъектРасчетов));
		Результат = Новый Структура();
		Результат.Вставить("Валюта",    Валюта);
		Результат.Вставить("ДатаКурса", ДатаКурса);
		Результат.Вставить("БазоваяВалюта", БазоваяВалюта);
		Результат.Вставить("КурсЧислитель", 0);
		Результат.Вставить("КурсЗнаменатель", 0);
		Если КурсыВалют.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(Результат, КурсыВалют[0]);
		КонецЕсли;
	Иначе
		Результат = РегистрыСведений.ОтносительныеКурсыВалют.ПолучитьПоследнее(ДатаКурса, Новый Структура("Валюта, БазоваяВалюта", Валюта, БазоваяВалюта));
		Результат.Вставить("Валюта",    Валюта);
		Результат.Вставить("ДатаКурса", ДатаКурса);
		Результат.Вставить("БазоваяВалюта", БазоваяВалюта);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция получает коэффициенты пересчета сумм из валюты документа в валюту взаиморасчетов,
// в валюты управленческого и регламентированного учета.
//
// Параметры:
//	ВалютаДокумента - СправочникСсылка.Валюты - Валюта документа
//	ВалютаВзаиморасчетов - СправочникСсылка.Валюты - Валюта взаиморасчетов документа
//	Период - Дата - Дата документа
//	Организация - СправочникСсылка.Организации - Организация документа
//	КурсЧислительДокумента - Число - Необязательный, курс-числитель валюты документа
//	КурсЗнаменательДокумента - Число - Необязательный, курс-знаменатель валюты документа.
//	ОбъектРасчетов - СправочникСсылка.ОбъектыРасчетов - Необязательный,Объект расчетов для определения источника курса валюты.
//
// Возвращаемое значение:
//   Структура - Параметры курса валюты:
//	   * КоэффициентПересчетаВВалютуВзаиморасчетов 	- Число - Коэффициент пересчета в валюту взаиморасчетов.
//	   * КоэффициентПересчетаВВалютуУПР 			- Число - Коэффициент пересчета в валюту управленческого учета.
//	   * КоэффициентПересчетаВВалютуРегл 			- Число - Коэффициент пересчета в валюту регламентированного учета.
//
Функция ПолучитьКоэффициентыПересчетаВалюты(ВалютаДокумента, ВалютаВзаиморасчетов, Период, Организация, КурсЧислительДокумента = Неопределено, КурсЗнаменательДокумента = Неопределено, ОбъектРасчетов = Неопределено) Экспорт

	ВалютаУпр  = Константы.ВалютаУправленческогоУчета.Получить();
	ВалютаРегл = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
	
	Запрос = Новый Запрос;
	Если ЗначениеЗаполнено(ОбъектРасчетов) Тогда
		МассивВалют = Новый Массив;
		МассивВалют.Добавить(ВалютаДокумента);
		МассивВалют.Добавить(ВалютаВзаиморасчетов);
		МассивВалют.Добавить(ВалютаУпр);
		МассивВалют.Добавить(ВалютаРегл);
		ТаблицаКурсов = ПолучитьКурсВалютыПоОбъектамРасчетов(
			МассивВалют, 
			Период, 
			ВалютаРегл, 
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОбъектРасчетов));
	Иначе
		ТекстЗапроса = "
			|ВЫБРАТЬ
			|	ОтносительныеКурсыВалют.Валюта КАК Валюта,
			|	ОтносительныеКурсыВалют.КурсЧислитель КАК КурсЧислитель,
			|	ОтносительныеКурсыВалют.КурсЗнаменатель КАК КурсЗнаменатель
			|ИЗ
			|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&Период,
			|		(Валюта = &ВалютаУпр ИЛИ Валюта = &ВалютаРегл ИЛИ Валюта = &ВалютаВзаиморасчетов ИЛИ Валюта = &ВалютаДокумента) И БазоваяВалюта = &ВалютаРегл
			|	) КАК ОтносительныеКурсыВалют
			|";
		Запрос.УстановитьПараметр("ВалютаДокумента",      ВалютаДокумента);
		Запрос.УстановитьПараметр("ВалютаВзаиморасчетов", ВалютаВзаиморасчетов);
		Запрос.УстановитьПараметр("Период",     Период);
		Запрос.УстановитьПараметр("ВалютаУпр",  ВалютаУпр);
		Запрос.УстановитьПараметр("ВалютаРегл", ВалютаРегл);
		Запрос.Текст = ТекстЗапроса;
		ТаблицаКурсов = Запрос.Выполнить().Выгрузить();
	КонецЕсли;

	КурсЧислительВалютыУпр = 1;
	КурсЗнаменательВалютыУпр = 1;

	КурсЧислительВалютыРегл = 1;
	КурсЗнаменательВалютыРегл = 1;

	КурсЧислительВзаиморасчетов = 1;
	КурсЗнаменательВзаиморасчетов = 1;

	КурсЧислительВалютыДокумента = 1;
	КурсЗнаменательВалютыДокумента = 1;

	Для Каждого СтрокаКурса Из ТаблицаКурсов Цикл

		Если СтрокаКурса.Валюта = ВалютаУпр Тогда

			КурсЧислительВалютыУпр = СтрокаКурса.КурсЧислитель;
			КурсЗнаменательВалютыУпр = СтрокаКурса.КурсЗнаменатель;

		КонецЕсли;

		Если СтрокаКурса.Валюта = ВалютаРегл Тогда

			КурсЧислительВалютыРегл = СтрокаКурса.КурсЧислитель;
			КурсЗнаменательВалютыРегл = СтрокаКурса.КурсЗнаменатель;

		КонецЕсли;

		Если СтрокаКурса.Валюта = ВалютаВзаиморасчетов Тогда

			КурсЧислительВзаиморасчетов = СтрокаКурса.КурсЧислитель;
			КурсЗнаменательВзаиморасчетов = СтрокаКурса.КурсЗнаменатель;

		КонецЕсли;

		Если СтрокаКурса.Валюта = ВалютаДокумента Тогда

			КурсЧислительВалютыДокумента = СтрокаКурса.КурсЧислитель;
			КурсЗнаменательВалютыДокумента = СтрокаКурса.КурсЗнаменатель;

		КонецЕсли;
	КонецЦикла;

	Если ЗначениеЗаполнено(КурсЧислительДокумента) Тогда
		
		Если ВалютаДокумента = ВалютаРегл И НЕ ВалютаВзаиморасчетов = ВалютаРегл Тогда
			КурсЧислительВзаиморасчетов = КурсЧислительДокумента;
			КурсЗнаменательВзаиморасчетов = КурсЗнаменательДокумента;
			КурсЧислительВалютыДокумента = 1;
			КурсЗнаменательВалютыДокумента = 1;
		Иначе
			КурсЧислительВалютыДокумента = КурсЧислительДокумента;
			КурсЗнаменательВалютыДокумента = КурсЗнаменательДокумента;
			КурсЧислительВзаиморасчетов = 1;
			КурсЗнаменательВзаиморасчетов = 1;
		КонецЕсли;
		
	КонецЕсли;
	
	Результат = Новый Структура("КоэффициентПересчетаВВалютуВзаиморасчетов, КоэффициентПересчетаВВалютуУПР, КоэффициентПересчетаВВалютуРегл");

	Результат.КоэффициентПересчетаВВалютуУпр  = КурсЧислительВалютыДокумента * КурсЗнаменательВалютыУпр / (КурсЗнаменательВалютыДокумента * КурсЧислительВалютыУпр); 
	Результат.КоэффициентПересчетаВВалютуРегл = КурсЧислительВалютыДокумента * КурсЗнаменательВалютыРегл / (КурсЗнаменательВалютыДокумента * КурсЧислительВалютыРегл);
	Результат.КоэффициентПересчетаВВалютуВзаиморасчетов = КурсЧислительВалютыДокумента * КурсЗнаменательВзаиморасчетов / (КурсЗнаменательВалютыДокумента * КурсЧислительВзаиморасчетов);

	Возврат Результат;

КонецФункции

// Функция получает коэффициент пересчета из текущей валюты в новую валюту.
//
// Параметры:
//	ТекущаяВалюта - СправочникСсылка.Валюты - Текущая валюта документа
//	НоваяВалюта - СправочникСсылка.Валюты - Новая валюта документа
//	Дата - Дата - Дата документа.
//	БазоваяВалюта - СправочникСсылка.Валюты - Базовая валюта, относительно которой необходимо получить курс.
//											Если Неопределено, то курсы получаются относительно значения из константы БазоваяВалютаПоУмолчанию.
//	ОбъектыРасчетов - Массив из СправочникСсылка.ОбъектыРасчетов
//
// Возвращаемое значение:
//	ТаблицаЗначений - ТаблицаКоэффициентов:
//		* ОбъектРасчетов - СправочникСсылка.ОбъектыРасчетов - 
//		* Коэффициент - Число - 
//
Функция ПолучитьКоэффициентыПересчетаПоОбъектамРасчетов(ТекущаяВалюта, НоваяВалюта, Дата, Знач БазоваяВалюта = Неопределено, ОбъектыРасчетов) Экспорт
	
	Если Не ЗначениеЗаполнено(БазоваяВалюта) Тогда
		БазоваяВалюта = ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию();
	КонецЕсли;
	
	ТаблицаКоэффициентов = Новый ТаблицаЗначений;
	ТаблицаКоэффициентов.Колонки.Добавить("ОбъектРасчетов", Новый ОписаниеТипов("СправочникСсылка.ОбъектыРасчетов"));
	ТаблицаКоэффициентов.Колонки.Добавить("Коэффициент", Новый ОписаниеТипов("Число"));
	МассивВалют = Новый Массив;
	МассивВалют.Добавить(ТекущаяВалюта);
	МассивВалют.Добавить(НоваяВалюта);
	ТаблицаКурсов = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыПоОбъектамРасчетов(
		МассивВалют, 
		Дата,
		БазоваяВалюта,
		ОбъектыРасчетов);
	ТаблицаКурсов.Индексы.Добавить("ОбъектРасчетов");
	Для Каждого ОбъектРасчетов Из ОбъектыРасчетов Цикл
		СтрокиВалют = ТаблицаКурсов.НайтиСтроки(Новый Структура("ОбъектРасчетов", ОбъектРасчетов));
		КурсЧислительТекущейВалюты   = 1;
		КурсЗнаменательТекущейВалюты = 1;
		КурсЧислительНовойВалюты     = 1;
		КурсЗнаменательНовойВалюты   = 1;
		Для Каждого СтрокаКурса Из СтрокиВалют Цикл
			Если СтрокаКурса.Валюта = ТекущаяВалюта Тогда
				КурсЧислительТекущейВалюты   = СтрокаКурса.КурсЧислитель;
				КурсЗнаменательТекущейВалюты = СтрокаКурса.КурсЗнаменатель;
			КонецЕсли;
			Если СтрокаКурса.Валюта = НоваяВалюта Тогда
				КурсЧислительНовойВалюты   = СтрокаКурса.КурсЧислитель;
				КурсЗнаменательНовойВалюты = СтрокаКурса.КурсЗнаменатель;
			КонецЕсли;
		КонецЦикла;
		НоваяСтрока = ТаблицаКоэффициентов.Добавить();
		НоваяСтрока.ОбъектРасчетов = ОбъектРасчетов;
		НоваяСтрока.Коэффициент = 
			(КурсЧислительТекущейВалюты * КурсЗнаменательНовойВалюты) 
				/ (КурсЧислительНовойВалюты * КурсЗнаменательТекущейВалюты);
	КонецЦикла;
	
	Возврат ТаблицаКоэффициентов;
	
КонецФункции

// Возвращает курс валюты на дату.
//
// Параметры:
//   Валюта    - Массив из СправочникСсылка.Валюты - валюты, для которых получается курс.
//   ДатаКурса - Дата - дата, на которую получается курс.
//   БазоваяВалюта - СправочникСсылка.Валюты - Валюта, относительно которой необходимо получить курс
//   ОбъектРасчетов - Массив из СправочникСсылка.ОбъектыРасчетов - Объекты расчетов, по которым определяется вариант курса договора
//   
// Возвращаемое значение: 
//   ТаблицаЗначений:
//    * КурсЧислитель    - Число - Числитель курса валюты на указанную дату.
//    * КурсЗнаменатель  - Число - Знаменатель курса валюты на указанную дату.
//    * Валюта           - СправочникСсылка.Валюты - ссылка валюты.
//    * ОбъектРасчетов   - СправочникСсылка.ОбъектыРасчетов - Объект расчетов, по которому определен курс
//    * ДатаКурса        - Дата - дата получения курса.
//    * БазоваяВалюта    - СправочникСсылка.Валюты - Валюта, относительно которой получены  курс.
//
Функция ПолучитьКурсВалютыПоОбъектамРасчетов(Валюты, ДатаКурса, Знач БазоваяВалюта = Неопределено, ОбъектыРасчетов) Экспорт
	
	Если Не ЗначениеЗаполнено(БазоваяВалюта) Тогда
		БазоваяВалюта = ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаКурсовПоОбъектуРасчетов();
		Запрос.УстановитьПараметр("ВалютаУпр", ЗначениеНастроекПовтИсп.ВалютаУправленческогоУчета());
	Запрос.УстановитьПараметр("ОбъектыРасчетов", ОбъектыРасчетов);
	Запрос.УстановитьПараметр("Период", ДатаКурса);
	Запрос.УстановитьПараметр("МассивВалют", Валюты);
	Запрос.УстановитьПараметр("ВалютаРегл", БазоваяВалюта);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Функция пересчитывает сумму документа из текущей валюты в новую валюту.
//
// Параметры:
//	СуммаДокумента - Число - Текущая сумма документа
//	ТекущаяВалюта - СправочникСсылка.Валюты - Текущая валюта документа
//	НоваяВалюта - СправочникСсылка.Валюты - Новая валюта документа
//	Дата - Дата - Дата документа.
//	БазоваяВалюта - СправочникСсылка.Валюты - Валюта, относительно которой необходимо получать курсы.
//
// Возвращаемое значение:
//	Число - Новая сумма документа
//
Функция ПересчитатьСуммуДокументаВВалюту(СуммаДокумента, ТекущаяВалюта, НоваяВалюта, Дата, БазоваяВалюта) Экспорт
	
	СтруктураКурсовТекущейВалюты = ПолучитьКурсВалюты(ТекущаяВалюта, Дата, БазоваяВалюта);
	СтруктураКурсовНовойВалюты = ПолучитьКурсВалюты(НоваяВалюта, Дата, БазоваяВалюта);
	
	НоваяСуммаДокумента = ПересчитатьПоКурсу(СуммаДокумента, СтруктураКурсовТекущейВалюты, СтруктураКурсовНовойВалюты);
	
	Возврат НоваяСуммаДокумента;
	
КонецФункции // ПересчитатьСуммуДокументаВВалюту()

// Возвращает курс валюты управленческого учета относительно валюты регламентированного.
//
// Параметры:
//  Дата - Дата - Дата на которую необходимо получить курс валюты.
//  ВалютаРегламентированногоУчета - СправочникСсылка.Валюты - Валюта регламентированного учета.
//  
// Возвращаемое значение:
//  Число - Курс валюты управленческого учета
//
Функция ПолучитьКурсВалютыУправленческогоУчета(Дата, ВалютаРегламентированногоУчета) Экспорт

	Отбор = Новый Структура("Валюта, БазоваяВалюта", 
				Константы.ВалютаУправленческогоУчета.Получить(), ВалютаРегламентированногоУчета);
	СтруктураКурса  = РегистрыСведений.ОтносительныеКурсыВалют.ПолучитьПоследнее(Дата, Отбор);
	Курс = СтруктураКурса.КурсЧислитель / СтруктураКурса.КурсЗнаменатель;
	Возврат Курс;

КонецФункции

// Процедура заполняет КурсЧислитель и КурсЗнаменатель документа по умолчанию
//
// Параметры:
//	КурсЧислитель - Число - КурсЧислитель для расчета
//	КурсЗнаменатель - Число - КурсЗнаменатель для расчета
//	ВалютаДокумента - СправочникСсылка.Валюты - Валюта документа
//	ВалютаВзаиморасчетов - СправочникСсылка.Валюты - Валюта взаиморасчетов документа
//	Организация - СправочникСсылка.Организации - Организация документа
//	Дата - Дата - Необязательный, дата документа.
//	Договор - СправочникСсылка.ДоговорыКонтрагентов, СправочникСсылка.ДоговорыМеждуОрганизациями - Договор для проверки на ниличие курса
//	ОбъектРасчетов - СправочникСсылка.ОбъектыРасчетов - ОбъектРасчетов, по которому определяется курс валюты
//
Процедура ЗаполнитьКурсДокументаПоУмолчанию(КурсЧислитель, КурсЗнаменатель, ВалютаДокумента, ВалютаВзаиморасчетов, Организация, Дата = Неопределено, Договор = Неопределено, ОбъектРасчетов = Неопределено) Экспорт
	
	ВалютаРеглУчета = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
	Если ЗначениеЗаполнено(Организация) Тогда
		БазоваяВалюта = ВалютаРеглУчета;
	Иначе
		БазоваяВалюта = Константы.БазоваяВалютаПоУмолчанию.Получить();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ВариантКурсаДоговора = Перечисления.ВариантыКурсаДоговора.Переменный;
	Если ЗначениеЗаполнено(Договор) Тогда
		ВариантКурсаДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВариантКурсаДоговора");
	КонецЕсли;
	
	Если ВалютаДокумента = ВалютаВзаиморасчетов 
		ИЛИ НЕ ЗначениеЗаполнено(ВалютаДокумента) 
		ИЛИ НЕ ЗначениеЗаполнено(ВалютаВзаиморасчетов) Тогда
		
		КурсЧислитель = 1;
		КурсЗнаменатель = 1;
		Возврат;
		
	ИначеЕсли ВалютаДокумента = ВалютаРеглУчета И НЕ ВалютаВзаиморасчетов = ВалютаРеглУчета Тогда
		
		Если ВариантКурсаДоговора = Перечисления.ВариантыКурсаДоговора.УстановленныйВДоговоре Тогда
			СтруктураКурса = РегистрыСведений.КурсыВалютРасчетовПоДоговорам.ПолучитьЗначенияКурсаВалютыДоговора(Договор, Дата);
		Иначе
			СтруктураКурса = ПолучитьКурсВалюты(ВалютаВзаиморасчетов, Дата, БазоваяВалюта, ОбъектРасчетов);
		КонецЕсли;
		
	ИначеЕсли НЕ ВалютаДокумента = ВалютаРеглУчета И ВалютаВзаиморасчетов = ВалютаРеглУчета Тогда
		
		СтруктураКурса = ПолучитьКурсВалюты(ВалютаДокумента, Дата, БазоваяВалюта, ОбъектРасчетов);
		
	Иначе
		
		Если ВариантКурсаДоговора = Перечисления.ВариантыКурсаДоговора.УстановленныйВДоговоре Тогда
			КурсВалютыВзаиморасчетов = РегистрыСведений.КурсыВалютРасчетовПоДоговорам.ПолучитьЗначенияКурсаВалютыДоговора(Договор, Дата);
		Иначе
			КурсВалютыВзаиморасчетов = ПолучитьКурсВалюты(ВалютаВзаиморасчетов, Дата, БазоваяВалюта, ОбъектРасчетов);
		КонецЕсли;
		КурсВалютыДокумента      = ПолучитьКурсВалюты(ВалютаДокумента, Дата, БазоваяВалюта, ОбъектРасчетов);
		СтруктураКурса           = ПолучитьКроссКурсВалют(КурсВалютыДокумента, КурсВалютыВзаиморасчетов);
		
	КонецЕсли;
	
	КурсЧислитель      = СтруктураКурса.КурсЧислитель;
	КурсЗнаменатель = СтруктураКурса.КурсЗнаменатель;
	
КонецПроцедуры

// Функция возвращает кросс-курс двух валют
//
// Параметры:
//	Валюта1 - Структура - Параметры курса валюты, относительно которой рассчитывается курс:
//       * КурсЧислитель      - Число - Курс валюты относительно валюты регламентированного учета.
//       * КурсЗнаменатель - Число - Кратность валюты относительно валюты регламентированного учета.
//	Валюта2 - Структура - Параметры курса валюты, курс которой рассчитывается:
//       * КурсЧислитель      - Число - Курс валюты относительно валюты регламентированного учета.
//       * КурсЗнаменатель - Число - Кратность относительно валюты регламентированного учета.
//
// Возвращаемое значение: 
//   Структура - Параметры кросс-курса:
//       * КурсЧислитель - Число - Числитель кросс-курса
//       * КурсЗнаменатель - Число - Знаменатель кросс-курса
//
Функция ПолучитьКроссКурсВалют(Валюта1, Валюта2) Экспорт
	
	Если Валюта1.КурсЧислитель <> 0
		И Валюта1.КурсЗнаменатель <> 0
		И Валюта2.КурсЧислитель <> 0
		И Валюта2.КурсЗнаменатель <> 0 Тогда
			
			ЧислительПриведенный = Валюта1.КурсЧислитель * Валюта2.КурсЗнаменатель / (Валюта2.КурсЧислитель * Валюта1.КурсЗнаменатель);
			ЗнаменательПриведенный = 1;
			
			Если ЧислительПриведенный < 1 Тогда
				Пока ЧислительПриведенный * 10 < 1 Цикл
					ЧислительПриведенный = ЧислительПриведенный * 10;
					ЗнаменательПриведенный = ЗнаменательПриведенный * 10;
				КонецЦикла;
			КонецЕсли;
	Иначе
		ЧислительПриведенный = 1;
		ЗнаменательПриведенный = 1;
	КонецЕсли;
	
	Точность = Метаданные.ОпределяемыеТипы.КурсВалюты.Тип.КвалификаторыЧисла.РазрядностьДробнойЧасти;
	
	СтруктураКурса = СтруктураКурсаВалюты();
	СтруктураКурса.КурсЧислитель = Окр(ЧислительПриведенный, Точность);
	СтруктураКурса.КурсЗнаменатель = ЗнаменательПриведенный;
	
	Возврат СтруктураКурса;
	
КонецФункции

// Функция возвращает структуру параметров курса валюты
//
// Параметры:
//	КурсЧислитель - Число - Необязательный, числитель курса валюты.
//	КурсЗнаменатель - Число - Необязательный, Знаменатель курса валюты.
//
// Возвращаемое значение: 
//   Структура - Параметры курса валюты:
//	   * КурсЧислитель - Число - курс валюты.
//	   * КурсЗнаменатель - Число - кратность курса.
//
Функция СтруктураКурсаВалюты(КурсЧислитель = 0, КурсЗнаменатель = 0) Экспорт
	Структура = Новый Структура;
	Структура.Вставить("КурсЧислитель", КурсЧислитель);
	Структура.Вставить("КурсЗнаменатель", КурсЗнаменатель);
	Возврат Структура;
КонецФункции

// Пересчитывает сумму из одной валюты в другую.
//
// Параметры:
//  Сумма          - Число - сумма, которую необходимо пересчитать;
//  БазоваяВалюта - СправочникСсылка.Валюты - валюта, относительно которой получается курс;
//  ИсходнаяВалюта - СправочникСсылка.Валюты - пересчитываемая валюта;
//  НоваяВалюта    - СправочникСсылка.Валюты - валюта, в которую необходимо пересчитать;
//  Дата           - Дата - дата курсов валют.
//  ОбъектРасчетов - СправочникСсылка.ОбъектыРасчетов - Необязательный, объект расчетов, по которому определяется вариант курса договора
//
// Возвращаемое значение:
//  Число - пересчитанная сумма.
//
Функция ПересчитатьВВалюту(Сумма, БазоваяВалюта, ИсходнаяВалюта, НоваяВалюта, Дата, ОбъектРасчетов = Неопределено) Экспорт
	
	Возврат ПересчитатьПоКурсу(Сумма,
		ПолучитьКурсВалюты(ИсходнаяВалюта, Дата, БазоваяВалюта, ОбъектРасчетов),
		ПолучитьКурсВалюты(НоваяВалюта, Дата, БазоваяВалюта, ОбъектРасчетов));
	
КонецФункции

// Пересчитывает сумму из текущей валюты в новую валюту по параметрам их курсов. 
// Параметры курсов валют можно получить функцией РаботаСКурсамиВалют.ПолучитьКурсВалюты.
//
// Параметры:
//   Сумма - Число     - сумма, которую следует пересчитать.
//
//   ПараметрыТекущегоКурса - Структура - параметры курса валюты, из которой надо пересчитать:
//    * Валюта - СправочникСсылка.Валюты - ссылка пересчитываемой валюты.
//    * КурсЧислитель - Число - курс пересчитываемой валюты.
//    * КурсЗнаменатель - Число - кратность пересчитываемой валюты.
//
//   ПараметрыНовогоКурса   - Структура - параметры курса валюты, в  которую надо пересчитать:
//    * Валюта - СправочникСсылка.Валюты - ссылка валюты, в которую идет пересчет.
//    * КурсЧислитель - Число - курс числитель валюты, в которую идет пересчет.
//    * КурсЗнаменатель - Число - курс знаменатель валюты, в которую идет пересчет.
//
// Возвращаемое значение: 
//   Число - сумма, пересчитанная по новому курсу.
//
Функция ПересчитатьПоКурсу(Сумма, ПараметрыТекущегоКурса, ПараметрыНовогоКурса) Экспорт
	Если ПараметрыТекущегоКурса.Валюта = ПараметрыНовогоКурса.Валюта
		Или (ПараметрыТекущегоКурса.КурсЧислитель = ПараметрыНовогоКурса.КурсЧислитель 
			И ПараметрыТекущегоКурса.КурсЗнаменатель = ПараметрыНовогоКурса.КурсЗнаменатель) Тогда
		
		Возврат Сумма;
	КонецЕсли;
	
	Если ПараметрыТекущегоКурса.КурсЧислитель = 0
		Или ПараметрыТекущегоКурса.КурсЗнаменатель = 0
		Или ПараметрыНовогоКурса.КурсЧислитель = 0
		Или ПараметрыНовогоКурса.КурсЗнаменатель = 0 Тогда
		
		Возврат 0;
	КонецЕсли;
	
	Возврат Окр((Сумма * ПараметрыТекущегоКурса.КурсЧислитель * ПараметрыНовогоКурса.КурсЗнаменатель) 
		/ (ПараметрыНовогоКурса.КурсЧислитель * ПараметрыТекущегоКурса.КурсЗнаменатель), 2);
КонецФункции

// Формирует представление суммы прописью в указанной валюте.
//
// Параметры:
//   СуммаЧислом - Число - сумма, которую надо представить прописью.
//   Валюта - СправочникСсылка.Валюты - валюта, в которой нужно представить сумму.
//   БезДробнойЧасти - Булево - указать Истина, если требуется получить сумму без дробной части (без копеек).
//   КодЯзыка - Строка - язык, на котором требуется получить сумму прописью.
//                       Состоит из кода языка по ISO 639-1 и, опционально, кода страны по ISO 3166-1, разделенных
//                       символом подчеркивания. Примеры: "en", "en_US", "en_GB", "ru", "ru_RU".
//                       Значение по умолчанию - язык конфигурации.
//
// Возвращаемое значение:
//   Строка - сумма прописью.
//
Функция СформироватьСуммуПрописью(СуммаЧислом, Валюта, БезДробнойЧасти = Ложь, Знач КодЯзыка = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(КодЯзыка) Тогда
		КодЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
	КонецЕсли;
	
	Сумма = ?(СуммаЧислом < 0, -СуммаЧислом, СуммаЧислом);
	ПараметрыПрописи = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Валюта, "ПараметрыПрописи", , КодЯзыка);
	Результат = ЧислоПрописью(Сумма, "L=" + КодЯзыка + ";ДП=Ложь", ПараметрыПрописи); // АПК:1297 АПК:1357
	Если БезДробнойЧасти И Цел(Сумма) = Сумма Тогда
		Результат = Лев(Результат, СтрНайти(Результат, "0") - 1);
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Конструктор параметров варианта курса договора
// 
// Возвращаемое значение:
//  Структура:
// * ВариантКурсаДоговора - ПеречислениеСсылка.ВариантыКурсаДоговора - Значение по умолчанию Переменный
// * Договор - Неопределено, СправочникСсылка.ДоговорыКонтрагентов, СправочникСсылка.ДоговорыМеждуОрганизациями - Договор с установленным курсом
// * ВалютаВзаиморасчетов - СправочникСсылка.Валюты - Валюта взаиморасчетов договора
Функция ПараметрыВариантаКурсаДоговора() Экспорт
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ВариантКурсаДоговора", Перечисления.ВариантыКурсаДоговора.Переменный);
	СтруктураПараметров.Вставить("Договор", Неопределено);
	СтруктураПараметров.Вставить("ВалютаВзаиморасчетов", Справочники.Валюты.ПустаяСсылка());
	СтруктураПараметров.Вставить("ОбъектРасчетов", Справочники.ОбъектыРасчетов.ПустаяСсылка());
	
	Возврат СтруктураПараметров;
	
КонецФункции

// Выполняет синхронизацию между РС.КурсыВалют и РС.ОтносительныеКурсыВалют.
//
// Параметры:
//  Источник - РегистрСведенийНаборЗаписей - Набор записей РС.КурсыВалют или РС.ОтносительныеКурсыВалют.
//
Процедура СинхронизоватьКурсыВалют(Источник) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	БазоваяВалюта = Константы.ВалютаРегламентированногоУчета.Получить();
	Если Не ЗначениеЗаполнено(БазоваяВалюта) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Источник) = Тип("РегистрСведенийНаборЗаписей.КурсыВалют") Тогда
		Приемник = РегистрыСведений.ОтносительныеКурсыВалют.СоздатьНаборЗаписей();
		ИмяРегистра = Метаданные.РегистрыСведений.ОтносительныеКурсыВалют.Имя;
	Иначе
		Если Источник.Отбор.БазоваяВалюта.Значение <> БазоваяВалюта Тогда
			Возврат;
		КонецЕсли;
		Приемник = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
		БазоваяВалюта = Неопределено;
		ИмяРегистра = Метаданные.РегистрыСведений.КурсыВалют.Имя;
	КонецЕсли;
	
	Если Источник.Количество() > 0 Тогда
		ОбновитьСинхронизируемыеКурсы(Источник, Приемник, ИмяРегистра, БазоваяВалюта);
	Иначе
		УдалитьСинхронизируемыеКурсы(Источник, Приемник, БазоваяВалюта)
	КонецЕсли;
	
КонецПроцедуры

// Проверяет наличие установленного относительного курса на 1 января 1980 года.
// В случае отсутствия устанавливает курс числитель и курс знаменатель равными единице.
//
// Параметры:
//  Валюта - СправочникСсылка.Валюты - ссылка на элемент справочника Валют.
//
Процедура ПроверитьКорректностьОтносительногоКурсаНа01_01_1980(Валюта) Экспорт
	
	ДатаКурса = Дата("19800101");
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОтносительныеКурсыВалют");
	ЭлементБлокировки.УстановитьЗначение("Период", ДатаКурса);
	ЭлементБлокировки.УстановитьЗначение("Валюта", Валюта);
	ЭлементБлокировки.УстановитьЗначение("БазоваяВалюта", Валюта);
	
	НачатьТранзакцию();
	Попытка
		Блокировка.Заблокировать();
		СтруктураКурса = РегистрыСведений.ОтносительныеКурсыВалют.ПолучитьПоследнее(ДатаКурса, Новый Структура("Валюта, БазоваяВалюта", Валюта, Валюта));
		
		Если (СтруктураКурса.КурсЧислитель = 0) Или (СтруктураКурса.КурсЗнаменатель = 0) Тогда
			НаборЗаписей = РегистрыСведений.ОтносительныеКурсыВалют.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Период.Установить(ДатаКурса);
			НаборЗаписей.Отбор.Валюта.Установить(Валюта);
			НаборЗаписей.Отбор.БазоваяВалюта.Установить(Валюта);
			Запись = НаборЗаписей.Добавить();
			Запись.Период = ДатаКурса;
			Запись.Валюта = Валюта;
			Запись.БазоваяВалюта = Валюта;
			Запись.КурсЧислитель = 1;
			Запись.КурсЗнаменатель = 1;
			НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения");
			НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьКонтрольПодчиненныхВалют");
			НаборЗаписей.Записать();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#Область КэшКурсовВалют

// Функция возвращает таблицу - кэш курсов валют
//
// Возвращаемое значение: 
//  ТаблицаЗначений - Таблица, содержащая колонки:
//     * Валюта 	- СправочникСсылка.Валюты -  элемент справочника "Валюты".
//     
//	   * ДатаКурса 	- Дата - дата курса.
//	   * КурсЧислитель - Число - Числитель курса валюты.
//	   * КурсЗнаменатель - Число - Знаменатель курса валюты.
//
Функция ИнициализироватьКэшКурсовВалют() Экспорт
	
	КэшКурсов = Новый ТаблицаЗначений;
	КэшКурсов.Колонки.Добавить("Валюта");
	КэшКурсов.Колонки.Добавить("БазоваяВалюта");
	КэшКурсов.Колонки.Добавить("ДатаКурса");
	КэшКурсов.Колонки.Добавить("КурсЧислитель");
	КэшКурсов.Колонки.Добавить("КурсЗнаменатель");
	
	КэшКурсов.Индексы.Добавить("Валюта, ДатаКурса, БазоваяВалюта");
	
	Возврат КэшКурсов;
	
КонецФункции // ИнициализироватьКэшКурсовВалют()

// Функция возвращает отношение курса к кратности курса валюты на дату
//
// Параметры:
//  Валюта - СправочникСсылка.Валюты - - Валюта, курс которой необходимо получить
//  ДатаКурса - Дата-  Дата, на которую следует получить курс
//  КэшКурсов - см. ИнициализироватьКэшКурсовВалют
//	БазоваяВалюта - СправочникСсылка.Валюты - Базовая валюта, относительно которой необходимо получить курс.
//											Если Неопределено, то курсы получаются относительно значения из константы БазоваяВалютаПоУмолчанию.
//	
// Возвращаемое значение: 
//	Число      - Курс валюты
//
Функция ПолучитьКурсВалютыИзКэша(Знач Валюта, Знач ДатаКурса, КэшКурсов, Знач БазоваяВалюта = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Валюта) Тогда
		ТекстСообщения = НСтр("ru = 'Ошибка получения курса валют: передана пустая валюта'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	ДатаКурса = НачалоДня(ДатаКурса);
	
	Если Не ЗначениеЗаполнено(БазоваяВалюта) Тогда
		БазоваяВалюта = ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию();
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура("Валюта, ДатаКурса, БазоваяВалюта",
									   Валюта, ДатаКурса, БазоваяВалюта);
	РезультатПоиска = КэшКурсов.НайтиСтроки(СтруктураПоиска);
	
	Если РезультатПоиска.Количество() = 0 Тогда
		СтруктураКурса = ПолучитьКурсВалюты(Валюта, ДатаКурса, БазоваяВалюта);
		ЗаписьКэша = КэшКурсов.Добавить();
		ЗаписьКэша.Валюта = СтруктураПоиска.Валюта;
		ЗаписьКэша.ДатаКурса = СтруктураПоиска.ДатаКурса;
		ЗаписьКэша.БазоваяВалюта  = СтруктураПоиска.БазоваяВалюта;
		ЗаписьКэша.КурсЧислитель  = СтруктураКурса.КурсЧислитель;
		ЗаписьКэша.КурсЗнаменатель = СтруктураКурса.КурсЗнаменатель;
	Иначе
		СтруктураКурса = РезультатПоиска[0];
	КонецЕсли;
	
	Если СтруктураКурса.КурсЧислитель = 0 Или СтруктураКурса.КурсЗнаменатель = 0 Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Получение курса валют: обнаружен нулевой курс валюты %1 относительно базовой валюты %2.'"), Валюта, БазоваяВалюта);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат 0;
	КонецЕсли;
	
	Возврат СтруктураКурса.КурсЧислитель / СтруктураКурса.КурсЗнаменатель;
	
КонецФункции // ПолучитьКурсВалютыИзКэша()

// Функция возвращает таблицу - кэш курсов валют
//
// Возвращаемое значение: 
//  ТаблицаЗначений - Таблица, содержащая колонки:
//     * Договор 	- СправочникСсылка.ДоговорыКонтрагентов, СправочникСсылка.ДоговорыМеждуОрганизациями - Договор с установленным курсом
//     
//	   * ДатаКурса 	- Дата - дата курса.
//	   * КурсЧислитель - Число - Числитель курса валюты.
//	   * КурсЗнаменатель - Число - Знаменатель курса валюты.
//
Функция ИнициализироватьКэшКурсовВалютДоговоров() Экспорт
	
	КэшКурсов = Новый ТаблицаЗначений;
	КэшКурсов.Колонки.Добавить("Договор");
	КэшКурсов.Колонки.Добавить("ДатаКурса");
	КэшКурсов.Колонки.Добавить("КурсЧислитель");
	КэшКурсов.Колонки.Добавить("КурсЗнаменатель");
	
	КэшКурсов.Индексы.Добавить("Договор, ДатаКурса");
	
	Возврат КэшКурсов;
	
КонецФункции // ИнициализироватьКэшКурсовВалют()

// Функция возвращает отношение курса к кратности курса валюты на дату
//
// Параметры:
//  Договор - СправочникСсылка.Валюты - - Валюта, курс которой необходимо получить
//  ДатаКурса - Дата-  Дата, на которую следует получить курс
//  КэшКурсов - см. ИнициализироватьКэшКурсовВалют
//	
// Возвращаемое значение: 
//	Число      - Курс валюты
//
Функция ПолучитьКурсВалютыДоговораИзКэша(Знач Договор, Знач ДатаКурса, КэшКурсов) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Договор) Тогда
		ТекстСообщения = НСтр("ru = 'Ошибка получения курса валют по договору: передан пустой договор'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	ДатаКурса = НачалоДня(ДатаКурса);
	
	СтруктураПоиска = Новый Структура("Договор, ДатаКурса",
									   Договор, ДатаКурса);
	РезультатПоиска = КэшКурсов.НайтиСтроки(СтруктураПоиска);
	
	Если РезультатПоиска.Количество() = 0 Тогда
		СтруктураКурса = РегистрыСведений.КурсыВалютРасчетовПоДоговорам.ПолучитьЗначенияКурсаВалютыДоговора(Договор, ДатаКурса);
		ЗаписьКэша = КэшКурсов.Добавить();
		ЗаписьКэша.Договор = СтруктураПоиска.Договор;
		ЗаписьКэша.ДатаКурса = СтруктураПоиска.ДатаКурса;
		ЗаписьКэша.КурсЧислитель  = СтруктураКурса.КурсЧислитель;
		ЗаписьКэша.КурсЗнаменатель = СтруктураКурса.КурсЗнаменатель;
	Иначе
		СтруктураКурса = РезультатПоиска[0];
	КонецЕсли;
	
	Если СтруктураКурса.КурсЧислитель = 0 Или СтруктураКурса.КурсЗнаменатель = 0 Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Получение курса валют: обнаружен нулевой курс валюты договора %1.'"), Договор);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат 0;
	КонецЕсли;
	
	Возврат СтруктураКурса.КурсЧислитель / СтруктураКурса.КурсЗнаменатель;
	
КонецФункции // ПолучитьКурсВалютыИзКэша()

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполнить данные курса ЕЦБ для валюты.
// 
// Параметры:
//  ВыбраннаяВалюта  - СправочникСсылка.Валюты - 
//  БазоваяВалюта - СправочникСсылка.Валюты - 
// 
// Возвращаемое значение:
//  Структура - Заполнить данные курса ЕЦБ для валюты:
// 	* ДатаКурса - Дата -
// 	* КурсЧислитель - Число -
// 	* КурсЗнаменатель - Число -
Функция ЗаполнитьДанныеКурсаЕЦБДляВалюты(ВыбраннаяВалюта, БазоваяВалюта) Экспорт
	
	ДанныеКурса = Новый Структура("ДатаКурса, КурсЧислитель, КурсЗнаменатель");
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	РегКурсы.Период КАК Период,
	               |	РегКурсы.КурсЧислитель КАК КурсЧислитель,
	               |	РегКурсы.КурсЗнаменатель КАК КурсЗнаменатель
	               |ИЗ
	               |	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(
	               |			&ОкончаниеПериодаЗагрузки,
	               |			Валюта = &ВыбраннаяВалюта
	               |				И БазоваяВалюта = &БазоваяВалюта) КАК РегКурсы";
	Запрос.УстановитьПараметр("ВыбраннаяВалюта", ВыбраннаяВалюта);
	Запрос.УстановитьПараметр("БазоваяВалюта", БазоваяВалюта);
	Запрос.УстановитьПараметр("ОкончаниеПериодаЗагрузки", ТекущаяДатаСеанса());
	
	ВыборкаКурс = Запрос.Выполнить().Выбрать();
	ВыборкаКурс.Следующий();
	
	ДанныеКурса.ДатаКурса = ВыборкаКурс.Период;
	ДанныеКурса.КурсЧислитель = ВыборкаКурс.КурсЧислитель;
	ДанныеКурса.КурсЗнаменатель = ВыборкаКурс.КурсЗнаменатель;
	
	Возврат ДанныеКурса;
	
КонецФункции

Процедура ЗагрузитьАктуальныйКурсЕЦБ(ПараметрыЗагрузки = Неопределено, АдресРезультата = Неопределено) Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ЗагрузкаКурсовВалютЕЦБ);
	
	Если Метаданные.Обработки.Найти("ЗагрузкаКурсовВалютЕЦБ") <> Неопределено И ПолучитьФункциональнуюОпцию("ИспользоватьЗагрузкуКурсовВалютЕЦБ") Тогда
		Обработки["ЗагрузкаКурсовВалютЕЦБ"].ЗагрузитьАктуальныйКурс(ПараметрыЗагрузки, АдресРезультата);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьЗапись(Приемник)
	
	Приемник.ДополнительныеСвойства.Вставить("СинхронизацияКурсовВалют");
	Приемник.Записать();
	
КонецПроцедуры

Процедура ОбновитьСинхронизируемыеКурсы(Источник, Приемник, ИмяРегистра, БазоваяВалюта)
	
	Период = Источник.Отбор.Период.Значение;
	Валюта = Источник.Отбор.Валюта.Значение;
	
	Если ЗначениеЗаполнено(Период) Тогда
		Приемник.Отбор.Период.Установить(Период);
	КонецЕсли;
	Если ЗначениеЗаполнено(Валюта) Тогда
		Приемник.Отбор.Валюта.Установить(Валюта);
	КонецЕсли;
	Если ЗначениеЗаполнено(БазоваяВалюта) Тогда
		Приемник.Отбор.БазоваяВалюта.Установить(БазоваяВалюта);
	КонецЕсли;
	
	Для Каждого ЗаписьИсточник Из Источник Цикл
		
		НоваяПриемник = Приемник.Добавить();
		НоваяПриемник.Период = ЗаписьИсточник.Период;
		НоваяПриемник.Валюта = ЗаписьИсточник.Валюта;
		
		Если ЗначениеЗаполнено(БазоваяВалюта) Тогда
			НоваяПриемник.БазоваяВалюта = БазоваяВалюта;
		КонецЕсли;
		
		Если ИмяРегистра = Метаданные.РегистрыСведений.КурсыВалют.Имя Тогда
			НоваяПриемник.Курс = ЗаписьИсточник.КурсЧислитель;
			НоваяПриемник.Кратность = ЗаписьИсточник.КурсЗнаменатель;
		Иначе
			НоваяПриемник.КурсЧислитель = ЗаписьИсточник.Курс;
			НоваяПриемник.КурсЗнаменатель = ЗаписьИсточник.Кратность;
		КонецЕсли;
		
	КонецЦикла;
	
	ВыполнитьЗапись(Приемник);
	
КонецПроцедуры

Процедура УдалитьСинхронизируемыеКурсы(Источник, Приемник, БазоваяВалюта)
	
	Период = Источник.Отбор.Период.Значение;
	Валюта = Источник.Отбор.Валюта.Значение;
	
	Приемник.Отбор.Период.Установить(Период);
	Приемник.Отбор.Валюта.Установить(Валюта);
	Если ЗначениеЗаполнено(БазоваяВалюта) Тогда
		Приемник.Отбор.БазоваяВалюта.Установить(БазоваяВалюта);
	КонецЕсли;
	
	ВыполнитьЗапись(Приемник);
	
КонецПроцедуры

// Относительные курсы актуальны.
// 
// Параметры:
//  БазоваяВалюта - СправочникСсылка.Валюты
// 
// Возвращаемое значение:
// 	Булево - Признак актуальности относительных курсов.
//
Функция ОтносительныеКурсыАктуальны(БазоваяВалюта) Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Валюты.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втВалюты
	|ИЗ
	|	Справочник.Валюты КАК Валюты
	|ГДЕ
	|	Валюты.СпособУстановкиКурса = ЗНАЧЕНИЕ(Перечисление.СпособыУстановкиКурсаВалюты.ЗагрузкаИзИнтернета)
	|	И Валюты.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	1 КАК Поле1
	|ИЗ
	|	втВалюты КАК Валюты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК ОтносительныеКурсыВалют
	|		ПО Валюты.Ссылка = ОтносительныеКурсыВалют.Валюта
	|			И (ОтносительныеКурсыВалют.Период = &ТекущаяДата)
	|			И (ОтносительныеКурсыВалют.БазоваяВалюта = &БазоваяВалюта)
	|ГДЕ
	|	ОтносительныеКурсыВалют.Валюта ЕСТЬ NULL";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("БазоваяВалюта", БазоваяВалюта);
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить().Пустой();
КонецФункции

// Загружаемые валюты.
// 
// Возвращаемое значение:
//  Массив Из СправочникСсылка.Валюты - загружаемые валюты.
//
Функция ЗагружаемыеВалюты() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Валюты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Валюты КАК Валюты
	|ГДЕ
	|	Валюты.СпособУстановкиКурса = ЗНАЧЕНИЕ(Перечисление.СпособыУстановкиКурсаВалюты.ЗагрузкаИзИнтернета)
	|	И НЕ Валюты.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Валюты.НаименованиеПолное";

	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

// Список зависимых валют.
// 
// Параметры:
//  ВалютаБазовая - СправочникСсылка.Валюты - Валюта, для которой нужно получить зависимые валюты
//  ДополнительныеСвойства - Структура, Неопределено - Дополнительные свойства для кэширования
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица зависимых валют:
//   * Ссылка - СправочникСсылка.Валюты
//   * Наценка - Число
//   * СпособУстановкиКурса - ПеречислениеСсылка.СпособыУстановкиКурсаВалюты
//   * ФормулаРасчетаКурса - Строка
//
Функция СписокЗависимыхВалют(ВалютаБазовая, ДополнительныеСвойства = Неопределено) Экспорт
	
	Кэшировать = (ТипЗнч(ДополнительныеСвойства) = Тип("Структура"));
	
	Если Кэшировать Тогда
		
		ЗависимыеВалюты = ДополнительныеСвойства.ЗависимыеВалюты.Получить(ВалютаБазовая);
		
		Если ТипЗнч(ЗависимыеВалюты) = Тип("ТаблицаЗначений") Тогда
			Возврат ЗависимыеВалюты;
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СпрВалюты.Ссылка,
	|	СпрВалюты.Наценка,
	|	СпрВалюты.СпособУстановкиКурса,
	|	СпрВалюты.ФормулаРасчетаКурса
	|ИЗ
	|	Справочник.Валюты КАК СпрВалюты
	|ГДЕ
	|	СпрВалюты.ОсновнаяВалюта = &ВалютаБазовая
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СпрВалюты.Ссылка,
	|	СпрВалюты.Наценка,
	|	СпрВалюты.СпособУстановкиКурса,
	|	СпрВалюты.ФормулаРасчетаКурса
	|ИЗ
	|	Справочник.Валюты КАК СпрВалюты
	|ГДЕ
	|	СпрВалюты.ФормулаРасчетаКурса ПОДОБНО &СимвольныйКод";
	
	Запрос.УстановитьПараметр("ВалютаБазовая", ВалютаБазовая);
	Запрос.УстановитьПараметр("СимвольныйКод", "%" + ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВалютаБазовая, "Наименование") + "%");
	
	ЗависимыеВалюты = Запрос.Выполнить().Выгрузить();
	
	Если Кэшировать Тогда
		
		ДополнительныеСвойства.ЗависимыеВалюты.Вставить(ВалютаБазовая, ЗависимыеВалюты);
		
	КонецЕсли;
	
	Возврат ЗависимыеВалюты;
	
КонецФункции

// См. подписку на событие "ПриЗаписиКурсовВалют"
Процедура ПриЗаписиКурсовВалютПриЗаписи(Источник, Отказ, Замещение) Экспорт
	
	Если Источник.ОбменДанными.Загрузка И Не Источник.ДополнительныеСвойства.Свойство("ЗагрузкаКурсовВалютВОбластьЗаВесьПериод") Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("ЗаписьПриОбновленииИнформационнойБазы") Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("СинхронизацияКурсовВалют") Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСКурсамиВалютУТ.СинхронизоватьКурсыВалют(Источник);
	
КонецПроцедуры

Функция ТекстЗапросаКурсовПоОбъектуРасчетов()
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	КурсДоговора.Период КАК Период,
	|	КурсДоговора.Договор КАК Договор,
	|	КурсДоговора.КурсЧислитель КАК КурсЧислитель,
	|	КурсДоговора.КурсЗнаменатель КАК КурсЗнаменатель
	|ПОМЕСТИТЬ втКурсыДоговоров
	|ИЗ
	|	РегистрСведений.КурсыВалютРасчетовПоДоговорам.СрезПоследних(&Период) КАК КурсДоговора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбъектыРасчетов.Ссылка                                                 КАК ОбъектРасчетов,
	|	ВЫБОР
	|		КОГДА ОбъектыРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|				И ЕСТЬNULL(ОбъектыРасчетов.Договор.ВариантКурсаДоговора, ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.Переменный)) 
	|					= ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.ФиксированныйНаДатуОтгрузки)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.ФиксированныйНаДатуОтгрузки)
	|		ИНАЧЕ ЕСТЬNULL(ОбъектыРасчетов.Договор.ВариантКурсаДоговора, ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.Переменный))
	|	КОНЕЦ                                                                  КАК ВариантКурсаДоговора,
	|	ОбъектыРасчетов.ТипОбъектаРасчетов                                     КАК ТипОбъектаРасчетов,
	|	ЕСТЬNULL(КурсыДокументов.КурсЧислительВалютыВзаиморасчетов, 0)         КАК КурсЧислительВалютыВзаиморасчетов,
	|	ЕСТЬNULL(КурсыДокументов.КурсЗнаменательВалютыВзаиморасчетов, 0)       КАК КурсЗнаменательВалютыВзаиморасчетов,
	|	ЕСТЬNULL(КурсыДокументов.КурсЧислительВалютыУправленческогоУчета, 0)   КАК КурсЧислительВалютыУправленческогоУчета,
	|	ЕСТЬNULL(КурсыДокументов.КурсЗнаменательВалютыУправленческогоУчета, 0) КАК КурсЗнаменательВалютыУправленческогоУчета,
	|	ЕСТЬNULL(КурсыДокументов.КурсЧислительВалютыДокумента, 0)              КАК КурсЧислительВалютыДокумента,
	|	ЕСТЬNULL(КурсыДокументов.КурсЗнаменательВалютыДокумента, 0)            КАК КурсЗнаменательВалютыДокумента,
	|	ЕСТЬNULL(КурсыДокументов.ВалютаДокумента, 0)                           КАК ВалютаДокумента,
	|	ОбъектыРасчетов.ВалютаВзаиморасчетов                                   КАК ВалютаВзаиморасчетов,
	|	ОбъектыРасчетов.Организация.ВалютаРегламентированногоУчета             КАК ВалютаРегламентированногоУчета,
	|	ОбъектыРасчетов.Дата                                                   КАК Дата,
	|	ЕСТЬNULL(КурсыДоговоров.Период, ДАТАВРЕМЯ(1, 1, 1))                    КАК ПериодКурсаДоговора,
	|	ЕСТЬNULL(КурсыДоговоров.КурсЧислитель, 0)                              КАК КурсЧислительВалютыДоговора,
	|	ЕСТЬNULL(КурсыДоговоров.КурсЗнаменатель, 0)                            КАК КурсЗнаменательВалютыДоговора
	|ПОМЕСТИТЬ ДанныеОбъектаРасчетов
	|ИЗ
	|	Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВалютыИКурсыДокументов КАК КурсыДокументов
	|		ПО ОбъектыРасчетов.Объект = КурсыДокументов.Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыДоговоров КАК КурсыДоговоров
	|		ПО ОбъектыРасчетов.Договор = КурсыДоговоров.Договор
	|ГДЕ
	|	ОбъектыРасчетов.Ссылка В (&ОбъектыРасчетов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеОбъектаРасчетов.ОбъектРасчетов                 КАК ОбъектРасчетов,
	|	ДанныеОбъектаРасчетов.ВалютаДокумента                КАК Валюта,
	|	ДанныеОбъектаРасчетов.ВалютаРегламентированногоУчета КАК БазоваяВалюта,
	|	ДанныеОбъектаРасчетов.Дата                           КАК ДатаКурса,
	|	ДанныеОбъектаРасчетов.КурсЧислительВалютыДокумента   КАК КурсЧислитель,
	|	ДанныеОбъектаРасчетов.КурсЗнаменательВалютыДокумента КАК КурсЗнаменатель
	|ПОМЕСТИТЬ ВтКурсыПредварительно
	|ИЗ
	|	ДанныеОбъектаРасчетов КАК ДанныеОбъектаРасчетов
	|ГДЕ
	|	ДанныеОбъектаРасчетов.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.ФиксированныйНаДатуОтгрузки)
	|	И ДанныеОбъектаРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|	И ДанныеОбъектаРасчетов.ВалютаДокумента В(&МассивВалют)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеОбъектаРасчетов.ОбъектРасчетов                      КАК ОбъектРасчетов,
	|	ДанныеОбъектаРасчетов.ВалютаВзаиморасчетов                КАК Валюта,
	|	ДанныеОбъектаРасчетов.ВалютаРегламентированногоУчета      КАК БазоваяВалюта,
	|	ДанныеОбъектаРасчетов.Дата                                КАК ДатаКурса,
	|	ДанныеОбъектаРасчетов.КурсЧислительВалютыВзаиморасчетов   КАК КурсЧислитель,
	|	ДанныеОбъектаРасчетов.КурсЗнаменательВалютыВзаиморасчетов КАК КурсЗнаменатель
	|ИЗ
	|	ДанныеОбъектаРасчетов КАК ДанныеОбъектаРасчетов
	|ГДЕ
	|	ДанныеОбъектаРасчетов.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.ФиксированныйНаДатуОтгрузки)
	|	И ДанныеОбъектаРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|	И ДанныеОбъектаРасчетов.ВалютаВзаиморасчетов В(&МассивВалют)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеОбъектаРасчетов.ОбъектРасчетов                            КАК ОбъектРасчетов,
	|	&ВалютаУпр                                                      КАК Валюта,
	|	ДанныеОбъектаРасчетов.ВалютаРегламентированногоУчета            КАК БазоваяВалюта,
	|	ДанныеОбъектаРасчетов.Дата                                      КАК ДатаКурса,
	|	ДанныеОбъектаРасчетов.КурсЧислительВалютыУправленческогоУчета   КАК КурсЧислитель,
	|	ДанныеОбъектаРасчетов.КурсЗнаменательВалютыУправленческогоУчета КАК КурсЗнаменатель
	|ИЗ
	|	ДанныеОбъектаРасчетов КАК ДанныеОбъектаРасчетов
	|ГДЕ
	|	ДанныеОбъектаРасчетов.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.ФиксированныйНаДатуОтгрузки)
	|	И ДанныеОбъектаРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|	И &ВалютаУпр В (&МассивВалют)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеОбъектаРасчетов.ОбъектРасчетов                 КАК ОбъектРасчетов,
	|	ДанныеОбъектаРасчетов.ВалютаВзаиморасчетов           КАК Валюта,
	|	ДанныеОбъектаРасчетов.ВалютаРегламентированногоУчета КАК БазоваяВалюта,
	|	ДанныеОбъектаРасчетов.ПериодКурсаДоговора            КАК ДатаКурса,
	|	ДанныеОбъектаРасчетов.КурсЧислительВалютыДоговора    КАК КурсЧислитель,
	|	ДанныеОбъектаРасчетов.КурсЗнаменательВалютыДоговора  КАК КурсЗнаменатель
	|ИЗ
	|	ДанныеОбъектаРасчетов КАК ДанныеОбъектаРасчетов
	|ГДЕ
	|	ДанныеОбъектаРасчетов.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|	И ДанныеОбъектаРасчетов.ВалютаВзаиморасчетов В(&МассивВалют)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВтКурсыПредварительно.ОбъектРасчетов  КАК ОбъектРасчетов,
	|	ВтКурсыПредварительно.Валюта          КАК Валюта,
	|	ВтКурсыПредварительно.БазоваяВалюта   КАК БазоваяВалюта,
	|	ВтКурсыПредварительно.ДатаКурса       КАК ДатаКурса,
	|	ВтКурсыПредварительно.КурсЧислитель   КАК КурсЧислитель,
	|	ВтКурсыПредварительно.КурсЗнаменатель КАК КурсЗнаменатель
	|ИЗ
	|	ВтКурсыПредварительно КАК ВтКурсыПредварительно
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(ДанныеОбъектаРасчетов.ОбъектРасчетов,
	|		ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)) КАК ОбъектРасчетов,
	|	ОтносительныеКурсыВалютСрезПоследних.Валюта            КАК Валюта,
	|	ОтносительныеКурсыВалютСрезПоследних.БазоваяВалюта     КАК БазоваяВалюта,
	|	ОтносительныеКурсыВалютСрезПоследних.Период            КАК ДатаКурса,
	|	ОтносительныеКурсыВалютСрезПоследних.КурсЧислитель     КАК КурсЧислитель,
	|	ОтносительныеКурсыВалютСрезПоследних.КурсЗнаменатель   КАК КурсЗнаменатель
	|ИЗ
	|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(
	|			&Период,
	|			БазоваяВалюта = &ВалютаРегл
	|				И Валюта В (&МассивВалют)
	|				И НЕ Валюта В
	|						(ВЫБРАТЬ
	|							Т.Валюта
	|						ИЗ
	|							ВтКурсыПредварительно КАК Т)) КАК ОтносительныеКурсыВалютСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеОбъектаРасчетов КАК ДанныеОбъектаРасчетов
	|		ПО (ИСТИНА)
	|ГДЕ
	|	НЕ (ОтносительныеКурсыВалютСрезПоследних.Валюта, ДанныеОбъектаРасчетов.ОбъектРасчетов)
	|		В (ВЫБРАТЬ Т.Валюта, Т.ОбъектРасчетов ИЗ ВтКурсыПредварительно КАК Т)
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти
