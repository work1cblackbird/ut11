#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация


	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	//++ Локализация
	Если ЗначениеЗаполнено(Объект.ЧекККМ) Тогда
		ПроверитьВозможностьВводаНаОснованииЧекККМ(Объект, Объект.ЧекККМ, Отказ);
	КонецЕсли;
	
	КомиссионерВедетУчетПоРНПТ = Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОтКомиссионера
									И Справочники.СоглашенияСКлиентами.КомиссионерВедетУчетПоРНПТ(Объект.Соглашение);
	
	УчетПрослеживаемыхТоваровЛокализация.ПроверитьЗаполнениеНомераГТДВДокументахКомиссионера(Объект,
																							Отказ,
																							КомиссионерВедетУчетПоРНПТ);
	
	Если КомиссионерВедетУчетПоРНПТ
		Или Не Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОтКомиссионера Тогда
		
		Если Объект.ВозвратПорчи Тогда
			УчетПрослеживаемыхТоваровЛокализация.ПроверитьКорректностьНастроекТоваровРНПТ(
				Объект,
				Объект.Товары,
				Объект.Дата,
				"НоменклатураОприходование");
		Иначе
			УчетПрослеживаемыхТоваровЛокализация.ПроверитьКорректностьНастроекТоваровРНПТ(Объект,
																							Объект.Товары,
																							Объект.Дата);
		КонецЕсли;
		
		УчетПрослеживаемыхТоваровЛокализация.ПроверитьЗаполнениеКоличестваПоРНПТ(Объект, Отказ, Неопределено);
		УчетПрослеживаемыхТоваровЛокализация.ПроверитьДанныеПрослеживаемостиНомеровГТД(Объект, Объект.Товары, Объект.Дата);
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика формы документа
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Обрабатываемая форма документа.
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполненияНаСервере(Форма, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	//++ Локализация
	ДополнительныеПараметрыИСМП = СобытияФормИСМПКлиентСервер.ДополнительныеПараметрыИнтеграции(Форма);
	
	Если ПроверяемыеРеквизиты.Найти("Объект") <> Неопределено
		И ДополнительныеПараметрыИСМП <> Неопределено
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДополнительныеПараметрыИСМП, "ПроверкаЗаполненияОтключитьКонтрольСерий") Тогда
			
			ТекущийОбъект = Форма.РеквизитФормыВЗначение("Объект");
			ТекущийОбъект.ДополнительныеСвойства.Вставить("ПроверкаЗаполненияОтключитьКонтрольСерий", Истина);
			
			Если НЕ ТекущийОбъект.ПроверитьЗаполнение() Тогда
				Отказ = Истина
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Объект");
			
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	//++ Локализация
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ЧекККМ") Тогда
		ЧекККМЛокализация.ПроверитьВозможностьВводаНаОсновании(ДанныеЗаполнения);
		ЗаполнитьДокументНаОснованииЧекаККМ(
			Объект,
			ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ВходящаяТранспортнаяОперацияВЕТИС") Тогда
		
		ИнтеграцияВЕТИСУТ.ЗаполнитьВозвратТоваровОтКлиентаНаОснованииВходящейТранспортнойОперацииВЕТИС(Объект, ДанныеЗаполнения);
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ПриемкаТоваровИСМП") Тогда
		
		ИнтеграцияИСМПУТ.ЗаполнитьВозвратТоваровОтКлиентаНаОснованииПриемкиТоваровИСМП(Объект, ДанныеЗаполнения);
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ПогашениеСДИЗЗЕРНО") Тогда
		
		ИнтеграцияЗЕРНОУТ.ЗаполнитьВозвратТоваровОтКлиентаНаОснованииПогашенияСДИЗЗЕРНО(Объект, ДанныеЗаполнения);
	
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.НакладнаяСАТУРН") Тогда
		
		ИнтеграцияСАТУРНУТ.ЗаполнитьВозвратТоваровОтКлиентаНаОснованииНакладнаяСАТУРН(Объект, ДанныеЗаполнения);
		
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	//++ Локализация
	Объект.ЕстьМаркируемаяПродукцияГИСМ = ИнтеграцияГИСМ_УТ.ЕстьМаркируемаяПродукцияГИСМ(Объект.Товары);
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	//++ Локализация


	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	//++ Локализация
	Объект.ЕстьМаркируемаяПродукцияГИСМ = Ложь;
	Объект.ШтрихкодыУпаковок.Очистить();
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	//++ Локализация
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОтветственноеХранение") Тогда
		// МХ-1 
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "МХ1";
		КомандаПечати.Представление = НСтр("ru='Акт о приеме-передаче ТМЦ на хранение (МХ-1)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КонецЕсли;
	//-- Локализация
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	
КонецПроцедуры

//++ Локализация

// Формирует текст запроса для получения данных основания при печати Счет-фактуры.
//
// Возвращаемое значение:
//	Строка - текст запроса для получения данных основания.
//
Функция ТекстЗапросаДанныхОснованияДляПечатнойФормыСчетФактура() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка                                   КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО                                              КАК ХозяйственнаяОперация,
	|	ДанныеДокументов.Валюта                                   КАК Валюта,
	|	ДанныеДокументов.Организация                              КАК Организация,
	|	ДанныеДокументов.НалогообложениеНДС                       КАК НалогообложениеНДС,
	|	ДанныеДокументов.Подразделение                            КАК Подразделение,
	|	ДанныеДокументов.Склад                                    КАК Склад,
	|	НЕОПРЕДЕЛЕНО                                              КАК Грузоотправитель,
	|	НЕОПРЕДЕЛЕНО                                              КАК Грузополучатель,
	|	ЛОЖЬ                                                      КАК РасчетыЧерезОтдельногоКонтрагента,
	|	НЕОПРЕДЕЛЕНО                                              КАК Номенклатура,
	|	""""                                                      КАК Содержание,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)             КАК Комиссионер,
	|	НЕОПРЕДЕЛЕНО                                              КАК Основание,
	|	НЕОПРЕДЕЛЕНО                                              КАК ОснованиеДата,
	|	НЕОПРЕДЕЛЕНО                                              КАК ОснованиеНомер,
	|	НЕОПРЕДЕЛЕНО                                              КАК БанковскийСчетОрганизации,
	|	НЕОПРЕДЕЛЕНО                                              КАК БанковскийСчетКонтрагента,
	|	НЕОПРЕДЕЛЕНО                                              КАК БанковскийСчетГрузоотправителя,
	|	НЕОПРЕДЕЛЕНО                                              КАК БанковскийСчетГрузополучателя,
	|	НЕОПРЕДЕЛЕНО                                              КАК ДоверенностьНомер,
	|	НЕОПРЕДЕЛЕНО                                              КАК ДоверенностьДата,
	|	НЕОПРЕДЕЛЕНО                                              КАК ДоверенностьВыдана,
	|	НЕОПРЕДЕЛЕНО                                              КАК ДоверенностьЛицо,
	|	ДанныеДокументов.Менеджер                                 КАК Менеджер,
	|	НЕОПРЕДЕЛЕНО                                              КАК Кладовщик,
	|	НЕОПРЕДЕЛЕНО                                              КАК ДолжностьКладовщика,
	|	ДанныеДокументов.Дата                                     КАК Период
	|
	|//ОператорПОМЕСТИТЬ
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента КАК ДанныеДокументов
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&ДокументОснование_ВозвратТоваровОтКлиента)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает данные для формирования печатной формы МХ - 1.
//
// Параметры:
//	ПараметрыПечати	- Структура -дополнительные настройки печати.
//	МассивОбъектов	- Массив из ДокументСсылка.ВозвратТоваровОтКлиента - коллекция значений ссылок на документы,
//																			по которым необходимо получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемых для печати, содержащая следующие следующие свойства:
//		* РезультатПоШапке			- РезультатЗапроса - данные шапки документа.
//		* РезультатПоСкладам		- РезультатЗапроса - данные о складе ответственного хранения.
//		* РезультатПоТабличнойЧасти	- РезультатЗапроса - данные табличной части документа.
//		* РезультатПоОшибкам		- РезультатЗапроса - данные об ошибках, возникающих при печати документа.
//
Функция ПолучитьДанныеДляПечатнойФормыМХ1(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка									КАК Ссылка,
	|	ДанныеДокумента.Номер									КАК Номер,
	|	ДанныеДокумента.Дата									КАК Дата,
	|	ДанныеДокумента.Дата									КАК ДатаДокумента,
	|	ДанныеДокумента.Организация								КАК Организация,
	|	ДанныеДокумента.Склад									КАК Склад,
	|	ЕСТЬNULL(Склады.СкладОтветственногоХранения, ЛОЖЬ)		КАК СкладОтветственногоХранения,
	|	ДанныеДокумента.Организация = Склады.Поклажедержатель	КАК ОрганизацияПоклажедержатель,
	|	Склады.ИсточникИнформацииОЦенахДляПечати				КАК ИсточникИнформацииОЦенахДляПечати,
	|	Склады.УчетныйВидЦены									КАК ВидЦены,
	|	Склады.УчетныйВидЦены.ВалютаЦены						КАК ВалютаЦены,
	|	РасчетСебестоимостиТоваровОрганизации.Ссылка.ПредварительныйРасчет	КАК ПредварительныйРасчет,
	|	ДанныеДокумента.Дата									КАК ДатаПолученияСебестоимости
	|ПОМЕСТИТЬ ДанныеДокумента
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК РасчетСебестоимостиТоваровОрганизации
	|		ПО РасчетСебестоимостиТоваровОрганизации.Ссылка.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ)
	|			И РасчетСебестоимостиТоваровОрганизации.Ссылка.Проведен
	|			И ДанныеДокумента.Организация = РасчетСебестоимостиТоваровОрганизации.Организация
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО ДанныеДокумента.Склад = Склады.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Проведен
	|	И ДанныеДокумента.Ссылка В(&МассивДокументов)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка								КАК Ссылка,
	|	ДанныеДокумента.Номер								КАК Номер,
	|	ДанныеДокумента.Дата								КАК Дата,
	|	ДанныеДокумента.ДатаДокумента						КАК ДатаДокумента,
	|	ДанныеДокумента.Организация							КАК Организация,
	|	ДанныеДокумента.Склад								КАК Склад,
	|	ДанныеДокумента.ИсточникИнформацииОЦенахДляПечати	КАК ИсточникИнформацииОЦенахДляПечати,
	|	ДанныеДокумента.ВидЦены								КАК ВидЦены,
	|	ДанныеДокумента.ВалютаЦены							КАК ВалютаЦены,
	|	ДанныеДокумента.ПредварительныйРасчет				КАК ПредварительныйРасчет,
	|	ДанныеДокумента.ДатаПолученияСебестоимости			КАК ДатаПолученияСебестоимости
	|ПОМЕСТИТЬ ВтШапка
	|ИЗ
	|	ДанныеДокумента КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.СкладОтветственногоХранения
	|	И НЕ ДанныеДокумента.ОрганизацияПоклажедержатель
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Шапка.Склад КАК Склад,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.Серия КАК Серия,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения КАК ВидУпаковки,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Товары.Количество КАК Количество,
	|	КОНЕЦПЕРИОДА(Товары.Ссылка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	Шапка.ВидЦены КАК ВидЦены,
	|	Шапка.ВалютаЦены КАК ВалютаЦены
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО Товары.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	Товары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоВидуЦен)
	|		ИЛИ (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|			И Шапка.ПредварительныйРасчет ЕСТЬ NULL))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК Ссылка,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	Шапка.Организация КАК Организация,
	|	АналитикаУчетаНоменклатуры.СкладскаяТерритория КАК Склад,
	|	АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК Упаковка,
	|	ВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияНаименование,
	|	АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|	АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Представление ВидУпаковки,
	|	ВидыЗапасов.Количество КАК КоличествоУпаковок,
	|	ВидыЗапасов.Количество КАК Количество,
	|	КОНЕЦПЕРИОДА(ВидыЗапасов.Ссылка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	АналитикаУчетаНоменклатуры.СкладскаяТерритория.УчетныйВидЦены КАК ВидЦены,
	|	АналитикаУчетаНоменклатуры.СкладскаяТерритория.УчетныйВидЦены.ВалютаЦены КАК ВалютаЦены,
	|	Шапка.ПредварительныйРасчет КАК ПредварительныйРасчет
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.ВидыЗапасов КАК ВидыЗапасов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО ВидыЗапасов.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И АналитикаУчетаНоменклатуры.СкладскаяТерритория.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|;
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование", "Товары.Упаковка", "Товары.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код", "Товары.Упаковка", "Товары.Номенклатура"));
	
	СкладыСервер.ДополнитьТекстЗапросаДляПечатныхФормМХ1Х3(Запрос);
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	РезультатПоШапке			= МассивРезультатов[МассивРезультатов.ВГраница() - 3];
	РезультатПоСкладам			= МассивРезультатов[МассивРезультатов.ВГраница() - 2];
	РезультатПоТабличнойЧасти	= МассивРезультатов[МассивРезультатов.ВГраница() - 1];
	РезультатПоОшибкам			= МассивРезультатов[МассивРезультатов.ВГраница()];
	
	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("РезультатПоШапке",			РезультатПоШапке);
	СтруктураДанныхДляПечати.Вставить("РезультатПоСкладам",			РезультатПоСкладам);
	СтруктураДанныхДляПечати.Вставить("РезультатПоТабличнойЧасти",	РезультатПоТабличнойЧасти);
	СтруктураДанныхДляПечати.Вставить("РезультатПоОшибкам",			РезультатПоОшибкам);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Формирует временную таблицу, содержащую табличную часть по таблице данных документов.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц, содержащий таблицу ТаблицаДанныхДокументов с полями:
//		Ссылка.
//
//	ПараметрыЗаполнения - Структура - структура, возвращаемая функцией ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров.
//
Процедура ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения = Неопределено) Экспорт
	
	Если ПараметрыЗаполнения = Неопределено Тогда
		ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР КОГДА ВозвратТоваровОтКлиентаТовары.ДокументРеализации = НЕОПРЕДЕЛЕНО
	|		ТОГДА ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	|		ИНАЧЕ ВозвратТоваровОтКлиентаТовары.ДокументРеализации
	|	КОНЕЦ КАК Ссылка,
	|	ВозвратТоваровОтКлиентаТовары.Ссылка.Валюта КАК Валюта
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента.Товары КАК ВозвратТоваровОтКлиентаТовары
	|		ПО ТаблицаДанныхДокументов.Ссылка = ВозвратТоваровОтКлиентаТовары.Ссылка";
	
	ТаблицаОснований = Запрос.Выполнить().Выгрузить();
	МассивОснований = ТаблицаОснований.ВыгрузитьКолонку("Ссылка");
	
	// Избавимся от незаполненных документов продажи.
	МассивОснованийДляТипов = Новый Массив;
	Для Каждого ТекОснование Из МассивОснований Цикл
		Если Не ЗначениеЗаполнено(ТекОснование) Тогда
			Продолжить;
		КонецЕсли;
		
		МассивОснованийДляТипов.Добавить(ТекОснование);
	КонецЦикла;
	
	ТипыОснований = ОбщегоНазначенияУТ.СоответствиеМассивовПоТипамОбъектов(МассивОснованийДляТипов);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаОснований", ТаблицаОснований);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаОснований.Ссылка КАК Ссылка,
	|	ТаблицаОснований.Валюта КАК Валюта
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	&ТаблицаОснований КАК ТаблицаОснований";
	
	Запрос.Выполнить();
	
	ТекстыЗапроса = Новый Массив;
	
	Для Каждого ТипОснования Из ТипыОснований Цикл
		
		ИмяОбъекта  = Сред(ТипОснования.Ключ, 10);
		
		МенеджерОбъекта = ОбщегоНазначенияУТ.ПолучитьМодульЛокализации(ИмяОбъекта);
		Если МенеджерОбъекта = Неопределено Тогда
			МенеджерОбъекта = Документы[ИмяОбъекта];
		КонецЕсли;
		
		МенеджерОбъекта.ПоместитьВременнуюТаблицуТоваров(Запрос.МенеджерВременныхТаблиц, ПараметрыЗаполнения);
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаТоваров.Ссылка КАК Ссылка,
		|	ТаблицаТоваров.ВернутьМногооборотнуюТару КАК ВернутьМногооборотнуюТару,
		|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
		|	ТаблицаТоваров.Характеристика КАК Характеристика,
		|	ТаблицаТоваров.Серия КАК Серия,
		|	ТаблицаТоваров.Упаковка КАК Упаковка,
		|	ТаблицаТоваров.НомерГТД КАК НомерГТД,
		|	ТаблицаТоваров.Количество КАК Количество,
		|	ТаблицаТоваров.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ТаблицаТоваров.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
		|	ТаблицаТоваров.СуммаБезНДС КАК СуммаБезНДС,
		|	ТаблицаТоваров.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаТоваров.КодТНВЭД КАК КодТНВЭД,
		|	ТаблицаТоваров.СуммаНДС КАК СуммаНДС,
		|	ТаблицаТоваров.ЭтоТовар КАК ЭтоТовар,
		|	ТаблицаТоваров.ЭтоНеВозвратнаяТара КАК ЭтоНеВозвратнаяТара
		|ИЗ
		|	#ТаблицаТоваров КАК ТаблицаТоваров";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаТоваров", ИмяОбъекта + "ТаблицаТоваров");
		ТекстыЗапроса.Добавить(ТекстЗапроса);
	КонецЦикла;
	
	Если ТекстыЗапроса.Количество() = 0 Тогда
		// Если не заполнены документы основания, то создаем пустую исходную таблицу товаров.
		
		ТаблицаТоваровИсходная = Новый ТаблицаЗначений;
		ТаблицаТоваровИсходная.Колонки.Добавить("Ссылка", Документы.ТипВсеСсылки());
		ТаблицаТоваровИсходная.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		ТаблицаТоваровИсходная.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		ТаблицаТоваровИсходная.Колонки.Добавить("Серия", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
		ТаблицаТоваровИсходная.Колонки.Добавить("НомерГТД", Новый ОписаниеТипов("СправочникСсылка.НомераГТД"));
		ТаблицаТоваровИсходная.Колонки.Добавить("СтавкаНДС", Новый ОписаниеТипов("СправочникСсылка.СтавкиНДС"));
		ТаблицаТоваровИсходная.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
		ТаблицаТоваровИсходная.Колонки.Добавить("КоличествоПоРНПТ", Новый ОписаниеТипов("Число"));
		ТаблицаТоваровИсходная.Колонки.Добавить("СуммаБезНДС", Новый ОписаниеТипов("Число"));
		ТаблицаТоваровИсходная.Колонки.Добавить("СуммаНДС", Новый ОписаниеТипов("Число"));
	Иначе
		Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
		ТаблицаТоваровИсходная = Запрос.Выполнить().Выгрузить();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПересчитыватьВВалютуРегл", ПараметрыЗаполнения.ПересчитыватьВВалютуРегл);
	Запрос.УстановитьПараметр("ВключаяНомераГТД", ПараметрыЗаполнения.ВключаяНомераГТД);
	
	Запрос.УстановитьПараметр("ПустаяУпаковка", Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяГТД", Справочники.НомераГТД.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустойТНВЭД", Справочники.КлассификаторТНВЭД.ПустаяСсылка());
	
	Запрос.УстановитьПараметр("ТаблицаТоваровИсходная", ТаблицаТоваровИсходная);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	МИНИМУМ(ТаблицаДокумента.НомерСтроки) КАК НомерСтроки,
	|	Аналитика.Номенклатура КАК Номенклатура,
	|	Аналитика.Характеристика КАК Характеристика,
	|	Аналитика.Серия КАК Серия,
	|	ВЫБОР
	|		КОГДА &ВключаяНомераГТД
	|			ТОГДА ТаблицаДокумента.НомерГТД
	|		ИНАЧЕ &ПустаяГТД
	|	КОНЕЦ КАК НомерГТД,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	
	|	&ПустойТНВЭД КАК КодТНВЭД,
	|	ИСТИНА КАК ЭтоТовар,
	|	ВЫБОР
	|		КОГДА НЕ Аналитика.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|				ИЛИ НЕ Аналитика.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|				ИЛИ НЕ ТаблицаДокумента.Ссылка.ПредусмотренЗалогЗаТару
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЭтоНеВозвратнаяТара,
	|	ТаблицаДокумента.Ссылка.ПредусмотренЗалогЗаТару КАК ВернутьМногооборотнуюТару,
	|	&ПустаяУпаковка КАК Упаковка,
	|	ТаблицаДокумента.ДокументРеализации КАК ДокументРеализации,
	|	
	|	СУММА(ТаблицаДокумента.Количество) КАК Количество,
	|	СУММА(ТаблицаДокумента.КоличествоПоРНПТ) КАК КоличествоПоРНПТ,
	|	0 КАК КоличествоУпаковок,
	|	СУММА(ЕСТЬNULL(СуммыДокументовВВалютахУчета.СуммаБезНДСРегл, ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС)) КАК СуммаБезНДС,
	|	СУММА(ЕСТЬNULL(СуммыДокументовВВалютахУчета.СуммаНДСРегл, ТаблицаДокумента.СуммаНДС)) КАК СуммаНДС
	|ПОМЕСТИТЬ ТаблицаТоваровИзменение
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.ВидыЗапасов КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ТаблицаДокумента.Ссылка = ДанныеДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|		ПО ТаблицаДокумента.Ссылка = СуммыДокументовВВалютахУчета.Регистратор
	|			И ТаблицаДокумента.ИдентификаторСтроки = СуммыДокументовВВалютахУчета.ИдентификаторСтроки
	|			И (СуммыДокументовВВалютахУчета.Активность)
	|			И (&ПересчитыватьВВалютуРегл)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаДокумента.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Ссылка,
	|	Аналитика.Номенклатура,
	|	Аналитика.Характеристика,
	|	Аналитика.Серия,
	|	ВЫБОР
	|		КОГДА &ВключаяНомераГТД
	|			ТОГДА ТаблицаДокумента.НомерГТД
	|		ИНАЧЕ &ПустаяГТД
	|	КОНЕЦ,
	|	ТаблицаДокумента.СтавкаНДС,
	|	ТаблицаДокумента.ДокументРеализации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Серия КАК Серия,
	|	ТаблицаТоваров.НомерГТД КАК НомерГТД,
	|	ТаблицаТоваров.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ТаблицаТоваров.Количество КАК Количество,
	|	ТаблицаТоваров.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	ТаблицаТоваров.СуммаБезНДС КАК СуммаБезНДС,
	|	ТаблицаТоваров.СуммаНДС КАК СуммаНДС
	|ПОМЕСТИТЬ ТаблицаТоваровИсходная
	|ИЗ
	|	&ТаблицаТоваровИсходная КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваровИсходная.Ссылка КАК Ссылка,
	|	ТаблицаТоваровИсходная.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваровИсходная.Характеристика КАК Характеристика,
	|	ТаблицаТоваровИсходная.Серия КАК Серия,
	|	ТаблицаТоваровИсходная.НомерГТД КАК НомерГТД,
	|	ТаблицаТоваровИсходная.СтавкаНДС КАК СтавкаНДС,
	|	
	|	СУММА(ТаблицаТоваровИсходная.Количество) КАК Количество,
	|	СУММА(ТаблицаТоваровИсходная.КоличествоПоРНПТ) КАК КоличествоПоРНПТ,
	|	СУММА(ТаблицаТоваровИсходная.СуммаБезНДС) КАК СуммаБезНДС,
	|	СУММА(ТаблицаТоваровИсходная.СуммаНДС) КАК СуммаНДС
	|ПОМЕСТИТЬ ТаблицаТоваровИсходнаяСвернуто
	|ИЗ
	|	ТаблицаТоваровИсходная КАК ТаблицаТоваровИсходная
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваровИсходная.Ссылка,
	|	ТаблицаТоваровИсходная.Номенклатура,
	|	ТаблицаТоваровИсходная.Характеристика,
	|	ТаблицаТоваровИсходная.Серия,
	|	ТаблицаТоваровИсходная.НомерГТД,
	|	ТаблицаТоваровИсходная.СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваровИзменение.Ссылка КАК Ссылка,
	|	ТаблицаТоваровИзменение.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТоваровИзменение.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваровИзменение.Характеристика КАК Характеристика,
	|	ТаблицаТоваровИзменение.Серия КАК Серия,
	|	ТаблицаТоваровИзменение.Упаковка КАК Упаковка,
	|	ТаблицаТоваровИзменение.НомерГТД КАК НомерГТД,
	|	ТаблицаТоваровИзменение.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаТоваровИзменение.КодТНВЭД КАК КодТНВЭД,
	|	ТаблицаТоваровИзменение.ЭтоТовар КАК ЭтоТовар,
	|	ТаблицаТоваровИзменение.ЭтоНеВозвратнаяТара КАК ЭтоНеВозвратнаяТара,
	|	ТаблицаТоваровИзменение.ВернутьМногооборотнуюТару КАК ВернутьМногооборотнуюТару,
	|	ТаблицаТоваровИзменение.ДокументРеализации КАК ДокументРеализации,
	|	ТаблицаТоваровИзменение.Количество КАК Количество,
	|	ТаблицаТоваровИзменение.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТаблицаТоваровИзменение.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	ТаблицаТоваровИзменение.СуммаБезНДС КАК СуммаБезНДС,
	|	ТаблицаТоваровИзменение.СуммаНДС КАК СуммаНДС,
	|	ЛОЖЬ КАК ДоКорректировки
	|ПОМЕСТИТЬ ТаблицаТоваровВозвратТоваровОтКлиента
	|ИЗ
	|	ТаблицаТоваровИзменение КАК ТаблицаТоваровИзменение
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаТоваровИсходнаяСвернуто КАК ТаблицаТоваровИсходнаяСвернуто
	|		ПО ТаблицаТоваровИзменение.ДокументРеализации = ТаблицаТоваровИсходнаяСвернуто.Ссылка
	|			И ТаблицаТоваровИзменение.Номенклатура = ТаблицаТоваровИсходнаяСвернуто.Номенклатура
	|			И ТаблицаТоваровИзменение.Характеристика = ТаблицаТоваровИсходнаяСвернуто.Характеристика
	|			И ТаблицаТоваровИзменение.Серия = ТаблицаТоваровИсходнаяСвернуто.Серия
	|			И ТаблицаТоваровИзменение.НомерГТД = ТаблицаТоваровИсходнаяСвернуто.НомерГТД
	|			И ТаблицаТоваровИзменение.СтавкаНДС = ТаблицаТоваровИсходнаяСвернуто.СтавкаНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваровИзменение.Ссылка,
	|	ТаблицаТоваровИзменение.НомерСтроки,
	|	ТаблицаТоваровИзменение.Номенклатура,
	|	ТаблицаТоваровИзменение.Характеристика,
	|	ТаблицаТоваровИзменение.Серия КАК Серия,
	|	ТаблицаТоваровИзменение.Упаковка,
	|	ТаблицаТоваровИзменение.НомерГТД,
	|	ТаблицаТоваровИзменение.СтавкаНДС,
	|	ТаблицаТоваровИзменение.КодТНВЭД,
	|	ТаблицаТоваровИзменение.ЭтоТовар,
	|	ТаблицаТоваровИзменение.ЭтоНеВозвратнаяТара,
	|	ТаблицаТоваровИзменение.ВернутьМногооборотнуюТару,
	|	ТаблицаТоваровИзменение.ДокументРеализации,
	|	ЕСТЬNULL(ТаблицаТоваровИсходнаяСвернуто.Количество, 0),
	|	0,
	|	ЕСТЬNULL(ТаблицаТоваровИсходнаяСвернуто.КоличествоПоРНПТ, 0) КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(ТаблицаТоваровИсходнаяСвернуто.СуммаБезНДС, 0),
	|	ЕСТЬNULL(ТаблицаТоваровИсходнаяСвернуто.СуммаНДС, 0),
	|	ИСТИНА
	|ИЗ
	|	ТаблицаТоваровИзменение КАК ТаблицаТоваровИзменение
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаТоваровИсходнаяСвернуто КАК ТаблицаТоваровИсходнаяСвернуто
	|		ПО ТаблицаТоваровИзменение.ДокументРеализации = ТаблицаТоваровИсходнаяСвернуто.Ссылка
	|			И ТаблицаТоваровИзменение.Номенклатура = ТаблицаТоваровИсходнаяСвернуто.Номенклатура
	|			И ТаблицаТоваровИзменение.Характеристика = ТаблицаТоваровИсходнаяСвернуто.Характеристика
	|			И ТаблицаТоваровИзменение.Серия = ТаблицаТоваровИсходнаяСвернуто.Серия
	|			И ТаблицаТоваровИзменение.НомерГТД = ТаблицаТоваровИсходнаяСвернуто.НомерГТД
	|			И ТаблицаТоваровИзменение.СтавкаНДС = ТаблицаТоваровИсходнаяСвернуто.СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаТоваровИзменение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаТоваровИсходная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаТоваровИсходнаяСвернуто";
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Преобразует временную таблицу товаров, созданную функцией ПоместитьВременнуюТаблицуТоваров()
// к виду, используемому при печати счетов-фактуры.
// После преобразования временная таблица уничтожается.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц, содержащий таблицу ТаблицаТоваровВозвратТоваровОтКлиента.
//
Процедура ПреобразоватьВременнуюТаблицуТоваровДляСчетаФактуры(МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                  КАК Ссылка,
	|	ТаблицаДокумента.Номенклатура                            КАК Номенклатура,
	|	ТаблицаДокумента.Характеристика                          КАК Характеристика,
	|	ТаблицаДокумента.Серия                                   КАК Серия,
	|	ТаблицаДокумента.Упаковка                                КАК Упаковка,
	|	ТаблицаДокумента.НомерГТД                                КАК НомерГТД,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0) - ТаблицаДокумента.Количество                 КАК Количество,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоУпаковок, 0) - ТаблицаДокумента.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0)         КАК КоличествоДо,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоУпаковок, 0) КАК КоличествоУпаковокДо,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0) - ТаблицаДокумента.КоличествоПоРНПТ     КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0)   КАК КоличествоПоРНПТДо,
	|	0                                                        КАК КоличествоПоРНПТУвеличение,
	|	ТаблицаДокумента.КоличествоПоРНПТ                        КАК КоличествоПоРНПТУменьшение,
	|	ТаблицаДокумента.СтавкаНДС                               КАК СтавкаНДС,
	|	ТаблицаДокумента.КодТНВЭД                                КАК КодТНВЭД,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) - ТаблицаДокумента.СуммаБезНДС               КАК СуммаБезНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)                                              КАК СуммаБезНДСДо,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0) - ТаблицаДокумента.СуммаНДС                     КАК СуммаНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)                                                 КАК СуммаНДСДо,
	|	0                                                        КАК МежценоваяРазница,
	|	0                                                        КАК МежценоваяРазницаДо,
	|	0                                                        КАК РазницаБезНДСУвеличение,
	|	ТаблицаДокумента.СуммаБезНДС                             КАК РазницаБезНДСУменьшение,
	|	0                                                        КАК РазницаНДСУвеличение,
	|	ТаблицаДокумента.СуммаНДС                                КАК РазницаНДСУменьшение,
	|	0                                                        КАК РазницаСНДСУвеличение,
	|	ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС КАК РазницаСНДСУменьшение,
	|	ТаблицаДокумента.НомерСтроки                             КАК НомерСтроки,
	|	ТаблицаДокумента.ЭтоТовар                                КАК ЭтоТовар,
	|	ЛОЖЬ                                                     КАК ВернутьМногооборотнуюТару
	|ПОМЕСТИТЬ ВозвратТоваровОтКлиентаТаблицаТоваров
	|ИЗ
	|	ТаблицаТоваровВозвратТоваровОтКлиента КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаТоваровВозвратТоваровОтКлиента КАК ТаблицаТоваровПредыдущие
	|		ПО (ТаблицаТоваровПредыдущие.ДоКорректировки)
	|			И ТаблицаДокумента.Ссылка = ТаблицаТоваровПредыдущие.Ссылка
	|			И ТаблицаДокумента.ДокументРеализации = ТаблицаТоваровПредыдущие.ДокументРеализации
	|			И ТаблицаДокумента.Номенклатура = ТаблицаТоваровПредыдущие.Номенклатура
	|			И ТаблицаДокумента.Характеристика = ТаблицаТоваровПредыдущие.Характеристика
	|			И ТаблицаДокумента.Серия = ТаблицаТоваровПредыдущие.Серия
	|			И ТаблицаДокумента.НомерГТД = ТаблицаТоваровПредыдущие.НомерГТД
	|			И ТаблицаДокумента.СтавкаНДС = ТаблицаТоваровПредыдущие.СтавкаНДС
	|ГДЕ
	|	НЕ ТаблицаДокумента.ДоКорректировки
	|;
	|
	|/////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаТоваровВозвратТоваровОтКлиента
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

//-- Локализация
#КонецОбласти


// Заполняет массив допустимых наименований входящих документов.
//
// Параметры:
//  МассивНаименований	 - Массив - массив наименования входящих документов.
//
Процедура ДополнитьНаименованияВходящихДокументов(МассивНаименований) Экспорт
	
КонецПроцедуры

//++ Локализация
#Область Фискализация

// Возвращает параметры операции фискализации чека для печати чека по документу
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Форма документ, из которого печатается чек - содержит:
// 	* Объект - ДокументОбъект - Документ-объект, основной параметр формы.
// Возвращаемое значение:
// 	Структура - Структура параметров операции фискализации чека
Функция ОсновныеПараметрыОперации(Форма) Экспорт
	
	ОсновныеПараметрыОперации = ФормированиеФискальныхЧековСерверПереопределяемый.СтруктураОсновныхПараметровОперации();
	
	ОсновныеПараметрыОперации.ДокументСсылка       = Форма.Объект.Ссылка;
	ОсновныеПараметрыОперации.Организация          = Форма.Объект.Организация;
	ОсновныеПараметрыОперации.Контрагент           = Форма.Объект.Контрагент;
	ОсновныеПараметрыОперации.Партнер              = Форма.Объект.Партнер;
	ОсновныеПараметрыОперации.Валюта               = Форма.Объект.Валюта;
	ОсновныеПараметрыОперации.СуммаДокумента       = Форма.Объект.СуммаДокумента;
	
	Возврат ОсновныеПараметрыОперации;
	
КонецФункции

// Определяет, разрешено ли пробитие фискального чека по документу
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Форма документ, из которого печатается чек - содержит:
// 	* Объект - ДокументОбъект - Документ-объект, основной параметр формы.
// Возвращаемое значение:
// 	Булево - Истина, если разрешено пробитие чека
Функция РазрешеноПробитиеФискальныхЧековПоДокументу(Форма) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Форма.Объект.Контрагент)
		ИЛИ НЕ ЗначениеЗаполнено(Форма.Объект.ХозяйственнаяОперация) Тогда
		
		Возврат Ложь;
	КонецЕсли;
	
	РазрешеноПробитиеФискальныхЧековПоДокументу = Истина;
	
	ЮрФизЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Форма.Объект.Контрагент, "ЮрФизЛицо");
	РазрешеноПробитиеФискальныхЧековПоДокументу = (ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо);
	
	Если Форма.Объект.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ВозвратТоваровОтКлиента
		И Форма.Объект.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ВозвратОтРозничногоПокупателя Тогда
		
		РазрешеноПробитиеФискальныхЧековПоДокументу	= Ложь;
	КонецЕсли;
	
	Возврат РазрешеноПробитиеФискальныхЧековПоДокументу;
	
КонецФункции

// Формирует массив форматированных строк для формирования гиперссылки пробития фискального чека
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ-ссылка, по которому пробивается фискальный чек
// 	Форма - ФормаКлиентскогоПриложения - Форма документ, из которого печатается чек - содержит:
// 	* Объект - ДокументОбъект - Документ-объект, основной параметр формы.
// 	МассивПредставлений - Массив из ФорматированнаяСтрока - Массив форматированных строк для формирования гиперссылки
//    пробития фискального чека.
Процедура ОбновитьГиперссылкуПробитияФискальногоЧека(ДокументСсылка, Форма, МассивПредставлений) Экспорт
	
	ФискальнаяОперацияДанныеЖурнала = ФормированиеФискальныхЧековСервер.ДанныеПробитогоФискальногоЧекаПоДокументу(ДокументСсылка);
	
	Если ФискальнаяОперацияДанныеЖурнала <> Неопределено Тогда
		
		НомерЧекаККМ = ФискальнаяОперацияДанныеЖурнала.НомерЧекаККМ;
		ТекстСсылки = "ОткрытьЗаписьФискальнойОперации";
		
		ФормированиеФискальныхЧековСервер.ДобавитьВПредставлениеГиперссылкиКомандуЧекПробит(МассивПредставлений, НомерЧекаККМ, ТекстСсылки);
		
	ИначеЕсли ФормированиеФискальныхЧековСервер.ЕстьПравоНаПробитиеФискальногоЧекаПоДокументу(ДокументСсылка) Тогда
		
		Если ФормированиеФискальныхЧековСервер.ЕстьПодключенноеОборудованиеККассамОрганизации(Форма.Объект.Организация) Тогда 
			ФормированиеФискальныхЧековСервер.ДобавитьВПредставлениеГиперссылкиКомандуПробитьЧек(МассивПредставлений, "ПробитьЧек");
		Иначе
			ФормированиеФискальныхЧековСервер.ДобавитьВПредставлениеГиперссылкиСтатусЧекНеПробит(МассивПредставлений, "НастроитьОборудование");
		КонецЕсли;
		
	Иначе
		
		ФормированиеФискальныхЧековСервер.ДобавитьВПредставлениеГиперссылкиСтатусЧекНеПробит(МассивПредставлений);
		
	КонецЕсли;
	
КонецПроцедуры

// Определяет виды фискальных чеков, доступных по документу
// 
// Параметры:
// 	ВидыЧеков - ТаблицаЗначений - Таблица значений, содержащая виды фискальных чеков, доступных по документу
// 	Операция - ПеречислениеСсылка.ХозяйственныеОперации - Хозяйственная операция по документу
// 	ИмяКомандыПробитияЧека - Строка - Имя команды пробития чека
Процедура ЗаполнитьВидыФискальныхЧековПоДокументу(ВидыЧеков, Операция, ИмяКомандыПробитияЧека) Экспорт
	
	ВидЧека = ВидыЧеков.Добавить();
	ВидЧека.ТипФискальногоДокумента = Перечисления.ТипыФискальныхДокументовККТ.КассовыйЧек;
	ВидЧека.ТипРасчетаДенежнымиСредствами = Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств;
	ВидЧека.ВидЧекаКоррекции = Неопределено;
	
КонецПроцедуры

// Возвращает таблицу товаров для заполнения позиций строк в параметрах чека
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ для получения товарных позиций
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблицу с товарными позициями с количественными и суммовыми показателями
Функция ПозицииНоменклатурыПоДокументу(ДокументСсылка) Экспорт
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка,
		"ЦенаВключаетНДС, ПредусмотренЗалогЗаТару, ВозвратПереданнойМногооборотнойТары");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки                   КАК НомерСтроки,
	|	ТабличнаяЧасть.Номенклатура                  КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика                КАК Характеристика,
	|	ТабличнаяЧасть.Упаковка                      КАК Упаковка,
	|	
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.КоличествоУпаковок = 0
	|			ТОГДА 1
	|		ИНАЧЕ ТабличнаяЧасть.КоличествоУпаковок
	|	КОНЕЦ КАК Количество,
	|	
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА &ЦенаВключаетНДС ТОГДА
	|			ТабличнаяЧасть.Цена
	|		КОГДА ТабличнаяЧасть.КоличествоУпаковок = 0 ТОГДА
	|			ТабличнаяЧасть.СуммаСНДС
	|		ИНАЧЕ
	|			(ТабличнаяЧасть.СуммаСНДС)
	|			/ ТабличнаяЧасть.КоличествоУпаковок
	|	КОНЕЦ КАК ЧИСЛО(31,2)) КАК Цена,
	|	
	|	ТабличнаяЧасть.СуммаСНДС КАК Сумма,
	|	ТабличнаяЧасть.СтавкаНДС КАК СтавкаНДС,
	|	ТабличнаяЧасть.СуммаНДС КАК СуммаНДС
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|	И (НЕ &ВернутьМногооборотнуюТару
	|		ИЛИ (Не &ТребуетсяЗалогЗаТару
	|			И ТабличнаяЧасть.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ИЛИ &ТребуетсяЗалогЗаТару)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Количество             КАК Количество,
	|	ТаблицаНоменклатуры.Сумма                  КАК Сумма,
	|	ТаблицаНоменклатуры.СтавкаНДС              КАК СтавкаНДС,
	|	ТаблицаНоменклатуры.СуммаНДС               КАК СуммаНДС,
	|	ТаблицаНоменклатуры.Цена                   КАК Цена,
	|	
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаНоменклатуры.Номенклатура) = ТИП(Справочник.Номенклатура) ТОГДА
	|			СпрНоменклатура.НаименованиеПолное
	|		ИНАЧЕ
	|			ТаблицаНоменклатуры.Номенклатура
	|	КОНЕЦ КАК НоменклатураНаименование,
	|	ЕСТЬNULL(СпрХарактеристикиНоменклатуры.НаименованиеПолное, """") КАК ХарактеристикаНаименование,
	|	ТаблицаНоменклатуры.Упаковка               КАК Упаковка,
	|	ТаблицаНоменклатуры.Упаковка               КАК УпаковкаНаименование
	|
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|	ПО СпрНоменклатура.Ссылка = ТаблицаНоменклатуры.Номенклатура
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК СпрХарактеристикиНоменклатуры
	|	ПО СпрХарактеристикиНоменклатуры.Ссылка = ТаблицаНоменклатуры.Характеристика
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаНоменклатуры.НомерСтроки
	|";
	Запрос.УстановитьПараметр("Ссылка",                    ДокументСсылка);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС",           РеквизитыДокумента.ЦенаВключаетНДС);
	Запрос.УстановитьПараметр("ТребуетсяЗалогЗаТару",      РеквизитыДокумента.ПредусмотренЗалогЗаТару);
	Запрос.УстановитьПараметр("ВернутьМногооборотнуюТару", РеквизитыДокумента.ВозвратПереданнойМногооборотнойТары);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Определяет систему налогообложения по документу
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ для определения системы налогообложения
// Возвращаемое значение:
// 	ПеречислениеСсылка.ТипыСистемНалогообложенияККТ - Система налогообложения по документу
Функция СистемаНалогообложенияПоДокументу(ДокументСсылка) Экспорт
	
	СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД;
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Организация, НалогообложениеНДС");
	Если РеквизитыДокумента.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаПоПатенту Тогда
		СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.Патент;
	ИначеЕсли РеквизитыДокумента.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД Тогда
		СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД;
	Иначе
		СистемаНалогообложения = РозничныеПродажиЛокализация.СистемаНалогообложенияФискальнойОперации(РеквизитыДокумента.Организация);
	КонецЕсли;
	
	Возврат СистемаНалогообложения;
	
КонецФункции

// Возвращает таблицу товаров для заполнения позиций строк в параметрах чека на поставку
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ для получения товарных позиций
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблица с товарными позициями с количественными и суммовыми показателями
Функция ПредметыРасчетовПоДокументу(ДокументСсылка) Экспорт
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка,
		"ЦенаВключаетНДС, ПредусмотренЗалогЗаТару, ВозвратПереданнойМногооборотнойТары");
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки                   КАК НомерСтроки,
	|	ТабличнаяЧасть.АналитикаУчетаНоменклатуры    КАК АналитикаУчетаНоменклатуры,
	|	ТабличнаяЧасть.Номенклатура                  КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика                КАК Характеристика,
	|	ТабличнаяЧасть.Серия                         КАК Серия,
	|	ТабличнаяЧасть.СтатусУказанияСерий           КАК СтатусУказанияСерий,
	|	ТабличнаяЧасть.Упаковка                      КАК Упаковка,
	|	ТабличнаяЧасть.Номенклатура.ЕдиницаИзмерения КАК БазоваяУпаковка,
	|	
	|	СпрНоменклатура.КодВидаНоменклатурнойКлассификации,
	|	
	|	ТабличнаяЧасть.Номенклатура.ТипНоменклатуры  КАК ТипНоменклатуры,
	|	ТабличнаяЧасть.Номенклатура.ПодакцизныйТовар КАК ПодакцизныйТовар,
	|	ТабличнаяЧасть.Номенклатура.ОсобенностьУчета КАК ОсобенностьУчета,
	|
	|	ТабличнаяЧасть.Номенклатура.НаименованиеПолное   КАК НоменклатураНаименование,
	|	ТабличнаяЧасть.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТабличнаяЧасть.Упаковка                          КАК УпаковкаНаименование,
	|
	|	ТабличнаяЧасть.Количество 					КАК Количество,
	|	ТабличнаяЧасть.КоличествоУпаковок 			КАК КоличествоУпаковок,
	|
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА &ЦенаВключаетНДС ТОГДА
	|			ТабличнаяЧасть.Цена
	|		КОГДА ТабличнаяЧасть.КоличествоУпаковок = 0 ТОГДА
	|			ТабличнаяЧасть.СуммаСНДС
	|		ИНАЧЕ
	|			(ТабличнаяЧасть.СуммаСНДС)
	|			/ ТабличнаяЧасть.КоличествоУпаковок
	|	КОНЕЦ КАК ЧИСЛО(31,2)) КАК Цена,
	|	
	|	0                        КАК СуммаСкидки,
	|	ТабличнаяЧасть.СтавкаНДС КАК СтавкаНДС,
	|	ТабличнаяЧасть.СуммаНДС  КАК СуммаНДС,
	|	ТабличнаяЧасть.СуммаСНДС КАК СуммаСНДС,
	|	ТабличнаяЧасть.Штрихкод КАК Штрихкод,
	|	СпрНоменклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины В (&МерныеТипыЕдиницИзмерений) КАК МернаяЕдиницаИзмерения
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.Товары КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО СпрНоменклатура.Ссылка = ТабличнаяЧасть.Номенклатура
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|	И (НЕ &ВернутьМногооборотнуюТару
	|		ИЛИ (Не &ТребуетсяЗалогЗаТару
	|			И ТабличнаяЧасть.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ИЛИ &ТребуетсяЗалогЗаТару)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШтрихкодыУпаковок.ШтрихкодУпаковки               КАК ШтрихкодУпаковки,
	|	ШтрихкодыУпаковок.ЧастичноеВыбытиеВариантУчета   КАК ЧастичноеВыбытиеВариантУчета,
	|	ШтрихкодыУпаковок.ЧастичноеВыбытиеНоменклатура   КАК ЧастичноеВыбытиеНоменклатура,
	|	ШтрихкодыУпаковок.ЧастичноеВыбытиеХарактеристика КАК ЧастичноеВыбытиеХарактеристика,
	|	ШтрихкодыУпаковок.ЧастичноеВыбытиеКоличество     КАК ЧастичноеВыбытиеКоличество
	|ПОМЕСТИТЬ ТаблицаАкцизныеМарки
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.ШтрихкодыУпаковок КАК ШтрихкодыУпаковок
	|ГДЕ
	|	ШтрихкодыУпаковок.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Упаковка КАК Упаковка,
	|	Штрихкоды.Штрихкод КАК Штрихкод,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Упаковка = Штрихкоды.Упаковка
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ПриоритетШтрихКода
	|ПОМЕСТИТЬ Штрихкоды
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК Штрихкоды
	|		ПО ТаблицаТовары.Номенклатура = Штрихкоды.Номенклатура
	|			И ТаблицаТовары.Характеристика = Штрихкоды.Характеристика
	|			И (ТаблицаТовары.Упаковка = Штрихкоды.Упаковка
	|				ИЛИ &ТекстЗапросаКоэффициентУпаковки1 = 1
	|					И ТаблицаТовары.Номенклатура.ЕдиницаИзмерения = ТаблицаТовары.Упаковка.ЕдиницаИзмерения
	|					И Штрихкоды.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Штрихкоды.Номенклатура КАК Номенклатура,
	|	Штрихкоды.Характеристика КАК Характеристика,
	|	Штрихкоды.Упаковка КАК Упаковка,
	|	МАКСИМУМ(Штрихкоды.ПриоритетШтрихКода) КАК ПриоритетШтрихКода
	|ПОМЕСТИТЬ ПриоритетыШтрихКода
	|ИЗ
	|	Штрихкоды КАК Штрихкоды
	|
	|СГРУППИРОВАТЬ ПО
	|	Штрихкоды.Номенклатура,
	|	Штрихкоды.Характеристика,
	|	Штрихкоды.Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Штрихкоды.Номенклатура КАК Номенклатура,
	|	Штрихкоды.Характеристика КАК Характеристика,
	|	Штрихкоды.Упаковка КАК Упаковка,
	|	МАКСИМУМ(Штрихкоды.Штрихкод) КАК Штрихкод
	|ПОМЕСТИТЬ ШтрихкодыНоменклатуры
	|ИЗ
	|	Штрихкоды КАК Штрихкоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПриоритетыШтрихКода КАК ПриоритетыШтрихКода
	|		ПО Штрихкоды.Номенклатура = ПриоритетыШтрихКода.Номенклатура
	|			И Штрихкоды.Характеристика = ПриоритетыШтрихКода.Характеристика
	|			И Штрихкоды.Упаковка = ПриоритетыШтрихКода.Упаковка
	|			И Штрихкоды.ПриоритетШтрихКода = ПриоритетыШтрихКода.ПриоритетШтрихКода
	|
	|СГРУППИРОВАТЬ ПО
	|	Штрихкоды.Номенклатура,
	|	Штрихкоды.Характеристика,
	|	Штрихкоды.Упаковка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	Товары.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.БазоваяУпаковка КАК БазоваяУпаковка,
	|	Товары.КодВидаНоменклатурнойКлассификации КАК КодВидаНоменклатурнойКлассификации,
	|	Товары.ТипНоменклатуры КАК ТипНоменклатуры,
	|	Товары.ПодакцизныйТовар КАК ПодакцизныйТовар,
	|	Товары.ОсобенностьУчета КАК ОсобенностьУчета,
	|	Товары.НоменклатураНаименование КАК НоменклатураНаименование,
	|	Товары.ХарактеристикаНаименование КАК ХарактеристикаНаименование,
	|	Товары.УпаковкаНаименование КАК УпаковкаНаименование,
	|	Товары.Количество КАК Количество,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Товары.Цена КАК Цена,
	|	Товары.СуммаСкидки КАК СуммаСкидки,
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	Товары.СуммаНДС КАК СуммаНДС,
	|	Товары.СуммаСНДС КАК СуммаСНДС,
	|	ВЫБОР
	|		КОГДА Товары.МернаяЕдиницаИзмерения
	|			ТОГДА """"
	|		КОГДА Товары.Штрихкод = """"
	|			ТОГДА ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """")
	|		ИНАЧЕ Товары.Штрихкод	
	|	КОНЕЦ КАК Штрихкод
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	ТаблицаТовары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО (ШтрихкодыНоменклатуры.Номенклатура = Товары.Номенклатура)
	|			И (ШтрихкодыНоменклатуры.Характеристика = Товары.Характеристика)
	|			И (ШтрихкодыНоменклатуры.Упаковка = Товары.Упаковка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Штрихкоды
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ШтрихкодыНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПриоритетыШтрихКода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.НомерСтроки КАК НомерСтроки,
	|	Серии.Серия КАК СерияТаблицыСерий,
	|	Серии.Количество КАК КоличествоУпаковок,
	|	Серии.Номенклатура КАК Номенклатура,
	|	Серии.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ Серии
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.Серии КАК Серии
	|ГДЕ
	|	Серии.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки,
	|	Товары.АналитикаУчетаНоменклатуры,
	|	Товары.ТипНоменклатуры,
	|	Товары.ПодакцизныйТовар,
	|	Товары.СтатусУказанияСерий,
	|	Товары.Упаковка,
	|	Товары.БазоваяУпаковка,
	|	Товары.ОсобенностьУчета,
	|	Товары.Номенклатура,
	|	Товары.НоменклатураНаименование,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.ХарактеристикаНаименование,
	|	Товары.Серия,
	|	Товары.СтатусУказанияСерий,
	|	Товары.УпаковкаНаименование,
	|	Товары.КодВидаНоменклатурнойКлассификации,
	|	Товары.КоличествоУпаковок,
	|	Товары.Количество,
	|	Товары.Цена,
	|	Товары.СуммаСкидки,
	|	Товары.СтавкаНДС,
	|	Товары.СуммаНДС,
	|	Товары.СуммаСНДС,
	|	Товары.Штрихкод
	|ИЗ Товары
	|
	|УПОРЯДОЧИТЬ ПО
	|	Товары.НомерСтроки
	|");
	
	Запрос.УстановитьПараметр("Ссылка",          ДокументСсылка);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС", РеквизитыДокумента.ЦенаВключаетНДС);
	Запрос.УстановитьПараметр("ТребуетсяЗалогЗаТару"     , РеквизитыДокумента.ПредусмотренЗалогЗаТару);
	Запрос.УстановитьПараметр("ВернутьМногооборотнуюТару", РеквизитыДокумента.ВозвратПереданнойМногооборотнойТары);
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ТаблицаТовары.Упаковка",
		"ТаблицаТовары.Номенклатура"));
	Запрос.УстановитьПараметр("МерныеТипыЕдиницИзмерений", Справочники.УпаковкиЕдиницыИзмерения.МерныеТипыЕдиницИзмерений());
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Товары = РезультатЗапроса[РезультатЗапроса.Количество() - 1].Выгрузить(); // ТаблицаЗначений
	Если НЕ ИнтеграцияГИСМ.ПодсистемаНеИспользуется() Тогда
		ДанныеКИЗиЕГАИС(ДокументСсылка, Товары);
	КонецЕсли;
	
	ТаблицаТоваровСРаспределениемПоВидамЗапасов(Товары, ДанныеПоВидамЗапасов(ДокументСсылка, РеквизитыДокумента.ЦенаВключаетНДС));
	
	ДанныеДляИСМП = РозничныеПродажиЛокализация.ДанныеДляИСМП(ДокументСсылка, МенеджерВременныхТаблиц);
	ФормированиеФискальныхЧековСерверПереопределяемый.ТаблицаТоваровСРаспределениемКодовМаркировкиИСМП(Товары, ДанныеДляИСМП);
	
	Возврат Товары;
	
КонецФункции

#КонецОбласти
//-- Локализация

#Область МаркируемаяПродукция

// Определяет ведение учета маркируемой продукции.
// 
// Возвращаемое значение:
//  Булево - Используется маркируемая продукция
Функция ИспользуетсяМаркируемаяПродукция() Экспорт
	
	Возврат СкладыЛокализация.ИспользуетсяМаркируемаяПродукция();
	
КонецФункции

// Заполнить кеш штрихкодов упаковок.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
Процедура ЗаполнитьКешШтрихкодовУпаковок(Форма) Экспорт
	
	//++ Локализация
	ПроверкаИПодборПродукцииИС.ЗаполнитьКешШтрихкодовУпаковок(Форма);
	//-- Локализация
	
КонецПроцедуры

// Сформировать штрих коды упаковок.
// 
// Параметры:
//  РаспоряженияДляОрдеров - Массив из ОпределяемыйТип.РаспоряжениеНаПоступление - Распоряжения для ордеров
//  ТаблицаШтрихкодыУпаковок - ТаблицаЗначений - Таблица штрихкоды упаковок
Процедура СформироватьШтрихКодыУпаковок(РаспоряженияДляОрдеров, ТаблицаШтрихкодыУпаковок) Экспорт

	//++ Локализация
	ТаблицаШтрихкодыУпаковок = ИнициализироватьТаблицуШтрихкодыУпаковок();
	ТаблицаОрдеров = ПолучитьОрдераПоРаспоряжению(РаспоряженияДляОрдеров);
	Для Каждого Строка Из ТаблицаОрдеров Цикл

		ШтрихкодыУпаковокПоОрдеру = ШтрихкодированиеИСМП.ВложенныеШтрихкодыУпаковокПоДокументу(
			Строка.Ордер, Неопределено);
			
		Если ШтрихкодыУпаковокПоОрдеру.ЕстьОшибки Тогда
			Продолжить;
		КонецЕсли; 
			
		Для Каждого МаркированныйТовар Из ШтрихкодыУпаковокПоОрдеру.ВложенныеШтрихкоды.МаркированныеТовары Цикл
			НоваяСтрока = ТаблицаШтрихкодыУпаковок.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, МаркированныйТовар);
			НоваяСтрока.Склад = Строка.Склад;
		КонецЦикла;
	КонецЦикла;
	//-- Локализация
КонецПроцедуры

//++ Локализация

Функция ПолучитьОрдераПоРаспоряжению(Распоряжения)
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Таблица.ДокументПоступления КАК Распоряжение,
	|	Выразить(Таблица.Регистратор КАК Документ.ПриходныйОрдерНаТовары) КАК Ордер,
	|	Выразить(Таблица.Регистратор КАК Документ.ПриходныйОрдерНаТовары).Склад КАК Склад
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Обороты(,, Регистратор, ДокументПоступления В (&Распоряжения)) КАК Таблица
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Таблица.Регистратор) = ТИП(Документ.ПриходныйОрдерНаТовары)
	|	И Таблица.КОформлениюОрдеровРасход > 0
	|СГРУППИРОВАТЬ ПО
	|	Таблица.ДокументПоступления,
	|	Выразить(Таблица.Регистратор КАК Документ.ПриходныйОрдерНаТовары),
	|	Выразить(Таблица.Регистратор КАК Документ.ПриходныйОрдерНаТовары).Склад
	|УПОРЯДОЧИТЬ ПО
	|	Ордер, Распоряжение";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("Распоряжения", Распоряжения);
	
	ТаблицаОрдеровПоРаспоряжению = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаОрдеровПоРаспоряжению;
	
КонецФункции

//-- Локализация

// Сформировать структуру штрихкоды упаковок.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Сформировать структуру штрихкоды упаковок:
// * ШтрихкодУпаковки - СправочникСсылка -
// * ИзОрдера - Булево -
Функция СформироватьСтруктуруШтрихкодыУпаковок() Экспорт

	Таблица = Новый ТаблицаЗначений();
	//++ Локализация
	Таблица.Колонки.Добавить("ШтрихкодУпаковки",Новый ОписаниеТипов("СправочникСсылка.ШтрихкодыУпаковокТоваров"));
	Таблица.Колонки.Добавить("ИзОрдера",Новый ОписаниеТипов("Булево"));
	//-- Локализация
	
	Возврат Таблица;
	
КонецФункции
#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация


	//-- Локализация
	
КонецПроцедуры

//++ Локализация


//-- Локализация

#КонецОбласти

#Область Прочее

//++ Локализация
Процедура ЗаполнитьДокументНаОснованииЧекаККМ(Объект, Знач ДокументОснование)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтРозничногоПокупателя) КАК ХозяйственнаяОперация,
		|	ОтчетОРозничныхПродажах.Ссылка    КАК ДокументРеализации,
		|	ОтчетОРозничныхПродажах.Подразделение КАК Подразделение,
		|	ЧекККМ.Проведен                   КАК Проведен,
		|	ЧекККМ.Партнер                    КАК Партнер,
		|	ЧекККМ.Ссылка                     КАК Ссылка,
		|	ЧекККМ.Статус                     КАК Статус,
		|	ЧекККМ.КассоваяСмена              КАК КассоваяСмена,
		|	ЧекККМ.Организация                КАК Организация,
		|	ЧекККМ.Валюта                     КАК Валюта,
		|	ЧекККМ.Валюта                     КАК ВалютаВзаиморасчетов,
		|	ЧекККМ.Склад                      КАК Склад,
		|	ЧекККМ.ЦенаВключаетНДС            КАК ЦенаВключаетНДС,
		|	ЧекККМ.НалогообложениеНДС         КАК НалогообложениеНДС,
		|
		|	ВЫБОР КОГДА ЧекККМ.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит) ТОГДА ЛОЖЬ ИНАЧЕ ИСТИНА КОНЕЦ КАК ЕстьОшибкиСтатус,
		|
		|	ЧекККМ.Серии.(
		|		Номенклатура                                 КАК Номенклатура,
		|		Характеристика                               КАК Характеристика,
		|		Серия                                        КАК Серия,
		|		Количество                                   КАК Количество
		|	) КАК Серии
		|
		|ИЗ
		|	Документ.ЧекККМ КАК ЧекККМ
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
		|		ПО ЧекККМ.КассоваяСмена = ОтчетОРозничныхПродажах.КассоваяСмена
		|ГДЕ
		|	ЧекККМ.Ссылка = &ДокументОснование");
		
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	ДанныеЧекККМ = Запрос.Выполнить().Выбрать();
	ДанныеЧекККМ.Следующий();
		
	МассивДопустимыхСтатусов = Новый Массив();
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЧековККМ.Пробит);
	
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
		ДанныеЧекККМ.Ссылка,
		ДанныеЧекККМ.Статус,
		,
		ДанныеЧекККМ.ЕстьОшибкиСтатус,
		МассивДопустимыхСтатусов);
	
	ПроверитьВозможностьВводаНаОснованииЧекККМ(Объект, ДанныеЧекККМ);
	
	ЗаполнитьЗначенияСвойств(Объект, ДанныеЧекККМ, , "Ссылка,Проведен");
	Объект.ЧекККМ = ДанныеЧекККМ.Ссылка;
		
	ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Объект.Партнер, Объект.Контрагент);
	
	ПродажиСервер.ЗаполнитьВозвращаемыеТоварыПоЧеку(Объект, "Товары");
	
	Если Объект.Товары.Количество() = 0 Тогда
		ТекстОшибки = НСтр("ru='По данному чеку ККМ нет товаров для возврата или все товары уже возвращены.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ДокументРеализации = РозничныеПродажи.ПолучитьОтчетОРозничныхПродажахПоЧекуККМ(ДокументОснование);
	
	Для Каждого ТекСтрока Из Объект.Товары Цикл
		
		ТекСтрока.ДокументРеализации = ДокументРеализации;
		ТекСтрока.СпособОпределенияСебестоимости = ?(ЗначениеЗаполнено(ТекСтрока.ДокументРеализации),
			Перечисления.СпособыОпределенияСебестоимостиВозврата.ИзДокументаПродажи,
			Перечисления.СпособыОпределенияСебестоимостиВозврата.ИзТекущегоДокумента);
			
	КонецЦикла;
	
	Объект.Серии.Загрузить(ДанныеЧекККМ.Серии.Выгрузить());
	
КонецПроцедуры

Процедура ПроверитьВозможностьВводаНаОснованииЧекККМ(Объект, ЧекККМ, Отказ = Неопределено)
	
	Если ТипЗнч(ЧекККМ) = Тип("ДокументСсылка.ЧекККМ") Тогда
		ДанныеЧекККМ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЧекККМ, "Проведен,Статус,Ссылка,КассоваяСмена");
	Иначе
		ДанныеЧекККМ = ЧекККМ;
	КонецЕсли;

	ДатаДляПроверкиСтатусаКассовойСмены = ?(ЗначениеЗаполнено(Объект.Ссылка), Объект.Дата, ТекущаяДатаСеанса());

	ТекстОшибки = "";
	Если Не ДанныеЧекККМ.Проведен Тогда
		ТекстОшибки = НСтр("ru='Чек ККМ не проведен. Ввод на основании невозможен.'");
	ИначеЕсли ДанныеЧекККМ.Статус <> Перечисления.СтатусыЧековККМ.Пробит Тогда
		ТекстОшибки = НСтр("ru='Чек ККМ не пробит. Ввод на основании невозможен.'");
	ИначеЕсли РозничныеПродажи.СменаОткрыта(ДанныеЧекККМ.КассоваяСмена, ДатаДляПроверкиСтатусаКассовойСмены) Тогда
		ТекстОшибки = НСтр("ru='Кассовая смена открыта! Ввод на основании чека документа ""Возврат товаров от клиента"" при открытой кассовой смене запрещен.'");
	ИначеЕсли Документы.ЧекККМКоррекции.ЕстьКоррекцияПоЧекуККМ(ДанныеЧекККМ.Ссылка) Тогда
		ТекстОшибки =  НСтр("ru = 'По Чеку ККМ введен Чек ККМ коррекции. Ввод на основании чека документа ""Возврат товаров от клиента"" запрещен.'");
	ИначеЕсли Документы.ЧекККМВозврат.ЕстьВозвратПоЧекуККМ(ДанныеЧекККМ.Ссылка) Тогда
		ТекстОшибки =  НСтр("ru = 'По Чеку ККМ введен Чек ККМ на возврат. Ввод на основании чека документа ""Возврат товаров от клиента"" запрещен.'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		Если Отказ = Неопределено Тогда
			ВызватьИсключение ТекстОшибки;
		Иначе
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,,,Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#Область ГосИС

#Область ДействияПриОбменеГИСМ

Функция ОбновитьСтатусПослеПодготовкиКПередачеДанных(ДокументСсылка, Операция) Экспорт
	
	НовыйСтатус        = Неопределено;
	ДальнейшееДействие = Неопределено;
	
	ИспользоватьАвтоматическийОбмен = ПолучитьФункциональнуюОпцию("ИспользоватьАвтоматическуюОтправкуПолучениеДанныхГИСМ");
	
	Если Операция = Перечисления.ОперацииОбменаГИСМ.ПередачаДанных Тогда
		НовыйСтатус = Перечисления.СтатусыИнформированияГИСМ.КПередаче;
		Если ИспользоватьАвтоматическийОбмен Тогда
			ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ОжидайтеПередачуДанныхРегламентнымЗаданием;
		Иначе
			ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ВыполнитеОбмен;
		КонецЕсли;
	КонецЕсли;
	
	Если НовыйСтатус = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	РегистрыСведений.СтатусыИнформированияГИСМ.ОбновитьСтатус(
		ДокументСсылка,
		НовыйСтатус,
		ДальнейшееДействие);
	
	Возврат НовыйСтатус;
	
КонецФункции

Функция ОбновитьСтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки) Экспорт
	
	НовыйСтатус     = Неопределено;
	ДальнейшееДействие = Неопределено;
	
	Если Операция = Перечисления.ОперацииОбменаГИСМ.ПередачаДанных Тогда
		
		Если СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийГИСМ.Принято Тогда
			
			НовыйСтатус = Перечисления.СтатусыИнформированияГИСМ.Передано;
			ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ОжидайтеПолучениеКвитанцииОФиксации;
			
		ИначеЕсли СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийГИСМ.Отклонено
			ИЛИ СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийГИСМ.Ошибка Тогда
			
			НовыйСтатус = Перечисления.СтатусыИнформированияГИСМ.ОтклоненоГИСМ;
			ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ПередайтеДанные;
			
		КонецЕсли;
		
	ИначеЕсли Операция = Перечисления.ОперацииОбменаГИСМ.ПередачаДанныхПолучениеКвитанции Тогда
		
		Если СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийГИСМ.Принято Тогда
			
			НовыйСтатус = Перечисления.СтатусыИнформированияГИСМ.ПринятоГИСМ;
			ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.НеТребуется;
			
		ИначеЕсли СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийГИСМ.Отклонено
			ИЛИ СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийГИСМ.Ошибка Тогда
			
			НовыйСтатус = Перечисления.СтатусыИнформированияГИСМ.ОтклоненоГИСМ;
			ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ПередайтеДанные;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НовыйСтатус = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	РегистрыСведений.СтатусыИнформированияГИСМ.ОбновитьСтатус(ДокументСсылка, НовыйСтатус, ДальнейшееДействие);
	
	Возврат НовыйСтатус;
	
КонецФункции

#КонецОбласти

#Область СообщенияГИСМ

Функция СообщениеКПередачеXML(ДокументСсылка, Операция) Экспорт
	
	Если Операция = Перечисления.ОперацииОбменаГИСМ.ПередачаДанных Тогда
		Возврат ВозвратТоваровОтКлиентаXML(ДокументСсылка);
	ИначеЕсли Операция = Перечисления.ОперацииОбменаГИСМ.ПередачаДанныхПолучениеКвитанции Тогда
		Возврат ИнтеграцияГИСМВызовСервера.ЗапросКвитанцииОФиксацииПоСсылкеXML(ДокументСсылка, Перечисления.ОперацииОбменаГИСМ.ПередачаДанных);
	КонецЕсли;
	
КонецФункции

Функция ВозвратТоваровОтКлиентаXML(ДокументСсылка) Экспорт
	
	СообщенияXML = Новый Массив;
	
	Версия = ИнтеграцияГИСМ.ВерсииСхемОбмена().Клиент;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ГИСМПрисоединенныеФайлы.ВладелецФайла КАК Ссылка,
	|	КОЛИЧЕСТВО(ГИСМПрисоединенныеФайлы.Ссылка) КАК ПоследнийНомерВерсии
	|ПОМЕСТИТЬ ВременнаяТаблица
	|ИЗ
	|	Справочник.ГИСМПрисоединенныеФайлы КАК ГИСМПрисоединенныеФайлы
	|ГДЕ
	|	ГИСМПрисоединенныеФайлы.ВладелецФайла = &Ссылка
	|	И ГИСМПрисоединенныеФайлы.Операция = ЗНАЧЕНИЕ(Перечисление.ОперацииОбменаГИСМ.ПередачаДанных)
	|	И ГИСМПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыСообщенийГИСМ.Исходящее)
	|СГРУППИРОВАТЬ ПО
	|	ГИСМПрисоединенныеФайлы.ВладелецФайла
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратТоваровОтКлиента.Дата                       КАК Дата,
	|	ВозвратТоваровОтКлиента.Номер                      КАК Номер,
	|	ЕСТЬNULL(ВременнаяТаблица.ПоследнийНомерВерсии, 0) КАК ПоследнийНомерВерсии,
	|	ВозвратТоваровОтКлиента.Организация                КАК Организация,
	|	ВозвратТоваровОтКлиента.Подразделение              КАК Подразделение
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента КАК ВозвратТоваровОтКлиента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблица КАК ВременнаяТаблица
	|		ПО ВозвратТоваровОтКлиента.Ссылка = ВременнаяТаблица.Ссылка
	|ГДЕ
	|	ВозвратТоваровОтКлиента.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.НомерСтроки        КАК НомерСтроки,
	|	Серии.Номенклатура       КАК Номенклатура,
	|	Серии.Характеристика     КАК Характеристика,
	|	Серии.Серия.НомерКиЗГИСМ КАК НомерКиЗ,
	|	Серии.Серия.RFIDTID      КАК TID,
	|	Серии.Серия.RFIDEPC      КАК EPC
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.Серии КАК Серии
	|ГДЕ
	|	Серии.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Серии.НомерСтроки");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Результат = Запрос.ВыполнитьПакет();
	Шапка = Результат[1].Выбрать();
	Товары = Результат[2].Выгрузить();
	Если Не Шапка.Следующий()
		Или Товары.Количество() = 0 Тогда
		
		СообщениеXML = ИнтеграцияГИСМКлиентСервер.СтруктураСообщенияXML();
		СообщениеXML.Документ = ДокументСсылка;
		СообщениеXML.Описание = ИнтеграцияГИСМ.ОписаниеОперацииПередачиДанных(
			Перечисления.ОперацииОбменаГИСМ.ПередачаДанныхВозвратОтРозничногоПокупателя, ДокументСсылка);
		СообщениеXML.ТекстОшибки = НСтр("ru = 'Нет данных для выгрузки.'");
		СообщенияXML.Добавить(СообщениеXML);
		Возврат СообщенияXML;
		
	КонецЕсли;
	
	НомерВерсии = Шапка.ПоследнийНомерВерсии + 1;
	
	РеквизитыОрганизации = ИнтеграцияГИСМВызовСервера.ИННКППGLNОрганизации(Шапка.Организация, Шапка.Подразделение);
	
	СообщениеXML = ИнтеграцияГИСМКлиентСервер.СтруктураСообщенияXML();
	СообщениеXML.Описание = ИнтеграцияГИСМ.ОписаниеОперацииПередачиДанных(
		Перечисления.ОперацииОбменаГИСМ.ПередачаДанныхВозвратОтРозничногоПокупателя, ДокументСсылка, НомерВерсии);
	
	ИмяТипа   = "query";
	ИмяПакета = "return_signs";
	
	ПередачаДанных = ИнтеграцияГИСМ.ОбъектXDTOПоИмениСвойства(Неопределено, ИмяТипа, Версия);
	
	ВозвратТоваров = ИнтеграцияГИСМ.ОбъектXDTO(ИмяПакета, Версия);
	ВозвратТоваров.action_id  = ВозвратТоваров.action_id;
	
	Попытка
		ВозвратТоваров.sender_gln = РеквизитыОрганизации.GLN;
	Исключение
		ИнтеграцияГИСМКлиентСервер.ДобавитьТекстОшибкиНеЗаполненGLNОрганизации(СообщениеXML, РеквизитыОрганизации.GLN, Шапка);
	КонецПопытки;
	
	ВозвратТоваров.return_doc_num  = Шапка.Номер;
	ВозвратТоваров.return_doc_date = Шапка.Дата;
	
	ХранилищеВременныхДат = Новый Соответствие;
	ИнтеграцияГИСМ.УстановитьДатуСЧасовымПоясом(
		ВозвратТоваров,
		"return_date",
		Шапка.Дата,
		ХранилищеВременныхДат);
	
	ВозвратТоваров.returns = ИнтеграцияГИСМ.ОбъектXDTOПоИмениСвойства(ВозвратТоваров, "returns", Версия);
	ВозвратТоваров.returns.signs = ИнтеграцияГИСМ.ОбъектXDTOПоИмениСвойства(ВозвратТоваров.returns, "signs", Версия);
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		
		НоваяСтрока = ИнтеграцияГИСМ.ОбъектXDTOПоИмениСвойства(ВозвратТоваров.returns.signs, "sign", Версия);
		
		Если ЗначениеЗаполнено(СтрокаТЧ.НомерКиЗ) Тогда
			
			НоваяСтрока.sign_num = СтрокаТЧ.НомерКиЗ;
			
		ИначеЕсли ЗначениеЗаполнено(СтрокаТЧ.TID) Тогда
			
			НоваяСтрока.sign_tid = СтрокаТЧ.TID;
			
		ИначеЕсли ЗначениеЗаполнено(СтрокаТЧ.EPC) Тогда
			
			Попытка
				НоваяСтрока.sign_sgtin = МенеджерОборудованияКлиентСервер.ПреобразоватьHEXВБинарнуюСтроку(СтрокаТЧ.EPC);
			Исключение
				ИнтеграцияГИСМКлиентСервер.ДобавитьТекстОшибки(
					СообщениеXML,
					СтрШаблон(НСтр("ru = 'Для номенклатуры %1 указан некорректный EPC ""%2"".'"),
						ОбщегоНазначенияИС.ПредставлениеНоменклатуры(СтрокаТЧ.Номенклатура, СтрокаТЧ.Характеристика),
						СтрокаТЧ.EPC));
			КонецПопытки;
			
		КонецЕсли;
		
		ВозвратТоваров.returns.signs.sign.Добавить(НоваяСтрока);
		
	КонецЦикла;
	
	ПередачаДанных.version    = ПередачаДанных.version;
	ПередачаДанных[ИмяПакета] = ВозвратТоваров;
	
	ТекстСообщенияXML = ИнтеграцияГИСМ.ОбъектXDTOВXML(ПередачаДанных, ИмяТипа, Версия);
	ТекстСообщенияXML = ИнтеграцияГИСМ.ПреобразоватьВременныеДаты(ХранилищеВременныхДат, ТекстСообщенияXML);
	
	СообщениеXML.ТекстСообщенияXML  = ТекстСообщенияXML;
	СообщениеXML.КонвертSOAP = ИнтеграцияГИСМВызовСервера.ПоместитьТекстСообщенияXMLВКонвертSOAP(ТекстСообщенияXML);
	
	СообщениеXML.ТипСообщения = Перечисления.ТипыСообщенийГИСМ.Исходящее;
	СообщениеXML.Организация  = Шапка.Организация;
	СообщениеXML.Операция     = Перечисления.ОперацииОбменаГИСМ.ПередачаДанных;
	СообщениеXML.Документ     = ДокументСсылка;
	СообщениеXML.Версия       = НомерВерсии;
	
	СообщенияXML.Добавить(СообщениеXML);
	
	Возврат СообщенияXML;
	
КонецФункции

#КонецОбласти

#КонецОбласти

//-- Локализация

#КонецОбласти

#Область Фискализация

//++ Локализация

// Обновляет таблицу товаров данными для передачи кодов КИЗ и ЕГАИС в ККТ
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ЧекККМ - Документ.
//	ДанныеПоТоварам - ТаблицаЗначений - Данные по номенклатуре для вывода в позиции чека
//
Процедура ДанныеКИЗиЕГАИС(ДокументСсылка, ДанныеПоТоварам)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Товары.ИдентификаторСтроки,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия,
	|	Товары.КоличествоУпаковок КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.Номенклатура КАК Номенклатура,
	|	Серии.Характеристика КАК Характеристика,
	|	Серии.Серия КАК Серия,
	|	Серии.Количество КАК Количество
	|ПОМЕСТИТЬ Серии
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.Серии КАК Серии
	|ГДЕ
	|	Серии.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШтрихкодыУпаковок.ШтрихкодУпаковки КАК АкцизнаяМарка
	|ПОМЕСТИТЬ АкцизныеМарки
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.ШтрихкодыУпаковок КАК ШтрихкодыУпаковок
	|ГДЕ
	|	ШтрихкодыУпаковок.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.Номенклатура КАК Номенклатура,
	|	Серии.Характеристика КАК Характеристика,
	|	СправочникСерии.НомерКиЗГИСМ КАК ШтрихкодСтрока,
	|	Серии.Количество КАК Количество
	|ИЗ
	|	Серии КАК Серии
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СправочникСерии
	|		ПО Серии.Серия = СправочникСерии.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Серии.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ОсобенностьУчета В (ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродукцияИзНатуральногоМеха))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	СправочникСерии.НомерКиЗГИСМ КАК ШтрихкодСтрока,
	|	Товары.Количество
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СправочникСерии
	|		ПО Товары.Серия = СправочникСерии.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	(НЕ Товары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
	|		И СправочникНоменклатура.ОсобенностьУчета В (ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродукцияИзНатуральногоМеха))";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	ШтрихкодыУпаковок = Запрос.Выполнить().Выгрузить();
	
	ТоварыРазобранные = ДанныеПоТоварам.СкопироватьКолонки();
	
	Для Каждого СтрокаТовары Из ДанныеПоТоварам Цикл
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Номенклатура", СтрокаТовары.Номенклатура);
		СтруктураОтбора.Вставить("Характеристика", СтрокаТовары.Характеристика);
		
		МассивМаркированныхТоваров = ШтрихкодыУпаковок.НайтиСтроки(СтруктураОтбора);
		
		Если МассивМаркированныхТоваров.Количество() = 0 Тогда
			СтрокаТоварыРазобранные = ТоварыРазобранные.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТоварыРазобранные, СтрокаТовары);
			
			Продолжить;
		КонецЕсли;
		
		КоличествоСтрокМассиваМаркированныхТоваров = МассивМаркированныхТоваров.Количество(); 
		ТекущаяСтрока = 1;
		
		Пока КоличествоСтрокМассиваМаркированныхТоваров >= ТекущаяСтрока Цикл
			Если СтрокаТовары.КоличествоУпаковок = 0 Тогда
				Прервать;
			КонецЕсли;
			
			МаркированныйТовар = МассивМаркированныхТоваров[КоличествоСтрокМассиваМаркированныхТоваров - ТекущаяСтрока];
			
			СтрокаТоварыРазобранные = ТоварыРазобранные.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТоварыРазобранные, СтрокаТовары);
			
			СтрокаТоварыРазобранные.Штрихкод = МаркированныйТовар.ШтрихкодСтрока;
			
			СтрокаТоварыРазобранные.КоличествоУпаковок = 1;
			СтрокаТоварыРазобранные.СуммаСкидки        = Окр(СтрокаТовары.СуммаСкидки / СтрокаТовары.КоличествоУпаковок, 2);
			СтрокаТоварыРазобранные.СуммаНДС           = Окр(СтрокаТовары.СуммаНДС / СтрокаТовары.КоличествоУпаковок, 2);
			СтрокаТоварыРазобранные.СуммаСНДС          = Окр(СтрокаТовары.СуммаСНДС / СтрокаТовары.КоличествоУпаковок, 2);
			СтрокаТоварыРазобранные.Цена               = СтрокаТоварыРазобранные.СуммаСНДС - СтрокаТоварыРазобранные.СуммаСкидки;
			
			СтрокаТовары.КоличествоУпаковок = СтрокаТовары.КоличествоУпаковок - СтрокаТоварыРазобранные.КоличествоУпаковок;
			СтрокаТовары.СуммаСкидки        = СтрокаТовары.СуммаСкидки - СтрокаТоварыРазобранные.СуммаСкидки;
			СтрокаТовары.СуммаНДС           = СтрокаТовары.СуммаНДС - СтрокаТоварыРазобранные.СуммаНДС;
			СтрокаТовары.СуммаСНДС          = СтрокаТовары.СуммаСНДС - СтрокаТоварыРазобранные.СуммаСНДС;
			
			ШтрихкодыУпаковок.Удалить(МаркированныйТовар);
			
			ТекущаяСтрока = ТекущаяСтрока + 1;
		КонецЦикла;
		
	КонецЦикла;
	
	ДанныеПоТоварам = ТоварыРазобранные;
	
КонецПроцедуры

Функция ДанныеПоВидамЗапасов(ДокументСсылка, ЦенаВключаетНДС)
	
	Запрос = Новый Запрос( 
	"ВЫБРАТЬ
	|	ВозвратТоваровОтКлиентаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	
	|	ВозвратТоваровОтКлиентаВидыЗапасов.СтавкаНДС                  КАК СтавкаНДС,
	|	
	|	СУММА(ВозвратТоваровОтКлиентаВидыЗапасов.Количество)          КАК КоличествоУпаковок,
	|	
	|	ЕСТЬNULL(НомераГТД.Код, НЕОПРЕДЕЛЕНО)                         КАК НомерТаможеннойДекларации,
	|	ЕСТЬNULL(СтраныМира.Код, НЕОПРЕДЕЛЕНО)                        КАК КодСтраныПроисхождения,
	|
	|	СпрВидыЗапасов.ТипЗапасов                                     КАК ТипЗапасов,
	|	ВЫБОР
	|		КОГДА СпрВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА СпрВидыЗапасов.Контрагент
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                         КАК Комитент
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.ВидыЗапасов КАК ВозвратТоваровОтКлиентаВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК НомераГТД
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМира
	|			ПО НомераГТД.СтранаПроисхождения = СтраныМира.Ссылка
	|		ПО ВозвратТоваровОтКлиентаВидыЗапасов.НомерГТД = НомераГТД.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК СпрВидыЗапасов
	|		ПО СпрВидыЗапасов.Ссылка = ВозвратТоваровОтКлиентаВидыЗапасов.ВидЗапасов
	|
	|ГДЕ
	|	ВозвратТоваровОтКлиентаВидыЗапасов.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВозвратТоваровОтКлиентаВидыЗапасов.Ссылка,
	|	ВозвратТоваровОтКлиентаВидыЗапасов.АналитикаУчетаНоменклатуры,
	|	
	|	ВозвратТоваровОтКлиентаВидыЗапасов.СтавкаНДС,
	|	
	|	ЕСТЬNULL(НомераГТД.Код, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(СтраныМира.Код, НЕОПРЕДЕЛЕНО),
	|
	|	СпрВидыЗапасов.ТипЗапасов,
	|	ВЫБОР
	|		КОГДА СпрВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА СпрВидыЗапасов.Контрагент
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|");
	
	Запрос.УстановитьПараметр("Ссылка"			, ДокументСсылка);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС"	, ЦенаВключаетНДС);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ТаблицаТоваровСРаспределениемПоВидамЗапасов(Товары, ДанныеПоВидамЗапасов)
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьИмпортныеТовары") Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаТоваровСРаспределениемПоВидамЗапасов = Товары.СкопироватьКолонки(); // ТаблицаЗначений
	ТаблицаТоваровСРаспределениемПоВидамЗапасов.Колонки.Добавить("НомерТаможеннойДекларации");
	ТаблицаТоваровСРаспределениемПоВидамЗапасов.Колонки.Добавить("КодСтраныПроисхождения");
	ТаблицаТоваровСРаспределениемПоВидамЗапасов.Колонки.Добавить("ТипЗапасов");
	ТаблицаТоваровСРаспределениемПоВидамЗапасов.Колонки.Добавить("Комитент");
	
	Товары.Сортировать("НомерСтроки Убыв");
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("АналитикаУчетаНоменклатуры", СтрокаТЧ.АналитикаУчетаНоменклатуры);
		СтруктураОтбора.Вставить("СтавкаНДС"				 , СтрокаТЧ.СтавкаНДС);
		
		МассивСтрокДанныхПоВидамЗапасов = ДанныеПоВидамЗапасов.НайтиСтроки(СтруктураОтбора);
		
		Если МассивСтрокДанныхПоВидамЗапасов.Количество() = 0 Тогда
			СтрокаТоваровСРаспределениемПоВидамЗапасов = ТаблицаТоваровСРаспределениемПоВидамЗапасов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТоваровСРаспределениемПоВидамЗапасов, СтрокаТЧ);
			
			Продолжить;
		КонецЕсли;
		
		
		КоличествоСтрокМассиваСтрокДанныхПоВидамЗапасов = МассивСтрокДанныхПоВидамЗапасов.Количество(); 
		ТекущаяСтрока = 1;
		
		Пока КоличествоСтрокМассиваСтрокДанныхПоВидамЗапасов >= ТекущаяСтрока Цикл
			Если СтрокаТЧ.КоличествоУпаковок = 0 Тогда
				Прервать;
			КонецЕсли;
			
			СтрокаДанныхПоВидамЗапасов = МассивСтрокДанныхПоВидамЗапасов[КоличествоСтрокМассиваСтрокДанныхПоВидамЗапасов - ТекущаяСтрока];
			
			СтрокаТоваровСРаспределениемПоВидамЗапасов = ТаблицаТоваровСРаспределениемПоВидамЗапасов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТоваровСРаспределениемПоВидамЗапасов, СтрокаТЧ);
			
			СтрокаТоваровСРаспределениемПоВидамЗапасов.НомерТаможеннойДекларации = СтрокаДанныхПоВидамЗапасов.НомерТаможеннойДекларации;
			СтрокаТоваровСРаспределениемПоВидамЗапасов.КодСтраныПроисхождения = СтрокаДанныхПоВидамЗапасов.КодСтраныПроисхождения;
			
			СтрокаТоваровСРаспределениемПоВидамЗапасов.ТипЗапасов = СтрокаДанныхПоВидамЗапасов.ТипЗапасов;
			СтрокаТоваровСРаспределениемПоВидамЗапасов.Комитент = СтрокаДанныхПоВидамЗапасов.Комитент;
			
			Если СтрокаТЧ.КоличествоУпаковок > СтрокаДанныхПоВидамЗапасов.КоличествоУпаковок Тогда 
				СтрокаТоваровСРаспределениемПоВидамЗапасов.КоличествоУпаковок = СтрокаДанныхПоВидамЗапасов.КоличествоУпаковок;
				СтрокаТоваровСРаспределениемПоВидамЗапасов.СуммаСкидки = Окр(СтрокаТЧ.СуммаСкидки / СтрокаТЧ.КоличествоУпаковок * СтрокаТоваровСРаспределениемПоВидамЗапасов.КоличествоУпаковок, 2);
				СтрокаТоваровСРаспределениемПоВидамЗапасов.СуммаСНДС = Окр(СтрокаТЧ.СуммаСНДС / СтрокаТЧ.КоличествоУпаковок * СтрокаТоваровСРаспределениемПоВидамЗапасов.КоличествоУпаковок, 2);
				СтрокаТоваровСРаспределениемПоВидамЗапасов.СуммаНДС = Окр(СтрокаТЧ.СуммаНДС / СтрокаТЧ.КоличествоУпаковок * СтрокаТоваровСРаспределениемПоВидамЗапасов.КоличествоУпаковок, 2);
				
				СтрокаТЧ.КоличествоУпаковок = СтрокаТЧ.КоличествоУпаковок - СтрокаТоваровСРаспределениемПоВидамЗапасов.КоличествоУпаковок;
				СтрокаТЧ.СуммаСкидки 		= СтрокаТЧ.СуммаСкидки - СтрокаТоваровСРаспределениемПоВидамЗапасов.СуммаСкидки;
				СтрокаТЧ.СуммаСНДС 			= СтрокаТЧ.СуммаСНДС - СтрокаТоваровСРаспределениемПоВидамЗапасов.СуммаСНДС;
				СтрокаТЧ.СуммаНДС  			= СтрокаТЧ.СуммаНДС - СтрокаТоваровСРаспределениемПоВидамЗапасов.СуммаНДС;
				
				ДанныеПоВидамЗапасов.Удалить(СтрокаДанныхПоВидамЗапасов);
			ИначеЕсли СтрокаТЧ.КоличествоУпаковок < СтрокаДанныхПоВидамЗапасов.КоличествоУпаковок Тогда
				СтрокаДанныхПоВидамЗапасов.КоличествоУпаковок = СтрокаДанныхПоВидамЗапасов.КоличествоУпаковок - СтрокаТЧ.КоличествоУпаковок;
				СтрокаТЧ.КоличествоУпаковок = 0;
			Иначе
				ДанныеПоВидамЗапасов.Удалить(СтрокаДанныхПоВидамЗапасов);
				СтрокаТЧ.КоличествоУпаковок = 0;
			КонецЕсли;
			
			ТекущаяСтрока = ТекущаяСтрока + 1;
		КонецЦикла;
		
	КонецЦикла;
	
	ТаблицаТоваровСРаспределениемПоВидамЗапасов.Сортировать("НомерСтроки Возр");
	
	Товары = ТаблицаТоваровСРаспределениемПоВидамЗапасов;
	
КонецПроцедуры

//-- Локализация

#КонецОбласти

//++ Локализация
#Область МаркируемаяПродукция

// Инициализировать таблицу штрихкоды упаковок.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Инициализировать таблицу штрихкоды упаковок:
// * Номенклатура - СправочникСсылка.Номенклатура -
// * Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры -
// * Серия - СправочникСсылка.СерииНоменклатуры -
// * Склад - СправочникСсылка.Склады -
// * Количество - Число -
// * ШтрихкодУпаковки - СправочникСсылка.ШтрихкодыУпаковокТоваров -
Функция ИнициализироватьТаблицуШтрихкодыУпаковок()
	
	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Таблица.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Таблица.Колонки.Добавить("Серия", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	Таблица.Колонки.Добавить("Склад", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	Таблица.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
	Таблица.Колонки.Добавить("ШтрихКодУпаковки", Новый ОписаниеТипов("СправочникСсылка.ШтрихкодыУпаковокТоваров"));
	
	Возврат Таблица;
	
КонецФункции

#КонецОбласти
//-- Локализация

#КонецОбласти
