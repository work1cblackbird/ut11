#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация


	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	//++ Локализация
	
	
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.КорректировкаРеализации - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	//++ Локализация
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ШтрихкодыУпаковок = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "ШтрихкодыУпаковок");
		Если ШтрихкодыУпаковок <> Неопределено Тогда
			Объект.ШтрихкодыУпаковок.Загрузить(ШтрихкодыУпаковок.Выгрузить());
		КонецЕсли;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("АктОРасхожденияхПослеОтгрузкиОснование") Тогда
		//Объект.ШтрихкодыУпаковок.Загрузить(ДанныеЗаполнения.АктОРасхожденияхПослеОтгрузкиОснование.ШтрихкодыУпаковок.Выгрузить());
		ЗаполнитьШтрихкодыУпаковокПоАктуОРасхожденияхПослеОтгрузки(Объект, ДанныеЗаполнения);
		
	КонецЕсли;
	//-- Локализация
	Возврат;
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	//++ Локализация


	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	//++ Локализация
	Документы.ТранспортнаяНакладная.ОбновитьРеквизитыТранспортныхНакладныхПриЗаписиДокументаОснования(Объект, Отказ);

	//-- Локализация
	
	Возврат;
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	//++ Локализация
	Объект.ШтрихкодыУпаковок.Очистить();
	//-- Локализация
	Возврат;

КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	//++ Локализация
	Документы.СчетФактураВыданный.ДобавитьКомандуСоздатьНаОснованииИсправленныйСчетФактура(КомандыСозданияНаОсновании);
	//-- Локализация
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	//++ Локализация
	
	УсловиеВидимостиПечатныхФормПоТоварам = Новый Структура;
	УсловиеВидимостиПечатныхФормПоТоварам.Вставить("Реквизит", Метаданные.Документы.КорректировкаРеализации.Реквизиты.ВидКорректировки.Имя);
	УсловиеВидимостиПечатныхФормПоТоварам.Вставить("ВидСравнения", ВидСравнения.ВСписке);
	МассивОпераций = Новый СписокЗначений();
	МассивОпераций.Добавить(Перечисления.ХозяйственныеОперации.ИсправлениеОшибок);
	МассивОпераций.Добавить(Перечисления.ХозяйственныеОперации.КорректировкаПоСогласованиюСторон);
	МассивОпераций.Добавить(Перечисления.ХозяйственныеОперации.ВозвратТоваровОтКлиента);
	МассивОпераций.Добавить(Перечисления.ХозяйственныеОперации.РеализацияКлиенту);
	УсловиеВидимостиПечатныхФормПоТоварам.Вставить("Значение", МассивОпераций);
	
	Если НЕ ПраваПользователяПовтИсп.ЭтоПартнер() Тогда
	
		Если ПолучитьФункциональнуюОпцию("ИспользоватьАктыНаПередачуПрав") Тогда
			
			// Акт на передачу прав
			КомандаПечати = КомандыПечати.Добавить();
			КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
			КомандаПечати.Идентификатор = "АктНаПередачуПрав";
			КомандаПечати.Представление = НСтр("ru = 'Акт на передачу прав'");
			КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
			КомандаПечати.Порядок = 99;
			КомандаПечати.УсловияВидимости.Добавить(УсловиеВидимостиПечатныхФормПоТоварам);
			
			
		КонецЕсли;
	
		// Товарная накладная (ТОРГ-12)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "ТОРГ12";
		КомандаПечати.Представление = НСтр("ru = 'Товарная накладная (ТОРГ-12)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьУслуги", Истина);
		КомандаПечати.Порядок = 2;
		КомандаПечати.УсловияВидимости.Добавить(УсловиеВидимостиПечатныхФормПоТоварам);
		
		// Товарная накладная без услуг (ТОРГ-12)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "ТОРГ12";
		КомандаПечати.Представление = НСтр("ru = 'Товарная накладная без услуг (ТОРГ-12)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьУслуги", Ложь);
		КомандаПечати.Порядок = 3;
		КомандаПечати.УсловияВидимости.Добавить(УсловиеВидимостиПечатныхФормПоТоварам);
		
		// Товарная накладная с номерами ГТД (ТОРГ-12)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "ТОРГ12";
		КомандаПечати.Представление = НСтр("ru = 'Товарная накладная с номерами ГТД (ТОРГ-12)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьУслуги", Ложь);
		КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьГТД",    Истина);
		КомандаПечати.Порядок = 4;
		КомандаПечати.УсловияВидимости.Добавить(УсловиеВидимостиПечатныхФормПоТоварам);
		
	КонецЕсли;
	
	// Счет-фактура
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "СчетФактура";
	КомандаПечати.Представление = НСтр("ru = 'Счет-фактура'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВВалюте", Ложь);
	КомандаПечати.Порядок = 5;
	КомандаПечати.УсловияВидимости.Добавить(УсловиеВидимостиПечатныхФормПоТоварам);

	Если НЕ ПраваПользователяПовтИсп.ЭтоПартнер() Тогда
		// Счет-фактура (в валюте)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "СчетФактураВВалюте";
		КомандаПечати.Представление = НСтр("ru = 'Счет-фактура (в валюте)'");
		КомандаПечати.ФункциональныеОпции = "ИспользоватьНесколькоВалют";
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВВалюте", Истина);
		КомандаПечати.Порядок = 6;
		КомандаПечати.УсловияВидимости.Добавить(УсловиеВидимостиПечатныхФормПоТоварам);
	КонецЕсли;
	
	Если НЕ ПраваПользователяПовтИсп.ЭтоПартнер() Тогда
		
		// Универсальный передаточный документ (УПД)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "УПД";
		КомандаПечати.Представление = НСтр("ru = 'Универсальный передаточный документ (УПД)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 11;
		КомандаПечати.УсловияВидимости.Добавить(УсловиеВидимостиПечатныхФормПоТоварам);
		
		// Универсальный корректировочный документ (УКД)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "УКД";
		КомандаПечати.Представление = НСтр("ru = 'Универсальный корректировочный документ (УКД)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 12;
		КомандаПечати.УсловияВидимости.Добавить(УсловиеВидимостиПечатныхФормПоТоварам);
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОтветственноеХранение") Тогда
		// МХ-1 
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "МХ1";
		КомандаПечати.Представление = НСтр("ru='Акт о приеме-передаче ТМЦ на хранение (МХ-1)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.УсловияВидимости.Добавить(УсловиеВидимостиПечатныхФормПоТоварам);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОтветственноеХранение") Тогда
		// МХ-3 
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "МХ3";
		КомандаПечати.Представление = НСтр("ru = 'Акт о возврате ТМЦ, сданных на хранение (МХ-3)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.УсловияВидимости.Добавить(УсловиеВидимостиПечатныхФормПоТоварам);
	КонецЕсли;
	
	Документы.ТранспортнаяНакладная.ДобавитьКомандыПечати(КомандыПечати, , УсловиеВидимостиПечатныхФормПоТоварам);
	//-- Локализация
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	//++ Локализация
	//-- Локализация
КонецПроцедуры

//++ Локализация

// Функция получает данные для формирования печатной формы ТОРГ - 12
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив - Массив ссылок на документы, по которым необходимо получить данные.
//
// Возвращаемое значение:
// 	Структура:
// 		* РезультатПоШапке - РезультатЗапроса
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса
//
Функция ПолучитьДанныеДляПечатнойФормыТОРГ12(ПараметрыПечати, МассивОбъектов) Экспорт

	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.Валюта КАК Валюта,
	|	ДанныеДокументов.Организация КАК Организация,
	|	ДанныеДокументов.ТипКорректировки КАК ТипКорректировки,
	|	ДанныеДокументов.ВидКорректировки КАК ХозяйственнаяОперация
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК ДокументОснование,
	|	ЛОЖЬ КАК Корректировочный
	|
	|ПОМЕСТИТЬ СчетаФактурыОснования
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Выполнить();
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	Если ПараметрыПечати.Свойство("ВыводитьГТД")Тогда
		ПараметрыЗаполнения.ВключаяНомераГТД = ПараметрыПечати.Свойство("ВыводитьГТД");
	КонецЕсли;
	ПараметрыЗаполнения.ОбработатьНастройкиПечатиНаборов = Истина;
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	ПродажиСервер.ПоместитьВременнуюТаблицуКоэффициентыУпаковок(МенеджерВременныхТаблиц, "КорректировкаРеализацииТаблицаТоваров");
	
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);	
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка КАК Ссылка,
	|	КорректировкаРеализации.ДокументОснование.Номер КАК Номер,
	|	КорректировкаРеализации.ДокументОснование.Дата КАК Дата,
	|	НЕОПРЕДЕЛЕНО КАК Статус,
	|	КорректировкаРеализации.Партнер КАК Партнер,
	|	КорректировкаРеализации.Контрагент КАК Контрагент,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Организация.ОбособленноеПодразделение
	|			ТОГДА КорректировкаРеализации.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ КорректировкаРеализации.Организация
	|	КОНЕЦ КАК Организация,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	КорректировкаРеализации.Отпустил КАК Кладовщик,
	|	КорректировкаРеализации.ОтпустилДолжность КАК ДолжностьКладовщика,
	|	КорректировкаРеализации.Организация.Префикс КАК Префикс,
	|	КорректировкаРеализации.Основание КАК Основание,
	|	КорректировкаРеализации.ОснованиеДата КАК ОснованиеДата,
	|	КорректировкаРеализации.ОснованиеНомер КАК ОснованиеНомер,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА КорректировкаРеализации.Контрагент
	|		ИНАЧЕ КорректировкаРеализации.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА КорректировкаРеализации.Организация
	|		ИНАЧЕ КорректировкаРеализации.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	КорректировкаРеализации.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации,
	|	КорректировкаРеализации.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	КорректировкаРеализации.БанковскийСчетГрузоотправителя КАК БанковскийСчетГрузоотправителя,
	|	КорректировкаРеализации.БанковскийСчетГрузополучателя КАК БанковскийСчетГрузополучателя,
	|	КорректировкаРеализации.АдресДоставки КАК АдресДоставки,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	КорректировкаРеализации.НалогообложениеНДС КАК НалогообложениеНДС,
	|	КорректировкаРеализации.Валюта КАК Валюта,
	|	КорректировкаРеализации.ДоверенностьНомер КАК ДоверенностьНомер,
	|	КорректировкаРеализации.ДоверенностьДата КАК ДоверенностьДата,
	|	КорректировкаРеализации.ДоверенностьВыдана КАК ДоверенностьВыдана,
	|	КорректировкаРеализации.ДоверенностьЛицо КАК ДоверенностьЛицо,
	|	&ЕдиницаИзмеренияВеса КАК ЕдиницаИзмеренияВеса
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО КорректировкаРеализации.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО КорректировкаРеализации.Ссылка = ДанныеДокументов.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	ТаблицаТоваров.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ТаблицаТоваров.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	ТаблицаТоваров.НоменклатураНабора КАК НоменклатураНабора,
	|	ТаблицаТоваров.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ТаблицаТоваров.ЭтоКомплектующие КАК ЭтоКомплектующие,
	|	ТаблицаТоваров.ЭтоНабор КАК ЭтоНабор,
	|	ТаблицаТоваров.ПолныйНабор КАК ПолныйНабор,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(ТаблицаТоваров.Номенклатура.НаименованиеПолное, ТаблицаТоваров.Содержание) КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаТоваров.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаТоваров.Номенклатура.Код
	|	КОНЕЦ КАК Артикул,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаТоваров.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаТоваров.Номенклатура.Код
	|	КОНЕЦ КАК НоменклатураКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения1
	|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ &ТекстЗапросаКодЕдиницыИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТаблицаТоваров.Серия.Наименование КАК СерияНаименование,
	|	ТаблицаТоваров.Упаковка КАК Упаковка,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) = 1
	|			ТОГДА """"
	|		ИНАЧЕ ТаблицаТоваров.Упаковка.Наименование
	|	КОНЕЦ КАК УпаковкаНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			&ТекстЗапросаНаименованиеЕдиницыИзмерения1
	|		ИНАЧЕ
	|			&ТекстЗапросаНаименованиеЕдиницыИзмерения2
	|	КОНЕЦ КАК ВидУпаковки,
	|	ТаблицаТоваров.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаТоваров.НомерГТД КАК НомерГТД,
	|	ТаблицаТоваров.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ ТаблицаТоваров.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ КоэффициентыУпаковок.Количество / КоэффициентыУпаковок.КоэффициентВложеннойУпаковки
	|	КОНЕЦ КАК КоличествоМест,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ВЫБОР
	|					КОГДА КоэффициентыУпаковок.Количество < КоэффициентыУпаковок.КоэффициентВложеннойУпаковки
	|						ТОГДА КоэффициентыУпаковок.Количество
	|					ИНАЧЕ КоэффициентыУпаковок.КоэффициентВложеннойУпаковки
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|					ТОГДА 1
	|				ИНАЧЕ &ТекстЗапросаКоэффициентУпаковки
	|			КОНЕЦ
	|	КОНЕЦ КАК КоличествоВОдномМесте,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.Количество
	|	КОНЕЦ КАК Цена,
	|	ТаблицаТоваров.СуммаБезНДС КАК СуммаБезНДС,
	|	ТаблицаТоваров.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТоваров.СуммаБезНДС + ТаблицаТоваров.СуммаНДС КАК СуммаСНДС,
	|	ТаблицаТоваров.Количество * &ТекстЗапросаВес2 КАК МассаНетто,
	|	ВЫБОР
	|		КОГДА &ЗаполненаЕдиницаИзмеренияВеса
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаТоваров.Упаковка.Вес ЕСТЬ NULL ТОГДА
	|						ТаблицаТоваров.Количество * &ТекстЗапросаВес1
	|					ИНАЧЕ
	|						ТаблицаТоваров.КоличествоУпаковок * &ТекстЗапросаВес1
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК МассаБрутто,
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА
	|			ТаблицаТоваров.Ссылка.ДокументОснование.ВернутьМногооборотнуюТару
	|			И ТаблицаТоваров.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	КорректировкаРеализацииТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыУпаковок КАК КоэффициентыУпаковок
	|		ПО ТаблицаТоваров.Ссылка = КоэффициентыУпаковок.Ссылка
	|			И ТаблицаТоваров.КоличествоУпаковок = КоэффициентыУпаковок.КоличествоУпаковок
	|			И ТаблицаТоваров.НомерСтроки = КоэффициентыУпаковок.НомерСтроки
	|			И НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|ГДЕ
	|	(ТаблицаТоваров.Ссылка.ДокументОснование ССЫЛКА Документ.РеализацияТоваровУслуг
	|	ИЛИ ТаблицаТоваров.Ссылка.ДокументОснование ССЫЛКА Документ.РеализацияУслугПрочихАктивов
	|	ИЛИ ТаблицаТоваров.Ссылка.ДокументОснование ССЫЛКА Документ.ПервичныйДокумент)
	|	И (ТаблицаТоваров.ЭтоТовар
	|			ИЛИ &ВыводитьУслуги)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтрокиНаборы,
	|	ЭтоНабор УБЫВ,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ТаблицаТоваров.Упаковка",
		"ТаблицаТоваров.Номенклатура"));
		
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаВес1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаВес2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"КоэффициентыУпаковок.ВидУпаковки",
			"ТаблицаТоваров.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.УстановитьПараметр("ВыводитьУслуги",                  ПараметрыПечати.ВыводитьУслуги);
	Запрос.УстановитьПараметр("КолонкаКодов",                    КолонкаКодов);
	Запрос.УстановитьПараметр("ЕдиницаИзмеренияВеса",            Константы.ЕдиницаИзмеренияВеса.Получить());
	Запрос.УстановитьПараметр("ЗаполненаЕдиницаИзмеренияВеса",   ЗначениеЗаполнено(Константы.ЕдиницаИзмеренияВеса.Получить()));
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения", Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
	
	МассивРезультатов         = Запрос.ВыполнитьПакет();
	РезультатПоШапке          = МассивРезультатов[0];
	РезультатПоТабличнойЧасти = МассивРезультатов[1];
	
	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("РезультатПоШапке", РезультатПоШапке);
	СтруктураДанныхДляПечати.Вставить("РезультатПоТабличнойЧасти", РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Формирует текст запроса для получения данных основания при печати Счет-фактуры.
//
// Возвращаемое значение:
// 	Строка
Функция ТекстЗапросаДанныхОснованияДляПечатнойФормыСчетФактура() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка                                   КАК Ссылка,
	|	ДанныеДокументов.ВидКорректировки                         КАК ХозяйственнаяОперация,
	|	ДанныеДокументов.Валюта                                   КАК Валюта,
	|	ДанныеДокументов.Организация                              КАК Организация,
	|	ДанныеДокументов.НалогообложениеНДС                       КАК НалогообложениеНДС,
	|	ДанныеДокументов.Подразделение                            КАК Подразделение,
	|	ДанныеДокументов.Склад                                    КАК Склад,
	|	ДанныеДокументов.Грузоотправитель                         КАК Грузоотправитель,
	|	ДанныеДокументов.Грузополучатель                          КАК Грузополучатель,
	|	ЛОЖЬ                                                      КАК РасчетыЧерезОтдельногоКонтрагента,
	|	НЕОПРЕДЕЛЕНО                                              КАК Номенклатура,
	|	""""                                                      КАК Содержание,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)             КАК Комиссионер,
	|	ДанныеДокументов.Основание                                КАК Основание,
	|	ДанныеДокументов.ОснованиеДата                            КАК ОснованиеДата,
	|	ДанныеДокументов.ОснованиеНомер                           КАК ОснованиеНомер,
	|	ДанныеДокументов.БанковскийСчетОрганизации                КАК БанковскийСчетОрганизации,
	|	ДанныеДокументов.БанковскийСчетКонтрагента                КАК БанковскийСчетКонтрагента,
	|	ДанныеДокументов.БанковскийСчетГрузоотправителя           КАК БанковскийСчетГрузоотправителя,
	|	ДанныеДокументов.БанковскийСчетГрузополучателя            КАК БанковскийСчетГрузополучателя,
	|	ДанныеДокументов.ДоверенностьНомер                        КАК ДоверенностьНомер,
	|	ДанныеДокументов.ДоверенностьДата                         КАК ДоверенностьДата,
	|	ДанныеДокументов.ДоверенностьВыдана                       КАК ДоверенностьВыдана,
	|	ДанныеДокументов.ДоверенностьЛицо                         КАК ДоверенностьЛицо,
	|	ДанныеДокументов.Менеджер                                 КАК Менеджер,
	|	ДанныеДокументов.Отпустил.Наименование                    КАК Кладовщик,
	|	ДанныеДокументов.ОтпустилДолжность                        КАК ДолжностьКладовщика,
	|	ДанныеДокументов.Дата                                     КАК Период
	|
	|//ОператорПОМЕСТИТЬ
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокументов
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&ДокументОснование_КорректировкаРеализации)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает данные для формирования печатной формы УПД
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.КорректировкаРеализации - ссылки на документы, по которым необходимо
//																		получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемая при печати УПД, содержащая следующие свойства:
//		* РезультатПоШапке - РезультатЗапроса - общие данные накладной.
//		* РезультатПоТабличнойЧасти - РезультатЗапроса - данные табличной части накладной.
//		* Маркировка - см. ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое
//		* ПрослеживаемыеКомплектующие - см. УчетПрослеживаемыхТоваровЛокализация.ПрослеживаемыеКомплектующиеДляПечатиДанных
//
Функция ПолучитьДанныеДляПечатнойФормыУПД(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;

	// ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	Контракт = Неопределено;
	ЕстьЭлектронноеАктирование = Ложь;
	Для Каждого Основание Из МассивОбъектов Цикл
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Основание, "Договор") Тогда
			ДоговорОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "Договор");
			Если ТипЗнч(ДоговорОснования) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
				Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорОснования, "Организация");
				ПараметрыОтправкиВЕИС = ЭлектронноеАктированиеЕИСУТ.ПараметрыОтправкиВЕИС(
						Организация, ДоговорОснования);
				ЕстьЭлектронноеАктирование = ПараметрыОтправкиВЕИС.ВозможнаОтправка
					И ЭлектронноеАктированиеЕИСУТ.ДокументОтправляетсяВЕИС(Основание);
				Контракт = ПараметрыОтправкиВЕИС.Контракт;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	// Конец ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка                КАК Ссылка,
	|	ДанныеДокументов.Валюта                КАК Валюта,
	|	ДанныеДокументов.Организация           КАК Организация,
	|	ДанныеДокументов.Подразделение         КАК Подразделение,
	|	ДанныеДокументов.Склад                 КАК Склад,
	|	ДанныеДокументов.ВидКорректировки      КАК ХозяйственнаяОперация,
	|	ДанныеДокументов.ТипКорректировки      КАК ТипКорректировки,
	|	ДанныеДокументов.Дата                  КАК Период
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	УчетНДСРФ.ДополнитьЗапросПолученияДанныхДляПечатиУПД(Запрос, "ХозяйственнаяОперация,ТипКорректировки");
	
	Запрос.Текст = Запрос.Текст + "
	|
	|////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК ДокументОснование,
	|	ЛОЖЬ КАК Корректировочный
	|
	|ПОМЕСТИТЬ СчетаФактурыОснования
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	
	Запрос.Выполнить();
	
	Обработки.ПечатьОбщихФорм.ПоместитьВременнуюТаблицуДанныхПоставщика(МенеджерВременныхТаблиц);
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	ПараметрыЗаполнения.ВключаяНомераГТД = Истина;
	ПараметрыЗаполнения.ОбработатьНастройкиПечатиНаборов = Истина;
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	// Вычисление даты, номера документа ПДУ и порядкового номера исправления
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокумента.МоментВремени КАК МоментВремени,
	|	ДанныеДокумента.ДокументОснование КАК ОснованиеСсылка,
	|	ДанныеДокумента.ДокументОснование.МоментВремени КАК ОснованиеМоментВремени,
	|	ДанныеДокумента.ДокументОснование.Дата КАК ОснованиеДата,
	|	ДанныеДокумента.ДокументОснование.Номер КАК ОснованиеНомер,
	|	МАКСИМУМ(ЕСТЬNULL(ДанныеДокументаКорректировки.Дата, ДанныеДокумента.ДокументОснование.Дата)) КАК КорректировкаДата
	|ПОМЕСТИТЬ ДанныеДляРасчетаСКорректировкамиПромежуточная
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК ДанныеДокумента
	|		ПО ДанныеДокументов.Ссылка = ДанныеДокумента.Ссылка
	|			И ДанныеДокумента.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИсправлениеОшибок)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК ДанныеДокументаКорректировки
	|		ПО (ДанныеДокумента.ДокументОснование = ДанныеДокументаКорректировки.ДокументОснование)
	|			И ДанныеДокумента.МоментВремени >= ДанныеДокументаКорректировки.МоментВремени
	|			И (ДанныеДокументаКорректировки.Проведен)
	|			И (ДанныеДокументаКорректировки.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПоСогласованиюСторон))
	|	
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокументов.Ссылка,
	|	ДанныеДокумента.МоментВремени,
	|	ДанныеДокумента.ДокументОснование,
	|	ДанныеДокумента.ДокументОснование.Номер,
	|	ДанныеДокумента.ДокументОснование.МоментВремени
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДляРасчетаСКорректировками.Ссылка КАК Ссылка,
	|	ДанныеДляРасчетаСКорректировками.МоментВремени КАК МоментВремени,
	|	ДанныеДляРасчетаСКорректировками.ОснованиеСсылка КАК ОснованиеСсылка,
	|	ДанныеДляРасчетаСКорректировками.ОснованиеНомер КАК ОснованиеНомер,
	|	ДанныеДляРасчетаСКорректировками.ОснованиеМоментВремени КАК ОснованиеМоментВремени,
	|	ДанныеДляРасчетаСКорректировками.ОснованиеДата КАК ОснованиеДата,
	|	ДанныеДляРасчетаСКорректировками.КорректировкаДата КАК КорректировкаДата,
	|	МАКСИМУМ(ДанныеДокументаКорректировки.Ссылка) КАК КорректировкаСсылка
	|ПОМЕСТИТЬ ДанныеДляРасчетаСКорректировками
	|ИЗ
	|	ДанныеДляРасчетаСКорректировкамиПромежуточная КАК ДанныеДляРасчетаСКорректировками
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК ДанныеДокументаКорректировки
	|		ПО ДанныеДляРасчетаСКорректировками.ОснованиеСсылка = ДанныеДокументаКорректировки.ДокументОснование
	|			И ДанныеДляРасчетаСКорректировками.КорректировкаДата = ДанныеДокументаКорректировки.Дата
	|			И (ДанныеДокументаКорректировки.Проведен)
	|			И (ДанныеДокументаКорректировки.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПоСогласованиюСторон))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДляРасчетаСКорректировками.Ссылка,
	|	ДанныеДляРасчетаСКорректировками.МоментВремени,
	|	ДанныеДляРасчетаСКорректировками.ОснованиеСсылка,
	|	ДанныеДляРасчетаСКорректировками.ОснованиеНомер,
	|	ДанныеДляРасчетаСКорректировками.ОснованиеМоментВремени,
	|	ДанныеДляРасчетаСКорректировками.ОснованиеДата,
	|	ДанныеДляРасчетаСКорректировками.КорректировкаДата
	|;
	|
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДляРасчетаСКорректировками.Ссылка КАК Ссылка,
	|	ДанныеДляРасчетаСКорректировками.КорректировкаДата КАК Дата,
	|	ДанныеДокументаКорректировки.Номер КАК Номер,
	|	ДанныеДокументаИсправления.Ссылка КАК ДанныеДокументаИсправленияСсылка
	|ПОМЕСТИТЬ ДанныеСКорректировкамиИИсправлениямиПромежуточный
	|ИЗ
	|	ДанныеДляРасчетаСКорректировками КАК ДанныеДляРасчетаСКорректировками
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК ДанныеДокументаКорректировки
	|		ПО ДанныеДляРасчетаСКорректировками.КорректировкаСсылка = ДанныеДокументаКорректировки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК ДанныеДокументаИсправления
	|		ПО ДанныеДляРасчетаСКорректировками.ОснованиеСсылка = ДанныеДокументаИсправления.ДокументОснование
	|			И (ДанныеДокументаКорректировки.МоментВремени <= ДанныеДокументаИсправления.МоментВремени)
	|			И ДанныеДляРасчетаСКорректировками.МоментВремени >= ДанныеДокументаИсправления.МоментВремени
	|			И (ДанныеДокументаИсправления.Проведен)
	|			И (ДанныеДокументаИсправления.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИсправлениеОшибок))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДляРасчетаСКорректировками.Ссылка,
	|	ДанныеДляРасчетаСКорректировками.КорректировкаДата,
	|	ДанныеДокументаКорректировки.Номер,
	|	ДанныеДокументаИсправления.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДляРасчетаСКорректировками.Ссылка,
	|	ДанныеДляРасчетаСКорректировками.КорректировкаДата,
	|	ДанныеДляРасчетаСКорректировками.ОснованиеНомер,
	|	ДанныеДокументаИсправления.Ссылка
	|ИЗ
	|	ДанныеДляРасчетаСКорректировками КАК ДанныеДляРасчетаСКорректировками
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК ДанныеДокументаКорректировки
	|		ПО ДанныеДляРасчетаСКорректировками.КорректировкаСсылка = ДанныеДокументаКорректировки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК ДанныеДокументаИсправления
	|		ПО ДанныеДляРасчетаСКорректировками.ОснованиеСсылка = ДанныеДокументаИсправления.ДокументОснование
	|			И ДанныеДляРасчетаСКорректировками.ОснованиеДата <= ДанныеДокументаИсправления.Дата
	|			И ДанныеДляРасчетаСКорректировками.МоментВремени >= ДанныеДокументаИсправления.МоментВремени
	|			И (ДанныеДокументаИсправления.Проведен)
	|			И (ДанныеДокументаИсправления.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИсправлениеОшибок))
	|ГДЕ
	|	ДанныеДокументаКорректировки.Ссылка ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДляРасчетаСКорректировками.Ссылка,
	|	ДанныеДляРасчетаСКорректировками.КорректировкаДата,
	|	ДанныеДляРасчетаСКорректировками.ОснованиеНомер,
	|	ДанныеДокументаИсправления.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеСКорректировкамиИИсправлениямиПромежуточный.Ссылка КАК Ссылка,
	|	ДанныеСКорректировкамиИИсправлениямиПромежуточный.Дата КАК Дата,
	|	ДанныеСКорректировкамиИИсправлениямиПромежуточный.Номер КАК Номер,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеСКорректировкамиИИсправлениямиПромежуточный.ДанныеДокументаИсправленияСсылка) КАК НомерИсправления
	|ПОМЕСТИТЬ ДанныеСКорректировкамиИИсправлениями
	|ИЗ
	|	ДанныеСКорректировкамиИИсправлениямиПромежуточный КАК ДанныеСКорректировкамиИИсправлениямиПромежуточный
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеСКорректировкамиИИсправлениямиПромежуточный.Ссылка,
	|	ДанныеСКорректировкамиИИсправлениямиПромежуточный.Дата,
	|	ДанныеСКорректировкамиИИсправлениямиПромежуточный.Номер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеДляРасчетаСКорректировкамиПромежуточная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеДляРасчетаСКорректировками
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеСКорректировкамиИИсправлениямиПромежуточный
	|;
	|";
	
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА НЕ ТаблицаТоваров.ЭтоТовар
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьУслуги,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ТаблицаТоваров.ЭтоТовар
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьТовары,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(ТаблицаТоваров.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьПрослеживаемыеТовары
	|ПОМЕСТИТЬ
	|	НоменклатураДокументов
	|ИЗ
	|	КорректировкаРеализацииТаблицаТоваров КАК ТаблицаТоваров
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	&ПредставлениеСчетФактура КАК ПредставлениеДокумента,
	|	2 КАК СтатусУПД,
	|	ЕСТЬNULL(ДанныеСКорректировкамиИИсправлениями.Номер, ДанныеДокумента.Номер) КАК Номер,
	|	ЕСТЬNULL(ДанныеСКорректировкамиИИсправлениями.Дата, ДанныеДокумента.Дата) КАК Дата,
	|	ЕСТЬNULL(ДанныеСКорректировкамиИИсправлениями.НомерИсправления, НЕОПРЕДЕЛЕНО) КАК НомерИсправления,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИсправлениеОшибок)
	|			ТОГДА ДанныеДокумента.Дата
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ДатаИсправления,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИсправлениеОшибок)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Исправление,
	|	НЕОПРЕДЕЛЕНО КАК НомерСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправленияСчетаФактуры,
	|	ЛОЖЬ КАК КорректировочныйСчетФактура,
	|	"""" КАК СтрокаПоДокументу,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаСчетаФактуры,
	|	ДанныеДокумента.Партнер КАК Партнер,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Контрагент.ОбособленноеПодразделение
	|			ТОГДА ДанныеДокумента.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ ДанныеДокумента.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ДанныеДокумента.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ДанныеПоставщика.ГоловнаяОрганизация КАК Организация,
	|	ДанныеДокумента.Организация.Префикс КАК Префикс,
	|	ДанныеДокумента.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации,
	|	ДанныеДокумента.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	ДанныеДокумента.БанковскийСчетГрузоотправителя КАК БанковскийСчетГрузоотправителя,
	|	ДанныеДокумента.БанковскийСчетГрузополучателя КАК БанковскийСчетГрузополучателя,
	|	0 КАК ИндексПодразделения,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ДанныеДокумента.Контрагент
	|		ИНАЧЕ ДанныеДокумента.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ДанныеДокумента.Организация
	|		ИНАЧЕ ДанныеДокумента.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	ДанныеПоставщика.КПППоставщика КАК КПППоставщика,
	|	""""                           КАК КПППокупателя,
	|	ДанныеДокумента.Контрагент.ИНН КАК ИННПокупателя,
	|	ДанныеДокумента.АдресДоставки КАК АдресДоставки,
	|	ДанныеДокумента.Валюта КАК Валюта,
	|	ДанныеДокумента.Валюта.НаименованиеПолное КАК ВалютаНаименованиеПолное,
	|	ДанныеДокумента.Валюта.Код КАК ВалютаКод,
	|	ВЫБОР
	|		КОГДА НоменклатураДокументов.ЕстьУслуги
	|				И НЕ НоменклатураДокументов.ЕстьТовары
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТолькоУслуги,
	|	НоменклатураДокументов.ЕстьПрослеживаемыеТовары КАК ЕстьПрослеживаемыеТовары,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоПередачаНаКомиссию,
	|	ДанныеДокумента.Основание КАК Основание,
	|	ДанныеДокумента.ОснованиеДата КАК ОснованиеДата,
	|	ДанныеДокумента.ОснованиеНомер КАК ОснованиеНомер,
	|	ДанныеДокумента.ДоверенностьНомер КАК ДоверенностьНомер,
	|	ДанныеДокумента.ДоверенностьДата КАК ДоверенностьДата,
	|	ДанныеДокумента.ДоверенностьВыдана КАК ДоверенностьВыдана,
	|	ДанныеДокумента.ДоверенностьЛицо КАК ДоверенностьЛицо,
	|	ДанныеДокумента.Менеджер КАК Менеджер,	
	|	ДанныеДокумента.Отпустил КАК Кладовщик,
	|	ДанныеДокумента.ОтпустилДолжность КАК ДолжностьКладовщика,
	|	ДанныеДокументов.ТребуетсяНаличиеСФ КАК ТребуетсяНаличиеСФ
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ДанныеДокумента.Ссылка = ДанныеДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоставщика КАК ДанныеПоставщика
	|		ПО ДанныеДокумента.Ссылка = ДанныеПоставщика.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураДокументов КАК НоменклатураДокументов
	|		ПО ДанныеДокумента.Ссылка = НоменклатураДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеСКорректировкамиИИсправлениями КАК ДанныеСКорректировкамиИИсправлениями
	|		ПО ДанныеДокумента.Ссылка = ДанныеСКорректировкамиИИсправлениями.Ссылка
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ТаблицаДокумента.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	ТаблицаДокумента.ЭтоНабор КАК ЭтоНабор,
	|	ТаблицаДокумента.ЭтоКомплектующие КАК ЭтоКомплектующие,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.НоменклатураПартнера КАК НоменклатураПартнера,
	|	ЕСТЬNULL(ТаблицаДокумента.Номенклатура.НаименованиеПолное, ТаблицаДокумента.Содержание) КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаДокумента.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаДокумента.Номенклатура.Код
	|	КОНЕЦ КАК НоменклатураКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ &ТекстЗапросаКодЕдиницыИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения               КАК ЕдиницаИзмеренияТНВЭД,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияТНВЭДНаименование,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Код           КАК ЕдиницаИзмеренияТНВЭДКод,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТаблицаДокумента.Серия.Наименование КАК СерияНаименование,	
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.НомерГТД КАК НомерГТДСсылка,
	|	ЕСТЬNULL(ТаблицаДокумента.НомерГТД.ТипНомераГТД, НЕОПРЕДЕЛЕНО) КАК ТипНомераГТД,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.НомерГТД.РегистрационныйНомер = """"
	|			ТОГДА ТаблицаДокумента.НомерГТД
	|		ИНАЧЕ ТаблицаДокумента.НомерГТД.РегистрационныйНомер
	|	КОНЕЦ КАК НомерГТД,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Количество
	|		ИНАЧЕ ТаблицаДокумента.КоличествоУпаковок
	|	КОНЕЦ КАК Количество,
	|	ТаблицаДокумента.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	ТаблицаДокумента.Количество КАК КоличествоБазовыхЕдиниц,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.Количество
	|		ИНАЧЕ ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.КоличествоУпаковок
	|	КОНЕЦ КАК Цена,
	|	ТаблицаДокумента.СуммаБезНДС КАК СуммаБезНДС,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС КАК СуммаСНДС,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Упаковка КАК Упаковка,	
	|	ЛОЖЬ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	КорректировкаРеализацииТаблицаТоваров КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	ИЛИ (ТаблицаДокумента.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	И НЕ ТаблицаДокумента.ВернутьМногооборотнуюТару)
	|	ИЛИ ТаблицаДокумента.Номенклатура.ТипНоменклатуры ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	ЭтоНабор УБЫВ,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка
	//+маркировка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка           КАК Ссылка,
	|	ТаблицаДокумента.ШтрихкодУпаковки КАК Штрихкод
	|ИЗ
	|	Документ.КорректировкаРеализации.ШтрихкодыУпаковок КАК ТаблицаДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаДокумента.Ссылка = ДокументыПрямогоОбмена.ДокументОснование
	|		И НЕ ДокументыПрямогоОбмена.ПометкаУдаления
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В(&МассивОбъектов)
	|	И ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка								КАК Ссылка,
	|	ТаблицаДокумента.НомерГТД							КАК НомерГТД,
	|	Комплектующие.НомерРНПТ								КАК НомерРНПТ,
	|	ВЫБОР
	|		КОГДА Комплектующие.НомерРНПТ.РегистрационныйНомер = """"
	|			ТОГДА Комплектующие.НомерРНПТ
	|		ИНАЧЕ Комплектующие.НомерРНПТ.РегистрационныйНомер
	|	КОНЕЦ												КАК НомерТовара,
	|	НомераГТД.СуммаПоРНПТ								КАК ОбщаяСуммаПоКомплекту,
	|	Комплектующие.СуммаПоРНПТ							КАК СуммаПоРНПТ,
	|	Комплектующие.КоличествоПоРНПТ						КАК Количество,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД					КАК ЕдиницаИзмерения,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД.Код				КАК ЕдиницаИзмеренияКод,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД.Представление	КАК ЕдиницаИзмеренияНаименование
	|ИЗ
	|	КорректировкаРеализацииТаблицаТоваров КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД.ПрослеживаемыеКомплектующие КАК Комплектующие
	|		ПО ТаблицаДокумента.НомерГТД = Комплектующие.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК НомераГТД
	|		ПО ТаблицаДокумента.НомерГТД = НомераГТД.Ссылка";


	// ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	Если ЕстьЭлектронноеАктирование Тогда
		Запрос.Текст = Запрос.Текст	+
		"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка КАК Ссылка,
		|	ТаблицаДокумента.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
		|	ТаблицаДокумента.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
		|	ТаблицаДокумента.ЭтоНабор КАК ЭтоНабор,
		|	ТаблицаДокумента.ЭтоКомплектующие КАК ЭтоКомплектующие,
		|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
		|	ТаблицаДокумента.НоменклатураПартнера КАК НоменклатураПартнера,
		|	ЕСТЬNULL(ТаблицаДокумента.Номенклатура.НаименованиеПолное, ТаблицаДокумента.Содержание) КАК НоменклатураНаименование,
		|	ВЫБОР
		|		КОГДА &КолонкаКодов = ""Артикул""
		|			ТОГДА ТаблицаДокумента.Номенклатура.Артикул
		|		ИНАЧЕ ТаблицаДокумента.Номенклатура.Код
		|	КОНЕЦ КАК НоменклатураКод,
		|	ВЫБОР
		|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
		|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения
		|		ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения
		|	КОНЕЦ КАК ЕдиницаИзмерения,
		|	ВЫБОР
		|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
		|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Представление
		|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения
		|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
		|	ВЫБОР
		|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
		|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Код
		|		ИНАЧЕ &ТекстЗапросаКодЕдиницыИзмерения
		|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
		|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения               КАК ЕдиницаИзмеренияТНВЭД,
		|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияТНВЭДНаименование,
		|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Код           КАК ЕдиницаИзмеренияТНВЭДКод,
		|	ТаблицаДокумента.Характеристика КАК Характеристика,
		|	ТаблицаДокумента.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
		|	ТаблицаДокумента.Серия.Наименование КАК СерияНаименование,	
		|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.НомерГТД.РегистрационныйНомер = """"
		|			ТОГДА ТаблицаДокумента.НомерГТД
		|		ИНАЧЕ ТаблицаДокумента.НомерГТД.РегистрационныйНомер
		|	КОНЕЦ КАК НомерГТД,
		|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
		|	ВЫБОР
		|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
		|			ТОГДА ТаблицаДокумента.Количество
		|		ИНАЧЕ ТаблицаДокумента.КоличествоУпаковок
		|	КОНЕЦ КАК Количество,
		|	ТаблицаДокумента.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
		|	ТаблицаДокумента.Количество КАК КоличествоБазовыхЕдиниц,
		|	ВЫБОР
		|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
		|			ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.Количество
		|		ИНАЧЕ ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.КоличествоУпаковок
		|	КОНЕЦ КАК Цена,
		|	ТаблицаДокумента.СуммаБезНДС КАК СуммаБезНДС,
		|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
		|	ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС КАК СуммаСНДС,
		|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
		|	ТаблицаДокумента.Упаковка КАК Упаковка,	
		|	ЛОЖЬ КАК ЭтоВозвратнаяТара
		|
		|ПОМЕСТИТЬ ТаблицаТоваровДокументов
		|ИЗ
		|	КорректировкаРеализацииТаблицаТоваров КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|	ИЛИ (ТаблицаДокумента.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|	И НЕ ТаблицаДокумента.ВернутьМногооборотнуюТару)
		|	ИЛИ ТаблицаДокумента.Номенклатура.ТипНоменклатуры ЕСТЬ NULL
		|"
		+ ЭлектронноеАктированиеЕИСУТ.ТекстЗапросаДанныеДляПечатиСчетовФактур("МассивОбъектов");
		Запрос.УстановитьПараметр("ГосударственныйКонтрактЕИС", Контракт);
		Запрос.УстановитьПараметр("ЭтапИсполненияКонтрактаЕИС", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(МассивОбъектов[0], "ЭтапГосконтрактаЕИС"));
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС

	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
	
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения", Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
	Запрос.УстановитьПараметр("ПредставлениеСчетФактура", НСтр("ru='счет-фактура'"));
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	
	МассивРезультатов			= Запрос.ВыполнитьПакет();
	РезультатПоШапке			= МассивРезультатов[1];
	РезультатПоТабличнойЧасти	= МассивРезультатов[2];
	ШтрихкодыУпаковок			= МассивРезультатов[3].Выгрузить();
	
	Если ПараметрыПечати.Свойство("ЗаполнениеЭлектронногоДокумента") Тогда
		ТаблицаТоварыДляЭДО = ИнтеграцияИСМПУТ.ТаблицаТоварыДляЭДО(РезультатПоТабличнойЧасти.Выгрузить(), "КоличествоБазовыхЕдиниц");
		Если ТаблицаТоварыДляЭДО.Количество() Тогда
			ПараметрыСканирования = ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования(ТаблицаТоварыДляЭДО[0].Ссылка);
		КонецЕсли;
		Маркировка = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимоеИКодыОСУ(
			ШтрихкодыУпаковок,
			ТаблицаТоварыДляЭДО,
			ПараметрыСканирования);
	Иначе
		Маркировка = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое(ШтрихкодыУпаковок);
	КонецЕсли;
	
	ПрослеживаемыеКомплектующие	= УчетПрослеживаемыхТоваровЛокализация.ПрослеживаемыеКомплектующиеДляПечатиДанных(
									МассивРезультатов[4]);
	
	ДанныеДляПечати = Новый Структура;
	ДанныеДляПечати.Вставить("РезультатПоШапке",			РезультатПоШапке);
	ДанныеДляПечати.Вставить("РезультатПоТабличнойЧасти",	РезультатПоТабличнойЧасти);
	ДанныеДляПечати.Вставить("Маркировка",					Маркировка);
	ДанныеДляПечати.Вставить("ПрослеживаемыеКомплектующие",	ПрослеживаемыеКомплектующие);
	

	// ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	Если ЕстьЭлектронноеАктирование Тогда	
		КоличествоРезультатов     = МассивРезультатов.Количество() - 5;
		ЭлектронноеАктированиеЕИСУТ.ПоместитьРезультатВыполненияЗапросаВДанныеДляПечати(
			МассивРезультатов, ДанныеДляПечати, КоличествоРезультатов);
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС

	
	Возврат ДанныеДляПечати;
	
КонецФункции

// Функция получает данные для формирования печатной формы УКД
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.КорректировкаРеализации - Массив ссылок на документы, 
//																	по которым необходимо получить данные.
//
// Возвращаемое значение:
// 	Структура:
// 		* РезультатПоШапке - РезультатЗапроса
// 		* РезультатПоИсходнымДанным - РезультатЗапроса
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса
// 		* ПрослеживаемыеКомплектующие - см. УчетПрослеживаемыхТоваровЛокализация.ПрослеживаемыеКомплектующиеДляПечатиДанных
//
Функция ПолучитьДанныеДляПечатнойФормыУКД(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка                КАК Ссылка,
	|	ДанныеДокументов.Валюта                КАК Валюта,
	|	ДанныеДокументов.Организация           КАК Организация,
	|	ДанныеДокументов.Подразделение         КАК Подразделение,
	|	ДанныеДокументов.Склад                 КАК Склад,
	|	ДанныеДокументов.ДокументОснование     КАК ДокументОснование,
	|	ДанныеДокументов.ВидКорректировки      КАК ХозяйственнаяОперация,
	|	ДанныеДокументов.ТипКорректировки      КАК ТипКорректировки,
	|	ДанныеДокументов.Дата                  КАК Период
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК ДокументОснование,
	|	ИСТИНА КАК Корректировочный
	|
	|ПОМЕСТИТЬ СчетаФактурыОснования
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка");
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Выполнить();
	
	Обработки.ПечатьОбщихФорм.ПоместитьВременнуюТаблицуДанныхПоставщика(МенеджерВременныхТаблиц);
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	ПараметрыЗаполнения.ВключаяНомераГТД       = Истина;
	ПараметрыЗаполнения.ВключаяДоКорректировки = Истина;
	ПараметрыЗаполнения.ОбработатьНастройкиПечатиНаборов = Истина;
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	СформироватьВременнуюТаблицуНоменклатураДокументов(МенеджерВременныхТаблиц);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДанныхДокументов.Ссылка КАК Ссылка,
	|	ДанныеОснования.Номер КАК НомерСчетаФактуры,
	|	ДанныеОснования.Дата КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправленияСчетаФактуры
	|
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК ДанныеОснования
	|		ПО ТаблицаДанныхДокументов.ДокументОснование = ДанныеОснования.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДанныхДокументов.Ссылка,
	|	ДанныеОснования.Номер,
	|	ДанныеОснования.Дата,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов КАК ДанныеОснования
	|		ПО ТаблицаДанныхДокументов.ДокументОснование = ДанныеОснования.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДанныхДокументов.Ссылка,
	|	ДанныеОснования.Номер,
	|	ДанныеОснования.Дата,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктВыполненныхРабот КАК ДанныеОснования
	|		ПО ТаблицаДанныхДокументов.ДокументОснование = ДанныеОснования.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДанныхДокументов.Ссылка,
	|	ДанныеОснования.Номер,
	|	ДанныеОснования.Дата,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПервичныйДокумент КАК ДанныеОснования
	|		ПО ТаблицаДанныхДокументов.ДокументОснование = ДанныеОснования.Ссылка
	|
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	2 КАК СтатусУПД,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Дата КАК Дата,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправления,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправления,
	|	ЛОЖЬ КАК Исправление,
	|	НЕОПРЕДЕЛЕНО КАК НомерСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаСчетаФактуры,
	|	ДанныеДокумента.Партнер КАК Партнер,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Контрагент.ОбособленноеПодразделение
	|			ТОГДА ДанныеДокумента.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ ДанныеДокумента.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ДанныеПоставщика.ГоловнаяОрганизация КАК Организация,
	|	ДанныеДокумента.Организация.Префикс КАК Префикс,
	|	ДанныеДокумента.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации,
	|	ДанныеДокумента.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	ДанныеДокумента.БанковскийСчетГрузоотправителя КАК БанковскийСчетГрузоотправителя,
	|	ДанныеДокумента.БанковскийСчетГрузополучателя КАК БанковскийСчетГрузополучателя,
	|	0 КАК ИндексПодразделения,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	ДанныеПоставщика.КПППоставщика КАК КПППоставщика,
	|	ДанныеДокумента.Контрагент.КПП КАК КПППокупателя,
	|	ДанныеДокумента.Валюта КАК Валюта,
	|	ДанныеДокумента.Валюта.НаименованиеПолное КАК ВалютаНаименованиеПолное,
	|	ДанныеДокумента.Валюта.Код КАК ВалютаКод,
	|	ДанныеДокумента.Основание КАК Основание,
	|	ДанныеДокумента.ОснованиеДата КАК ОснованиеДата,
	|	ДанныеДокумента.ОснованиеНомер КАК ОснованиеНомер,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПоСогласованиюСторон)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ПечатьНеТребуется,
	|	ДанныеДокумента.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ЕСТЬNULL(НоменклатураДокументов.ЕстьПрослеживаемыеТовары, ЛОЖЬ) КАК ЕстьПрослеживаемыеТовары
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ДанныеДокумента.Ссылка = ДанныеДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоставщика КАК ДанныеПоставщика
	|		ПО ДанныеДокумента.Ссылка = ДанныеПоставщика.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураДокументов КАК НоменклатураДокументов
	|		ПО ДанныеДокумента.Ссылка = НоменклатураДокументов.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ТаблицаДокумента.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	ТаблицаДокумента.ЭтоНабор,
	|	ТаблицаДокумента.ЭтоКомплектующие КАК ЭтоКомплектующие,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(ТаблицаДокумента.Номенклатура.НаименованиеПолное, ТаблицаДокумента.Содержание) КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаДокумента.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаДокумента.Номенклатура.Код
	|	КОНЕЦ КАК НоменклатураКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения1
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения1
	|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ &ТекстЗапросаКодЕдиницыИзмерения1
	|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТаблицаДокумента.Упаковка КАК Упаковка,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.НомерГТД КАК НомерГТДСсылка,
	|	ЕСТЬNULL(ТаблицаДокумента.НомерГТД.ТипНомераГТД, НЕОПРЕДЕЛЕНО) КАК ТипНомераГТД,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.НомерГТД.РегистрационныйНомер = """"
	|			ТОГДА ТаблицаДокумента.НомерГТД
	|		ИНАЧЕ ТаблицаДокумента.НомерГТД.РегистрационныйНомер
	|	КОНЕЦ КАК НомерГТД,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Количество
	|		ИНАЧЕ ТаблицаДокумента.КоличествоУпаковок
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0)
	|		ИНАЧЕ ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоУпаковок, 0)
	|	КОНЕЦ КАК КоличествоДо,
	|	ТаблицаДокумента.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0) КАК КоличествоПоРНПТДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.КоличествоПоРНПТ >= ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0)
	|			ТОГДА ТаблицаДокумента.КоличествоПоРНПТ - ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоПрослежУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.КоличествоПоРНПТ < ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0)
	|			ТОГДА -(ТаблицаДокумента.КоличествоПоРНПТ - ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоПрослежУменьшение,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ВЫБОР КОГДА ТаблицаДокумента.Количество <> 0 ТОГДА
	|				ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.Количество
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР КОГДА ТаблицаДокумента.КоличествоУпаковок <> 0 ТОГДА
	|				ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.КоличествоУпаковок
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|	КОНЕЦ КАК Цена,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ВЫБОР КОГДА ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0) <> 0 ТОГДА
	|				ТаблицаТоваровПредыдущие.СуммаБезНДС / ТаблицаТоваровПредыдущие.Количество
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР КОГДА ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоУпаковок, 0) <> 0 ТОГДА
	|				ТаблицаТоваровПредыдущие.СуммаБезНДС / ТаблицаТоваровПредыдущие.КоличествоУпаковок
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|	КОНЕЦ КАК ЦенаДо,
	|	ТаблицаДокумента.СуммаБезНДС КАК СуммаБезНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) КАК СуммаБезНДСДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС >= ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаБезНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС < ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаБезНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаБезНДСУменьшение,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0) КАК СуммаНДСДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаНДС >= ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаНДС < ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаНДСУменьшение,
	|	ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС КАК СуммаСНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0) КАК СуммаСНДСДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС >=
	|				ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС -
	|				(ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаСНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС <
	|				ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС -
	|				(ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаСНДСУменьшение,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ЛОЖЬ КАК ЭтоВозвратнаяТара,
	|	1 КАК Порядок,
	|	ТаблицаДокумента.КодТНВЭД КАК КодТНВЭД,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения               КАК ЕдиницаИзмеренияТНВЭД,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияТНВЭДНаименование,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Код           КАК ЕдиницаИзмеренияТНВЭДКод,
	|	ТаблицаДокумента.Количество КАК КоличествоБазовыхЕдиниц,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0) КАК КоличествоБазовыхЕдиницДо
	|ИЗ
	|	КорректировкаРеализацииТаблицаТоваров КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ КорректировкаРеализацииТаблицаТоваров КАК ТаблицаТоваровПредыдущие
	|		ПО (ТаблицаТоваровПредыдущие.ДоКорректировки)
	|			И ТаблицаДокумента.Ссылка = ТаблицаТоваровПредыдущие.Ссылка
	|			И ТаблицаДокумента.Номенклатура = ТаблицаТоваровПредыдущие.Номенклатура
	|			И ТаблицаДокумента.Характеристика = ТаблицаТоваровПредыдущие.Характеристика
	|			И ТаблицаДокумента.Упаковка = ТаблицаТоваровПредыдущие.Упаковка
	|			И ТаблицаДокумента.НомерГТД = ТаблицаТоваровПредыдущие.НомерГТД
	|			И ТаблицаДокумента.СтавкаНДС = ТаблицаТоваровПредыдущие.СтавкаНДС
	|			И ТаблицаДокумента.Содержание = ТаблицаТоваровПредыдущие.Содержание
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаФактурыОснования КАК СчетаФактуры
	|		ПО ТаблицаДокумента.Ссылка = СчетаФактуры.ДокументОснование
	|ГДЕ
	|	НЕ ТаблицаДокумента.ДоКорректировки
	|	И (НЕ ЕСТЬNULL(СчетаФактуры.Корректировочный, ИСТИНА)
	|			ИЛИ ТаблицаДокумента.СуммаНДС <> ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ИЛИ ТаблицаДокумента.СуммаБезНДС <> ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|			ИЛИ &УсловиеМаркируемаяПродукция)
	|	И (ТаблицаДокумента.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	ИЛИ (ТаблицаДокумента.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	И НЕ ТаблицаДокумента.ВернутьМногооборотнуюТару)
	|	ИЛИ ТаблицаДокумента.Номенклатура.ТипНоменклатуры ЕСТЬ NULL)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваровПредыдущие.Ссылка,
	|	ТаблицаТоваровПредыдущие.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ТаблицаТоваровПредыдущие.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	ТаблицаТоваровПредыдущие.ЭтоНабор,
	|	ТаблицаТоваровПредыдущие.ЭтоКомплектующие КАК ЭтоКомплектующие,
	|	ТаблицаТоваровПредыдущие.Номенклатура,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.Номенклатура.НаименованиеПолное, ТаблицаТоваровПредыдущие.Содержание),
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаТоваровПредыдущие.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаТоваровПредыдущие.Номенклатура.Код
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваровПредыдущие.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения2
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваровПредыдущие.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения2
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваровПредыдущие.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ &ТекстЗапросаКодЕдиницыИзмерения2
	|	КОНЕЦ,
	|	ТаблицаТоваровПредыдущие.Характеристика,
	|	ТаблицаТоваровПредыдущие.Характеристика.НаименованиеПолное,
	|	ТаблицаТоваровПредыдущие.Упаковка,
	|	ТаблицаТоваровПредыдущие.СтавкаНДС,
	|	ТаблицаТоваровПредыдущие.НомерГТД КАК НомерГТДСсылка,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.НомерГТД.ТипНомераГТД, НЕОПРЕДЕЛЕНО) КАК ТипНомераГТД,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваровПредыдущие.НомерГТД.РегистрационныйНомер = """"
	|			ТОГДА ТаблицаТоваровПредыдущие.НомерГТД
	|		ИНАЧЕ ТаблицаТоваровПредыдущие.НомерГТД.РегистрационныйНомер
	|	КОНЕЦ КАК НомерГТД,
	|	ТаблицаТоваровПредыдущие.НомерГТД.СтранаПроисхождения,
	|	ТаблицаТоваровПредыдущие.НомерГТД.СтранаПроисхождения.Код,
	|	0,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваровПредыдущие.Количество
	|		ИНАЧЕ ТаблицаТоваровПредыдущие.КоличествоУпаковок
	|	КОНЕЦ,
	|	0,
	|	ТаблицаТоваровПредыдущие.КоличествоПоРНПТ,
	|	0,
	|	ТаблицаТоваровПредыдущие.КоличествоПоРНПТ,
	|	0 ,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ВЫБОР КОГДА ТаблицаТоваровПредыдущие.Количество <> 0 ТОГДА
	|				ТаблицаТоваровПредыдущие.СуммаБезНДС / ТаблицаТоваровПредыдущие.Количество
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР КОГДА ТаблицаТоваровПредыдущие.КоличествоУпаковок <> 0 ТОГДА
	|				ТаблицаТоваровПредыдущие.СуммаБезНДС / ТаблицаТоваровПредыдущие.КоличествоУпаковок
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|	КОНЕЦ,
	|	0,
	|	ТаблицаТоваровПредыдущие.СуммаБезНДС,
	|	0,
	|	ТаблицаТоваровПредыдущие.СуммаБезНДС,
	|	0,
	|	ТаблицаТоваровПредыдущие.СуммаНДС,
	|	0,
	|	ТаблицаТоваровПредыдущие.СуммаНДС,
	|	0,
	|	ТаблицаТоваровПредыдущие.СуммаБезНДС + ТаблицаТоваровПредыдущие.СуммаНДС,
	|	0,
	|	ТаблицаТоваровПредыдущие.СуммаБезНДС + ТаблицаТоваровПредыдущие.СуммаНДС,
	|	ТаблицаТоваровПредыдущие.НомерСтроки,
	|	ЛОЖЬ,
	|	2,
	|	ТаблицаТоваровПредыдущие.КодТНВЭД КАК КодТНВЭД,
	|	ТаблицаТоваровПредыдущие.НомерГТД.ЕдиницаИзмерения               КАК ЕдиницаИзмеренияТНВЭД,
	|	ТаблицаТоваровПредыдущие.НомерГТД.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияТНВЭДНаименование,
	|	ТаблицаТоваровПредыдущие.НомерГТД.ЕдиницаИзмерения.Код           КАК ЕдиницаИзмеренияТНВЭДКод,
	|	ТаблицаТоваровПредыдущие.Количество КАК КоличествоБазовыхЕдиниц,
	|	0 КАК КоличествоБазовыхЕдиницДо
	|ИЗ
	|	КорректировкаРеализацииТаблицаТоваров КАК ТаблицаТоваровПредыдущие
	|		ЛЕВОЕ СОЕДИНЕНИЕ КорректировкаРеализацииТаблицаТоваров КАК ТаблицаТоваровПослеКорректировки
	|		ПО (НЕ ТаблицаТоваровПослеКорректировки.ДоКорректировки)
	|			И ТаблицаТоваровПредыдущие.Ссылка = ТаблицаТоваровПослеКорректировки.Ссылка
	|			И ТаблицаТоваровПредыдущие.Номенклатура = ТаблицаТоваровПослеКорректировки.Номенклатура
	|			И ТаблицаТоваровПредыдущие.Характеристика = ТаблицаТоваровПослеКорректировки.Характеристика
	|			И ТаблицаТоваровПредыдущие.Упаковка = ТаблицаТоваровПослеКорректировки.Упаковка
	|			И ТаблицаТоваровПредыдущие.НомерГТД = ТаблицаТоваровПослеКорректировки.НомерГТД
	|			И ТаблицаТоваровПредыдущие.СтавкаНДС = ТаблицаТоваровПослеКорректировки.СтавкаНДС
	|			И ТаблицаТоваровПредыдущие.Содержание = ТаблицаТоваровПослеКорректировки.Содержание
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаФактурыОснования КАК СчетаФактуры
	|		ПО ТаблицаТоваровПредыдущие.Ссылка = СчетаФактуры.ДокументОснование
	|ГДЕ
	|	ТаблицаТоваровПредыдущие.ДоКорректировки
	|	И ТаблицаТоваровПослеКорректировки.Ссылка ЕСТЬ NULL 
	|	И ЕСТЬNULL(СчетаФактуры.Корректировочный, ИСТИНА)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДанныхДокументов.Ссылка           КАК Ссылка,
	|	ТаблицаШтрихкодов.ШтрихкодУпаковки КАК Штрихкод
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.ШтрихкодыУпаковок КАК ТаблицаШтрихкодов
	|		ПО ТаблицаДанныхДокументов.Ссылка = ТаблицаШтрихкодов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаДанныхДокументов.Ссылка = ДокументыПрямогоОбмена.ДокументОснование
	|		И НЕ ДокументыПрямогоОбмена.ПометкаУдаления
	|ГДЕ
	|	ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредыдущиеДокументы.Основной         КАК Ссылка,
	|	ТаблицаШтрихкодовДо.ШтрихкодУпаковки КАК Штрихкод
	|ИЗ
	|	ПредыдущиеДокументы КАК ПредыдущиеДокументы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.ШтрихкодыУпаковок КАК ТаблицаШтрихкодовДо
	|		ПО ПредыдущиеДокументы.Предыдущий = ТаблицаШтрихкодовДо.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаШтрихкодовДо.Ссылка = ДокументыПрямогоОбмена.ДокументОснование
	|		И НЕ ДокументыПрямогоОбмена.ПометкаУдаления
	|ГДЕ
	|	ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПредыдущиеДокументы.Основной,
	|	ТаблицаШтрихкодовДо.ШтрихкодУпаковки
	|ИЗ
	|	ПредыдущиеДокументы КАК ПредыдущиеДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.ШтрихкодыУпаковок КАК ТаблицаШтрихкодовДо
	|		ПО ПредыдущиеДокументы.Предыдущий = ТаблицаШтрихкодовДо.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО (ТаблицаШтрихкодовДо.Ссылка = ДокументыПрямогоОбмена.ДокументОснование)
	|			И (НЕ ДокументыПрямогоОбмена.ПометкаУдаления)
	|ГДЕ
	|	ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка								КАК Ссылка,
	|	ТаблицаДокумента.НомерГТД							КАК НомерГТД,
	|	Комплектующие.НомерРНПТ								КАК НомерРНПТ,
	|	ВЫБОР
	|		КОГДА Комплектующие.НомерРНПТ.РегистрационныйНомер = """"
	|			ТОГДА Комплектующие.НомерРНПТ
	|		ИНАЧЕ Комплектующие.НомерРНПТ.РегистрационныйНомер
	|	КОНЕЦ												КАК НомерТовара,
	|	НомераГТД.СуммаПоРНПТ								КАК ОбщаяСуммаПоКомплекту,
	|	Комплектующие.СуммаПоРНПТ							КАК СуммаПоРНПТ,
	|	Комплектующие.КоличествоПоРНПТ						КАК Количество,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД					КАК ЕдиницаИзмерения,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД.Код				КАК ЕдиницаИзмеренияКод,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД.Представление	КАК ЕдиницаИзмеренияНаименование
	|ИЗ
	|	КорректировкаРеализацииТаблицаТоваров КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД.ПрослеживаемыеКомплектующие КАК Комплектующие
	|		ПО ТаблицаДокумента.НомерГТД = Комплектующие.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК НомераГТД
	|		ПО ТаблицаДокумента.НомерГТД = НомераГТД.Ссылка";
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаТоваровПредыдущие.Упаковка",
			"ТаблицаТоваровПредыдущие.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаТоваровПредыдущие.Упаковка",
			"ТаблицаТоваровПредыдущие.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаТоваровПредыдущие.Упаковка",
			"ТаблицаТоваровПредыдущие.Номенклатура"));
	
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения", Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	Запрос.УстановитьПараметр("ПустаяУпаковкаНоменклатуры", Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
	
	ВидыМаркируемойПродукции = ИнтеграцияИСМП.ИспользуемыеВидыМаркируемойПродукцииИСМП();
	ИнтеграцияИСМПУТ.УстановитьУсловиеПоВидуМаркируемойПродукции(Запрос.Текст, ВидыМаркируемойПродукции, "ТаблицаДокумента.Номенклатура");

	МассивРезультатов         = Запрос.ВыполнитьПакет();
	РезультатПоИсходнымДанным = МассивРезультатов[0];
	РезультатПоШапке          = МассивРезультатов[1];
	РезультатПоТабличнойЧасти = МассивРезультатов[2];
	
	Если ПараметрыПечати.Свойство("ЗаполнениеЭлектронногоДокумента") Тогда
		
		ТаблицаТоварыДляЭДО = ИнтеграцияИСМПУТ.ТаблицаТоварыДляЭДО(РезультатПоТабличнойЧасти.Выгрузить(), "КоличествоБазовыхЕдиницДо");
		Если ТаблицаТоварыДляЭДО.Количество() Тогда
			ПараметрыСканирования = ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования(ТаблицаТоварыДляЭДО[0].Ссылка);
		КонецЕсли;
		МаркировкаДо = МассивРезультатов[4].Выгрузить();
		МаркировкаДо = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимоеИКодыОСУ(
			МаркировкаДо,
			ТаблицаТоварыДляЭДО,
			ПараметрыСканирования);
		
		Если МаркировкаДо.Количество() = 0 Тогда // Коды переданы через прямой обмен с ИС МП
			Маркировка = МаркировкаДо.СкопироватьКолонки();
		Иначе
			
			ТаблицаТоварыДляЭДО = ИнтеграцияИСМПУТ.ТаблицаТоварыДляЭДО(РезультатПоТабличнойЧасти.Выгрузить(), "КоличествоБазовыхЕдиниц");
			Маркировка = МассивРезультатов[3].Выгрузить();
			Маркировка = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимоеИКодыОСУ(
				Маркировка,
				ТаблицаТоварыДляЭДО,
				ПараметрыСканирования);
			
		КонецЕсли;
		
	Иначе
		
		МаркировкаДо = МассивРезультатов[4].Выгрузить();
		МаркировкаДо = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое(МаркировкаДо);
		Если МаркировкаДо.Количество() = 0 Тогда // Коды переданы через прямой обмен с ИС МП
			Маркировка = МаркировкаДо.СкопироватьКолонки();
		Иначе
			
			Маркировка = МассивРезультатов[3].Выгрузить();
			Маркировка = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое(Маркировка);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПрослеживаемыеКомплектующие	= УчетПрослеживаемыхТоваровЛокализация.ПрослеживаемыеКомплектующиеДляПечатиДанных(
									МассивРезультатов[5]);

	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("РезультатПоШапке", РезультатПоШапке);
	СтруктураДанныхДляПечати.Вставить("РезультатПоТабличнойЧасти", РезультатПоТабличнойЧасти);
	СтруктураДанныхДляПечати.Вставить("РезультатПоИсходнымДанным", РезультатПоИсходнымДанным);
	СтруктураДанныхДляПечати.Вставить("Маркировка", Маркировка);
	СтруктураДанныхДляПечати.Вставить("МаркировкаДо", МаркировкаДо);
	СтруктураДанныхДляПечати.Вставить("ПрослеживаемыеКомплектующие",	ПрослеживаемыеКомплектующие);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Возвращает данные для формирования печатной формы МХ - 1.
//
// Параметры:
//	ПараметрыПечати	- Структура -дополнительные настройки печати.
//	МассивОбъектов	- Массив из ДокументСсылка.КорректировкаРеализации - коллекция значений ссылок на документы,
//																			по которым необходимо получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемых для печати, содержащая следующие следующие свойства:
//		* РезультатПоШапке			- РезультатЗапроса - данные шапки документа.
//		* РезультатПоСкладам		- РезультатЗапроса - данные о складе ответственного хранения.
//		* РезультатПоТабличнойЧасти	- РезультатЗапроса - данные табличной части документа.
//		* РезультатПоОшибкам		- РезультатЗапроса - данные об ошибках, возникающих при печати документа.
//
Функция ПолучитьДанныеДляПечатнойФормыМХ1(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка									КАК Ссылка,
	|	ДанныеДокумента.Номер									КАК Номер,
	|	ДанныеДокумента.Дата									КАК Дата,
	|	ДанныеДокумента.Дата									КАК ДатаДокумента,
	|	ДанныеДокумента.Организация								КАК Организация,
	|	ДанныеДокумента.Организация.Префикс						КАК Префикс,
	|	ТоварыДокумента.Склад									КАК Склад,
	|	ЕСТЬNULL(Склады.СкладОтветственногоХранения, ЛОЖЬ)		КАК СкладОтветственногоХранения,
	|	ДанныеДокумента.Организация = Склады.Поклажедержатель	КАК ОрганизацияПоклажедержатель,
	|	Склады.ИсточникИнформацииОЦенахДляПечати				КАК ИсточникИнформацииОЦенахДляПечати,
	|	Склады.УчетныйВидЦены									КАК ВидЦены,
	|	Склады.УчетныйВидЦены.ВалютаЦены						КАК ВалютаЦены,
	|	РасчетСебестоимостиТоваровОрганизации.Ссылка.ПредварительныйРасчет	КАК ПредварительныйРасчет,
	|	ДанныеДокумента.Дата									КАК ДатаПолученияСебестоимости
	|ПОМЕСТИТЬ ДанныеДокумента
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ТоварыДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК ДанныеДокумента
	|		ПО ТоварыДокумента.Ссылка = ДанныеДокумента.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК РасчетСебестоимостиТоваровОрганизации
	|		ПО РасчетСебестоимостиТоваровОрганизации.Ссылка.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ)
	|			И РасчетСебестоимостиТоваровОрганизации.Ссылка.Проведен
	|			И ДанныеДокумента.Организация = РасчетСебестоимостиТоваровОрганизации.Организация
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО ТоварыДокумента.Склад = Склады.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Проведен
	|	И ДанныеДокумента.Ссылка В(&МассивДокументов)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка								КАК Ссылка,
	|	ДанныеДокумента.Номер								КАК Номер,
	|	ДанныеДокумента.Дата								КАК Дата,
	|	ДанныеДокумента.ДатаДокумента						КАК ДатаДокумента,
	|	ДанныеДокумента.Организация							КАК Организация,
	|	ДанныеДокумента.Префикс								КАК Префикс,
	|	ДанныеДокумента.Склад								КАК Склад,
	|	ДанныеДокумента.ИсточникИнформацииОЦенахДляПечати	КАК ИсточникИнформацииОЦенахДляПечати,
	|	ДанныеДокумента.ВидЦены								КАК ВидЦены,
	|	ДанныеДокумента.ВалютаЦены							КАК ВалютаЦены,
	|	ДанныеДокумента.ПредварительныйРасчет				КАК ПредварительныйРасчет,
	|	ДанныеДокумента.ДатаПолученияСебестоимости			КАК ДатаПолученияСебестоимости
	|ПОМЕСТИТЬ ВтШапка
	|ИЗ
	|	ДанныеДокумента КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.СкладОтветственногоХранения
	|	И НЕ ДанныеДокумента.ОрганизацияПоклажедержатель
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Склад КАК Склад,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.Серия КАК Серия,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения1 КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения1 КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения1 КАК ВидУпаковки,
	|	-Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	-Товары.Количество КАК Количество,
	|	КОНЕЦПЕРИОДА(Товары.Ссылка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	Шапка.ВидЦены КАК ВидЦены,
	|	Шапка.ВалютаЦены КАК ВалютаЦены
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО Товары.Склад = Шапка.Склад
	|			И Товары.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	Товары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоВидуЦен)
	|		ИЛИ (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|			И Шапка.ПредварительныйРасчет ЕСТЬ NULL))
	|	И Товары.ВариантОтражения В(
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюУвеличитьСкладскиеОстатки))
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК Ссылка,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	Шапка.Организация КАК Организация,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория КАК Склад,
	|	ВидыЗапасов.Упаковка КАК Упаковка,
	|	ВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения2 КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения2 КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения2 КАК ВидУпаковки,
	|	ВидыЗапасов.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ВидыЗапасов.Количество КАК Количество,
	|	ВидыЗапасов.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	КОНЕЦПЕРИОДА(ВидыЗапасов.Ссылка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория.УчетныйВидЦены КАК ВидЦены,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория.УчетныйВидЦены.ВалютаЦены КАК ВалютаЦены,
	|	Шапка.ПредварительныйРасчет КАК ПредварительныйРасчет
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК ВидыЗапасов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория = Шапка.Склад
	|			И ВидыЗапасов.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|;
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование", "Товары.Упаковка", "Товары.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код", "Товары.Упаковка", "Товары.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование", "ВидыЗапасов.Упаковка", "ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код", "ВидыЗапасов.Упаковка", "ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура"));
	
	СкладыСервер.ДополнитьТекстЗапросаДляПечатныхФормМХ1Х3(Запрос);
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	Документы.КорректировкаРеализации.УстановитьПараметрыВтВидыЗапасов(
		Запрос,
		Ложь, // ПересчитыватьВВалютуРегл
		Ложь,); // ВключаяДоКорректировки
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	РезультатПоШапке			= МассивРезультатов[МассивРезультатов.ВГраница() - 3];
	РезультатПоСкладам			= МассивРезультатов[МассивРезультатов.ВГраница() - 2];
	РезультатПоТабличнойЧасти	= МассивРезультатов[МассивРезультатов.ВГраница() - 1];
	РезультатПоОшибкам			= МассивРезультатов[МассивРезультатов.ВГраница()];
	
	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("РезультатПоШапке",			РезультатПоШапке);
	СтруктураДанныхДляПечати.Вставить("РезультатПоСкладам",			РезультатПоСкладам);
	СтруктураДанныхДляПечати.Вставить("РезультатПоТабличнойЧасти",	РезультатПоТабличнойЧасти);
	СтруктураДанныхДляПечати.Вставить("РезультатПоОшибкам",			РезультатПоОшибкам);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Возвращает данные для формирования печатной формы МХ - 3.
//
// Параметры:
//	ПараметрыПечати	- Структура -дополнительные настройки печати.
//	МассивОбъектов	- Массив из ДокументСсылка.КорректировкаРеализации - коллекция значений ссылок на документы,
//																			по которым необходимо получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемых для печати, содержащая следующие следующие свойства:
//		* РезультатПоШапке			- РезультатЗапроса - данные шапки документа.
//		* РезультатПоСкладам		- РезультатЗапроса - данные о складе ответственного хранения.
//		* РезультатПоТабличнойЧасти	- РезультатЗапроса - данные табличной части документа.
//		* РезультатПоОшибкам		- РезультатЗапроса - данные об ошибках, возникающих при печати документа.
//
Функция ПолучитьДанныеДляПечатнойФормыМХ3(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка									КАК Ссылка,
	|	ДанныеДокумента.Номер									КАК Номер,
	|	ДанныеДокумента.Дата									КАК Дата,
	|	ДанныеДокумента.Дата									КАК ДатаДокумента,
	|	ДанныеДокумента.Организация								КАК Организация,
	|	ДанныеДокумента.Организация.Префикс						КАК Префикс,
	|	ТоварыДокумента.Склад									КАК Склад,
	|	ЕСТЬNULL(Склады.СкладОтветственногоХранения, ЛОЖЬ)		КАК СкладОтветственногоХранения,
	|	ДанныеДокумента.Организация = Склады.Поклажедержатель	КАК ОрганизацияПоклажедержатель,
	|	Склады.ИсточникИнформацииОЦенахДляПечати				КАК ИсточникИнформацииОЦенахДляПечати,
	|	Склады.УчетныйВидЦены									КАК ВидЦены,
	|	Склады.УчетныйВидЦены.ВалютаЦены						КАК ВалютаЦены,
	|	РасчетСебестоимостиТоваровОрганизации.Ссылка.ПредварительныйРасчет	КАК ПредварительныйРасчет,
	|	ДанныеДокумента.Дата									КАК ДатаПолученияСебестоимости
	|ПОМЕСТИТЬ ДанныеДокумента
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ТоварыДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК ДанныеДокумента
	|		ПО ТоварыДокумента.Ссылка = ДанныеДокумента.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК РасчетСебестоимостиТоваровОрганизации
	|		ПО РасчетСебестоимостиТоваровОрганизации.Ссылка.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ)
	|			И РасчетСебестоимостиТоваровОрганизации.Ссылка.Проведен
	|			И ДанныеДокумента.Организация = РасчетСебестоимостиТоваровОрганизации.Организация
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО ТоварыДокумента.Склад = Склады.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Проведен
	|	И ДанныеДокумента.Ссылка В(&МассивДокументов)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка								КАК Ссылка,
	|	ДанныеДокумента.Номер								КАК Номер,
	|	ДанныеДокумента.Дата								КАК Дата,
	|	ДанныеДокумента.ДатаДокумента						КАК ДатаДокумента,
	|	ДанныеДокумента.Организация							КАК Организация,
	|	ДанныеДокумента.Префикс								КАК Префикс,
	|	ДанныеДокумента.Склад								КАК Склад,
	|	ДанныеДокумента.ИсточникИнформацииОЦенахДляПечати	КАК ИсточникИнформацииОЦенахДляПечати,
	|	ДанныеДокумента.ВидЦены								КАК ВидЦены,
	|	ДанныеДокумента.ВалютаЦены							КАК ВалютаЦены,
	|	ДанныеДокумента.ПредварительныйРасчет				КАК ПредварительныйРасчет,
	|	ДанныеДокумента.ДатаПолученияСебестоимости			КАК ДатаПолученияСебестоимости
	|ПОМЕСТИТЬ ВтШапка
	|ИЗ
	|	ДанныеДокумента КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.СкладОтветственногоХранения
	|	И НЕ ДанныеДокумента.ОрганизацияПоклажедержатель
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Склад КАК Склад,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.Серия КАК Серия,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения1 КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения1 КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения1 КАК ВидУпаковки,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Товары.Количество КАК Количество,
	|	КОНЕЦПЕРИОДА(Товары.Ссылка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	Шапка.ВидЦены КАК ВидЦены,
	|	Шапка.ВалютаЦены КАК ВалютаЦены
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО Товары.Склад = Шапка.Склад
	|			И Товары.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	Товары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоВидуЦен)
	|		ИЛИ (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|			И Шапка.ПредварительныйРасчет ЕСТЬ NULL))
	|	И Товары.ВариантОтражения В(
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УвеличитьРеализациюУменьшитьСкладскиеОстатки))
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК Ссылка,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	Шапка.Организация КАК Организация,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория КАК Склад,
	|	ВидыЗапасов.Упаковка КАК Упаковка,
	|	ВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения2 КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения2 КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения2 КАК ВидУпаковки,
	|	ВидыЗапасов.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ВидыЗапасов.Количество КАК Количество,
	|	ВидыЗапасов.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	КОНЕЦПЕРИОДА(ВидыЗапасов.Ссылка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория.УчетныйВидЦены КАК ВидЦены,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория.УчетныйВидЦены.ВалютаЦены КАК ВалютаЦены,
	|	Шапка.ПредварительныйРасчет КАК ПредварительныйРасчет
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК ВидыЗапасов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория = Шапка.Склад
	|			И ВидыЗапасов.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|;
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование", "Товары.Упаковка", "Товары.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код", "Товары.Упаковка", "Товары.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование", "ВидыЗапасов.Упаковка", "ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код", "ВидыЗапасов.Упаковка", "ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура"));
	
	СкладыСервер.ДополнитьТекстЗапросаДляПечатныхФормМХ1Х3(Запрос);
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	Документы.КорректировкаРеализации.УстановитьПараметрыВтВидыЗапасов(
		Запрос,
		Ложь, // ПересчитыватьВВалютуРегл
		Ложь,); // ВключаяДоКорректировки
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	РезультатПоШапке			= МассивРезультатов[МассивРезультатов.ВГраница() - 3];
	РезультатПоСкладам			= МассивРезультатов[МассивРезультатов.ВГраница() - 2];
	РезультатПоТабличнойЧасти	= МассивРезультатов[МассивРезультатов.ВГраница() - 1];
	РезультатПоОшибкам			= МассивРезультатов[МассивРезультатов.ВГраница()];
	
	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("РезультатПоШапке",			РезультатПоШапке);
	СтруктураДанныхДляПечати.Вставить("РезультатПоСкладам",			РезультатПоСкладам);
	СтруктураДанныхДляПечати.Вставить("РезультатПоТабличнойЧасти",	РезультатПоТабличнойЧасти);
	СтруктураДанныхДляПечати.Вставить("РезультатПоОшибкам",			РезультатПоОшибкам);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Функция получает данные для формирования печатной формы Акт на передачу прав
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.КорректировкаРеализации - Массив ссылок на документы,
//	по которым необходимо получить данные.
//
// Возвращаемое значение:
// 	Структура:
// 		* РезультатПоШапке - РезультатЗапроса
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса
//
Функция ПолучитьДанныеДляПечатнойФормыАктНаПередачуПрав(ПараметрыПечати, МассивОбъектов) Экспорт
	Если ПараметрыПечати.Свойство("ВыводитьГТД") Тогда
		ПараметрыПечати.Удалить("ВыводитьГТД");
	КонецЕсли;
	Если ПараметрыПечати.Свойство("ВыводитьУслуги") Тогда
		ПараметрыПечати.ВыводитьУслуги = Истина;
	Иначе
		ПараметрыПечати.Вставить("ВыводитьУслуги", Истина);
	КонецЕсли;
	Возврат ПолучитьДанныеДляПечатнойФормыТОРГ12(ПараметрыПечати, МассивОбъектов);
КонецФункции

// Формирует временную таблицу, содержащую табличную часть по таблице данных документов.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц, содержащий таблицу ТаблицаДанныхДокументов с полями:
//		Ссылка,
//		Валюта.
//
//	ПараметрыЗаполнения - Структура - структура, возвращаемая функцией ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров.
//
Процедура ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения = Неопределено) Экспорт
	
	Если ПараметрыЗаполнения = Неопределено Тогда
		ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	КонецЕсли;
	
	Если МенеджерВременныхТаблиц.Таблицы.Найти("СчетаФактурыОснования") = Неопределено Тогда
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДокументов.Ссылка КАК ДокументОснование,
		|	ЛОЖЬ КАК Корректировочный
		|
		|ПОМЕСТИТЬ СчетаФактурыОснования
		|ИЗ
		|	ТаблицаДанныхДокументов КАК ДанныеДокументов
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка";
		Запрос.Выполнить();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета",     Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ПересчитыватьВВалютуРегл",       ПараметрыЗаполнения.ПересчитыватьВВалютуРегл);
	Запрос.УстановитьПараметр("ВключаяНомераГТД",               ПараметрыЗаполнения.ВключаяНомераГТД);
	Запрос.УстановитьПараметр("ПустаяГТД",                      Справочники.НомераГТД.ПустаяСсылка());
	УчетПрослеживаемыхТоваровЛокализация.УстановитьПараметрыИспользованияУчетаПрослеживаемыхТоваров(Запрос);
	
	// Актуализировать расчеты для получения сумм по товарам документа-основания (например, реализации товаров и услуг).
	Если ПараметрыЗаполнения.ПересчитыватьВВалютуРегл И ПараметрыЗаполнения.АктуализироватьРасчеты Тогда
		Если НЕ ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
			Запрос.Текст = "
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
			|ИЗ
			|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
			|
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|		ТаблицаДанныхДокументов КАК ДанныеДокументов
			|	ПО
			|		РасчетыСКлиентами.Регистратор = ДанныеДокументов.Ссылка
			|
			|ГДЕ
			|	ДанныеДокументов.Валюта <> ДанныеДокументов.Ссылка.Организация.ВалютаРегламентированногоУчета
			|	И РасчетыСКлиентами.Активность
			|";
			ТаблицаАналитик = Запрос.Выполнить().Выгрузить();
			МассивАналитикУчетаПоПартнерам = ТаблицаАналитик.ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
			
			Если МассивАналитикУчетаПоПартнерам.Количество() > 0 Тогда 
				ОкончаниеПериодаРасчета = КонецМесяца(ВзаиморасчетыСервер.ПолучитьМаксимальнуюДатуВКоллекцииДокументов(МенеджерВременныхТаблиц)) + 1;
				АналитикиРасчета = РаспределениеВзаиморасчетовВызовСервера.АналитикиРасчета();
				АналитикиРасчета.АналитикиУчетаПоПартнерам = МассивАналитикУчетаПоПартнерам;
				Попытка
					РаспределениеВзаиморасчетовВызовСервера.РаспределитьВсеРасчетыСКлиентами(ОкончаниеПериодаРасчета, АналитикиРасчета);
				Исключение
					ТекстСообщения = НСтр("ru ='Печатная форма сформирована по неактуальным данным.
					|Необходимо актуализировать взаиморасчеты вручную и переформировать печатную форму.'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				КонецПопытки;
			КонецЕсли;
			
		Иначе
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДанныеДокументов.Ссылка КАК Ссылка
			|ИЗ
			|	ТаблицаДанныхДокументов КАК ДанныеДокументов
			|ГДЕ 
			|	ДанныеДокументов.Валюта <> ДанныеДокументов.Ссылка.Организация.ВалютаРегламентированногоУчета
			|	ИЛИ ДанныеДокументов.Валюта <> &ВалютаУправленческогоУчета";
			МассивДокументов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
			РегистрыСведений.СуммыДокументовВВалютахУчета.РассчитатьСуммыДокументовВВалютахУчета(МассивДокументов);
			
		КонецЕсли;
	КонецЕсли;
	
	Документы.КорректировкаРеализации.УстановитьПараметрыВтВидыЗапасов(
		Запрос,
		ПараметрыЗаполнения.ПересчитыватьВВалютуРегл,
		ПараметрыЗаполнения.ВключаяДоКорректировки);
	
	Запрос.Текст = Документы.КорректировкаРеализации.ТекстЗапросаВтВидыЗапасов() + "
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Основной,
	|	СвязанныеДокументы.Подчиненный КАК Предыдущий
	|ИЗ
	|	ВтСвязанныеДокументы КАК СвязанныеДокументы
	|
	|ГДЕ
	|	СвязанныеДокументы.Подчиненный <> СвязанныеДокументы.Основной
	|
	|УПОРЯДОЧИТЬ ПО
	|	СвязанныеДокументы.МоментВремени УБЫВ
	|
	|ИТОГИ ПО
	|	СвязанныеДокументы.Основной
	|";
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаПредыдущихДокументов = Новый ТаблицаЗначений;
	ТаблицаПредыдущихДокументов.Колонки.Добавить("Основной",   Новый ОписаниеТипов("ДокументСсылка.КорректировкаРеализации"));
	ТаблицаПредыдущихДокументов.Колонки.Добавить("Предыдущий", Новый ОписаниеТипов("
		|ДокументСсылка.КорректировкаРеализации,
		|ДокументСсылка.ПервичныйДокумент,
		|ДокументСсылка.РеализацияТоваровУслуг,
		|ДокументСсылка.АктВыполненныхРабот,
		|ДокументСсылка.РеализацияУслугПрочихАктивов"));
	
	ВыборкаПоСсылкам = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоСсылкам.Следующий() Цикл
		Выборка = ВыборкаПоСсылкам.Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(ТаблицаПредыдущихДокументов.Добавить(), Выборка);
		КонецЕсли;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ПредыдущиеДокументы", ТаблицаПредыдущихДокументов);
	
	Запрос.Текст = "
	|////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредыдущиеДокументы.Основной   КАК Основной,
	|	ПредыдущиеДокументы.Предыдущий КАК Предыдущий
	|
	|ПОМЕСТИТЬ ПредыдущиеДокументы
	|ИЗ
	|	&ПредыдущиеДокументы КАК ПредыдущиеДокументы
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка                КАК Ссылка,
	|	ТаблицаТоваров.Содержание            КАК Содержание,
	|	ТаблицаТоваров.Номенклатура          КАК Номенклатура,
	|	ТаблицаТоваров.НоменклатураПартнера  КАК НоменклатураПартнера,
	|	ТаблицаТоваров.Характеристика        КАК Характеристика,
	|	ТаблицаТоваров.Упаковка              КАК Упаковка,
	|	МАКСИМУМ(ТаблицаТоваров.НомерСтроки) КАК НомерСтроки
	|
	|ПОМЕСТИТЬ СтрокиТоваров
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ТаблицаТоваров
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО
	|		ТаблицаТоваров.Ссылка = ДанныеДокументов.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.НоменклатураПартнера,
	|	ТаблицаТоваров.Содержание,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.Упаковка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.Упаковка
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	Товары.НоменклатураНабора КАК НоменклатураНабора,
	|	Товары.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	"""" КАК Содержание,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.НоменклатураПартнера  КАК НоменклатураПартнера,
	|	Товары.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	Товары.Количество КАК Количество,
	|	Товары.Количество КАК КоличествоУпаковок,
	|	0 КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Товары.СуммаСНДС - Товары.СуммаНДС) КАК СуммаБезНДС,
	|	
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Товары.СуммаНДС) КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ЛОЖЬ КАК ДоКорректировки
	|ПОМЕСТИТЬ ВсеУслуги
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО Товары.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыРегл
	|		ПО Товары.Ссылка = СуммыРегл.Регистратор
	|			И Товары.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	Товары.Номенклатура.ТипНоменклатуры НЕ В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	ИЛИ Товары.Номенклатура.ТипНоменклатуры ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	Товары.НоменклатураНабора КАК НоменклатураНабора,
	|	Товары.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	Товары.Содержание КАК Содержание,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.НоменклатураПартнера  КАК НоменклатураПартнера,
	|	Товары.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	Товары.Количество КАК Количество,
	|	Товары.Количество КАК КоличествоУпаковок,
	|	0 КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Товары.СуммаСНДС - Товары.СуммаНДС) КАК СуммаБезНДС,
	|	
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Товары.СуммаНДС) КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ЛОЖЬ КАК ДоКорректировки
	|ИЗ
	|	Документ.АктВыполненныхРабот.Услуги КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО Товары.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыРегл
	|		ПО Товары.Ссылка = СуммыРегл.Регистратор
	|			И Товары.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	Товары.Содержание КАК Содержание,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка) КАК НоменклатураПартнера,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	Товары.Количество КАК Количество,
	|	Товары.Количество КАК КоличествоУпаковок,
	|	0 КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Товары.СуммаСНДС - Товары.СуммаНДС) КАК СуммаБезНДС,
	|	
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Товары.СуммаНДС) КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ЛОЖЬ КАК ДоКорректировки
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Доходы КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО Товары.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыРегл
	|		ПО Товары.Ссылка = СуммыРегл.Регистратор
	|			И Товары.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|//предыдущие корректировки реализации
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	Товары.НоменклатураНабора КАК НоменклатураНабора,
	|	Товары.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	Товары.Содержание КАК Содержание,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.НоменклатураПартнера КАК НоменклатураПартнера,
	|	Товары.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	Товары.Количество КАК Количество,
	|	Товары.Количество КАК КоличествоУпаковок,
	|	0 КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Товары.СуммаСНДС - Товары.СуммаНДС) КАК СуммаБезНДС,
	|	
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Товары.СуммаНДС) КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ЛОЖЬ КАК ДоКорректировки
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО Товары.Ссылка = СвязанныеДокументы.Подчиненный
	|			И СвязанныеДокументы.Основной <> СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыРегл
	|		ПО Товары.Ссылка = СуммыРегл.Регистратор
	|			И Товары.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	(
	|		Товары.Номенклатура.ТипНоменклатуры НЕ В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ИЛИ Товары.Номенклатура.ТипНоменклатуры ЕСТЬ NULL
	|	)
	|	И Товары.Ссылка.ТипКорректировки = ЗНАЧЕНИЕ(Перечисление.ТипыКорректировки.ОбычнаяКорректировка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|//текущая корректировка реализации
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	Товары.НоменклатураНабора КАК НоменклатураНабора,
	|	Товары.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	Товары.Содержание КАК Содержание,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.НоменклатураПартнера КАК НоменклатураПартнера,
	|	Товары.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	Товары.Количество КАК Количество,
	|	Товары.Количество КАК КоличествоУпаковок,
	|	0 КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Товары.СуммаСНДС - Товары.СуммаНДС) КАК СуммаБезНДС,
	|	
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Товары.СуммаНДС) КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ЛОЖЬ КАК ДоКорректировки
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО Товары.Ссылка = СвязанныеДокументы.Основной
	|			И СвязанныеДокументы.Основной = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыРегл
	|		ПО Товары.Ссылка = СуммыРегл.Регистратор
	|			И Товары.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	(
	|		Товары.Номенклатура.ТипНоменклатуры НЕ В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ИЛИ Товары.Номенклатура.ТипНоменклатуры ЕСТЬ NULL
	|	)
	|	И Товары.Ссылка.ТипКорректировки = ЗНАЧЕНИЕ(Перечисление.ТипыКорректировки.ОбычнаяКорректировка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Состояние "после корректировки", по корректировке реализации (по первичному документу).
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	Расхождения.НоменклатураНабора КАК НоменклатураНабора,
	|	Расхождения.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	Расхождения.Содержание КАК Содержание,
	|	Расхождения.Номенклатура КАК Номенклатура,
	|	Расхождения.НоменклатураПартнера  КАК НоменклатураПартнера,
	|	Расхождения.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	ЕСТЬNULL(Товары.Количество, 0) КАК Количество,
	|	ЕСТЬNULL(Товары.Количество, 0) КАК КоличествоУпаковок,
	|	0 КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, ЕСТЬNULL(Товары.Сумма, 0)) КАК СуммаБезНДС,
	|	
	|	Расхождения.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, ЕСТЬNULL(Товары.СуммаНДС, 0)) КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ЛОЖЬ КАК ДоКорректировки
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК Расхождения
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументыКорректировкаПоПервичнымДокументам КАК СвязанныеДокументы
	|		ПО Расхождения.Ссылка = СвязанныеДокументы.Основной
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Товары КАК Товары
	|		ПО Товары.Ссылка = Расхождения.Ссылка
	|		И Товары.Номенклатура = Расхождения.Номенклатура
	|		И Товары.Характеристика = Расхождения.Характеристика
	|		И Товары.Серия = Расхождения.Серия
	|		И Товары.Назначение = Расхождения.Назначение
	|		И Товары.Содержание = Расхождения.Содержание
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТСуммыДокументовВВалютахУчета КАК СуммыРегл
	|		ПО Товары.Ссылка = СуммыРегл.Ссылка
	|			И Товары.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	(
	|		Расхождения.Номенклатура.ТипНоменклатуры НЕ В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ИЛИ Расхождения.Номенклатура.ТипНоменклатуры ЕСТЬ NULL
	|	)
	|	И Расхождения.Ссылка.ТипКорректировки В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыКорректировки.КорректировкаДоВводаОстатковТоваровИУслуг),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыКорректировки.КорректировкаДоВводаОстатковУслугИПрочихАктивов)
	|	)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Исходная продажа по корректировке реализации (по первичному документу).
	// Выводится если &ВключаяДоКорректировки
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	Расхождения.НоменклатураНабора КАК НоменклатураНабора,
	|	Расхождения.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	Расхождения.Содержание КАК Содержание,
	|	Расхождения.Номенклатура КАК Номенклатура,
	|	Расхождения.НоменклатураПартнера  КАК НоменклатураПартнера,
	|	Расхождения.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	ЕСТЬNULL(Товары.Количество, 0) - Расхождения.Количество КАК Количество,
	|	ЕСТЬNULL(Товары.Количество, 0) - Расхождения.Количество КАК КоличествоУпаковок,
	|	0 КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл - СуммыРеглРасхождения.СуммаБезНДСРегл,
	|		ЕСТЬNULL(Товары.Сумма, 0) - Расхождения.Сумма) КАК СуммаБезНДС,
	|	
	|	Расхождения.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл - СуммыРеглРасхождения.СуммаНДСРегл,
	|		ЕСТЬNULL(Товары.СуммаНДС, 0) - Расхождения.СуммаНДС) КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ИСТИНА КАК ДоКорректировки
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК Расхождения
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументыКорректировкаПоПервичнымДокументам КАК СвязанныеДокументы
	|		ПО Расхождения.Ссылка = СвязанныеДокументы.Основной
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Товары КАК Товары
	|		ПО Товары.Ссылка = Расхождения.Ссылка
	|		И Товары.Номенклатура = Расхождения.Номенклатура
	|		И Товары.Характеристика = Расхождения.Характеристика
	|		И Товары.Серия = Расхождения.Серия
	|		И Товары.Назначение = Расхождения.Назначение
	|		И Товары.Содержание = Расхождения.Содержание
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТСуммыДокументовВВалютахУчета КАК СуммыРегл
	|		ПО Товары.Ссылка = СуммыРегл.Ссылка
	|			И Товары.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И &ПересчитыватьВВалютуРегл
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыРеглРасхождения
	|		ПО Расхождения.Ссылка = СуммыРеглРасхождения.Регистратор
	|			И Расхождения.ИдентификаторСтроки = СуммыРеглРасхождения.ИдентификаторСтроки
	|			И СуммыРеглРасхождения.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	&ВключаяДоКорректировки
	|	И (
	|		Товары.Номенклатура.ТипНоменклатуры НЕ В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ИЛИ Товары.Номенклатура.ТипНоменклатуры ЕСТЬ NULL
	|		)
	|	И Товары.Ссылка.ТипКорректировки В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыКорректировки.КорректировкаДоВводаОстатковТоваровИУслуг),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыКорректировки.КорректировкаДоВводаОстатковУслугИПрочихАктивов)
	|	)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Состояние "после корректировки", по корректировке реализации (по первичному документу), сценарий "нет расхождений".
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	Товары.НоменклатураНабора КАК НоменклатураНабора,
	|	Товары.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	Товары.Содержание КАК Содержание,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.НоменклатураПартнера  КАК НоменклатураПартнера,
	|	Товары.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	Товары.Количество КАК Количество,
	|	Товары.Количество КАК КоличествоУпаковок,
	|	0 КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, ЕСТЬNULL(Товары.Сумма, 0)) КАК СуммаБезНДС,
	|	
	|	Расхождения.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, ЕСТЬNULL(Товары.СуммаНДС, 0)) КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ЛОЖЬ КАК ДоКорректировки
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументыКорректировкаПоПервичнымДокументам КАК СвязанныеДокументы
	|		ПО Товары.Ссылка = СвязанныеДокументы.Основной
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Расхождения КАК Расхождения
	|		ПО Товары.Ссылка = Расхождения.Ссылка
	|		И Товары.Номенклатура = Расхождения.Номенклатура
	|		И Товары.Характеристика = Расхождения.Характеристика
	|		И Товары.Серия = Расхождения.Серия
	|		И Товары.Назначение = Расхождения.Назначение
	|		И Товары.Содержание = Расхождения.Содержание
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТСуммыДокументовВВалютахУчета КАК СуммыРегл
	|		ПО Товары.Ссылка = СуммыРегл.Ссылка
	|			И Товары.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	(
	|		Товары.Номенклатура.ТипНоменклатуры НЕ В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ИЛИ Товары.Номенклатура.ТипНоменклатуры ЕСТЬ NULL
	|	)
	|	И Товары.Ссылка.ТипКорректировки В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыКорректировки.КорректировкаДоВводаОстатковТоваровИУслуг),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыКорректировки.КорректировкаДоВводаОстатковУслугИПрочихАктивов)
	|	)
	|	И Расхождения.Номенклатура ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Исходная продажа по корректировке реализации (по первичному документу), сценарий "нет расхождений".
	// Выводится если &ВключаяДоКорректировки
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	Товары.НоменклатураНабора КАК НоменклатураНабора,
	|	Товары.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	Товары.Содержание КАК Содержание,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.НоменклатураПартнера  КАК НоменклатураПартнера,
	|	Товары.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	Товары.Количество КАК Количество,
	|	Товары.Количество КАК КоличествоУпаковок,
	|	0 КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, ЕСТЬNULL(Товары.Сумма, 0)) КАК СуммаБезНДС,
	|	
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, ЕСТЬNULL(Товары.СуммаНДС, 0)) КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ИСТИНА КАК ДоКорректировки
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументыКорректировкаПоПервичнымДокументам КАК СвязанныеДокументы
	|		ПО Товары.Ссылка = СвязанныеДокументы.Основной
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Расхождения КАК Расхождения
	|		ПО Товары.Ссылка = Расхождения.Ссылка
	|		И Товары.Номенклатура = Расхождения.Номенклатура
	|		И Товары.Характеристика = Расхождения.Характеристика
	|		И Товары.Серия = Расхождения.Серия
	|		И Товары.Назначение = Расхождения.Назначение
	|		И Товары.Содержание = Расхождения.Содержание
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТСуммыДокументовВВалютахУчета КАК СуммыРегл
	|		ПО Товары.Ссылка = СуммыРегл.Ссылка
	|			И Товары.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	&ВключаяДоКорректировки
	|	И (
	|		Товары.Номенклатура.ТипНоменклатуры НЕ В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ИЛИ Товары.Номенклатура.ТипНоменклатуры ЕСТЬ NULL
	|	)
	|	И Товары.Ссылка.ТипКорректировки В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыКорректировки.КорректировкаДоВводаОстатковТоваровИУслуг),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыКорректировки.КорректировкаДоВводаОстатковУслугИПрочихАктивов)
	|	)
	|	И Расхождения.Номенклатура ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Исходная продажа по документу реализации.
	|// Выводится если &ВключаяДоКорректировки
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	Товары.НоменклатураНабора КАК НоменклатураНабора,
	|	Товары.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	"""" КАК Содержание,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.НоменклатураПартнера КАК НоменклатураПартнера,
	|	Товары.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	Товары.Количество КАК Количество,
	|	Товары.Количество КАК КоличествоУпаковок,
	|	0 КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Товары.СуммаСНДС - Товары.СуммаНДС) КАК СуммаБезНДС,
	|	
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Товары.СуммаНДС) КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ИСТИНА КАК ДоКорректировки
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО Товары.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыРегл
	|		ПО Товары.Ссылка = СуммыРегл.Регистратор
	|			И Товары.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	&ВключаяДоКорректировки
	|	И (Товары.Номенклатура.ТипНоменклатуры НЕ В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	) ИЛИ Товары.Номенклатура.ТипНоменклатуры ЕСТЬ NULL)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Исходная продажа по акту выполненных работ.
	|// Выводится если &ВключаяДоКорректировки
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	Услуги.НоменклатураНабора КАК НоменклатураНабора,
	|	Услуги.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	Услуги.Содержание КАК Содержание,
	|	Услуги.Номенклатура КАК Номенклатура,
	|	Услуги.НоменклатураПартнера КАК НоменклатураПартнера,
	|	Услуги.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	Услуги.Количество КАК Количество,
	|	Услуги.Количество КАК КоличествоУпаковок,
	|	0 КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Услуги.СуммаСНДС - Услуги.СуммаНДС) КАК СуммаБезНДС,
	|	
	|	Услуги.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Услуги.СуммаНДС) КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ИСТИНА КАК ДоКорректировки
	|ИЗ
	|	Документ.АктВыполненныхРабот.Услуги КАК Услуги
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО Услуги.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыРегл
	|		ПО Услуги.Ссылка = СуммыРегл.Регистратор
	|			И Услуги.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	&ВключаяДоКорректировки
	|	И (Услуги.Номенклатура.ТипНоменклатуры НЕ В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	) ИЛИ Услуги.Номенклатура.ТипНоменклатуры ЕСТЬ NULL)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Исходная продажа по реализации услуг и прочих активов.
	|// Выводится если &ВключаяДоКорректировки
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	Доходы.Содержание КАК Содержание,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка) КАК НоменклатураПартнера,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	Доходы.Количество КАК Количество,
	|	Доходы.Количество КАК КоличествоУпаковок,
	|	0 КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Доходы.СуммаСНДС - Доходы.СуммаНДС) КАК СуммаБезНДС,
	|	
	|	Доходы.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Доходы.СуммаНДС) КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ИСТИНА КАК ДоКорректировки
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Доходы КАК Доходы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО Доходы.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыРегл
	|		ПО Доходы.Ссылка = СуммыРегл.Регистратор
	|			И Доходы.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	&ВключаяДоКорректировки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Предыдущие корректировки реализации, которые нужно показать вместе с исходной продажей
	|//Выводится если &ВключаяДоКорректировки
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	Товары.НоменклатураНабора КАК НоменклатураНабора,
	|	Товары.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	Товары.Содержание КАК Содержание,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.НоменклатураПартнера КАК НоменклатураПартнера,
	|	Товары.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	Товары.Количество КАК Количество,
	|	Товары.Количество КАК КоличествоУпаковок,
	|	0 КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Товары.СуммаСНДС - Товары.СуммаНДС) КАК СуммаБезНДС,
	|	
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Товары.СуммаНДС) КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ИСТИНА КАК ДоКорректировки
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО Товары.Ссылка = СвязанныеДокументы.Подчиненный
	|			И СвязанныеДокументы.Основной <> СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыРегл
	|		ПО Товары.Ссылка = СуммыРегл.Регистратор
	|			И Товары.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	&ВключаяДоКорректировки
	|	И (Товары.Номенклатура.ТипНоменклатуры НЕ В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	ИЛИ Товары.Номенклатура.ТипНоменклатуры ЕСТЬ NULL)
	|	И Товары.Ссылка.ТипКорректировки = ЗНАЧЕНИЕ(Перечисление.ТипыКорректировки.ОбычнаяКорректировка)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                 КАК Ссылка,
	|	ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.Ссылка, ЗНАЧЕНИЕ(Справочник.ВариантыКомплектацииНоменклатуры.ПустаяСсылка)) КАК ВариантКомплектацииНоменклатуры,
	|	ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.ВариантПредставленияНабораВПечатныхФормах, ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ПустаяСсылка)) КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.ВариантРасчетаЦеныНабора, ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ПустаяСсылка)) КАК ВариантРасчетаЦеныНабора,
	|	ЕСТЬNULL(ТаблицаДокумента.АналитикаУчетаНаборов.НоменклатураНабора, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))   КАК НоменклатураНабора,
	|	ЕСТЬNULL(ТаблицаДокумента.АналитикаУчетаНаборов.ХарактеристикаНабора, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК ХарактеристикаНабора,
	|	ЛОЖЬ ЭтоКомплектующие,
	|	ЛОЖЬ КАК ЭтоНабор,
	|	0 КАК НомерСтрокиНаборы,
	|	ЛОЖЬ КАК ПолныйНабор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ТОГДА
	|			ТаблицаДокумента.ВернутьМногооборотнуюТару
	|		ИНАЧЕ
	|		 	ЛОЖЬ
	|	КОНЕЦ КАК ВернутьМногооборотнуюТару,	 	
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999)              КАК НомерСтроки,
	|	""""                                                    КАК Содержание,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(СтрокиТоваров.НоменклатураПартнера, ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка)) КАК НоменклатураПартнера,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Серия 		КАК Серия,
	|	ТаблицаДокумента.Упаковка                               КАК Упаковка,
	|	
	|	ВЫБОР КОГДА &ВключаяНомераГТД ТОГДА
	|		ТаблицаДокумента.НомерГТД
	|	ИНАЧЕ
	|		&ПустаяГТД
	|	КОНЕЦ КАК НомерГТД,
	|	
	|	ВЫБОР КОГДА СУММА(ТаблицаДокумента.Количество) > 0 ТОГДА
	|		СУММА(ТаблицаДокумента.Количество)
	|	ИНАЧЕ
	|		-СУММА(ТаблицаДокумента.Количество)
	|	КОНЕЦ КАК Количество,
	|	
	|	ВЫБОР КОГДА СУММА(ТаблицаДокумента.КоличествоУпаковок) > 0 ТОГДА
	|		СУММА(ТаблицаДокумента.КоличествоУпаковок)
	|	ИНАЧЕ
	|		-СУММА(ТаблицаДокумента.КоличествоУпаковок)
	|	КОНЕЦ КАК КоличествоУпаковок,
	|	
	|	ВЫБОР
	|		КОГДА НЕ &ВключаяНомераГТД
	|			ТОГДА 0
	|		КОГДА СУММА(ТаблицаДокумента.КоличествоПоРНПТ) > 0
	|			ТОГДА СУММА(ТаблицаДокумента.КоличествоПоРНПТ)
	|		ИНАЧЕ -СУММА(ТаблицаДокумента.КоличествоПоРНПТ)
	|	КОНЕЦ КАК КоличествоПоРНПТ,
	|
	|	ВЫБОР КОГДА СУММА(ТаблицаДокумента.СуммаБезНДС) > 0 ТОГДА
	|		СУММА(ТаблицаДокумента.СуммаБезНДС)
	|	ИНАЧЕ
	|		-СУММА(ТаблицаДокумента.СуммаБезНДС)
	|	КОНЕЦ КАК СуммаБезНДС,
	|	
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.КодТНВЭД КАК КодТНВЭД,
	|	
	|	ВЫБОР КОГДА СУММА(ТаблицаДокумента.СуммаНДС) > 0 ТОГДА
	|		СУММА(ТаблицаДокумента.СуммаНДС)
	|	ИНАЧЕ
	|		-СУММА(ТаблицаДокумента.СуммаНДС)
	|	КОНЕЦ КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|		 	ЛОЖЬ
	|	КОНЕЦ КАК ЭтоТовар,	 			
	|	ВЫБОР
	|		КОГДА
	|			НЕ ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			ИЛИ НЕ ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ИЛИ НЕ ТаблицаДокумента.ВернутьМногооборотнуюТару
	|		ТОГДА
	|			ЛОЖЬ
	|		ИНАЧЕ
	|			ИСТИНА
	|	КОНЕЦ КАК ЭтоНеВозвратнаяТара,
	|	ТаблицаДокумента.ДоКорректировки КАК ДоКорректировки
	|
	|ПОМЕСТИТЬ КорректировкаРеализацииТаблицаТоваров
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаРеализации КАК ДанныеКорректировки
	|	ПО
	|		ДанныеКорректировки.Ссылка = ТаблицаДокумента.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		СтрокиТоваров КАК СтрокиТоваров
	|	ПО
	|		ТаблицаДокумента.Ссылка           = СтрокиТоваров.Ссылка
	|		И ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура = СтрокиТоваров.Номенклатура
	|		И СтрокиТоваров.Содержание        = """"
	|		И ТаблицаДокумента.АналитикаУчетаНоменклатуры.Характеристика = СтрокиТоваров.Характеристика
	|		И ТаблицаДокумента.Упаковка       = СтрокиТоваров.Упаковка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
	|		ПО ВариантыКомплектацииНоменклатуры.Владелец = ТаблицаДокумента.АналитикаУчетаНаборов.НоменклатураНабора
	|		И ВариантыКомплектацииНоменклатуры.Характеристика = ТаблицаДокумента.АналитикаУчетаНаборов.ХарактеристикаНабора
	|		И ВариантыКомплектацииНоменклатуры.Основной
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		СчетаФактурыОснования КАК СчетаФактуры
	|	ПО
	|		ТаблицаДокумента.Ссылка = СчетаФактуры.ДокументОснование
	|ГДЕ
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	ИЛИ (ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			И ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ПрослеживаемыйТовар
	|			И &ИспользоватьУчетПрослеживаемыхИмпортныхТоваров)
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Ссылка,
	|	ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.Ссылка, ЗНАЧЕНИЕ(Справочник.ВариантыКомплектацииНоменклатуры.ПустаяСсылка)),
	|	ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.ВариантПредставленияНабораВПечатныхФормах, ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ПустаяСсылка)),
	|	ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.ВариантРасчетаЦеныНабора, ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ПустаяСсылка)),
	|	ЕСТЬNULL(ТаблицаДокумента.АналитикаУчетаНаборов.НоменклатураНабора, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)),
	|	ЕСТЬNULL(ТаблицаДокумента.АналитикаУчетаНаборов.ХарактеристикаНабора, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)),
	|	ТаблицаДокумента.ВернутьМногооборотнуюТару,
	|	СтрокиТоваров.НомерСтроки,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура,
	|	ЕСТЬNULL(СтрокиТоваров.НоменклатураПартнера, ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка)),
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Характеристика,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Серия,
	|	ТаблицаДокумента.Упаковка,
	|	СчетаФактуры.Корректировочный,
	|	
	|	ВЫБОР КОГДА &ВключаяНомераГТД ТОГДА
	|		ТаблицаДокумента.НомерГТД
	|	ИНАЧЕ
	|		&ПустаяГТД
	|	КОНЕЦ,
	|	
	|	ТаблицаДокумента.СтавкаНДС,
	|	ТаблицаДокумента.КодТНВЭД,
	|	ТаблицаДокумента.ДоКорректировки,
	|	ДанныеКорректировки.ХозяйственнаяОперация,
	|	ДанныеКорректировки.ТипКорректировки
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаДокумента.Количество) <> 0
	|	И (СУММА(ТаблицаДокумента.Количество) > 0 
	|		ИЛИ ДанныеКорректировки.ХозяйственнаяОперация В(
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПерепоставленногоТовара),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара))
	|		ИЛИ ДанныеКорректировки.ТипКорректировки В(
	|				ЗНАЧЕНИЕ(Перечисление.ТипыКорректировки.КорректировкаДоВводаОстатковТоваровИУслуг),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыКорректировки.КорректировкаДоВводаОстатковУслугИПрочихАктивов))
	|		)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	| 
	|ВЫБРАТЬ
	|	Услуги.Ссылка КАК Ссылка,
	|	ВариантыКомплектацииНоменклатуры.Ссылка КАК ВариантКомплектацииНоменклатуры,
	|	ВариантыКомплектацииНоменклатуры.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ВариантыКомплектацииНоменклатуры.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	Услуги.НоменклатураНабора КАК НоменклатураНабора,
	|	Услуги.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ЛОЖЬ КАК ЭтоКомплектующие,
	|	ЛОЖЬ КАК ЭтоНабор,
	|	0 КАК НомерСтрокиНаборы,
	|	ЛОЖЬ КАК ПолныйНабор,
	|	Услуги.ВернутьМногооборотнуюТару КАК ВернутьМногооборотнуюТару,
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999) КАК НомерСтроки,
	|	Услуги.Содержание КАК Содержание,
	|	Услуги.Номенклатура КАК Номенклатура,
	|	Услуги.НоменклатураПартнера КАК НоменклатураПартнера,
	|	Услуги.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|	Услуги.Упаковка КАК Упаковка,
	|	Услуги.НомерГТД КАК НомерГТД,
	|	
	|	ВЫБОР КОГДА СУММА(Услуги.Количество) > 0 ТОГДА
	|		СУММА(Услуги.Количество)
	|	ИНАЧЕ
	|		-СУММА(Услуги.Количество)
	|	КОНЕЦ КАК Количество,
	|	
	|	ВЫБОР КОГДА СУММА(Услуги.КоличествоУпаковок) > 0 ТОГДА
	|		СУММА(Услуги.КоличествоУпаковок)
	|	ИНАЧЕ
	|		-СУММА(Услуги.КоличествоУпаковок)
	|	КОНЕЦ КАК КоличествоУпаковок,
	|	
	|	ВЫБОР КОГДА СУММА(Услуги.КоличествоПоРНПТ) > 0 ТОГДА
	|		СУММА(Услуги.КоличествоПоРНПТ)
	|	ИНАЧЕ
	|		-СУММА(Услуги.КоличествоПоРНПТ)
	|	КОНЕЦ КАК КоличествоПоРНПТ,
	|
	|	ВЫБОР КОГДА СУММА(Услуги.СуммаБезНДС) > 0 ТОГДА
	|		СУММА(Услуги.СуммаБезНДС)
	|	ИНАЧЕ
	|		-СУММА(Услуги.СуммаБезНДС)
	|	КОНЕЦ КАК СуммаБезНДС,
	|
	|	Услуги.СтавкаНДС КАК СтавкаНДС,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка) КАК КодТНВЭД,
	|	
	|	ВЫБОР КОГДА СУММА(Услуги.СуммаНДС) > 0 ТОГДА
	|		СУММА(Услуги.СуммаНДС)
	|	ИНАЧЕ
	|		-СУММА(Услуги.СуммаНДС)
	|	КОНЕЦ КАК СуммаНДС,
	|
	|	Услуги.ЭтоТовар КАК ЭтоТовар,
	|	Услуги.ЭтоНеВозвратнаяТара КАК ЭтоНеВозвратнаяТара,
	|	Услуги.ДоКорректировки КАК ДоКорректировки
	|ИЗ 
	|	ВсеУслуги КАК Услуги
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		СтрокиТоваров КАК СтрокиТоваров
	|	ПО
	|		Услуги.Ссылка           = СтрокиТоваров.Ссылка
	|		И Услуги.Номенклатура   = СтрокиТоваров.Номенклатура
	|		И Услуги.Содержание     = СтрокиТоваров.Содержание
	|		И Услуги.Характеристика = СтрокиТоваров.Характеристика
	|		И Услуги.Упаковка       = СтрокиТоваров.Упаковка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
	|		ПО ВариантыКомплектацииНоменклатуры.Владелец = Услуги.НоменклатураНабора
	|		И ВариантыКомплектацииНоменклатуры.Характеристика = Услуги.ХарактеристикаНабора
	|		И ВариантыКомплектацииНоменклатуры.Основной
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтВидыЗапасов КАК ВидыЗапасов
	|		ПО ВидыЗапасов.Ссылка = Услуги.Ссылка
	|		И ВидыЗапасов.Номенклатура = Услуги.Номенклатура
	|ГДЕ
	|	ВидыЗапасов.Ссылка ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	Услуги.Ссылка,
	|	ВариантыКомплектацииНоменклатуры.Ссылка,
	|	ВариантыКомплектацииНоменклатуры.ВариантПредставленияНабораВПечатныхФормах,
	|	ВариантыКомплектацииНоменклатуры.ВариантРасчетаЦеныНабора,
	|	Услуги.НоменклатураНабора,
	|	Услуги.ХарактеристикаНабора,
	|	Услуги.ВернутьМногооборотнуюТару,
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999),
	|	Услуги.Содержание,
	|	Услуги.Номенклатура,
	|	Услуги.НоменклатураПартнера,
	|	Услуги.Характеристика,
	|	Услуги.Упаковка,
	|	Услуги.НомерГТД,
	|	Услуги.СтавкаНДС,
	|	Услуги.ЭтоТовар,
	|	Услуги.ЭтоНеВозвратнаяТара,
	|	Услуги.ДоКорректировки
	|ИМЕЮЩИЕ
	|	СУММА(Услуги.Количество) <> 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВтСвязанныеДокументы
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СтрокиТоваров
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВтВидыЗапасов
	|";
	
	Если ПараметрыЗаполнения.ОбработатьНастройкиПечатиНаборов Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПОМЕСТИТЬ КорректировкаРеализацииТаблицаТоваров", "ПОМЕСТИТЬ КорректировкаРеализацииТаблицаТоваровПреобразования");
		Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() + "
		|ВЫБРАТЬ
		|	ТаблицаТоваров.Ссылка КАК Ссылка,
		|	ТаблицаТоваров.НоменклатураНабора КАК НоменклатураНабора,
		|	ТаблицаТоваров.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|	ТаблицаТоваров.ДоКорректировки КАК ДоКорректировки,
		|	МАКСИМУМ(ТаблицаТоваров.ЭтоТовар) КАК ЭтоТовар,
		|	МИНИМУМ(ТаблицаТоваров.НомерСтроки) КАК НомерСтроки,
		|	СУММА(ВЫБОР
		|			КОГДА &ЗаполненаЕдиницаИзмеренияВеса
		|				ТОГДА ВЫБОР
		|						КОГДА ТаблицаТоваров.Упаковка.Вес ЕСТЬ NULL
		|							ТОГДА ТаблицаТоваров.Количество
		|						ИНАЧЕ ВЫБОР
		|								КОГДА ТаблицаТоваров.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Вес)
		|									ТОГДА 0
		|								ИНАЧЕ ТаблицаТоваров.КоличествоУпаковок
		|							КОНЕЦ
		|					КОНЕЦ * &ТекстЗапросаВесУпаковки
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК МассаБрутто,
		|	СУММА(ТаблицаТоваров.Количество * &ТекстЗапросаВесНоменклатуры) КАК МассаНетто,
		|	СУММА(ТаблицаТоваров.СуммаБезНДС) КАК СуммаБезНДС,
		|	СУММА(ТаблицаТоваров.СуммаНДС) КАК СуммаНДС
		|ПОМЕСТИТЬ ВременнаяТаблицаНаборыПодготовка
		|ИЗ
		|	КорректировкаРеализацииТаблицаТоваровПреобразования КАК ТаблицаТоваров
		|ГДЕ
		|	ТаблицаТоваров.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаТоваров.Ссылка,
		|	ТаблицаТоваров.НоменклатураНабора,
		|	ТаблицаТоваров.ДоКорректировки,
		|	ТаблицаТоваров.ХарактеристикаНабора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДанныхДокументов.Ссылка КАК Ссылка,
		|	ВариантыКомплектацииНоменклатурыТовары.Ссылка КАК ВариантКомплектацииНоменклатуры,
		|	ВариантыКомплектацииНоменклатурыТовары.Ссылка.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
		|	ВариантыКомплектацииНоменклатурыТовары.Ссылка.ВариантРасчетаЦеныНабора  КАК ВариантРасчетаЦеныНабора,
		|	ВариантыКомплектацииНоменклатурыТовары.Ссылка.Владелец КАК НоменклатураНабора,
		|	ВариантыКомплектацииНоменклатурыТовары.Ссылка.Характеристика КАК ХарактеристикаНабора,
		|	ВариантыКомплектацииНоменклатурыТовары.Номенклатура КАК Номенклатура,
		|	ВариантыКомплектацииНоменклатурыТовары.Характеристика КАК Характеристика,
		|	ВариантыКомплектацииНоменклатурыТовары.Количество КАК Количество
		|ПОМЕСТИТЬ ВременнаяТаблицаНаборыЧастьПервая
		|ИЗ
		|	Справочник.ВариантыКомплектацииНоменклатуры.Товары КАК ВариантыКомплектацииНоменклатурыТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВариантыКомплектацииНоменклатурыТовары.Ссылка В
		|			(ВЫБРАТЬ
		|				КорректировкаРеализацииТаблицаТоваровПреобразования.ВариантКомплектацииНоменклатуры
		|			ИЗ
		|				КорректировкаРеализацииТаблицаТоваровПреобразования КАК КорректировкаРеализацииТаблицаТоваровПреобразования)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Ссылка КАК Ссылка,
		|	Товары.ВариантКомплектацииНоменклатуры КАК ВариантКомплектацииНоменклатуры,
		|	Товары.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
		|	Товары.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
		|	Товары.НоменклатураНабора КАК НоменклатураНабора,
		|	Товары.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.ДоКорректировки КАК ДоКорректировки,
		|	ВЫБОР
		|		КОГДА Товары.ВариантКомплектацииНоменклатуры.НоменклатураОсновногоКомпонента = Товары.Номенклатура
		|				И Товары.ВариантКомплектацииНоменклатуры.ХарактеристикаОсновногоКомпонента = Товары.Характеристика
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ОсновнаяКомплектующая,
		|	Товары.СтавкаНДС КАК СтавкаНДС,
		|	ВЫБОР
		|		КОГДА Товары.ВариантКомплектацииНоменклатуры.НоменклатураОсновногоКомпонента = Товары.Номенклатура
		|				И Товары.ВариантКомплектацииНоменклатуры.ХарактеристикаОсновногоКомпонента = Товары.Характеристика
		|			ТОГДА Товары.НомерГТД
		|		ИНАЧЕ NULL
		|	КОНЕЦ КАК НомерГТД,
		|	ВЫБОР
		|		КОГДА Товары.ВариантКомплектацииНоменклатуры.НоменклатураОсновногоКомпонента = Товары.Номенклатура
		|				И Товары.ВариантКомплектацииНоменклатуры.ХарактеристикаОсновногоКомпонента = Товары.Характеристика
		|			ТОГДА Товары.КодТНВЭД
		|		ИНАЧЕ NULL
		|	КОНЕЦ КАК КодТНВЭД,
		|	Наборы.Количество КАК КоличествоПоУмолчанию,
		|	Товары.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА Товары.ВариантКомплектацииНоменклатуры.НоменклатураОсновногоКомпонента = Товары.Номенклатура
		|				И Товары.ВариантКомплектацииНоменклатуры.ХарактеристикаОсновногоКомпонента = Товары.Характеристика
		|			ТОГДА Товары.КоличествоПоРНПТ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоПоРНПТ
		|ПОМЕСТИТЬ ВременнаяТаблицаНаборыЧастьВторая
		|ИЗ
		|	КорректировкаРеализацииТаблицаТоваровПреобразования КАК Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНаборыЧастьПервая КАК Наборы
		|		ПО Товары.НоменклатураНабора = Наборы.НоменклатураНабора
		|			И Товары.ХарактеристикаНабора = Наборы.ХарактеристикаНабора
		|			И Товары.Ссылка = Наборы.Ссылка
		|			И Товары.Номенклатура = Наборы.Номенклатура
		|			И Товары.Характеристика = Наборы.Характеристика
		|ГДЕ
		|	Товары.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Результат.Ссылка КАК Ссылка,
		|	Результат.ВариантКомплектацииНоменклатуры КАК ВариантКомплектацииНоменклатуры,
		|	Результат.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
		|	Результат.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
		|	Результат.НоменклатураНабора КАК НоменклатураНабора,
		|	Результат.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|	Результат.ДоКорректировки КАК ДоКорректировки,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА Результат.ОсновнаяКомплектующая
		|				ТОГДА Результат.СтавкаНДС
		|			ИНАЧЕ NULL
		|		КОНЕЦ) КАК СтавкаНДС,
		|	МАКСИМУМ(Результат.НомерГТД) КАК НомерГТД,
		|	МАКСИМУМ(Результат.КодТНВЭД) КАК КодТНВЭД,
		|	(ВЫРАЗИТЬ(МИНИМУМ(ВЫБОР
		|				КОГДА Результат.КоличествоПоУмолчанию <> 0
		|						И Результат.ОсновнаяКомплектующая
		|					ТОГДА Результат.Количество / Результат.КоличествоПоУмолчанию
		|				ИНАЧЕ NULL
		|			КОНЕЦ) + 0.5 КАК ЧИСЛО(10, 0))) - 1 КАК Количество,
		|	МАКСИМУМ(Результат.КоличествоПоРНПТ) КАК КоличествоПоРНПТ,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА Результат.КоличествоПоУмолчанию <> 0
		|				ТОГДА Результат.Количество / Результат.КоличествоПоУмолчанию
		|			ИНАЧЕ NULL
		|		КОНЕЦ) КАК КоэффициентМаксимум,
		|	(ВЫРАЗИТЬ(МИНИМУМ(ВЫБОР
		|				КОГДА Результат.КоличествоПоУмолчанию <> 0
		|					ТОГДА Результат.Количество / Результат.КоличествоПоУмолчанию
		|				ИНАЧЕ NULL
		|			КОНЕЦ) + 0.5 КАК ЧИСЛО(10, 0))) - 1 КАК КоэффициентМинимум
		|ПОМЕСТИТЬ ВременнаяТаблицаНаборыДополнительно
		|ИЗ
		|	ВременнаяТаблицаНаборыЧастьВторая КАК Результат
		|
		|СГРУППИРОВАТЬ ПО
		|	Результат.Ссылка,
		|	Результат.ДоКорректировки,
		|	Результат.ВариантКомплектацииНоменклатуры,
		|	Результат.ВариантРасчетаЦеныНабора,
		|	Результат.ВариантПредставленияНабораВПечатныхФормах,
		|	Результат.НоменклатураНабора,
		|	Результат.ХарактеристикаНабора
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Результат.Ссылка,
		|	Результат.НоменклатураНабора,
		|	Результат.ХарактеристикаНабора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаНаборыДополнительно.ВариантКомплектацииНоменклатуры КАК ВариантКомплектацииНоменклатуры,
		|	ВременнаяТаблицаНаборыДополнительно.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
		|	ВременнаяТаблицаНаборыДополнительно.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
		|	Таблица.Ссылка КАК Ссылка,
		|	Таблица.НоменклатураНабора КАК НоменклатураНабора,
		|	Таблица.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|	Таблица.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВременнаяТаблицаНаборыДополнительно.НомерГТД, ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)) КАК НомерГТД,
		|	ЕСТЬNULL(ВременнаяТаблицаНаборыДополнительно.КодТНВЭД, ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)) КАК КодТНВЭД,
		|	ЕСТЬNULL(ВременнаяТаблицаНаборыДополнительно.Количество, 1) КАК КоличествоУпаковок,
		|	ЕСТЬNULL(ВременнаяТаблицаНаборыДополнительно.Количество, 1) КАК Количество,
		|	ЕСТЬNULL(ВременнаяТаблицаНаборыДополнительно.КоличествоПоРНПТ, 1) КАК КоличествоПоРНПТ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаНаборыДополнительно.КоэффициентМинимум = ВременнаяТаблицаНаборыДополнительно.КоэффициентМаксимум
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПолныйНабор,
		|	Таблица.ДоКорректировки КАК ДоКорректировки,
		|	Таблица.МассаБрутто КАК МассаБрутто,
		|	Таблица.МассаНетто КАК МассаНетто,
		|	Таблица.СуммаБезНДС КАК СуммаБезНДС,
		|	Таблица.СуммаНДС КАК СуммаНДС,
		|	Таблица.ЭтоТовар КАК ЭтоТовар,
		|	ВременнаяТаблицаНаборыДополнительно.СтавкаНДС КАК СтавкаНДС
		|ПОМЕСТИТЬ ВременнаяТаблицаНаборы
		|ИЗ
		|	ВременнаяТаблицаНаборыПодготовка КАК Таблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНаборыДополнительно КАК ВременнаяТаблицаНаборыДополнительно
		|		ПО Таблица.НоменклатураНабора = ВременнаяТаблицаНаборыДополнительно.НоменклатураНабора
		|			И Таблица.ХарактеристикаНабора = ВременнаяТаблицаНаборыДополнительно.ХарактеристикаНабора
		|			И Таблица.Ссылка = ВременнаяТаблицаНаборыДополнительно.Ссылка
		|			И Таблица.ДоКорректировки = ВременнаяТаблицаНаборыДополнительно.ДоКорректировки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка,
		|	НоменклатураНабора,
		|	ХарактеристикаНабора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаТоваров.Ссылка КАК Ссылка,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
		|			ТОГДА ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ПустаяСсылка)
		|	КОНЕЦ КАК ВариантПредставленияНабораВПечатныхФормах,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
		|			ТОГДА ВременнаяТаблицаНаборы.ВариантРасчетаЦеныНабора
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ПустаяСсылка)
		|	КОНЕЦ КАК ВариантРасчетаЦеныНабора,
		|	ТаблицаТоваров.НоменклатураНабора КАК НоменклатураНабора,
		|	ТаблицаТоваров.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоКомплектующие,
		|	ЛОЖЬ КАК ЭтоНабор,
		|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТоваров.ВернутьМногооборотнуюТару КАК ВернутьМногооборотнуюТару,
		|	ТаблицаТоваров.ЭтоНеВозвратнаяТара КАК ЭтоНеВозвратнаяТара,
		|	ТаблицаТоваров.Содержание КАК Содержание,
		|	ТаблицаТоваров.ДоКорректировки КАК ДоКорректировки,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
		|			ТОГДА ВременнаяТаблицаНаборы.НомерСтроки
		|		ИНАЧЕ ТаблицаТоваров.НомерСтроки
		|	КОНЕЦ КАК НомерСтрокиНаборы,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
		|			ТОГДА ВременнаяТаблицаНаборы.ПолныйНабор
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПолныйНабор,
		|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
		|	ТаблицаТоваров.НоменклатураПартнера КАК НоменклатураПартнера,
		|	ТаблицаТоваров.Количество КАК Количество,
		|	ТаблицаТоваров.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ТаблицаТоваров.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
		|	ТаблицаТоваров.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаТоваров.НомерГТД КАК НомерГТД,
		|	ТаблицаТоваров.КодТНВЭД КАК КодТНВЭД,
		|	ТаблицаТоваров.СуммаБезНДС КАК СуммаБезНДС,
		|	ТаблицаТоваров.СуммаНДС КАК СуммаНДС,
		|	ТаблицаТоваров.Характеристика КАК Характеристика,
		|	ТаблицаТоваров.Серия КАК Серия,
		|	ТаблицаТоваров.Упаковка КАК Упаковка,
		|	ВЫБОР
		|		КОГДА &ЗаполненаЕдиницаИзмеренияВеса
		|			ТОГДА ВЫБОР
		|					КОГДА ТаблицаТоваров.Упаковка.Вес ЕСТЬ NULL
		|						ТОГДА ТаблицаТоваров.Количество
		|					ИНАЧЕ ВЫБОР
		|							КОГДА ТаблицаТоваров.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Вес)
		|								ТОГДА 0
		|							ИНАЧЕ ТаблицаТоваров.КоличествоУпаковок
		|						КОНЕЦ
		|				КОНЕЦ * &ТекстЗапросаВесУпаковки
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК МассаБрутто,
		|	ТаблицаТоваров.Количество * &ТекстЗапросаВесНоменклатуры КАК МассаНетто,
		|	ТаблицаТоваров.ЭтоТовар КАК ЭтоТовар
		|ПОМЕСТИТЬ КорректировкаРеализацииТаблицаТоваров
		|ИЗ
		|	КорректировкаРеализацииТаблицаТоваровПреобразования КАК ТаблицаТоваров
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНаборы КАК ВременнаяТаблицаНаборы
		|		ПО (ВременнаяТаблицаНаборы.НоменклатураНабора = ТаблицаТоваров.НоменклатураНабора)
		|			И (ВременнаяТаблицаНаборы.ХарактеристикаНабора = ТаблицаТоваров.ХарактеристикаНабора)
		|			И (ВременнаяТаблицаНаборы.Ссылка = ТаблицаТоваров.Ссылка)
		|			И (ВременнаяТаблицаНаборы.ДоКорректировки = ТаблицаТоваров.ДоКорректировки)
		|ГДЕ
		|	(ТаблицаТоваров.НоменклатураНабора = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|			ИЛИ ТаблицаТоваров.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|				И ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах В (ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ТолькоКомплектующие), ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.НаборИКомплектующие)))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаНаборы.Ссылка,
		|	ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах,
		|	ВременнаяТаблицаНаборы.ВариантРасчетаЦеныНабора,
		|	ВременнаяТаблицаНаборы.НоменклатураНабора,
		|	ВременнаяТаблицаНаборы.ХарактеристикаНабора,
		|	ЛОЖЬ,
		|	ИСТИНА,
		|	0,
		|	ЛОЖЬ,
		|	ИСТИНА,
		|	"""",
		|	ВременнаяТаблицаНаборы.ДоКорректировки,
		|	ВременнаяТаблицаНаборы.НомерСтроки,
		|	ВременнаяТаблицаНаборы.ПолныйНабор,
		|	ВременнаяТаблицаНаборы.НоменклатураНабора,
		|	ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка), 
		|	ВременнаяТаблицаНаборы.Количество,
		|	ВременнаяТаблицаНаборы.КоличествоУпаковок,
		|	ВременнаяТаблицаНаборы.КоличествоПоРНПТ,
		|	ВременнаяТаблицаНаборы.СтавкаНДС,
		|	ВременнаяТаблицаНаборы.НомерГТД,
		|	ВременнаяТаблицаНаборы.КодТНВЭД,
		|	ВременнаяТаблицаНаборы.СуммаБезНДС,
		|	ВременнаяТаблицаНаборы.СуммаНДС,
		|	ВременнаяТаблицаНаборы.ХарактеристикаНабора,
		|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка),
		|	ВременнаяТаблицаНаборы.МассаБрутто,
		|	ВременнаяТаблицаНаборы.МассаНетто,
		|	ВременнаяТаблицаНаборы.ЭтоТовар
		|ИЗ
		|	ВременнаяТаблицаНаборы КАК ВременнаяТаблицаНаборы
		|ГДЕ
		|	ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах В (ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ТолькоНабор), ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.НаборИКомплектующие))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВременнаяТаблицаНаборыЧастьПервая
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВременнаяТаблицаНаборыЧастьВторая
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВременнаяТаблицаНаборыПодготовка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВременнаяТаблицаНаборыДополнительно
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВременнаяТаблицаНаборы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КорректировкаРеализацииТаблицаТоваровПреобразования";		
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаВесУпаковки",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
				"ТаблицаТоваров.Упаковка",
				"ТаблицаТоваров.Номенклатура"));
			
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст, 
			"&ТекстЗапросаВесНоменклатуры",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
				"ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения",
				"ТаблицаТоваров.Номенклатура"));
		Запрос.УстановитьПараметр("ЗаполненаЕдиницаИзмеренияВеса",   ЗначениеЗаполнено(Константы.ЕдиницаИзмеренияВеса.Получить()));
		
	КонецЕсли;
	
	Если ПараметрыЗаполнения.ДляСчетаФактуры Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
									"ПОМЕСТИТЬ КорректировкаРеализацииТаблицаТоваров",
									"ПОМЕСТИТЬ КорректировкаРеализацииТаблицаТоваровДляПреобразованияДляСчетаФактуры");
	КонецЕсли;
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Преобразует временную таблицу товаров, созданную функцией ПоместитьВременнуюТаблицуТоваров()
// к виду, используемому при печати счетов-фактуры.
// После преобразования временная таблица уничтожается.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц, содержащий таблицу КорректировкаРеализацииТаблицаТоваров.
//
Процедура ПреобразоватьВременнуюТаблицуТоваровДляСчетаФактуры(МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                  КАК Ссылка,
	|	ТаблицаДокумента.ВариантКомплектацииНоменклатуры         КАК ВариантКомплектацииНоменклатуры,
	|	ТаблицаДокумента.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ТаблицаДокумента.ВариантРасчетаЦеныНабора                КАК ВариантРасчетаЦеныНабора,
	|	ТаблицаДокумента.НоменклатураНабора                      КАК НоменклатураНабора,
	|	ТаблицаДокумента.ХарактеристикаНабора                    КАК ХарактеристикаНабора,
	|	ТаблицаДокумента.Номенклатура                            КАК Номенклатура,
	|	ТаблицаДокумента.НоменклатураПартнера                    КАК НоменклатураПартнера,
	|	ТаблицаДокумента.Характеристика                          КАК Характеристика,
	|	ТаблицаДокумента.Серия                                   КАК Серия,
	|	ТаблицаДокумента.Упаковка                                КАК Упаковка,
	|	ТаблицаДокумента.НомерГТД                                КАК НомерГТД,
	|	ТаблицаДокумента.Содержание                              КАК Содержание,
	|	ТаблицаДокумента.Количество                              КАК Количество,
	|	ТаблицаДокумента.КоличествоУпаковок                      КАК КоличествоУпаковок,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0)         КАК КоличествоДо,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоУпаковок, 0) КАК КоличествоУпаковокДо,
	|	ТаблицаДокумента.КоличествоПоРНПТ                        КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0)   КАК КоличествоПоРНПТДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.КоличествоПоРНПТ >= ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0)
	|			ТОГДА ТаблицаДокумента.КоличествоПоРНПТ - ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ                                                    КАК КоличествоПоРНПТУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.КоличествоПоРНПТ < ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0)
	|			ТОГДА -(ТаблицаДокумента.КоличествоПоРНПТ - ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ                                                    КАК КоличествоПоРНПТУменьшение,
	|	ТаблицаДокумента.СтавкаНДС                               КАК СтавкаНДС,
	|	ТаблицаДокумента.КодТНВЭД                                КАК КодТНВЭД,
	|	ТаблицаДокумента.СуммаБезНДС                             КАК СуммаБезНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)        КАК СуммаБезНДСДо,
	|	ТаблицаДокумента.СуммаНДС                                КАК СуммаНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)           КАК СуммаНДСДо,
	|	0                                                        КАК МежценоваяРазница,
	|	0                                                        КАК МежценоваяРазницаДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС >= ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ                                                    КАК РазницаБезНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС < ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаБезНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ                                                    КАК РазницаБезНДСУменьшение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаНДС >= ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ИНАЧЕ 0
	|	КОНЕЦ                                                    КАК РазницаНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаНДС < ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ                                                    КАК РазницаНДСУменьшение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС >=
	|				ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС -
	|				(ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ                                                    КАК РазницаСНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС <
	|				ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС -
	|				(ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)))
	|		ИНАЧЕ 0
	|	КОНЕЦ                                                    КАК РазницаСНДСУменьшение,
	|	ТаблицаДокумента.НомерСтроки                             КАК НомерСтроки,
	|	ТаблицаДокумента.ЭтоТовар                                КАК ЭтоТовар,
	|	ЛОЖЬ                                                     КАК ВернутьМногооборотнуюТару
	|ПОМЕСТИТЬ КорректировкаРеализацииТаблицаТоваров
	|ИЗ
	|	КорректировкаРеализацииТаблицаТоваровДляПреобразованияДляСчетаФактуры КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ КорректировкаРеализацииТаблицаТоваровДляПреобразованияДляСчетаФактуры КАК ТаблицаТоваровПредыдущие
	|		ПО (ТаблицаТоваровПредыдущие.ДоКорректировки)
	|			И ТаблицаДокумента.Ссылка = ТаблицаТоваровПредыдущие.Ссылка
	|			И ТаблицаДокумента.Номенклатура = ТаблицаТоваровПредыдущие.Номенклатура
	|			И ТаблицаДокумента.Характеристика = ТаблицаТоваровПредыдущие.Характеристика
	|			И ТаблицаДокумента.Упаковка = ТаблицаТоваровПредыдущие.Упаковка
	|			И ТаблицаДокумента.НомерГТД = ТаблицаТоваровПредыдущие.НомерГТД
	|			И ТаблицаДокумента.СтавкаНДС = ТаблицаТоваровПредыдущие.СтавкаНДС
	|			И ТаблицаДокумента.Содержание = ТаблицаТоваровПредыдущие.Содержание
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаФактурыОснования КАК СчетаФактуры
	|		ПО ТаблицаДокумента.Ссылка = СчетаФактуры.ДокументОснование
	|ГДЕ
	|	НЕ ТаблицаДокумента.ДоКорректировки
	|	И (НЕ СчетаФактуры.Корректировочный
	|			ИЛИ ТаблицаДокумента.СуммаНДС <> ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ИЛИ ТаблицаДокумента.СуммаБезНДС <> ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0))
	|	И (ТаблицаДокумента.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	ИЛИ (ТаблицаДокумента.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	И НЕ ТаблицаДокумента.ВернутьМногооборотнуюТару)
	|	ИЛИ ТаблицаДокумента.Номенклатура.ТипНоменклатуры ЕСТЬ NULL)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваровПредыдущие.Ссылка                                          КАК Ссылка,
	|	ТаблицаТоваровПредыдущие.ВариантКомплектацииНоменклатуры                 КАК ВариантКомплектацииНоменклатуры,
	|	ТаблицаТоваровПредыдущие.ВариантПредставленияНабораВПечатныхФормах       КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ТаблицаТоваровПредыдущие.ВариантРасчетаЦеныНабора                        КАК ВариантРасчетаЦеныНабора,
	|	ТаблицаТоваровПредыдущие.НоменклатураНабора                              КАК НоменклатураНабора,
	|	ТаблицаТоваровПредыдущие.ХарактеристикаНабора                            КАК ХарактеристикаНабора,
	|	ТаблицаТоваровПредыдущие.Номенклатура                                    КАК Номенклатура,
	|	ТаблицаТоваровПредыдущие.НоменклатураПартнера                            КАК НоменклатураПартнера,
	|	ТаблицаТоваровПредыдущие.Характеристика                                  КАК Характеристика,
	|	ТаблицаТоваровПредыдущие.Серия                                           КАК Серия,
	|	ТаблицаТоваровПредыдущие.Упаковка                                        КАК Упаковка,
	|	ТаблицаТоваровПредыдущие.НомерГТД                                        КАК НомерГТД,
	|	ТаблицаТоваровПредыдущие.Содержание                                      КАК Содержание,
	|	0                                                                        КАК Количество,
	|	0                                                                        КАК КоличествоУпаковок,
	|	ТаблицаТоваровПредыдущие.Количество                                      КАК КоличествоДо,
	|	ТаблицаТоваровПредыдущие.КоличествоУпаковок                              КАК КоличествоУпаковокДо,
	|	0                                                                        КАК КоличествоПоРНПТ,
	|	ТаблицаТоваровПредыдущие.КоличествоПоРНПТ                                КАК КоличествоПоРНПТДо,
	|	0                                                                        КАК КоличествоПоРНПТУвеличение,
	|	ТаблицаТоваровПредыдущие.КоличествоПоРНПТ                                КАК КоличествоПоРНПТУменьшение,
	|	ТаблицаТоваровПредыдущие.СтавкаНДС                                       КАК СтавкаНДС,
	|	ТаблицаТоваровПредыдущие.КодТНВЭД                                        КАК КодТНВЭД,
	|	0                                                                        КАК СуммаБезНДС,
	|	ТаблицаТоваровПредыдущие.СуммаБезНДС                                     КАК СуммаБезНДСДо,
	|	0                                                                        КАК СуммаНДС,
	|	ТаблицаТоваровПредыдущие.СуммаНДС                                        КАК СуммаНДСДо,
	|	0                                                                        КАК МежценоваяРазница,
	|	0                                                                        КАК МежценоваяРазницаДо,
	|	0                                                                        КАК РазницаБезНДСУвеличение,
	|	ТаблицаТоваровПредыдущие.СуммаБезНДС                                     КАК РазницаБезНДСУменьшение,
	|	0                                                                        КАК РазницаНДСУвеличение,
	|	ТаблицаТоваровПредыдущие.СуммаНДС                                        КАК РазницаНДСУменьшение,
	|	0                                                                        КАК РазницаСНДСУвеличение,
	|	ТаблицаТоваровПредыдущие.СуммаБезНДС + ТаблицаТоваровПредыдущие.СуммаНДС КАК РазницаСНДСУменьшение,
	|	ТаблицаТоваровПредыдущие.НомерСтроки                                     КАК НомерСтроки,
	|	ТаблицаТоваровПредыдущие.ЭтоТовар                                        КАК ЭтоТовар,
	|	ЛОЖЬ                                                                     КАК ВернутьМногооборотнуюТару
	|ИЗ
	|	КорректировкаРеализацииТаблицаТоваровДляПреобразованияДляСчетаФактуры КАК ТаблицаТоваровПредыдущие
	|		ЛЕВОЕ СОЕДИНЕНИЕ КорректировкаРеализацииТаблицаТоваровДляПреобразованияДляСчетаФактуры КАК ТаблицаТоваровПослеКорректировки
	|		ПО (НЕ ТаблицаТоваровПослеКорректировки.ДоКорректировки)
	|			И ТаблицаТоваровПредыдущие.Ссылка = ТаблицаТоваровПослеКорректировки.Ссылка
	|			И ТаблицаТоваровПредыдущие.Номенклатура = ТаблицаТоваровПослеКорректировки.Номенклатура
	|			И ТаблицаТоваровПредыдущие.Характеристика = ТаблицаТоваровПослеКорректировки.Характеристика
	|			И ТаблицаТоваровПредыдущие.Упаковка = ТаблицаТоваровПослеКорректировки.Упаковка
	|			И ТаблицаТоваровПредыдущие.НомерГТД = ТаблицаТоваровПослеКорректировки.НомерГТД
	|			И ТаблицаТоваровПредыдущие.СтавкаНДС = ТаблицаТоваровПослеКорректировки.СтавкаНДС
	|			И ТаблицаТоваровПредыдущие.Содержание = ТаблицаТоваровПослеКорректировки.Содержание
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаФактурыОснования КАК СчетаФактуры
	|		ПО ТаблицаТоваровПредыдущие.Ссылка = СчетаФактуры.ДокументОснование
	|ГДЕ
	|	ТаблицаТоваровПредыдущие.ДоКорректировки
	|	И ТаблицаТоваровПослеКорректировки.Ссылка ЕСТЬ NULL 
	|	И СчетаФактуры.Корректировочный
	|;
	|
	|/////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ КорректировкаРеализацииТаблицаТоваровДляПреобразованияДляСчетаФактуры
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

//-- Локализация

#КонецОбласти


#Область Фискализация

//++ Локализация

// Возвращает параметры операции фискализации чека для печати чека по документу
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Форма документ, из которого печатается чек - содержит:
// 	* Объект - ДокументОбъект - Документ-объект, основной параметр формы.
// Возвращаемое значение:
// 	Структура - Структура параметров операции фискализации чека
Функция ОсновныеПараметрыОперации(Форма) Экспорт
	
	ОсновныеПараметрыОперации = ФормированиеФискальныхЧековСерверПереопределяемый.СтруктураОсновныхПараметровОперации();
	
	ОсновныеПараметрыОперации.ДокументСсылка       = Форма.Объект.Ссылка;
	ОсновныеПараметрыОперации.Организация          = Форма.Объект.Организация;
	ОсновныеПараметрыОперации.Контрагент           = Форма.Объект.Контрагент;
	ОсновныеПараметрыОперации.Партнер              = Форма.Объект.Партнер;
	ОсновныеПараметрыОперации.Валюта               = Форма.Объект.Валюта;
	ОсновныеПараметрыОперации.СуммаДокумента       = Форма.Объект.СуммаДокумента;
	
	Возврат ОсновныеПараметрыОперации;
	
КонецФункции

// Определяет, разрешено ли пробитие фискального чека по документу
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Форма документ, из которого печатается чек - содержит:
// 	* Объект - ДокументОбъект - Документ-объект, основной параметр формы.
// Возвращаемое значение:
// 	Булево - Истина, если разрешено пробитие чека
Функция РазрешеноПробитиеФискальныхЧековПоДокументу(Форма) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Форма.Объект.Контрагент) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	РазрешеноПробитиеФискальныхЧековПоДокументу = Истина;
	
	ЮрФизЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Форма.Объект.Контрагент, "ЮрФизЛицо");
	РазрешеноПробитиеФискальныхЧековПоДокументу = (ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо);
	
	Возврат РазрешеноПробитиеФискальныхЧековПоДокументу;
	
КонецФункции

// Формирует массив форматированных строк для формирования гиперссылки пробития фискального чека
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ-ссылка, по которому пробивается фискальный чек
// 	Форма - ФормаКлиентскогоПриложения - Форма документ, из которого печатается чек - содержит:
// 	* Объект - ДокументОбъект - Документ-объект, основной параметр формы.
// 	МассивПредставлений - Массив из ФорматированнаяСтрока - Массив форматированных строк для формирования гиперссылки
//    пробития фискального чека.
Процедура ОбновитьГиперссылкуПробитияФискальногоЧека(ДокументСсылка, Форма, МассивПредставлений) Экспорт

	Если ЗначениеЗаполнено(ДокументСсылка) Тогда  
		ДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ДокументОснование");
	Иначе
		ДокументОснование = Форма.Объект.ДокументОснование;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументОснование) Тогда  	
		ФискальнаяОперацияДокументОснование = ФормированиеФискальныхЧековСервер.ДанныеПробитогоФискальногоЧекаПоДокументу(ДокументОснование);
		Если ФискальнаяОперацияДокументОснование <> Неопределено
			И ФискальнаяОперацияДокументОснование.ЕдиныйЧек Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	СписокФискальныхОпераций = ОборудованиеЧекопечатающиеУстройстваВызовСервера.СписокФискальныхОпераций(ДокументСсылка);
	
	Если СписокФискальныхОпераций.Количество() Тогда
		
		ТекущийИндексСписка = -1;
		Для Каждого ФискальнаяОперацияДанныеЖурнала Из СписокФискальныхОпераций Цикл
			ТекущийИндексСписка = ТекущийИндексСписка + 1;
			НомерЧекаККМ = ФискальнаяОперацияДанныеЖурнала.НомерЧекаККМ;
			ТекстСсылки = "ОткрытьЗаписьФискальнойОперации"+ФискальнаяОперацияДанныеЖурнала.ИдентификаторЗаписи;
			
			ФормированиеФискальныхЧековСервер.ДобавитьВПредставлениеГиперссылкиКомандуЧекПробит(МассивПредставлений, НомерЧекаККМ, ТекстСсылки);
			
			Если ТекущийИндексСписка < СписокФискальныхОпераций.ВГраница() Тогда 
				Отступ =  Новый ФорматированнаяСтрока("  ");
				МассивПредставлений.Добавить(Отступ);
			КонецЕсли;	
		КонецЦикла;
				
	ИначеЕсли ФормированиеФискальныхЧековСервер.ЕстьПравоНаПробитиеФискальногоЧекаПоДокументу(ДокументСсылка) Тогда
		
		Если ФормированиеФискальныхЧековСервер.ЕстьПодключенноеОборудованиеККассамОрганизации(Форма.Объект.Организация) Тогда 
			ФормированиеФискальныхЧековСервер.ДобавитьВПредставлениеГиперссылкиКомандуПробитьЧек(МассивПредставлений, "ПробитьЧек");
		Иначе
			ФормированиеФискальныхЧековСервер.ДобавитьВПредставлениеГиперссылкиСтатусЧекНеПробит(МассивПредставлений, "НастроитьОборудование");
		КонецЕсли;
		
	Иначе
		
		ФормированиеФискальныхЧековСервер.ДобавитьВПредставлениеГиперссылкиСтатусЧекНеПробит(МассивПредставлений);
		
	КонецЕсли;
	
КонецПроцедуры

// Определяет виды фискальных чеков, доступных по документу
// 
// Параметры:
// 	ВидыЧеков - ТаблицаЗначений - Таблица значений, содержащая виды фискальных чеков, доступных по документу
// 	Операция - ПеречислениеСсылка.ХозяйственныеОперации - Хозяйственная операция по документу
// 	ИмяКомандыПробитияЧека - Строка - Имя команды пробития чека
Процедура ЗаполнитьВидыФискальныхЧековПоДокументу(ВидыЧеков, Операция, ИмяКомандыПробитияЧека) Экспорт
	
	ВидЧека = ВидыЧеков.Добавить();
	ВидЧека.ТипФискальногоДокумента = Перечисления.ТипыФискальныхДокументовККТ.КассовыйЧекКоррекции;
	ВидЧека.ТипРасчетаДенежнымиСредствами = Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств;
	ВидЧека.ВидЧекаКоррекции = Перечисления.ВидыЧековКоррекции.ИсправлениеОшибки;
	
КонецПроцедуры

// Возвращает таблицу товаров для заполнения позиций строк в параметрах чека на оплату
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ для получения товарных позиций
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблица с товарными позициями с количественными и суммовыми показателями
Функция ПозицииНоменклатурыПоДокументу(ДокументСсылка) Экспорт
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "ДокументОснование, ЦенаВключаетНДС");
	
	ТребуетсяЗалогЗаТару = Ложь;
	ВернутьМногооборотнуюТару = Ложь;
	Если ТипЗнч(РеквизитыДокумента.ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		РеквизитыДокументаОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыДокумента.ДокументОснование, "ТребуетсяЗалогЗаТару, ВернутьМногооборотнуюТару");
		ТребуетсяЗалогЗаТару = РеквизитыДокументаОснования.ТребуетсяЗалогЗаТару;
		ВернутьМногооборотнуюТару = РеквизитыДокументаОснования.ВернутьМногооборотнуюТару;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки,
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|		  И НЕ ТабличнаяЧасть.Содержание = """"
	|			ТОГДА ТабличнаяЧасть.Содержание
	|		ИНАЧЕ ТабличнаяЧасть.Номенклатура
	|	КОНЕЦ КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Упаковка КАК Упаковка,
	|	ТабличнаяЧасть.КоличествоУпаковок КАК Количество,
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА &ЦенаВключаетНДС
	|			ТОГДА ТабличнаяЧасть.Цена
	|		КОГДА ТабличнаяЧасть.КоличествоУпаковок = 0
	|			ТОГДА ТабличнаяЧасть.СуммаСНДС
	|		ИНАЧЕ (ТабличнаяЧасть.СуммаСНДС) / ТабличнаяЧасть.КоличествоУпаковок
	|	КОНЕЦ КАК ЧИСЛО(31, 2)) КАК Цена,
	|	ТабличнаяЧасть.СуммаСНДС КАК Сумма,
	|	ТабличнаяЧасть.СтавкаНДС КАК СтавкаНДС,
	|	ТабличнаяЧасть.СуммаНДС КАК СуммаНДС
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|	И ТабличнаяЧасть.Ссылка.Проведен
	|	И (НЕ &ВернутьМногооборотнуюТару
	|		ИЛИ (НЕ &ТребуетсяЗалогЗаТару
	|			И ТабличнаяЧасть.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ИЛИ &ТребуетсяЗалогЗаТару)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Количество             КАК Количество,
	|	ТаблицаНоменклатуры.Сумма                  КАК Сумма,
	|	ТаблицаНоменклатуры.СтавкаНДС              КАК СтавкаНДС,
	|	ТаблицаНоменклатуры.СуммаНДС               КАК СуммаНДС,
	|	ТаблицаНоменклатуры.Цена                   КАК Цена,
	|	
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаНоменклатуры.Номенклатура) = ТИП(Строка) ТОГДА
	|			ТаблицаНоменклатуры.Номенклатура
	|		ИНАЧЕ
	|			ТаблицаНоменклатуры.Номенклатура.НаименованиеПолное
	|	КОНЕЦ КАК НоменклатураНаименование,
	|	ЕСТЬNULL(ТаблицаНоменклатуры.Характеристика.НаименованиеПолное, """") КАК ХарактеристикаНаименование,
	|	ТаблицаНоменклатуры.Упаковка               КАК Упаковка,
	|	ТаблицаНоменклатуры.Упаковка               КАК УпаковкаНаименование
	|	
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаНоменклатуры.НомерСтроки
	|";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС"          , РеквизитыДокумента.ЦенаВключаетНДС);
	Запрос.УстановитьПараметр("ТребуетсяЗалогЗаТару"     , ТребуетсяЗалогЗаТару);
	Запрос.УстановитьПараметр("ВернутьМногооборотнуюТару", ВернутьМногооборотнуюТару);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Определяет систему налогообложения по документу
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ для определения системы налогообложения
// Возвращаемое значение:
// 	ПеречислениеСсылка.ТипыСистемНалогообложенияККТ - Система налогообложения по документу
Функция СистемаНалогообложенияПоДокументу(ДокументСсылка) Экспорт
	
	СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД;
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Организация, НалогообложениеНДС");
	Если РеквизитыДокумента.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаПоПатенту Тогда
		СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.Патент;
	ИначеЕсли РеквизитыДокумента.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД Тогда
		СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД;
	Иначе
		СистемаНалогообложения = РозничныеПродажиЛокализация.СистемаНалогообложенияФискальнойОперации(РеквизитыДокумента.Организация);
	КонецЕсли;
	
	Возврат СистемаНалогообложения;
	
КонецФункции

// Возвращает таблицу товаров для заполнения позиций строк в параметрах чека на поставку
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ для получения товарных позиций
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблица с товарными позициями с количественными и суммовыми показателями
Функция ПредметыРасчетовПоДокументу(ДокументСсылка) Экспорт
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "ДокументОснование, ЦенаВключаетНДС, Организация");
	
	ПараметрыЗалогаЗаТару = Новый Структура("ТребуетсяЗалогЗаТару, ВернутьМногооборотнуюТару", Ложь, Ложь);
	Если ТипЗнч(РеквизитыДокумента.ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		РеквизитыДокументаОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыДокумента.ДокументОснование, "ТребуетсяЗалогЗаТару, ВернутьМногооборотнуюТару");
		ЗаполнитьЗначенияСвойств(ПараметрыЗалогаЗаТару, РеквизитыДокументаОснования);
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.ЗаказКлиента КАК Заказ,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия,
	|	ТабличнаяЧасть.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ТабличнаяЧасть.Упаковка КАК Упаковка,
	|	СпрНоменклатура.ЕдиницаИзмерения КАК БазоваяУпаковка,
	|	СпрНоменклатура.КодВидаНоменклатурнойКлассификации КАК КодВидаНоменклатурнойКлассификации,
	|	ЕСТЬNULL(СпрНоменклатура.ТипНоменклатуры, ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПустаяСсылка)) КАК ТипНоменклатуры,
	|	ЕСТЬNULL(СпрНоменклатура.ПодакцизныйТовар, ЛОЖЬ) КАК ПодакцизныйТовар,
	|	ЕСТЬNULL(СпрНоменклатура.ОсобенностьУчета, ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПустаяСсылка)) КАК ОсобенностьУчета,
	|	ВЫБОР
	|		КОГДА СпрНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|				И ЕСТЬNULL(ТабличнаяЧасть.Характеристика.Принципал, СпрНоменклатура.Принципал) <> НЕОПРЕДЕЛЕНО
	|				И ЕСТЬNULL(ТабличнаяЧасть.Характеристика.Принципал, СпрНоменклатура.Принципал) <> &Организация
	|			ТОГДА ЕСТЬNULL(ТабличнаяЧасть.Характеристика.Контрагент, СпрНоменклатура.Контрагент)
	|		ИНАЧЕ ЕСТЬNULL(СпрНоменклатура.Контрагент, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|	КОНЕЦ КАК Агент,
	|	ЕСТЬNULL(СпрНоменклатура.НаименованиеПолное, ТабличнаяЧасть.Содержание) КАК НоменклатураНаименование,
	|	ЕСТЬNULL(ТабличнаяЧасть.Характеристика.НаименованиеПолное, """") КАК ХарактеристикаНаименование,
	|	ТабличнаяЧасть.Упаковка КАК УпаковкаНаименование,
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.Количество = 0
	|			ТОГДА 1
	|		ИНАЧЕ ТабличнаяЧасть.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.КоличествоУпаковок = 0
	|			ТОГДА 1
	|		ИНАЧЕ ТабличнаяЧасть.КоличествоУпаковок
	|	КОНЕЦ КАК КоличествоУпаковок,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА &ЦенаВключаетНДС
	|				ТОГДА ТабличнаяЧасть.Цена
	|			КОГДА ТабличнаяЧасть.КоличествоУпаковок = 0
	|				ТОГДА ТабличнаяЧасть.СуммаСНДС
	|			ИНАЧЕ ТабличнаяЧасть.СуммаСНДС / ТабличнаяЧасть.КоличествоУпаковок
	|		КОНЕЦ КАК ЧИСЛО(31, 2)) КАК Цена,
	|	0 КАК СуммаСкидки,
	|	ТабличнаяЧасть.СтавкаНДС КАК СтавкаНДС,
	|	ТабличнаяЧасть.СуммаНДС КАК СуммаНДС,
	|	ТабличнаяЧасть.СуммаСНДС КАК СуммаСНДС,
	|	СпрНоменклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины В (&МерныеТипыЕдиницИзмерений) КАК МернаяЕдиницаИзмерения
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО (СпрНоменклатура.Ссылка = ТабличнаяЧасть.Номенклатура)
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|	И (НЕ &ВернутьМногооборотнуюТару
	|			ИЛИ НЕ &ТребуетсяЗалогЗаТару
	|				И СпрНоменклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			ИЛИ &ТребуетсяЗалогЗаТару)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШтрихкодыУпаковок.ШтрихкодУпаковки КАК ШтрихкодУпаковки,
	|	ШтрихкодыУпаковок.ЧастичноеВыбытиеВариантУчета КАК ЧастичноеВыбытиеВариантУчета,
	|	ШтрихкодыУпаковок.ЧастичноеВыбытиеНоменклатура КАК ЧастичноеВыбытиеНоменклатура,
	|	ШтрихкодыУпаковок.ЧастичноеВыбытиеХарактеристика КАК ЧастичноеВыбытиеХарактеристика,
	|	ШтрихкодыУпаковок.ЧастичноеВыбытиеКоличество КАК ЧастичноеВыбытиеКоличество
	|ПОМЕСТИТЬ ТаблицаАкцизныеМарки
	|ИЗ
	|	Документ.КорректировкаРеализации.ШтрихкодыУпаковок КАК ШтрихкодыУпаковок
	|ГДЕ
	|	ШтрихкодыУпаковок.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Упаковка КАК Упаковка,
	|	Штрихкоды.Штрихкод КАК Штрихкод,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Упаковка = Штрихкоды.Упаковка
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ПриоритетШтрихКода
	|ПОМЕСТИТЬ Штрихкоды
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК Штрихкоды
	|		ПО ТаблицаТовары.Номенклатура = Штрихкоды.Номенклатура
	|			И ТаблицаТовары.Характеристика = Штрихкоды.Характеристика
	|			И (ТаблицаТовары.Упаковка = Штрихкоды.Упаковка
	|				ИЛИ &ТекстЗапросаКоэффициентУпаковки1 = 1
	|					И ТаблицаТовары.Номенклатура.ЕдиницаИзмерения = ТаблицаТовары.Упаковка.ЕдиницаИзмерения
	|					И Штрихкоды.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Штрихкоды.Номенклатура КАК Номенклатура,
	|	Штрихкоды.Характеристика КАК Характеристика,
	|	Штрихкоды.Упаковка КАК Упаковка,
	|	МАКСИМУМ(Штрихкоды.ПриоритетШтрихКода) КАК ПриоритетШтрихКода
	|ПОМЕСТИТЬ ПриоритетыШтрихКода
	|ИЗ
	|	Штрихкоды КАК Штрихкоды
	|
	|СГРУППИРОВАТЬ ПО
	|	Штрихкоды.Номенклатура,
	|	Штрихкоды.Характеристика,
	|	Штрихкоды.Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Штрихкоды.Номенклатура КАК Номенклатура,
	|	Штрихкоды.Характеристика КАК Характеристика,
	|	Штрихкоды.Упаковка КАК Упаковка,
	|	МАКСИМУМ(Штрихкоды.Штрихкод) КАК Штрихкод
	|ПОМЕСТИТЬ ШтрихкодыНоменклатуры
	|ИЗ
	|	Штрихкоды КАК Штрихкоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПриоритетыШтрихКода КАК ПриоритетыШтрихКода
	|		ПО Штрихкоды.Номенклатура = ПриоритетыШтрихКода.Номенклатура
	|			И Штрихкоды.Характеристика = ПриоритетыШтрихКода.Характеристика
	|			И Штрихкоды.Упаковка = ПриоритетыШтрихКода.Упаковка
	|			И Штрихкоды.ПриоритетШтрихКода = ПриоритетыШтрихКода.ПриоритетШтрихКода
	|
	|СГРУППИРОВАТЬ ПО
	|	Штрихкоды.Номенклатура,
	|	Штрихкоды.Характеристика,
	|	Штрихкоды.Упаковка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Заказ КАК Заказ,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	Товары.Серия КАК Серия,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.БазоваяУпаковка КАК БазоваяУпаковка,
	|	Товары.КодВидаНоменклатурнойКлассификации КАК КодВидаНоменклатурнойКлассификации,
	|	Товары.ТипНоменклатуры КАК ТипНоменклатуры,
	|	Товары.ПодакцизныйТовар КАК ПодакцизныйТовар,
	|	Товары.ОсобенностьУчета КАК ОсобенностьУчета,
	|	Товары.Агент КАК Агент,
	|	Товары.НоменклатураНаименование КАК НоменклатураНаименование,
	|	Товары.ХарактеристикаНаименование КАК ХарактеристикаНаименование,
	|	Товары.УпаковкаНаименование КАК УпаковкаНаименование,
	|	Товары.Количество КАК Количество,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Товары.Цена КАК Цена,
	|	Товары.СуммаСкидки КАК СуммаСкидки,
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	Товары.СуммаНДС КАК СуммаНДС,
	|	Товары.СуммаСНДС КАК СуммаСНДС,
	|	ВЫБОР
	|		КОГДА Товары.МернаяЕдиницаИзмерения
	|			ТОГДА """"
	|		ИНАЧЕ ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """")
	|	КОНЕЦ КАК Штрихкод
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	ТаблицаТовары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО (ШтрихкодыНоменклатуры.Номенклатура = Товары.Номенклатура)
	|			И (ШтрихкодыНоменклатуры.Характеристика = Товары.Характеристика)
	|			И (ШтрихкодыНоменклатуры.Упаковка = Товары.Упаковка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Штрихкоды
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ШтрихкодыНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПриоритетыШтрихКода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК СерияТаблицыСерий,
	|	Товары.Количество КАК КоличествоУпаковок
	|ПОМЕСТИТЬ Серии
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Заказ КАК Заказ,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	Товары.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.БазоваяУпаковка КАК БазоваяУпаковка,
	|	Товары.ТипНоменклатуры КАК ТипНоменклатуры,
	|	Товары.ПодакцизныйТовар КАК ПодакцизныйТовар,
	|	Товары.ОсобенностьУчета КАК ОсобенностьУчета,
	|	Товары.Агент КАК Агент,
	|	Товары.НоменклатураНаименование КАК НоменклатураНаименование,
	|	Товары.ХарактеристикаНаименование КАК ХарактеристикаНаименование,
	|	Товары.УпаковкаНаименование КАК УпаковкаНаименование,
	|	Товары.КодВидаНоменклатурнойКлассификации КАК КодВидаНоменклатурнойКлассификации,
	|	Товары.Количество КАК Количество,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Товары.Цена КАК Цена,
	|	Товары.СуммаСкидки КАК СуммаСкидки,
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	Товары.СуммаНДС КАК СуммаНДС,
	|	Товары.СуммаСНДС КАК СуммаСНДС,
	|	Товары.Штрихкод КАК Штрихкод
	|ИЗ
	|	Товары КАК Товары
	|
	|УПОРЯДОЧИТЬ ПО
	|	Товары.НомерСтроки");
	
	Запрос.УстановитьПараметр("Ссылка"			         , ДокументСсылка);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС"	         , РеквизитыДокумента.ЦенаВключаетНДС);
	Запрос.УстановитьПараметр("ТребуетсяЗалогЗаТару"     , ПараметрыЗалогаЗаТару.ТребуетсяЗалогЗаТару);
	Запрос.УстановитьПараметр("ВернутьМногооборотнуюТару", ПараметрыЗалогаЗаТару.ВернутьМногооборотнуюТару);
	Запрос.УстановитьПараметр("Организация"              , РеквизитыДокумента.Организация);
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ТаблицаТовары.Упаковка",
		"ТаблицаТовары.Номенклатура"));
	Запрос.УстановитьПараметр("МерныеТипыЕдиницИзмерений", Справочники.УпаковкиЕдиницыИзмерения.МерныеТипыЕдиницИзмерений());
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Товары = РезультатЗапроса[РезультатЗапроса.Количество() - 1].Выгрузить(); // ТаблицаЗначений
	
	ТаблицаТоваровСРаспределениемПоВидамЗапасов(Товары, ДанныеДляВидовЗапасов(ДокументСсылка, РеквизитыДокумента.ДокументОснование, ПараметрыЗалогаЗаТару));
	
	ДанныеДляИСМП = РозничныеПродажиЛокализация.ДанныеДляИСМП(ДокументСсылка, МенеджерВременныхТаблиц);
	ФормированиеФискальныхЧековСерверПереопределяемый.ТаблицаТоваровСРаспределениемКодовМаркировкиИСМП(Товары, ДанныеДляИСМП);
	
	Возврат Товары;
	
КонецФункции

// Проверяет наличие маркированных товаров в корректировке реализации.
// 
// Параметры:
//  ДокументСсылка - ДокументСсылка.КорректировкаРеализации - Коррректировка реализации
//
// Возвращаемое значение:
//  Булево
//
Функция ДокументСодержитМаркированныйТовар(ДокументСсылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК СодержитМаркированныйТовар
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК КорректировкаРеализацииРасхождения
	|ГДЕ
	|	КорректировкаРеализацииРасхождения.Ссылка = &Ссылка
	|	И КорректировкаРеализацииРасхождения.Номенклатура.ОсобенностьУчета В(&ОсобенностиУчетаНоменклатуры)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализацииТовары
	|ГДЕ
	|	КорректировкаРеализацииТовары.Ссылка = &Ссылка
	|	И КорректировкаРеализацииТовары.Номенклатура.ОсобенностьУчета В(&ОсобенностиУчетаНоменклатуры)";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ОсобенностиУчетаНоменклатуры", ИнтеграцияИСМПУТ.ОсобенностиУчетаНоменклатуры());
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

//-- Локализация

#КонецОбласти

//++ Локализация

// Правила получения значений реквизитов ТТН
// 
// Возвращаемое значение:
// см. Документы.ТранспортнаяНакладная.ПараметрыФормированияТранспортныхНакладных
//
Функция ПараметрыФормированияТранспортныхНакладных() Экспорт
	
	ПараметрыФормированияТранспортныхНакладных =
		Документы.ТранспортнаяНакладная.ПараметрыФормированияТранспортныхНакладных();
		
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.Грузоотправитель = 
	"	ВЫБОР
	|		КОГДА ОснованиеТранспортнойНакладной.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ОснованиеТранспортнойНакладной.Организация
	|		ИНАЧЕ ОснованиеТранспортнойНакладной.Грузоотправитель
	|	КОНЕЦ";
	
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.Грузополучатель = 
	"		ВЫБОР
	|			КОГДА ОснованиеТранспортнойНакладной.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ОснованиеТранспортнойНакладной.Контрагент
	|			ИНАЧЕ ОснованиеТранспортнойНакладной.Грузополучатель
	|		КОНЕЦ";
	
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.ЗаказчикПеревозки                 = "ОснованиеТранспортнойНакладной.Контрагент";
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.БанковскийСчетЗаказчикаПеревозки  = "ОснованиеТранспортнойНакладной.БанковскийСчетКонтрагента";
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.Плательщик                        = "ОснованиеТранспортнойНакладной.Контрагент";
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.БанковскийСчетПлательщика         = "ОснованиеТранспортнойНакладной.БанковскийСчетКонтрагента";
	
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.ПеревозчикПартнер                 = "ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)";
	
	ПараметрыФормированияТранспортныхНакладных.ЕстьДополнительнаяПроверкаВозможностиСозданияНакладной = Истина;
	ПараметрыФормированияТранспортныхНакладных.ИменаРеквизитовДляДополнительнойПроверки.Добавить("ДокументОснование");
	
	ПараметрыФормированияТранспортныхНакладных.РеквизитыШапкиДляПечати.ХозяйственнаяОперация = "ОснованиеТранспортнойНакладной.ВидКорректировки";
	ПараметрыФормированияТранспортныхНакладных.РеквизитыШапкиДляПечати.ТипКорректировки = "ОснованиеТранспортнойНакладной.ТипКорректировки";
	
	Возврат	ПараметрыФормированияТранспортныхНакладных;
	
КонецФункции

// Проверка возможности создания транспортной накладной на основании документа
//
// Параметры:
//  ДокументСсылка		 - ДокументСсылка.КорректировкаРеализации
//  ЗначенияРеквизитов	 - см. КорректировкаРеализацииЛокализация.ПараметрыФормированияТранспортныхНакладных.ИменаРеквизитовДляДополнительнойПроверки
//  ВыводитьСообщение	 - Булево
//  ПутьКПолю			 - Строка
//
// Возвращаемое значение:
//  Булево - 
//
Функция МожноСоздаватьТранспортнуюНакладную(ДокументСсылка, ЗначенияРеквизитов, ВыводитьСообщение, ПутьКПолю = "") Экспорт
	
	Если ТипЗнч(ЗначенияРеквизитов.ДокументОснование) = Тип("ДокументСсылка.РеализацияУслугПрочихАктивов")
		Или ТипЗнч(ЗначенияРеквизитов.ДокументОснование) = Тип("ДокументСсылка.АктВыполненныхРабот") Тогда
		
		Если ВыводитьСообщение Тогда
			ТекстСообщения =  НСтр("ru = 'Невозможно создать транспортную накладную для документа ""%Документ%"", т.к. у него в качестве документа-основания выступает ""%ДокументОснование%"".'");	
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", ДокументСсылка);	
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДокументОснование%", ЗначенияРеквизитов.ДокументОснование);
			
			Если ПустаяСтрока(ПутьКПолю) Тогда
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			Иначе
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,ПутьКПолю);
			КонецЕсли;
		КонецЕсли;
		
		Возврат Ложь;
	
	ИначеЕсли ТипЗнч(ЗначенияРеквизитов.ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		ХозяйственнаяОперация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначенияРеквизитов.ДокументОснование, "ХозяйственнаяОперация");
		
		Если КомиссионнаяТорговляСервер.РеализацияЧерезКомиссионера(ХозяйственнаяОперация) Тогда
		
			Если ВыводитьСообщение Тогда
				ТекстСообщения = НСтр("ru = 'Невозможно создать транспортную накладную для документа %Документ% с хозяйственной операцией %ХозяйственнаяОперация%.'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", ДокументСсылка);
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ХозяйственнаяОперация%", ХозяйственнаяОперация);
				
				Если ПустаяСтрока(ПутьКПолю) Тогда
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
				Иначе
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,ПутьКПолю);
				КонецЕсли;
			КонецЕсли;
		
			Возврат Ложь;
			
		Иначе
			Возврат Истина;
		КонецЕсли;
		
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

//-- Локализация

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация


	//-- Локализация
	
КонецПроцедуры
//++ Локализация


//-- Локализация

#КонецОбласти

#Область Прочее

//++ Локализация

Процедура СформироватьВременнуюТаблицуНоменклатураДокументов(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(ТаблицаТоваров.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьПрослеживаемыеТовары
	|ПОМЕСТИТЬ
	|	НоменклатураДокументов
	|ИЗ
	|	КорректировкаРеализацииТаблицаТоваров КАК ТаблицаТоваров
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	
	Запрос.Выполнить();
	
КонецПроцедуры


//-- Локализация
#КонецОбласти

#Область Фискализация

//++ Локализация

Функция ДанныеДляВидовЗапасов(ДокументСсылка, ДокументОснование, ПараметрыЗалогаЗаТару)
	
	Запрос = Новый Запрос( 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслугВидыЗапасов.ЗаказКлиента КАК Заказ,
	|	РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	РеализацияТоваровУслугВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	РеализацияТоваровУслугВидыЗапасов.НомерГТД,
	|	РеализацияТоваровУслугВидыЗапасов.Упаковка КАК Упаковка,
	|	РеализацияТоваровУслугВидыЗапасов.Количество КАК Количество,
	|	РеализацияТоваровУслугВидыЗапасов.КоличествоУпаковок КАК КоличествоУпаковок,
	|	РеализацияТоваровУслугВидыЗапасов.СтавкаНДС КАК СтавкаНДС
	|ПОМЕСТИТЬ ВидыЗапасовИТовары
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ВидыЗапасов КАК РеализацияТоваровУслугВидыЗапасов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитикиУчетаНоменклатуры
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|			ПО (СпрНоменклатура.Ссылка = КлючиАналитикиУчетаНоменклатуры.Номенклатура)
	|		ПО (КлючиАналитикиУчетаНоменклатуры.Ссылка = РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры)
	|ГДЕ
	|	РеализацияТоваровУслугВидыЗапасов.Ссылка = &ДокументОснование
	|	И (НЕ &ВернутьМногооборотнуюТару
	|		ИЛИ (НЕ &ТребуетсяЗалогЗаТару
	|			И СпрНоменклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ИЛИ &ТребуетсяЗалогЗаТару)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализацииВидыЗапасовОприходование.ЗаказКлиента КАК Заказ,
	|	КорректировкаРеализацииВидыЗапасовОприходование.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	КорректировкаРеализацииВидыЗапасовОприходование.ВидЗапасов КАК ВидЗапасов,
	|	КорректировкаРеализацииВидыЗапасовОприходование.НомерГТД,
	|	КорректировкаРеализацииВидыЗапасовОприходование.Упаковка КАК Упаковка,
	|	-КорректировкаРеализацииВидыЗапасовОприходование.Количество КАК Количество,
	|	-КорректировкаРеализацииВидыЗапасовОприходование.КоличествоУпаковок КАК КоличествоУпаковок,
	|	КорректировкаРеализацииВидыЗапасовОприходование.СтавкаНДС КАК СтавкаНДС
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК КорректировкаРеализацииВидыЗапасовОприходование
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитикиУчетаНоменклатуры
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|			ПО (СпрНоменклатура.Ссылка = КлючиАналитикиУчетаНоменклатуры.Номенклатура)
	|		ПО (КлючиАналитикиУчетаНоменклатуры.Ссылка = КорректировкаРеализацииВидыЗапасовОприходование.АналитикаУчетаНоменклатуры)
	|ГДЕ
	|	КорректировкаРеализацииВидыЗапасовОприходование.Ссылка.ДокументОснование = &ДокументОснование
	|	И КорректировкаРеализацииВидыЗапасовОприходование.Ссылка.Проведен = ИСТИНА
	|	И (НЕ &ВернутьМногооборотнуюТару
	|		ИЛИ (НЕ &ТребуетсяЗалогЗаТару
	|			И СпрНоменклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ИЛИ &ТребуетсяЗалогЗаТару)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализацииВидыЗапасовСписание.ЗаказКлиента КАК Заказ,
	|	КорректировкаРеализацииВидыЗапасовСписание.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	КорректировкаРеализацииВидыЗапасовСписание.ВидЗапасов КАК ВидЗапасов,
	|	КорректировкаРеализацииВидыЗапасовСписание.НомерГТД,
	|	КорректировкаРеализацииВидыЗапасовСписание.Упаковка КАК Упаковка,
	|	КорректировкаРеализацииВидыЗапасовСписание.Количество КАК Количество,
	|	КорректировкаРеализацииВидыЗапасовСписание.КоличествоУпаковок КАК КоличествоУпаковок,
	|	КорректировкаРеализацииВидыЗапасовСписание.СтавкаНДС КАК СтавкаНДС
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК КорректировкаРеализацииВидыЗапасовСписание
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитикиУчетаНоменклатуры
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|			ПО (СпрНоменклатура.Ссылка = КлючиАналитикиУчетаНоменклатуры.Номенклатура)
	|		ПО (КлючиАналитикиУчетаНоменклатуры.Ссылка = КорректировкаРеализацииВидыЗапасовСписание.АналитикаУчетаНоменклатуры)
	|ГДЕ
	|	КорректировкаРеализацииВидыЗапасовСписание.Ссылка.ДокументОснование = &ДокументОснование
	|	И КорректировкаРеализацииВидыЗапасовСписание.Ссылка.Проведен = ИСТИНА
	|	И (НЕ &ВернутьМногооборотнуюТару
	|		ИЛИ (НЕ &ТребуетсяЗалогЗаТару
	|			И СпрНоменклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ИЛИ &ТребуетсяЗалогЗаТару)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыЗапасовИТовары.Заказ,
	|	ВидыЗапасовИТовары.АналитикаУчетаНоменклатуры,
	|	КлючиАналитикиУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	КлючиАналитикиУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	КлючиАналитикиУчетаНоменклатуры.Серия КАК Серия,
	|	ВидыЗапасовИТовары.ВидЗапасов,
	|	СпрВидыЗапасов.ТипЗапасов КАК ТипЗапасов,
	|	
	|	ВидыЗапасовИТовары.НомерГТД            КАК НомерГТД,
	|	ЕСТЬNULL(НомераГТД.Код, НЕОПРЕДЕЛЕНО)  КАК НомерТаможеннойДекларации,
	|	ЕСТЬNULL(СтраныМира.Код, НЕОПРЕДЕЛЕНО) КАК КодСтраныПроисхождения,
	|	
	|	ВЫБОР
	|		КОГДА СпрВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА СпрВидыЗапасов.Контрагент
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Комитент,
	|	ВидыЗапасовИТовары.Упаковка,
	|	ВидыЗапасовИТовары.СтавкаНДС,
	|	СУММА(ВидыЗапасовИТовары.Количество) КАК Количество,
	|	СУММА(ВидыЗапасовИТовары.КоличествоУпаковок) КАК КоличествоУпаковок
	|ИЗ
	|	ВидыЗапасовИТовары КАК ВидыЗапасовИТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитикиУчетаНоменклатуры
	|		ПО (КлючиАналитикиУчетаНоменклатуры.Ссылка = ВидыЗапасовИТовары.АналитикаУчетаНоменклатуры)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК СпрВидыЗапасов
	|		ПО (СпрВидыЗапасов.Ссылка = ВидыЗапасовИТовары.ВидЗапасов)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК НомераГТД
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМира
	|			ПО НомераГТД.СтранаПроисхождения = СтраныМира.Ссылка
	|		ПО ВидыЗапасовИТовары.НомерГТД = НомераГТД.Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ВидыЗапасовИТовары.Заказ,
	|	ВидыЗапасовИТовары.АналитикаУчетаНоменклатуры,
	|	ВидыЗапасовИТовары.ВидЗапасов,
	|	ВидыЗапасовИТовары.НомерГТД,
	|	ЕСТЬNULL(НомераГТД.Код, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(СтраныМира.Код, НЕОПРЕДЕЛЕНО),
	|	КлючиАналитикиУчетаНоменклатуры.Номенклатура,
	|	КлючиАналитикиУчетаНоменклатуры.Характеристика,
	|	КлючиАналитикиУчетаНоменклатуры.Серия,
	|	СпрВидыЗапасов.ТипЗапасов,
	|	ВЫБОР
	|		КОГДА СпрВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА СпрВидыЗапасов.Контрагент
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВидыЗапасовИТовары.Упаковка,
	|	ВидыЗапасовИТовары.СтавкаНДС");
	
	Запрос.УстановитьПараметр("Ссылка"              , ДокументСсылка);
	Запрос.УстановитьПараметр("ДокументОснование"   , ДокументОснование);
	Запрос.УстановитьПараметр("ТребуетсяЗалогЗаТару", ПараметрыЗалогаЗаТару.ТребуетсяЗалогЗаТару);
	Запрос.УстановитьПараметр("ВернутьМногооборотнуюТару", ПараметрыЗалогаЗаТару.ВернутьМногооборотнуюТару);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Параметры:
// 	Товары - ТаблицаЗначений
// 	ДанныеДляВидовЗапасов - ТаблицаЗначений
Процедура ТаблицаТоваровСРаспределениемПоВидамЗапасов(Товары, ДанныеДляВидовЗапасов)
	
	ИспользоватьИмпортныеТовары = ПолучитьФункциональнуюОпцию("ИспользоватьИмпортныеТовары");
	
	ТаблицаТоваровСРаспределениемПоВидамЗапасов = Товары.СкопироватьКолонки();
	
	ТаблицаТоваровСРаспределениемПоВидамЗапасов.Колонки.Добавить("ТипЗапасов");
	ТаблицаТоваровСРаспределениемПоВидамЗапасов.Колонки.Добавить("Комитент");
	
	Если ИспользоватьИмпортныеТовары Тогда
		ТаблицаТоваровСРаспределениемПоВидамЗапасов.Колонки.Добавить("НомерТаможеннойДекларации");
		ТаблицаТоваровСРаспределениемПоВидамЗапасов.Колонки.Добавить("КодСтраныПроисхождения");
	КонецЕсли;
	
	Товары.Сортировать("НомерСтроки Убыв");
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Номенклатура"  , СтрокаТЧ.Номенклатура);
		СтруктураОтбора.Вставить("Характеристика", СтрокаТЧ.Характеристика);
		СтруктураОтбора.Вставить("Серия"         , СтрокаТЧ.Серия);
		СтруктураОтбора.Вставить("Заказ"         , СтрокаТЧ.Заказ);
		СтруктураОтбора.Вставить("СтавкаНДС"     , СтрокаТЧ.СтавкаНДС);
		
		МассивСтрокДанныеДляВидовЗапасов = ДанныеДляВидовЗапасов.НайтиСтроки(СтруктураОтбора);
		
		Если МассивСтрокДанныеДляВидовЗапасов.Количество() = 0 Тогда
			СтрокаТоваровСРаспределениемПоВидамЗапасов = ТаблицаТоваровСРаспределениемПоВидамЗапасов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТоваровСРаспределениемПоВидамЗапасов, СтрокаТЧ);
			
			Продолжить;
		КонецЕсли;
		
		
		КоличествоСтрокМассиваСтрокДанныхДляВидовЗапасов = МассивСтрокДанныеДляВидовЗапасов.Количество(); 
		ТекущаяСтрока = 1;
		
		Пока КоличествоСтрокМассиваСтрокДанныхДляВидовЗапасов >= ТекущаяСтрока Цикл
			Если СтрокаТЧ.КоличествоУпаковок = 0 Тогда
				Прервать;
			КонецЕсли;
			
			СтрокаДанныхДляВидовЗапасов = МассивСтрокДанныеДляВидовЗапасов[КоличествоСтрокМассиваСтрокДанныхДляВидовЗапасов - ТекущаяСтрока];
			
			СтрокаТоваровСРаспределениемПоВидамЗапасов = ТаблицаТоваровСРаспределениемПоВидамЗапасов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТоваровСРаспределениемПоВидамЗапасов, СтрокаТЧ);
			
			СтрокаТоваровСРаспределениемПоВидамЗапасов.ТипЗапасов = СтрокаДанныхДляВидовЗапасов.ТипЗапасов;
			СтрокаТоваровСРаспределениемПоВидамЗапасов.Комитент = СтрокаДанныхДляВидовЗапасов.Комитент;
			
			Если ИспользоватьИмпортныеТовары Тогда
				СтрокаТоваровСРаспределениемПоВидамЗапасов.НомерТаможеннойДекларации = СтрокаДанныхДляВидовЗапасов.НомерТаможеннойДекларации;
				СтрокаТоваровСРаспределениемПоВидамЗапасов.КодСтраныПроисхождения = СтрокаДанныхДляВидовЗапасов.КодСтраныПроисхождения;
			КонецЕсли;
			
			Если СтрокаТЧ.КоличествоУпаковок > СтрокаДанныхДляВидовЗапасов.КоличествоУпаковок Тогда 
				СтрокаТоваровСРаспределениемПоВидамЗапасов.КоличествоУпаковок = СтрокаДанныхДляВидовЗапасов.КоличествоУпаковок;
				СтрокаТоваровСРаспределениемПоВидамЗапасов.СуммаСкидки = Окр(СтрокаТЧ.СуммаСкидки / СтрокаТЧ.КоличествоУпаковок * СтрокаТоваровСРаспределениемПоВидамЗапасов.КоличествоУпаковок, 2);
				СтрокаТоваровСРаспределениемПоВидамЗапасов.СуммаСНДС = Окр(СтрокаТЧ.СуммаСНДС / СтрокаТЧ.КоличествоУпаковок * СтрокаТоваровСРаспределениемПоВидамЗапасов.КоличествоУпаковок, 2);
				СтрокаТоваровСРаспределениемПоВидамЗапасов.СуммаНДС = Окр(СтрокаТЧ.СуммаНДС / СтрокаТЧ.КоличествоУпаковок * СтрокаТоваровСРаспределениемПоВидамЗапасов.КоличествоУпаковок, 2);
				
				СтрокаТЧ.КоличествоУпаковок = СтрокаТЧ.КоличествоУпаковок - СтрокаТоваровСРаспределениемПоВидамЗапасов.КоличествоУпаковок;
				СтрокаТЧ.СуммаСкидки 		= СтрокаТЧ.СуммаСкидки - СтрокаТоваровСРаспределениемПоВидамЗапасов.СуммаСкидки;
				СтрокаТЧ.СуммаСНДС 			= СтрокаТЧ.СуммаСНДС - СтрокаТоваровСРаспределениемПоВидамЗапасов.СуммаСНДС;
				СтрокаТЧ.СуммаНДС  			= СтрокаТЧ.СуммаНДС - СтрокаТоваровСРаспределениемПоВидамЗапасов.СуммаНДС;
				
				ДанныеДляВидовЗапасов.Удалить(СтрокаДанныхДляВидовЗапасов);
			ИначеЕсли СтрокаТЧ.КоличествоУпаковок < СтрокаДанныхДляВидовЗапасов.КоличествоУпаковок Тогда
				СтрокаДанныхДляВидовЗапасов.КоличествоУпаковок = СтрокаДанныхДляВидовЗапасов.КоличествоУпаковок - СтрокаТЧ.КоличествоУпаковок;
				СтрокаТЧ.КоличествоУпаковок = 0;
			Иначе
				ДанныеДляВидовЗапасов.Удалить(СтрокаДанныхДляВидовЗапасов);
				СтрокаТЧ.КоличествоУпаковок = 0;
			КонецЕсли;
			
			ТекущаяСтрока = ТекущаяСтрока + 1;
		КонецЦикла;
		
	КонецЦикла;
	
	ТаблицаТоваровСРаспределениемПоВидамЗапасов.Сортировать("НомерСтроки Возр");
	
	Товары = ТаблицаТоваровСРаспределениемПоВидамЗапасов;
	
КонецПроцедуры

//-- Локализация

#КонецОбласти

#Область АктыОРасхождениях

//++ Локализация
Процедура ЗаполнитьШтрихкодыУпаковокПоАктуОРасхожденияхПослеОтгрузки(Объект, СтруктураПараметров)
	
	Если СтруктураПараметров.Свойство("АктОРасхожденияхПослеОтгрузкиОснование") Тогда
		ДокументОснованиеАктОРасхождениях = СтруктураПараметров.АктОРасхожденияхПослеОтгрузкиОснование;
		Если Не ЗначениеЗаполнено(ДокументОснованиеАктОРасхождениях) Тогда
			Возврат;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", ДокументОснованиеАктОРасхождениях);
		Запрос.УстановитьПараметр("ВариантыДействийПоРасхождениямКодовМаркировкиИСМППризнать", ИнтеграцияИСМПКлиентСервер.ВариантДействийПоРасхождениямКодовМаркировкиИСМППризнать());
		
		Если Объект.ВидКорректировки <> Перечисления.ХозяйственныеОперации.РеализацияПерепоставленногоТовара
			И Объект.ВидКорректировки <> Перечисления.ХозяйственныеОперации.ВозвратНедопоставленногоТовара Тогда
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	ШтрихкодыУпаковок.ШтрихкодУпаковки КАК ШтрихкодУпаковки
				|ИЗ
				|	Документ.АктОРасхожденияхПослеОтгрузки.ШтрихкодыУпаковок КАК ШтрихкодыУпаковок
				|ГДЕ
				|	ШтрихкодыУпаковок.Ссылка = &Ссылка";
			
		Иначе
			
			Если Объект.ВидКорректировки = Перечисления.ХозяйственныеОперации.РеализацияПерепоставленногоТовара Тогда
				Запрос.УстановитьПараметр("ТипРасхождения", Перечисления.ТипыРасхожденийИСМП.Излишек);
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ШтрихкодыУпаковок.ШтрихкодУпаковки КАК ШтрихкодУпаковки
				|ИЗ
				|	Документ.АктОРасхожденияхПослеОтгрузки.ШтрихкодыУпаковокРасхождения КАК Расхождения
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктОРасхожденияхПослеОтгрузки.ШтрихкодыУпаковок КАК ШтрихкодыУпаковок
				|		ПО Расхождения.ЗначениеШтрихкода = ШтрихкодыУпаковок.ЗначениеШтрихкода
				|			И Расхождения.Ссылка = ШтрихкодыУпаковок.Ссылка
				|ГДЕ
				|	ШтрихкодыУпаковок.Ссылка = &Ссылка
				|	И Расхождения.ТипРасхождения = &ТипРасхождения
				|	И Расхождения.Признан = &ВариантыДействийПоРасхождениямКодовМаркировкиИСМППризнать
				|	И Расхождения.Ссылка = &Ссылка
				|
				|СГРУППИРОВАТЬ ПО
				|	ШтрихкодыУпаковок.ШтрихкодУпаковки";
			ИначеЕсли Объект.ВидКорректировки = Перечисления.ХозяйственныеОперации.ВозвратНедопоставленногоТовара Тогда
				ТипыРасхожденияКВозврату = Новый Массив;
				ТипыРасхожденияКВозврату.Добавить(Перечисления.ТипыРасхожденийИСМП.Недостача);
				ТипыРасхожденияКВозврату.Добавить(Перечисления.ТипыРасхожденийИСМП.Брак);
				Запрос.УстановитьПараметр("ТипРасхождения", ТипыРасхожденияКВозврату);
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ШтрихкодыУпаковок.Ссылка КАК ШтрихкодУпаковки
				|ИЗ
				|	Документ.АктОРасхожденияхПослеОтгрузки.ШтрихкодыУпаковокРасхождения КАК Расхождения
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковок
				|		ПО Расхождения.ЗначениеШтрихкода = ШтрихкодыУпаковок.ЗначениеШтрихкода
				|			И Расхождения.Ссылка = &Ссылка
				|ГДЕ
				|	Расхождения.ТипРасхождения В (&ТипРасхождения)
				|	И Расхождения.Признан = &ВариантыДействийПоРасхождениямКодовМаркировкиИСМППризнать
				|
				|СГРУППИРОВАТЬ ПО
				|	ШтрихкодыУпаковок.Ссылка";
			КонецЕсли;
			
		КонецЕсли;
		
		Объект.ШтрихкодыУпаковок.Загрузить(Запрос.Выполнить().Выгрузить());
		
	КонецЕсли;
	
КонецПроцедуры

//-- Локализация

#КонецОбласти

#КонецОбласти
