////////////////////////////////////////////////////////////////////////////////
// Общий модуль КонвертацияЭДО
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает печатную форму электронного документа по двоичным данным файла.
// 
// Параметры:
// 		ДанныеЭлектронногоДокумента - ДвоичныеДанные - двоичные данные визуализируемого электронного документа.
// 		ДанныеЭлектронногоДокументаДляИзвлеченияПараметров - ДвоичныеДанные, Массив из ДвоичныеДанные - двоичные данные  
// 		электронного документа (или их массив) из которого необходимо извлечь параметры для визуализируемого документа.
// 		ПараметрыВизуализации - Структура - параметры визуализации электронного документа.
// 		КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики.
// 		 
// Возвращаемое значение:
//  Структура:
//   * ПредставлениеДокумента - Неопределено,ТабличныйДокумент
//   * Успех - Булево
Функция ВизуализацияЭлектронногоДокумента(ДанныеЭлектронногоДокумента,
											ДанныеЭлектронногоДокументаДляИзвлеченияПараметров = Неопределено, 
											ПараметрыВизуализации = Неопределено, 
											КонтекстДиагностики = Неопределено) Экспорт
	
	ПараметрыФайла = ПараметрыФайлаПроизвольногоДокумента(ДанныеЭлектронногоДокумента, КонтекстДиагностики);
	
	Если Не ЗначениеЗаполнено(ПараметрыФайла) Тогда
		РезультатФормирования = Новый Структура;
		РезультатФормирования.Вставить("ПредставлениеДокумента", Неопределено);
		РезультатФормирования.Вставить("Успех", Ложь);
		
		Возврат РезультатФормирования;
	КонецЕсли;
	
	ДополнительныеПараметрыПредставления = Новый Структура;
	
	Если ЗначениеЗаполнено(ДанныеЭлектронногоДокументаДляИзвлеченияПараметров) Тогда
		Если ТипЗнч(ДанныеЭлектронногоДокументаДляИзвлеченияПараметров) = Тип("Массив") Тогда
			Для Каждого ТекущиеДанные Из ДанныеЭлектронногоДокументаДляИзвлеченияПараметров Цикл
				ПараметрыФайлаИзвлеченияПараметров = ИзвлечьПараметрыЭлектронногоДокумента(ТекущиеДанные, КонтекстДиагностики);
				Если ПараметрыФайлаИзвлеченияПараметров <> Неопределено Тогда
					ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ДополнительныеПараметрыПредставления, ПараметрыФайлаИзвлеченияПараметров, Ложь);
				КонецЕсли;
			КонецЦикла;	
		Иначе	
			ПараметрыФайлаИзвлеченияПараметров = ИзвлечьПараметрыЭлектронногоДокумента(ДанныеЭлектронногоДокументаДляИзвлеченияПараметров, КонтекстДиагностики);
			Если ПараметрыФайлаИзвлеченияПараметров <> Неопределено Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ДополнительныеПараметрыПредставления, ПараметрыФайлаИзвлеченияПараметров);
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;	
	
	Если ПараметрыВизуализации <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ДополнительныеПараметрыПредставления, ПараметрыВизуализации);
	КонецЕсли;
	
	
	Если ПараметрыФайла.Свойство("ПараметрыПредставления") Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыФайла.ПараметрыПредставления, ДополнительныеПараметрыПредставления);
	Иначе
		ПараметрыФайла.Вставить("ПараметрыПредставления", ДополнительныеПараметрыПредставления);		
	КонецЕсли;
		
	Возврат ПредставлениеПроизвольногоДокумента(ДанныеЭлектронногоДокумента, ПараметрыФайла, КонтекстДиагностики);

КонецФункции

// Обработчик начального заполнения правил преобразования форматов.
// 
Процедура ВыполнитьЗаполнениеПравилПреобразования() Экспорт
	
	КаталогПравилПреобразования = ФайловаяСистема.СоздатьВременныйКаталог();	
	
	ПотокДанныхМакета = РегистрыСведений.ПравилаПреобразованияФорматов.ПолучитьМакет("ПравилаПреобразованияФорматов").ОткрытьПотокДляЧтения();
	
	АрхивПравилПреобразования = Новый ЧтениеZipФайла(ПотокДанныхМакета); 
 	АрхивПравилПреобразования.ИзвлечьВсе(КаталогПравилПреобразования);

	КонтекстДиагностики = ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики();

	ПровайдерПравил = НовыйФайловыйПровайдерПравил(КаталогПравилПреобразования);
	НачатьТранзакцию();
	Попытка
		ОбновитьОписаниеФорматовДляПреобразования(ПровайдерПравил, Неопределено, КонтекстДиагностики);
		ЗафиксироватьТранзакцию();
	Исключение

		ОтменитьТранзакцию();
		ТекстСообщения =  НСтр("ru = 'Не удалось выполнить заполнение правил преобразования форматов по причине:'") + Символы.ПС
			+ ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);

	КонецПопытки;
	
	ФайловаяСистема.УдалитьВременныйКаталог(КаталогПравилПреобразования);
	
КонецПроцедуры

Процедура ЗаполнитьПереченьСпециальныхОбстоятельствДокументаЭДО() Экспорт

	ДвоичныеДанныеКэша = РегистрыСведений.ПереченьСпециальныхОбстоятельствДокументаЭДО.ПолучитьМакет(
		"ПереченьСпециальныхОбстоятельствДокументаЭДО");
	Параметры = Новый Структура("ДанныеИзСервиса", ДвоичныеДанныеКэша);
	РегистрыСведений.ПереченьСпециальныхОбстоятельствДокументаЭДО.ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры);
		
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсиюНачальноеЗаполнение() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ЕстьЗаписи
		|ИЗ
		|	РегистрСведений.ФорматыДляПреобразования КАК ФорматыДляПреобразования
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ЕстьЗаписи
		|ИЗ
		|	РегистрСведений.ПереченьСпециальныхОбстоятельствДокументаЭДО КАК ПереченьСпецОбстоятельствДокументаЭДО";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Если РезультатЗапроса[0].Пустой() Тогда
		ВыполнитьЗаполнениеПравилПреобразования();
	КонецЕсли;

	Если РезультатЗапроса[1].Пустой() Тогда
		ЗаполнитьПереченьСпециальныхОбстоятельствДокументаЭДО();
	КонецЕсли;

КонецПроцедуры

Функция ПреобразоватьФорматЭД(ПотокИсходногоXML, Параметры = Неопределено, КонтекстДиагностики = Неопределено) Экспорт
	
	Если Параметры = Неопределено Тогда
		
		Преобразователь = КонвертацияЭДОПовтИсп.ПреобразованиеXSL_ПараметрыПроизвольногоДокумента();
		Если Преобразователь = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
	Иначе
		
		ТекстПравила = ТекстПравилаПреобразованияФормата(Параметры, КонтекстДиагностики);
		Если Не ЗначениеЗаполнено(ТекстПравила) Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Преобразователь = Новый ПреобразованиеXSL;
		Преобразователь.ЗагрузитьТаблицуСтилейXSLИзСтроки(ТекстПравила);
		
		ПараметрыXSL = Неопределено;
		Если Параметры.Свойство("ПараметрыXSL", ПараметрыXSL) И ЗначениеЗаполнено(ПараметрыXSL) Тогда
			
			Для Каждого ПараметрXSL Из ПараметрыXSL Цикл
				Если ТипЗнч(ПараметрXSL.Значение) = Тип("Структура") Тогда
					ЗначениеПараметра = КонвертироватьЗначениеПоИнструкции(ПараметрXSL.Значение);
					Если ЗначениеПараметра = Неопределено Тогда
						Продолжить;
					КонецЕсли;
				Иначе
					ЗначениеПараметра = ПараметрXSL.Значение;
				КонецЕсли;
				
				Если ТипЗнч(ЗначениеПараметра) = Тип("Строка") Тогда
					Если СтрНайти(ЗначениеПараметра, "'") <> 0 Тогда
						Если СтрНайти(ЗначениеПараметра, """") <> 0 Тогда
							// В параметрах кавычки не экранируются, поэтому заменяем двойные на две одинарные.
							ЗначениеПараметра = СтрЗаменить(ЗначениеПараметра, """", "''");
						КонецЕсли;
						ЗначениеПараметра = СтрШаблон("""%1""", ЗначениеПараметра);
					ИначеЕсли СтрНайти(ЗначениеПараметра, """") <> 0 Тогда
						// В параметрах кавычки не экранируются, поэтому заменяем двойные на две одинарные.
						ЗначениеПараметра = СтрЗаменить(ЗначениеПараметра, """", "''");
						ЗначениеПараметра = СтрШаблон("""%1""", ЗначениеПараметра);
					Иначе
						ЗначениеПараметра = СтрШаблон("'%1'", ЗначениеПараметра);
					КонецЕсли;
				ИначеЕсли ТипЗнч(ЗначениеПараметра) = Тип("Булево") Тогда
					ЗначениеПараметра = ?(ЗначениеПараметра, 1, 0);
				КонецЕсли;
				
				Преобразователь.ДобавитьПараметр(ПараметрXSL.Ключ, ЗначениеПараметра);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПотокИтоговогоXML = Новый ПотокВПамяти;
	
	ЗаписьИтоговогоXML = Новый ЗаписьXML;
	ЗаписьИтоговогоXML.ОткрытьПоток(ПотокИтоговогоXML);
	
	Попытка
		ПотокФайлаXML = ПотокФайлаДляПреобразованияXSL(ПотокИсходногоXML);
		ЧтениеИсходногоXML = Новый ЧтениеXML;
		ЧтениеИсходногоXML.ОткрытьПоток(ПотокФайлаXML);
		
		Преобразователь.Преобразовать(ЧтениеИсходногоXML, ЗаписьИтоговогоXML);
		
		ЧтениеИсходногоXML.Закрыть();
	Исключение
		ЗаписьИтоговогоXML.Закрыть();
		ПотокИтоговогоXML.Закрыть();
		
		ВидОперации = НСтр("ru = 'Преобразование формата.'");
		ВидОшибки = ОбработкаНеисправностейБЭДКлиентСервер.ВидОшибкиНеизвестнаяОшибка();
		
		ПредставлениеОшибкиПодробное = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		Ошибка = ОбработкаНеисправностейБЭД.НоваяОшибка(ВидОперации, ВидОшибки, ПредставлениеОшибкиПодробное, "");
		ОбработкаНеисправностейБЭД.ДобавитьОшибку(КонтекстДиагностики, Ошибка,
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСКонтрагентами);
		Возврат Неопределено;
	КонецПопытки;
	
	ЧтениеИсходногоXML.Закрыть();
	ЗаписьИтоговогоXML.Закрыть();
	
	Результат = Неопределено;
	Если Параметры = Неопределено Или Параметры.ИтоговыйФормат = "ПараметрыЭлектронногоДокумента" Тогда
		Результат = ЗначениеИзПотокаXML(ПотокИтоговогоXML, Тип("Структура"), Параметры, КонтекстДиагностики);
		ПотокИтоговогоXML.Закрыть();
		
	ИначеЕсли Параметры.ИтоговыйФормат = "ТабличныйДокумент" Тогда
		Результат = ЗначениеИзПотокаXML(ПотокИтоговогоXML, Тип("ТабличныйДокумент"), Параметры, КонтекстДиагностики);
		ПотокИтоговогоXML.Закрыть();
		
	ИначеЕсли Параметры.ИтоговыйФормат = "ТаблицаНоменклатуры" Тогда
		Результат = ЗначениеИзПотокаXML(ПотокИтоговогоXML, Тип("ТаблицаЗначений"), Параметры, КонтекстДиагностики);
		ПотокИтоговогоXML.Закрыть();
		
	ИначеЕсли Параметры.ИтоговыйФормат = "CML" Тогда
		Результат = ПотокИтоговогоXML.ЗакрытьИПолучитьДвоичныеДанные();
		
	КонецЕсли;
	
	Если ТипЗнч(Результат) = Тип("ТабличныйДокумент")
		И (Результат.ВысотаТаблицы = 0 Или Результат.ШиринаТаблицы = 0) Тогда
		// Могли не определиться размеры табличного документа, тогда выводим его в новый табличный документ.
		ТабличныйДокумент = Новый ТабличныйДокумент;
		ТабличныйДокумент.Вывести(Результат);
		Результат = ТабличныйДокумент;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПараметрыФайлаПроизвольногоДокумента(ФайлXML, КонтекстДиагностики = Неопределено, ПроверятьПоСхемеXML = Истина) Экспорт
	
	Если Не ЗначениеЗаполнено(ФайлXML) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(ФайлXML) = Тип("Строка") Тогда
		ПотокИсходногоXML = Новый ФайловыйПоток(ФайлXML, РежимОткрытияФайла.Открыть);
	Иначе
		ПотокИсходногоXML = ФайлXML.ОткрытьПотокДляЧтения();
	КонецЕсли;
	
	ПараметрыФайлаXML = ПреобразоватьФорматЭД(ПотокИсходногоXML);
	Если ПараметрыФайлаXML = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ПроверятьПоСхемеXML И ПараметрыФайлаXML.Свойство("ИмяТипаXML") Тогда
		ФайлСоответствуетСхемеXML = ПроверитьЭлектронныйДокументПоСхемеXML(ПотокИсходногоXML, ПараметрыФайлаXML, КонтекстДиагностики);
	Иначе
		ФайлСоответствуетСхемеXML = Истина;
	КонецЕсли;
	
	ПотокИсходногоXML.Закрыть();
	
	Если НЕ ФайлСоответствуетСхемеXML Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ПараметрыФайлаXML.Свойство("ОтражениеВУчете") И ЗначениеЗаполнено(ПараметрыФайлаXML.ОтражениеВУчете) Тогда
		Если ПараметрыФайлаXML.ОтражениеВУчете.ВариантЗаполнения = "СвРК" Тогда
			ПараметрыФайлаXML.ТипДокумента = "СведенияОРеализацииКомиссионером";
		ИначеЕсли ПараметрыФайлаXML.ОтражениеВУчете.ВариантЗаполнения = "СвИСРК" Тогда
			ПараметрыФайлаXML.ТипДокумента = "КорректировкаСведенийОРеализацииКомиссионером";
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыФайлаXML.ТипДокумента = "МашиночитаемаяДоверенность"
		И НЕ ПараметрыФайлаXML.Свойство("ОтражениеВУчете") Тогда
		ПараметрыФайлаXML.Вставить("ОтражениеВУчете", Неопределено);			
	КонецЕсли;
	
	Возврат ПараметрыФайлаXML;
	
КонецФункции

Функция ДанныеCMLПроизвольногоДокумента(ДанныеXML, ПараметрыДокумента = Неопределено) Экспорт
	
	ПотокИсходногоXML = ДанныеXML.ОткрытьПотокДляЧтения();
	
	ИсходныйФормат = Неопределено;
	ВариантЗаполнения = Неопределено;
	ОтражениеВУчете = Неопределено;
	Если ПараметрыДокумента = Неопределено 
		ИЛИ НЕ ПараметрыДокумента.Свойство("ИсходныйФормат", ИсходныйФормат)
		ИЛИ НЕ ЗначениеЗаполнено(ИсходныйФормат) Тогда
		ПараметрыФайлаXML = ПреобразоватьФорматЭД(ПотокИсходногоXML);
		Если ПараметрыФайлаXML = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		ПараметрыФайлаXML.Свойство("ИсходныйФормат", ИсходныйФормат);
		ПараметрыФайлаXML.Свойство("ВариантЗаполнения", ВариантЗаполнения);
		ПараметрыФайлаXML.Свойство("ОтражениеВУчете", ОтражениеВУчете);
		ПотокИсходногоXML.Перейти(0, ПозицияВПотоке.Начало);
	Иначе
		ПараметрыДокумента.Свойство("ИсходныйФормат", ИсходныйФормат);
		ПараметрыДокумента.Свойство("ВариантЗаполнения", ВариантЗаполнения);
		ПараметрыДокумента.Свойство("ОтражениеВУчете", ОтражениеВУчете);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ИсходныйФормат) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтражениеВУчете)
		И ОтражениеВУчете.Свойство("Формат")
		И ОтражениеВУчете.Формат = "CML" Тогда
		ОтражениеВУчете.Свойство("ВариантЗаполнения", ВариантЗаполнения);
	КонецЕсли;
	
	ПараметрыПреобразования = Новый Структура;
	ПараметрыПреобразования.Вставить("ИтоговыйФормат", "CML");
	ПараметрыПреобразования.Вставить("ИсходныйФормат", ИсходныйФормат);
	Если ВариантЗаполнения <> Неопределено Тогда
		ПараметрыПреобразования.Вставить("ВариантЗаполнения", ВариантЗаполнения);
	КонецЕсли;
	
	Если ПараметрыДокумента <> Неопределено И ПараметрыДокумента.Свойство("ПараметрыОтражения") Тогда
		ПараметрыПреобразования.Вставить("ПараметрыXSL", ПараметрыДокумента.ПараметрыОтражения);
	КонецЕсли;
	
	ДанныеCML = ПреобразоватьФорматЭД(ПотокИсходногоXML, ПараметрыПреобразования);
	
	ПотокИсходногоXML.Закрыть();
	
	Возврат ДанныеCML;
	
КонецФункции

Функция ПолучитьПараметрыЭлектронногоДокумента(ДанныеЭлектронногоДокумента, КонтекстДиагностики = Неопределено) Экспорт
	
	Возврат ИзвлечьПараметрыЭлектронногоДокумента(ДанныеЭлектронногоДокумента, КонтекстДиагностики);
	
КонецФункции

// Текст правила преобразования формата.
// 
// Параметры:
//  ПараметрыПравила - Структура:
// * ИсходныйФормат - Строка
// * ИтоговыйФормат - Строка
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
// 
// Возвращаемое значение:
//  - Неопределено
//  - Строка
Функция ТекстПравилаПреобразованияФормата(ПараметрыПравила, КонтекстДиагностики = Неопределено) Экспорт
	
	ПровайдерПравил = НовыйПровайдерПравилИзСервиса();
	ОбновитьОписаниеФорматовДляПреобразования(ПровайдерПравил, ПараметрыПравила, КонтекстДиагностики);
	
	ДополнитьПараметрыПреобразования(ПараметрыПравила, КонтекстДиагностики);
	Если Не ЗначениеЗаполнено(ПараметрыПравила.ВерсияИтоговогоФормата) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Правило
	|ИЗ
	|	РегистрСведений.ПравилаПреобразованияФорматов
	|ГДЕ
	|	ИсходныйФормат = &ИсходныйФормат
	|	И ВариантЗаполнения = &ВариантЗаполнения
	|	И ИтоговыйФормат = &ИтоговыйФормат
	|	И ВерсияИтоговогоФормата В (&ВерсияИтоговогоФормата)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВерсияИтоговогоФормата УБЫВ";
	
	Запрос.УстановитьПараметр("ИсходныйФормат",         ПараметрыПравила.ИсходныйФормат);
	Запрос.УстановитьПараметр("ВариантЗаполнения",      ПараметрыПравила.ВариантЗаполнения);
	Запрос.УстановитьПараметр("ИтоговыйФормат",         ПараметрыПравила.ИтоговыйФормат);
	Запрос.УстановитьПараметр("ВерсияИтоговогоФормата", ПараметрыПравила.ВерсияИтоговогоФормата);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	ТекстПравила = Неопределено;
	Если Выборка.Следующий() И Выборка.Правило <> Неопределено Тогда
		ТекстПравила = Выборка.Правило.Получить();
	КонецЕсли;
	
	Возврат ТекстПравила;
	
КонецФункции

#Область ПоставляемыеДанные	
	
Процедура ПриРегистрацииОбработчиковПоставляемыхДанных(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.ВидДанных = "ПравилаПреобразованияФорматовЭДО20";
	Обработчик.КодОбработчика = "ПравилаПреобразованияФорматовЭДО20";
	Обработчик.Обработчик = ОбщегоНазначения.ОбщийМодуль("КонвертацияЭДО");
	
КонецПроцедуры	

// Вызывается при получении уведомления о новых данных.
// В теле следует проверить, необходимы ли эти данные приложению,
// и если да - установить флажок Загружать.
// 
// Параметры:
//   Дескриптор - ОбъектXDTO - Descriptor.
//   Загружать  - Булево - возвращаемое.
//
Процедура ДоступныНовыеДанные(Знач Дескриптор, Загружать) Экспорт
	
	Если Дескриптор.DataType = "ПравилаПреобразованияФорматовЭДО20" Тогда
		
		ДатаЗагрузки = Константы.ДатаЗагрузкиОписанияФорматовДляПреобразования.Получить();
		
		ДатаОбновления = XMLЗначение(Тип("Дата"), Дескриптор.Properties.Property.Получить(0).Value);
		
		Если ДатаОбновления >= ДатаЗагрузки Тогда
			Загружать = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается после вызова ДоступныНовыеДанные, позволяет разобрать данные.
//
// Параметры:
//   Дескриптор - ОбъектXDTO - Дескриптор.
//   ПутьКФайлу - Строка - Полное имя извлеченного файла. Файл будет автоматически удален
//                  после завершения процедуры.
//
Процедура ОбработатьНовыеДанные(Знач Дескриптор, Знач ПутьКФайлу) Экспорт
	
	Если Дескриптор.DataType = "ПравилаПреобразованияФорматовЭДО20" Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		ЗагруженоБезОшибок = Истина;
		ТекущаяУниверсальнаяДата = ТекущаяУниверсальнаяДата();
		
		ВременныйКаталог = РаботаСФайламиБЭД.ВременныйКаталог();
		
		ЧтениеZipФайла = Новый ЧтениеZipФайла(ПутьКФайлу);
		ЧтениеZipФайла.ИзвлечьВсе(ВременныйКаталог);
		ЧтениеZipФайла.Закрыть();
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФорматыДляПреобразования.ИсходныйФормат КАК ИсходныйФормат,
		|	ФорматыДляПреобразования.ДатаОбновленияПравил КАК ДатаОбновленияПравил,
		|	ФорматыДляПреобразования.СсылкаНаРесурс КАК СсылкаНаРесурс
		|ИЗ
		|	РегистрСведений.ФорматыДляПреобразования КАК ФорматыДляПреобразования";
		
		ТаблицаФорматов = Запрос.Выполнить().Выгрузить();
		ТаблицаФорматов.Колонки.Добавить("Обработан", Новый ОписаниеТипов("Булево"));
		
		ПутьКФайлуОписанияФорматов = ВременныйКаталог + "supported_formats_v20.xml";
		ОписаниеФормата = Новый ЧтениеXML;
		ОписаниеФормата.ОткрытьФайл(ПутьКФайлуОписанияФорматов);
		
		Пока ОписаниеФормата.Прочитать() Цикл
			
			Если ОписаниеФормата.Имя <> "fmt"
				ИЛИ ОписаниеФормата.ТипУзла <> ТипУзлаXML.НачалоЭлемента Тогда
				Продолжить;
			КонецЕсли;
			
			ИсходныйФормат = ОписаниеФормата.ЗначениеАтрибута("id");
			ДатаОбновленияПравил = Дата(ОписаниеФормата.ЗначениеАтрибута("upd"));
			СсылкаНаОписаниеПравил = ОписаниеФормата.ЗначениеАтрибута("src");
			
			НайденныйФормат = ТаблицаФорматов.Найти(ИсходныйФормат, "ИсходныйФормат");
			Если НайденныйФормат = Неопределено
				ИЛИ ДатаОбновленияПравил > НайденныйФормат.ДатаОбновленияПравил
				ИЛИ СсылкаНаОписаниеПравил <> НайденныйФормат.СсылкаНаРесурс Тогда
				
				НачатьТранзакцию();
				
				Попытка
					
					НаборПравилПреобразования = РегистрыСведений.ПравилаПреобразованияФорматов.СоздатьНаборЗаписей();
					НаборПравилПреобразования.Отбор.ИсходныйФормат.Установить(ИсходныйФормат);
					
					ПутьКФайлуОписанияПравил = ВременныйКаталог + СсылкаНаОписаниеПравил;
					ОписаниеПравила = Новый ЧтениеXML;
					ОписаниеПравила.ОткрытьФайл(ПутьКФайлуОписанияПравил);
					
					Пока ОписаниеПравила.Прочитать() Цикл
						
						Если ОписаниеПравила.Имя <> "rule"
							ИЛИ ОписаниеПравила.ТипУзла <> ТипУзлаXML.НачалоЭлемента Тогда
							Продолжить;
						КонецЕсли;
						
						ПутьКФайлуОписанияПравил = ВременныйКаталог + ОписаниеПравила.ЗначениеАтрибута("src");
						ЧтениеТекстаПравил = Новый ЧтениеТекста(ПутьКФайлуОписанияПравил, "UTF-8");
						ТекстПравила = ЧтениеТекстаПравил.Прочитать();
						ЧтениеТекстаПравил.Закрыть();
						Правило = Новый ХранилищеЗначения(ТекстПравила, Новый СжатиеДанных());
						
						НоваяЗапись = НаборПравилПреобразования.Добавить();
						НоваяЗапись.ИсходныйФормат         = ИсходныйФормат;
						НоваяЗапись.ВариантЗаполнения      = ОписаниеПравила.ЗначениеАтрибута("type");
						НоваяЗапись.ИтоговыйФормат         = ОписаниеПравила.ЗначениеАтрибута("out");
						НоваяЗапись.ВерсияИтоговогоФормата = ОписаниеПравила.ЗначениеАтрибута("ver");
						НоваяЗапись.ДатаОбновления         = Дата(ОписаниеПравила.ЗначениеАтрибута("upd"));
						НоваяЗапись.Правило                = Правило;
						
					КонецЦикла;
					
					НаборПравилПреобразования.Записать();
					
					ОписаниеПравила.Закрыть();
					
					МенеджерЗаписи = РегистрыСведений.ФорматыДляПреобразования.СоздатьМенеджерЗаписи();
					МенеджерЗаписи.ИсходныйФормат       = ИсходныйФормат;
					МенеджерЗаписи.ДатаОбновленияПравил = ДатаОбновленияПравил;
					МенеджерЗаписи.СсылкаНаРесурс       = СсылкаНаОписаниеПравил;
					МенеджерЗаписи.Записать();
					
					ЗафиксироватьТранзакцию();
				Исключение
					ОтменитьТранзакцию();
					
					ЗагруженоБезОшибок = Ложь;
					
					ВидОперации = НСтр("ru = 'Загрузка правил преобразования форматов электронных документов.'");
					ТекстОшибки = СтрШаблон(
						НСтр("ru = 'Не удалось загрузить правила преобразования формата %1.'"), ИсходныйФормат)
						+ Символы.ПС + НСтр("ru = 'По причине:'")
						+ Символы.ПС + ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					ЭлектронноеВзаимодействие.ОбработатьОшибку(ВидОперации, ТекстОшибки);
				КонецПопытки;
				
			КонецЕсли;
			
			Если НайденныйФормат <> Неопределено Тогда
				НайденныйФормат.Обработан = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
		ОписаниеФормата.Закрыть();
		
		ПараметрыОтбора = Новый Структура("Обработан", Ложь);
		ФорматыКУдалению = ТаблицаФорматов.НайтиСтроки(ПараметрыОтбора);
		Если ФорматыКУдалению.Количество() Тогда
			Для Каждого СвойстваФормата Из ФорматыКУдалению Цикл
				
				НачатьТранзакцию();
				Попытка
					МенеджерЗаписи = РегистрыСведений.ФорматыДляПреобразования.СоздатьМенеджерЗаписи();
					МенеджерЗаписи.ИсходныйФормат = СвойстваФормата.ИсходныйФормат;
					МенеджерЗаписи.Удалить();
					
					НаборЗаписей = РегистрыСведений.ПравилаПреобразованияФорматов.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.ИсходныйФормат.Установить(СвойстваФормата.ИсходныйФормат);
					НаборЗаписей.Записать();
					
					ЗафиксироватьТранзакцию();
				Исключение
					ОтменитьТранзакцию();
					
					ЗагруженоБезОшибок = Ложь;
					
					ВидОперации = НСтр("ru = 'Загрузка правил преобразования форматов электронных документов.'");
					ТекстОшибки = СтрШаблон(
					НСтр("ru = 'Не удалось удалить неиспользуемые правила преобразования формата %1 из информационной базы.'"),
					СвойстваФормата.ИсходныйФормат)
					+ Символы.ПС + НСтр("ru = 'По причине:'")
					+ Символы.ПС + ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					ЭлектронноеВзаимодействие.ОбработатьОшибку(ВидОперации, ТекстОшибки);
				КонецПопытки;
				
			КонецЦикла;
		КонецЕсли;
		
		Если ЗагруженоБезОшибок Тогда
			Константы.ДатаЗагрузкиОписанияФорматовДляПреобразования.Установить(ТекущаяУниверсальнаяДата);
		КонецЕсли;
		
		РаботаСФайламиБЭД.УдалитьВременныеФайлы(ВременныйКаталог);
		
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при отмене обработки данных в случае сбоя.
//
// Параметры:
//   Дескриптор - ОбъектXDTO - Descriptor.
//@skip-warning
Процедура ОбработкаДанныхОтменена(Знач Дескриптор) Экспорт

КонецПроцедуры

	
#КонецОбласти	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область КонвертерЭлектронныхДокументов

Функция ПредставлениеПроизвольногоДокумента(ФайлXML, ПараметрыДокумента, КонтекстДиагностики = Неопределено)
	
	РезультатФормирования = Новый Структура;
	РезультатФормирования.Вставить("ПредставлениеДокумента", Неопределено);
	РезультатФормирования.Вставить("Успех", Ложь);
	
	Если ЗначениеЗаполнено(ПараметрыДокумента.ИсходныйФормат)
		И Не ДляФорматаДоступноФормированиеТабличногоДокумента(ПараметрыДокумента.ИсходныйФормат) Тогда
		Возврат РезультатФормирования;
	КонецЕсли;
	
	ПараметрыПреобразования = Новый Структура;
	ПараметрыПреобразования.Вставить("ИтоговыйФормат", "ТабличныйДокумент");
	ПараметрыПреобразования.Вставить("ИсходныйФормат", ПараметрыДокумента.ИсходныйФормат);
	ВариантЗаполнения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыДокумента, "ВариантЗаполнения", "");
	ПараметрыПреобразования.Вставить("ВариантЗаполнения", ВариантЗаполнения);
	Если ПараметрыДокумента.Свойство("ПараметрыПредставления") Тогда
		ПараметрыПреобразования.Вставить("ПараметрыXSL", ПараметрыДокумента.ПараметрыПредставления);
	КонецЕсли;
	
	Если ТипЗнч(ФайлXML) = Тип("Строка") Тогда
		ПотокИсходногоXML = Новый ФайловыйПоток(ФайлXML, РежимОткрытияФайла.Открыть);
	Иначе
		ПотокИсходногоXML = ФайлXML.ОткрытьПотокДляЧтения();
	КонецЕсли;
	
	ТабличныйДокумент = ПреобразоватьФорматЭД(ПотокИсходногоXML, ПараметрыПреобразования);
	
	ПотокИсходногоXML.Закрыть();
	
	РезультатФормирования.ПредставлениеДокумента = ТабличныйДокумент;
	РезультатФормирования.Успех = (ТабличныйДокумент <> Неопределено);
	
	Возврат РезультатФормирования;
	
КонецФункции

Функция ИзвлечьПараметрыЭлектронногоДокумента(ДанныеЭлектронногоДокумента, КонтекстДиагностики = Неопределено)
	
	ПараметрыФайла = ПараметрыФайлаПроизвольногоДокумента(ДанныеЭлектронногоДокумента, КонтекстДиагностики);
	
	Если ПараметрыФайла <> Неопределено Тогда
		ПараметрыФайла.Вставить("ИтоговыйФормат", "ПараметрыЭлектронногоДокумента");
	КонецЕсли;
	
	Возврат ПреобразоватьФорматЭД(ДанныеЭлектронногоДокумента.ОткрытьПотокДляЧтения(), ПараметрыФайла, КонтекстДиагностики);
	
КонецФункции

Функция КонвертироватьЗначениеПоИнструкции(Инструкция)
	
	Результат = Неопределено;
	
	Если Инструкция.Правило = "СуммаПрописью" Тогда
		
		Если Не Инструкция.Свойство("СуммаДокумента")
			Или Не Инструкция.Свойство("КодВалюты") Тогда
			Возврат Результат;
		КонецЕсли;
		
		ОбменСКонтрагентамиПереопределяемый.СуммаПрописью(Инструкция.СуммаДокумента, Инструкция.КодВалюты, Результат);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ЗначениеИзПотокаXML(ПотокXML, ТипЗначения, ПараметрыПреобразования, КонтекстДиагностики)
	
	ПотокXML.Перейти(0, ПозицияВПотоке.Начало);
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьПоток(ПотокXML);
	Попытка
		Результат = СериализаторXDTO.ПрочитатьXML(ЧтениеXML, ТипЗначения);
	Исключение
		Результат = Неопределено;

		ВидОперации = НСтр("ru = 'Преобразование формата.'");
		ВидОшибки  = ОбработкаНеисправностейБЭДКлиентСервер.ВидОшибкиНеизвестнаяОшибка();
		ТекстОшибки = НСтр("ru = 'Не удалось сериализовать результат преобразования формата.'")
			 			+ ПараметрыПреобразованияВСтроку(ПараметрыПреобразования);

		ПредставлениеОшибкиКраткое = ТекстОшибки;
		ПредставлениеОшибкиПодробное = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	
		Ошибка = ОбработкаНеисправностейБЭД.НоваяОшибка(
					ВидОперации, ВидОшибки, ПредставлениеОшибкиПодробное, ПредставлениеОшибкиКраткое);
		ОбработкаНеисправностейБЭД.ДобавитьОшибку(КонтекстДиагностики, Ошибка,
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСКонтрагентами);
	КонецПопытки;
	
	ЧтениеXML.Закрыть();
	
	Возврат Результат;
	
КонецФункции

Функция ПроверитьЭлектронныйДокументПоСхемеXML(ПотокИсходногоXML, ПараметрыФайлаXML, КонтекстДиагностики = Неопределено)
	
	URIПространстваИмен = Неопределено;
	Если НЕ (ПараметрыФайлаXML.Свойство("ИсходныйФормат", URIПространстваИмен)
		И ЗначениеЗаполнено(URIПространстваИмен)) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИмяТипаXML = Неопределено;
	Если НЕ (ПараметрыФайлаXML.Свойство("ИмяТипаXML", ИмяТипаXML)
		И ЗначениеЗаполнено(ИмяТипаXML)) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Фабрика = КонвертацияЭДОПовтИсп.ФабрикаXDTOЭлектронногоДокумента(URIПространстваИмен);
	Если Фабрика = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТипЗначенияXDTO = Фабрика.Тип(URIПространстваИмен, ИмяТипаXML);
	Если ТипЗначенияXDTO = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПотокКонтрольногоXML = Новый ПотокВПамяти;
	
	ПотокИсходногоXML.Перейти(0, ПозицияВПотоке.Начало);
	
	Попытка
		ЧтениеИсходногоXML = Новый ЧтениеXML;
		ЧтениеИсходногоXML.ОткрытьПоток(ПотокИсходногоXML);
		
		ПостроительDOM = Новый ПостроительDOM;
		ДокументDOM = ПостроительDOM.Прочитать(ЧтениеИсходногоXML);
		ЧтениеИсходногоXML.Закрыть();
		
		ДокументDOM.ЭлементДокумента.УстановитьСоответствиеПространстваИмен("", URIПространстваИмен);
		
		ЗаписьКонтрольногоXML = Новый ЗаписьXML;
		ЗаписьКонтрольногоXML.ОткрытьПоток(ПотокКонтрольногоXML);
		
		ЗаписьDOM = Новый ЗаписьDOM;
		ЗаписьDOM.Записать(ДокументDOM, ЗаписьКонтрольногоXML);
		ЗаписьКонтрольногоXML.Закрыть();
		
		ПотокКонтрольногоXML.Перейти(0, ПозицияВПотоке.Начало);
		
		ЧтениеКонтрольногоXML = Новый ЧтениеXML;
		ЧтениеКонтрольногоXML.ОткрытьПоток(ПотокКонтрольногоXML);
		
		ИсходныйЭД = Фабрика.ПрочитатьXML(ЧтениеКонтрольногоXML, ТипЗначенияXDTO);
		
		Результат = Истина;
	Исключение
		Результат = Ложь;
		
		ВидОперации = НСтр("ru = 'Преобразование формата.'");
		ВидОшибки = ОбработкаНеисправностейБЭДКлиентСервер.ВидОшибкиНеизвестнаяОшибка();

		МассивСтрок = Новый Массив;
		МассивСтрок.Добавить(НСтр("ru = 'Не удалось преобразовать файл электронного документа с параметрами:'"));		
		МассивСтрок.Добавить(НСтр("ru = 'ИсходныйФормат='") + ПараметрыФайлаXML.ИсходныйФормат);
		МассивСтрок.Добавить(НСтр("ru = 'ИмяТипаXML='") + ПараметрыФайлаXML.ИмяТипаXML);
		Если ПараметрыФайлаXML.Свойство("ИдентификаторДокумента") Тогда
			МассивСтрок.Добавить(НСтр("ru = 'ИдентификаторДокумента='") + ПараметрыФайлаXML.ИдентификаторДокумента);
		КонецЕсли;
		
		ПредставлениеОшибкиКраткое = СтрСоединить(МассивСтрок, Символы.ПС);
		ПредставлениеОшибкиПодробное = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		 		
		Ошибка = ОбработкаНеисправностейБЭД.НоваяОшибка(
					ВидОперации, ВидОшибки, ПредставлениеОшибкиПодробное, ПредставлениеОшибкиКраткое);
		ОбработкаНеисправностейБЭД.ДобавитьОшибку(КонтекстДиагностики, Ошибка,
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСКонтрагентами);
	КонецПопытки;
	
	ЧтениеКонтрольногоXML.Закрыть();
	ПотокКонтрольногоXML.Закрыть();
	
	Возврат Результат;
	
КонецФункции

// Дополняет неинициализированные или переопределяемые поля параметров подбора правил
// 
// Параметры:
//  ПараметрыПравила - См. ТекстПравилаПреобразованияФормата.ПараметрыПравила
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики 
Процедура ДополнитьПараметрыПреобразования(ПараметрыПравила, КонтекстДиагностики = Неопределено)
	
	ВерсияИтоговогоФормата = ПоддерживаемаяВерсияИтоговогоФормата(ПараметрыПравила.ИтоговыйФормат);
	ПараметрыПравила.Вставить("ВерсияИтоговогоФормата", ВерсияИтоговогоФормата);
	Если Не ЗначениеЗаполнено(ВерсияИтоговогоФормата) Тогда
		ВидОперации = НСтр("ru = 'Преобразование формата.'");
		ВидОшибки = ОбработкаНеисправностейБЭДКлиентСервер.ВидОшибкиНеизвестнаяОшибка();
		ТекстОшибки = НСтр("ru = 'Не удалось определить версию итогового формата для преобразования.'")
			+ ПараметрыПреобразованияВСтроку(ПараметрыПравила);
		
		Ошибка = ОбработкаНеисправностейБЭД.НоваяОшибка(ВидОперации, ВидОшибки, ТекстОшибки, ТекстОшибки);
		ОбработкаНеисправностейБЭД.ДобавитьОшибку(КонтекстДиагностики, Ошибка,
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСКонтрагентами);
	КонецЕсли;
	
	Если Не ПараметрыПравила.Свойство("ВариантЗаполнения") Тогда
		ПараметрыПравила.Вставить("ВариантЗаполнения", "");
	КонецЕсли;
	
КонецПроцедуры

// Возвращает массив поддерживаемых версий переданного итогового формата.
//
// Параметры:
//
// Возвращаемое значение:
//   Массив из Строка - версии формата.
//
Функция ПоддерживаемаяВерсияИтоговогоФормата(ИтоговыйФормат)
	
	ВерсииФормата = Новый Массив;
	
	Если ИтоговыйФормат = "ПараметрыЭлектронногоДокумента" Тогда
		ВерсииФормата.Добавить("2.0");
		ВерсииФормата.Добавить("2.1");
	ИначеЕсли ИтоговыйФормат = "СхемаXML" Тогда
		ВерсииФормата.Добавить("1.0");
	ИначеЕсли ИтоговыйФормат = "ТаблицаНоменклатуры" Тогда
		ВерсииФормата.Добавить("1.0");
	ИначеЕсли ИтоговыйФормат = "ТабличныйДокумент" Тогда
		ВерсииФормата.Добавить("1.0");
	ИначеЕсли ИтоговыйФормат = "CML" Тогда
		ВерсииФормата.Добавить(ФорматыЭДО_CML.ВерсияСхемыCML208());
	КонецЕсли;
	
	Возврат ВерсииФормата;
	
КонецФункции

// Таблица сведений о ресурсах, содержащих структуру правил форматов и их актуальности.
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
// * ИсходныйФормат - Строка
// * ДатаОбновленияПравил - Дата
// * СсылкаНаРесурс - Строка
Функция НоваяТаблицаОписанийФорматов()
	ОписанияФорматов = Новый ТаблицаЗначений;
	ОписанияФорматов.Колонки.Добавить("ИсходныйФормат", Новый ОписаниеТипов("Строка"));
	ОписанияФорматов.Колонки.Добавить("ДатаОбновленияПравил", Новый ОписаниеТипов("Дата"));
	ОписанияФорматов.Колонки.Добавить("СсылкаНаРесурс", Новый ОписаниеТипов("Строка"));
	Возврат ОписанияФорматов;
КонецФункции

// Синхронизирует кеш форматов информационной базы с провайдером правил.
// Возвращает измененные форматы. 
// 
// Параметры:
//  Провайдер - См. НовыйПровайдерПравилФорматов
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
// 
// Возвращаемое значение:
//  См. НоваяТаблицаОписанийФорматов
Функция СинхронизироватьОписаниеФорматовСПровайдером(Провайдер, КонтекстДиагностики)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ФорматыДляПреобразования.ИсходныйФормат КАК ИсходныйФормат,
	|	ФорматыДляПреобразования.ДатаОбновленияПравил КАК ДатаОбновленияПравил,
	|	ФорматыДляПреобразования.СсылкаНаРесурс КАК СсылкаНаРесурс,
	|	ЛОЖЬ КАК Обработан
	|ИЗ
	|	РегистрСведений.ФорматыДляПреобразования КАК ФорматыДляПреобразования";
	
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаФорматов = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ПотокФайла = ПотокДляЧтенияФайлаПровайдера(Провайдер, "supported_formats_v20.xml", КонтекстДиагностики);
	Если ПотокФайла = Неопределено Тогда
		Возврат НоваяТаблицаОписанийФорматов();
	КонецЕсли;
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьПоток(ПотокФайла);
	НаборЗаписей = РегистрыСведений.ФорматыДляПреобразования.СоздатьНаборЗаписей();
	Пока ЧтениеXML.Прочитать() Цикл
		Если ЧтениеXML.Имя <> "fmt" Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущийФормат = ТаблицаФорматов.Найти(ЧтениеXML.ЗначениеАтрибута("id"), "ИсходныйФормат");
		Если ТекущийФормат = Неопределено
			Или Дата(ЧтениеXML.ЗначениеАтрибута("upd")) > ТекущийФормат.ДатаОбновленияПравил
			Или ЧтениеXML.ЗначениеАтрибута("src") <> ТекущийФормат.СсылкаНаРесурс Тогда
			
			Формат = ЧтениеXML.ЗначениеАтрибута("id");
			
			НаборЗаписей.Очистить();
			НаборЗаписей.Отбор.ИсходныйФормат.Установить(Формат);
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.ИсходныйФормат = Формат;
			НоваяЗапись.ДатаОбновленияПравил = Дата(ЧтениеXML.ЗначениеАтрибута("upd"));
			НоваяЗапись.СсылкаНаРесурс = ЧтениеXML.ЗначениеАтрибута("src");
			УстановитьПривилегированныйРежим(Истина);
			НаборЗаписей.Записать();
			УстановитьПривилегированныйРежим(Ложь);
			
			Если ТекущийФормат <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(ТекущийФормат, НоваяЗапись);
			КонецЕсли;
			
		КонецЕсли;
		
		Если ТекущийФормат = Неопределено Тогда
			ТекущийФормат = ТаблицаФорматов.Добавить();
			ЗаполнитьЗначенияСвойств(ТекущийФормат, НоваяЗапись);
		КонецЕсли;
		ТекущийФормат.Обработан = Истина;
	КонецЦикла;
	
	ЧтениеXML.Закрыть();
	ПотокФайла.Закрыть();
	
	ОтборПоПолюОбработан = Новый Структура("Обработан", Ложь);
	УстаревшиеФорматы = ТаблицаФорматов.НайтиСтроки(ОтборПоПолюОбработан);
	Если ЗначениеЗаполнено(УстаревшиеФорматы) Тогда
		Для Каждого ОписаниеФормата Из УстаревшиеФорматы Цикл
			УдалитьДанныеИсходногоФормата(ОписаниеФормата.ИсходныйФормат, КонтекстДиагностики);
		КонецЦикла;
	КонецЕсли;
	
	ОтметитьДатуСинхронизацииСПровайдером(Провайдер);
	
	ОтборПоПолюОбработан.Обработан = Истина;
	ИзмененныеФорматы = ТаблицаФорматов.Скопировать(ОтборПоПолюОбработан);
	Возврат ИзмененныеФорматы;
	
КонецФункции

// Обновляет при необходимости кеш информационной базы об актуальности и структуре правил форматов.
// Если не задан параметр ОбновляемоеПравило, то также обновляет правила преобразований по всем изменениям форматов.
// Если параметр задан, то обновляет только описанное правило (рекомендуется в случаях, когда требуется "ленивое"
// обновление)
// 
// Параметры:
//  Провайдер - См. НовыйПровайдерПравилФорматов
//  ОбновляемоеПравило - См. ТекстПравилаПреобразованияФормата.ПараметрыПравила
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики 
Процедура ОбновитьОписаниеФорматовДляПреобразования(Провайдер, ОбновляемоеПравило = Неопределено, КонтекстДиагностики = Неопределено)
	
	ОбновитьВсеПравила = ОбновляемоеПравило = Неопределено;
	Если ДанныеСПровайдеромСинхронизированы(Провайдер) Тогда
		ИзмененныеФорматы = НоваяТаблицаОписанийФорматов();
	Иначе
		ИзмененныеФорматы = СинхронизироватьОписаниеФорматовСПровайдером(Провайдер, КонтекстДиагностики);
	КонецЕсли;
	
	ОписаниеРесурса = Новый Структура("СсылкаНаРесурс, ДатаОбновленияПравил", "", '00010101');
	
	Если ОбновитьВсеПравила Тогда
		Для Каждого ОписаниеФормата Из ИзмененныеФорматы Цикл
			ЗаполнитьЗначенияСвойств(ОписаниеРесурса, ОписаниеФормата);
			ОбновитьПравилоПреобразования(Провайдер, ОписаниеФормата.ИсходныйФормат, ОписаниеРесурса,
				КонтекстДиагностики);
		КонецЦикла;
	Иначе
		Отбор = Новый Структура("ИсходныйФормат", ОбновляемоеПравило.ИсходныйФормат);
		НайденныеОписания = ИзмененныеФорматы.НайтиСтроки(Отбор);
		
		Если ЗначениеЗаполнено(НайденныеОписания) Тогда
			ЗаполнитьЗначенияСвойств(ОписаниеРесурса, НайденныеОписания[0]);
		Иначе
			УстановитьПривилегированныйРежим(Истина);
			ЗначениеИзБазы = РегистрыСведений.ФорматыДляПреобразования.Получить(Отбор);
			УстановитьПривилегированныйРежим(Ложь);
			ЗаполнитьЗначенияСвойств(ОписаниеРесурса, ЗначениеИзБазы);
		КонецЕсли;
		
		ОбновитьПравилоПреобразования(Провайдер, ОбновляемоеПравило.ИсходныйФормат, ОписаниеРесурса, КонтекстДиагностики);
		
	КонецЕсли;
	
КонецПроцедуры

// Обновляет кеш правил преобразования в информационной базе.
// Обновление происходит в разрезе формата, т.е. актуализируются описанные в ресурсе правила всех версий,
// вариантов заполнения и выходных форматов.
// 
// Параметры:
//  Провайдер - См. НовыйПровайдерПравилФорматов
//  Формат - Строка - Имя формата (пространство имен)
//  ОписаниеРесурса - Структура:
// * СсылкаНаРесурс - Строка 
// * ДатаОбновленияПравил - Дата
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
Процедура ОбновитьПравилоПреобразования(Провайдер, Формат, ОписаниеРесурса, КонтекстДиагностики)
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИсходныйФормат", Формат);
	Запрос.УстановитьПараметр("ДатаОбновленияФормата", ОписаниеРесурса.ДатаОбновленияПравил);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ
				   |	ВариантЗаполнения,
				   |	ИтоговыйФормат,
				   |	ВерсияИтоговогоФормата,
				   |	ДатаОбновления,
				   |	ЛОЖЬ КАК Обработано
				   |ПОМЕСТИТЬ
				   |	ПравилаФормата
				   |ИЗ
				   |	РегистрСведений.ПравилаПреобразованияФорматов
				   |ГДЕ
				   |	ИсходныйФормат = &ИсходныйФормат
				   |;
				   |
				   |ВЫБРАТЬ ПЕРВЫЕ 1
				   |	1
				   |ИЗ
				   |	ПравилаФормата
				   |ГДЕ
				   |	ДатаОбновления < &ДатаОбновленияФормата";
	УстановитьПривилегированныйРежим(Истина);
	ЗапросУстаревшихПравил = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	ЗапросПравилФормата = Запрос.МенеджерВременныхТаблиц.Таблицы["ПравилаФормата"].ПолучитьДанные();
	ДанныеСинхронизированы = ЗапросУстаревшихПравил.Пустой() И Не ЗапросПравилФормата.Пустой();
	Если ДанныеСинхронизированы Тогда
		Возврат;
	КонецЕсли;
	
	ПравилаФормата = ЗапросПравилФормата.Выгрузить();
	
	ПотокФайла = ПотокДляЧтенияФайлаПровайдера(Провайдер, ОписаниеРесурса.СсылкаНаРесурс, КонтекстДиагностики);
	Если ПотокФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьПоток(ПотокФайла);
	
	Пока ЧтениеXML.Прочитать() Цикл
		Если ЧтениеXML.Имя = "convertion-rules" Тогда
			Если ЧтениеXML.ЗначениеАтрибута("fmt") <> Формат Тогда
				ЧтениеXML.Закрыть();
				ПотокФайла.Закрыть();
				Возврат;
			КонецЕсли;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Отбор = Новый Структура("ВариантЗаполнения, ИтоговыйФормат, ВерсияИтоговогоФормата", "", "", "");
	
	НаборЗаписей = РегистрыСведений.ПравилаПреобразованияФорматов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИсходныйФормат.Установить(Формат);
	Пока ЧтениеXML.Прочитать() Цикл
		Если ЧтениеXML.Имя <> "rule" Тогда
			Продолжить;
		КонецЕсли;
		
		Отбор.ВариантЗаполнения = ЧтениеXML.ЗначениеАтрибута("type");
		Отбор.ИтоговыйФормат = ЧтениеXML.ЗначениеАтрибута("out");
		Отбор.ВерсияИтоговогоФормата = ЧтениеXML.ЗначениеАтрибута("ver");
		ТекущееПравило = ПравилаФормата.НайтиСтроки(Отбор);
		
		Если Не ЗначениеЗаполнено(ТекущееПравило)
			Или ТекущееПравило[0].ДатаОбновления < ОписаниеРесурса.ДатаОбновленияПравил Тогда
				
			АдресФайлаПравил = ЧтениеXML.ЗначениеАтрибута("src");
			ПотокПравила = ПотокДляЧтенияФайлаПровайдера(Провайдер, АдресФайлаПравил, КонтекстДиагностики);
			Если ПотокПравила = Неопределено Тогда
				Прервать;
			КонецЕсли;
			
			ЧтениеТекстаПравила = Новый ЧтениеТекста(ПотокПравила, "UTF-8");
			ТекстПравила = ЧтениеТекстаПравила.Прочитать();
			
			Правило = Новый ХранилищеЗначения(ТекстПравила, Новый СжатиеДанных());
			
			ЧтениеТекстаПравила.Закрыть();
			ПотокПравила.Закрыть();
			
			НаборЗаписей.Очистить();
			НаборЗаписей.Отбор.ВариантЗаполнения.Установить(Отбор.ВариантЗаполнения);
			НаборЗаписей.Отбор.ИтоговыйФормат.Установить(Отбор.ИтоговыйФормат);
			НаборЗаписей.Отбор.ВерсияИтоговогоФормата.Установить(Отбор.ВерсияИтоговогоФормата);
			
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Отбор);
			НоваяЗапись.ИсходныйФормат = Формат;
			НоваяЗапись.ДатаОбновления = ОписаниеРесурса.ДатаОбновленияПравил;
			НоваяЗапись.Правило = Правило;
			УстановитьПривилегированныйРежим(Истина);
			НаборЗаписей.Записать();
			УстановитьПривилегированныйРежим(Ложь);
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекущееПравило) Тогда
			ТекущееПравило[0].Обработано = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	ЧтениеXML.Закрыть();
	ПотокФайла.Закрыть();
		
КонецПроцедуры

// Возвращает поток для чтения файла из интернета
// 
// Параметры:
// 	URL - см. АдресФайлаПравилДляПреобразованияФорматов
// 	КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
// 	 
// Возвращаемое значение:
// 	Поток
//
Функция ОткрытьПотокДляЧтенияФайлаИзИнтернета(URL, КонтекстДиагностики)
	
	РезультатПолученияФайла = ПолучениеФайловИзИнтернета.СкачатьФайлВоВременноеХранилище(URL, Неопределено, Ложь);
	Если НЕ РезультатПолученияФайла.Статус Тогда
		
		ВидОперации = НСтр("ru = 'Преобразование формата.'");
		ВидОшибки = ОбработкаНеисправностейБЭДКлиентСервер.ВидОшибкиНеизвестнаяОшибка();
		
		ТекстОшибки = НСтр("ru = 'Получение файла из интернета'") + РезультатПолученияФайла.СообщениеОбОшибке;
		 		
		Ошибка = ОбработкаНеисправностейБЭД.НоваяОшибка(
					ВидОперации, ВидОшибки, ТекстОшибки, ТекстОшибки);
					
		// Передаем пустой контекст диагностики для фиксирования ошибок только в журнале регистрации
		ОбработкаНеисправностейБЭД.ДобавитьОшибку(ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики(), Ошибка,
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСКонтрагентами);
		Возврат Неопределено;
		
	КонецЕсли;
	
	ДанныеФайла = ПолучитьИзВременногоХранилища(РезультатПолученияФайла.Путь);
	УдалитьИзВременногоХранилища(РезультатПолученияФайла.Путь);
	
	Возврат ДанныеФайла.ОткрытьПотокДляЧтения();
	
КонецФункции

// Каскадно удаляет записи правил преобразования формата.
// 
// Параметры:
//  ИсходныйФормат - Строка
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
Процедура УдалитьДанныеИсходногоФормата(ИсходныйФормат, КонтекстДиагностики)
	
	НачатьТранзакцию();
	Попытка
		ОчищаемыеРегистры = Новый Массив;
		ОчищаемыеРегистры.Добавить(РегистрыСведений.ФорматыДляПреобразования);
		ОчищаемыеРегистры.Добавить(РегистрыСведений.ПравилаПреобразованияФорматов);
		
		Для Каждого РегистрКОчистке Из ОчищаемыеРегистры Цикл
			НаборЗаписей = РегистрКОчистке.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ИсходныйФормат.Установить(ИсходныйФормат);
			УстановитьПривилегированныйРежим(Истина);
			НаборЗаписей.Записать();
			УстановитьПривилегированныйРежим(Ложь);
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();		

		ВидОперации = НСтр("ru = 'Преобразование формата.'");
		ВидОшибки = ОбработкаНеисправностейБЭДКлиентСервер.ВидОшибкиНеизвестнаяОшибка();
		
		ПредставлениеОшибкиКраткое =  СтрШаблон(
			НСтр("ru = 'Не удалось удалить неиспользуемые правила преобразования формата %1 из информационной базы.'"),
				ИсходныйФормат);				
		ПредставлениеОшибкиПодробное = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		 		
		Ошибка = ОбработкаНеисправностейБЭД.НоваяОшибка(
					ВидОперации, ВидОшибки, ПредставлениеОшибкиПодробное, ПредставлениеОшибкиКраткое);
		ОбработкаНеисправностейБЭД.ДобавитьОшибку(КонтекстДиагностики, Ошибка,
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСКонтрагентами);
	КонецПопытки;
	
КонецПроцедуры

Функция ПараметрыПреобразованияВСтроку(Параметры)
	
	Если Параметры = Неопределено Тогда
		Результат = НСтр("ru = 'ИсходныйФормат=ПроизвольныйXML;
			|ИтоговыйФормат=ПараметрыЭлектронногоДокумента;
			|ВерсияИтоговогоФормата='") + ПоддерживаемаяВерсияИтоговогоФормата("ПараметрыЭлектронногоДокумента");
		Возврат Результат;
	КонецЕсли;
	
	МассивСтрок = Новый Массив;
	
	ЗначениеСвойства="";
	
	Если Параметры.Свойство("ИсходныйФормат", ЗначениеСвойства) И ЗначениеЗаполнено(ЗначениеСвойства) Тогда
		МассивСтрок.Добавить(НСтр("ru = 'ИсходныйФормат='") + ЗначениеСвойства);
	КонецЕсли;
	
	Если Параметры.Свойство("ВариантЗаполнения", ЗначениеСвойства) И ЗначениеЗаполнено(ЗначениеСвойства) Тогда
		МассивСтрок.Добавить(НСтр("ru = 'ВариантЗаполнения='") + ЗначениеСвойства);
	КонецЕсли;
	
	Если Параметры.Свойство("ИтоговыйФормат", ЗначениеСвойства) И ЗначениеЗаполнено(ЗначениеСвойства) Тогда
		МассивСтрок.Добавить(НСтр("ru = 'ИтоговыйФормат='") + ЗначениеСвойства);
	КонецЕсли;
	
	МассивСтрок.Добавить(НСтр("ru = 'ВерсияИтоговогоФормата='") + ПоддерживаемаяВерсияИтоговогоФормата(ЗначениеСвойства));
	
	ПараметрыXSL = Новый Структура;
	
	Если Параметры.Свойство("ПараметрыXSL", ПараметрыXSL) И ЗначениеЗаполнено(ПараметрыXSL) Тогда
		МассивСтрок.Добавить(НСтр("ru = 'ПараметрыXSL:'"));
		
		Для Каждого ПараметрXSL Из ПараметрыXSL Цикл
			
			Если ТипЗнч(ПараметрXSL.Значение) = Тип("Структура") Тогда
				МассивСтрок.Добавить(ПараметрXSL.Ключ + ":");
				Для Каждого ПараметрИнструкции Из ПараметрXSL.Значение Цикл
					МассивСтрок.Добавить(Символы.Таб + СтрШаблон(НСтр("ru = '%1=%2'"),
						ПараметрИнструкции.Ключ, ПараметрИнструкции.Значение));
				КонецЦикла;
			Иначе
				МассивСтрок.Добавить(СтрШаблон(НСтр("ru = '%1=%2'"), ПараметрXSL.Ключ, ПараметрXSL.Значение));
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат СтрСоединить(МассивСтрок, Символы.ПС);
	
КонецФункции

// Определяет возможность формирования табличного документа для переданного формата.
//
// Параметры:
// 	Формат - Строка
//
// Возвращаемое значение:
// 	Булево
//
Функция ДляФорматаДоступноФормированиеТабличногоДокумента(Формат)

	Результат = Истина;
	
	Если ФорматыЭДО_ФНС.ЭтоПространствоИменМЧД(Формат) Тогда
		Результат = Ложь;
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

#Область ПровайдерПравилФорматов

// Описание поставщика правил преобразований для форматов электронных документов.
// Для создания объекта рекомендуется использовать соответствующие типу конструкторы:
//  См. НовыйФайловыйПровайдерПравил
//  См. НовыйПровайдерПравилИзСервиса
// 
// Возвращаемое значение:
//  Структура:
// * Тип - Строка - См. ТипыПровайдеров
// * АдресРесурса - Строка - Адрес провайдера (корневой каталог или URL)
Функция НовыйПровайдерПравилФорматов()
	Провайдер = Новый Структура;
	Провайдер.Вставить("Тип", "");
	Провайдер.Вставить("АдресРесурса", "");
	Возврат Провайдер;
КонецФункции

// Поставщик правил преобразований из каталога файловой системы
// 
// Параметры:
//  Каталог - Строка - путь к корневому каталогу правил
// 
// Возвращаемое значение:
//  См. НовыйПровайдерПравилФорматов
Функция НовыйФайловыйПровайдерПравил(Каталог)
	Провайдер = НовыйПровайдерПравилФорматов();
	Провайдер.Тип = ТипыПровайдеров().Каталог;
	Провайдер.АдресРесурса = Каталог;
	Возврат Провайдер;
КонецФункции

// Поставщик правил преобразований из сервиса настроек ЭДО
// 
// Возвращаемое значение:
//  См. НовыйПровайдерПравилФорматов
Функция НовыйПровайдерПравилИзСервиса()
	Провайдер = НовыйПровайдерПравилФорматов();
	Провайдер.Тип = ТипыПровайдеров().СервисЭДО;
	Провайдер.АдресРесурса = СинхронизацияЭДО.АдресОблачногоХранилищаНастроек() + "/format_conversion_rules/";
	Возврат Провайдер;
КонецФункции

// Возвращаемое значение:
//  ФиксированнаяСтруктура:
// * СервисЭДО - Строка
// * Каталог - Строка
Функция ТипыПровайдеров()
	Виды = Новый ФиксированнаяСтруктура("СервисЭДО, Каталог", "СервисЭДО", "Каталог");
	Возврат Виды;
КонецФункции

// Признак, что с провайдером производилась синхронизация и данные кеша актуальны
// 
// Параметры:
//  Провайдер - См. НовыйПровайдерПравилФорматов
// 
// Возвращаемое значение:
//  Булево
Функция ДанныеСПровайдеромСинхронизированы(Провайдер)
	ТипыПровайдеров = ТипыПровайдеров();
	
	Если Провайдер.Тип = ТипыПровайдеров.СервисЭДО Тогда
		Если ОбщегоНазначения.РазделениеВключено() Тогда
			Возврат Истина;
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Истина);
		ДатаЗагрузкиОписанияФорматов = Константы.ДатаЗагрузкиОписанияФорматовДляПреобразования.Получить();
		ТекущаяУниверсальнаяДата = НачалоДня(ТекущаяУниверсальнаяДата());
		Возврат ТекущаяУниверсальнаяДата = ДатаЗагрузкиОписанияФорматов;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Фиксирует факт синхронизации с провайдером правил.
// 
// Параметры:
//  Провайдер - См. НовыйПровайдерПравилФорматов 
Процедура ОтметитьДатуСинхронизацииСПровайдером(Провайдер)
	ТипыПровайдеров = ТипыПровайдеров();
	Если Провайдер.Тип = ТипыПровайдеров.СервисЭДО Тогда
		ТекущаяДата = ТекущаяУниверсальнаяДата();
		УстановитьПривилегированныйРежим(Истина);
		Константы.ДатаЗагрузкиОписанияФорматовДляПреобразования.Установить(ТекущаяДата);
	КонецЕсли;
КонецПроцедуры

// Поток для чтения файла провайдера.
// 
// Параметры:
//  Провайдер - См. НовыйПровайдерПравилФорматов
//  ИмяФайла - Строка - Имя файла
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
// 
// Возвращаемое значение:
//  Неопределено, Поток -
Функция ПотокДляЧтенияФайлаПровайдера(Провайдер, ИмяФайла, КонтекстДиагностики = Неопределено)
	ТипыПровайдеров = ТипыПровайдеров();
	АдресКФайлу = Провайдер.АдресРесурса + ИмяФайла;
	Если Провайдер.Тип = ТипыПровайдеров.СервисЭДО Тогда
		Возврат ОткрытьПотокДляЧтенияФайлаИзИнтернета(АдресКФайлу, КонтекстДиагностики);
	ИначеЕсли Провайдер.Тип = ТипыПровайдеров.Каталог Тогда
		ДанныеФайла = Новый ДвоичныеДанные(АдресКФайлу);
		Возврат ДанныеФайла.ОткрытьПотокДляЧтения();
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ПреобразованиеXSL

// Преобразует поток для обхода ограничения платформы по работе со сверхбольшими файлами XML.
// Если в файле xml встречается тэг, значение которого превышает размер 10 Мб, то при преобразовании XSL возникнет
// критическая ошибка, так как преобразователь не может добраться до конца файла.
// При чтении файлов через ПостроительDOM подобные тэги преобразуются в символ "Х". Далее из построителя
// формируется новый поток с измененными данными. Считаем такой подход верным, так как значения сверхбольшой длины
// на практике могут встретиться только в тэгах, хранящих информацию об электронных подписях (архвиных подписях), а
// они не нужны ни в визуализации, ни в механизмах получения параметров.
// 
// Параметры:
//  ПотокФайлаXML - Поток
//                - ПотокВПамяти
//                - ФайловыйПоток
// 
// Возвращаемое значение:
//  Поток, ФайловыйПоток, ПотокВПамяти - потоки итоговоых данных
Функция ПотокФайлаДляПреобразованияXSL(ПотокФайлаXML)
	
	МинимальныйРазмерФайлаДляАнализа = 10485760; // 10*1024*1024 = 10 Мб
	
	Если ПотокФайлаXML.Размер() < МинимальныйРазмерФайлаДляАнализа Тогда
		
		Возврат ПотокФайлаXML;
		
	Иначе
		
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.ОткрытьПоток(ПотокФайлаXML);
		
		ПостроительDOM = Новый ПостроительDOM();
		ДокументDOM = ПостроительDOM.Прочитать(ЧтениеXML);
		ЧтениеXML.Закрыть();
		
		ПотокЗаписи = Новый ПотокВПамяти;
		
		ЗаписьXML = Новый ЗаписьXML;
		ЗаписьXML.ОткрытьПоток(ПотокЗаписи);
		
		ЗаписьDOM = Новый ЗаписьDOM;
		ЗаписьDOM.Записать(ДокументDOM, ЗаписьXML);
		
		ЗаписьXML.Закрыть();
		
		ПотокЗаписи.Перейти(0, ПозицияВПотоке.Начало);
		
		Возврат ПотокЗаписи;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти