
#Область СлужебныеПроцедурыИФункции

// Распаковывает транспортные контейнеры.
// 
// Параметры:
// 	Контейнеры - Массив из ДокументСсылка.ТранспортныйКонтейнерЭДО
Процедура РаспаковатьКонтейнеры(Контейнеры) Экспорт
	
	ТекстСообщения = НСтр("ru = 'Выполняется распаковка транспортных контейнеров ЭДО. Подождите...'");
	Состояние(НСтр("ru = 'Распаковка.'"), , ТекстСообщения);
	
	ПараметрыВыполнения = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	ПараметрыВыполнения.ОбъектыДействий.ТранспортныеКонтейнеры = Контейнеры;
	
	НаборДействий = ПараметрыВыполнения.НаборДействий;
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий,
		ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Загрузить"));
	
	Оповещение = Новый ОписаниеОповещения("ЗавершитьРаспаковкуКонтейнеров", ТранспортныеКонтейнерыЭДОСлужебныйКлиент);
	
	ИнтерфейсДокументовЭДОКлиент.НачатьВыполнениеДействийПоЭДО(Оповещение, ПараметрыВыполнения);
	
КонецПроцедуры

// Параметры:
//  Результат - Структура:
//  * Итог - см. ИнтерфейсДокументовЭДОКлиентСервер.НовыйИтогВыполненияДействийПоЭДО
//  * ОшибкиФормирования - Массив из Структура
//  * КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  Контекст - Неопределено
Процедура ЗавершитьРаспаковкуКонтейнеров(Результат, Контекст) Экспорт
	
	ДействиеЗагрузить = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Загрузить");
	
	РаспакованоКонтейнеров = Результат.Итог.ОбработаноПоДействиям[ДействиеЗагрузить];
	
	ШаблонОповещения = НСтр("ru = 'Распаковано контейнеров: %1.'");
	ТекстОповещения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОповещения, РаспакованоКонтейнеров);
	ЗаголовокОповещения = НСтр("ru = 'Обмен электронными документами'");
	ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения);
	Оповестить("ОбновитьСостояниеЭД");
	
	ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(Результат.КонтекстДиагностики);
	
КонецПроцедуры

#КонецОбласти