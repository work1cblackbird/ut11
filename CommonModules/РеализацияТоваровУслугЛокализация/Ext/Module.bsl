#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив из Строка - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация


	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив из Строка - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	//++ Локализация
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ИсходящаяТранспортнаяОперацияВЕТИС") Тогда
	
		ИнтеграцияВЕТИСУТ.ЗаполнитьРеализациюТоваровУслугНаОснованииИсходящейТранспортнойОперацииВЕТИС(Объект, ДанныеЗаполнения);
	
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ОформлениеСДИЗЗЕРНО") Тогда
	
		ИнтеграцияЗЕРНОУТ.ЗаполнитьРеализациюТоваровУслугНаОснованииОформленияСДИЗЗЕРНО(Объект, ДанныеЗаполнения);
		
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	//++ Локализация

	
	// ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	ЭлектронноеАктированиеЕИСУТ.ПередЗаписьюДокумента(Объект.Договор, Объект.Товары);
	// Конец ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС

	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		УчетПрослеживаемыхТоваровЛокализация.ПроверитьДанныеПрослеживаемостиНомеровГТД(Объект, Объект.ВидыЗапасов, Объект.Дата, "ВидыЗапасов");
	КонецЕсли;
	
	ИнтеграцияИСМП.ПередЗаписьюДокумента(Объект, Отказ);
	
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	//++ Локализация
	Документы.ТранспортнаяНакладная.ОбновитьРеквизитыТранспортныхНакладныхПриЗаписиДокументаОснования(Объект, Отказ);

	//-- Локализация
	Возврат;
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	//++ Локализация
	Объект.ЕстьМаркируемаяПродукцияГИСМ = Ложь;
	Объект.ШтрихкодыУпаковок.Очистить();
	//-- Локализация
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	//++ Локализация
	Документы.СчетФактураВыданный.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);

	
	ОбменСГИСЭПДПереопределяемый.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	РегистрыСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ДобавитьКомандуСоздатьНаОсновании(
		КомандыСозданияНаОсновании,
		"ИспользоватьОтгрузкуБезПереходаПраваСобственности");
	//-- Локализация
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	//++ Локализация
	ЭтоПартнер = ПраваПользователяПовтИсп.ЭтоПартнер();
	
	Если НЕ ЭтоПартнер Тогда
		// Товарная накладная (ТОРГ-12)
		КомандаПечати = КомандыПечати.Добавить();	// СтрокаТаблицыЗначений
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "ТОРГ12";
		КомандаПечати.Представление = НСтр("ru = 'Товарная накладная (ТОРГ-12)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьУслуги", Истина);
		КомандаПечати.Порядок = 17;
	КонецЕсли;

	Если НЕ ЭтоПартнер Тогда
		// Товарная накладная без услуг (ТОРГ-12)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "ТОРГ12БезУслуг";
		КомандаПечати.Представление = НСтр("ru = 'Товарная накладная без услуг (ТОРГ-12)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьУслуги", Ложь);
		КомандаПечати.Порядок = 18;

		// Товарная накладная с номерами ГТД (ТОРГ-12)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "ТОРГ12_ГТД";
		КомандаПечати.Представление = НСтр("ru = 'Товарная накладная с номерами ГТД (ТОРГ-12)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьУслуги", Ложь);
		КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьГТД",    Истина);
		КомандаПечати.Порядок = 19;
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьАктыНаПередачуПрав") Тогда
			// Акт на передачу прав
			КомандаПечати = КомандыПечати.Добавить();
			КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
			КомандаПечати.Идентификатор = "АктНаПередачуПрав";
			КомандаПечати.Представление = НСтр("ru = 'Акт на передачу прав'");
			КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
			КомандаПечати.Порядок = 20;
		КонецЕсли;
	КонецЕсли;

	// Счет-фактура
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "СчетФактура";
	КомандаПечати.Представление = НСтр("ru = 'Счет-фактура'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВВалюте", Ложь);
	КомандаПечати.Порядок = 20;

	Если НЕ ЭтоПартнер Тогда
		// Счет-фактура (в валюте)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "СчетФактураВВалюте";
		КомандаПечати.Представление = НСтр("ru = 'Счет-фактура (в валюте)'");
		КомандаПечати.ФункциональныеОпции = "ИспользоватьНесколькоВалют";
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВВалюте", Истина);
		КомандаПечати.Порядок = 21;
	КонецЕсли;

	Если ПравоДоступа("Изменение", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		// Реестр номеров ГТД
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "РеестрНомеровГТД";
		КомандаПечати.Представление = НСтр("ru = 'Реестр номеров ГТД'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 22;
		КомандаПечати.ФункциональныеОпции = "ИспользоватьИмпортныеТовары";
	КонецЕсли;
	
	Если НЕ ЭтоПартнер Тогда
		// Накладная (М-15)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "М15";
		КомандаПечати.Представление = НСтр("ru = 'Накладная (М-15)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 25;
	КонецЕсли;
	
	Если (НЕ ЭтоПартнер
		 И (ПолучитьФункциональнуюОпцию("НеИспользоватьСчетаНаОплатуВыбиратьВариантВыводаСкидок")
		 	ИЛИ ПолучитьФункциональнуюОпцию("НеИспользоватьСчетаНаОплатуНеВыбиратьВариантВыводаСкидок"))) Тогда
		// Извещение
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьИзвещений";
		КомандаПечати.Идентификатор = "Извещение";
		КомандаПечати.Представление = НСтр("ru = 'Извещение'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 32;
	КонецЕсли;
		
	Если НЕ ЭтоПартнер
		 И ПолучитьФункциональнуюОпцию("ИспользоватьОтветственноеХранение") Тогда
		// МХ-3 
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "МХ3";
		КомандаПечати.Представление = НСтр("ru = 'Акт о возврате ТМЦ, сданных на хранение (МХ-3)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 39;
	КонецЕсли;
	
	Если НЕ ЭтоПартнер Тогда
		// Универсальный передаточный документ (УПД)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "УПД";
		КомандаПечати.Представление = НСтр("ru = 'Универсальный передаточный документ (УПД)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 37;
	КонецЕсли;
	
	// Порядок 23 - ТТН, 24 - Транспортная накладная
	Документы.ТранспортнаяНакладная.ДобавитьКомандыПечати(КомандыПечати, 23);
	//-- Локализация
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	//++ Локализация
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РеестрНомеровГТД") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"РеестрНомеровГТД",
			НСтр("ru = 'Реестр номеров ГТД'"),
			СформироватьПечатнуюФормуРеестрНомеровГТД(МассивОбъектов, ОбъектыПечати),
			,
			"Документ.РеализацияТоваровУслуг.ПФ_MXL_РеестрНомеровГТД");
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

Процедура СформироватьКомплектПечатныхФорм(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, КомплектПечатныхФорм) Экспорт
	
	//++ Локализация
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(
		КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"АктНаПередачуПрав");	//СтрокаТаблицыЗначений
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.РеализацияТоваровУслуг", КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуАктНаПередачуПрав(СтруктураТипов, ОбъектыПечати, ПараметрыПечати));
	КонецЦикла;
		
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"ТОРГ12");
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.РеализацияТоваровУслуг", КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуТОРГ12(СтруктураТипов, ОбъектыПечати,  Новый Структура("ВыводитьУслуги", Истина)));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"ТОРГ12БезУслуг");
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.РеализацияТоваровУслуг", КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуТОРГ12(СтруктураТипов, ОбъектыПечати,  Новый Структура("ВыводитьУслуги", Ложь)));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"ТОРГ12_ГТД");
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.РеализацияТоваровУслуг", КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуТОРГ12(СтруктураТипов, ОбъектыПечати, Новый Структура("ВыводитьУслуги, ВыводитьГТД", Ложь, Истина)));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"СчетФактура");
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.РеализацияТоваровУслуг", КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуСчетФактура(СтруктураТипов, ОбъектыПечати, Новый Структура("ПечатьВВалюте", Ложь)));
	КонецЦикла;
			
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"СчетФактураВВалюте");
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.РеализацияТоваровУслуг", КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуСчетФактура(СтруктураТипов, ОбъектыПечати, Новый Структура("ПечатьВВалюте", Истина)));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"УПД");
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.РеализацияТоваровУслуг", КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуУПД(СтруктураТипов, ОбъектыПечати, ПараметрыПечати));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"МХ3");
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.РеализацияТоваровУслуг", КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуМХ3(СтруктураТипов, ОбъектыПечати, ПараметрыПечати));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"М15");
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.РеализацияТоваровУслуг", КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуМ15(СтруктураТипов, ОбъектыПечати, ПараметрыПечати));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"ТТН");
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		ОбъектыКоторыеМожноПечатать = Документы.ТранспортнаяНакладная.ОбъектыКоторыеМожноПечатать(КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Документы.ТранспортнаяНакладная.СформироватьПечатнуюФормуТТН(ОбъектыКоторыеМожноПечатать, ОбъектыПечати, ПараметрыПечати));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"ТранспортнаяНакладная");
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		ОбъектыКоторыеМожноПечатать = Документы.ТранспортнаяНакладная.ОбъектыКоторыеМожноПечатать(КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
				КоллекцияПечатныхФорм,
				КомплектПечати.Имя,
				КомплектПечати.Представление,
				Документы.ТранспортнаяНакладная.СформироватьПечатнуюФормуТранспортнойНакладной(ОбъектыКоторыеМожноПечатать, ОбъектыПечати));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"Извещение");
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.РеализацияТоваровУслуг", КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьИзвещений.СформироватьПечатнуюФормуИзвещение(СтруктураТипов, ОбъектыПечати, ПараметрыПечати));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"РеестрНомеровГТД");
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.РеализацияТоваровУслуг", КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			СформироватьПечатнуюФормуРеестрНомеровГТД(КомплектПечати.Объекты, ОбъектыПечати));
		КонецЦикла;
	//-- Локализация
	
КонецПроцедуры

Процедура КомплектПечатныхФорм(КомплектПечатныхФорм) Экспорт
	
	//++ Локализация
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм, "М15", НСтр("ru = 'Накладная (М-15)'"),                                   0);	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьАктыНаПередачуПрав") Тогда
		РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм, "АктНаПередачуПрав", НСтр("ru = 'Акт на передачу прав'"),             2);
	КонецЕсли;
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм, "ТОРГ12", НСтр("ru = 'Товарная накладная (ТОРГ-12)'"),                    0);
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм, "ТОРГ12БезУслуг", НСтр("ru = 'Товарная накладная без услуг (ТОРГ-12)'"),  2);
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм, "ТОРГ12_ГТД", НСтр("ru = 'Товарная накладная с номерами ГТД (ТОРГ-12)'"), 0);
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм, "СчетФактура", НСтр("ru = 'Счет-фактура'"),                               2);
	Если ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют") Тогда
		РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм, "СчетФактураВВалюте", НСтр("ru = 'Счет-фактура (в валюте)'"),         0);
	КонецЕсли;
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм, "УПД", НСтр("ru = 'Универсальный передаточный документ (УПД)'"), 0);
	Если ПолучитьФункциональнуюОпцию("ИспользоватьТТН") Тогда
		РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм, "ТТН", НСтр("ru = 'Товарно-транспортная накладная (1-Т)'"),               0);
		РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм, "ТранспортнаяНакладная", НСтр("ru = 'Транспортная накладная'"),           0);		
	КонецЕсли;

	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм, "РеестрНомеровГТД", НСтр("ru = 'Реестр номеров ГТД'"),                    0);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОтветственноеХранение") Тогда
		РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм,
			"МХ3", 
			НСтр("ru = 'Акт о возврате ТМЦ, сданных на хранение (МХ-3)'"),
			0);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НеИспользоватьСчетаНаОплатуВыбиратьВариантВыводаСкидок")
		ИЛИ ПолучитьФункциональнуюОпцию("НеИспользоватьСчетаНаОплатуНеВыбиратьВариантВыводаСкидок") Тогда
		РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм, "Извещение", НСтр("ru = 'Извещение'"), 0);
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

//++ Локализация
// Возвращаемое значение:
//	см. Документы.РеализацияТоваровУслуг.ДанныеДляПечатныхФормСчетаНаОплатуИзвещения
Функция ПолучитьДанныеДляПечатнойФормыИзвещения(ПараметрыПечати, МассивОбъектов) Экспорт
	
	Возврат Документы.РеализацияТоваровУслуг.ДанныеДляПечатныхФормСчетаНаОплатуИзвещения(ПараметрыПечати, МассивОбъектов);
	
КонецФункции

// Функция получает данные для формирования печатной формы ТОРГ - 12
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка - Массив ссылок на документы, по которым необходимо получить данные.
//
// Возвращаемое значение:
// 	Структура:
// 		* РезультатПоШапке - РезультатЗапроса
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса
//
Функция ПолучитьДанныеДляПечатнойФормыТОРГ12(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка  КАК Ссылка,
	|	ДанныеДокументов.Валюта  КАК Валюта,
	|	ДанныеДокументов.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Выполнить();
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	Если ПараметрыПечати.Свойство("ВыводитьГТД") Тогда
		ПараметрыЗаполнения.ВключаяНомераГТД = ПараметрыПечати.ВыводитьГТД;
	КонецЕсли;
	ПараметрыЗаполнения.ОбработатьНастройкиПечатиНаборов = Истина;
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	ПродажиСервер.ПоместитьВременнуюТаблицуКоэффициентыУпаковок(МенеджерВременныхТаблиц, "РеализацияТоваровУслугТаблицаТоваров");
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.Номер КАК Номер,
	|	РеализацияТоваровУслуг.Дата КАК Дата,
	|	РеализацияТоваровУслуг.Статус КАК Статус,
	|	РеализацияТоваровУслуг.Партнер КАК Партнер,
	|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Организация.ОбособленноеПодразделение
	|			ТОГДА РеализацияТоваровУслуг.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ РеализацияТоваровУслуг.Организация
	|	КОНЕЦ КАК Организация,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	РеализацияТоваровУслуг.Отпустил КАК Кладовщик,
	|	РеализацияТоваровУслуг.ОтпустилДолжность КАК ДолжностьКладовщика,
	|	РеализацияТоваровУслуг.Организация.Префикс КАК Префикс,
	|	РеализацияТоваровУслуг.Основание КАК Основание,
	|	РеализацияТоваровУслуг.ОснованиеДата КАК ОснованиеДата,
	|	РеализацияТоваровУслуг.ОснованиеНомер КАК ОснованиеНомер,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА РеализацияТоваровУслуг.Контрагент
	|		ИНАЧЕ РеализацияТоваровУслуг.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА РеализацияТоваровУслуг.Организация
	|		ИНАЧЕ РеализацияТоваровУслуг.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	РеализацияТоваровУслуг.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации,
	|	РеализацияТоваровУслуг.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	РеализацияТоваровУслуг.БанковскийСчетГрузоотправителя КАК БанковскийСчетГрузоотправителя,
	|	РеализацияТоваровУслуг.БанковскийСчетГрузополучателя КАК БанковскийСчетГрузополучателя,
	|	РеализацияТоваровУслуг.АдресДоставки КАК АдресДоставки,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	РеализацияТоваровУслуг.Валюта КАК Валюта,
	|	РеализацияТоваровУслуг.НалогообложениеНДС КАК НалогообложениеНДС,
	|	РеализацияТоваровУслуг.ДоверенностьНомер КАК ДоверенностьНомер,
	|	РеализацияТоваровУслуг.ДоверенностьДата КАК ДоверенностьДата,
	|	РеализацияТоваровУслуг.ДоверенностьВыдана КАК ДоверенностьВыдана,
	|	РеализацияТоваровУслуг.ДоверенностьЛицо КАК ДоверенностьЛицо,
	|	&ЕдиницаИзмеренияВеса КАК ЕдиницаИзмеренияВеса
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО РеализацияТоваровУслуг.Ссылка = ДанныеДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО РеализацияТоваровУслуг.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения1
	|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ &ТекстЗапросаКодЕдиницыИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
	|	ТаблицаТоваров.Упаковка КАК Упаковка,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) = 1
	|			ТОГДА """"
	|		ИНАЧЕ ТаблицаТоваров.Упаковка.Наименование
	|	КОНЕЦ КАК УпаковкаНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА &ТекстЗапросаНаименованиеЕдиницыИзмерения1
	|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения2
	|	КОНЕЦ КАК ВидУпаковки,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ ТаблицаТоваров.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|		И НЕ ТаблицаТоваров.ЭтоНабор
	|			ТОГДА ВЫБОР
	|				КОГДА ТаблицаТоваров.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|				И ((ТаблицаТоваров.Номенклатура.ВесИспользовать
	|				И ТаблицаТоваров.Номенклатура.ВесМожноУказыватьВДокументах
	|				И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ВесЕдиницаИзмерения)
	|				ИЛИ (ТаблицаТоваров.Номенклатура.ОбъемИспользовать
	|				И ТаблицаТоваров.Номенклатура.ОбъемМожноУказыватьВДокументах
	|				И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ОбъемЕдиницаИзмерения)
	|				ИЛИ (ТаблицаТоваров.Номенклатура.ДлинаИспользовать
	|				И ТаблицаТоваров.Номенклатура.ДлинаМожноУказыватьВДокументах
	|				И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ДлинаЕдиницаИзмерения)
	|				ИЛИ (ТаблицаТоваров.Номенклатура.ПлощадьИспользовать
	|				И ТаблицаТоваров.Номенклатура.ПлощадьМожноУказыватьВДокументах
	|				И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ПлощадьЕдиницаИзмерения))
	|					ТОГДА КоэффициентыУпаковок.КоличествоУпаковок
	|				ИНАЧЕ ТаблицаТоваров.Количество / КоэффициентыУпаковок.КоэффициентВложеннойУпаковки
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|			КОГДА ТаблицаТоваров.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			И &ВыводитьБазовыеЕдиницыИзмерения
	|			И ((ТаблицаТоваров.Номенклатура.ВесИспользовать
	|			И ТаблицаТоваров.Номенклатура.ВесМожноУказыватьВДокументах
	|			И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ВесЕдиницаИзмерения)
	|			ИЛИ (ТаблицаТоваров.Номенклатура.ОбъемИспользовать
	|			И ТаблицаТоваров.Номенклатура.ОбъемМожноУказыватьВДокументах
	|			И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ОбъемЕдиницаИзмерения)
	|			ИЛИ (ТаблицаТоваров.Номенклатура.ДлинаИспользовать
	|			И ТаблицаТоваров.Номенклатура.ДлинаМожноУказыватьВДокументах
	|			И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ДлинаЕдиницаИзмерения)
	|			ИЛИ (ТаблицаТоваров.Номенклатура.ПлощадьИспользовать
	|			И ТаблицаТоваров.Номенклатура.ПлощадьМожноУказыватьВДокументах
	|			И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ПлощадьЕдиницаИзмерения))
	|				ТОГДА ТаблицаТоваров.КоличествоУпаковок
	|			КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ИЛИ ТаблицаТоваров.Упаковка = (ВЫБОР
	|				КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|					ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
	|				ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения
	|			КОНЕЦ)
	|				ТОГДА ТаблицаТоваров.Количество
	|			ИНАЧЕ ТаблицаТоваров.Количество / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1)
	|		КОНЕЦ
	|	КОНЕЦ КАК КоличествоМестДробное,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|		И Не ТаблицаТоваров.ЭтоНабор
	|			ТОГДА ВЫБОР
	|				КОГДА ТаблицаТоваров.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|				И ТаблицаТоваров.Упаковка = &ТекстЗапросаЕдиницаИзмерения
	|					ТОГДА 1
	|				ИНАЧЕ КоэффициентыУпаковок.КоэффициентВложеннойУпаковки
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|			КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ИЛИ ТаблицаТоваров.Упаковка = (ВЫБОР
	|				КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|					ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
	|				ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения
	|			КОНЕЦ)
	|				ТОГДА 1
	|			ИНАЧЕ &ТекстЗапросаКоэффициентУпаковки
	|		КОНЕЦ
	|	КОНЕЦ КАК КоличествоВОдномМесте,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА &ТекстЗапросаВесУпаковки
	|		ИНАЧЕ &ТекстЗапросаВесВидаУпаковки
	|	КОНЕЦ КАК МассаБруттоОдногоМеста,
	|	ЕСТЬNULL(КоэффициентыУпаковок.КоэффициентУпаковки, 1) * ТаблицаТоваров.МассаНеттоНоменклатуры КАК
	|		МассаНеттоОднойУпаковки,
	|	ТаблицаТоваров.НомерГТД КАК НомерГТД,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	1 КАК КоэффициентУпаковки
	|ПОМЕСТИТЬ РасчитанныеДанныеУпаковокЕдиницИзмерения
	|ИЗ
	|	РеализацияТоваровУслугТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыУпаковок КАК КоэффициентыУпаковок
	|		ПО ТаблицаТоваров.Ссылка = КоэффициентыУпаковок.Ссылка
	|		И ТаблицаТоваров.НомерСтроки = КоэффициентыУпаковок.НомерСтроки
	|		И ТаблицаТоваров.Номенклатура = КоэффициентыУпаковок.Номенклатура
	|		И ТаблицаТоваров.КоличествоУпаковок = КоэффициентыУпаковок.КоличествоУпаковок
	|		И (НЕ &ВыводитьБазовыеЕдиницыИзмерения)
	|ГДЕ
	|	(ТаблицаТоваров.ЭтоТовар
	|	ИЛИ &ВыводитьУслуги)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НомерСтроки,
	|	Номенклатура,
	|	Характеристика,
	|	НомерГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	ТаблицаТоваров.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ТаблицаТоваров.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	ТаблицаТоваров.НоменклатураНабора КАК НоменклатураНабора,
	|	ТаблицаТоваров.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ТаблицаТоваров.ЭтоКомплектующие КАК ЭтоКомплектующие,
	|	ТаблицаТоваров.ЭтоНабор КАК ЭтоНабор,
	|	ТаблицаТоваров.ПолныйНабор КАК ПолныйНабор,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	ТаблицаТоваров.Номенклатура.Наименование КАК НоменклатураНаименованиеКраткое,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаТоваров.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаТоваров.Номенклатура.Код
	|	КОНЕЦ КАК НоменклатураКод,
	|	ДанныеУпаковок.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ДанныеУпаковок.ЕдиницаИзмеренияНаименование КАК ЕдиницаИзмеренияНаименование,
	|	ДанныеУпаковок.ЕдиницаИзмеренияКод КАК ЕдиницаИзмеренияКод,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТаблицаТоваров.Серия.Наименование КАК СерияНаименование,
	|	ДанныеУпаковок.Упаковка КАК Упаковка,
	|	ДанныеУпаковок.УпаковкаНаименование КАК УпаковкаНаименование,
	|	ДанныеУпаковок.ВидУпаковки КАК ВидУпаковки,
	|	ТаблицаТоваров.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаТоваров.НомерГТД КАК НомерГТД,
	|	ТаблицаТоваров.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ДанныеУпаковок.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА ДанныеУпаковок.КоличествоМестДробное = (ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное КАК ЧИСЛО(12, 0)))
	|			ТОГДА ДанныеУпаковок.КоличествоМестДробное
	|		ИНАЧЕ ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное + 0.5 КАК ЧИСЛО(12, 0))
	|	КОНЕЦ КАК КоличествоМест,
	|	ВЫБОР
	|		КОГДА ДанныеУпаковок.ЕдиницаИзмерения = ДанныеУпаковок.Упаковка.ЕдиницаИзмерения
	|			ТОГДА 1
	|		ИНАЧЕ ДанныеУпаковок.КоличествоВОдномМесте
	|	КОНЕЦ КАК КоличествоВОдномМесте,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ВЫБОР
	|				КОГДА НЕ ТаблицаТоваров.КоличествоУпаковок = 0
	|					ТОГДА ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.КоличествоУпаковок
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|			КОГДА НЕ ТаблицаТоваров.Количество = 0
	|				ТОГДА ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.Количество
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ КАК Цена,
	|	ТаблицаТоваров.СуммаБезНДС КАК СуммаБезНДС,
	|	ТаблицаТоваров.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТоваров.СуммаБезНДС + ТаблицаТоваров.СуммаНДС КАК СуммаСНДС,
	|	ТаблицаТоваров.МассаНетто КАК МассаНетто,
	|	ВЫБОР
	|		КОГДА ДанныеУпаковок.КоличествоМестДробное = (ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное КАК ЧИСЛО(12, 0)))
	|			ТОГДА ДанныеУпаковок.КоличествоМестДробное
	|		ИНАЧЕ ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное + 0.5 КАК ЧИСЛО(12, 0))
	|	КОНЕЦ * ЕСТЬNULL(ДанныеУпаковок.МассаБруттоОдногоМеста, 0) - (ВЫБОР
	|		КОГДА ДанныеУпаковок.КоличествоМестДробное = (ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное КАК ЧИСЛО(12, 0)))
	|			ТОГДА ДанныеУпаковок.КоличествоМестДробное
	|		ИНАЧЕ ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное + 0.5 КАК ЧИСЛО(12, 0))
	|	КОНЕЦ * ЕСТЬNULL(ДанныеУпаковок.КоличествоВОдномМесте, 1) - ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.Количество
	|		ИНАЧЕ (ВЫБОР 
	|				КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|				ТОГДА (ВЫБОР
	|						КОГДА ДанныеУпаковок.КоличествоМестДробное = (ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное КАК ЧИСЛО(12, 0)))
	|							ТОГДА ДанныеУпаковок.КоличествоМестДробное
	|						ИНАЧЕ ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное + 0.5 КАК ЧИСЛО(12, 0))
	|						КОНЕЦ) * ЕСТЬNULL(ДанныеУпаковок.КоличествоВОдномМесте, 1)
	|				ИНАЧЕ ТаблицаТоваров.КоличествоУпаковок * ЕСТЬNULL(ДанныеУпаковок.КоличествоВОдномМесте, 1)
	|			КОНЕЦ)
	|	КОНЕЦ) * ДанныеУпаковок.МассаНеттоОднойУпаковки / ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА 1
	|		ИНАЧЕ ЕСТЬNULL(ДанныеУпаковок.КоличествоВОдномМесте, 1)
	|	КОНЕЦ КАК МассаБрутто,
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТоваров.НомерСтрокиНаборы КАК НомерСтрокиНаборы,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.Ссылка.ВернутьМногооборотнуюТару
	|		И ТаблицаТоваров.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	РеализацияТоваровУслугТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РасчитанныеДанныеУпаковокЕдиницИзмерения КАК ДанныеУпаковок
	|		ПО ТаблицаТоваров.Ссылка = ДанныеУпаковок.Ссылка
	|		И ТаблицаТоваров.НомерСтроки = ДанныеУпаковок.НомерСтроки
	|		И ТаблицаТоваров.Номенклатура = ДанныеУпаковок.Номенклатура
	|		И ТаблицаТоваров.Характеристика = ДанныеУпаковок.Характеристика
	|		И ТаблицаТоваров.НомерГТД = ДанныеУпаковок.НомерГТД
	|ГДЕ
	|	(ТаблицаТоваров.ЭтоТовар
	|	ИЛИ &ВыводитьУслуги)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтрокиНаборы,
	|	ЭтоНабор УБЫВ,
	|	НомерСтроки
	|ИТОГИ
	|ПО
	|	Ссылка";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст, 
		"&ТекстЗапросаВесУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));

	Запрос.Текст = СтрЗаменить(
		Запрос.Текст, 
		"&ТекстЗапросаВесВидаУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"КоэффициентыУпаковок.ВидУпаковки",
			"ТаблицаТоваров.Номенклатура"));

	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"КоэффициентыУпаковок.ВидУпаковки",
			"ТаблицаТоваров.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.УстановитьПараметр("ВыводитьУслуги",                  ПараметрыПечати.ВыводитьУслуги);
	Запрос.УстановитьПараметр("КолонкаКодов",                    КолонкаКодов);
	Запрос.УстановитьПараметр("ЕдиницаИзмеренияВеса",            Константы.ЕдиницаИзмеренияВеса.Получить());
	Запрос.УстановитьПараметр("ЗаполненаЕдиницаИзмеренияВеса",   ЗначениеЗаполнено(Константы.ЕдиницаИзмеренияВеса.Получить()));
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения",
		Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить() ИЛИ ПараметрыЗаполнения.ВключаяНомераГТД);
	
	МассивРезультатов         = Запрос.ВыполнитьПакет();
	РезультатПоШапке          = МассивРезультатов[0];
	РезультатПоТабличнойЧасти = МассивРезультатов[МассивРезультатов.ВГраница()];
	
	СтруктураДанныхДляПечати 	= Новый Структура("РезультатПоШапке, РезультатПоТабличнойЧасти",
	                                               РезультатПоШапке, РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Функция получает данные для формирования печатной формы Акт на передачу прав
// Возвращаемое значение:
//	см. ПолучитьДанныеДляПечатнойФормыТОРГ12
Функция ПолучитьДанныеДляПечатнойФормыАктНаПередачуПрав(ПараметрыПечати, МассивОбъектов) Экспорт
	Если ПараметрыПечати.Свойство("ВыводитьГТД") Тогда
		ПараметрыПечати.Удалить("ВыводитьГТД");
	КонецЕсли;
	Если ПараметрыПечати.Свойство("ВыводитьУслуги") Тогда
		ПараметрыПечати.ВыводитьУслуги = Истина;
	Иначе
		ПараметрыПечати.Вставить("ВыводитьУслуги", Истина);
	КонецЕсли;
	Возврат ПолучитьДанныеДляПечатнойФормыТОРГ12(ПараметрыПечати, МассивОбъектов);
КонецФункции

Функция СформироватьПечатнуюФормуРеестрНомеровГТД(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РеализацияТоваровУслуг_РеестрНомеровГТД";
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов();
	ИмяКолонкиКодов = КолонкаКодов.ИмяКолонки;
	ПредставлениеКолонкиКодов = КолонкаКодов.ПредставлениеКолонки;
	ВыводитьКоды = ЗначениеЗаполнено(ИмяКолонкиКодов);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаВидыЗапасов.Ссылка КАК Ссылка
	|
	|ПОМЕСТИТЬ ВтТаблицаДокументов
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитики
	|		ПО ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры = КлючиАналитики.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ДанныеНоменклатуры
	|		ПО КлючиАналитики.Номенклатура = ДанныеНоменклатуры.Ссылка
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка В(&МассивОбъектов)
	|	И ТаблицаВидыЗапасов.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|	И ДанныеНоменклатуры.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|											ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Контрагент КАК Контрагент,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.Организация.Префикс КАК Префикс,
	|	ДанныеДокумента.Менеджер.ФизическоеЛицо КАК МенеджерФизическоеЛицо
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДанныеДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтТаблицаДокументов КАК ТаблицаДокументов
	|	ПО
	|		ДанныеДокумента.Ссылка = ТаблицаДокументов.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.Код КАК Код,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.Артикул КАК Артикул,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.НаименованиеПолное КАК НаименованиеНоменклатуры,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика.НаименованиеПолное КАК Характеристика,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Серия КАК Серия,
	|	ТаблицаВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ТаблицаВидыЗапасов.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА
	|			ТаблицаВидыЗапасов.Ссылка.ВернутьМногооборотнуюТару
	|			И ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозвратнаяТара,
	|	СУММА(ТаблицаВидыЗапасов.Количество) КАК Количество
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТаблицаДокументов КАК ТаблицаДокументов
	|		ПО ТаблицаВидыЗапасов.Ссылка = ТаблицаДокументов.Ссылка
	|ГДЕ
	|	ТаблицаВидыЗапасов.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|	И ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры В
	|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаВидыЗапасов.Ссылка,
	|	ТаблицаВидыЗапасов.НомерСтроки,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Серия,
	|	ТаблицаВидыЗапасов.НомерГТД
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	ТаблицаВидыЗапасов.НомерСтроки
	|
	|ИТОГИ ПО
	|	Ссылка
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДанныеДокумента
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтТаблицаДокументов КАК ТаблицаДокументов
	|	ПО
	|		ДанныеДокумента.Ссылка = ТаблицаДокументов.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка В(&МассивОбъектов)
	|	И ТаблицаДокументов.Ссылка ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|");
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
		
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ПФ_MXL_РеестрНомеровГТД");
		
	МассивРезультатов = Запрос.ВыполнитьПакет(); 
	// МассивРезультатов[0] - ВтТаблицаДокументов
	ДанныеПечати = МассивРезультатов[1].Выбрать();
	ВыборкаПоДокументам = МассивРезультатов[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаПоДокументамБезГТД = МассивРезультатов[3].Выбрать();
	
	ПервыйДокумент = Истина;
	Пока ДанныеПечати.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Область = Макет.ПолучитьОбласть("Шапка");
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, Область, ДанныеПечати.Ссылка);
		Область.Параметры.ТекстЗаголовка = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(ДанныеПечати, НСтр("ru='Реестр номеров ГТД к накладной'"));
		Область.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(
			ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Организация, ДанныеПечати.Дата),
			"ПолноеНаименование");
		Область.Параметры.ПредставлениеПартнера = ФормированиеПечатныхФорм.ОписаниеОрганизации(
			ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Контрагент, ДанныеПечати.Дата),
			"ПолноеНаименование");
		Область.Параметры.Организация = ДанныеПечати.Организация;
		Область.Параметры.Контрагент = ДанныеПечати.Контрагент;
		ТабличныйДокумент.Вывести(Область);
				
		Область = Макет.ПолучитьОбласть("ШапкаТаблицы|НачалоСтроки");
		ТабличныйДокумент.Вывести(Область);
		Если ВыводитьКоды Тогда
			Область = Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаКодов");
			Область.Параметры.ИмяКолонкиКодов = ПредставлениеКолонкиКодов;
			ТабличныйДокумент.Присоединить(Область);
		КонецЕсли;
		Область = Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаТоваров");
		ТабличныйДокумент.Присоединить(Область);
				
		НомерСтроки = 1;
		
		СтруктураПоиска = Новый Структура("Ссылка", ДанныеПечати.Ссылка);
		ВыборкаПоДокументам.НайтиСледующий(СтруктураПоиска);
		ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
		Пока ВыборкаПоТоварам.Следующий() Цикл
					
			Область = Макет.ПолучитьОбласть("СтрокаТаблицы|НачалоСтроки");
			Область.Параметры.НомерСтроки = НомерСтроки;
			НомерСтроки = НомерСтроки + 1;
			ТабличныйДокумент.Вывести(Область);
			
			Если ВыводитьКоды Тогда
				Область = Макет.ПолучитьОбласть("СтрокаТаблицы|КолонкаКодов");
				Область.Параметры.ЗначениеКода = ВыборкаПоТоварам[ИмяКолонкиКодов];
				ТабличныйДокумент.Присоединить(Область);
			КонецЕсли;
			
			Область = Макет.ПолучитьОбласть("СтрокаТаблицы|КолонкаТоваров");
			Область.Параметры.Заполнить(ВыборкаПоТоварам);
			
			ДополнительныеПараметрыПолученияНаименованияДляПечати = НоменклатураКлиентСервер.ДополнительныеПараметрыПредставлениеНоменклатурыДляПечати();
			ДополнительныеПараметрыПолученияНаименованияДляПечати.ВозвратнаяТара = ВыборкаПоТоварам.ЭтоВозвратнаяТара;		
			ДополнительныеПараметрыПолученияНаименованияДляПечати.КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
			
			Область.Параметры.Товар = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
				ВыборкаПоТоварам.НаименованиеНоменклатуры,
				ВыборкаПоТоварам.Характеристика,
				,
				,
				ДополнительныеПараметрыПолученияНаименованияДляПечати);
			
			ТабличныйДокумент.Присоединить(Область);
			
		КонецЦикла;
				
		Область = Макет.ПолучитьОбласть("ПодвалТаблицы|НачалоСтроки");
		ТабличныйДокумент.Вывести(Область);
		Если ВыводитьКоды Тогда
			Область = Макет.ПолучитьОбласть("ПодвалТаблицы|КолонкаКодов");
			ТабличныйДокумент.Присоединить(Область);
		КонецЕсли;
		Область = Макет.ПолучитьОбласть("ПодвалТаблицы|КолонкаТоваров");
		ТабличныйДокумент.Присоединить(Область);
						
		Область = Макет.ПолучитьОбласть("Подписи");
		Область.Параметры.ИтоговаяСтрока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Всего наименований %1'"), Строка(НомерСтроки - 1));
		Если ЗначениеЗаполнено(ДанныеПечати.МенеджерФизическоеЛицо) Тогда
			Область.Параметры.Менеджер = ФизическиеЛицаУТ.ФамилияИнициалыФизЛица(ДанныеПечати.МенеджерФизическоеЛицо, ДанныеПечати.Дата);
		КонецЕсли;
		ТабличныйДокумент.Вывести(Область);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
			
	КонецЦикла;
	
	// Выведем сообщения о документах, для которых не требуется печатать реестр номеров ГТД.
	Пока ВыборкаПоДокументамБезГТД.Следующий() Цикл
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В документе %1 отсутствуют товары с номерами ГТД. Печать реестра номеров ГТД не требуется.'"),
			Строка(ВыборкаПоДокументамБезГТД.Ссылка));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Текст,
			ВыборкаПоДокументамБезГТД.Ссылка);
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Функция получает данные для формирования печатной формы М15
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.РеализацияТоваровУслуг - Массив ссылок на документы, по которым необходимо получить данные.
//
// Возвращаемое значение:
// 	Структура:
// 		* РезультатПоШапке - РезультатЗапроса
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса
//
Функция ПолучитьДанныеДляПечатнойФормыМ15(ПараметрыПечати, МассивОбъектов) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.Валюта КАК Валюта,
	|	ДанныеДокументов.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Выполнить();
	
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц);
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.Номер КАК Номер,
	|	РеализацияТоваровУслуг.Дата КАК Дата,
	|	РеализацияТоваровУслуг.Дата КАК ДатаСоставления,
	|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|	РеализацияТоваровУслуг.Организация КАК Организация,
	|	РеализацияТоваровУслуг.Организация.Префикс КАК Префикс,
	|	РеализацияТоваровУслуг.Основание КАК Основание,
	|	РеализацияТоваровУслуг.Склад КАК Склад,
	|	РеализацияТоваровУслуг.Склад.Наименование КАК СкладНаименование,
	|	РеализацияТоваровУслуг.Подразделение КАК СтруктурноеПодразделение,
	|	РеализацияТоваровУслуг.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации,
	|	РеализацияТоваровУслуг.Валюта КАК Валюта,
	|	РеализацияТоваровУслуг.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	РеализацияТоваровУслуг.Отпустил КАК Кладовщик,
	|	РеализацияТоваровУслуг.ОтпустилДолжность КАК ДолжностьКладовщика
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО РеализацияТоваровУслуг.Ссылка = ДанныеДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО РеализацияТоваровУслуг.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	ТаблицаТоваров.Номенклатура.Код КАК НоменклатураКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения КАК ЕдиницаИзмеренияКод,
	|	ТаблицаТоваров.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТаблицаТоваров.Серия.Наименование КАК СерияНаименование,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) = 1
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТаблицаТоваров.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	ТаблицаТоваров.КоличествоУпаковок КАК Количество,
	|	0 КАК КоличествоМест,
	|	ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.КоличествоУпаковок КАК Цена,
	|	ТаблицаТоваров.СуммаБезНДС КАК СуммаБезНДС,
	|	ТаблицаТоваров.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТоваров.СуммаБезНДС + ТаблицаТоваров.СуммаНДС КАК СуммаСНДС,
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА
	|			ТаблицаТоваров.Ссылка.ВернутьМногооборотнуюТару
	|			И ТаблицаТоваров.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	РеализацияТоваровУслугТаблицаТоваров КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.ЭтоТовар
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	РезультатПоШапке = МассивРезультатов[0];
	РезультатПоТабличнойЧасти = МассивРезультатов[1];
	
	СтруктураДанныхДляПечати = Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти",
		РезультатПоШапке,
		РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Формирует текст запроса для получения данных основания при печати Счет-фактуры.
// Возвращаемое значение:
//	Строка
Функция ТекстЗапросаДанныхОснованияДляПечатнойФормыСчетФактура() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка                                   КАК Ссылка,
	|	ДанныеДокументов.ХозяйственнаяОперация                    КАК ХозяйственнаяОперация,
	|	ДанныеДокументов.Валюта                                   КАК Валюта,
	|	ДанныеДокументов.Организация                              КАК Организация,
	|	ДанныеДокументов.НалогообложениеНДС                       КАК НалогообложениеНДС,
	|	ДанныеДокументов.Подразделение                            КАК Подразделение,
	|	ДанныеДокументов.Склад                                    КАК Склад,
	|	ДанныеДокументов.Грузоотправитель                         КАК Грузоотправитель,
	|	ДанныеДокументов.Грузополучатель                          КАК Грузополучатель,
	|	ЛОЖЬ                                                      КАК РасчетыЧерезОтдельногоКонтрагента,
	|	НЕОПРЕДЕЛЕНО                                              КАК Номенклатура,
	|	""""                                                      КАК Содержание,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)             КАК Комиссионер,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокументов.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионера), 
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности)
	|				) ТОГДА
	|			ДанныеДокументов.Договор
	|		ИНАЧЕ
	|			ДанныеДокументов.Основание
	|		КОНЕЦ                                                 КАК Основание,
	|
	|	ДанныеДокументов.ОснованиеДата                            КАК ОснованиеДата,
	|	ДанныеДокументов.ОснованиеНомер                           КАК ОснованиеНомер,
	|	ДанныеДокументов.БанковскийСчетОрганизации                КАК БанковскийСчетОрганизации,
	|	ДанныеДокументов.БанковскийСчетКонтрагента                КАК БанковскийСчетКонтрагента,
	|	ДанныеДокументов.БанковскийСчетГрузоотправителя           КАК БанковскийСчетГрузоотправителя,
	|	ДанныеДокументов.БанковскийСчетГрузополучателя            КАК БанковскийСчетГрузополучателя,
	|	ДанныеДокументов.ДоверенностьНомер                        КАК ДоверенностьНомер,
	|	ДанныеДокументов.ДоверенностьДата                         КАК ДоверенностьДата,
	|	ДанныеДокументов.ДоверенностьВыдана                       КАК ДоверенностьВыдана,
	|	ДанныеДокументов.ДоверенностьЛицо                         КАК ДоверенностьЛицо,
	|	ДанныеДокументов.Менеджер                                 КАК Менеджер,
	|	ДанныеДокументов.Отпустил                                 КАК Кладовщик,
	|	ДанныеДокументов.ОтпустилДолжность                        КАК ДолжностьКладовщика,
	|	ДанныеДокументов.Дата                                     КАК Период
	|
	|//ОператорПОМЕСТИТЬ
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДанныеДокументов
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&ДокументОснование_РеализацияТоваровУслуг)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает данные для формирования печатной формы УПД
//
// Параметры:
//	ПараметрыПечати - Структура - параметры
//	МассивОбъектов - Массив из ДокументСсылка.РеализацияТоваровУслуг - ссылки на документы, по которым необходимо
//																		получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемая при печати УПД, содержащая следующие свойства:
//		* РезультатПоШапке - РезультатЗапроса - общие данные накладной.
//		* РезультатПоТабличнойЧасти - РезультатЗапроса - данные табличной части накладной.
//		* Маркировка - см. ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое
//		* ПрослеживаемыеКомплектующие - см. УчетПрослеживаемыхТоваровЛокализация.ПрослеживаемыеКомплектующиеДляПечатиДанных
//
Функция ПолучитьДанныеДляПечатнойФормыУПД(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;

	// ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	Контракт = Неопределено;
	ЕстьЭлектронноеАктирование = Ложь;
	Для Каждого Основание Из МассивОбъектов Цикл
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Основание, "Договор") Тогда
			ДоговорОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "Договор");
			Если ТипЗнч(ДоговорОснования) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
				Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорОснования, "Организация");
				ПараметрыОтправкиВЕИС = ЭлектронноеАктированиеЕИСУТ.ПараметрыОтправкиВЕИС(
						Организация, ДоговорОснования);
				ЕстьЭлектронноеАктирование = ПараметрыОтправкиВЕИС.ВозможнаОтправка
					И ЭлектронноеАктированиеЕИСУТ.ДокументОтправляетсяВЕИС(Основание);
				Контракт = ПараметрыОтправкиВЕИС.Контракт;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	// Конец ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.Валюта КАК Валюта,
	|	ДанныеДокументов.Организация КАК Организация,
	|	ДанныеДокументов.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
	|	ДанныеДокументов.Подразделение КАК Подразделение,
	|	ДанныеДокументов.Склад КАК Склад,
	|	ДанныеДокументов.Дата КАК Период
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	УчетНДСРФ.ДополнитьЗапросПолученияДанныхДляПечатиУПД(Запрос);

	Запрос.Выполнить();
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	ПараметрыЗаполнения.ВключаяНомераГТД                 = Истина;
	ПараметрыЗаполнения.ОбработатьНастройкиПечатиНаборов = Истина;
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	Обработки.ПечатьОбщихФорм.ПоместитьВременнуюТаблицуДанныхПоставщика(МенеджерВременныхТаблиц);
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА НЕ ТаблицаТоваров.ЭтоТовар
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьУслуги,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ТаблицаТоваров.ЭтоТовар
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьТовары,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(ТаблицаТоваров.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьПрослеживаемыеТовары
	|ПОМЕСТИТЬ НоменклатураДокументов
	|ИЗ
	|	РеализацияТоваровУслугТаблицаТоваров КАК ТаблицаТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	&ПредставлениеСчетФактура КАК ПредставлениеДокумента,
	|	2 КАК СтатусУПД,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Дата КАК Дата,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправления,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправления,
	|	ЛОЖЬ КАК Исправление,
	|	НЕОПРЕДЕЛЕНО КАК НомерСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправленияСчетаФактуры,
	|	ЛОЖЬ КАК КорректировочныйСчетФактура,
	|	"""" КАК СтрокаПоДокументу,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаСчетаФактуры,
	|	ДанныеДокумента.Партнер КАК Партнер,
	|
	|	ДанныеДокумента.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ДанныеПоставщика.ГоловнаяОрганизация КАК Организация,
	|
	|	ВЫБОР КОГДА &РеализацияЧерезКомиссионера ТОГДА
	|		ДанныеДокумента.КлиентКонтрагент
	|	ИНАЧЕ ВЫБОР
	|			КОГДА ДанныеДокумента.Контрагент.ОбособленноеПодразделение
	|				ТОГДА ДанныеДокумента.Контрагент.ГоловнойКонтрагент
	|			ИНАЧЕ ДанныеДокумента.Контрагент
	|		КОНЕЦ 
	|	КОНЕЦ КАК Контрагент,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ДанныеДокумента.Организация
	|		ИНАЧЕ ДанныеДокумента.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) ТОГДА
	|			ВЫБОР КОГДА &РеализацияЧерезКомиссионера ТОГДА
	|					ДанныеДокумента.КлиентКонтрагент
	|				ИНАЧЕ ДанныеДокумента.Контрагент
	|			КОНЕЦ
	|		ИНАЧЕ ДанныеДокумента.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|
	|	ДанныеДокумента.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации,
	|	ДанныеДокумента.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	ДанныеДокумента.БанковскийСчетГрузоотправителя КАК БанковскийСчетГрузоотправителя,
	|	ДанныеДокумента.БанковскийСчетГрузополучателя КАК БанковскийСчетГрузополучателя,
	|
	|	ЕСТЬNULL(ДанныеПоставщика.ИндексПодразделения, 0) КАК ИндексПодразделения,
	|
	|	ДанныеПоставщика.КПППоставщика КАК КПППоставщика,
	|
	|	ВЫБОР КОГДА &РеализацияЧерезКомиссионера ТОГДА
	|		ДанныеДокумента.КлиентКонтрагент.КПП
	|	ИНАЧЕ ВЫБОР
	|			КОГДА
	|				ДанныеДокумента.Грузополучатель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				И ДанныеДокумента.Грузополучатель.ГоловнойКонтрагент = ДанныеДокумента.Контрагент
	|			ТОГДА
	|				ДанныеДокумента.Грузополучатель.КПП
	|			ИНАЧЕ
	|				ДанныеДокумента.Контрагент.КПП
	|			КОНЕЦ
	|	КОНЕЦ КАК КПППокупателя,
	|
	|	ВЫБОР КОГДА &РеализацияЧерезКомиссионера ТОГДА
	|		ДанныеДокумента.КлиентКонтрагент
	|	ИНАЧЕ
	|		ДанныеДокумента.Контрагент.ИНН 
	|	КОНЕЦ КАК ИННПокупателя,
	|
	|	ДанныеДокумента.Организация.Префикс КАК Префикс,
	|	ДанныеДокумента.АдресДоставки КАК АдресДоставки,
	|
	|	ДанныеДокумента.Валюта КАК Валюта,
	|	ДанныеДокумента.Валюта.НаименованиеПолное КАК ВалютаНаименованиеПолное,
	|	ДанныеДокумента.Валюта.Код КАК ВалютаКод,
	|	ВЫБОР
	|		КОГДА НоменклатураДокументов.ЕстьУслуги
	|				И НЕ НоменклатураДокументов.ЕстьТовары
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТолькоУслуги,
	|	НоменклатураДокументов.ЕстьПрослеживаемыеТовары КАК ЕстьПрослеживаемыеТовары,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоПередачаНаКомиссию,
	|	ДанныеДокумента.Основание КАК Основание,
	|	ДанныеДокумента.ОснованиеДата КАК ОснованиеДата,
	|	ДанныеДокумента.ОснованиеНомер КАК ОснованиеНомер,
	|	ДанныеДокумента.ДоверенностьНомер КАК ДоверенностьНомер,
	|	ДанныеДокумента.ДоверенностьДата КАК ДоверенностьДата,
	|	ДанныеДокумента.ДоверенностьВыдана КАК ДоверенностьВыдана,
	|	ДанныеДокумента.ДоверенностьЛицо КАК ДоверенностьЛицо,
	|	ДанныеДокумента.Менеджер КАК Менеджер,	
	|	ДанныеДокумента.Отпустил КАК Кладовщик,
	|	ДанныеДокумента.ОтпустилДолжность КАК ДолжностьКладовщика,
	|	ДанныеДокументов.ТребуетсяНаличиеСФ КАК ТребуетсяНаличиеСФ
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ДанныеДокумента.Ссылка = ДанныеДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоставщика КАК ДанныеПоставщика
	|		ПО ДанныеДокумента.Ссылка = ДанныеПоставщика.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураДокументов КАК НоменклатураДокументов
	|		ПО ДанныеДокумента.Ссылка = НоменклатураДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ТаблицаДокумента.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	ТаблицаДокумента.НоменклатураНабора КАК НоменклатураНабора,
	|	ТаблицаДокумента.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ТаблицаДокумента.ЭтоКомплектующие КАК ЭтоКомплектующие,
	|	ТаблицаДокумента.ЭтоНабор КАК ЭтоНабор,
	|	ТаблицаДокумента.ПолныйНабор КАК ПолныйНабор,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.НоменклатураПартнера КАК НоменклатураПартнера,
	|	ТаблицаДокумента.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаДокумента.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаДокумента.Номенклатура.Код
	|	КОНЕЦ КАК НоменклатураКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ &ТекстЗапросаКодЕдиницыИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения               КАК ЕдиницаИзмеренияТНВЭД,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияТНВЭДНаименование,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Код           КАК ЕдиницаИзмеренияТНВЭДКод,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТаблицаДокумента.Серия.Наименование КАК СерияНаименование,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.НомерГТД КАК НомерГТДСсылка,
	|	ЕСТЬNULL(ТаблицаДокумента.НомерГТД.ТипНомераГТД, НЕОПРЕДЕЛЕНО) КАК ТипНомераГТД,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.НомерГТД.РегистрационныйНомер = """"
	|			ТОГДА ТаблицаДокумента.НомерГТД
	|		ИНАЧЕ ТаблицаДокумента.НомерГТД.РегистрационныйНомер
	|	КОНЕЦ КАК НомерГТД,
	|	ТаблицаДокумента.КодТНВЭД КАК КодТНВЭД,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Количество
	|		ИНАЧЕ ТаблицаДокумента.КоличествоУпаковок
	|	КОНЕЦ КАК Количество,
	|	ТаблицаДокумента.Количество КАК КоличествоБазовыхЕдиниц,
	|	ТаблицаДокумента.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ВЫБОР КОГДА НЕ ТаблицаДокумента.КоличествоУпаковок = 0 ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.КоличествоУпаковок ИНАЧЕ 0 КОНЕЦ
	|		ИНАЧЕ ВЫБОР КОГДА НЕ ТаблицаДокумента.Количество = 0 ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.Количество ИНАЧЕ 0 КОНЕЦ
	|	КОНЕЦ КАК Цена,
	|	ТаблицаДокумента.СуммаБезНДС КАК СуммаБезНДС,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС КАК СуммаСНДС,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Серия КАК Серия,
	|	ТаблицаДокумента.Упаковка КАК Упаковка,
	|	ЛОЖЬ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	РеализацияТоваровУслугТаблицаТоваров КАК ТаблицаДокумента
	|ГДЕ
	|	(ТаблицаДокумента.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			ИЛИ ТаблицаДокумента.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|				И НЕ ТаблицаДокумента.Ссылка.ВернутьМногооборотнуюТару)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДокумента.НомерСтрокиНаборы,
	|	ТаблицаДокумента.ЭтоНабор УБЫВ,
	|	ТаблицаДокумента.НомерСтроки
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ШтрихкодУпаковки КАК Штрихкод
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ШтрихкодыУпаковок КАК ТаблицаДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаДокумента.Ссылка = ДокументыПрямогоОбмена.ДокументОснование
	|		И НЕ ДокументыПрямогоОбмена.ПометкаУдаления
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В(&МассивОбъектов)
	|	И ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка								КАК Ссылка,
	|	ТаблицаДокумента.НомерГТД							КАК НомерГТД,
	|	Комплектующие.НомерРНПТ								КАК НомерРНПТ,
	|	ВЫБОР
	|		КОГДА Комплектующие.НомерРНПТ.РегистрационныйНомер = """"
	|			ТОГДА Комплектующие.НомерРНПТ
	|		ИНАЧЕ Комплектующие.НомерРНПТ.РегистрационныйНомер
	|	КОНЕЦ												КАК НомерТовара,
	|	НомераГТД.СуммаПоРНПТ								КАК ОбщаяСуммаПоКомплекту,
	|	Комплектующие.СуммаПоРНПТ							КАК СуммаПоРНПТ,
	|	Комплектующие.КоличествоПоРНПТ						КАК Количество,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД					КАК ЕдиницаИзмерения,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД.Код				КАК ЕдиницаИзмеренияКод,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД.Представление	КАК ЕдиницаИзмеренияНаименование
	|ИЗ
	|	РеализацияТоваровУслугТаблицаТоваров КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД.ПрослеживаемыеКомплектующие КАК Комплектующие
	|		ПО ТаблицаДокумента.НомерГТД = Комплектующие.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК НомераГТД
	|		ПО ТаблицаДокумента.НомерГТД = НомераГТД.Ссылка";
	
	// ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	Если ЕстьЭлектронноеАктирование Тогда
		Запрос.Текст = Запрос.Текст +
		"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка КАК Ссылка,
		|	ТаблицаДокумента.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
		|	ТаблицаДокумента.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
		|	ТаблицаДокумента.НоменклатураНабора КАК НоменклатураНабора,
		|	ТаблицаДокумента.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|	ТаблицаДокумента.ЭтоКомплектующие КАК ЭтоКомплектующие,
		|	ТаблицаДокумента.ЭтоНабор КАК ЭтоНабор,
		|	ТаблицаДокумента.ПолныйНабор КАК ПолныйНабор,
		|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
		|	ТаблицаДокумента.НоменклатураПартнера КАК НоменклатураПартнера,
		|	ТаблицаДокумента.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
		|	ВЫБОР
		|		КОГДА &КолонкаКодов = ""Артикул""
		|			ТОГДА ТаблицаДокумента.Номенклатура.Артикул
		|		ИНАЧЕ ТаблицаДокумента.Номенклатура.Код
		|	КОНЕЦ КАК НоменклатураКод,
		|	ВЫБОР
		|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
		|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения
		|		ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения
		|	КОНЕЦ КАК ЕдиницаИзмерения,
		|	ВЫБОР
		|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
		|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Представление
		|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения
		|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
		|	ВЫБОР
		|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
		|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Код
		|		ИНАЧЕ &ТекстЗапросаКодЕдиницыИзмерения
		|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
		|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения               КАК ЕдиницаИзмеренияТНВЭД,
		|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияТНВЭДНаименование,
		|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Код           КАК ЕдиницаИзмеренияТНВЭДКод,
		|	ТаблицаДокумента.Характеристика КАК Характеристика,
		|	ТаблицаДокумента.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
		|	ТаблицаДокумента.Серия.Наименование КАК СерияНаименование,	
		|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.НомерГТД.РегистрационныйНомер = """"
		|			ТОГДА ТаблицаДокумента.НомерГТД
		|		ИНАЧЕ ТаблицаДокумента.НомерГТД.РегистрационныйНомер
		|	КОНЕЦ КАК НомерГТД,
		|	ТаблицаДокумента.КодТНВЭД КАК КодТНВЭД,
		|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
		|	ВЫБОР
		|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
		|			ТОГДА ТаблицаДокумента.Количество
		|		ИНАЧЕ ТаблицаДокумента.КоличествоУпаковок
		|	КОНЕЦ КАК Количество,
		|	ТаблицаДокумента.Количество КАК КоличествоБазовыхЕдиниц,
		|	ТаблицаДокумента.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
		|	ВЫБОР
		|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
		|			ТОГДА ВЫБОР КОГДА НЕ ТаблицаДокумента.КоличествоУпаковок = 0 ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.КоличествоУпаковок ИНАЧЕ 0 КОНЕЦ
		|		ИНАЧЕ ВЫБОР КОГДА НЕ ТаблицаДокумента.Количество = 0 ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.Количество ИНАЧЕ 0 КОНЕЦ
		|	КОНЕЦ КАК Цена,
		|	ТаблицаДокумента.СуммаБезНДС КАК СуммаБезНДС,
		|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
		|	ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС КАК СуммаСНДС,
		|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
		|	ТаблицаДокумента.Серия КАК Серия,
		|	ТаблицаДокумента.Упаковка КАК Упаковка,
		|	ЛОЖЬ КАК ЭтоВозвратнаяТара
		|
		|ПОМЕСТИТЬ ТаблицаТоваровДокументов
		|ИЗ
		|	РеализацияТоваровУслугТаблицаТоваров КАК ТаблицаДокумента
		|ГДЕ
		|	(ТаблицаДокумента.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|			ИЛИ ТаблицаДокумента.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|				И НЕ ТаблицаДокумента.Ссылка.ВернутьМногооборотнуюТару)
		|"
		+ ЭлектронноеАктированиеЕИСУТ.ТекстЗапросаДанныеДляПечатиСчетовФактур("МассивОбъектов");
		Запрос.УстановитьПараметр("ГосударственныйКонтрактЕИС", Контракт);
		Запрос.УстановитьПараметр("ЭтапИсполненияКонтрактаЕИС", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(МассивОбъектов[0], "ЭтапГосконтрактаЕИС"));
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС

	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&РеализацияЧерезКомиссионера",
		КомиссионнаяТорговляСервер.ТекстЗапросаРеализацияЧерезКомиссионера("ДанныеДокумента.ХозяйственнаяОперация"));
	
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения", Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
	Запрос.УстановитьПараметр("ПредставлениеСчетФактура", НСтр("ru='счет-фактура'"));
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	
	МассивРезультатов			= Запрос.ВыполнитьПакет();
	РезультатПоШапке			= МассивРезультатов[1];
	РезультатПоТабличнойЧасти	= МассивРезультатов[2];
	ШтрихкодыУпаковок			= МассивРезультатов[3].Выгрузить();
	
	Если ПараметрыПечати.Свойство("ЗаполнениеЭлектронногоДокумента") Тогда
		ТаблицаТоварыДляЭДО = ИнтеграцияИСМПУТ.ТаблицаТоварыДляЭДО(РезультатПоТабличнойЧасти.Выгрузить(), "КоличествоБазовыхЕдиниц");
		Если ТаблицаТоварыДляЭДО.Количество() Тогда
			ПараметрыСканирования = ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования(ТаблицаТоварыДляЭДО[0].Ссылка);
		КонецЕсли;
		Маркировка = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимоеИКодыОСУ(
			ШтрихкодыУпаковок,
			ТаблицаТоварыДляЭДО,
			ПараметрыСканирования);
	Иначе
		Маркировка = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое(ШтрихкодыУпаковок);
	КонецЕсли;
	
	ПрослеживаемыеКомплектующие	= УчетПрослеживаемыхТоваровЛокализация.ПрослеживаемыеКомплектующиеДляПечатиДанных(
									МассивРезультатов[4]);
	
	ДанныеДляПечати = Новый Структура;
	ДанныеДляПечати.Вставить("РезультатПоШапке",			РезультатПоШапке);
	ДанныеДляПечати.Вставить("РезультатПоТабличнойЧасти",	РезультатПоТабличнойЧасти);
	ДанныеДляПечати.Вставить("Маркировка",					Маркировка);
	ДанныеДляПечати.Вставить("ПрослеживаемыеКомплектующие",	ПрослеживаемыеКомплектующие);


	// ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	Если ЕстьЭлектронноеАктирование Тогда	
		КоличествоРезультатов     = МассивРезультатов.Количество() - 5;
		ЭлектронноеАктированиеЕИСУТ.ПоместитьРезультатВыполненияЗапросаВДанныеДляПечати(
			МассивРезультатов, ДанныеДляПечати, КоличествоРезультатов);
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС

	
	Возврат ДанныеДляПечати;
	
КонецФункции

// Возвращает данные для формирования печатной формы МХ - 3.
//
// Параметры:
//	ПараметрыПечати	- Структура -дополнительные настройки печати.
//	МассивОбъектов	- Массив из ДокументСсылка.РеализацияТоваровУслуг - коллекция значений ссылок на документы,
//																		по которым необходимо получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемых для печати, содержащая следующие следующие свойства:
//		* РезультатПоШапке			- РезультатЗапроса - данные шапки документа.
//		* РезультатПоСкладам		- РезультатЗапроса - данные о складе ответственного хранения.
//		* РезультатПоТабличнойЧасти	- РезультатЗапроса - данные табличной части документа.
//		* РезультатПоОшибкам		- РезультатЗапроса - данные об ошибках, возникающих при печати документа.
//
Функция ПолучитьДанныеДляПечатнойФормыМХ3(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка									КАК Ссылка,
	|	ДанныеДокумента.Номер									КАК Номер,
	|	ДанныеДокумента.Дата									КАК Дата,
	|	ДанныеДокумента.Дата									КАК ДатаДокумента,
	|	ДанныеДокумента.Организация								КАК Организация,
	|	ДанныеДокумента.Организация.Префикс						КАК Префикс,
	|	ТоварыДокумента.Склад									КАК Склад,
	|	ЕСТЬNULL(Склады.СкладОтветственногоХранения, ЛОЖЬ)		КАК СкладОтветственногоХранения,
	|	ДанныеДокумента.Организация = Склады.Поклажедержатель	КАК ОрганизацияПоклажедержатель,
	|	Склады.ИсточникИнформацииОЦенахДляПечати				КАК ИсточникИнформацииОЦенахДляПечати,
	|	Склады.УчетныйВидЦены									КАК ВидЦены,
	|	Склады.УчетныйВидЦены.ВалютаЦены						КАК ВалютаЦены,
	|	РасчетСебестоимостиТоваровОрганизации.Ссылка.ПредварительныйРасчет	КАК ПредварительныйРасчет,
	|	ДанныеДокумента.Дата									КАК ДатаПолученияСебестоимости
	|ПОМЕСТИТЬ ДанныеДокумента
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТоварыДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК ДанныеДокумента
	|		ПО ТоварыДокумента.Ссылка = ДанныеДокумента.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК РасчетСебестоимостиТоваровОрганизации
	|		ПО РасчетСебестоимостиТоваровОрганизации.Ссылка.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ)
	|			И РасчетСебестоимостиТоваровОрганизации.Ссылка.Проведен
	|			И ДанныеДокумента.Организация = РасчетСебестоимостиТоваровОрганизации.Организация
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО ТоварыДокумента.Склад = Склады.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Проведен
	|	И ДанныеДокумента.Ссылка В(&МассивДокументов)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка								КАК Ссылка,
	|	ДанныеДокумента.Номер								КАК Номер,
	|	ДанныеДокумента.Дата								КАК Дата,
	|	ДанныеДокумента.ДатаДокумента						КАК ДатаДокумента,
	|	ДанныеДокумента.Организация							КАК Организация,
	|	ДанныеДокумента.Префикс								КАК Префикс,
	|	ДанныеДокумента.Склад								КАК Склад,
	|	ДанныеДокумента.ИсточникИнформацииОЦенахДляПечати	КАК ИсточникИнформацииОЦенахДляПечати,
	|	ДанныеДокумента.ВидЦены								КАК ВидЦены,
	|	ДанныеДокумента.ВалютаЦены							КАК ВалютаЦены,
	|	ДанныеДокумента.ПредварительныйРасчет				КАК ПредварительныйРасчет,
	|	ДанныеДокумента.ДатаПолученияСебестоимости			КАК ДатаПолученияСебестоимости
	|ПОМЕСТИТЬ ВтШапка
	|ИЗ
	|	ДанныеДокумента КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.СкладОтветственногоХранения
	|	И НЕ ДанныеДокумента.ОрганизацияПоклажедержатель
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Склад КАК Склад,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Серия КАК Серия,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения1 КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения1 КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения1 КАК ВидУпаковки,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Товары.Количество КАК Количество,
	|	КОНЕЦПЕРИОДА(Товары.Ссылка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	Шапка.ВидЦены КАК ВидЦены,
	|	Шапка.ВалютаЦены КАК ВалютаЦены
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО Товары.Склад = Шапка.Склад
	|			И Товары.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	Товары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоВидуЦен)
	|		ИЛИ (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|			И Шапка.ПредварительныйРасчет ЕСТЬ NULL))
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК Ссылка,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	Шапка.Организация КАК Организация,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.МестоХранения КАК Склад,
	|	ВидыЗапасов.Упаковка КАК Упаковка,
	|	ВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения2 КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения2 КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения2 КАК ВидУпаковки,
	|	ВидыЗапасов.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ВидыЗапасов.Количество КАК Количество,
	|	КОНЕЦПЕРИОДА(ВидыЗапасов.Ссылка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория.УчетныйВидЦены КАК ВидЦены,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория.УчетныйВидЦены.ВалютаЦены КАК ВалютаЦены,
	|	Шапка.ПредварительныйРасчет КАК ПредварительныйРасчет
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ВидыЗапасов КАК ВидыЗапасов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО ВидыЗапасов.АналитикаУчетаНоменклатуры.МестоХранения = Шапка.Склад
	|			И ВидыЗапасов.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|;
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование", "Товары.Упаковка", "Товары.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код", "Товары.Упаковка", "Товары.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование", "ВидыЗапасов.Упаковка", "ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код", "ВидыЗапасов.Упаковка", "ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура"));
	
	СкладыСервер.ДополнитьТекстЗапросаДляПечатныхФормМХ1Х3(Запрос);
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	РезультатПоШапке			= МассивРезультатов[МассивРезультатов.ВГраница() - 3];
	РезультатПоСкладам			= МассивРезультатов[МассивРезультатов.ВГраница() - 2];
	РезультатПоТабличнойЧасти	= МассивРезультатов[МассивРезультатов.ВГраница() - 1];
	РезультатПоОшибкам			= МассивРезультатов[МассивРезультатов.ВГраница()];
	
	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("РезультатПоШапке",			РезультатПоШапке);
	СтруктураДанныхДляПечати.Вставить("РезультатПоСкладам",			РезультатПоСкладам);
	СтруктураДанныхДляПечати.Вставить("РезультатПоТабличнойЧасти",	РезультатПоТабличнойЧасти);
	СтруктураДанныхДляПечати.Вставить("РезультатПоОшибкам",			РезультатПоОшибкам);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Формирует временную таблицу, содержащую табличную часть по таблице данных документов.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц, содержащий таблицу ТаблицаДанныхДокументов с полями:
//		Ссылка,
//		Валюта.
//
//	ПараметрыЗаполнения - Структура - структура, возвращаемая функцией ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров.
//
Процедура ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения = Неопределено) Экспорт
	
	Если ПараметрыЗаполнения = Неопределено Тогда
		ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета",     Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ПересчитыватьВВалютуРегл",       ПараметрыЗаполнения.ПересчитыватьВВалютуРегл);
	Запрос.УстановитьПараметр("ВключаяНомераГТД",               ПараметрыЗаполнения.ВключаяНомераГТД);
	Запрос.УстановитьПараметр("ПустаяГТД",                      Справочники.НомераГТД.ПустаяСсылка());
	Запрос.УстановитьПараметр("ВыводитьСерии",                  ПараметрыЗаполнения.ВыводитьСерииНоменклатуры);
	
	Если ПараметрыЗаполнения.ПересчитыватьВВалютуРегл И ПараметрыЗаполнения.АктуализироватьРасчеты Тогда
		Если НЕ ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
			Запрос.Текст = "
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
			|ИЗ
			|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
			|
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|		ТаблицаДанныхДокументов КАК ДанныеДокументов
			|	ПО
			|		РасчетыСКлиентами.Регистратор = ДанныеДокументов.Ссылка
			|
			|ГДЕ
			|	ДанныеДокументов.Валюта <> ДанныеДокументов.Ссылка.Организация.ВалютаРегламентированногоУчета
			|	И РасчетыСКлиентами.Активность
			|";
			ТаблицаАналитик = Запрос.Выполнить().Выгрузить();
			МассивАналитикУчетаПоПартнерам = ТаблицаАналитик.ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
			
			Если МассивАналитикУчетаПоПартнерам.Количество() > 0 Тогда
				ОкончаниеПериодаРасчета = КонецМесяца(ВзаиморасчетыСервер.ПолучитьМаксимальнуюДатуВКоллекцииДокументов(МенеджерВременныхТаблиц)) + 1;
				АналитикиРасчета = РаспределениеВзаиморасчетовВызовСервера.АналитикиРасчета();
				АналитикиРасчета.АналитикиУчетаПоПартнерам = МассивАналитикУчетаПоПартнерам;
				Попытка
					РаспределениеВзаиморасчетовВызовСервера.РаспределитьВсеРасчетыСКлиентами(ОкончаниеПериодаРасчета, АналитикиРасчета);
				Исключение
					ТекстСообщения = НСтр("ru ='Печатная форма сформирована по неактуальным данным.
					|Необходимо актуализировать взаиморасчеты вручную и переформировать печатную форму.'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				КонецПопытки;
			КонецЕсли;
		Иначе
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДанныеДокументов.Ссылка КАК Ссылка
			|ИЗ
			|	ТаблицаДанныхДокументов КАК ДанныеДокументов
			|ГДЕ 
			|	ДанныеДокументов.Валюта <> ДанныеДокументов.Ссылка.Организация.ВалютаРегламентированногоУчета
			|	ИЛИ ДанныеДокументов.Валюта <> &ВалютаУправленческогоУчета";
			МассивДокументов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
			РегистрыСведений.СуммыДокументовВВалютахУчета.РассчитатьСуммыДокументовВВалютахУчета(МассивДокументов);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.АналитикаУчетаНаборов КАК АналитикаУчетаНаборов,
	|	ТаблицаТоваров.Упаковка КАК Упаковка,
	|	ТаблицаТоваров.НоменклатураПартнера КАК НоменклатураПартнера,
	|	ТаблицаТоваров.Цена КАК Цена,
	|	МАКСИМУМ(ТаблицаТоваров.НомерСтроки) КАК НомерСтроки
	|
	|ПОМЕСТИТЬ СтрокиТоваров
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТоваров
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО
	|		ТаблицаТоваров.Ссылка = ДанныеДокументов.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.АналитикаУчетаНаборов,
	|	ТаблицаТоваров.Упаковка,
	|	ТаблицаТоваров.НоменклатураПартнера,
	|	ТаблицаТоваров.Цена
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	АналитикаУчетаНоменклатуры,
	|	АналитикаУчетаНаборов,
	|	Упаковка,
	|	НоменклатураПартнера,
	|	Цена
	|;
	|
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|ПОМЕСТИТЬ ВидыЗапасовРабот
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ВидыЗапасов КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ТаблицаТоваров.Ссылка = ДанныеДокументов.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаТоваров.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Аналитика.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	АналитикаУчетаНоменклатуры
	|;
	|
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                 КАК Ссылка,
	|	ДАТАВРЕМЯ(1,1,1)                                        КАК ДатаСчетаФактурыКомиссионера,
	|	НЕОПРЕДЕЛЕНО                                            КАК НомерСчетаФактурыКомиссионера,
	|	ТаблицаДокумента.Ссылка.КлиентКонтрагент                КАК Покупатель,
	|	ВариантыКомплектацииНоменклатуры.Ссылка КАК ВариантКомплектацииНоменклатуры,
	|	ВариантыКомплектацииНоменклатуры.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ВариантыКомплектацииНоменклатуры.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	ТаблицаДокумента.НоменклатураНабора КАК НоменклатураНабора,
	|	ТаблицаДокумента.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ТаблицаДокумента.НомерСтроки                            КАК НомерСтроки,
	|	ТаблицаДокумента.Номенклатура                           КАК Номенклатура,
	|	ТаблицаДокумента.НоменклатураПартнера                   КАК НоменклатураПартнера,
	|	ТаблицаДокумента.Характеристика                         КАК Характеристика,
	|	ТаблицаДокумента.Серия                                  КАК Серия,
	|	&ПустаяГТД                                              КАК НомерГТД,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)    КАК КодТНВЭД,
	|	ТаблицаДокумента.Количество                             КАК Количество,
	|	ТаблицаДокумента.Количество                             КАК КоличествоУпаковок,
	|	0                                                       КАК КоличествоПоРНПТ,
	|	ТаблицаДокумента.Цена                                   КАК Цена,
	|	ЕСТЬNULL(
	|		СуммыДокументовВВалютахУчета.БазаНДСРегл,
	|		ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС
	|	) КАК СуммаБезНДС,
	|	
	|	ТаблицаДокумента.СтавкаНДС                              КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(
	|		СуммыДокументовВВалютахУчета.СуммаНДСРегл,
	|		ТаблицаДокумента.СуммаНДС
	|	) КАК СуммаНДС,
	|	
	|	ЛОЖЬ                                                    КАК ЭтоТовар,
	|	ЛОЖЬ                                                    КАК ЭтоНеВозвратнаяТара,
	|	ЛОЖЬ                                                    КАК ВернутьМногооборотнуюТару,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)  КАК Упаковка,
	|	0                                                       КАК МассаНеттоНоменклатуры
	|
	|ПОМЕСТИТЬ РеализацияТоваровУслугТаблицаТоваров
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО
	|		ТаблицаДокумента.Ссылка = ДанныеДокументов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|	ПО
	|		ТаблицаДокумента.Ссылка = СуммыДокументовВВалютахУчета.Регистратор
	|		И ТаблицаДокумента.ИдентификаторСтроки = СуммыДокументовВВалютахУчета.ИдентификаторСтроки
	|		И СуммыДокументовВВалютахУчета.Активность
	|		И &ПересчитыватьВВалютуРегл
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
	|		ПО ВариантыКомплектацииНоменклатуры.Владелец = ТаблицаДокумента.НоменклатураНабора
	|		И ВариантыКомплектацииНоменклатуры.Характеристика = ТаблицаДокумента.ХарактеристикаНабора
	|		И ВариантыКомплектацииНоменклатуры.Основной
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВидыЗапасовРабот КАК ВидыЗапасовРабот
	|		ПО ТаблицаДокумента.Ссылка = ВидыЗапасовРабот.Ссылка
	|		И ТаблицаДокумента.АналитикаУчетаНоменклатуры = ВидыЗапасовРабот.АналитикаУчетаНоменклатуры
	|
	|ГДЕ
	|	ТаблицаДокумента.Номенклатура.ТипНоменклатуры В
	|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
	|	И ВидыЗапасовРабот.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                        КАК Ссылка,
	|	ДАТАВРЕМЯ(1,1,1)                                               КАК ДатаСчетаФактурыКомиссионера,
	|	НЕОПРЕДЕЛЕНО                                                   КАК НомерСчетаФактурыКомиссионера,
	|	ТаблицаДокумента.Ссылка.КлиентКонтрагент                       КАК Покупатель,
	|	ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.Ссылка, ЗНАЧЕНИЕ(Справочник.ВариантыКомплектацииНоменклатуры.ПустаяСсылка)) КАК ВариантКомплектацииНоменклатуры,
	|	ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.ВариантПредставленияНабораВПечатныхФормах, ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ПустаяСсылка)) КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.ВариантРасчетаЦеныНабора, ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ПустаяСсылка)) КАК ВариантРасчетаЦеныНабора,
	|	ЕСТЬNULL(ТаблицаДокумента.АналитикаУчетаНаборов.НоменклатураНабора, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))   КАК НоменклатураНабора,
	|	ЕСТЬNULL(ТаблицаДокумента.АналитикаУчетаНаборов.ХарактеристикаНабора, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК ХарактеристикаНабора,
	|	МИНИМУМ(СтрокиТоваров.НомерСтроки)                             КАК НомерСтроки,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура       КАК Номенклатура,
	|	СтрокиТоваров.НоменклатураПартнера                   	   	   КАК НоменклатураПартнера,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Характеристика     КАК Характеристика,
	|	ВЫБОР КОГДА &ВыводитьСерии ТОГДА
	|		ТаблицаДокумента.АналитикаУчетаНоменклатуры.Серия
	|	ИНАЧЕ 
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Серия,
	|	
	|	ВЫБОР КОГДА &ВключаяНомераГТД ТОГДА
	|		ТаблицаДокумента.НомерГТД
	|	ИНАЧЕ
	|		&ПустаяГТД
	|	КОНЕЦ КАК НомерГТД,
	|	ТаблицаДокумента.КодТНВЭД                                      КАК КодТНВЭД,
	|
	|	СУММА(ТаблицаДокумента.Количество)                             КАК Количество,
	|	СУММА(ТаблицаДокумента.КоличествоУпаковок)                     КАК КоличествоУпаковок,
	|	СУММА(ВЫБОР
	|			КОГДА &ВключаяНомераГТД
	|				ТОГДА ТаблицаДокумента.КоличествоПоРНПТ
	|			ИНАЧЕ 0
	|	КОНЕЦ)                                                         КАК КоличествоПоРНПТ,
	|	
	|	ТаблицаДокумента.Цена                                          КАК Цена,
	|	
	|	СУММА(ЕСТЬNULL(
	|		СуммыДокументовВВалютахУчета.БазаНДСРегл,
	|		ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС
	|	)) КАК СуммаБезНДС,
	|	
	|	ТаблицаДокумента.СтавкаНДС                                     КАК СтавкаНДС,
	|	
	|	СУММА(ЕСТЬNULL(
	|		СуммыДокументовВВалютахУчета.СуммаНДСРегл,
	|		ТаблицаДокумента.СуммаНДС
	|	)) КАК СуммаНДС,
	|	
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ
	|			ИСТИНА
	|	КОНЕЦ                                                         КАК ЭтоТовар,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА ЛОЖЬ
	|		КОГДА
	|			ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			И ТаблицаДокумента.Ссылка.ВернутьМногооборотнуюТару
	|		ТОГДА
	|			ЛОЖЬ
	|		ИНАЧЕ
	|			ИСТИНА
	|	КОНЕЦ                                                          КАК ЭтоНеВозвратнаяТара,
	|	ТаблицаДокумента.Ссылка.ВернутьМногооборотнуюТару              КАК ВернутьМногооборотнуюТару,
	|	ТаблицаДокумента.Упаковка                                      КАК Упаковка,
	|	&ТекстЗапросаВесНоменклатуры КАК МассаНеттоНоменклатуры
	|
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ВидыЗапасов КАК ТаблицаДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО
	|		ТаблицаДокумента.Ссылка = ДанныеДокументов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|	ПО
	|		ТаблицаДокумента.Ссылка = СуммыДокументовВВалютахУчета.Регистратор
	|		И ТаблицаДокумента.ИдентификаторСтроки = СуммыДокументовВВалютахУчета.ИдентификаторСтроки
	|		И СуммыДокументовВВалютахУчета.Активность
	|		И &ПересчитыватьВВалютуРегл
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		СтрокиТоваров КАК СтрокиТоваров
	|	ПО
	|		ТаблицаДокумента.Ссылка = СтрокиТоваров.Ссылка
	|		И ТаблицаДокумента.АналитикаУчетаНоменклатуры = СтрокиТоваров.АналитикаУчетаНоменклатуры
	|		И ТаблицаДокумента.АналитикаУчетаНаборов = СтрокиТоваров.АналитикаУчетаНаборов
	|		И ТаблицаДокумента.Упаковка = СтрокиТоваров.Упаковка
	|		И ТаблицаДокумента.Цена = СтрокиТоваров.Цена
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
	|		ПО ВариантыКомплектацииНоменклатуры.Владелец = ТаблицаДокумента.АналитикаУчетаНаборов.НоменклатураНабора
	|		И ВариантыКомплектацииНоменклатуры.Характеристика = ТаблицаДокумента.АналитикаУчетаНаборов.ХарактеристикаНабора
	|		И ВариантыКомплектацииНоменклатуры.Основной
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Ссылка,
	|	ВариантыКомплектацииНоменклатуры.Ссылка,
	|	ВариантыКомплектацииНоменклатуры.ВариантПредставленияНабораВПечатныхФормах,
	|	ВариантыКомплектацииНоменклатуры.ВариантРасчетаЦеныНабора,
	|	ТаблицаДокумента.АналитикаУчетаНаборов.НоменклатураНабора,
	|	ТаблицаДокумента.АналитикаУчетаНаборов.ХарактеристикаНабора,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура,
	|	СтрокиТоваров.НоменклатураПартнера,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Характеристика,
	|	ВЫБОР КОГДА &ВыводитьСерии ТОГДА
	|		ТаблицаДокумента.АналитикаУчетаНоменклатуры.Серия
	|	ИНАЧЕ 
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.СтавкаНДС,
	|	ТаблицаДокумента.Упаковка,
	|	ТаблицаДокумента.Цена,
	|	ТаблицаДокумента.КодТНВЭД,
	|
	|	ВЫБОР КОГДА &ВключаяНомераГТД ТОГДА
	|		ТаблицаДокумента.НомерГТД
	|	ИНАЧЕ
	|		&ПустаяГТД
	|	КОНЕЦ,
	|	
	|	ВЫБОР
	|		КОГДА
	|			ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			И ТаблицаДокумента.Ссылка.ВернутьМногооборотнуюТару
	|		ТОГДА
	|			ЛОЖЬ
	|		ИНАЧЕ
	|			ИСТИНА
	|	КОНЕЦ,
	|	ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.Ссылка, ЗНАЧЕНИЕ(Справочник.ВариантыКомплектацииНоменклатуры.ПустаяСсылка)),
	|	ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.ВариантПредставленияНабораВПечатныхФормах, ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ПустаяСсылка)),
	|	ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.ВариантРасчетаЦеныНабора, ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ПустаяСсылка)),
	|	ЕСТЬNULL(ТаблицаДокумента.АналитикаУчетаНаборов.НоменклатураНабора, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)),
	|	ЕСТЬNULL(ТаблицаДокумента.АналитикаУчетаНаборов.ХарактеристикаНабора, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СтрокиТоваров
	|;
	|
	|/////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВидыЗапасовРабот
	|";
	
	Если ПараметрыЗаполнения.ОбработатьНастройкиПечатиНаборов Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПОМЕСТИТЬ РеализацияТоваровУслугТаблицаТоваров", "ПОМЕСТИТЬ РеализацияТоваровУслугТаблицаТоваровДляПреобразования");
		Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() +
		"ВЫБРАТЬ
		|	ТаблицаТоваров.Ссылка КАК Ссылка,
		|	ТаблицаТоваров.НоменклатураНабора КАК НоменклатураНабора,
		|	ТаблицаТоваров.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|	МАКСИМУМ(ТаблицаТоваров.ЭтоТовар) КАК ЭтоТовар,
		|	МИНИМУМ(ТаблицаТоваров.НомерСтроки) КАК НомерСтроки,
		|	СУММА(ВЫБОР
		|			КОГДА &ЗаполненаЕдиницаИзмеренияВеса
		|				ТОГДА ВЫБОР
		|						КОГДА ТаблицаТоваров.Упаковка.Вес ЕСТЬ NULL
		|							ТОГДА ТаблицаТоваров.Количество
		|						ИНАЧЕ ВЫБОР
		|								КОГДА ТаблицаТоваров.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Вес)
		|									ТОГДА 0
		|								ИНАЧЕ ТаблицаТоваров.КоличествоУпаковок
		|							КОНЕЦ
		|					КОНЕЦ * &ТекстЗапросаВесУпаковки
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК МассаБрутто,
		|	СУММА(ТаблицаТоваров.Количество * ТаблицаТоваров.МассаНеттоНоменклатуры) КАК МассаНетто,
		|	СУММА(ТаблицаТоваров.МассаНеттоНоменклатуры) КАК МассаНеттоНоменклатуры,
		|	СУММА(ТаблицаТоваров.СуммаБезНДС) КАК СуммаБезНДС,
		|	СУММА(ТаблицаТоваров.СуммаНДС) КАК СуммаНДС
		|ПОМЕСТИТЬ ВременнаяТаблицаНаборыПодготовка
		|ИЗ
		|	РеализацияТоваровУслугТаблицаТоваровДляПреобразования КАК ТаблицаТоваров
		|ГДЕ
		|	ТаблицаТоваров.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаТоваров.Ссылка,
		|	ТаблицаТоваров.НоменклатураНабора,
		|	ТаблицаТоваров.ХарактеристикаНабора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Результат.Ссылка КАК Ссылка,
		|	Результат.ВариантКомплектацииНоменклатуры КАК ВариантКомплектацииНоменклатуры,
		|	Результат.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
		|	Результат.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
		|	Результат.НоменклатураНабора КАК НоменклатураНабора,
		|	Результат.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА Результат.ОсновнаяКомплектующая
		|				ТОГДА Результат.СтавкаНДС
		|			ИНАЧЕ NULL
		|		КОНЕЦ) КАК СтавкаНДС,
		|	МАКСИМУМ(Результат.НомерГТД) КАК НомерГТД,
		|	МАКСИМУМ(Результат.КодТНВЭД) КАК КодТНВЭД,
		|	(ВЫРАЗИТЬ(МИНИМУМ(ВЫБОР
		|				КОГДА Результат.КоличествоПоУмолчанию <> 0
		|						И Результат.ОсновнаяКомплектующая
		|					ТОГДА Результат.Количество / Результат.КоличествоПоУмолчанию
		|				ИНАЧЕ NULL
		|			КОНЕЦ) + 0.5 КАК ЧИСЛО(10, 0))) - 1 КАК Количество,
		|	МАКСИМУМ(Результат.КоличествоПоРНПТ) КАК КоличествоПоРНПТ,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА Результат.КоличествоПоУмолчанию <> 0
		|				ТОГДА Результат.Количество / Результат.КоличествоПоУмолчанию
		|			ИНАЧЕ NULL
		|		КОНЕЦ) КАК КоэффициентМаксимум,
		|	(ВЫРАЗИТЬ(МИНИМУМ(ВЫБОР
		|				КОГДА Результат.КоличествоПоУмолчанию <> 0
		|					ТОГДА Результат.Количество / Результат.КоличествоПоУмолчанию
		|				ИНАЧЕ NULL
		|			КОНЕЦ) + 0.5 КАК ЧИСЛО(10, 0))) - 1 КАК КоэффициентМинимум
		|ПОМЕСТИТЬ ВременнаяТаблицаНаборыДополнительно
		|ИЗ
		|	(ВЫБРАТЬ
		|		Таблица.Ссылка КАК Ссылка,
		|		Таблица.ВариантКомплектацииНоменклатуры КАК ВариантКомплектацииНоменклатуры,
		|		Таблица.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
		|		Таблица.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
		|		Таблица.НоменклатураНабора КАК НоменклатураНабора,
		|		Таблица.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		МАКСИМУМ(Таблица.СтавкаНДС) КАК СтавкаНДС,
		|		МАКСИМУМ(Таблица.НомерГТД) КАК НомерГТД,
		|		МАКСИМУМ(Таблица.КодТНВЭД) КАК КодТНВЭД,
		|		МАКСИМУМ(Таблица.ОсновнаяКомплектующая) КАК ОсновнаяКомплектующая,
		|		СУММА(Таблица.КоличествоПоУмолчанию) КАК КоличествоПоУмолчанию,
		|		СУММА(Таблица.Количество) КАК Количество,
		|		СУММА(Таблица.КоличествоПоРНПТ) КАК КоличествоПоРНПТ
		|	ИЗ
		|		(ВЫБРАТЬ
		|			Товары.Ссылка КАК Ссылка,
		|			Товары.ВариантКомплектацииНоменклатуры КАК ВариантКомплектацииНоменклатуры,
		|			Товары.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
		|			Товары.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
		|			Товары.НоменклатураНабора КАК НоменклатураНабора,
		|			Товары.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|			Товары.Номенклатура КАК Номенклатура,
		|			Товары.Характеристика КАК Характеристика,
		|			ВЫБОР
		|				КОГДА Товары.ВариантКомплектацииНоменклатуры.НоменклатураОсновногоКомпонента = Товары.Номенклатура
		|						И Товары.ВариантКомплектацииНоменклатуры.ХарактеристикаОсновногоКомпонента = Товары.Характеристика
		|					ТОГДА ИСТИНА
		|				ИНАЧЕ ЛОЖЬ
		|			КОНЕЦ КАК ОсновнаяКомплектующая,
		|			Товары.СтавкаНДС КАК СтавкаНДС,
		|			ВЫБОР
		|				КОГДА Товары.ВариантКомплектацииНоменклатуры.НоменклатураОсновногоКомпонента = Товары.Номенклатура
		|						И Товары.ВариантКомплектацииНоменклатуры.ХарактеристикаОсновногоКомпонента = Товары.Характеристика
		|					ТОГДА Товары.НомерГТД
		|				ИНАЧЕ NULL
		|			КОНЕЦ КАК НомерГТД,
		|			ВЫБОР
		|				КОГДА Товары.ВариантКомплектацииНоменклатуры.НоменклатураОсновногоКомпонента = Товары.Номенклатура
		|						И Товары.ВариантКомплектацииНоменклатуры.ХарактеристикаОсновногоКомпонента = Товары.Характеристика
		|					ТОГДА Товары.КодТНВЭД
		|				ИНАЧЕ NULL
		|			КОНЕЦ КАК КодТНВЭД,
		|			0 КАК КоличествоПоУмолчанию,
		|			Товары.Количество КАК Количество,
		|			ВЫБОР
		|				КОГДА Товары.ВариантКомплектацииНоменклатуры.НоменклатураОсновногоКомпонента = Товары.Номенклатура
		|						И Товары.ВариантКомплектацииНоменклатуры.ХарактеристикаОсновногоКомпонента = Товары.Характеристика
		|					ТОГДА Товары.КоличествоПоРНПТ
		|				ИНАЧЕ 0
		|			КОНЕЦ КАК КоличествоПоРНПТ
		|		ИЗ
		|			РеализацияТоваровУслугТаблицаТоваровДляПреобразования КАК Товары
		|		ГДЕ
		|			Товары.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ТаблицаДанныхДокументов.Ссылка,
		|			ВариантыКомплектацииНоменклатурыТовары.Ссылка,
		|			ВариантыКомплектацииНоменклатурыТовары.Ссылка.ВариантПредставленияНабораВПечатныхФормах,
		|			ВариантыКомплектацииНоменклатурыТовары.Ссылка.ВариантРасчетаЦеныНабора,
		|			ВариантыКомплектацииНоменклатурыТовары.Ссылка.Владелец,
		|			ВариантыКомплектацииНоменклатурыТовары.Ссылка.Характеристика,
		|			ВариантыКомплектацииНоменклатурыТовары.Номенклатура,
		|			ВариантыКомплектацииНоменклатурыТовары.Характеристика,
		|			ЛОЖЬ,
		|			NULL,
		|			NULL,
		|			NULL,
		|			ВариантыКомплектацииНоменклатурыТовары.Количество,
		|			0,
		|			0
		|		ИЗ
		|			Справочник.ВариантыКомплектацииНоменклатуры.Товары КАК ВариантыКомплектацииНоменклатурыТовары
		|				ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
		|				ПО (ИСТИНА)
		|		ГДЕ
		|			ВариантыКомплектацииНоменклатурыТовары.Ссылка В
		|					(ВЫБРАТЬ
		|						РеализацияТоваровУслугТаблицаТоваровДляПреобразования.ВариантКомплектацииНоменклатуры
		|					ИЗ
		|						РеализацияТоваровУслугТаблицаТоваровДляПреобразования КАК РеализацияТоваровУслугТаблицаТоваровДляПреобразования)) КАК Таблица
		|	
		|	СГРУППИРОВАТЬ ПО
		|		Таблица.Ссылка,
		|		Таблица.ВариантКомплектацииНоменклатуры,
		|		Таблица.ВариантРасчетаЦеныНабора,
		|		Таблица.ВариантПредставленияНабораВПечатныхФормах,
		|		Таблица.НоменклатураНабора,
		|		Таблица.ХарактеристикаНабора,
		|		Таблица.Номенклатура,
		|		Таблица.Характеристика) КАК Результат
		|
		|СГРУППИРОВАТЬ ПО
		|	Результат.Ссылка,
		|	Результат.ВариантКомплектацииНоменклатуры,
		|	Результат.ВариантРасчетаЦеныНабора,
		|	Результат.ВариантПредставленияНабораВПечатныхФормах,
		|	Результат.НоменклатураНабора,
		|	Результат.ХарактеристикаНабора
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Результат.Ссылка,
		|	Результат.НоменклатураНабора,
		|	Результат.ХарактеристикаНабора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаНаборыДополнительно.ВариантКомплектацииНоменклатуры КАК ВариантКомплектацииНоменклатуры,
		|	ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(Таблица.Ссылка КАК Документ.РеализацияТоваровУслуг).ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию)
		|			ТОГДА ВЫБОР
		|					КОГДА ВременнаяТаблицаНаборыДополнительно.ВариантПредставленияНабораВПечатныхФормах = ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ТолькоНабор)
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.НаборИКомплектующие)
		|					ИНАЧЕ ВременнаяТаблицаНаборыДополнительно.ВариантПредставленияНабораВПечатныхФормах
		|				КОНЕЦ
		|		ИНАЧЕ ВременнаяТаблицаНаборыДополнительно.ВариантПредставленияНабораВПечатныхФормах
		|	КОНЕЦ КАК ВариантПредставленияНабораВПечатныхФормах,
		|	ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(Таблица.Ссылка КАК Документ.РеализацияТоваровУслуг).ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию)
		|			ТОГДА ВЫБОР
		|					КОГДА ВЫБОР
		|								КОГДА ВременнаяТаблицаНаборыДополнительно.ВариантПредставленияНабораВПечатныхФормах = ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ТолькоНабор)
		|									ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.НаборИКомплектующие)
		|								ИНАЧЕ ВременнаяТаблицаНаборыДополнительно.ВариантПредставленияНабораВПечатныхФормах
		|							КОНЕЦ = ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.НаборИКомплектующие)
		|							И ВременнаяТаблицаНаборыДополнительно.ВариантРасчетаЦеныНабора В (ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ЦенаЗадаетсяЗаНаборРаспределяетсяПоЦенам), ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ЦенаЗадаетсяЗаНаборРаспределяетсяПоДолям))
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.РассчитываетсяИзЦенКомплектующих)
		|					ИНАЧЕ ВременнаяТаблицаНаборыДополнительно.ВариантРасчетаЦеныНабора
		|				КОНЕЦ
		|		ИНАЧЕ ВременнаяТаблицаНаборыДополнительно.ВариантРасчетаЦеныНабора
		|	КОНЕЦ КАК ВариантРасчетаЦеныНабора,
		|	Таблица.Ссылка КАК Ссылка,
		|	Таблица.НоменклатураНабора КАК НоменклатураНабора,
		|	Таблица.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|	Таблица.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВременнаяТаблицаНаборыДополнительно.НомерГТД, ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)) КАК НомерГТД,
		|	ЕСТЬNULL(ВременнаяТаблицаНаборыДополнительно.КодТНВЭД, ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)) КАК КодТНВЭД,
		|	ЕСТЬNULL(ВременнаяТаблицаНаборыДополнительно.Количество, 1) КАК КоличествоУпаковок,
		|	ЕСТЬNULL(ВременнаяТаблицаНаборыДополнительно.Количество, 1) КАК Количество,
		|	ЕСТЬNULL(ВременнаяТаблицаНаборыДополнительно.КоличествоПоРНПТ, 1) КАК КоличествоПоРНПТ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаНаборыДополнительно.КоэффициентМинимум = ВременнаяТаблицаНаборыДополнительно.КоэффициентМаксимум
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПолныйНабор,
		|	Таблица.МассаБрутто КАК МассаБрутто,
		|	Таблица.МассаНетто КАК МассаНетто,
		|	Таблица.МассаНеттоНоменклатуры КАК МассаНеттоНоменклатуры,
		|	Таблица.СуммаБезНДС КАК СуммаБезНДС,
		|	Таблица.СуммаНДС КАК СуммаНДС,
		|	Таблица.ЭтоТовар КАК ЭтоТовар,
		|	ВременнаяТаблицаНаборыДополнительно.СтавкаНДС КАК СтавкаНДС
		|ПОМЕСТИТЬ ВременнаяТаблицаНаборы
		|ИЗ
		|	ВременнаяТаблицаНаборыПодготовка КАК Таблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНаборыДополнительно КАК ВременнаяТаблицаНаборыДополнительно
		|		ПО Таблица.НоменклатураНабора = ВременнаяТаблицаНаборыДополнительно.НоменклатураНабора
		|			И Таблица.ХарактеристикаНабора = ВременнаяТаблицаНаборыДополнительно.ХарактеристикаНабора
		|			И Таблица.Ссылка = ВременнаяТаблицаНаборыДополнительно.Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка,
		|	НоменклатураНабора,
		|	ХарактеристикаНабора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаТоваров.Ссылка КАК Ссылка,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
		|			ТОГДА ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ПустаяСсылка)
		|	КОНЕЦ КАК ВариантПредставленияНабораВПечатныхФормах,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
		|			ТОГДА ВременнаяТаблицаНаборы.ВариантРасчетаЦеныНабора
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ПустаяСсылка)
		|	КОНЕЦ КАК ВариантРасчетаЦеныНабора,
		|	ТаблицаТоваров.НоменклатураНабора КАК НоменклатураНабора,
		|	ТаблицаТоваров.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоКомплектующие,
		|	ЛОЖЬ КАК ЭтоНабор,
		|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
		|			ТОГДА ВременнаяТаблицаНаборы.НомерСтроки
		|		ИНАЧЕ ТаблицаТоваров.НомерСтроки
		|	КОНЕЦ КАК НомерСтрокиНаборы,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
		|			ТОГДА ВременнаяТаблицаНаборы.ПолныйНабор
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПолныйНабор,
		|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
		|	ТаблицаТоваров.НоменклатураПартнера КАК НоменклатураПартнера,
		|	ТаблицаТоваров.Количество КАК Количество,
		|	ТаблицаТоваров.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ТаблицаТоваров.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
		|	ТаблицаТоваров.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаТоваров.НомерГТД КАК НомерГТД,
		|	ТаблицаТоваров.КодТНВЭД КАК КодТНВЭД,
		|	ТаблицаТоваров.СуммаБезНДС КАК СуммаБезНДС,
		|	ТаблицаТоваров.СуммаНДС КАК СуммаНДС,
		|	ТаблицаТоваров.Характеристика КАК Характеристика,
		|	ТаблицаТоваров.Серия КАК Серия,
		|	ТаблицаТоваров.Упаковка КАК Упаковка,
		|	ВЫБОР
		|		КОГДА &ЗаполненаЕдиницаИзмеренияВеса
		|			ТОГДА ВЫБОР
		|					КОГДА ТаблицаТоваров.Упаковка.Вес ЕСТЬ NULL
		|						ТОГДА ТаблицаТоваров.Количество
		|					ИНАЧЕ ВЫБОР
		|							КОГДА ТаблицаТоваров.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Вес)
		|								ТОГДА 0
		|							ИНАЧЕ ТаблицаТоваров.КоличествоУпаковок
		|						КОНЕЦ
		|				КОНЕЦ * &ТекстЗапросаВесУпаковки
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК МассаБрутто,
		|	ТаблицаТоваров.Количество * ТаблицаТоваров.МассаНеттоНоменклатуры КАК МассаНетто,
		|	ТаблицаТоваров.МассаНеттоНоменклатуры КАК МассаНеттоНоменклатуры,
		|	ТаблицаТоваров.ЭтоТовар КАК ЭтоТовар
		|ПОМЕСТИТЬ РеализацияТоваровУслугТаблицаТоваров
		|ИЗ
		|	РеализацияТоваровУслугТаблицаТоваровДляПреобразования КАК ТаблицаТоваров
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНаборы КАК ВременнаяТаблицаНаборы
		|		ПО (ВременнаяТаблицаНаборы.НоменклатураНабора = ТаблицаТоваров.НоменклатураНабора)
		|			И (ВременнаяТаблицаНаборы.ХарактеристикаНабора = ТаблицаТоваров.ХарактеристикаНабора)
		|			И (ВременнаяТаблицаНаборы.Ссылка = ТаблицаТоваров.Ссылка)
		|ГДЕ
		|	(ТаблицаТоваров.НоменклатураНабора = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|			ИЛИ ТаблицаТоваров.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|				И ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах В (ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ТолькоКомплектующие), ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.НаборИКомплектующие)))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаНаборы.Ссылка,
		|	ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах,
		|	ВременнаяТаблицаНаборы.ВариантРасчетаЦеныНабора,
		|	ВременнаяТаблицаНаборы.НоменклатураНабора,
		|	ВременнаяТаблицаНаборы.ХарактеристикаНабора,
		|	ЛОЖЬ,
		|	ИСТИНА,
		|	ВременнаяТаблицаНаборы.НомерСтроки,
		|	ВременнаяТаблицаНаборы.НомерСтроки,
		|	ВременнаяТаблицаНаборы.ПолныйНабор,
		|	ВременнаяТаблицаНаборы.НоменклатураНабора,
		|	ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка),
		|	ВременнаяТаблицаНаборы.Количество,
		|	ВременнаяТаблицаНаборы.КоличествоУпаковок,
		|	ВременнаяТаблицаНаборы.КоличествоПоРНПТ,
		|	ВременнаяТаблицаНаборы.СтавкаНДС,
		|	ВременнаяТаблицаНаборы.НомерГТД,
		|	ВременнаяТаблицаНаборы.КодТНВЭД,
		|	ВременнаяТаблицаНаборы.СуммаБезНДС,
		|	ВременнаяТаблицаНаборы.СуммаНДС,
		|	ВременнаяТаблицаНаборы.ХарактеристикаНабора,
		|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка),
		|	ВременнаяТаблицаНаборы.МассаБрутто,
		|	ВременнаяТаблицаНаборы.МассаНетто,
		|	ВременнаяТаблицаНаборы.МассаНеттоНоменклатуры КАК МассаНеттоНоменклатуры,
		|	ВременнаяТаблицаНаборы.ЭтоТовар
		|ИЗ
		|	ВременнаяТаблицаНаборы КАК ВременнаяТаблицаНаборы
		|ГДЕ
		|	ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах В (ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ТолькоНабор), ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.НаборИКомплектующие))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВременнаяТаблицаНаборыПодготовка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВременнаяТаблицаНаборыДополнительно
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВременнаяТаблицаНаборы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ РеализацияТоваровУслугТаблицаТоваровДляПреобразования";		
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаВесУпаковки",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
				"ТаблицаТоваров.Упаковка",
				"ТаблицаТоваров.Номенклатура"));
			
		Запрос.УстановитьПараметр("ЗаполненаЕдиницаИзмеренияВеса",   ЗначениеЗаполнено(Константы.ЕдиницаИзмеренияВеса.Получить()));
		
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст, 
		"&ТекстЗапросаВесНоменклатуры",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения",
			"ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура"));
	
	Запрос.Выполнить();
	
КонецПроцедуры

#Область СчетНаОплату_1_01

// Возвращает данные для формирования электронного Счета на оплату.
//
// Параметры:
//	МассивОбъектов - Массив из ДокументСсылка.РеализацияТоваровУслуг - ссылки на документы, по которым необходимо
//																		получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемая для формирования электронного Счета на оплату, содержащая следующие свойства:
//		* РезультатПоШапке - РезультатЗапроса - общие данные документа.
//		* РезультатПоЭтапамОплаты - РезультатЗапроса - данные графика оплаты по документу.
//		* РезультатПоТабличнойЧасти - РезультатЗапроса - данные табличной части документа.
//
Функция ДанныеДокументовДляСчетаНаОплату_1_01(МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	ОтображатьСкидки = Константы.ОтображениеСкидокВПечатныхФормахДокументовПродажи.Получить()
						<> Перечисления.ВариантыВыводаСкидокВПечатныхФормах.НеВыводитьСкидки;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДанныхДокументовДляСчетаНаОплату();
	
	Запрос.УстановитьПараметр("МассивОбъектов",		МассивОбъектов);
	Запрос.УстановитьПараметр("КолонкаКодов",		КолонкаКодов);
	Запрос.УстановитьПараметр("ОтображатьСкидки",	ОтображатьСкидки);
	
	ПакетРезультатаЗапроса		= Запрос.ВыполнитьПакет();
	МаксимальныйИндексПакета	= ПакетРезультатаЗапроса.ВГраница();
	РезультатПоШапке			= ПакетРезультатаЗапроса[МаксимальныйИндексПакета - 2];
	РезультатПоЭтапамОплаты		= ПакетРезультатаЗапроса[МаксимальныйИндексПакета - 1];
	РезультатПоТабличнойЧасти	= ПакетРезультатаЗапроса[МаксимальныйИндексПакета];
	
	ДанныеДокументов = Новый Структура;
	ДанныеДокументов.Вставить("РезультатПоШапке",			РезультатПоШапке);
	ДанныеДокументов.Вставить("РезультатПоЭтапамОплаты",	РезультатПоЭтапамОплаты);
	ДанныеДокументов.Вставить("РезультатПоТабличнойЧасти",	РезультатПоТабличнойЧасти);
	
	Возврат ДанныеДокументов;
	
КонецФункции

// Возвращает текст запроса для получения данных о товарах накладной-основания, используемого при формировании ЭД
// Счета на оплату.
//
// Параметры:
//	ЭтоТекстВТ - Булево - признак необходимости формирования текста запроса временной таблицы. Если значение Истина,
//							тогда формируется текст временной таблицы.
//
// Возвращаемое значение:
//	Строка - текст запроса для получения данных.
//
Функция ТекстЗапросаВТТоваровОснованияСчетаНаОплату_1_01(ЭтоТекстВТ = Истина) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТоварыДокумента.Ссылка					КАК Ссылка,
	|	ДанныеДокумента.ХозяйственнаяОперация	КАК ХозяйственнаяОперация,
	|	ТоварыДокумента.НомерСтроки				КАК НомерСтроки,
	|	ВариантыКомплектации.Ссылка										КАК ВариантКомплектацииНоменклатуры,
	|	ВариантыКомплектации.ВариантПредставленияНабораВПечатныхФормах	КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ВариантыКомплектации.ВариантРасчетаЦеныНабора					КАК ВариантРасчетаЦеныНабора,
	|	ТоварыДокумента.НоменклатураНабора		КАК НоменклатураНабора,
	|	ТоварыДокумента.ХарактеристикаНабора	КАК ХарактеристикаНабора,
	|	ТоварыДокумента.Номенклатура			КАК Номенклатура,
	|	ТоварыДокумента.Характеристика			КАК Характеристика,
	|	ТоварыДокумента.Упаковка				КАК Упаковка,
	|	ТоварыДокумента.Количество				КАК Количество,
	|	ТоварыДокумента.КоличествоУпаковок		КАК КоличествоУпаковок,
	|	ВЫБОР
	|		КОГДА &ОтображатьСкидки
	|			ТОГДА ВЫБОР
	|					КОГДА ДанныеДокумента.ЦенаВключаетНДС
	|							ИЛИ ТоварыДокумента.СуммаРучнойСкидки <> 0
	|							ИЛИ ТоварыДокумента.СуммаАвтоматическойСкидки <> 0
	|						ТОГДА (ТоварыДокумента.Сумма - ТоварыДокумента.СуммаНДС) / ТоварыДокумента.КоличествоУпаковок
	|					ИНАЧЕ ТоварыДокумента.Цена
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ДанныеДокумента.ЦенаВключаетНДС
	|					ТОГДА (ТоварыДокумента.Сумма - ТоварыДокумента.СуммаНДС) / ТоварыДокумента.КоличествоУпаковок
	|				ИНАЧЕ ТоварыДокумента.Сумма / ТоварыДокумента.КоличествоУпаковок
	|			КОНЕЦ
	|	КОНЕЦ									КАК Цена,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ЦенаВключаетНДС
	|			ТОГДА ТоварыДокумента.Сумма - ТоварыДокумента.СуммаНДС
	|		ИНАЧЕ ТоварыДокумента.Сумма
	|	КОНЕЦ									КАК СтоимостьБезНДС,
	|	ТоварыДокумента.СтавкаНДС				КАК СтавкаНДС,
	|	ТоварыДокумента.СуммаСНДС				КАК СтоимостьСНДС,
	|	ВЫБОР
	|		КОГДА &ОтображатьСкидки
	|			ТОГДА ТоварыДокумента.СуммаРучнойСкидки + ТоварыДокумента.СуммаАвтоматическойСкидки
	|		ИНАЧЕ 0
	|	КОНЕЦ									КАК СуммаСкидки,
	|	ВЫБОР
	|		КОГДА &ОтображатьСкидки
	|			ТОГДА ВЫБОР
	|					КОГДА ДанныеДокумента.ЦенаВключаетНДС
	|						ТОГДА (ТоварыДокумента.Сумма - ТоварыДокумента.СуммаНДС
	|							+ ТоварыДокумента.СуммаРучнойСкидки + ТоварыДокумента.СуммаАвтоматическойСкидки)
	|							/ ТоварыДокумента.КоличествоУпаковок
	|					ИНАЧЕ ТоварыДокумента.Цена
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ДанныеДокумента.ЦенаВключаетНДС
	|					ТОГДА (ТоварыДокумента.Сумма - ТоварыДокумента.СуммаНДС) / ТоварыДокумента.КоличествоУпаковок
	|				ИНАЧЕ ТоварыДокумента.Сумма / ТоварыДокумента.КоличествоУпаковок
	|			КОНЕЦ
	|	КОНЕЦ									КАК ЦенаБезСкидки,
	|	ВЫБОР
	|		КОГДА &ОтображатьСкидки
	|			ТОГДА ВЫБОР
	|					КОГДА ДанныеДокумента.ЦенаВключаетНДС
	|						ТОГДА ТоварыДокумента.Сумма - ТоварыДокумента.СуммаНДС
	|							+ ТоварыДокумента.СуммаРучнойСкидки + ТоварыДокумента.СуммаАвтоматическойСкидки
	|					ИНАЧЕ ТоварыДокумента.Сумма + ТоварыДокумента.СуммаРучнойСкидки
	|						+ ТоварыДокумента.СуммаАвтоматическойСкидки
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ДанныеДокумента.ЦенаВключаетНДС
	|					ТОГДА ТоварыДокумента.Сумма - ТоварыДокумента.СуммаНДС
	|				ИНАЧЕ ТоварыДокумента.Сумма
	|			КОНЕЦ
	|	КОНЕЦ									КАК СтоимостьБезНДСБезСкидки,
	|	ВЫБОР
	|		КОГДА &ОтображатьСкидки
	|			ТОГДА ТоварыДокумента.СуммаСНДС + ТоварыДокумента.СуммаАвтоматическойСкидки
	|				+ ТоварыДокумента.СуммаРучнойСкидки
	|		ИНАЧЕ ТоварыДокумента.СуммаСНДС
	|	КОНЕЦ									КАК СтоимостьСНДСБезСкидки,
	|	ТоварыДокумента.СуммаНДС				КАК СуммаНДС,
	|	""""									КАК Содержание,
	|	ВЫБОР
	|		КОГДА ТоварыДокумента.Номенклатура.ТипНоменклатуры В(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|															ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ									КАК ЭтоТовар,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ВернутьМногооборотнуюТару
	|				И ТоварыДокумента.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ									КАК ЭтоВозвратнаяТара
	|ПОМЕСТИТЬ ТоварыОснования
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТоварыДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОснований КАК ТаблицаОснований
	|		ПО ТоварыДокумента.Ссылка = ТаблицаОснований.ДокументОснование
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК ДанныеДокумента
	|		ПО ТоварыДокумента.Ссылка = ДанныеДокумента.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектации
	|		ПО ТоварыДокумента.НоменклатураНабора = ВариантыКомплектации.Владелец
	|			И ТоварыДокумента.ХарактеристикаНабора = ВариантыКомплектации.Характеристика
	|			И ВариантыКомплектации.Основной
	|
	|ГДЕ
	|	ТоварыДокумента.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ИЛИ ТоварыДокумента.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			И (НЕ ДанныеДокумента.ВернутьМногооборотнуюТару
	|				ИЛИ ДанныеДокумента.ТребуетсяЗалогЗаТару)";
	
	Если Не ЭтоТекстВТ Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТоварыОснования", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

//-- Локализация
#КонецОбласти


//++ Локализация
#Область Фискализация

// Возвращает параметры операции фискализации чека для печати чека по документу
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Форма документ, из которого печатается чек - содержит:
// 	* Объект - ДокументОбъект - Документ-объект, основной параметр формы.
// Возвращаемое значение:
// 	см. ФормированиеФискальныхЧековСерверПереопределяемый.СтруктураОсновныхПараметровОперации
Функция ОсновныеПараметрыОперации(Форма) Экспорт
	
	ОсновныеПараметрыОперации = ФормированиеФискальныхЧековСерверПереопределяемый.СтруктураОсновныхПараметровОперации();
	
	ОсновныеПараметрыОперации.ДокументСсылка       = Форма.Объект.Ссылка;
	ОсновныеПараметрыОперации.Организация          = Форма.Объект.Организация;
	ОсновныеПараметрыОперации.Контрагент           = Форма.Объект.Контрагент;
	ОсновныеПараметрыОперации.Партнер              = Форма.Объект.Партнер;
	ОсновныеПараметрыОперации.Валюта               = Форма.Объект.Валюта;
	ОсновныеПараметрыОперации.ВалютаВзаиморасчетов = Форма.Объект.ВалютаВзаиморасчетов;
	ОсновныеПараметрыОперации.СуммаДокумента       = Форма.Объект.СуммаДокумента;
	
	Возврат ОсновныеПараметрыОперации;
	
КонецФункции

// Определяет, разрешено ли пробитие фискального чека по документу
// 
// Параметры:
//	Форма - ФормаКлиентскогоПриложения, Структура - для контекстных вызовов ожидается форма, для внеконтекстных - структура:
//			*Объект - Структура - (для контекстных вызовов - основной объект формы):
//				**Контрагент - СправочникСсылка.Контрагенты
//				**ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации
// Возвращаемое значение:
// 	Булево - Истина, если разрешено пробитие чека
Функция РазрешеноПробитиеФискальныхЧековПоДокументу(Форма) Экспорт
	
	РазрешеноПробитиеФискальныхЧековПоДокументу = Ложь;
	
	Если ЗначениеЗаполнено(Форма.Объект.Контрагент)
		И ЗначениеЗаполнено(Форма.Объект.ХозяйственнаяОперация) Тогда
		
		ЮрФизЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Форма.Объект.Контрагент, "ЮрФизЛицо");
		
		КонтрагентЯвляетсяФизЛицом = (ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо);
		ДокументЯвляетсяРеализациейКлиенту =
			(Форма.Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиенту
				ИЛИ Форма.Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности);
		
		Если КонтрагентЯвляетсяФизЛицом И ДокументЯвляетсяРеализациейКлиенту Тогда
			РазрешеноПробитиеФискальныхЧековПоДокументу	= Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат РазрешеноПробитиеФискальныхЧековПоДокументу;
	
КонецФункции

// Формирует массив форматированных строк для формирования гиперссылки пробития фискального чека
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ-ссылка, по которому пробивается фискальный чек
// 	Форма - ФормаКлиентскогоПриложения - Форма документ, из которого печатается чек - содержит:
// 	* Объект - ДокументОбъект - Документ-объект, основной параметр формы.
// 	МассивПредставлений - Массив из ФорматированнаяСтрока - Массив форматированных строк для формирования гиперссылки
//    пробития фискального чека.
Процедура ОбновитьГиперссылкуПробитияФискальногоЧека(ДокументСсылка, Форма, МассивПредставлений) Экспорт
	
	ФискальнаяОперацияДанныеЖурнала = ФормированиеФискальныхЧековСервер.ДанныеПробитогоФискальногоЧекаПоДокументу(ДокументСсылка);
	
	Если ФискальнаяОперацияДанныеЖурнала <> Неопределено Тогда
		
		НомерЧекаККМ = ФискальнаяОперацияДанныеЖурнала.НомерЧекаККМ;
		ТекстСсылки = "ОткрытьЗаписьФискальнойОперации";
		
		ФормированиеФискальныхЧековСервер.ДобавитьВПредставлениеГиперссылкиКомандуЧекПробит(МассивПредставлений, НомерЧекаККМ, ТекстСсылки);
		
	ИначеЕсли ФормированиеФискальныхЧековСервер.ЕстьПравоНаПробитиеФискальногоЧекаПоДокументу(ДокументСсылка) Тогда
		
		Если ФормированиеФискальныхЧековСервер.ЕстьПодключенноеОборудованиеККассамОрганизации(Форма.Объект.Организация) Тогда 
			ФормированиеФискальныхЧековСервер.ДобавитьВПредставлениеГиперссылкиКомандуПробитьЧек(МассивПредставлений, "ПробитьЧек");
		Иначе
			ФормированиеФискальныхЧековСервер.ДобавитьВПредставлениеГиперссылкиСтатусЧекНеПробит(МассивПредставлений, "НастроитьОборудование");
		КонецЕсли;
		
	Иначе
		
		ФормированиеФискальныхЧековСервер.ДобавитьВПредставлениеГиперссылкиСтатусЧекНеПробит(МассивПредставлений);
		
	КонецЕсли;
	
КонецПроцедуры

// Определяет виды фискальных чеков, доступных по документу
// 
// Параметры:
// 	ВидыЧеков - ТаблицаЗначений - Таблица значений, содержащая виды фискальных чеков, доступных по документу
// 	Операция - ПеречислениеСсылка.ХозяйственныеОперации - Хозяйственная операция по документу
// 	ИмяКомандыПробитияЧека - Строка - Имя команды пробития чека
Процедура ЗаполнитьВидыФискальныхЧековПоДокументу(ВидыЧеков, Операция, ИмяКомандыПробитияЧека) Экспорт
	
	ВидЧека = ВидыЧеков.Добавить();
	ВидЧека.ТипФискальногоДокумента = Перечисления.ТипыФискальныхДокументовККТ.КассовыйЧек;
	ВидЧека.ТипРасчетаДенежнымиСредствами = Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств;
	ВидЧека.ВидЧекаКоррекции = Неопределено;
	
	ВидЧека = ВидыЧеков.Добавить();
	ВидЧека.ТипФискальногоДокумента = Перечисления.ТипыФискальныхДокументовККТ.КассовыйЧекКоррекции;
	ВидЧека.ТипРасчетаДенежнымиСредствами = Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств;
	ВидЧека.ВидЧекаКоррекции = Перечисления.ВидыЧековКоррекции.НеприменениеККТ;
	
КонецПроцедуры

// Возвращает таблицу товаров для заполнения позиций строк в параметрах чека на оплату
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ для получения товарных позиций
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица с товарными позициями с количественными и суммовыми показателями:
// * НоменклатураНаименование - Строка -
// * ХарактеристикаНаименование - Строка -
// * Упаковка - СправочникСсылка.УпаковкиЕдиницыИзмерения - 
// * УпаковкаНаименование - Строка -
// * Количество - Число - 
// * Цена - Число -
// * Сумма - Число - 
// * СтавкаНДС - СправочникСсылка.СтавкиНДС -
// * СуммаНДС - Число - 
Функция ПозицииНоменклатурыПоДокументу(ДокументСсылка) Экспорт
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "ЦенаВключаетНДС, ТребуетсяЗалогЗаТару, ВернутьМногооборотнуюТару");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Упаковка КАК Упаковка,
	|	ТабличнаяЧасть.КоличествоУпаковок КАК Количество,
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА &ЦенаВключаетНДС
	|			ТОГДА ТабличнаяЧасть.Цена
	|		КОГДА ТабличнаяЧасть.КоличествоУпаковок = 0
	|			ТОГДА ТабличнаяЧасть.СуммаСНДС
	|		ИНАЧЕ (ТабличнаяЧасть.СуммаСНДС + ТабличнаяЧасть.СуммаРучнойСкидки
	|			 + ТабличнаяЧасть.СуммаАвтоматическойСкидки) / ТабличнаяЧасть.КоличествоУпаковок
	|	КОНЕЦ КАК ЧИСЛО(31, 2)) КАК Цена,
	|	ТабличнаяЧасть.СуммаСНДС КАК Сумма,
	|	ТабличнаяЧасть.СтавкаНДС КАК СтавкаНДС,
	|	ТабличнаяЧасть.СуммаНДС КАК СуммаНДС
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|	И (НЕ &ВернутьМногооборотнуюТару
	|		ИЛИ (Не &ТребуетсяЗалогЗаТару
	|			И ТабличнаяЧасть.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ИЛИ &ТребуетсяЗалогЗаТару)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Количество             КАК Количество,
	|	ТаблицаНоменклатуры.Сумма                  КАК Сумма,
	|	ТаблицаНоменклатуры.СтавкаНДС              КАК СтавкаНДС,
	|	ТаблицаНоменклатуры.СуммаНДС               КАК СуммаНДС,
	|	ТаблицаНоменклатуры.Цена                   КАК Цена,
	|	
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаНоменклатуры.Номенклатура) = ТИП(Строка) ТОГДА
	|			ТаблицаНоменклатуры.Номенклатура
	|		ИНАЧЕ
	|			ТаблицаНоменклатуры.Номенклатура.НаименованиеПолное
	|	КОНЕЦ КАК НоменклатураНаименование,
	|	ЕСТЬNULL(ТаблицаНоменклатуры.Характеристика.НаименованиеПолное, """") КАК ХарактеристикаНаименование,
	|	ТаблицаНоменклатуры.Упаковка               КАК Упаковка,
	|	ТаблицаНоменклатуры.Упаковка               КАК УпаковкаНаименование
	|
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаНоменклатуры.НомерСтроки
	|";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС"          , РеквизитыДокумента.ЦенаВключаетНДС);
	Запрос.УстановитьПараметр("ТребуетсяЗалогЗаТару"     , РеквизитыДокумента.ТребуетсяЗалогЗаТару);
	Запрос.УстановитьПараметр("ВернутьМногооборотнуюТару", РеквизитыДокумента.ВернутьМногооборотнуюТару);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Определяет систему налогообложения по документу
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ для определения системы налогообложения
// Возвращаемое значение:
// 	ПеречислениеСсылка.ТипыСистемНалогообложенияККТ - Система налогообложения по документу
Функция СистемаНалогообложенияПоДокументу(ДокументСсылка) Экспорт
	
	СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД;
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Организация, НалогообложениеНДС");
	Если РеквизитыДокумента.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаПоПатенту Тогда
		СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.Патент;
	ИначеЕсли РеквизитыДокумента.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД Тогда
		СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД;
	Иначе
		СистемаНалогообложения = РозничныеПродажиЛокализация.СистемаНалогообложенияФискальнойОперации(РеквизитыДокумента.Организация);
	КонецЕсли;
	
	Возврат СистемаНалогообложения;
	
КонецФункции

// Возвращает таблицу товаров для заполнения позиций строк в параметрах чека на поставку
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ для получения товарных позиций
// Возвращаемое значение:
// 	ТаблицаЗначений - 
Функция ПредметыРасчетовПоДокументу(ДокументСсылка) Экспорт
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "ЦенаВключаетНДС, ТребуетсяЗалогЗаТару, ВернутьМногооборотнуюТару, Организация");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.АналитикаУчетаНоменклатуры    КАК АналитикаУчетаНоменклатуры, 
	|	ТабличнаяЧасть.ЗаказКлиента                  КАК Заказ,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия,
	|	ТабличнаяЧасть.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ТабличнаяЧасть.Упаковка КАК Упаковка,
	|	СпрНоменклатура.ЕдиницаИзмерения КАК БазоваяУпаковка,
	|	
	|	СпрНоменклатура.КодВидаНоменклатурнойКлассификации,
	|
	|	СпрНоменклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	СпрНоменклатура.ПодакцизныйТовар КАК ПодакцизныйТовар,
	|	СпрНоменклатура.ОсобенностьУчета КАК ОсобенностьУчета,
	|	
	|	ВЫБОР КОГДА СпрНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|				И ЕСТЬNULL(ТабличнаяЧасть.Характеристика.Принципал, СпрНоменклатура.Принципал) <> НЕОПРЕДЕЛЕНО
	|				И ЕСТЬNULL(ТабличнаяЧасть.Характеристика.Принципал, СпрНоменклатура.Принципал) <> &Организация
	|	ТОГДА ЕСТЬNULL(ТабличнаяЧасть.Характеристика.Контрагент, СпрНоменклатура.Контрагент)
	|	ИНАЧЕ ЕСТЬNULL(СпрНоменклатура.Контрагент, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|	КОНЕЦ КАК Агент,
	|	
	|	СпрНоменклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	ТабличнаяЧасть.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТабличнаяЧасть.Упаковка КАК УпаковкаНаименование,
	|
	|	ТабличнаяЧасть.Количество КАК Количество,
	|	ТабличнаяЧасть.КоличествоУпаковок КАК КоличествоУпаковок,
	|
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА &ЦенаВключаетНДС
	|			ТОГДА ТабличнаяЧасть.Цена
	|		КОГДА ТабличнаяЧасть.КоличествоУпаковок = 0
	|			ТОГДА ТабличнаяЧасть.СуммаСНДС
	|		ИНАЧЕ (ТабличнаяЧасть.СуммаСНДС + ТабличнаяЧасть.СуммаРучнойСкидки
	|			 + ТабличнаяЧасть.СуммаАвтоматическойСкидки) / ТабличнаяЧасть.КоличествоУпаковок
	|	КОНЕЦ КАК ЧИСЛО(31, 2)) КАК Цена,
	|
	|	ТабличнаяЧасть.СуммаРучнойСкидки + ТабличнаяЧасть.СуммаАвтоматическойСкидки КАК СуммаСкидки,
	|	ТабличнаяЧасть.СтавкаНДС КАК СтавкаНДС,
	|	ТабличнаяЧасть.СуммаНДС КАК СуммаНДС,
	|	ТабличнаяЧасть.СуммаСНДС КАК СуммаСНДС,
	|	СпрНоменклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины В (&МерныеТипыЕдиницИзмерений) КАК МернаяЕдиницаИзмерения
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО СпрНоменклатура.Ссылка = ТабличнаяЧасть.Номенклатура
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|	И (НЕ &ВернутьМногооборотнуюТару
	|		ИЛИ (Не &ТребуетсяЗалогЗаТару
	|			И СпрНоменклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ИЛИ &ТребуетсяЗалогЗаТару)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШтрихкодыУпаковок.ШтрихкодУпаковки                             КАК ШтрихкодУпаковки,
	|	ШтрихкодыУпаковок.ЧастичноеВыбытиеВариантУчета                 КАК ЧастичноеВыбытиеВариантУчета,
	|	ШтрихкодыУпаковок.ЧастичноеВыбытиеНоменклатура                 КАК ЧастичноеВыбытиеНоменклатура,
	|	ШтрихкодыУпаковок.ЧастичноеВыбытиеХарактеристика               КАК ЧастичноеВыбытиеХарактеристика,
	|	ШтрихкодыУпаковок.ЧастичноеВыбытиеКоличество                   КАК ЧастичноеВыбытиеКоличество,
	|	ШтрихкодыУпаковок.РазрешительныйРежимИдентификаторЗапросаГИСМТ КАК РазрешительныйРежимИдентификаторЗапросаГИСМТ,
	|	ШтрихкодыУпаковок.РазрешительныйРежимДатаЗапросаГИСМТ          КАК РазрешительныйРежимДатаЗапросаГИСМТ
	|ПОМЕСТИТЬ ТаблицаАкцизныеМарки
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ШтрихкодыУпаковок КАК ШтрихкодыУпаковок
	|ГДЕ
	|	ШтрихкодыУпаковок.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Упаковка КАК Упаковка,
	|	Штрихкоды.Штрихкод КАК Штрихкод,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Упаковка = Штрихкоды.Упаковка
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ПриоритетШтрихКода
	|ПОМЕСТИТЬ Штрихкоды
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК Штрихкоды
	|		ПО ТаблицаТовары.Номенклатура = Штрихкоды.Номенклатура
	|			И ТаблицаТовары.Характеристика = Штрихкоды.Характеристика
	|			И (ТаблицаТовары.Упаковка = Штрихкоды.Упаковка
	|				ИЛИ &ТекстЗапросаКоэффициентУпаковки1 = 1
	|					И ТаблицаТовары.Номенклатура.ЕдиницаИзмерения = ТаблицаТовары.Упаковка.ЕдиницаИзмерения
	|					И Штрихкоды.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Штрихкоды.Номенклатура КАК Номенклатура,
	|	Штрихкоды.Характеристика КАК Характеристика,
	|	Штрихкоды.Упаковка КАК Упаковка,
	|	МАКСИМУМ(Штрихкоды.ПриоритетШтрихКода) КАК ПриоритетШтрихКода
	|ПОМЕСТИТЬ ПриоритетыШтрихКода
	|ИЗ
	|	Штрихкоды КАК Штрихкоды
	|
	|СГРУППИРОВАТЬ ПО
	|	Штрихкоды.Номенклатура,
	|	Штрихкоды.Характеристика,
	|	Штрихкоды.Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Штрихкоды.Номенклатура КАК Номенклатура,
	|	Штрихкоды.Характеристика КАК Характеристика,
	|	Штрихкоды.Упаковка КАК Упаковка,
	|	МАКСИМУМ(Штрихкоды.Штрихкод) КАК Штрихкод
	|ПОМЕСТИТЬ ШтрихкодыНоменклатуры
	|ИЗ
	|	Штрихкоды КАК Штрихкоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПриоритетыШтрихКода КАК ПриоритетыШтрихКода
	|		ПО Штрихкоды.Номенклатура = ПриоритетыШтрихКода.Номенклатура
	|			И Штрихкоды.Характеристика = ПриоритетыШтрихКода.Характеристика
	|			И Штрихкоды.Упаковка = ПриоритетыШтрихКода.Упаковка
	|			И Штрихкоды.ПриоритетШтрихКода = ПриоритетыШтрихКода.ПриоритетШтрихКода
	|
	|СГРУППИРОВАТЬ ПО
	|	Штрихкоды.Номенклатура,
	|	Штрихкоды.Характеристика,
	|	Штрихкоды.Упаковка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Товары.Заказ КАК Заказ,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	Товары.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.БазоваяУпаковка КАК БазоваяУпаковка,
	|	Товары.КодВидаНоменклатурнойКлассификации,
	|	Товары.ТипНоменклатуры КАК ТипНоменклатуры,
	|	Товары.ПодакцизныйТовар КАК ПодакцизныйТовар,
	|	Товары.ОсобенностьУчета КАК ОсобенностьУчета,
	|	Товары.Агент КАК Агент,
	|	Товары.НоменклатураНаименование КАК НоменклатураНаименование,
	|	Товары.ХарактеристикаНаименование КАК ХарактеристикаНаименование,
	|	Товары.УпаковкаНаименование КАК УпаковкаНаименование,
	|	Товары.Количество КАК Количество,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Товары.Цена КАК Цена,
	|	Товары.СуммаСкидки КАК СуммаСкидки,
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	Товары.СуммаНДС КАК СуммаНДС,
	|	Товары.СуммаСНДС КАК СуммаСНДС,
	|	ВЫБОР
	|		КОГДА Товары.МернаяЕдиницаИзмерения
	|			ТОГДА """"
	|		ИНАЧЕ ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """")
	|	КОНЕЦ КАК Штрихкод
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	ТаблицаТовары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО (ШтрихкодыНоменклатуры.Номенклатура = Товары.Номенклатура)
	|			И (ШтрихкодыНоменклатуры.Характеристика = Товары.Характеристика)
	|			И (ШтрихкодыНоменклатуры.Упаковка = Товары.Упаковка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Штрихкоды
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ШтрихкодыНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПриоритетыШтрихКода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.НомерСтроки КАК НомерСтроки,
	|	Серии.Серия КАК СерияТаблицыСерий,
	|	Серии.Количество КАК КоличествоУпаковок,
	|	Серии.Номенклатура КАК Номенклатура,
	|	Серии.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ Серии
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Серии КАК Серии
	|ГДЕ
	|	Серии.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки,
	|	Товары.Заказ,
	|	Товары.АналитикаУчетаНоменклатуры,
	|	Товары.ТипНоменклатуры,
	|	Товары.ПодакцизныйТовар,
	|	Товары.ОсобенностьУчета,
	|	Товары.Агент,
	|	Товары.Номенклатура,
	|	Товары.НоменклатураНаименование,
	|	Товары.Характеристика,
	|	Товары.ХарактеристикаНаименование,
	|	Товары.Серия,
	|	Товары.СтатусУказанияСерий,
	|	Товары.Упаковка,
	|	Товары.БазоваяУпаковка,
	|	Товары.УпаковкаНаименование,
	|	Товары.КодВидаНоменклатурнойКлассификации,
	|	Товары.КоличествоУпаковок,
	|	Товары.Количество,
	|	Товары.Цена,
	|	Товары.СуммаСкидки,
	|	Товары.СтавкаНДС,
	|	Товары.СуммаНДС,
	|	Товары.СуммаСНДС,
	|	Товары.Штрихкод
	|ИЗ
	|	Товары
	|
	|УПОРЯДОЧИТЬ ПО
	|	Товары.НомерСтроки
	|";
	Запрос.УстановитьПараметр("Ссылка"                   , ДокументСсылка);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС"          , РеквизитыДокумента.ЦенаВключаетНДС);
	Запрос.УстановитьПараметр("ТребуетсяЗалогЗаТару"     , РеквизитыДокумента.ТребуетсяЗалогЗаТару);
	Запрос.УстановитьПараметр("ВернутьМногооборотнуюТару", РеквизитыДокумента.ВернутьМногооборотнуюТару);
	Запрос.УстановитьПараметр("Организация"              , РеквизитыДокумента.Организация);
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ТаблицаТовары.Упаковка",
		"ТаблицаТовары.Номенклатура"));
	Запрос.УстановитьПараметр("МерныеТипыЕдиницИзмерений", Справочники.УпаковкиЕдиницыИзмерения.МерныеТипыЕдиницИзмерений());
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Товары = РезультатЗапроса[РезультатЗапроса.Количество() - 1].Выгрузить();
	Если НЕ ИнтеграцияГИСМ.ПодсистемаНеИспользуется() Тогда
		ДанныеКИЗиЕГАИС(ДокументСсылка, Товары);
	КонецЕсли;
	
	ТаблицаТоваровСРаспределениемПоВидамЗапасов(Товары, ДанныеПоВидамЗапасов(ДокументСсылка, РеквизитыДокумента.ЦенаВключаетНДС));
	
	ДанныеДляИСМП = РозничныеПродажиЛокализация.ДанныеДляИСМП(ДокументСсылка, МенеджерВременныхТаблиц);
	ФормированиеФискальныхЧековСерверПереопределяемый.ТаблицаТоваровСРаспределениемКодовМаркировкиИСМП(Товары, ДанныеДляИСМП);
	
	Возврат Товары;
	
КонецФункции

#КонецОбласти
//-- Локализация

#Область МаркируемаяПродукция

// Заполнить кеш штрихкодов упаковок.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
Процедура ЗаполнитьКешШтрихкодовУпаковок(Форма) Экспорт
	
	//++ Локализация
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ПараметрыИнтеграцииГосИС")
		И Форма.ПараметрыИнтеграцииГосИС.Получить("ДанныеШтрихкодовУпаковокГосИС") <> Неопределено Тогда
		ПроверкаИПодборПродукцииИС.ЗаполнитьКешШтрихкодовУпаковок(Форма);
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

// Установить параметры особенности учета номенклатуры.
// 
// Параметры:
//  ПараметрыЗаполнения - Структура -
//  ПараметрыЗапроса - Структура -
Процедура УстановитьПараметрыОсобенностиУчетаНоменклатуры(ПараметрыЗаполнения, ПараметрыЗапроса) Экспорт
	
	НеОтбиратьПоОсобенностямУчета = Истина;
	ОсобенностиУчетаНоменклатуры = Новый Массив;

	//++ Локализация
	ВидГОСИС = Неопределено;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИЛиСвойствоОбъекта(ПараметрыЗаполнения, "ВидГОСИС") Тогда
		ВидГОСИС = ПараметрыЗаполнения.ВидГОСИС;
	КонецЕсли;
	 
	Если ВидГОСИС = "ЕГАИС" Тогда
		НеОтбиратьПоОсобенностямУчета = Ложь;
		ОсобенностиУчетаНоменклатуры = ИнтеграцияЕГАИСУТ.ОсобенностиУчетаНоменклатуры();
	ИначеЕсли ВидГОСИС = "ГИСМ" Тогда
		НеОтбиратьПоОсобенностямУчета = Ложь;
		ОсобенностиУчетаНоменклатуры = ИнтеграцияГИСМУТ.ОсобенностиУчетаНоменклатуры();
	ИначеЕсли ВидГОСИС = "ВЕТИС" Тогда
		НеОтбиратьПоОсобенностямУчета = Ложь;
		ОсобенностиУчетаНоменклатуры = ИнтеграцияВЕТИСУТ.ОсобенностиУчетаНоменклатуры();
	ИначеЕсли ВидГОСИС = "ИСМП" Тогда
		НеОтбиратьПоОсобенностямУчета = Ложь;
		ОсобенностиУчетаНоменклатуры = ИнтеграцияИСМПУТ.ОсобенностиУчетаНоменклатуры();
	ИначеЕсли ВидГОСИС = "ЗЕРНО" Тогда
		НеОтбиратьПоОсобенностямУчета = Ложь;
		ОсобенностиУчетаНоменклатуры = ИнтеграцияЗЕРНОУТ.ОсобенностиУчетаНоменклатуры();
	ИначеЕсли ВидГОСИС = "САТУРН" Тогда
		НеОтбиратьПоОсобенностямУчета = Ложь;
		ОсобенностиУчетаНоменклатуры = ИнтеграцияСАТУРНУТ.ОсобенностиУчетаНоменклатуры();
	ИначеЕсли ВидГОСИС = "Не задан" Тогда
		НеОтбиратьПоОсобенностямУчета = Ложь;
		ОсобенностиУчетаНоменклатуры = ИнтеграцияИСУТ.ОсобенностиУчетаНоменклатурыБезПодсистем();
	КонецЕсли;
	//-- Локализация
	
	ПараметрыЗапроса.Вставить("НеОтбиратьПоОсобенностямУчета", НеОтбиратьПоОсобенностямУчета);
	ПараметрыЗапроса.Вставить("ОсобенностиУчетаНоменклатуры", ОсобенностиУчетаНоменклатуры);
	
КонецПроцедуры

// Возвращает текст запроса остатка к оформлению.
// 
// Параметры:
//  ТекстЗапроса - Строка - текст запроса.
//
Процедура ТекстЗапросаОрдераПоРаспоряжению(ТекстЗапроса) Экспорт
	
//++ Локализация
	ТекстЗапроса = "ВЫБРАТЬ
	|	Таблица.Регистратор КАК Ордер
	|ИЗ
	|	РегистрНакопления.ТоварыКОтгрузке.Обороты(,, Регистратор, ДокументОтгрузки В (&Распоряжения)) КАК Таблица
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Таблица.Регистратор) = ТИП(Документ.РасходныйОрдерНаТовары)
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Регистратор
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.КОтгрузкеРасход) > 0";
//-- Локализация
	
КонецПроцедуры

// Получить распоряжения.
// 
// Параметры:
//  ПроверяемыйДокумент - ДокументСсылка.РеализацияТоваровУслуг - Документ распоряжение
// 
// Возвращаемое значение:
//  Массив из ДокументСсылка - документы распоряжения
Функция ПолучитьРаспоряженияПоДокументу(ПроверяемыйДокумент) Экспорт
	
	МассивРаспоряженийНаОтгрузку = Новый Массив();
	
//++ Локализация
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугТовары.ЗаказКлиента Как Распоряжение
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка = &ПроверяемыйДокумент";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("ПроверяемыйДокумент", ПроверяемыйДокумент);
	
	ТаблицаРаспоряжений = Запрос.Выполнить().Выгрузить();
	МассивРаспоряженийНаОтгрузку = ТаблицаРаспоряжений.ВыгрузитьКолонку("Распоряжение");
	МассивРаспоряженийНаОтгрузку.Добавить(ПроверяемыйДокумент);
	
//-- Локализация

	Возврат МассивРаспоряженийНаОтгрузку;
	
КонецФункции

#Область ФормированиеОтгруженнойМаркируемойПродукции

// Сформировать отгруженные марки.
// 
// Параметры:
//  МассивЗаказовКлиентов - Массив - заказов клиентов
//  ОтгруженныеМарки - ТаблицаЗначений - Отгруженные марки
Процедура СформироватьОтгруженныеМарки(МассивЗаказовКлиентов, ОтгруженныеМарки) Экспорт

	//++ Локализация
	УстановитьПривилегированныйРежим(Истина);
	ОтгруженныеМарки = ИнициализироватьТаблицуОтгруженныхМарок();

	ТаблицаОрдеров = ПолучитьОрдераПоРаспоряжению(МассивЗаказовКлиентов);
	Для Каждого Строка Из ТаблицаОрдеров Цикл

		ШтрихкодыУпаковокПоОрдеру = ШтрихкодированиеИСМП.ВложенныеШтрихкодыУпаковокПоДокументу(
			Строка.Ордер, Неопределено);
			
		Если ШтрихкодыУпаковокПоОрдеру.ЕстьОшибки Тогда
			Продолжить;
		КонецЕсли; 
			
		Для Каждого МаркированныйТовар Из ШтрихкодыУпаковокПоОрдеру.ВложенныеШтрихкоды.МаркированныеТовары Цикл
			НоваяСтрока = ОтгруженныеМарки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, МаркированныйТовар);
			НоваяСтрока.ЗаказКлиента = Строка.ДокументОтгрузки;
			НоваяСтрока.Склад = Строка.Склад;
		КонецЦикла;
	КонецЦикла;
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

//++ Локализация

// Правила получения значений реквизитов ТТН
// 
// Возвращаемое значение:
//  см. Документы.ТранспортнаяНакладная.ПараметрыФормированияТранспортныхНакладных
//
Функция ПараметрыФормированияТранспортныхНакладных() Экспорт
	
	ПараметрыФормированияТранспортныхНакладных =
		Документы.ТранспортнаяНакладная.ПараметрыФормированияТранспортныхНакладных();
		
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.Грузоотправитель = 
	"	ВЫБОР
	|		КОГДА ОснованиеТранспортнойНакладной.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ОснованиеТранспортнойНакладной.Организация
	|		ИНАЧЕ ОснованиеТранспортнойНакладной.Грузоотправитель
	|	КОНЕЦ";
	
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.Грузополучатель = 
	"		ВЫБОР
	|			КОГДА ОснованиеТранспортнойНакладной.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ОснованиеТранспортнойНакладной.Контрагент
	|			ИНАЧЕ ОснованиеТранспортнойНакладной.Грузополучатель
	|		КОНЕЦ";
	
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.ЗаказчикПеревозки                 = "ОснованиеТранспортнойНакладной.Контрагент";
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.БанковскийСчетЗаказчикаПеревозки  = "ОснованиеТранспортнойНакладной.БанковскийСчетКонтрагента";
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.Плательщик                        = "ОснованиеТранспортнойНакладной.Контрагент";
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.БанковскийСчетПлательщика         = "ОснованиеТранспортнойНакладной.БанковскийСчетКонтрагента";
	
	ПараметрыФормированияТранспортныхНакладных.ЕстьЗаказы = Истина;
	ПараметрыФормированияТранспортныхНакладных.ИмяПоляЗаказВТЧТовары = "ЗаказКлиента";
	ПараметрыФормированияТранспортныхНакладных.ИмяПоляНакладнаяПоЗаказу = "РеализацияПоЗаказам";
	
	ПараметрыФормированияТранспортныхНакладных.ИспользоватьРасширенныеВозможностиЗаказа = Истина;
	ПараметрыФормированияТранспортныхНакладных.ТекстУсловияИспользоватьРасширенныеВозможностиЗаказа =
		"ДокументТовары.ЗаказКлиента.ЭтоЗаказКакСчет";
	ПараметрыФормированияТранспортныхНакладных.ЕстьДополнительнаяПроверкаВозможностиСозданияНакладной = Истина;
	ПараметрыФормированияТранспортныхНакладных.ИменаРеквизитовДляДополнительнойПроверки.Добавить("Статус");
	ПараметрыФормированияТранспортныхНакладных.ИменаРеквизитовДляДополнительнойПроверки.Добавить("ХозяйственнаяОперация");
	
	Возврат	ПараметрыФормированияТранспортныхНакладных;
	
КонецФункции

// Правила получения значений реквизитов ЭТН
// 
// Возвращаемое значение:
//  см. ОбменСГИСЭПДПереопределяемый.ПараметрыФормированияЭлектронныхТранспортныхНакладных
//
Функция ПараметрыФормированияЭлектронныхТранспортныхНакладных() Экспорт
	
	ПараметрыФормированияТранспортныхНакладных =
		ОбменСГИСЭПДПереопределяемый.ПараметрыФормированияЭлектронныхТранспортныхНакладных();
		
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.СсылкаТитулГрузоотправителяГрузополучатель = 
	"		ВЫБОР
	|			КОГДА ОснованиеТранспортнойНакладной.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ОснованиеТранспортнойНакладной.Контрагент
	|			ИНАЧЕ ОснованиеТранспортнойНакладной.Грузополучатель
	|		КОНЕЦ";
	
	ПараметрыФормированияТранспортныхНакладных.ЕстьЗаказы = Истина;
	ПараметрыФормированияТранспортныхНакладных.ИмяПоляЗаказВТЧТовары = "ЗаказКлиента";
	ПараметрыФормированияТранспортныхНакладных.ИмяПоляНакладнаяПоЗаказу = "РеализацияПоЗаказам";
	
	ПараметрыФормированияТранспортныхНакладных.ИспользоватьРасширенныеВозможностиЗаказа = Истина;
	ПараметрыФормированияТранспортныхНакладных.ТекстУсловияИспользоватьРасширенныеВозможностиЗаказа =
		"ДокументТовары.ЗаказКлиента.ЭтоЗаказКакСчет";
	ПараметрыФормированияТранспортныхНакладных.ЕстьДополнительнаяПроверкаВозможностиСозданияНакладной = Истина;
	ПараметрыФормированияТранспортныхНакладных.ИменаРеквизитовДляДополнительнойПроверки.Добавить("Статус");
	ПараметрыФормированияТранспортныхНакладных.ИменаРеквизитовДляДополнительнойПроверки.Добавить("ХозяйственнаяОперация");
	
	Возврат	ПараметрыФормированияТранспортныхНакладных;
	
КонецФункции

// Правила получения значений реквизитов ЭЗН
// 
// Возвращаемое значение:
//  см. ОбменСГИСЭПДПереопределяемый.ПараметрыФормированияЭлектронныхЗаказНарядов
//
Функция ПараметрыФормированияЭлектронныхЗаказНарядов() Экспорт
	
	ПараметрыФормированияТранспортныхНакладных =
		ОбменСГИСЭПДПереопределяемый.ПараметрыФормированияЭлектронныхЗаказНарядов();
		
	ПараметрыФормированияТранспортныхНакладных.ЕстьЗаказы = Истина;
	ПараметрыФормированияТранспортныхНакладных.ИмяПоляЗаказВТЧТовары = "ЗаказКлиента";
	ПараметрыФормированияТранспортныхНакладных.ИмяПоляНакладнаяПоЗаказу = "РеализацияПоЗаказам";
	
	ПараметрыФормированияТранспортныхНакладных.ЕстьДополнительнаяПроверкаВозможностиСозданияНакладной = Истина;
	ПараметрыФормированияТранспортныхНакладных.ИменаРеквизитовДляДополнительнойПроверки.Добавить("Статус");
	ПараметрыФормированияТранспортныхНакладных.ИменаРеквизитовДляДополнительнойПроверки.Добавить("ХозяйственнаяОперация");
	
	Возврат	ПараметрыФормированияТранспортныхНакладных;
	
КонецФункции

// Проверка возможности создания транспортной накладной на основании документа
//
// Параметры:
//  ДокументСсылка		 - ДокументСсылка.РеализацияТоваровУслуг
//  ЗначенияРеквизитов	 - см. ПараметрыФормированияТранспортныхНакладных
//  ВыводитьСообщение	 - Булево
//  ПутьКПолю			 - Строка
//
// Возвращаемое значение:
//  Булево - 
//
Функция МожноСоздаватьТранспортнуюНакладную(ДокументСсылка, ЗначенияРеквизитов, ВыводитьСообщение, ПутьКПолю = "") Экспорт
	
	Если ЗначенияРеквизитов.Статус = Перечисления.СтатусыРеализацийТоваровУслуг.КПредоплате Тогда
		
		Если ВыводитьСообщение Тогда
			ТекстСообщения = НСтр("ru = 'Документ %Документ%  находится в статусе %Статус%. Создание транспортных в этом статусе не возможно.'");	
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", ДокументСсылка);	
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Статус%", ЗначенияРеквизитов.Статус);
			
			Если ПустаяСтрока(ПутьКПолю) Тогда
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			Иначе
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,ПутьКПолю);
			КонецЕсли;
		КонецЕсли;
		
		Возврат Ложь;
		
	ИначеЕсли КомиссионнаяТорговляСервер.РеализацияЧерезКомиссионера(ЗначенияРеквизитов.ХозяйственнаяОперация) Тогда
		
		Если ВыводитьСообщение Тогда
			ТекстСообщения = НСтр("ru = 'Невозможно создать транспортную накладную для документа %Документ% с хозяйственной операцией %ХозяйственнаяОперация%.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", ДокументСсылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ХозяйственнаяОперация%", ЗначенияРеквизитов.ХозяйственнаяОперация);
			
			Если ПустаяСтрока(ПутьКПолю) Тогда
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			Иначе
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,ПутьКПолю);
			КонецЕсли;
		КонецЕсли;
		
		Возврат Ложь;
	
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

//-- Локализация

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект) Экспорт
	
	//++ Локализация

	
	ТекстЗапросаТаблицаПрослеживаемыеТоварыОтгруженныеВЕАЭС(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);

	//-- Локализация
	
КонецПроцедуры

//++ Локализация


Функция ТекстЗапросаТаблицаПрослеживаемыеТоварыОтгруженныеВЕАЭС(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)
	ИмяРегистра = "ПрослеживаемыеТоварыОтгруженныеВЕАЭС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 

	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасов", ТекстыЗапроса) Тогда
		Документы.РеализацияТоваровУслуг.ТекстЗапросаВтТаблицаВидыЗапасовДекоратор(Запрос, ТекстыЗапроса, ДокументОбъект);
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	&Контрагент КАК Контрагент,
	|	&Ссылка КАК СопроводительныйДокумент,
	|	ТаблицаВидыЗапасов.Номенклатура КАК Номенклатура,
	|	ТаблицаВидыЗапасов.НомерГТД КАК РНПТ,
	|	ТаблицаВидыЗапасов.НомерСтроки КАК ПорядковыйНомер,
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ТаблицаВидыЗапасов.КоличествоПоРНПТ КАК КоличествоПрослеживаемости,
	|	ТаблицаВидыЗапасов.СуммаБезНДСРегл + ТаблицаВидыЗапасов.НДСРегл КАК Сумма
	|ИЗ
	|	ВтТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|ГДЕ
	|	&ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|	И &КонтрагентУчастникЕАЭС
	|	И &КонтрагентНеРезидент
	|	И НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ) >= &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров
	|	И ТаблицаВидыЗапасов.Ссылка = &Ссылка
	|	И ТаблицаВидыЗапасов.ПрослеживаемыйТовар
	|	И ТаблицаВидыЗапасов.НоменклатураТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|														ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И &ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию)
	|	И &Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.КПредоплате)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции


//-- Локализация

#КонецОбласти

//++ Локализация
#Область Печать

#Область СчетНаОплату_1_01

Функция ТекстЗапросаДанныхДокументовДляСчетаНаОплату()
	
	ТекстыЗапроса = Новый Массив;
	ТекстыЗапроса.Добавить(ТекстЗапросаВТОснованийСчетаНаОплату_1_01());
	ТекстыЗапроса.Добавить(ТекстЗапросаВТТоваровОснованияСчетаНаОплату_1_01());
	ТекстыЗапроса.Добавить(СчетНаОплатуКлиентуЛокализация.ТекстЗапросаВТТоваровСчетаНаОплату_1_01());
	ТекстыЗапроса.Добавить(СчетНаОплатуКлиентуЛокализация.ТекстЗапросаВТНоменклатураДокументовСчетаНаОплату_1_01());
	ТекстыЗапроса.Добавить(ТекстЗапросаДанныеШапкиСчетаНаОплату_1_01());
	ТекстыЗапроса.Добавить(ТекстЗапросаДанныеЭтаповОплатыСчетаНаОплату_1_01());
	ТекстыЗапроса.Добавить(СчетНаОплатуКлиентуЛокализация.ТекстЗапросаДанныеТоваровСчетаНаОплату_1_01());
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТОснованийСчетаНаОплату_1_01()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОснований.Ссылка	КАК Ссылка,
	|	ТаблицаОснований.Ссылка	КАК ДокументОснование
	|ПОМЕСТИТЬ ТаблицаОснований
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ТаблицаОснований
	|ГДЕ
	|	ТаблицаОснований.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ДокументОснование";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДанныеШапкиСчетаНаОплату_1_01()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка	КАК Ссылка,
	|	ДанныеДокумента.Номер	КАК НомерДокумента,
	|	ДанныеДокумента.Дата	КАК ДатаДокумента,
	|	ДанныеДокумента.Ссылка										КАК ДокументОснование,
	|	ПРЕДСТАВЛЕНИЕ(ДанныеДокумента.Ссылка)						КАК НаименованиеОснования,
	|	ДанныеДокумента.Номер										КАК НомерОснования,
	|	ДанныеДокумента.Дата										КАК ДатаОснования,
	|	ЕСТЬNULL(ДанныеДоговора.ИдентификаторГосКонтракта, """")	КАК ИдентификаторГосКонтракта,
	|	""""														КАК ДополнительныеСведения,
	|	ЕСТЬNULL(ДанныеСчетаОрганизации.Владелец, ДанныеДокумента.Организация)	КАК ОрганизацияПолучатель,
	|	ДанныеДокумента.БанковскийСчетОрганизации								КАК БанковскийСчет,
	|	ДанныеДокумента.Организация					КАК Организация,
	|	ДанныеДокумента.Контрагент					КАК Контрагент,
	|	ДанныеДокумента.БанковскийСчетКонтрагента	КАК БанковскийСчетКонтрагента,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ДанныеДокумента.Организация
	|		ИНАЧЕ ДанныеДокумента.Грузоотправитель
	|	КОНЕЦ											КАК Грузоотправитель,
	|	ДанныеДокумента.БанковскийСчетГрузоотправителя	КАК БанковскийСчетГрузоотправителя,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ДанныеДокумента.Контрагент
	|		ИНАЧЕ ДанныеДокумента.Грузополучатель
	|	КОНЕЦ											КАК Грузополучатель,
	|	ДанныеДокумента.БанковскийСчетГрузополучателя	КАК БанковскийСчетГрузополучателя,
	|	ДанныеДокумента.СуммаДокумента	КАК СтоимостьСНДС,
	|	ДанныеДокумента.Дата						КАК ДатаНачалаПоставки,
	|	""""										КАК НазначениеПлатежа,
	|	ДанныеДокумента.ИдентификаторПлатежа		КАК ИнформацияДляОплаты,
	|	ПРЕДСТАВЛЕНИЕ(ДанныеДокумента.ФормаОплаты)	КАК СпособОплаты,
	|	ДанныеДокумента.Валюта								КАК Валюта,
	|	ДанныеВалюты.НаименованиеПолное						КАК НаименованиеВалюты,
	|	ДанныеВалюты.Код									КАК КодВалюты,
	|	ДанныеОрганизации.ВалютаРегламентированногоУчета	КАК ВалютаРегламентированногоУчета,
	|	ДанныеДокумента.НалогообложениеНДС	КАК НалогообложениеНДС,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.НалогообложениеНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|													ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ								КАК УчитыватьНДС,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ								КАК ОперацияОблагаетсяНДСУПокупателя,
	|	ЛОЖЬ								КАК ЧастичнаяОплата,
	|	100									КАК ПроцентОплаты,
	|	ВЫБОР
	|		КОГДА НоменклатураДокументов.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		КОГДА НоменклатураДокументов.ЕстьУслуги
	|				И НЕ НоменклатураДокументов.ЕстьТовары
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ								КАК ТолькоУслуги
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОснований КАК ТаблицаОснований
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОснований.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДанныеДоговора
	|		ПО ДанныеДокумента.Договор = ДанныеДоговора.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчетаОрганизаций КАК ДанныеСчетаОрганизации
	|		ПО ДанныеДокумента.БанковскийСчетОрганизации = ДанныеСчетаОрганизации.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Валюты КАК ДанныеВалюты
	|		ПО ДанныеДокумента.Валюта = ДанныеВалюты.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ДанныеОрганизации
	|		ПО ДанныеДокумента.Организация = ДанныеОрганизации.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураДокументов КАК НоменклатураДокументов
	|		ПО ДанныеДокумента.Ссылка = НоменклатураДокументов.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеДокумента.Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДанныеЭтаповОплатыСчетаНаОплату_1_01()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	1							КАК Порядок,
	|	ЭтапыОплаты.Ссылка			КАК Ссылка,
	|	ЭтапыОплаты.НомерСтроки		КАК НомерСтроки,
	|	ЭтапыОплаты.ДатаПлатежа		КАК ДатаПлатежа,
	|	ЭтапыОплаты.ПроцентПлатежа	КАК ПроцентПлатежа,
	|	ЭтапыОплаты.СуммаПлатежа - ЭтапыОплаты.СуммаЗалогаЗаТару КАК СуммаПлатежа,
	|	ЛОЖЬ						КАК ЭтоЗалогЗаТару
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ЭтапыГрафикаОплаты КАК ЭтапыОплаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОснований КАК ТаблицаОснований
	|		ПО ЭтапыОплаты.Ссылка = ТаблицаОснований.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2								КАК Порядок,
	|	ЭтапыОплаты.Ссылка				КАК Ссылка,
	|	ЭтапыОплаты.НомерСтроки			КАК НомерСтроки,
	|	ЭтапыОплаты.ДатаПлатежа			КАК ДатаПлатежа,
	|	0								КАК ПроцентПлатежа,
	|	ЭтапыОплаты.СуммаЗалогаЗаТару	КАК СуммаПлатежа,
	|	ИСТИНА							КАК ЭтоЗалогЗаТару
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ЭтапыГрафикаОплаты КАК ЭтапыОплаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОснований КАК ТаблицаОснований
	|		ПО ЭтапыОплаты.Ссылка = ТаблицаОснований.Ссылка
	|
	|ГДЕ
	|	ЭтапыОплаты.СуммаЗалогаЗаТару > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки,
	|	Порядок";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти
//-- Локализация

#Область Фискализация

//++ Локализация

// Обновляет таблицу товаров данными для передачи кодов КИЗ и ЕГАИС в ККТ
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ЧекККМ - Документ.
//	ДанныеПоТоварам - ТаблицаЗначений - Данные по номенклатуре для вывода в позиции чека
//
Процедура ДанныеКИЗиЕГАИС(ДокументСсылка, ДанныеПоТоварам)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Товары.ИдентификаторСтроки,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия,
	|	Товары.КоличествоУпаковок КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.Номенклатура КАК Номенклатура,
	|	Серии.Характеристика КАК Характеристика,
	|	Серии.Серия КАК Серия,
	|	Серии.Количество КАК Количество
	|ПОМЕСТИТЬ Серии
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Серии КАК Серии
	|ГДЕ
	|	Серии.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШтрихкодыУпаковок.ШтрихкодУпаковки КАК АкцизнаяМарка
	|ПОМЕСТИТЬ АкцизныеМарки
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ШтрихкодыУпаковок КАК ШтрихкодыУпаковок
	|ГДЕ
	|	ШтрихкодыУпаковок.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.Номенклатура КАК Номенклатура,
	|	Серии.Характеристика КАК Характеристика,
	|	СправочникСерии.НомерКиЗГИСМ КАК ШтрихкодСтрока,
	|	Серии.Количество КАК Количество
	|ИЗ
	|	Серии КАК Серии
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СправочникСерии
	|		ПО Серии.Серия = СправочникСерии.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Серии.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ОсобенностьУчета В (ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродукцияИзНатуральногоМеха))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	СправочникСерии.НомерКиЗГИСМ КАК ШтрихкодСтрока,
	|	Товары.Количество
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СправочникСерии
	|		ПО Товары.Серия = СправочникСерии.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	(НЕ Товары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
	|		И СправочникНоменклатура.ОсобенностьУчета В (ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродукцияИзНатуральногоМеха))";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	ШтрихкодыУпаковок = Запрос.Выполнить().Выгрузить();
	
	ТоварыРазобранные = ДанныеПоТоварам.СкопироватьКолонки();
	
	Для Каждого СтрокаТовары Из ДанныеПоТоварам Цикл
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Номенклатура", СтрокаТовары.Номенклатура);
		СтруктураОтбора.Вставить("Характеристика", СтрокаТовары.Характеристика);
		
		МассивМаркированныхТоваров = ШтрихкодыУпаковок.НайтиСтроки(СтруктураОтбора);
		
		Если МассивМаркированныхТоваров.Количество() = 0 Тогда
			СтрокаТоварыРазобранные = ТоварыРазобранные.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТоварыРазобранные, СтрокаТовары);
			
			Продолжить;
		КонецЕсли;
		
		КоличествоСтрокМассиваМаркированныхТоваров = МассивМаркированныхТоваров.Количество(); 
		ТекущаяСтрока = 1;
		
		Пока КоличествоСтрокМассиваМаркированныхТоваров >= ТекущаяСтрока Цикл
			Если СтрокаТовары.КоличествоУпаковок = 0 Тогда
				Прервать;
			КонецЕсли;
			
			МаркированныйТовар = МассивМаркированныхТоваров[КоличествоСтрокМассиваМаркированныхТоваров - ТекущаяСтрока];
			
			СтрокаТоварыРазобранные = ТоварыРазобранные.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТоварыРазобранные, СтрокаТовары);
			
			СтрокаТоварыРазобранные.Штрихкод = МаркированныйТовар.ШтрихкодСтрока;
			
			СтрокаТоварыРазобранные.КоличествоУпаковок = 1;
			СтрокаТоварыРазобранные.СуммаСкидки        = Окр(СтрокаТовары.СуммаСкидки / СтрокаТовары.КоличествоУпаковок, 2);
			СтрокаТоварыРазобранные.СуммаНДС           = Окр(СтрокаТовары.СуммаНДС / СтрокаТовары.КоличествоУпаковок, 2);
			СтрокаТоварыРазобранные.СуммаСНДС          = Окр(СтрокаТовары.СуммаСНДС / СтрокаТовары.КоличествоУпаковок, 2);
			СтрокаТоварыРазобранные.Цена               = СтрокаТоварыРазобранные.СуммаСНДС - СтрокаТоварыРазобранные.СуммаСкидки;
			
			СтрокаТовары.КоличествоУпаковок = СтрокаТовары.КоличествоУпаковок - СтрокаТоварыРазобранные.КоличествоУпаковок;
			СтрокаТовары.СуммаСкидки        = СтрокаТовары.СуммаСкидки - СтрокаТоварыРазобранные.СуммаСкидки;
			СтрокаТовары.СуммаНДС           = СтрокаТовары.СуммаНДС - СтрокаТоварыРазобранные.СуммаНДС;
			СтрокаТовары.СуммаСНДС          = СтрокаТовары.СуммаСНДС - СтрокаТоварыРазобранные.СуммаСНДС;
			
			ШтрихкодыУпаковок.Удалить(МаркированныйТовар);
			
			ТекущаяСтрока = ТекущаяСтрока + 1;
		КонецЦикла;
		
	КонецЦикла;
	
	ДанныеПоТоварам = ТоварыРазобранные;
	
КонецПроцедуры

Функция ДанныеПоВидамЗапасов(ДокументСсылка, ЦенаВключаетНДС)
	
	Запрос = Новый Запрос( 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	РеализацияТоваровУслугВидыЗапасов.ЗаказКлиента               КАК Заказ,
	|	
	|	РеализацияТоваровУслугВидыЗапасов.СтавкаНДС                  КАК СтавкаНДС,
	|	
	|	СУММА(РеализацияТоваровУслугВидыЗапасов.КоличествоУпаковок)  КАК КоличествоУпаковок,
	|	СУММА(РеализацияТоваровУслугВидыЗапасов.Количество)          КАК Количество,
	|	
	|	ЕСТЬNULL(НомераГТД.Код, НЕОПРЕДЕЛЕНО)                        КАК НомерТаможеннойДекларации,
	|	ЕСТЬNULL(СтраныМира.Код, НЕОПРЕДЕЛЕНО)                       КАК КодСтраныПроисхождения,
	|
	|	СпрВидыЗапасов.ТипЗапасов                                    КАК ТипЗапасов,
	|	ВЫБОР
	|		КОГДА СпрВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА СпрВидыЗапасов.Контрагент
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                        КАК Комитент
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ВидыЗапасов КАК РеализацияТоваровУслугВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК НомераГТД
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМира
	|			ПО НомераГТД.СтранаПроисхождения = СтраныМира.Ссылка
	|		ПО РеализацияТоваровУслугВидыЗапасов.НомерГТД = НомераГТД.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК СпрВидыЗапасов
	|		ПО СпрВидыЗапасов.Ссылка = РеализацияТоваровУслугВидыЗапасов.ВидЗапасов
	|
	|ГДЕ
	|	РеализацияТоваровУслугВидыЗапасов.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровУслугВидыЗапасов.Ссылка,
	|	РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры,
	|	РеализацияТоваровУслугВидыЗапасов.ЗаказКлиента,
	|	
	|	РеализацияТоваровУслугВидыЗапасов.СтавкаНДС,
	|	
	|	ЕСТЬNULL(НомераГТД.Код, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(СтраныМира.Код, НЕОПРЕДЕЛЕНО),
	|
	|	СпрВидыЗапасов.ТипЗапасов,
	|	ВЫБОР
	|		КОГДА СпрВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА СпрВидыЗапасов.Контрагент
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|");
	
	Запрос.УстановитьПараметр("Ссылка"			, ДокументСсылка);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС"	, ЦенаВключаетНДС);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Параметры:
// 	Товары - ТаблицаЗначений
// 	ДанныеПоВидамЗапасов - ТаблицаЗначений
Процедура ТаблицаТоваровСРаспределениемПоВидамЗапасов(Товары, ДанныеПоВидамЗапасов)
	
	Если Не (ПолучитьФункциональнуюОпцию("ИспользоватьИмпортныеТовары") 
			Или ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриЗакупках")) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаТоваровСРаспределениемПоВидамЗапасов = Товары.СкопироватьКолонки();
	ТаблицаТоваровСРаспределениемПоВидамЗапасов.Колонки.Добавить("НомерТаможеннойДекларации");
	ТаблицаТоваровСРаспределениемПоВидамЗапасов.Колонки.Добавить("КодСтраныПроисхождения");
	ТаблицаТоваровСРаспределениемПоВидамЗапасов.Колонки.Добавить("ТипЗапасов");
	ТаблицаТоваровСРаспределениемПоВидамЗапасов.Колонки.Добавить("Комитент");
	
	Товары.Сортировать("НомерСтроки Убыв");
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("АналитикаУчетаНоменклатуры", СтрокаТЧ.АналитикаУчетаНоменклатуры);
		СтруктураОтбора.Вставить("Заказ"					 , СтрокаТЧ.Заказ);
		СтруктураОтбора.Вставить("СтавкаНДС"				 , СтрокаТЧ.СтавкаНДС);
		
		МассивСтрокДанныхПоВидамЗапасов = ДанныеПоВидамЗапасов.НайтиСтроки(СтруктураОтбора);
		
		Если МассивСтрокДанныхПоВидамЗапасов.Количество() = 0 Тогда
			СтрокаТоваровСРаспределениемПоВидамЗапасов = ТаблицаТоваровСРаспределениемПоВидамЗапасов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТоваровСРаспределениемПоВидамЗапасов, СтрокаТЧ);
			
			Продолжить;
		КонецЕсли;
		
		КоличествоСтрокМассиваСтрокДанныхПоВидамЗапасов = МассивСтрокДанныхПоВидамЗапасов.Количество(); 
		ТекущаяСтрока = 1;
		
		Пока КоличествоСтрокМассиваСтрокДанныхПоВидамЗапасов >= ТекущаяСтрока Цикл
			Если СтрокаТЧ.КоличествоУпаковок = 0 Тогда
				Прервать;
			КонецЕсли;
			
			СтрокаДанныхПоВидамЗапасов = МассивСтрокДанныхПоВидамЗапасов[КоличествоСтрокМассиваСтрокДанныхПоВидамЗапасов - ТекущаяСтрока];
			
			СтрокаТоваровСРаспределениемПоВидамЗапасов = ТаблицаТоваровСРаспределениемПоВидамЗапасов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТоваровСРаспределениемПоВидамЗапасов, СтрокаТЧ);
			
			СтрокаТоваровСРаспределениемПоВидамЗапасов.НомерТаможеннойДекларации = СтрокаДанныхПоВидамЗапасов.НомерТаможеннойДекларации;
			СтрокаТоваровСРаспределениемПоВидамЗапасов.КодСтраныПроисхождения = СтрокаДанныхПоВидамЗапасов.КодСтраныПроисхождения;
			
			СтрокаТоваровСРаспределениемПоВидамЗапасов.ТипЗапасов = СтрокаДанныхПоВидамЗапасов.ТипЗапасов;
			СтрокаТоваровСРаспределениемПоВидамЗапасов.Комитент = СтрокаДанныхПоВидамЗапасов.Комитент;
			
			Если СтрокаТЧ.КоличествоУпаковок > СтрокаДанныхПоВидамЗапасов.КоличествоУпаковок Тогда
				СтрокаТоваровСРаспределениемПоВидамЗапасов.Количество         = СтрокаДанныхПоВидамЗапасов.Количество;
				СтрокаТоваровСРаспределениемПоВидамЗапасов.КоличествоУпаковок = СтрокаДанныхПоВидамЗапасов.КоличествоУпаковок;
				СтрокаТоваровСРаспределениемПоВидамЗапасов.СуммаСкидки = Окр(СтрокаТЧ.СуммаСкидки / СтрокаТЧ.КоличествоУпаковок * СтрокаТоваровСРаспределениемПоВидамЗапасов.КоличествоУпаковок, 2);
				СтрокаТоваровСРаспределениемПоВидамЗапасов.СуммаСНДС = Окр(СтрокаТЧ.СуммаСНДС / СтрокаТЧ.КоличествоУпаковок * СтрокаТоваровСРаспределениемПоВидамЗапасов.КоличествоУпаковок, 2);
				СтрокаТоваровСРаспределениемПоВидамЗапасов.СуммаНДС = Окр(СтрокаТЧ.СуммаНДС / СтрокаТЧ.КоличествоУпаковок * СтрокаТоваровСРаспределениемПоВидамЗапасов.КоличествоУпаковок, 2);
				
				СтрокаТЧ.Количество         = СтрокаТЧ.Количество - СтрокаТоваровСРаспределениемПоВидамЗапасов.Количество;
				СтрокаТЧ.КоличествоУпаковок = СтрокаТЧ.КоличествоУпаковок - СтрокаТоваровСРаспределениемПоВидамЗапасов.КоличествоУпаковок;
				СтрокаТЧ.СуммаСкидки        = СтрокаТЧ.СуммаСкидки - СтрокаТоваровСРаспределениемПоВидамЗапасов.СуммаСкидки;
				СтрокаТЧ.СуммаСНДС          = СтрокаТЧ.СуммаСНДС - СтрокаТоваровСРаспределениемПоВидамЗапасов.СуммаСНДС;
				СтрокаТЧ.СуммаНДС           = СтрокаТЧ.СуммаНДС - СтрокаТоваровСРаспределениемПоВидамЗапасов.СуммаНДС;
				
				ДанныеПоВидамЗапасов.Удалить(СтрокаДанныхПоВидамЗапасов);
			ИначеЕсли СтрокаТЧ.КоличествоУпаковок < СтрокаДанныхПоВидамЗапасов.КоличествоУпаковок Тогда
				СтрокаДанныхПоВидамЗапасов.Количество         = СтрокаДанныхПоВидамЗапасов.Количество - СтрокаТЧ.Количество;
				СтрокаДанныхПоВидамЗапасов.КоличествоУпаковок = СтрокаДанныхПоВидамЗапасов.КоличествоУпаковок - СтрокаТЧ.КоличествоУпаковок;
				СтрокаТЧ.Количество         = 0;
				СтрокаТЧ.КоличествоУпаковок = 0;
			Иначе
				ДанныеПоВидамЗапасов.Удалить(СтрокаДанныхПоВидамЗапасов);
				СтрокаТЧ.Количество         = 0;
				СтрокаТЧ.КоличествоУпаковок = 0;
			КонецЕсли;
			
			ТекущаяСтрока = ТекущаяСтрока + 1;
		КонецЦикла;
		
	КонецЦикла;
	
	ТаблицаТоваровСРаспределениемПоВидамЗапасов.Сортировать("НомерСтроки Возр");
	
	Товары = ТаблицаТоваровСРаспределениемПоВидамЗапасов;
	
КонецПроцедуры

//-- Локализация
#КонецОбласти

//++ Локализация
#Область ГосИС

#Область ДействияПриОбменеГИСМ

// Возвращаемое значение:
//	ПеречислениеСсылка.СтатусыИнформированияГИСМ,Неопределено
Функция ОбновитьСтатусПослеПодготовкиКПередачеДанных(ДокументСсылка, Операция) Экспорт
	
	НовыйСтатус        = Неопределено;
	ДальнейшееДействие = Неопределено;
	
	ИспользоватьАвтоматическийОбмен = ПолучитьФункциональнуюОпцию("ИспользоватьАвтоматическуюОтправкуПолучениеДанныхГИСМ");
	
	Если Операция = Перечисления.ОперацииОбменаГИСМ.ПередачаДанных Тогда
		НовыйСтатус = Перечисления.СтатусыИнформированияГИСМ.КПередаче;
		Если ИспользоватьАвтоматическийОбмен Тогда
			ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ОжидайтеПередачуДанныхРегламентнымЗаданием;
		Иначе
			ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ВыполнитеОбмен;
		КонецЕсли;
	КонецЕсли;
	
	Если НовыйСтатус = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	РегистрыСведений.СтатусыИнформированияГИСМ.ОбновитьСтатус(
		ДокументСсылка,
		НовыйСтатус,
		ДальнейшееДействие);
	
	Возврат НовыйСтатус;
	
КонецФункции

// Возвращаемое значение:
//	ПеречислениеСсылка.СтатусыИнформированияГИСМ,Неопределено
Функция ОбновитьСтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки) Экспорт
	
	НовыйСтатус     = Неопределено;
	ДальнейшееДействие = Неопределено;
	
	Если Операция = Перечисления.ОперацииОбменаГИСМ.ПередачаДанных Тогда
		
		Если СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийГИСМ.Принято Тогда
			
			НовыйСтатус = Перечисления.СтатусыИнформированияГИСМ.Передано;
			ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ОжидайтеПолучениеКвитанцииОФиксации;
			
		ИначеЕсли СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийГИСМ.Отклонено
			ИЛИ СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийГИСМ.Ошибка Тогда
			
			НовыйСтатус = Перечисления.СтатусыИнформированияГИСМ.ОтклоненоГИСМ;
			ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ПередайтеДанные;
			
		КонецЕсли;
		
	ИначеЕсли Операция = Перечисления.ОперацииОбменаГИСМ.ПередачаДанныхПолучениеКвитанции Тогда
		
		Если СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийГИСМ.Принято Тогда
			
			НовыйСтатус = Перечисления.СтатусыИнформированияГИСМ.ПринятоГИСМ;
			ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.НеТребуется;
			
		ИначеЕсли СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийГИСМ.Отклонено
			ИЛИ СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийГИСМ.Ошибка Тогда
			
			НовыйСтатус = Перечисления.СтатусыИнформированияГИСМ.ОтклоненоГИСМ;
			ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ПередайтеДанные;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НовыйСтатус = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	РегистрыСведений.СтатусыИнформированияГИСМ.ОбновитьСтатус(ДокументСсылка, НовыйСтатус, ДальнейшееДействие);
	
	Возврат НовыйСтатус;
	
КонецФункции

#КонецОбласти

#Область СообщенияГИСМ

Функция СообщениеКПередачеXML(ДокументСсылка, Операция) Экспорт
	
	Если Операция = Перечисления.ОперацииОбменаГИСМ.ПередачаДанных Тогда
		Возврат РеализацияВРозницуXML(ДокументСсылка);
	ИначеЕсли Операция = Перечисления.ОперацииОбменаГИСМ.ПередачаДанныхПолучениеКвитанции Тогда
		Возврат ИнтеграцияГИСМВызовСервера.ЗапросКвитанцииОФиксацииПоСсылкеXML(ДокументСсылка, Перечисления.ОперацииОбменаГИСМ.ПередачаДанных);
	КонецЕсли;
	
КонецФункции

Функция РеализацияВРозницуXML(ДокументСсылка) Экспорт
	
	Если ИнтеграцияГИСМ.ИспользоватьВозможностиВерсии("2.41") Тогда
		Возврат РеализацияВРозницуXML2_41(ДокументСсылка);
	Иначе
		Возврат РеализацияВРозницуXML2_40(ДокументСсылка);
	КонецЕсли;
	
КонецФункции

#Область Версия2_40

Функция РеализацияВРозницуXML2_40(ДокументСсылка) Экспорт
	
	СообщенияXML = Новый Массив;
	
	Версия = "2.40";
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ГИСМПрисоединенныеФайлы.ВладелецФайла КАК Ссылка,
	|	КОЛИЧЕСТВО(ГИСМПрисоединенныеФайлы.Ссылка) КАК ПоследнийНомерВерсии
	|ПОМЕСТИТЬ ВременнаяТаблица
	|ИЗ
	|	Справочник.ГИСМПрисоединенныеФайлы КАК ГИСМПрисоединенныеФайлы
	|ГДЕ
	|	ГИСМПрисоединенныеФайлы.ВладелецФайла = &Ссылка
	|	И ГИСМПрисоединенныеФайлы.Операция = ЗНАЧЕНИЕ(Перечисление.ОперацииОбменаГИСМ.ПередачаДанных)
	|	И ГИСМПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыСообщенийГИСМ.Исходящее)
	|
	|СГРУППИРОВАТЬ ПО
	|	ГИСМПрисоединенныеФайлы.ВладелецФайла
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Дата КАК Дата,
	|	ЕСТЬNULL(ВременнаяТаблица.ПоследнийНомерВерсии, 0) КАК ПоследнийНомерВерсии,
	|	РеализацияТоваровУслуг.Организация КАК Организация,
	|	РеализацияТоваровУслуг.Подразделение КАК Подразделение,
	|	РеализацияТоваровУслуг.Номер КАК Номер
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблица КАК ВременнаяТаблица
	|		ПО РеализацияТоваровУслуг.Ссылка = ВременнаяТаблица.Ссылка
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка
	|	И РеализацияТоваровУслуг.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.РозничныйПокупатель)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(Товары.НомерСтроки) КАК НомерСтроки,
	|	Серии.Серия.НомерКиЗГИСМ КАК НомерКиЗ,
	|	СУММА(ВЫБОР
	|			КОГДА Товары.КоличествоУпаковок <> 0
	|				ТОГДА ВЫБОР
	|						КОГДА НЕ Товары.Ссылка.ЦенаВключаетНДС
	|							ТОГДА ВЫРАЗИТЬ((Товары.Сумма + Товары.СуммаНДС) / Товары.КоличествоУпаковок КАК ЧИСЛО(31,2))
	|						ИНАЧЕ ВЫРАЗИТЬ(Товары.Сумма / Товары.КоличествоУпаковок КАК ЧИСЛО(31,2))
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) / СУММА(1) КАК Стоимость,
	|	СУММА(Товары.СуммаНДС) / СУММА(1) КАК СуммаНДС
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Серии КАК Серии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК Товары
	|		ПО (Товары.Ссылка = &Ссылка)
	|			И (Товары.Номенклатура = Серии.Номенклатура)
	|			И (Товары.Характеристика = Серии.Характеристика)
	|			И (Товары.Назначение = Серии.Назначение)
	|			И (Товары.Склад = Серии.Склад)
	|ГДЕ
	|	Серии.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Серии.Серия.НомерКиЗГИСМ
	|
	|УПОРЯДОЧИТЬ ПО
	|	МИНИМУМ(Товары.НомерСтроки)");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Результат = Запрос.ВыполнитьПакет();
	Шапка = Результат[1].Выбрать();
	Товары = Результат[2].Выгрузить();
	Если Не Шапка.Следующий()
		Или Товары.Количество() = 0 Тогда
		
		СообщениеXML = ИнтеграцияГИСМКлиентСервер.СтруктураСообщенияXML();
		СообщениеXML.Документ = ДокументСсылка;
		СообщениеXML.Описание = ИнтеграцияГИСМ.ОписаниеОперацииПередачиДанных(
			Перечисления.ОперацииОбменаГИСМ.ПередачаДанныхРозничнаяПродажа, ДокументСсылка);
		СообщениеXML.ТекстОшибки = НСтр("ru = 'Нет данных для выгрузки.'");
		СообщенияXML.Добавить(СообщениеXML);
		Возврат СообщенияXML;
		
	КонецЕсли;
	
	НомерВерсии = Шапка.ПоследнийНомерВерсии + 1;
	
	РеквизитыОрганизации = ИнтеграцияГИСМВызовСервера.ИННКППGLNОрганизации(Шапка.Организация, Шапка.Подразделение);
	
	СообщениеXML = ИнтеграцияГИСМКлиентСервер.СтруктураСообщенияXML();
	СообщениеXML.Описание = ИнтеграцияГИСМ.ОписаниеОперацииПередачиДанных(
		Перечисления.ОперацииОбменаГИСМ.ПередачаДанныхРозничнаяПродажа, ДокументСсылка, НомерВерсии);
	
	ИмяТипа   = "query";
	ИмяПакета = "retail_sale";
	
	ПередачаДанных = ИнтеграцияГИСМ.ОбъектXDTOПоИмениСвойства(Неопределено, ИмяТипа, Версия);
	
	РеализацияВРозницу = ИнтеграцияГИСМ.ОбъектXDTO(ИмяПакета, Версия);
	РеализацияВРозницу.action_id  = РеализацияВРозницу.action_id;
	
	Попытка
		РеализацияВРозницу.sender_gln = РеквизитыОрганизации.GLN;
	Исключение
		ИнтеграцияГИСМКлиентСервер.ДобавитьТекстОшибкиНеЗаполненGLNОрганизации(СообщениеXML, РеквизитыОрганизации.GLN, Шапка);
	КонецПопытки;
	
	РеализацияВРозницу.sales = ИнтеграцияГИСМ.ОбъектXDTOПоИмениСвойства(РеализацияВРозницу, "sales", Версия);
	
	ХранилищеВременныхДат = Новый Соответствие;
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		
		НоваяСтрока = ИнтеграцияГИСМ.ОбъектXDTOПоИмениСвойства(РеализацияВРозницу.sales, "detail", Версия);
		НоваяСтрока.sign_num = СтрокаТЧ.НомерКиЗ;
		НоваяСтрока.cost     = СтрокаТЧ.Стоимость;
		Если ЗначениеЗаполнено(СтрокаТЧ.СуммаНДС) Тогда
			НоваяСтрока.vat_value = СтрокаТЧ.СуммаНДС;
		КонецЕсли;
		
		НоваяСтрока.doc_type   = 4;
		НоваяСтрока.doc_name   = НСтр("ru = 'Отчет о розничных продажах'");
		НоваяСтрока.doc_number = Шапка.Номер;
		
		ИнтеграцияГИСМ.УстановитьДатуСЧасовымПоясом(
			НоваяСтрока,
			"sale_time",
			Шапка.Дата,
			ХранилищеВременныхДат);
		
		РеализацияВРозницу.sales.detail.Добавить(НоваяСтрока);
		
	КонецЦикла;
	
	ПередачаДанных.version    = ПередачаДанных.version;
	ПередачаДанных[ИмяПакета] = РеализацияВРозницу;
	
	ТекстСообщенияXML = ИнтеграцияГИСМ.ОбъектXDTOВXML(ПередачаДанных, ИмяТипа, Версия);
	ТекстСообщенияXML = ИнтеграцияГИСМ.ПреобразоватьВременныеДаты(ХранилищеВременныхДат, ТекстСообщенияXML);
	
	СообщениеXML.ТекстСообщенияXML  = ТекстСообщенияXML;
	СообщениеXML.КонвертSOAP = ИнтеграцияГИСМВызовСервера.ПоместитьТекстСообщенияXMLВКонвертSOAP(ТекстСообщенияXML);
	
	СообщениеXML.ТипСообщения = Перечисления.ТипыСообщенийГИСМ.Исходящее;
	СообщениеXML.Организация  = Шапка.Организация;
	СообщениеXML.Операция     = Перечисления.ОперацииОбменаГИСМ.ПередачаДанных;
	СообщениеXML.Документ     = ДокументСсылка;
	СообщениеXML.Версия       = НомерВерсии;
	
	СообщенияXML.Добавить(СообщениеXML);
	
	Возврат СообщенияXML;
	
КонецФункции

#КонецОбласти

#Область Версия2_41

Функция РеализацияВРозницуXML2_41(ДокументСсылка) Экспорт
	
	СообщенияXML = Новый Массив;
	
	Версия = ИнтеграцияГИСМ.ВерсииСхемОбмена().Клиент;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ГИСМПрисоединенныеФайлы.ВладелецФайла      КАК Ссылка,
	|	КОЛИЧЕСТВО(ГИСМПрисоединенныеФайлы.Ссылка) КАК ПоследнийНомерВерсии
	|ПОМЕСТИТЬ ВременнаяТаблица
	|ИЗ
	|	Справочник.ГИСМПрисоединенныеФайлы КАК ГИСМПрисоединенныеФайлы
	|ГДЕ
	|	ГИСМПрисоединенныеФайлы.ВладелецФайла = &Ссылка
	|	И ГИСМПрисоединенныеФайлы.Операция = ЗНАЧЕНИЕ(Перечисление.ОперацииОбменаГИСМ.ПередачаДанных)
	|	И ГИСМПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыСообщенийГИСМ.Исходящее)
	|
	|СГРУППИРОВАТЬ ПО
	|	ГИСМПрисоединенныеФайлы.ВладелецФайла
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Дата                       КАК Дата,
	|	ЕСТЬNULL(ВременнаяТаблица.ПоследнийНомерВерсии, 0) КАК ПоследнийНомерВерсии,
	|	РеализацияТоваровУслуг.Организация                 КАК Организация,
	|	РеализацияТоваровУслуг.Подразделение               КАК Подразделение,
	|	РеализацияТоваровУслуг.Номер                       КАК Номер
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблица КАК ВременнаяТаблица
	|		ПО РеализацияТоваровУслуг.Ссылка = ВременнаяТаблица.Ссылка
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка
	|	И РеализацияТоваровУслуг.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.РозничныйПокупатель)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(Товары.НомерСтроки) КАК НомерСтроки,
	|	Серии.Серия.НомерКиЗГИСМ КАК НомерКиЗ,
	|	СУММА(ВЫБОР
	|			КОГДА Товары.КоличествоУпаковок <> 0
	|				ТОГДА ВЫБОР
	|						КОГДА НЕ Товары.Ссылка.ЦенаВключаетНДС
	|							ТОГДА ВЫРАЗИТЬ((Товары.Сумма + Товары.СуммаНДС) / Товары.КоличествоУпаковок КАК ЧИСЛО(31,2))
	|						ИНАЧЕ ВЫРАЗИТЬ(Товары.Сумма / Товары.КоличествоУпаковок КАК ЧИСЛО(31,2))
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) / СУММА(1) КАК Стоимость,
	|	СУММА(Товары.СуммаНДС) / СУММА(1) КАК СуммаНДС
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Серии КАК Серии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК Товары
	|		ПО (Товары.Ссылка = &Ссылка)
	|			И (Товары.Номенклатура = Серии.Номенклатура)
	|			И (Товары.Характеристика = Серии.Характеристика)
	|			И (Товары.Назначение = Серии.Назначение)
	|			И (Товары.Склад = Серии.Склад)
	|ГДЕ
	|	Серии.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Серии.Серия.НомерКиЗГИСМ
	|
	|УПОРЯДОЧИТЬ ПО
	|	МИНИМУМ(Товары.НомерСтроки)");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Результат = Запрос.ВыполнитьПакет();
	Шапка = Результат[1].Выбрать();
	Товары = Результат[2].Выгрузить();
	Если Не Шапка.Следующий()
		Или Товары.Количество() = 0 Тогда
		
		СообщениеXML = ИнтеграцияГИСМКлиентСервер.СтруктураСообщенияXML();
		СообщениеXML.Документ = ДокументСсылка;
		СообщениеXML.Описание = ИнтеграцияГИСМ.ОписаниеОперацииПередачиДанных(
			Перечисления.ОперацииОбменаГИСМ.ПередачаДанныхРозничнаяПродажа, ДокументСсылка);
		СообщениеXML.ТекстОшибки = НСтр("ru = 'Нет данных для выгрузки.'");
		СообщенияXML.Добавить(СообщениеXML);
		Возврат СообщенияXML;
		
	КонецЕсли;
	
	НомерВерсии = Шапка.ПоследнийНомерВерсии + 1;
	
	РеквизитыОрганизации = ИнтеграцияГИСМВызовСервера.ИННКППGLNОрганизации(Шапка.Организация, Шапка.Подразделение);
	
	СообщениеXML = ИнтеграцияГИСМКлиентСервер.СтруктураСообщенияXML();
	СообщениеXML.Описание = ИнтеграцияГИСМ.ОписаниеОперацииПередачиДанных(
		Перечисления.ОперацииОбменаГИСМ.ПередачаДанныхРозничнаяПродажа, ДокументСсылка, НомерВерсии);
	
	ИмяТипа   = "query";
	ИмяПакета = "retail_sale";
	
	ПередачаДанных = ИнтеграцияГИСМ.ОбъектXDTOПоИмениСвойства(Неопределено, ИмяТипа, Версия);
	
	РеализацияВРозницу = ИнтеграцияГИСМ.ОбъектXDTO(ИмяПакета, Версия);
	РеализацияВРозницу.action_id  = РеализацияВРозницу.action_id;
	
	Попытка
		РеализацияВРозницу.sender_gln = РеквизитыОрганизации.GLN;
	Исключение
		ИнтеграцияГИСМКлиентСервер.ДобавитьТекстОшибкиНеЗаполненGLNОрганизации(СообщениеXML, РеквизитыОрганизации.GLN, Шапка);
	КонецПопытки;
	
	РеализацияВРозницу.sales = ИнтеграцияГИСМ.ОбъектXDTOПоИмениСвойства(РеализацияВРозницу, "sales", Версия);
	
	ХранилищеВременныхДат = Новый Соответствие;
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		
		НоваяСтрока = ИнтеграцияГИСМ.ОбъектXDTOПоИмениСвойства(РеализацияВРозницу.sales, "detail", Версия);
		
		НоваяСтрока.sign_num   = СтрокаТЧ.НомерКиЗ;
		НоваяСтрока.cost       = СтрокаТЧ.Стоимость;
		Если ЗначениеЗаполнено(СтрокаТЧ.СуммаНДС) Тогда
			НоваяСтрока.vat_value = СтрокаТЧ.СуммаНДС;
		КонецЕсли;
		НоваяСтрока.sale_time  = Шапка.Дата;
		
		НоваяСтрока.sale_docs = ИнтеграцияГИСМ.ОбъектXDTOПоИмениСвойства(НоваяСтрока, "sale_docs", Версия);
		doc = ИнтеграцияГИСМ.ОбъектXDTOПоИмениСвойства(НоваяСтрока.sale_docs, "doc", Версия);
		doc.doc_type   = 1;
		doc.doc_name   = НСтр("ru = 'Чек'");
		doc.doc_number = НСтр("ru = 'б/н'");
		doc.doc_date   = Шапка.Дата;
		НоваяСтрока.sale_docs.doc.Добавить(doc);
		
		РеализацияВРозницу.sales.detail.Добавить(НоваяСтрока);
		
	КонецЦикла;
	
	ПередачаДанных.version    = ПередачаДанных.version;
	ПередачаДанных[ИмяПакета] = РеализацияВРозницу;
	
	ТекстСообщенияXML = ИнтеграцияГИСМ.ОбъектXDTOВXML(ПередачаДанных, ИмяТипа, Версия);
	ТекстСообщенияXML = ИнтеграцияГИСМ.ПреобразоватьВременныеДаты(ХранилищеВременныхДат, ТекстСообщенияXML);
	
	СообщениеXML.ТекстСообщенияXML  = ТекстСообщенияXML;
	СообщениеXML.КонвертSOAP = ИнтеграцияГИСМВызовСервера.ПоместитьТекстСообщенияXMLВКонвертSOAP(ТекстСообщенияXML);
	
	СообщениеXML.ТипСообщения = Перечисления.ТипыСообщенийГИСМ.Исходящее;
	СообщениеXML.Организация  = Шапка.Организация;
	СообщениеXML.Операция     = Перечисления.ОперацииОбменаГИСМ.ПередачаДанных;
	СообщениеXML.Документ     = ДокументСсылка;
	СообщениеXML.Версия       = НомерВерсии;
	
	СообщенияXML.Добавить(СообщениеXML);
	
	Возврат СообщенияXML;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти
//-- Локализация

//++ Локализация
#Область МаркируемаяПродукция

// Получить ордера по распоряжению.
// 
// Параметры:
//  ДокументыОтгрузки - Массив из ДокументСсылка - Документы отгрузки
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Получить ордера по распоряжению
Функция ПолучитьОрдераПоРаспоряжению(ДокументыОтгрузки)
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Таблица.ДокументОтгрузки,
	|	ВЫРАЗИТЬ(Таблица.Регистратор КАК Документ.РасходныйОрдерНаТовары) КАК Ордер,
	|	ВЫРАЗИТЬ(Таблица.Регистратор КАК Документ.РасходныйОрдерНаТовары).Склад КАК Склад
	|ИЗ
	|	РегистрНакопления.ТоварыКОтгрузке.ОстаткиИОбороты(,, Регистратор,, ДокументОтгрузки В (&ДокументыОтгрузки)) КАК
	|		Таблица
	|ГДЕ
	|	Таблица.КОтгрузкеРасход > 0
	|	И Таблица.Регистратор ССЫЛКА Документ.РасходныйОрдерНаТовары
	|СГРУППИРОВАТЬ ПО
	|	ВЫРАЗИТЬ(Таблица.Регистратор КАК Документ.РасходныйОрдерНаТовары),
	|	Таблица.ДокументОтгрузки,
	|	ВЫРАЗИТЬ(Таблица.Регистратор КАК Документ.РасходныйОрдерНаТовары).Склад";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("ДокументыОтгрузки", ДокументыОтгрузки);
	
	ТаблицаОрдеровПоРаспоряжению = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаОрдеровПоРаспоряжению;
	
КонецФункции

// Инициализировать таблицу отгруженных марок.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Инициализировать таблицу отгруженных марок
Функция ИнициализироватьТаблицуОтгруженныхМарок()
	
	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("ЗаказКлиента");
	Таблица.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Таблица.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Таблица.Колонки.Добавить("Серия", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	Таблица.Колонки.Добавить("Склад", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	Таблица.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
	Таблица.Колонки.Добавить("ШтрихКодУпаковки", Новый ОписаниеТипов("СправочникСсылка.ШтрихкодыУпаковокТоваров"));
	
	Возврат Таблица;
	
КонецФункции

#КонецОбласти
//-- Локализация

// Параметры:
//	ПараметрыЗаполнения - Структура
//	РеквизитыДокумента - ВыборкаИзРезультатаЗапроса,Структура
Процедура ДополнитьПараметрыЗаполнения(ПараметрыЗаполнения, РеквизитыДокумента) Экспорт
	
	//++ Локализация
	СвойствоИмя = "ВидГОСИС";
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(РеквизитыДокумента, СвойствоИмя) Тогда
		ЗначениеСвойства = РеквизитыДокумента[СвойствоИмя];
	ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(РеквизитыДокумента, "ПараметрыОформления")
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ПараметрыЗаполнения.ПараметрыОформления, СвойствоИмя) Тогда
		ЗначениеСвойства = ПараметрыЗаполнения.ПараметрыОформления[СвойствоИмя];
	Иначе
		ЗначениеСвойства = Неопределено;
	КонецЕсли; 
	ПараметрыЗаполнения.Вставить(СвойствоИмя, ЗначениеСвойства);
	//-- Локализация	
	
КонецПроцедуры

// Конструктор возвращаемого значения
// Возвращаемое значение:
//	ТаблицаЗначений:
//	*СтрокаВыбрана - Булево
//	*Номенклатура - СправочникСсылка.Номенклатура
//	*Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
//	*Серия - СправочникСсылка.СерииНоменклатуры
Функция НовыйСтруктуруШтрихкодыУпаковокРеализации() Экспорт
	
	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("СтрокаВыбрана", 	Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("Номенклатура", 	Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Таблица.Колонки.Добавить("Характеристика", 	Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Таблица.Колонки.Добавить("Серия", 			Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	
	//++ Локализация
	Таблица.Колонки.Добавить("ШтрихкодУпаковки",Новый ОписаниеТипов("СправочникСсылка.ШтрихкодыУпаковокТоваров"));
	//-- Локализация
	
	Возврат Таблица;
	
КонецФункции

// Параметры:
//	ДокументСсылка - ДокументСсылка - 
//	ПараметрыСканирования - Структура, Неопределено - 
// Возвращаемое значение:
//	см. НовыйСтруктуруШтрихкодыУпаковокРеализации
Функция ПолучитьШтрихкодыУпаковокРеализации(
	ДокументСсылка, ПараметрыСканирования = Неопределено) Экспорт
	
	ШтрихкодыУпаковокРеализации = НовыйСтруктуруШтрихкодыУпаковокРеализации();
	
	//++ Локализация
	ШтрихкодыУпаковокПоРеализации = ШтрихкодированиеИСМП.ВложенныеШтрихкодыУпаковокПоДокументу(
		ДокументСсылка, ПараметрыСканирования);
		
	Если Не ШтрихкодыУпаковокПоРеализации.ЕстьОшибки Тогда
		Для Каждого МаркированныйТовар Из ШтрихкодыУпаковокПоРеализации.ВложенныеШтрихкоды.МаркированныеТовары Цикл
			НоваяСтрока = ШтрихкодыУпаковокРеализации.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, МаркированныйТовар);
		КонецЦикла;
	КонецЕсли; 
	//-- Локализация
	
	Возврат ШтрихкодыУпаковокРеализации;
	
КонецФункции

#КонецОбласти
