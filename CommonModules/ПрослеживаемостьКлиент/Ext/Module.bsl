
#Область СлужебныйПрограммныйИнтерфейс

#Область Выгрузка

Процедура ВыгрузитьУведомлениеПоПрослеживаемости(Форма) Экспорт

	Объект = Форма.Объект;
	
	Если Форма.Модифицированность 
		ИЛИ НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ТекстВопроса = 
			НСтр("ru = 'Перед выгрузкой в XML необходимо записать документ.'");
			
		ДополнительныеПараметры = Новый Структура("Форма", Форма);
		
		Оповещение = Новый ОписаниеОповещения("ОбработкаОтветаНаВопросОЗаписиУведомления", ЭтотОбъект, ДополнительныеПараметры);
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Записать и продолжить'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
		ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки);
		
	Иначе
		
		ВыгрузитьВФайлУведомлениеПоПрослеживаемости(Объект.Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаОтветаНаВопросОЗаписиУведомления(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ДополнительныеПараметры.Форма;
	Объект = Форма.Объект;
	
	Если Не Форма.Записать() Тогда
		Возврат;
	КонецЕсли;
	
	ВыгрузитьВФайлУведомлениеПоПрослеживаемости(Объект.Ссылка);
	
КонецПроцедуры

Процедура ВыгрузитьВФайлУведомлениеПоПрослеживаемости(Ссылка) Экспорт

	РезультатВыгрузки = ПрослеживаемостьВызовСервера.ПолучитьВыгружаемыеДанныеУведомленияПоПрослеживаемости(Ссылка);

	ОчиститьСообщения();
	
	Если РезультатВыгрузки <> Неопределено Тогда
		
		Если ЗначениеЗаполнено(РезультатВыгрузки.Ошибки) Тогда
			
			Сообщение = Новый СообщениеПользователю;
		
			Для каждого ТекущаяОшибка Из РезультатВыгрузки.Ошибки Цикл
			
				Сообщение.Текст = ТекущаяОшибка;
				Сообщение.Сообщить();
			
			КонецЦикла;
		Иначе
			
			ВыгрузитьУведомлениеВXML(РезультатВыгрузки);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыгрузитьУведомлениеВXML(РезультатВыгрузки)
	
	Попытка
			
		ПолучитьФайл(РезультатВыгрузки.АдресФайлаВыгрузки, РезультатВыгрузки.ИмяФайлаВыгрузки, Истина);
			
	Исключение
			
		Сообщение = Новый СообщениеПользователю;
			
		ТекстСообщения = "Не удалось записать файл """ + РезультатВыгрузки.ИмяФайлаВыгрузки 
		+ """! Возможно, недостаточно места на диске или диск защищен от записи.";
		
		Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1'"), ТекстСообщения);
		
		Сообщение.Сообщить();
			
	КонецПопытки;

КонецПроцедуры

#КонецОбласти
