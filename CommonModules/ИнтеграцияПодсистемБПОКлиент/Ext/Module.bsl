#Область ПрограммныйИнтерфейс

#Область ОбработчикиСобытийБСП
// Обработка программных событий, возникающих в подсистемах БСП.

// Определяет события, на которые подписана эта библиотека.
//
// Параметры:
//  Подписки - Структура - Ключами свойств структуры являются имена событий, на которые
//           подписана эта библиотека.
//
Процедура ПриОпределенииПодписокНаСобытияБСП(Подписки) Экспорт
	
	// БазоваяФункциональность
	Подписки.ПередНачаломРаботыСистемы = Истина;
	Подписки.ПриНачалеРаботыСистемы = Истина;
	Подписки.ПередЗавершениемРаботыСистемы = Истина;
	
КонецПроцедуры

// Функция, вызываемая перед началом работы системы.
//
Процедура ПередНачаломРаботыСистемы() Экспорт
	
	Если ОбщегоНазначенияБПОКлиент.РазделениеВключено() 
		И Не ОбщегоНазначенияБПОКлиент.ДоступноИспользованиеРазделенныхДанных() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыРаботыКлиента = ПараметрыРаботыКлиентаПриЗапуске();
	ПодготовитьПараметрыПриложения(ПараметрыРаботыКлиента);
	
КонецПроцедуры

// Функция, вызываемая при начале работы системы.
//
Процедура ПриНачалеРаботыСистемы() Экспорт
	
	Если ОбщегоНазначенияБПОКлиент.РазделениеВключено() 
		И Не ОбщегоНазначенияБПОКлиент.ДоступноИспользованиеРазделенныхДанных() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыРаботыКлиента = ПараметрыРаботыКлиентаПриЗапуске();
	
	// ++ НеМобильноеПриложение
	РабочееМесто = ПараметрыРаботыКлиента.РабочееМесто;
	ТребуетсяПолучитьMACАдрес = Ложь;
	Если ЗначениеЗаполнено(РабочееМесто) Тогда
		Если ПустаяСтрока(ПараметрыРаботыКлиента.MACАдресРабочегоМеста) Тогда
			ТребуетсяПолучитьMACАдрес = Истина;
		КонецЕсли;
	Иначе
		ТребуетсяПолучитьMACАдрес = Истина;
	КонецЕсли;
	
	Если ТребуетсяПолучитьMACАдрес Тогда
		Оповещение = Новый ОписаниеОповещения("ПолученMACАдресПриЗапуске", ЭтотОбъект);
		УстанавливатьРасширениеБраузера = Ложь;
		ЗапускПриложения = Истина;
		МенеджерОборудованияКлиент.НачатьПолучениеMACАдреса(Оповещение, УстанавливатьРасширениеБраузера, ЗапускПриложения);
	КонецЕсли;
	
	#Если Не ВебКлиент Тогда
	ВнешниеКомпонентыБПОКлиент.ПереустановитьКомпоненты(ПараметрыРаботыКлиента.ОборудованиеДляПереустановки);
	Если ОбщегоНазначенияБПОКлиент.ИспользуетсяРаспределеннаяФискализация() Тогда
		МодульРаспределеннаяФискализацияКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("РаспределеннаяФискализацияКлиент");
		МодульРаспределеннаяФискализацияКлиент.ПодключениеСистемыВзаимодействия(ПараметрыРаботыКлиента.ИдентификаторОбсужденияРаспределеннойФискализации); 
	КонецЕсли;
	
	Если ПараметрыРаботыКлиента.ИспользуетсяRDP Тогда
		ОбработатьРабочееМестоПриИспользованииRDP(ПараметрыРаботыКлиента);
	КонецЕсли;
	#КонецЕсли
	// -- НеМобильноеПриложение
	
	ПодготовитьПараметрыПриложения(ПараметрыРаботыКлиента);
	
КонецПроцедуры

// Процедура, вызываемая при начале работы системы, выполняет подготовку данных механизма.
//
Процедура ПередЗавершениемРаботыСистемы() Экспорт
	
КонецПроцедуры

#КонецОбласти

// Выполняет обработку внешнего события, вызывается из глобального модуля
//
// Параметры:
//  Источник - Строка.
//  Событие - Строка.
//  Данные - Строка.
Процедура ОбработкаВнешнегоСобытия(Источник, Событие, Данные) Экспорт

	ОписаниеОшибки  = "";
	// Подготовить данные
	ОписаниеСобытия = Новый Структура();
	ОписаниеСобытия.Вставить("Источник", Источник);
	ОписаниеСобытия.Вставить("Событие",  Событие);
	ОписаниеСобытия.Вставить("Данные",   Данные);
	// Передать на обработку данные.
	Результат = МенеджерОборудованияКлиент.ОбработатьСобытиеОтУстройства(ОписаниеСобытия, ОписаниеОшибки);
	Если Не Результат Тогда
		ОбщегоНазначенияБПОКлиент.СообщитьПользователю(НСтр("ru='При обработке внешнего события от устройства произошла ошибка.'") + Символы.ПС + ОписаниеОшибки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает значения параметров, необходимых для работы клиентского кода конфигурации
// без дополнительных серверных вызовов.
// 
// Возвращаемое значение:
//   Структура:
//     * ТекущаяДатаНаКлиенте - Дата
Функция ПараметрыРаботыКлиентаПриЗапуске() Экспорт
	
	Если ОбщегоНазначенияБПОКлиент.ИспользуетсяБСП() Тогда
		ПараметрыРаботыКлиента = ОбщегоНазначенияБПОКлиент.ПараметрыРаботыКлиентаПриЗапуске();
		Если ПараметрыРаботыКлиента.Свойство("ОборудованиеДляПереустановки") Тогда
			Возврат ПараметрыРаботыКлиента;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ИнтеграцияПодсистемБПОКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	
КонецФункции

Процедура ПолученMACАдресПриЗапуске(MACАдрес, Параметры) Экспорт
	
	Если ЗначениеЗаполнено(MACАдрес) Тогда
		
		ПодключаемоеОборудование = МенеджерОборудованияКлиент.ПодключаемоеОборудование();
		ПодключаемоеОборудование.MACАдрес = MACАдрес;
		
		Если ОбщегоНазначенияБПОКлиент.ЭтоВебКлиент() Тогда
			ЗапрашиватьНаСервере = Ложь;
			РабочееМесто = МенеджерОборудованияКлиент.РабочееМестоКлиента(ЗапрашиватьНаСервере);
			Если ЗначениеЗаполнено(РабочееМесто) Тогда
				МенеджерОборудованияВызовСервера.СохранитьMACАдресДляРабочееМеста(MACАдрес);
			Иначе
				ИдентификаторКлиента = МенеджерОборудованияКлиентСервер.ИдентификаторКлиентаДляРабочегоМеста();
				МенеджерОборудованияКлиент.ПривязатьРабочееМестоПоMACАдресу(ИдентификаторКлиента, MACАдрес); 
			КонецЕсли;
		Иначе
			МенеджерОборудованияВызовСервера.СохранитьMACАдресДляРабочееМеста(MACАдрес);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// ++ НеМобильноеПриложение 

Процедура ОбработатьРабочееМестоПриИспользованииRDP(ПараметрыРаботыКлиента)
	
	ИдентификаторКлиента = МенеджерОборудованияКлиент.ИдентификаторКлиентаДляРабочегоМеста();
	Параметры = Новый Структура;
	Параметры.Вставить("ИдентификаторКлиента", ИдентификаторКлиента);
	Параметры.Вставить("ИмяКомпьютера", ПараметрыРаботыКлиента.ИмяКомпьютера);
	Параметры.Вставить("MACАдрес", ПараметрыРаботыКлиента.MACАдресРабочегоМеста);
	ТекущееРМ = МенеджерОборудованияКлиент.РабочееМестоКлиента();
	Если ЗначениеЗаполнено(ТекущееРМ) Тогда
		Если ПараметрыРаботыКлиента.ИдентификаторКлиентаРабочегоМеста <> ИдентификаторКлиента Тогда
			ВопросСоздатьРабочееМестоАсинх(Параметры);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Асинх Процедура ВопросСоздатьРабочееМестоАсинх(Параметры)
	
	
	// Для текущего пользователя %1 не совпадает идентификатор клиента на текущем рабочем месте %2.
	// Возможно поменялись характеристики сервера RDP.
	// 
	// Выберите дальнейшее действие, или обратитесь к администратору:
	// 1. Обновить идентификатор у текущего рабочего места.
	// 2. Создать новое рабочее место.
	// 3. Продолжить работу.
	ТекущееРМ = МенеджерОборудованияКлиент.РабочееМестоКлиента();
	ТекстВопроса = СтрШаблон(НСтр("ru = 'Для текущего пользователя %1 не совпадает идентификатор клиента на текущем рабочем месте %2.
					  |Возможно поменялись характеристики сервера RDP.
					  |
					  |Выберите дальнейшее действие, или обратитесь к администратору:
					  |1. Обновить идентификатор у текущего рабочего места.
					  |2. Создать новое рабочее место.
					  |3. Продолжить работу.'"), ИмяПользователя(), ТекущееРМ);
		
	Кнопки = Новый СписокЗначений();
	Кнопки.Добавить("Обновить", НСтр("ru = 'Обновить'"));
	Кнопки.Добавить("Создать", НСтр("ru = 'Создать'"));
	Кнопки.Добавить("Продолжить", НСтр("ru = 'Продолжить'"));
	Ответ = Ждать ВопросАсинх(ТекстВопроса, Кнопки, , "Обновить");
	Если Ответ = "Создать" Тогда
		// нужно создать новое рабочее место
		ТекущееРМ = МенеджерОборудованияВызовСервера.СоздатьРабочееМестоКлиента(Параметры);
		МенеджерОборудованияКлиент.УстановитьРабочееМесто(ТекущееРМ);
	ИначеЕсли Ответ = "Обновить" Тогда
		МенеджерОборудованияВызовСервера.ОбновитьИдентификаторРабочегоМеста(ТекущееРМ, Параметры.ИдентификаторКлиента);
	КонецЕсли;
КонецПроцедуры

// -- НеМобильноеПриложение 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьПараметрыПриложения(ПараметрыРаботыКлиента)
	
	Если ПараметрыПриложения = Неопределено Тогда
		ПараметрыПриложения = Новый Соответствие();
	КонецЕсли;
	
	Если Не ОбщегоНазначенияБПОКлиент.ИспользуетсяБСП() Тогда
		ИмяПараметра = "БПО.ПодсистемыКонфигурации";
		Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
			ПараметрыПриложения.Вставить(ИмяПараметра, ПараметрыРаботыКлиента.ИменаПодсистем);
		КонецЕсли;
	КонецЕсли;
	
	ИмяПараметра = "БПО.ВерсияБСП";
	Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
		ПараметрыПриложения.Вставить(ИмяПараметра, ПараметрыРаботыКлиента.ВерсияБСП);
	КонецЕсли;
	
	ИмяПараметра = "БПО.СоответствиеТиповОборудования";
	Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
		ПараметрыПриложения.Вставить(ИмяПараметра, ПараметрыРаботыКлиента.СоответствиеТиповОборудования);
	КонецЕсли;
	
	ПодключаемоеОборудование = МенеджерОборудованияКлиент.ПодключаемоеОборудование();
	ПодключаемоеОборудование.РабочееМесто = ПараметрыРаботыКлиента.РабочееМесто;
	
КонецПроцедуры

#КонецОбласти

