
#Область СлужебныеПроцедурыИФункции

// Сохраняет настройку отображения форм демонстрационных сценариев и глоссария
//
// Параметры:
//  ВариантОтображения  - Строка - вариант отображения формы, может принимать значения "Редактирование", "Просмотр", "Перевод"
//
Процедура СохранитьНастройкуРежимаОтображенияФормы(ВариантОтображения) Экспорт

	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("РежимОтображенияФормаОписанияПроцессов",
		                                              "НастройкиПользователя",
		                                              ВариантОтображения);
	
КонецПроцедуры

// Выполняет перевод
//
// Параметры:
//  ИсходныйТекст             - Строка - Исходный текст
//  ИсходныйЯзык              - Строка - код языка, с которого осуществляется перевод
//  ЯзыкПеревода              - Строка - язык, на который будет выполняется перевод
//
// Возвращаемое значение:
//   Структура   - См.ДемонстрационныеСценарии.РезультатПереводаНаДругиеЯзыки 
//
Функция РезультатПереводаНаДругиеЯзыки(ИсходныйТекст, ИсходныйЯзык, ЯзыкПеревода) Экспорт

	Возврат ДемонстрационныеСценарии.РезультатПереводаНаДругиеЯзыки(ИсходныйТекст, ИсходныйЯзык, ЯзыкПеревода)

КонецФункции

// Получает ссылку на объект по навигационной ссылке
//
// Параметры:
//  НавигационнаяСсылка  - Строка - 
//
// Возвращаемое значение:
//   СправочникСсылка, ДокументСсылка, Неопределено - полученная из навигационной ссылки ссылка на объект
//
Функция СсылкаНаОбъектПоНавигационнойСсылке(НавигационнаяСсылка) Экспорт
	
	ПерваяТочка = Найти(НавигационнаяСсылка, "e1cib/data/");
	ВтораяТочка = Найти(НавигационнаяСсылка, "?ref=");
	
	ПредставлениеТипа = Сред(НавигационнаяСсылка, ПерваяТочка + 11, ВтораяТочка - ПерваяТочка - 11);
	ПредставлениеИдентификатора = Сред(НавигационнаяСсылка, ВтораяТочка + 5);
	ПредставлениеИдентификатора = Прав(ПредставлениеИдентификатора, 8) + "-"
	                              + Сред(ПредставлениеИдентификатора, 21, 4) + "-"
	                              + Сред(ПредставлениеИдентификатора, 17, 4) + "-"
	                              + Лев(ПредставлениеИдентификатора, 4) + "-"
	                              + Сред(ПредставлениеИдентификатора, 5, 12);
	
	Попытка
		ТипСсылки = ПредопределенноеЗначение(ПредставлениеТипа + ".ПустаяСсылка");
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ТипСсылки);
		Результат = МенеджерОбъекта.ПолучитьСсылку(Новый УникальныйИдентификатор(ПредставлениеИдентификатора));
	Исключение
		Результат = Неопределено; 
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
