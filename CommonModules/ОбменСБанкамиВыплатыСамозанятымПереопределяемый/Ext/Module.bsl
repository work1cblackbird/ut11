////////////////////////////////////////////////////////////////////////////////
// Подсистема "Обмен с банками. Выплаты самозанятым".
// Модуль предназначен для размещения переопределяемых процедур подсистемы.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ПолучениеДанныхВыгружаемогоДокумента

// Возвращает данные в виде структуры по связанному документу ПлатежноеПоручение
//
// Параметры:
//		РеестрВыплатыСамозанятым - ДокументСсылка
//		ДанныеПлатежногоПоручения - Структура - структура задается функцией-конструктором ОбменСБанкамиВыплатыСамозанятымСлужебный.ДанныеПлатежногоПоручения()
//
Процедура ДанныеПлатежногоПорученияРеестраВыплатСамозанятым(РеестрВыплатыСамозанятым, ДанныеПлатежногоПоручения) Экспорт
	
КонецПроцедуры

// Получает данные документов из документа РеестрВыплатыСамозанятым и заполняет данные в структуре ДанныеДокументов 
//
// Параметры:
//		РеестрВыплатыСамозанятым - ДокументСсылка - ссылка на документ,
//										по которому требуется получить данные.
//		ДанныеДокумента - Структура - структура задается функцией-конструктором 
//										ОбменСБанкамиВыплатыСамозанятымСлужебный.ДанныеЗаполненияРеестрВыплатСамозанятым()
//		
Процедура ДанныеРеестраВыплатСамозанятым(РеестрВыплатыСамозанятым, ДанныеДокумента) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СписаниеБезналичныхДенежныхСредств.Ссылка КАК Документ,
		|	СписаниеБезналичныхДенежныхСредств.Номер КАК НомерРеестра,
		|	СписаниеБезналичныхДенежныхСредств.Дата КАК ДатаРеестра,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(СписаниеБезналичныхДенежныхСредств.ТекстПлательщика КАК СТРОКА(1000))) <> """"
		|			ТОГДА ВЫРАЗИТЬ(СписаниеБезналичныхДенежныхСредств.ТекстПлательщика КАК СТРОКА(1000))
		|		ИНАЧЕ ВЫБОР
		|				КОГДА БанковскиеСчетаОрганизаций.ТекстКорреспондента <> """"
		|					ТОГДА БанковскиеСчетаОрганизаций.ТекстКорреспондента
		|				ИНАЧЕ Организации.НаименованиеПолное
		|			КОНЕЦ
		|	КОНЕЦ КАК НаименованиеОрганизации,
		|	ВЫБОР
		|		КОГДА СписаниеБезналичныхДенежныхСредств.ИННПлательщика <> """"
		|			ТОГДА СписаниеБезналичныхДенежныхСредств.ИННПлательщика
		|		ИНАЧЕ Организации.ИНН
		|	КОНЕЦ КАК ИНН,
		|	СписаниеБезналичныхДенежныхСредств.Организация КАК Организация,
		|	БанковскиеСчетаОрганизаций.НомерСчета КАК РасчетныйСчетОрганизации,
		|	ВЫБОР
		|		КОГДА БанковскиеСчетаОрганизаций.БИКБанкаДляРасчетов = """"
		|				И БанковскиеСчетаОрганизаций.БанкДляРасчетов = ЗНАЧЕНИЕ(Справочник.КлассификаторБанков.ПустаяСсылка)
		|			ТОГДА ВЫБОР
		|					КОГДА БанковскиеСчетаОрганизаций.РучноеИзменениеРеквизитовБанка
		|						ТОГДА БанковскиеСчетаОрганизаций.БИКБанка
		|					ИНАЧЕ БанковскиеСчетаОрганизаций.Банк.Код
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА БанковскиеСчетаОрганизаций.РучноеИзменениеРеквизитовБанкаДляРасчетов
		|					ТОГДА БанковскиеСчетаОрганизаций.БИКБанкаДляРасчетов
		|				ИНАЧЕ БанковскиеСчетаОрганизаций.БанкДляРасчетов.Код
		|			КОНЕЦ
		|	КОНЕЦ КАК БИК,
		|	&ДатаФормирования КАК ДатаФормирования,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(БанковскиеСчетаКонтрагентов.ТекстКорреспондента, """") = """"
		|			ТОГДА ЕСТЬNULL(Контрагенты.НаименованиеПолное, """")
		|		ИНАЧЕ БанковскиеСчетаКонтрагентов.ТекстКорреспондента
		|	КОНЕЦ КАК Контрагент,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(БанковскиеСчетаКонтрагентов.ИННКорреспондента, """") <> """"
		|			ТОГДА БанковскиеСчетаКонтрагентов.ИННКорреспондента
		|		ИНАЧЕ ЕСТЬNULL(Контрагенты.ИНН, """")
		|	КОНЕЦ КАК ИННСамозанятого,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(БанковскиеСчетаКонтрагентов.БанкДляРасчетов, ЗНАЧЕНИЕ(Справочник.КлассификаторБанков.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.КлассификаторБанков.ПустаяСсылка)
		|				И ЕСТЬNULL(БанковскиеСчетаКонтрагентов.БИКБанкаДляРасчетов, """") = """"
		|			ТОГДА ВЫБОР
		|					КОГДА ЕСТЬNULL(БанковскиеСчетаКонтрагентов.РучноеИзменениеРеквизитовБанка, ЛОЖЬ) = ИСТИНА
		|						ТОГДА ЕСТЬNULL(БанковскиеСчетаКонтрагентов.БИКБанка, """")
		|					ИНАЧЕ ЕСТЬNULL(БанковскиеСчетаКонтрагентов.Банк.Код, """")
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ЕСТЬNULL(БанковскиеСчетаКонтрагентов.РучноеИзменениеРеквизитовБанкаДляРасчетов, ЛОЖЬ) = ИСТИНА
		|					ТОГДА ЕСТЬNULL(БанковскиеСчетаКонтрагентов.БИКБанкаДляРасчетов, """")
		|				ИНАЧЕ ЕСТЬNULL(БанковскиеСчетаКонтрагентов.БанкДляРасчетов.Код, """")
		|			КОНЕЦ
		|	КОНЕЦ КАК БИКБанка,
		|	ЕСТЬNULL(БанковскиеСчетаКонтрагентов.НомерСчета, """") КАК НомерСчета,
		|	СписаниеБезналичныхДенежныхСредств.СуммаДокумента КАК СуммаДокумента,
		|	СписаниеБезналичныхДенежныхСредств.СписокКонтрагентов КАК СписокКонтрагентов,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(БанковскиеСчетаКонтрагентов.ТекстНазначения, """") <> """"
		|			ТОГДА БанковскиеСчетаКонтрагентов.ТекстНазначения
		|		ИНАЧЕ СписаниеБезналичныхДенежныхСредств.НазначениеПлатежа
		|	КОНЕЦ КАК НазначениеПлатежа
		|ПОМЕСТИТЬ ТаблицаОсновныхДанных
		|ИЗ
		|	Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО СписаниеБезналичныхДенежныхСредств.Организация = Организации.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчетаОрганизаций
		|		ПО СписаниеБезналичныхДенежныхСредств.БанковскийСчет = БанковскиеСчетаОрганизаций.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчетаКонтрагентов
		|		ПО СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента = БанковскиеСчетаКонтрагентов.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО СписаниеБезналичныхДенежныхСредств.Контрагент = Контрагенты.Ссылка
		|ГДЕ
		|	СписаниеБезналичныхДенежныхСредств.Ссылка = &Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	1 КАК Нпп,
		|	ТаблицаОсновныхДанных.Контрагент КАК Контрагент,
		|	ТаблицаОсновныхДанных.ИННСамозанятого КАК ИНН,
		|	ТаблицаОсновныхДанных.БИКБанка КАК БИК,
		|	ТаблицаОсновныхДанных.НомерСчета КАК НомерСчета,
		|	ТаблицаОсновныхДанных.СуммаДокумента КАК Сумма,
		|	ТаблицаОсновныхДанных.НазначениеПлатежа КАК НазначениеПлатежа
		|ИЗ
		|	ТаблицаОсновныхДанных КАК ТаблицаОсновныхДанных
		|ГДЕ
		|	НЕ ТаблицаОсновныхДанных.СписокКонтрагентов
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РеестрСамозанятых.НомерСтроки,
		|	ВЫБОР
		|		КОГДА БанковскиеСчетаКонтрагентов.ТекстКорреспондента = """"
		|			ТОГДА Контрагенты.НаименованиеПолное
		|		ИНАЧЕ БанковскиеСчетаКонтрагентов.ТекстКорреспондента
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА БанковскиеСчетаКонтрагентов.ИННКорреспондента <> """"
		|			ТОГДА БанковскиеСчетаКонтрагентов.ИННКорреспондента
		|		ИНАЧЕ Контрагенты.ИНН
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА БанковскиеСчетаКонтрагентов.БанкДляРасчетов = ЗНАЧЕНИЕ(Справочник.КлассификаторБанков.ПустаяСсылка)
		|				И БанковскиеСчетаКонтрагентов.БИКБанкаДляРасчетов = """"
		|			ТОГДА ВЫБОР
		|					КОГДА БанковскиеСчетаКонтрагентов.РучноеИзменениеРеквизитовБанка
		|						ТОГДА БанковскиеСчетаКонтрагентов.БИКБанка
		|					ИНАЧЕ БанковскиеСчетаКонтрагентов.Банк.Код
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА БанковскиеСчетаКонтрагентов.РучноеИзменениеРеквизитовБанкаДляРасчетов
		|					ТОГДА БанковскиеСчетаКонтрагентов.БИКБанкаДляРасчетов
		|				ИНАЧЕ БанковскиеСчетаКонтрагентов.БанкДляРасчетов.Код
		|			КОНЕЦ
		|	КОНЕЦ,
		|	БанковскиеСчетаКонтрагентов.НомерСчета,
		|	РеестрСамозанятых.Сумма,
		|	БанковскиеСчетаКонтрагентов.ТекстНазначения
		|ИЗ
		|	Документ.СписаниеБезналичныхДенежныхСредств.БанковскиеСчетаСпискаКонтрагентов КАК РеестрСамозанятых
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОсновныхДанных КАК ТаблицаОсновныхДанных
		|		ПО (ТаблицаОсновныхДанных.Документ = РеестрСамозанятых.Ссылка)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчетаКонтрагентов
		|		ПО РеестрСамозанятых.БанковскийСчетКонтрагента = БанковскиеСчетаКонтрагентов.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО РеестрСамозанятых.Контрагент = Контрагенты.Ссылка
		|ГДЕ
		|	ТаблицаОсновныхДанных.СписокКонтрагентов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОсновныхДанных.Документ КАК Документ,
		|	ТаблицаОсновныхДанных.НомерРеестра КАК НомерРеестра,
		|	ТаблицаОсновныхДанных.ДатаРеестра КАК ДатаРеестра,
		|	ТаблицаОсновныхДанных.НаименованиеОрганизации КАК НаименованиеОрганизации,
		|	ТаблицаОсновныхДанных.ИНН КАК ИНН,
		|	ТаблицаОсновныхДанных.Организация КАК Организация,
		|	ТаблицаОсновныхДанных.РасчетныйСчетОрганизации КАК РасчетныйСчетОрганизации,
		|	ТаблицаОсновныхДанных.БИК КАК БИК,
		|	ТаблицаОсновныхДанных.ДатаФормирования КАК ДатаФормирования
		|ИЗ
		|	ТаблицаОсновныхДанных КАК ТаблицаОсновныхДанных";
	
	Запрос.УстановитьПараметр("ДатаФормирования", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("Документ", РеестрВыплатыСамозанятым);
	
	ДанныеДокументов = Новый Соответствие;
	
	МассивЗапросов = Запрос.ВыполнитьПакет();
	
	ВыборкаПоСамозанятым = МассивЗапросов[МассивЗапросов.ВГраница() - 1].Выбрать();
	ДанныеПоДокументу = МассивЗапросов[МассивЗапросов.ВГраница()].Выбрать();
	
	Пока ДанныеПоДокументу.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(ДанныеДокумента, ДанныеПоДокументу);
		ДанныеДокумента.Вставить("ИдПервичногоДокумента", Строка(ДанныеПоДокументу.Документ.УникальныйИдентификатор()));
		ДанныеДокумента.Вставить("НомерРеестра", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеПоДокументу.НомерРеестра, Истина, Истина));
		ДанныеДокумента.Вставить("НомерПлатежногоПоручения", ДанныеДокумента.НомерРеестра);
		ДанныеДокумента.Вставить("ДатаПлатежногоПоручения", ДанныеДокумента.ДатаРеестра);
		СуммаИтого = 0;
		КоличествоЗаписей = 0;
		
		Пока ВыборкаПоСамозанятым.Следующий() Цикл
			
			ДанныеСтроки = ДанныеДокумента.ФизЛица.Добавить();
			Сведения = ФизическиеЛицаКлиентСервер.ЧастиИмени(ВыборкаПоСамозанятым.Контрагент);
			ЗаполнитьЗначенияСвойств(ДанныеСтроки, ВыборкаПоСамозанятым);
			ЗаполнитьЗначенияСвойств(ДанныеСтроки, Сведения);
			СуммаИтого = СуммаИтого + ДанныеСтроки.Сумма;
			КоличествоЗаписей = КоличествоЗаписей + 1;
			
		КонецЦикла;
		
		ДанныеДокумента.Вставить("КоличествоЗаписей", КоличествоЗаписей);
		ДанныеДокумента.Вставить("СуммаИтого", СуммаИтого);
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ЗаписьДанныхВЗагружаемыйДокумент

// Осуществляет загрузку информации по чекам самозанятых в документ Выплаты самозанятым
//
// Параметры:
//		ДанныеЗаполнения - Структура - данные для заполнения информации по чекам
//			- ОбщиеДанные - Структура - данные для заполнения обшей информации по чекам, 
//								определяется функцией-конструктором ОбменСБанкамиВыплатыСамозанятымСлужебный.ДанныеЗаполнения()
//			- МассивЧеков - Массив - массив из структур ДанныеЗаполненияЧекСамозанятого
//				* ДанныеЗаполненияЧекСамозанятого - Структура - данные для заполнения информации по чекам по каждому самозанятому, 
//						определяется функцией-конструктором ОбменСБанкамиВыплатыСамозанятымСлужебный.ДанныеЗаполнения()
//			- КонтрольныеСуммы - Структура - данные для контроля заполнения
//				* КоличествоЗаписей - Число - количество записей про выплаты самозанятым
//				* СуммаИтого - Число - итоговая сумма, выплаченная самозанятым банком
//		СсылкаНаДокумент - ДокументСсылка - документ, в котором происходит заполнение данных
//
Процедура ПриЗагрузкеРеестраЧековВыплатСамозанятым(ДанныеЗаполнения, СсылкаНаДокумент) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
