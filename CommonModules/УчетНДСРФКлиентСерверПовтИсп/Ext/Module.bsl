
#Область СлужебныйПрограммныйИнтерфейс

// Получение элемента справочника СтавкиНДС по перечислению СтавкиНДС
//
// Параметры:
//	ПеречислениеСтавкаНДС - ПеречислениеСсылка.СтавкиНДС - Значение ставки НДС.
//	ТипНалогообложенияНДС - ПеречислениеСсылка.ТипыНалогообложенияНДС - Налогообложение НДС, в рамках которого нужно найти ставку. 
//	                                                       Если Неопределено, то ставка получается по налогообложению ПродажаОблагаетсяНДС.
//	РасчетнаяСтавка       - Булево - значение реквизита РасчетнаяСтавка. Если Неопределено, ставка получается без учета данного реквизита.
// Возвращаемое значение:
//	СправочникСсылка.СтавкиНДС - Соответствующая перечислению ставка НДС.
//
Функция СтавкаНДСПоЗначениюПеречисления(ПеречислениеСтавкаНДС, ТипНалогообложенияНДС = Неопределено, РасчетнаяСтавка = Неопределено) Экспорт
	
	Возврат УчетНДСРФВызовСервера.СтавкаНДСПоЗначениюПеречисления(ПеречислениеСтавкаНДС, ТипНалогообложенияНДС, РасчетнаяСтавка);
	
КонецФункции

// Возвращает значение расчетной ставки НДС, соответствующей базовой ставке.
// Например, для ставки 20 будет возвращена ставка 20/120
// Если передана расчетная ставка, то будет возвращена она же.
// 
// Параметры:
// 	СтавкаНДС - СправочникСсылка.СтавкиНДС - Базовая ставка НДС
//
// Возвращаемое значение:
// 	СправочникСсылка.СтавкиНДС - Расчетная ставка НДС
//
Функция СоответствующаяРасчетнаяСтавкаНДС(СтавкаНДС) Экспорт
	
	Если Не ЗначениеЗаполнено(СтавкаНДС) Тогда
		Возврат СтавкаНДС;
	КонецЕсли;
	
	РеквизитыСтавки = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(
							СтавкаНДС, "РасчетнаяСтавка, СоответствующаяРасчетнаяСтавка");
	
	РасчетнаяСтавка =
		?(НЕ РеквизитыСтавки.РасчетнаяСтавка И ЗначениеЗаполнено(РеквизитыСтавки.СоответствующаяРасчетнаяСтавка),
			РеквизитыСтавки.СоответствующаяРасчетнаяСтавка,
			СтавкаНДС);
			
	Возврат РасчетнаяСтавка;
	
КонецФункции

// Возвращает значение базовой ставки НДС, соответствующей расчетной ставке.
// Например, для ставки 20/120 будет возвращена ставка 20
// Если передана базовая ставка, то будет возвращена она же.
// 
// Параметры:
// 	СтавкаНДС - СправочникСсылка.СтавкиНДС - Расчетная ставка НДС
//
// Возвращаемое значение:
// 	СправочникСсылка.СтавкиНДС - Базовая ставка НДС
//
Функция СоответствующаяБазоваяСтавкаНДС(СтавкаНДС) Экспорт
	
	Если Не ЗначениеЗаполнено(СтавкаНДС) Тогда
		Возврат СтавкаНДС;
	КонецЕсли;
	
	РеквизитыСтавки = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(
							СтавкаНДС, "РасчетнаяСтавка, СоответствующаяБазоваяСтавка");
	
	БазоваяСтавка = 
		?(РеквизитыСтавки.РасчетнаяСтавка И ЗначениеЗаполнено(РеквизитыСтавки.СоответствующаяБазоваяСтавка),
			РеквизитыСтавки.СоответствующаяБазоваяСтавка,
			СтавкаНДС);
			
	Возврат БазоваяСтавка;
	
КонецФункции

// Дополняет настройки учета НДС для РФ.
//
// Параметры:
//  НастройкиУчета - см. УчетНДСУПВызовСервера.НастройкиУчета
//
Процедура ДополнитьНастройкиУчета(НастройкиУчета) Экспорт
	
	УчетНДСРФВызовСервера.ДополнитьНастройкиУчета(НастройкиУчета);
	
КонецПроцедуры

#КонецОбласти