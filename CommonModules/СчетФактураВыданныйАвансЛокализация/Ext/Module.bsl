
#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив из Строка - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация


	//-- Локализация
	
КонецПроцедуры

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация
	ТекстЗапросаТаблицаЖурналУчетаСчетовФактур(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаНДСЗаписиКнигиПродаж(Запрос, ТекстыЗапроса, Регистры);

	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.СчетФактураВыданныйАванс - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.СчетФактураВыданныйАванс - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив Из Строка - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.СчетФактураВыданныйАванс - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	//++ Локализация
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(Объект.Дата) Тогда
		Дата = Объект.Дата;
	ИначеЕсли ЗначениеЗаполнено(Объект.ДатаВыставления) Тогда
		Дата = Объект.ДатаВыставления;
	Иначе
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&ДокументОснование КАК ДокументОснование,
	|	&Контрагент КАК Контрагент,
	|	&Дата КАК Дата
	|ПОМЕСТИТЬ Реквизиты
	|;
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ИсторияКППКонтрагентов.Период) КАК Период,
	|	ИсторияКППКонтрагентов.Ссылка           КАК Ссылка
	|ПОМЕСТИТЬ ЗначенияКПП
	|ИЗ
	|	Справочник.Контрагенты.ИсторияКПП КАК ИсторияКППКонтрагентов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		Реквизиты КАК Реквизиты
	|	ПО
	|		ИсторияКППКонтрагентов.Ссылка = Реквизиты.Контрагент
	|		И ИсторияКППКонтрагентов.Период <= Реквизиты.Дата
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсторияКППКонтрагентов.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Контрагент                                                      КАК Контрагент,
	|	ЕСТЬNULL(Контрагенты.ЮрФизЛицо, ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо))  КАК ЮрФизЛицо,
	|	ЕСТЬNULL(ИсторияКППКонтрагентов.КПП, Контрагенты.КПП)                     КАК КПП,
	|	&ИдентификаторГосКонтракта                                                КАК ИдентификаторГосКонтракта
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.Контрагенты КАК Контрагенты
	|	ПО
	|		Реквизиты.Контрагент = Контрагенты.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ЗначенияКПП КАК ЗначенияКПП
	|	ПО
	|		ИСТИНА
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.Контрагенты.ИсторияКПП КАК ИсторияКППКонтрагентов
	|	ПО
	|		ЗначенияКПП.Ссылка = ИсторияКППКонтрагентов.Ссылка
	|		И ЗначенияКПП.Период = ИсторияКППКонтрагентов.Период
	|";
	Запрос.УстановитьПараметр("Контрагент",        Объект.Контрагент);
	Запрос.УстановитьПараметр("Дата",              Дата);
	Запрос.УстановитьПараметр("ДокументОснование", Объект.ДокументОснование);
	
	ВыборИдентификаторГосКонтракта = "НЕОПРЕДЕЛЕНО";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИдентификаторГосКонтракта", ВыборИдентификаторГосКонтракта);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Объект.КППКонтрагента = Выборка.КПП;
	Объект.ИдентификаторГосКонтракта = Выборка.ИдентификаторГосКонтракта;
	
	ДатаВерсииКодов = ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДата());
	ВерсияКодовВидовОпераций = УчетНДСКлиентСервер.ВерсияКодовВидовОпераций(ДатаВерсииКодов);
	Если ВерсияКодовВидовОпераций >= 3 
		И ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ОперацияПоПлатежнойКарте")
		И Выборка.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		Объект.КодВидаОперации = "26";
	ИначеЕсли ВерсияКодовВидовОпераций >= 4
		И Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя Тогда
		Объект.КодВидаОперации = "33";
	Иначе
		Объект.КодВидаОперации = "02";
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.СчетФактураВыданныйАванс - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.СчетФактураВыданныйАванс - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	//++ Локализация
	Если Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя Тогда
		СтавкаПоУмолчанию = УчетНДСУП.СтавкаНДСПоУмолчанию(Объект.Организация, Объект.Дата, Истина);
		Для Каждого СтрокаАванса Из Объект.Авансы Цикл
			СтрокаАванса.СтавкаНДС = СтавкаПоУмолчанию;
		КонецЦикла;
	КонецЕсли;
	
	// заполнение признака СводныйКорректировочный
	ИсходныеСФ = Объект.Авансы.Выгрузить(,"ИсходныйСчетФактура");
	ИсходныеСФ.Свернуть("ИсходныйСчетФактура");
	Если Объект.Корректировочный Тогда
		Если ИсходныеСФ.Количество() = 1 Тогда
			Объект.СводныйКорректировочный = Ложь;
		Иначе
			Объект.СводныйКорректировочный = Истина;
		КонецЕсли;
	Иначе
		Объект.СводныйКорректировочный = Ложь;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		Объект.РучнаяКорректировкаЖурналаСФ = Ложь;
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.СчетФактураВыданныйАванс - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.СчетФактураВыданныйАванс - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект.СчетФактураВыданныйАванс - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//	КомандыСозданияНаОсновании - См. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//	Параметры - См. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
КонецПроцедуры

// Добавляет команду создания документа "Списание НДС на расходы".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//++ Локализация
	// Счет-фактура
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "СчетФактура";
	КомандаПечати.Представление = НСтр("ru = 'Счет-фактура'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВВалюте", Ложь);
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив Из ДокументСсылка.СчетФактураВыданныйАванс - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
КонецПроцедуры

#КонецОбласти


#Область Прочее

// Переопределяет номер на печать для счета-фактуры
// 
// Параметры:
// 	НомерНаПечать - Строка - Значение в стандартном реквизите Номер.
// 	Реквизиты - ВыборкаИзРезультатаЗапроса - Выборка реквизитов документа.
//
Процедура УстановитьНомерНаПечать(НомерНаПечать, Реквизиты) Экспорт
	
	//++ Локализация
	Если ТипЗнч(Реквизиты.ДокументОснование) = Тип("ДокументСсылка.ВводОстатков")
		ИЛИ ТипЗнч(Реквизиты.ДокументОснование) = Тип("ДокументСсылка.ВводОстатковВзаиморасчетов")
		ИЛИ ТипЗнч(Реквизиты.ДокументОснование) = Тип("ДокументСсылка.ПервичныйДокумент") Тогда
		НомерНаПечать = Реквизиты.Номер;
	Иначе
		
		ОбособленноеПодразделение = Ложь;
		ЦифровойИндексОбособленногоПодразделения = 0;
		
		Если ЗначениеЗаполнено(Реквизиты.Организация) Тогда
			РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
									Реквизиты.Организация, "ОбособленноеПодразделение, ЦифровойИндексОбособленногоПодразделения");
			ОбособленноеПодразделение = РеквизитыОрганизации.ОбособленноеПодразделение;
			ЦифровойИндексОбособленногоПодразделения = РеквизитыОрганизации.ЦифровойИндексОбособленногоПодразделения;
		КонецЕсли;
		
		НомерНаПечать = УчетНДСПереопределяемый.НомерСчетаФактурыНаПечать(
					?(Реквизиты.Исправление, Реквизиты.НомерСчетаФактурыОснования, Реквизиты.Номер), 
					Ложь,
					ОбособленноеПодразделение, 
					ЦифровойИндексОбособленногоПодразделения);
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

// Дополняет текст запроса получения данных документов расчетов
// 
// Параметры:
// 	МассивТекстовЗапроса - Массив из Строка - Массив текстов запроса получения данных документов
// 	Запрос - Запрос - Запрос для установки параметров
//
Процедура ДополнитьТекстыЗапросаДанныхДокументовРасчетов(МассивТекстовЗапроса, Запрос) Экспорт
	
	//++ Локализация
	
	Запрос.УстановитьПараметр("СтавкаНДС20_120", УчетНДСЛокализация.СтавкаНДСПоПеречислению(Перечисления.СтавкиНДС.НДС20_120));
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	АвансыДляФормированияСФ.ДокументОснование  КАК ДокументОснование,
	|	АвансыДляФормированияСФ.ОбъектРасчетов     КАК ОбъектРасчетов,
	|	&СтавкаНДС20                               КАК СтавкаНДС,
	|	ОперацияПоЯндексКассе.СуммаДокументаСНДС20 КАК Сумма
	|ИЗ
	|	Документ.ОперацияПоЯндексКассе КАК ОперацияПоЯндексКассе
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		АвансыДляФормированияСФ КАК АвансыДляФормированияСФ
	|	ПО
	|		ОперацияПоЯндексКассе.Ссылка = АвансыДляФормированияСФ.ДокументОснование
	|		И ОперацияПоЯндексКассе.ОбъектРасчетов = АвансыДляФормированияСФ.ОбъектРасчетов
	|ГДЕ
	|	ОперацияПоЯндексКассе.СуммаДокументаСНДС20 <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АвансыДляФормированияСФ.ДокументОснование  КАК ДокументОснование,
	|	АвансыДляФормированияСФ.ОбъектРасчетов     КАК ОбъектРасчетов,
	|	&СтавкаНДС10                               КАК СтавкаНДС,
	|	ОперацияПоЯндексКассе.СуммаДокументаСНДС10 КАК Сумма
	|ИЗ
	|	Документ.ОперацияПоЯндексКассе КАК ОперацияПоЯндексКассе
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		АвансыДляФормированияСФ КАК АвансыДляФормированияСФ
	|	ПО
	|		ОперацияПоЯндексКассе.Ссылка = АвансыДляФормированияСФ.ДокументОснование
	|		И ОперацияПоЯндексКассе.ОбъектРасчетов = АвансыДляФормированияСФ.ОбъектРасчетов
	|ГДЕ
	|	ОперацияПоЯндексКассе.СуммаДокументаСНДС10 <> 0
	|";
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("СтавкаНДС20", УчетНДСЛокализация.СтавкаНДСПоПеречислению(Перечисления.СтавкиНДС.НДС20));
	Запрос.УстановитьПараметр("СтавкаНДС10", УчетНДСЛокализация.СтавкаНДСПоПеречислению(Перечисления.СтавкиНДС.НДС10));
	
	//-- Локализация
	
КонецПроцедуры

// Переопределят ставку НДС, которая указывается в счете-фактуре
// 
// Параметры:
// 	СтавкаНДСАванса - СправочникСсылка.СтавкиНДС - Ставка НДС
Процедура УстановитьСтавкуНДСАванса(СтавкаНДСАванса) Экспорт
	
	//++ Локализация
	СтавкаНДСАванса = УчетНДСРФКлиентСерверПовтИсп.СоответствующаяРасчетнаяСтавкаНДС(СтавкаНДСАванса);
	//-- Локализация
	
КонецПроцедуры

// Дополняет массив менеджеров объектов, которые формируют ссылку "См. также" в рабочем месте.
// 
// Параметры:
// 	МассивМенеджеровСмТакже - Массив Из Строка - Полные имена объектов для получения гиперссылки
//
Процедура ЗаполнитьМенеджерыСмТакжеРабочегоМеста(МассивМенеджеровСмТакже) Экспорт
	
	//++ Локализация
	МассивМенеджеровСмТакже.Добавить(Метаданные.Обработки.ЖурналДокументовНДС.ПолноеИмя());
	//-- Локализация
	
КонецПроцедуры

// Дополняет параметры формы выбора ставки НДС в рабочем месте по формированию счетов-фактур.
// 
// Параметры:
// 	ПараметрыФормыВыбора - Структура - Парамеры выбора ставки
//
Процедура ДополнитьПараметрыФормыВыбораСтавкиНДСВРабочемМесте(ПараметрыФормыВыбора) Экспорт
	
	//++ Локализация
	ДополнительныйОтбор = Новый Структура();
	ДополнительныйОтбор.Вставить("РасчетнаяСтавка", Истина);
	ПараметрыФормыВыбора.Вставить("Отбор", ДополнительныйОтбор);
	//-- Локализация
	
КонецПроцедуры

// Функция возвращает признак того, что это реализация без перехода права собственности и порядок расчетов по расчетным документов.
//
// Возвращаемое значение:
//	Булево
//
Функция ЭтоРеализацияВПутиПоРасчетнымДокументам(ВыборкаАванс) Экспорт
	
	Значение = Ложь;
	//++ Локализация
	Если ВыборкаАванс.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности
		И ВыборкаАванс.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоНакладным Тогда
		Значение = Истина;
	КонецЕсли;
	//-- Локализация
	Возврат Значение;
	
КонецФункции

#КонецОбласти

#КонецОбласти

//++ Локализация

#Область СлужебныеПроцедурыИФункции

#Область Проведение
	
Функция ТекстЗапросаТаблицаЖурналУчетаСчетовФактур(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ЖурналУчетаСчетовФактур";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	УчетНДСРФ.УстановитьПараметрыЗапросаПроведенияПоЖурналуСчетаФактурыВыставленного(Запрос.Параметры, Запрос);
	
	ИнициализироватьВтСуммыДляЖурналаУчетаСчетовФактур(Запрос);
	ИнициализироватьВтИсходныеСчетаФактуры(Запрос);
	УстановитьПараметрИсправляемыйСчетФактура(Запрос);
	
#Область ТекстРучнаяКорректировка
	ТекстЗапросаРучнаяКорректировка = 
	"ВЫБРАТЬ
	|	ЖурналУчетаСчетовФактур.Период КАК Период,
	|	ЖурналУчетаСчетовФактур.Организация КАК Организация,
	|	ЖурналУчетаСчетовФактур.Контрагент КАК Контрагент,
	|	ЖурналУчетаСчетовФактур.СчетФактура КАК СчетФактура,
	|	ЖурналУчетаСчетовФактур.ЧастьЖурнала КАК ЧастьЖурнала,
	|	ЖурналУчетаСчетовФактур.ДатаВыставленияПолучения КАК ДатаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактур.КодСпособаВыставленияПолучения КАК КодСпособаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактур.КодВидаОперации КАК КодВидаОперации,
	|	ЖурналУчетаСчетовФактур.НомерСчетаФактуры КАК НомерСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры КАК НомерКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры КАК ДатаКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.НомерИсправления КАК НомерИсправления,
	|	ЖурналУчетаСчетовФактур.ДатаИсправления КАК ДатаИсправления,
	|	ЖурналУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры КАК НомерИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры КАК ДатаИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ИндексСтроки КАК ИндексСтроки,
	|	ЖурналУчетаСчетовФактур.Валюта КАК Валюта,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуре КАК СуммаПоСчетуФактуре,
	|	ЖурналУчетаСчетовФактур.СуммаНДС КАК СуммаНДС,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУменьшение КАК СуммаПоСчетуФактуреРазницаУменьшение,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУвеличение КАК СуммаПоСчетуФактуреРазницаУвеличение,
	|	ЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшение КАК СуммаНДСРазницаУменьшение,
	|	ЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличение КАК СуммаНДСРазницаУвеличение,
	|	ЖурналУчетаСчетовФактур.ПоСтавкеБезНДС КАК ПоСтавкеБезНДС,
	|	ЖурналУчетаСчетовФактур.СчетФактураНеВыставляется КАК СчетФактураНеВыставляется,
	|	ЖурналУчетаСчетовФактур.КППКонтрагента КАК КППКонтрагента,
	|	ЖурналУчетаСчетовФактур.ИННКонтрагента КАК ИННКонтрагента,
	|	ЖурналУчетаСчетовФактур.Посредник КАК Посредник,
	|	ЖурналУчетаСчетовФактур.СчетФактураВыданныйПокупателю КАК СчетФактураВыданныйПокупателю,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреКомиссия КАК СуммаПоСчетуФактуреКомиссия,
	|	ЖурналУчетаСчетовФактур.СуммаНДСКомиссия КАК СуммаНДСКомиссия,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУменьшениеКомиссия КАК СуммаПоСчетуФактуреРазницаУменьшениеКомиссия,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУвеличениеКомиссия КАК СуммаПоСчетуФактуреРазницаУвеличениеКомиссия,
	|	ЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшениеКомиссия КАК СуммаНДСРазницаУменьшениеКомиссия,
	|	ЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличениеКомиссия КАК СуммаНДСРазницаУвеличениеКомиссия,
	|	ЖурналУчетаСчетовФактур.КодВидаОперацииКомиссия КАК КодВидаОперацииКомиссия,
	|	ЖурналУчетаСчетовФактур.Субкомиссионер КАК Субкомиссионер,
	|	ЖурналУчетаСчетовФактур.КодВидаСделки КАК КодВидаСделки,
	|	ЖурналУчетаСчетовФактур.НомерСчетаФактурыПродавца КАК НомерСчетаФактурыПродавца,
	|	ЖурналУчетаСчетовФактур.ДатаСчетаФактурыПродавца КАК ДатаСчетаФактурыПродавца,
	|	ЖурналУчетаСчетовФактур.ИННПродавца КАК ИННПродавца,
	|	ЖурналУчетаСчетовФактур.КПППродавца КАК КПППродавца,
	|	ЖурналУчетаСчетовФактур.ИННСубкомиссионера КАК ИННСубкомиссионера,
	|	ЖурналУчетаСчетовФактур.КППСубкомиссионера КАК КППСубкомиссионера,
	|	ЖурналУчетаСчетовФактур.Сторно КАК Сторно,
	|	ЖурналУчетаСчетовФактур.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
	|	ЖурналУчетаСчетовФактур.СчетФактураПолученныйОтПродавца КАК СчетФактураПолученныйОтПродавца,
	|	ЖурналУчетаСчетовФактур.ИсправлениеСобственнойОшибки КАК ИсправлениеСобственнойОшибки,
	|	ЖурналУчетаСчетовФактур.Продавец КАК Продавец
	|ИЗ
	|	РегистрСведений.ЖурналУчетаСчетовФактур КАК ЖурналУчетаСчетовФактур
	|ГДЕ
	|	ЖурналУчетаСчетовФактур.Регистратор = &Ссылка
	|	И &РучнаяКорректировкаЖурналаСФ
	|";
#КонецОбласти
	
#Область ТекстСторноИсправление
	ТекстЗапросаСторноИсправление = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЖурналУчетаСчетовФактур.Организация КАК Организация,
	|	ЖурналУчетаСчетовФактур.Контрагент КАК Контрагент,
	|	ЖурналУчетаСчетовФактур.СчетФактура КАК СчетФактура,
	|	ЖурналУчетаСчетовФактур.ЧастьЖурнала КАК ЧастьЖурнала,
	|	ЖурналУчетаСчетовФактур.ДатаВыставленияПолучения КАК ДатаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактур.КодСпособаВыставленияПолучения КАК КодСпособаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактур.КодВидаОперации КАК КодВидаОперации,
	|	ЖурналУчетаСчетовФактур.НомерСчетаФактуры КАК НомерСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры КАК НомерКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры КАК ДатаКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.НомерИсправления КАК НомерИсправления,
	|	ЖурналУчетаСчетовФактур.ДатаИсправления КАК ДатаИсправления,
	|	ЖурналУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры КАК НомерИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры КАК ДатаИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ИндексСтроки КАК ИндексСтроки,
	|	ЖурналУчетаСчетовФактур.Валюта КАК Валюта,
	|	-ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуре КАК СуммаПоСчетуФактуре,
	|	-ЖурналУчетаСчетовФактур.СуммаНДС КАК СуммаНДС,
	|	-ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУменьшение КАК СуммаПоСчетуФактуреРазницаУменьшение,
	|	-ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУвеличение КАК СуммаПоСчетуФактуреРазницаУвеличение,
	|	-ЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшение КАК СуммаНДСРазницаУменьшение,
	|	-ЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличение КАК СуммаНДСРазницаУвеличение,
	|	ЖурналУчетаСчетовФактур.ПоСтавкеБезНДС КАК ПоСтавкеБезНДС,
	|	ЖурналУчетаСчетовФактур.СчетФактураНеВыставляется КАК СчетФактураНеВыставляется,
	|	ЖурналУчетаСчетовФактур.КППКонтрагента КАК КППКонтрагента,
	|	ЖурналУчетаСчетовФактур.ИННКонтрагента КАК ИННКонтрагента,
	|	ЖурналУчетаСчетовФактур.Посредник КАК Посредник,
	|	ЖурналУчетаСчетовФактур.СчетФактураВыданныйПокупателю КАК СчетФактураВыданныйПокупателю,
	|	-ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреКомиссия КАК СуммаПоСчетуФактуреКомиссия,
	|	-ЖурналУчетаСчетовФактур.СуммаНДСКомиссия КАК СуммаНДСКомиссия,
	|	-ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУменьшениеКомиссия КАК СуммаПоСчетуФактуреРазницаУменьшениеКомиссия,
	|	-ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУвеличениеКомиссия КАК СуммаПоСчетуФактуреРазницаУвеличениеКомиссия,
	|	-ЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшениеКомиссия КАК СуммаНДСРазницаУменьшениеКомиссия,
	|	-ЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличениеКомиссия КАК СуммаНДСРазницаУвеличениеКомиссия,
	|	ЖурналУчетаСчетовФактур.КодВидаОперацииКомиссия КАК КодВидаОперацииКомиссия,
	|	ЖурналУчетаСчетовФактур.Субкомиссионер КАК Субкомиссионер,
	|	ЖурналУчетаСчетовФактур.КодВидаСделки КАК КодВидаСделки,
	|	ЖурналУчетаСчетовФактур.НомерСчетаФактурыПродавца КАК НомерСчетаФактурыПродавца,
	|	ЖурналУчетаСчетовФактур.ДатаСчетаФактурыПродавца КАК ДатаСчетаФактурыПродавца,
	|	ЖурналУчетаСчетовФактур.ИННПродавца КАК ИННПродавца,
	|	ЖурналУчетаСчетовФактур.КПППродавца КАК КПППродавца,
	|	ЖурналУчетаСчетовФактур.ИННСубкомиссионера КАК ИННСубкомиссионера,
	|	ЖурналУчетаСчетовФактур.КППСубкомиссионера КАК КППСубкомиссионера,
	|	ИСТИНА КАК Сторно,
	|	ЖурналУчетаСчетовФактур.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
	|	ЖурналУчетаСчетовФактур.СчетФактураПолученныйОтПродавца КАК СчетФактураПолученныйОтПродавца,
	|	ЖурналУчетаСчетовФактур.ИсправлениеСобственнойОшибки КАК ИсправлениеСобственнойОшибки,
	|	ЖурналУчетаСчетовФактур.Продавец КАК Продавец
	|ИЗ
	|	РегистрСведений.ЖурналУчетаСчетовФактур.СрезПоследних(
	|		&Период,
	|		НЕ Сторно
	|		И Регистратор <> &Ссылка
	|		И СчетФактура = &ИсправляемыйСчетФактура) КАК ЖурналУчетаСчетовФактур
	|
	|ГДЕ
	|	&Исправление
	|	И НЕ &РучнаяКорректировкаЖурналаСФ
	|	И (ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреКомиссия > 0
	|		ИЛИ ЖурналУчетаСчетовФактур.СуммаНДСКомиссия > 0)
	|";
#КонецОбласти
	
#Область ТекстВыставленныйСчетФактураНаАванс
	ТекстВыставленныйСчетФактураНаАванс =
	"ВЫБРАТЬ
	|	&Период                                    КАК Период,
	|	&Организация                               КАК Организация,
	|	&Покупатель                                КАК Контрагент,
	|	СчетФактураВыданныйАванс.Ссылка            КАК СчетФактура,
	|	ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ВыставленныеСчетаФактуры) КАК ЧастьЖурнала,
	|	&ДатаВыставления                           КАК ДатаВыставленияПолучения,
	|	ВЫБОР
	|		КОГДА &ВыставленВЭлектронномВиде
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ                                      КАК КодСпособаВыставленияПолучения,
	|	СчетФактураВыданныйАванс.КодВидаОперации   КАК КодВидаОперации,
	|	&НомерНаПечать                             КАК НомерСчетаФактуры,
	|	ВЫБОР КОГДА &Исправление ТОГДА
	|			&ДатаСчетаФактурыОснования
	|		ИНАЧЕ &Период
	|	КОНЕЦ                                      КАК ДатаСчетаФактуры,
	|	NULL КАК НомерКорректировочногоСчетаФактуры,
	|	NULL КАК ДатаКорректировочногоСчетаФактуры,
	|	ВЫБОР 
	|		КОГДА &Исправление ТОГДА
	|			&НомерИсправления
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО 
	|	КОНЕЦ                                      КАК НомерИсправления,
	|	ВЫБОР 
	|		КОГДА &Исправление ТОГДА
	|			&Период
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО 
	|	КОНЕЦ                                      КАК ДатаИсправления,
	|	NULL КАК НомерИсправленияКорректировочногоСчетаФактуры,
	|	NULL КАК ДатаИсправленияКорректировочногоСчетаФактуры,
	|	NULL КАК ИндексСтроки,
	|	&Валюта                                    КАК Валюта,
	|	СуммыДляЖурналаУчетаСчетовФактур.Сумма     КАК СуммаПоСчетуФактуре,
	|	СуммыДляЖурналаУчетаСчетовФактур.СуммаНДС  КАК СуммаНДС,
	|	0                                          КАК СуммаПоСчетуФактуреРазницаУменьшение,
	|	0                                          КАК СуммаПоСчетуФактуреРазницаУвеличение,
	|	0                                          КАК СуммаНДСРазницаУменьшение,
	|	0                                          КАК СуммаНДСРазницаУвеличение,
	|	ЛОЖЬ                                       КАК ПоСтавкеБезНДС,
	|	ЛОЖЬ                                       КАК СчетФактураНеВыставляется,
	|
	|	СчетФактураВыданныйАванс.КППКонтрагента    КАК КППКонтрагента,
	|	СчетФактураВыданныйАванс.ИННКонтрагента    КАК ИННКонтрагента,
	|	
	|	&Комиссионер                               КАК Посредник,
	|	NULL                                       КАК СчетФактураВыданныйПокупателю,
	|	СуммыДляЖурналаУчетаСчетовФактур.СуммаПоСчетуФактуреКомиссия КАК СуммаПоСчетуФактуреКомиссия,
	|	СуммыДляЖурналаУчетаСчетовФактур.СуммаНДСКомиссия  КАК СуммаНДСКомиссия,
	|	0                                          КАК СуммаПоСчетуФактуреРазницаУменьшениеКомиссия,
	|	0                                          КАК СуммаПоСчетуФактуреРазницаУвеличениеКомиссия,
	|	0                                          КАК СуммаНДСРазницаУменьшениеКомиссия,
	|	0                                          КАК СуммаНДСРазницаУвеличениеКомиссия,
	|	СуммыДляЖурналаУчетаСчетовФактур.КодВидаОперацииКомиссия КАК КодВидаОперацииКомиссия,
	|	NULL КАК Субкомиссионер,
	|	NULL КАК КодВидаСделки,
	|	СчетФактураПолученныйАванс.Номер КАК НомерСчетаФактурыПродавца,
	|	NULL КАК ДатаСчетаФактурыПродавца,
	|	СчетФактураПолученныйАванс.ИННКонтрагента КАК ИННПродавца,
	|	СчетФактураПолученныйАванс.КППКонтрагента КАК КПППродавца,
	|	NULL КАК ИННСубкомиссионера,
	|	NULL КАК КППСубкомиссионера,
	|	ЛОЖЬ КАК Сторно,
	|	NULL КАК ИсправленныйСчетФактура,
	|	NULL КАК СчетФактураПолученныйОтПродавца,
	|	NULL КАК ИсправлениеСобственнойОшибки,
	|	СчетФактураПолученныйАванс.Контрагент КАК Продавец
	|ИЗ
	|	Документ.СчетФактураВыданныйАванс КАК СчетФактураВыданныйАванс
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСуммыДляЖурналаУчетаСчетовФактур КАК СуммыДляЖурналаУчетаСчетовФактур
	|	ПО
	|		СчетФактураВыданныйАванс.Ссылка = СуммыДляЖурналаУчетаСчетовФактур.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.СчетФактураПолученныйАванс КАК СчетФактураПолученныйАванс
	|	ПО
	|		СчетФактураВыданныйАванс.ДокументОснование = СчетФактураПолученныйАванс.Ссылка
	|ГДЕ
	|	СчетФактураВыданныйАванс.Ссылка = &Ссылка
	|	И НЕ &РучнаяКорректировкаЖурналаСФ
	|	И &Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|
	|";
#КонецОбласти
	
#Область ТекстКорректировочныйСчетФактураНаАванс
	ТекстКорректировочныйСчетФактураНаАванс =
	"ВЫБРАТЬ
	|	&Период                                    КАК Период,
	|	&Организация                               КАК Организация,
	|	&Покупатель                                КАК Контрагент,
	|	СчетФактураВыданныйАванс.Ссылка            КАК СчетФактура,
	|	ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ВыставленныеСчетаФактуры) КАК ЧастьЖурнала,
	|	&ДатаВыставления                           КАК ДатаВыставленияПолучения,
	|	ВЫБОР
	|		КОГДА &ВыставленВЭлектронномВиде
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ                                      КАК КодСпособаВыставленияПолучения,
	|	СчетФактураВыданныйАванс.КодВидаОперации   КАК КодВидаОперации,
	|	СуммыДляЖурналаУчетаСчетовФактур.НомерСчетаФактуры КАК НомерСчетаФактуры,
	|	СуммыДляЖурналаУчетаСчетовФактур.ДатаСчетаФактуры  КАК ДатаСчетаФактуры,
	|	&НомерНаПечать                             КАК НомерКорректировочногоСчетаФактуры,
	|	ВЫБОР КОГДА &Исправление ТОГДА
	|			&ДатаСчетаФактурыОснования
	|		ИНАЧЕ &Период
	|	КОНЕЦ                                      КАК ДатаКорректировочногоСчетаФактуры,
	|	СуммыДляЖурналаУчетаСчетовФактур.НомерИсправления  КАК НомерИсправления,
	|	СуммыДляЖурналаУчетаСчетовФактур.ДатаИсправления   КАК ДатаИсправления,
	|	ВЫБОР 
	|		КОГДА &Исправление ТОГДА
	|			&НомерИсправления
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО 
	|	КОНЕЦ                                      КАК НомерИсправленияКорректировочногоСчетаФактуры,
	|	ВЫБОР 
	|		КОГДА &Исправление ТОГДА
	|			&Период
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО 
	|	КОНЕЦ                                      КАК ДатаИсправленияКорректировочногоСчетаФактуры,
	|	СуммыДляЖурналаУчетаСчетовФактур.ИндексСтроки КАК ИндексСтроки,
	|	&Валюта                                    КАК Валюта,
	|	СуммыДляЖурналаУчетаСчетовФактур.Сумма     КАК СуммаПоСчетуФактуре,
	|	СуммыДляЖурналаУчетаСчетовФактур.СуммаНДС  КАК СуммаНДС,
	|	0                                          КАК СуммаПоСчетуФактуреРазницаУменьшение,
	|	СуммыДляЖурналаУчетаСчетовФактур.Сумма     КАК СуммаПоСчетуФактуреРазницаУвеличение,
	|	0                                          КАК СуммаНДСРазницаУменьшение,
	|	СуммыДляЖурналаУчетаСчетовФактур.СуммаНДС  КАК СуммаНДСРазницаУвеличение,
	|	ЛОЖЬ                                       КАК ПоСтавкеБезНДС,
	|	ЛОЖЬ                                       КАК СчетФактураНеВыставляется,
	|
	|	СчетФактураВыданныйАванс.КППКонтрагента    КАК КППКонтрагента,
	|	СчетФактураВыданныйАванс.ИННКонтрагента    КАК ИННКонтрагента,
	|	
	|	NULL КАК Посредник,
	|	NULL КАК СчетФактураВыданныйПокупателю,
	|	СуммыДляЖурналаУчетаСчетовФактур.СуммаПоСчетуФактуреКомиссия КАК СуммаПоСчетуФактуреКомиссия,
	|	СуммыДляЖурналаУчетаСчетовФактур.СуммаНДСКомиссия  КАК СуммаНДСКомиссия,
	|	0                                          КАК СуммаПоСчетуФактуреРазницаУменьшениеКомиссия,
	|	СуммыДляЖурналаУчетаСчетовФактур.СуммаПоСчетуФактуреКомиссия КАК СуммаПоСчетуФактуреРазницаУвеличениеКомиссия,
	|	0                                          КАК СуммаНДСРазницаУменьшениеКомиссия,
	|	СуммыДляЖурналаУчетаСчетовФактур.СуммаНДСКомиссия КАК СуммаНДСРазницаУвеличениеКомиссия,
	|	СуммыДляЖурналаУчетаСчетовФактур.КодВидаОперацииКомиссия КАК КодВидаОперацииКомиссия,
	|	NULL КАК Субкомиссионер,
	|	NULL КАК КодВидаСделки,
	|	NULL КАК НомерСчетаФактурыПродавца,
	|	NULL КАК ДатаСчетаФактурыПродавца,
	|	NULL КАК ИННПродавца,
	|	NULL КАК КПППродавца,
	|	NULL КАК ИННСубкомиссионера,
	|	NULL КАК КППСубкомиссионера,
	|	ЛОЖЬ КАК Сторно,
	|	NULL КАК ИсправленныйСчетФактура,
	|	NULL КАК СчетФактураПолученныйОтПродавца,
	|	NULL КАК ИсправлениеСобственнойОшибки,
	|	NULL КАК Продавец
	|ИЗ
	|	Документ.СчетФактураВыданныйАванс КАК СчетФактураВыданныйАванс
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтИсходныеСчетаФактуры КАК СуммыДляЖурналаУчетаСчетовФактур
	|	ПО
	|		СчетФактураВыданныйАванс.Ссылка = СуммыДляЖурналаУчетаСчетовФактур.Ссылка
	|ГДЕ
	|	СчетФактураВыданныйАванс.Ссылка = &Ссылка
	|	И НЕ &РучнаяКорректировкаЖурналаСФ
	|	И &Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|
	|";
#КонецОбласти
	
	МассивТекстов = Новый Массив;
	МассивТекстов.Добавить(ТекстЗапросаРучнаяКорректировка);
	МассивТекстов.Добавить(ТекстЗапросаСторноИсправление);
	МассивТекстов.Добавить(ТекстВыставленныйСчетФактураНаАванс);
	МассивТекстов.Добавить(ТекстКорректировочныйСчетФактураНаАванс);
	ТекстЗапроса = СтрСоединить(МассивТекстов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаНДСЗаписиКнигиПродаж(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "НДСЗаписиКнигиПродаж";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Документы.СчетФактураВыданныйАванс.ИнициализироватьВтТаблицаАвансы(Запрос);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВЫБОР 
	|		КОГДА &Исправление 
	|			ТОГДА &Период
	|		ИНАЧЕ ТаблицаАвансы.Период
	|	КОНЕЦ                                                      КАК Период,
	|	ТаблицаАвансы.Организация                                  КАК Организация,
	|	ТаблицаАвансы.Покупатель                                   КАК Покупатель,
	|	ВЫБОР КОГДА ТаблицаАвансы.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя)
	|		ТОГДА &Ссылка
	|		ИНАЧЕ ТаблицаАвансы.СчетФактура
	|	КОНЕЦ                                                      КАК СчетФактура,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыПолученные)      КАК ВидЦенности,
	|	ТаблицаАвансы.СтавкаНДС.ПеречислениеСтавкаНДС              КАК СтавкаНДС,
	|	ТаблицаАвансы.ДатаОплаты                                   КАК ДатаОплаты,
	|	ТаблицаАвансы.ДокументОплаты                               КАК ДокументОплаты,
	|	ВЫБОР КОГДА ТаблицаАвансы.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.НалогИсчисляетПокупатель)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ПолученАванс)
	|	КОНЕЦ                                                      КАК Событие,
	|	ВЫБОР 
	|		КОГДА &Исправление
	|			ТОГДА &Период
	|		ИНАЧЕ ТаблицаАвансы.ДатаСобытия
	|	КОНЕЦ                                                      КАК ДатаСобытия,
	|	ВЫБОР
	|		КОГДА &Исправление И НАЧАЛОПЕРИОДА(&ДатаСчетаФактурыОснования, КВАРТАЛ) < НАЧАЛОПЕРИОДА(&Период, КВАРТАЛ)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                                      КАК ЗаписьДополнительногоЛиста,
	|	ВЫБОР
	|		КОГДА &Исправление И НАЧАЛОПЕРИОДА(&ДатаСчетаФактурыОснования, КВАРТАЛ) < НАЧАЛОПЕРИОДА(&Период, КВАРТАЛ)
	|			ТОГДА &ДатаСчетаФактурыОснования
	|	КОНЕЦ                                                      КАК КорректируемыйПериод,
	|	ЛОЖЬ                                                       КАК СторнирующаяЗаписьДопЛиста,
	|	НЕОПРЕДЕЛЕНО                                               КАК ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА &Исправление
	|			ТОГДА &Ссылка 
	|	КОНЕЦ                                                      КАК ИсправленныйСчетФактура,
	|	&Исправление                                               КАК Исправление,
	|	ТаблицаАвансы.СуммаБезНДС                                  КАК СуммаБезНДС,
	|	ТаблицаАвансы.НДС                                          КАК НДС,
	|	ВЫБОР
	|		КОГДА &ВводОстатковОтражатьВУУ
	|			ТОГДА ТаблицаАвансы.НДСУпр
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА ТаблицаАвансы.НДСУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ                                                      КАК НДСУпр,
	|	Значение(Перечисление.ХозяйственныеОперации.НачислениеНДССПолученногоАванса) КАК ХозяйственнаяОперация,
	|	ТаблицаАвансы.НаправлениеДеятельности                      КАК НаправлениеДеятельности,
	|	ТаблицаАвансы.НомерДокументаОплаты                         КАК НомерДокументаОплаты,
	|	ТаблицаАвансы.ДатаДокументаОплаты                          КАК ДатаДокументаОплаты
	|	
	|ИЗ
	|	ВтТаблицаАвансы КАК ТаблицаАвансы
	|	
	|ГДЕ
	|	ТаблицаАвансы.ОтражатьВУчетеНДС";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции


Процедура УстановитьПараметрИсправляемыйСчетФактура(Запрос)
	
	Если Запрос.Параметры.Свойство("ИсправляемыйСчетФактура") Тогда
		Возврат;
	КонецЕсли;

	Если Запрос.Параметры.Исправление Тогда
	
		СчетФактураПредыдущееИсправление = СчетФактураПредыдущееИсправление(
			Запрос.Параметры.СчетФактураОснование, 
			Запрос.Параметры.НомерИсправления);
		ИсправляемыйСчетФактура = ?(СчетФактураПредыдущееИсправление = Неопределено, Запрос.Параметры.СчетФактураОснование,
			СчетФактураПредыдущееИсправление);
	
	Иначе
		ИсправляемыйСчетФактура = Неопределено;
	КонецЕсли; 
	
	Запрос.УстановитьПараметр("ИсправляемыйСчетФактура", ИсправляемыйСчетФактура);

КонецПроцедуры

Процедура ИнициализироватьВтСуммыДляЖурналаУчетаСчетовФактур(Запрос)
	
	Если Запрос.Параметры.Свойство("ВтСуммыДляЖурналаУчетаСчетовФактурИнициализирована") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросВтСуммыДляЖурналаУчетаСчетовФактур = Новый Запрос;
	ЗапросВтСуммыДляЖурналаУчетаСчетовФактур.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	ЗапросВтСуммыДляЖурналаУчетаСчетовФактур.УстановитьПараметр("Ссылка", Запрос.Параметры.Ссылка);
	ЗапросВтСуммыДляЖурналаУчетаСчетовФактур.УстановитьПараметр("ТипыЗапасовСобственные", Запрос.Параметры.ТипыЗапасовСобственные);
	ЗапросВтСуммыДляЖурналаУчетаСчетовФактур.УстановитьПараметр("КодВидаОперацииКомиссия", Запрос.Параметры.КодВидаОперацииКомиссия);
	
	ЗапросВтСуммыДляЖурналаУчетаСчетовФактур.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаАвансы.Ссылка КАК Ссылка,
	|	СУММА(ТаблицаАвансы.Сумма) КАК Сумма,
	|	СУММА(ТаблицаАвансы.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВЫБОР КОГДА ТаблицаАвансы.ТипЗапасов В (&ТипыЗапасовСобственные)
	|			ТОГДА 0 
	|			ИНАЧЕ ТаблицаАвансы.Сумма
	|		КОНЕЦ)  КАК СуммаПоСчетуФактуреКомиссия,
	|	СУММА(ВЫБОР КОГДА ТаблицаАвансы.ТипЗапасов В (&ТипыЗапасовСобственные)
	|			ТОГДА 0 
	|			ИНАЧЕ ТаблицаАвансы.СуммаНДС
	|		КОНЕЦ) КАК СуммаНДСКомиссия,
	|	МАКСИМУМ(ВЫБОР
	|		КОГДА ТаблицаАвансы.ТипЗапасов В (&ТипыЗапасовСобственные)
	|			ТОГДА """"
	|		ИНАЧЕ &КодВидаОперацииКомиссия
	|	КОНЕЦ)  КАК КодВидаОперацииКомиссия
	|ПОМЕСТИТЬ ВтСуммыДляЖурналаУчетаСчетовФактур
	|ИЗ
	|	Документ.СчетФактураВыданныйАванс.Авансы КАК ТаблицаАвансы
	|ГДЕ
	|	ТаблицаАвансы.Ссылка = &Ссылка
	|	И НЕ ТаблицаАвансы.Ссылка.Корректировочный
	|	И ТИПЗНАЧЕНИЯ(ТаблицаАвансы.Ссылка.ДокументОснование) <> ТИП(Документ.ПервичныйДокумент)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаАвансы.Ссылка
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	СчетФактураВыданныйАванс.Ссылка КАК Ссылка,
	|	ПервичныйДокумент.СуммаРегл КАК Сумма,
	|	0                           КАК СуммаНДС,
	|	0                           КАК СуммаПоСчетуФактуреКомиссия,
	|	0                           КАК СуммаНДСКомиссия,
	|	""""                        КАК КодВидаОперацииКомиссия
	|ИЗ
	|	Документ.СчетФактураВыданныйАванс КАК СчетФактураВыданныйАванс
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПервичныйДокумент КАК ПервичныйДокумент
	|	ПО
	|		СчетФактураВыданныйАванс.ДокументОснование = ПервичныйДокумент.Ссылка 
	|ГДЕ
	|	СчетФактураВыданныйАванс.Ссылка = &Ссылка
	|	И НЕ СчетФактураВыданныйАванс.Корректировочный
	|";
	ЗапросВтСуммыДляЖурналаУчетаСчетовФактур.Выполнить();
	
	Запрос.УстановитьПараметр("ВтСуммыДляЖурналаУчетаСчетовФактурИнициализирована", Истина);

КонецПроцедуры

Процедура ИнициализироватьВтИсходныеСчетаФактуры(Запрос)
	
	Если Запрос.Параметры.Свойство("ВтИсходныеСчетаФактурыИнициализирована") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросВтИсходныеСчетаФактуры = Новый Запрос;
	ЗапросВтИсходныеСчетаФактуры.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	ЗапросВтИсходныеСчетаФактуры.УстановитьПараметр("Ссылка", Запрос.Параметры.Ссылка);
	ЗапросВтИсходныеСчетаФактуры.УстановитьПараметр("ТипыЗапасовСобственные", Запрос.Параметры.ТипыЗапасовСобственные);
	ЗапросВтИсходныеСчетаФактуры.УстановитьПараметр("КодВидаОперацииКомиссия", Запрос.Параметры.КодВидаОперацииКомиссия);
	
	ЗапросВтИсходныеСчетаФактуры.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаАвансы.Ссылка КАК Ссылка,
	|	0 КАК ИндексСтроки,
	|	СУММА(ТаблицаАвансы.Сумма) КАК Сумма,
	|	СУММА(ТаблицаАвансы.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаАвансы.ТипЗапасов В (&ТипыЗапасовСобственные)
	|				ТОГДА 0
	|			ИНАЧЕ ТаблицаАвансы.Сумма
	|		КОНЕЦ) КАК СуммаПоСчетуФактуреКомиссия,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаАвансы.ТипЗапасов В (&ТипыЗапасовСобственные)
	|				ТОГДА 0
	|			ИНАЧЕ ТаблицаАвансы.СуммаНДС
	|		КОНЕЦ) КАК СуммаНДСКомиссия,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ТаблицаАвансы.ТипЗапасов В (&ТипыЗапасовСобственные)
	|				ТОГДА """"
	|			ИНАЧЕ &КодВидаОперацииКомиссия
	|		КОНЕЦ) КАК КодВидаОперацииКомиссия,
	|	ТаблицаАвансы.ИсходныйСчетФактура КАК ИсходныйСчетФактура,
	|	ТаблицаАвансы.ИсходныйСчетФактура.Номер КАК НомерСчетаФактуры,
	|	ТаблицаАвансы.ИсходныйСчетФактура.Дата КАК ДатаСчетаФактуры,
	|	ТаблицаАвансы.ИсходныйСчетФактура.Исправление КАК Исправление,
	|	ТаблицаАвансы.ИсходныйСчетФактура.НомерИсправления КАК НомерИсправления,
	|	ДАТАВРЕМЯ(1,1,1) КАК ДатаИсправления,
	|	ТаблицаАвансы.ИсходныйСчетФактура.СчетФактураОснование.Номер КАК НомерСчетаФактурыОснования,
	|	ТаблицаАвансы.ИсходныйСчетФактура.СчетФактураОснование.Дата КАК ДатаСчетаФактурыОснования
	|ИЗ
	|	Документ.СчетФактураВыданныйАванс.Авансы КАК ТаблицаАвансы
	|ГДЕ
	|	ТаблицаАвансы.Ссылка = &Ссылка
	|	И ТаблицаАвансы.Ссылка.Корректировочный
	|	И ТИПЗНАЧЕНИЯ(ТаблицаАвансы.Ссылка.ДокументОснование) <> ТИП(Документ.ПервичныйДокумент)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаАвансы.Ссылка,
	|	ТаблицаАвансы.ИсходныйСчетФактура";
	ИсходныеСчетаФактуры = ЗапросВтИсходныеСчетаФактуры.Выполнить().Выгрузить();
	
	ОбособленноеПодразделение = Ложь;
	ЦифровойИндексОбособленногоПодразделения = 0;
	Если ЗначениеЗаполнено(Запрос.Параметры.Организация) Тогда
		РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
								Запрос.Параметры.Организация, "ОбособленноеПодразделение, ЦифровойИндексОбособленногоПодразделения");
		ОбособленноеПодразделение = РеквизитыОрганизации.ОбособленноеПодразделение;
		ЦифровойИндексОбособленногоПодразделения = РеквизитыОрганизации.ЦифровойИндексОбособленногоПодразделения;
	КонецЕсли;
	
	Для Каждого СтрокаСФ Из ИсходныеСчетаФактуры Цикл
		
		СтрокаСФ.НомерСчетаФактуры = УчетНДСПереопределяемый.НомерСчетаФактурыНаПечать(
					?(СтрокаСФ.Исправление, СтрокаСФ.НомерСчетаФактурыОснования, СтрокаСФ.НомерСчетаФактуры), 
					Ложь,
					ОбособленноеПодразделение, 
					ЦифровойИндексОбособленногоПодразделения);
		Если СтрокаСФ.Исправление Тогда
			СтрокаСФ.НомерИсправления = СтрокаСФ.НомерИсправления;
			СтрокаСФ.ДатаИсправления = СтрокаСФ.ДатаСчетаФактуры;
			СтрокаСФ.ДатаСчетаФактуры = СтрокаСФ.ДатаСчетаФактурыОснования;
		КонецЕсли;
		
		СтрокаСФ.ИндексСтроки = ИсходныеСчетаФактуры.Индекс(СтрокаСФ);
		
	КонецЦикла;
	ЗапросВтИсходныеСчетаФактуры.УстановитьПараметр("ИсходныеСчетаФактуры", ИсходныеСчетаФактуры);
	
	ЗапросВтИсходныеСчетаФактуры.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаАвансы.Ссылка КАК Ссылка,
	|	ТаблицаАвансы.ИндексСтроки КАК ИндексСтроки,
	|	ТаблицаАвансы.Сумма КАК Сумма,
	|	ТаблицаАвансы.СуммаНДС КАК СуммаНДС,
	|	ТаблицаАвансы.СуммаПоСчетуФактуреКомиссия КАК СуммаПоСчетуФактуреКомиссия,
	|	ТаблицаАвансы.СуммаНДСКомиссия КАК СуммаНДСКомиссия,
	|	ТаблицаАвансы.КодВидаОперацииКомиссия КАК КодВидаОперацииКомиссия,
	|	ТаблицаАвансы.НомерСчетаФактуры КАК НомерСчетаФактуры,
	|	ТаблицаАвансы.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
	|	ТаблицаАвансы.Исправление КАК Исправление,
	|	ТаблицаАвансы.НомерИсправления КАК НомерИсправления,
	|	ТаблицаАвансы.ДатаИсправления КАК ДатаИсправления,
	|	ТаблицаАвансы.НомерСчетаФактурыОснования КАК НомерСчетаФактурыОснования,
	|	ТаблицаАвансы.ДатаСчетаФактурыОснования КАК ДатаСчетаФактурыОснования
	|ПОМЕСТИТЬ ВтИсходныеСчетаФактуры
	|ИЗ
	|	&ИсходныеСчетаФактуры КАК ТаблицаАвансы";
	
	ЗапросВтИсходныеСчетаФактуры.Выполнить();
	
	
	Запрос.УстановитьПараметр("ВтИсходныеСчетаФактурыИнициализирована", Истина);

КонецПроцедуры

Функция СчетФактураПредыдущееИсправление(СчетФактураОснование, НомерИсправления)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СчетФактура.Ссылка КАК ИсправленныйСчетФактура,
	|	СчетФактура.НомерИсправления КАК НомерИсправления
	|ИЗ
	|	Документ.СчетФактураВыданныйАванс КАК СчетФактура
	|ГДЕ
	|	СчетФактура.СчетФактураОснование = &СчетФактураОснование
	|	И СчетФактура.Проведен
	|	И СчетФактура.Исправление
	|	И СчетФактура.НомерИсправления <> &НомерИсправления
	|";
	
	Запрос.УстановитьПараметр("СчетФактураОснование", СчетФактураОснование);
	Запрос.УстановитьПараметр("НомерИсправления", НомерИсправления);
	
	ПредыдущийНомер = 0;
	ИсправленныйСчетФактура = Неопределено;
	НомерИсправленияЧислом = ?(ЗначениеЗаполнено(НомерИсправления),Число(НомерИсправления),1);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НомерИзВыборки = Число(Выборка.НомерИсправления);
		Если НомерИзВыборки < НомерИсправленияЧислом
			И НомерИзВыборки > ПредыдущийНомер Тогда
			ПредыдущийНомер = НомерИзВыборки;
			ИсправленныйСчетФактура = Выборка.ИсправленныйСчетФактура;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИсправленныйСчетФактура
	
КонецФункции

#КонецОбласти

Функция ПолучитьДанныеДляПечатнойФормыСчетФактура(ПараметрыПечати, МассивОбъектов) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	Запрос.УстановитьПараметр("ПредставлениеСчетФактура", НСтр("ru = 'счет-фактура'"));
	Запрос.УстановитьПараметр("ПредставлениеПредварительнаяОплата", НСтр("ru = 'Предварительная оплата'"));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СчетФактураВыданныйАванс.Ссылка КАК Ссылка,
	|	СчетФактураВыданныйАванс.Ссылка КАК ИсходныйСчетФактура,
	|	ЛОЖЬ КАК Корректировочный
	|ПОМЕСТИТЬ СчетаФактуры
	|ИЗ
	|	Документ.СчетФактураВыданныйАванс КАК СчетФактураВыданныйАванс
	|ГДЕ
	|	СчетФактураВыданныйАванс.Ссылка В (&МассивДокументов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ИсходныйСчетФактура,
	|	ИСТИНА КАК Корректировочный
	|ИЗ
	|	Документ.СчетФактураВыданныйАванс.Авансы КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&МассивДокументов)
	|	И ТаблицаДокумента.Ссылка.Корректировочный
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаФактуры.Ссылка КАК Ссылка,
	|	СчетФактураВыданныйАванс.Организация КАК Организация,
	|	СчетФактураВыданныйАванс.Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|	СчетФактураВыданныйАванс.Дата КАК Период
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	СчетаФактуры КАК СчетаФактуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданныйАванс КАК СчетФактураВыданныйАванс
	|		ПО СчетаФактуры.Ссылка = СчетФактураВыданныйАванс.Ссылка
	|		AND СчетаФактуры.ИсходныйСчетФактура = СчетФактураВыданныйАванс.Ссылка";
	
	Запрос.Выполнить();
	
	// Получим КПП организации с учетом истории изменений.
	Обработки.ПечатьОбщихФорм.ПоместитьВременнуюТаблицуДанныхПоставщика(МенеджерВременныхТаблиц);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаОснований.Ссылка                               КАК Ссылка,
	|	ТаблицаОснований.ИсходныйСчетФактура                  КАК ДокументОснование,
	|	ТаблицаОснований.ИсходныйСчетФактура                  КАК ИсходныйДокумент,
	|	ТаблицаОснований.ИсходныйСчетФактура.Номер КАК НомерСчетаФактуры,
	|	ВЫБОР КОГДА ТаблицаОснований.ИсходныйСчетФактура.Исправление ТОГДА
	|		ЕСТЬNULL(ТаблицаОснований.ИсходныйСчетФактура.СчетФактураОснование.Дата, ДАТАВРЕМЯ(1,1,1))
	|	ИНАЧЕ 
	|		ТаблицаОснований.ИсходныйСчетФактура.Дата
	|	КОНЕЦ КАК ДатаСчетаФактуры,
	|	ВЫБОР КОГДА ТаблицаОснований.ИсходныйСчетФактура.Исправление ТОГДА
	|		ТаблицаОснований.ИсходныйСчетФактура.НомерИсправления 
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НомерИсправленияСчетаФактуры,
	|	ВЫБОР КОГДА ТаблицаОснований.ИсходныйСчетФактура.Исправление ТОГДА
	|		ТаблицаОснований.ИсходныйСчетФактура.Дата
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ДатаИсправленияСчетаФактуры
	|ИЗ
	|	Документ.СчетФактураВыданныйАванс.Авансы КАК ТаблицаОснований
	|ГДЕ
	|	ТаблицаОснований.Ссылка В(&МассивДокументов)
	|	И ТаблицаОснований.Ссылка.Корректировочный
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	ИсходныйДокумент
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                 КАК Ссылка,
	|	ВариантыКомплектацииНоменклатуры.Ссылка КАК ВариантКомплектацииНоменклатуры,
	|	ВариантыКомплектацииНоменклатуры.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ВариантыКомплектацииНоменклатуры.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	ТаблицаДокумента.НоменклатураНабора КАК НоменклатураНабора,
	|	ТаблицаДокумента.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ТаблицаДокумента.НомерСтроки                            КАК НомерСтроки,
	|	ТаблицаДокумента.Номенклатура                           КАК Номенклатура,
	|	ТаблицаДокумента.Характеристика                         КАК Характеристика,
	|	ТаблицаДокумента.Содержание                         	КАК Содержание,
	|	ТаблицаДокумента.Сумма                              	КАК Сумма,
	|	ТаблицаДокумента.СтавкаНДС                              КАК СтавкаНДС,
	|	ТаблицаДокумента.СуммаНДС                              	КАК СуммаНДС,
	|	
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Номенклатура.ТипНоменклатуры В
	|				(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ 													КАК ЭтоТовар,
	|	ЛОЖЬ                                                    КАК ЭтоНеВозвратнаяТара,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)  КАК Упаковка
	|
	|ПОМЕСТИТЬ РеализацияТоваровУслугТаблицаТоваров
	|ИЗ
	|	Документ.СчетФактураВыданныйАванс.Авансы КАК ТаблицаДокумента
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
	|		ПО ВариантыКомплектацииНоменклатуры.Владелец = ТаблицаДокумента.НоменклатураНабора
	|		И ВариантыКомплектацииНоменклатуры.Характеристика = ТаблицаДокумента.ХарактеристикаНабора
	|		И ВариантыКомплектацииНоменклатуры.Основной
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В(ВЫБРАТЬ Т.ИсходныйСчетФактура ИЗ СчетаФактуры КАК Т)
	|	И ТаблицаДокумента.Ссылка.Проведен
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА НЕ ТаблицаТоваров.ЭтоТовар
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьУслуги,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ТаблицаТоваров.ЭтоТовар
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьТовары,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(ТаблицаТоваров.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьПрослеживаемыеТовары
	|ПОМЕСТИТЬ
	|	НоменклатураДокументов
	|ИЗ
	|	РеализацияТоваровУслугТаблицаТоваров КАК ТаблицаТоваров
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактура.Ссылка КАК Ссылка,
	|	СчетФактура.НалогообложениеНДС КАК НалогообложениеНДС,
	|	СчетФактура.ИдентификаторГосКонтракта КАК ИдентификаторГосКонтракта,
	|	&ПредставлениеСчетФактура КАК ПредставлениеДокумента,
	|	СчетФактура.Номер КАК Номер,
	|	ВЫБОР КОГДА СчетФактура.Исправление ТОГДА
	|		ЕСТЬNULL(СчетФактура.СчетФактураОснование.Дата, ДАТАВРЕМЯ(1,1,1))
	|	ИНАЧЕ 
	|		СчетФактура.Дата
	|	КОНЕЦ КАК Дата,
	|	ВЫБОР КОГДА СчетФактура.Исправление ТОГДА
	|		СчетФактура.НомерИсправления 
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НомерИсправления,
	|	ВЫБОР КОГДА СчетФактура.Исправление ТОГДА
	|		СчетФактура.Дата
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ДатаИсправления,
	|	СчетФактура.Исправление КАК Исправление,
	|	НЕОПРЕДЕЛЕНО КАК НомерСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправленияСчетаФактуры,
	|	СчетФактура.Корректировочный КАК КорректировочныйСчетФактура,
	|	СчетФактура.СтрокаПлатежноРасчетныеДокументы КАК СтрокаПоДокументу,
	|	СчетФактура.Организация.ВалютаРегламентированногоУчета КАК ВалютаСчетаФактуры,
	|	ВЫБОР
	|		КОГДА СчетФактура.Контрагент.ОбособленноеПодразделение
	|			ТОГДА СчетФактура.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ СчетФактура.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР
	|		КОГДА СчетФактура.Организация.ОбособленноеПодразделение
	|			ТОГДА СчетФактура.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ СчетФактура.Организация
	|	КОНЕЦ КАК Организация,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	СчетФактура.Организация.Префикс КАК Префикс,
	|
	|	ЕСТЬNULL(ДанныеПоставщика.ИндексПодразделения, 0) КАК ИндексПодразделения,
	|
	|	СчетФактура.Контрагент КАК Грузополучатель,
	|	СчетФактура.ДокументОснование.Номер КАК НомерПлатежноРасчетногоДокумента,
	|	СчетФактура.ДокументОснование.Дата КАК ДатаПлатежноРасчетногоДокумента,
	|	СчетФактура.Организация КАК Грузоотправитель,
	|
	|	ДанныеПоставщика.КПППоставщика КАК КПППоставщика,
	|
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетОрганизации,
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетКонтрагента,
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетГрузоотправителя,
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетГрузополучателя,
	|
	|	ВЫБОР
	|		КОГДА СчетФактура.КППКонтрагента ПОДОБНО """" ТОГДА
	|			ВЫБОР 
	|				КОГДА СчетФактура.Контрагент ССЫЛКА Справочник.Контрагенты ТОГДА 
	|					""""
	|				КОГДА СчетФактура.Контрагент ССЫЛКА Справочник.Организации 
	|						И НЕ ДанныеОрганизацийПокупатель.Ссылка ЕСТЬ NULL ТОГДА 
	|							ВЫБОР
	|								КОГДА ДанныеОрганизацийПокупатель.КрупнейшийНалогоплательщик ТОГДА
	|									ДанныеОрганизацийПокупатель.КрупнейшийНалогоплательщикКПП
	|								ИНАЧЕ
	|									ДанныеОрганизацийПокупатель.КПП
	|								КОНЕЦ
	|				ИНАЧЕ 
	|					""""
	|				КОНЕЦ
	|		ИНАЧЕ
	|			СчетФактура.КППКонтрагента
	|		КОНЕЦ КАК КПППокупателя,
	|
	|	ВЫБОР 
	|		КОГДА СчетФактура.ИННКонтрагента ПОДОБНО """" ТОГДА
	|			ВЫБОР 
	|				КОГДА СчетФактура.Контрагент ССЫЛКА Справочник.Контрагенты ТОГДА 
	|					ЕстьNULL(ДанныеКонтрагента.ИНН, """")
	|				КОГДА СчетФактура.Контрагент ССЫЛКА Справочник.Организации ТОГДА 
	|					ЕстьNULL(ДанныеОрганизацийПокупатель.ИНН, """")
	|				ИНАЧЕ 
	|					""""
	|				КОНЕЦ
	|		ИНАЧЕ
	|			СчетФактура.ИННКонтрагента
	|		КОНЕЦ КАК ИННПокупателя,
	|
	|	НЕОПРЕДЕЛЕНО КАК АдресДоставки,
	|	СчетФактура.Организация.ВалютаРегламентированногоУчета КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаНаименованиеПолное,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКод,
	|	ВЫБОР
	|		КОГДА НоменклатураДокументов.ЕстьУслуги
	|				И НЕ НоменклатураДокументов.ЕстьТовары
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТолькоУслуги,
	|	НоменклатураДокументов.ЕстьПрослеживаемыеТовары КАК ЕстьПрослеживаемыеТовары,
	|	ЛОЖЬ КАК ЭтоПередачаНаКомиссию,
	|	НЕОПРЕДЕЛЕНО КАК ПредставлениеВыставленКомиссионеру,
	|	ВЫБОР
	|		КОГДА СчетФактура.ДокументОснование ССЫЛКА Документ.СчетФактураПолученныйАванс ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПеревыставленныйСчетФактура
	|ИЗ
	|	Документ.СчетФактураВыданныйАванс КАК СчетФактура
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураДокументов КАК НоменклатураДокументов
	|		ПО СчетФактура.Ссылка = НоменклатураДокументов.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО СчетФактура.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ДанныеОрганизацийПокупатель
	|		ПО ДанныеОрганизацийПокупатель.Ссылка = СчетФактура.Контрагент
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК ДанныеКонтрагента
	|		ПО ДанныеКонтрагента.Ссылка = СчетФактура.Контрагент
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоставщика КАК ДанныеПоставщика
	|		ПО СчетФактура.Ссылка = ДанныеПоставщика.Ссылка
	|ГДЕ
	|	СчетФактура.Ссылка В(&МассивДокументов)
	|	И СчетФактура.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка                КАК Ссылка,
	|	ТаблицаТоваров.НоменклатураНабора    КАК НоменклатураНабора,
	|	ТаблицаТоваров.ХарактеристикаНабора  КАК ХарактеристикаНабора,
	|	МИНИМУМ(ТаблицаТоваров.НомерСтроки)  КАК НомерСтроки,
	|	СУММА(ТаблицаТоваров.Сумма)    		 КАК Сумма,
	|	СУММА(ТаблицаТоваров.СуммаНДС)       КАК СуммаНДС
	|ПОМЕСТИТЬ ВременнаяТаблицаНаборыПодготовка
	|ИЗ
	|	РеализацияТоваровУслугТаблицаТоваров КАК ТаблицаТоваров
	|
	|ГДЕ
	|	ТаблицаТоваров.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.НоменклатураНабора,
	|	ТаблицаТоваров.ХарактеристикаНабора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Ссылка                                    КАК Ссылка,
	|	Товары.ВариантКомплектацииНоменклатуры           КАК ВариантКомплектацииНоменклатуры,
	|	Товары.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	Товары.НоменклатураНабора,
	|	Товары.ХарактеристикаНабора,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	ВЫБОР КОГДА Товары.ВариантКомплектацииНоменклатуры.НоменклатураОсновногоКомпонента = Товары.Номенклатура
	|		И Товары.ВариантКомплектацииНоменклатуры.ХарактеристикаОсновногоКомпонента = Товары.Характеристика ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОсновнаяКомплектующая,
	|	Товары.СтавкаНДС КАК СтавкаНДС
	|ПОМЕСТИТЬ ВременнаяТаблицаНаборыДополнительноЧастьПервая
	|ИЗ
	|	РеализацияТоваровУслугТаблицаТоваров КАК Товары
	|
	|ГДЕ
	|	Товары.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Ссылка                                                                                КАК Ссылка,
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка                                           КАК ВариантКомплектацииНоменклатуры,
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка.Владелец                                  КАК НоменклатураНабора,
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка.Характеристика                            КАК ХарактеристикаНабора,
	|	ВариантыКомплектацииНоменклатурыТовары.Номенклатура   КАК Номенклатура,
	|	ВариантыКомплектацииНоменклатурыТовары.Характеристика КАК Характеристика,
	|	ЛОЖЬ КАК ОсновнаяКомплектующая,
	|	NULL КАК СтавкаНДС
	|ИЗ
	|	Справочник.ВариантыКомплектацииНоменклатуры.Товары КАК ВариантыКомплектацииНоменклатурыТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ Т.Ссылка ИЗ РеализацияТоваровУслугТаблицаТоваров КАК Т) КАК Т
	|		ПО ИСТИНА
	|ГДЕ
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка В (ВЫБРАТЬ РАЗЛИЧНЫЕ Т.ВариантКомплектацииНоменклатуры ИЗ РеализацияТоваровУслугТаблицаТоваров КАК Т)
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Ссылка,
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка,
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка.Владелец,
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка.Характеристика,
	|	ВариантыКомплектацииНоменклатурыТовары.Номенклатура,
	|	ВариантыКомплектацииНоменклатурыТовары.Характеристика,
	|	ВариантыКомплектацииНоменклатурыТовары.Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Ссылка,
	|	Таблица.ВариантКомплектацииНоменклатуры,
	|	Таблица.ВариантПредставленияНабораВПечатныхФормах,
	|	Таблица.НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	МАКСИМУМ(Таблица.СтавкаНДС) КАК СтавкаНДС,
	|	МАКСИМУМ(Таблица.ОсновнаяКомплектующая) КАК ОсновнаяКомплектующая
	|ПОМЕСТИТЬ ВременнаяТаблицаНаборыДополнительноЧастьВторая
	|ИЗ
	|	ВременнаяТаблицаНаборыДополнительноЧастьПервая КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Ссылка,
	|	Таблица.ВариантКомплектацииНоменклатуры,
	|	Таблица.ВариантПредставленияНабораВПечатныхФормах,
	|	Таблица.НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Результат.Ссылка,
	|	Результат.ВариантКомплектацииНоменклатуры,
	|	Результат.ВариантПредставленияНабораВПечатныхФормах,
	|	Результат.НоменклатураНабора,
	|	Результат.ХарактеристикаНабора,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА Результат.ОсновнаяКомплектующая
	|				ТОГДА Результат.СтавкаНДС
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК СтавкаНДС
	|ПОМЕСТИТЬ ВременнаяТаблицаНаборыДополнительно
	|ИЗ
	|	ВременнаяТаблицаНаборыДополнительноЧастьВторая КАК Результат
	|СГРУППИРОВАТЬ ПО
	|	Результат.Ссылка,
	|	Результат.ВариантКомплектацииНоменклатуры,
	|	Результат.ВариантПредставленияНабораВПечатныхФормах,
	|	Результат.НоменклатураНабора,
	|	Результат.ХарактеристикаНабора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаНаборыДополнительно.ВариантКомплектацииНоменклатуры,
	|	ВременнаяТаблицаНаборыДополнительно.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|
	|	Таблица.Ссылка                            КАК Ссылка,
	|	Таблица.НоменклатураНабора                КАК НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора              КАК ХарактеристикаНабора,
	|	Таблица.НомерСтроки                       КАК НомерСтроки,
	|	ИСТИНА 										КАК ПолныйНабор,
	|	Таблица.Сумма                             КАК Сумма,
	|	Таблица.СуммаНДС                          КАК СуммаНДС,
	|	ВременнаяТаблицаНаборыДополнительно.СтавкаНДС КАК СтавкаНДС
	|ПОМЕСТИТЬ ВременнаяТаблицаНаборы
	|ИЗ
	|	ВременнаяТаблицаНаборыПодготовка КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНаборыДополнительно КАК ВременнаяТаблицаНаборыДополнительно
	|		ПО Таблица.НоменклатураНабора = ВременнаяТаблицаНаборыДополнительно.НоменклатураНабора
	|		И Таблица.ХарактеристикаНабора = ВременнаяТаблицаНаборыДополнительно.ХарактеристикаНабора
	|		И Таблица.Ссылка = ВременнаяТаблицаНаборыДополнительно.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ПустаяСсылка) КАК ВариантРасчетаЦеныНабора,
	|	ТаблицаДокумента.НоменклатураНабора КАК НоменклатураНабора,
	|	ТаблицаДокумента.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ТаблицаДокумента.ЭтоКомплектующие КАК ЭтоКомплектующие,
	|	ТаблицаДокумента.ЭтоНабор КАК ЭтоНабор,
	|	ТаблицаДокумента.ПолныйНабор КАК ПолныйНабор,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(ТаблицаДокумента.Содержание КАК СТРОКА(1))) <> """"
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаДокумента.Содержание КАК СТРОКА(1000))
	|		КОГДА ТаблицаДокумента.ЭтоНабор
	|			ТОГДА ТаблицаДокумента.Номенклатура.НаименованиеПолное
	|		КОГДА ТаблицаДокумента.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ &ПредставлениеПредварительнаяОплата
	|	КОНЕЦ КАК НоменклатураНаименование,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	"""" КАК НомерГТД,
	|	"""" КАК СтранаПроисхождения,
	|	"""" КАК СтранаПроисхожденияКод,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК ЕдиницаИзмерения,
	|	0 КАК Количество,
	|	0 КАК КоличествоПоРНПТ,
	|	0 КАК Цена,
	|	0 КАК СуммаБезНДС,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	ТаблицаДокумента.Сумма КАК СуммаСНДС,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.НомерСтрокиНаборы КАК НомерСтрокиНаборы,
	|	ЛОЖЬ КАК ЭтоВозвратнаяТара
	|ПОМЕСТИТЬ РезультатПоТабличнойЧасти
	|ИЗ
	|(
	|	ВЫБРАТЬ
	|		ТаблицаТоваров.Ссылка,
	|
	|		ВЫБОР КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0 ТОГДА
	|			ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ПустаяСсылка)
	|		КОНЕЦ КАК ВариантПредставленияНабораВПечатныхФормах,
	|
	|		ТаблицаТоваров.НоменклатураНабора,
	|		ТаблицаТоваров.ХарактеристикаНабора,
	|		ВЫБОР КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0 ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|		КОНЕЦ КАК ЭтоКомплектующие,
	|		ЛОЖЬ КАК ЭтоНабор,
	|		ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|		ВЫБОР КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0 ТОГДА
	|			ВременнаяТаблицаНаборы.НомерСтроки
	|		ИНАЧЕ
	|			ТаблицаТоваров.НомерСтроки
	|		КОНЕЦ КАК НомерСтрокиНаборы,
	|		ВЫБОР КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0 ТОГДА
	|			ВременнаяТаблицаНаборы.ПолныйНабор
	|		ИНАЧЕ
	|			ЛОЖЬ
	|		КОНЕЦ КАК ПолныйНабор,
	|		ТаблицаТоваров.Номенклатура,
	|		ТаблицаТоваров.Содержание,
	|		ТаблицаТоваров.СтавкаНДС,
	|		ТаблицаТоваров.Сумма,
	|		ТаблицаТоваров.СуммаНДС,
	|		ТаблицаТоваров.Характеристика,
	|		ТаблицаТоваров.Упаковка
	|	ИЗ
	|		РеализацияТоваровУслугТаблицаТоваров КАК ТаблицаТоваров
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНаборы КАК ВременнаяТаблицаНаборы
	|			ПО ВременнаяТаблицаНаборы.НоменклатураНабора = ТаблицаТоваров.НоменклатураНабора
	|			 И ВременнаяТаблицаНаборы.ХарактеристикаНабора = ТаблицаТоваров.ХарактеристикаНабора
	|			 И ВременнаяТаблицаНаборы.Ссылка = ТаблицаТоваров.Ссылка
	|
	|	ГДЕ
	|		ТаблицаТоваров.НоменклатураНабора = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|		ИЛИ (ТаблицаТоваров.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	        И ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах В (ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ТолькоКомплектующие),
	|	                                                                			  ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.НаборИКомплектующие)))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВременнаяТаблицаНаборы.Ссылка,
	|		ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах,
	|		ВременнаяТаблицаНаборы.НоменклатураНабора,
	|		ВременнаяТаблицаНаборы.ХарактеристикаНабора,
	|		ЛОЖЬ КАК ЭтоКомплектующие,
	|		ИСТИНА КАК ЭтоНабор,
	|		0 КАК НомерСтроки,
	|		ВременнаяТаблицаНаборы.НомерСтроки КАК НомерСтрокиНаборы,
	|		ВременнаяТаблицаНаборы.ПолныйНабор КАК ПолныйНабор,
	|		ВременнаяТаблицаНаборы.НоменклатураНабора,
	|		"""",
	|		ВременнаяТаблицаНаборы.СтавкаНДС,
	|		ВременнаяТаблицаНаборы.Сумма,
	|		ВременнаяТаблицаНаборы.СуммаНДС,
	|		ВременнаяТаблицаНаборы.ХарактеристикаНабора,
	|		ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка
	|	ИЗ
	|		ВременнаяТаблицаНаборы КАК ВременнаяТаблицаНаборы
	|	ГДЕ
	|		ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах В (ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ТолькоНабор),
	|	                                                                        ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.НаборИКомплектующие))
	|) КАК ТаблицаДокумента
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка,
	|	Таблица.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	Таблица.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	Таблица.НоменклатураНабора КАК НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	Таблица.ЭтоКомплектующие КАК ЭтоКомплектующие,
	|	Таблица.ЭтоНабор КАК ЭтоНабор,
	|	Таблица.ПолныйНабор КАК ПолныйНабор,
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.НоменклатураНаименование КАК НоменклатураНаименование,
	|	Таблица.Характеристика КАК Характеристика,
	|	Таблица.ХарактеристикаНаименование КАК ХарактеристикаНаименование,
	|	Таблица.НомерГТД КАК НомерГТД,
	|	Таблица.СтранаПроисхождения КАК СтранаПроисхождения,
	|	Таблица.СтранаПроисхожденияКод КАК СтранаПроисхожденияКод,
	|	МАКСИМУМ(Таблица.СтавкаНДСДо) КАК СтавкаНДСДо,
	|	МАКСИМУМ(Таблица.СтавкаНДС) КАК СтавкаНДС,
	|	Таблица.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Таблица.Количество КАК Количество,
	|	Таблица.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	Таблица.Цена КАК Цена,
	|	СУММА(Таблица.СуммаБезНДС) КАК СуммаБезНДС,
	|	СУММА(Таблица.СуммаНДС) КАК СуммаНДС,
	|	СУММА(Таблица.СуммаНДСДо) КАК СуммаНДСДо,
	|	СУММА(Таблица.СуммаНДСУвеличение) КАК РазницаНДСУвеличение,
	|	СУММА(Таблица.СуммаСНДС) КАК СуммаСНДС,
	|	СУММА(Таблица.СуммаСНДСДо) КАК СуммаСНДСДо,
	|	СУММА(Таблица.СуммаСНДСУвеличение) КАК РазницаСНДСУвеличение,
	|	МАКСИМУМ(Таблица.НомерСтроки) КАК НомерСтроки,
	|	МАКСИМУМ(Таблица.НомерСтрокиНаборы) КАК НомерСтрокиНаборы,
	|	Таблица.ЭтоВозвратнаяТара КАК ЭтоВозвратнаяТара,
	|	0 КАК КоличествоПоРНПТУвеличение,
	|	0 КАК КоличествоПоРНПТУменьшение
	|ИЗ
	|	(ВЫБРАТЬ
	|		КорректировочныеСФ.Ссылка КАК Ссылка,
	|		ТаблицаДо.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|		ТаблицаДо.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|		ТаблицаДо.НоменклатураНабора КАК НоменклатураНабора,
	|		ТаблицаДо.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|		ТаблицаДо.ЭтоКомплектующие КАК ЭтоКомплектующие,
	|		ТаблицаДо.ЭтоНабор КАК ЭтоНабор,
	|		ТаблицаДо.ПолныйНабор КАК ПолныйНабор,
	|		ТаблицаДо.Номенклатура КАК Номенклатура,
	|		ТаблицаДо.НоменклатураНаименование КАК НоменклатураНаименование,
	|		ТаблицаДо.Характеристика КАК Характеристика,
	|		ТаблицаДо.ХарактеристикаНаименование КАК ХарактеристикаНаименование,
	|		ТаблицаДо.НомерГТД КАК НомерГТД,
	|		ТаблицаДо.СтранаПроисхождения КАК СтранаПроисхождения,
	|		ТаблицаДо.СтранаПроисхожденияКод КАК СтранаПроисхожденияКод,
	|		ТаблицаДо.СтавкаНДС КАК СтавкаНДСДо,
	|		НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
	|		ТаблицаДо.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ТаблицаДо.Количество КАК Количество,
	|		ТаблицаДо.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|		ТаблицаДо.Цена КАК Цена,
	|		ТаблицаДо.СуммаБезНДС КАК СуммаБезНДС,
	|		0 КАК СуммаНДС,
	|		ТаблицаДо.СуммаНДС КАК СуммаНДСДо,
	|		0 КАК СуммаНДСУвеличение,
	|		0 КАК СуммаСНДС,
	|		ТаблицаДо.СуммаСНДС КАК СуммаСНДСДо,
	|		0 КАК СуммаСНДСУвеличение,
	|		ТаблицаДо.НомерСтроки КАК НомерСтроки,
	|		ТаблицаДо.НомерСтрокиНаборы КАК НомерСтрокиНаборы,
	|		ТаблицаДо.ЭтоВозвратнаяТара КАК ЭтоВозвратнаяТара
	|	ИЗ
	|		РезультатПоТабличнойЧасти КАК ТаблицаДо
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СчетаФактуры КАК КорректировочныеСФ
	|			ПО КорректировочныеСФ.ИсходныйСчетФактура = ТаблицаДо.Ссылка
	|				И КорректировочныеСФ.Корректировочный
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПосле.Ссылка,
	|		ТаблицаПосле.ВариантПредставленияНабораВПечатныхФормах,
	|		ТаблицаПосле.ВариантРасчетаЦеныНабора,
	|		ТаблицаПосле.НоменклатураНабора,
	|		ТаблицаПосле.ХарактеристикаНабора,
	|		ТаблицаПосле.ЭтоКомплектующие,
	|		ТаблицаПосле.ЭтоНабор,
	|		ТаблицаПосле.ПолныйНабор,
	|		ТаблицаПосле.Номенклатура,
	|		ТаблицаПосле.НоменклатураНаименование,
	|		ТаблицаПосле.Характеристика,
	|		ТаблицаПосле.ХарактеристикаНаименование,
	|		ТаблицаПосле.НомерГТД,
	|		ТаблицаПосле.СтранаПроисхождения,
	|		ТаблицаПосле.СтранаПроисхожденияКод,
	|		НЕОПРЕДЕЛЕНО,
	|		ТаблицаПосле.СтавкаНДС,
	|		ТаблицаПосле.ЕдиницаИзмерения,
	|		ТаблицаПосле.Количество,
	|		ТаблицаПосле.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|		ТаблицаПосле.Цена,
	|		ТаблицаПосле.СуммаБезНДС,
	|		0,
	|		0,
	|		ТаблицаПосле.СуммаНДС,
	|		0,
	|		0,
	|		ТаблицаПосле.СуммаСНДС,
	|		ТаблицаПосле.НомерСтроки,
	|		ТаблицаПосле.НомерСтрокиНаборы,
	|		ТаблицаПосле.ЭтоВозвратнаяТара
	|	ИЗ
	|		РезультатПоТабличнойЧасти КАК ТаблицаПосле
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СчетаФактуры КАК КорректировочныеСФ
	|			ПО КорректировочныеСФ.ИсходныйСчетФактура = ТаблицаПосле.Ссылка
	|				И НЕ КорректировочныеСФ.Корректировочный
	|	ГДЕ
	|		ТаблицаПосле.Ссылка.Корректировочный
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		КорректировочныеСФ.Ссылка,
	|		РезультатПоТабличнойЧасти.ВариантПредставленияНабораВПечатныхФормах,
	|		РезультатПоТабличнойЧасти.ВариантРасчетаЦеныНабора,
	|		РезультатПоТабличнойЧасти.НоменклатураНабора,
	|		РезультатПоТабличнойЧасти.ХарактеристикаНабора,
	|		РезультатПоТабличнойЧасти.ЭтоКомплектующие,
	|		РезультатПоТабличнойЧасти.ЭтоНабор,
	|		РезультатПоТабличнойЧасти.ПолныйНабор,
	|		РезультатПоТабличнойЧасти.Номенклатура,
	|		РезультатПоТабличнойЧасти.НоменклатураНаименование,
	|		РезультатПоТабличнойЧасти.Характеристика,
	|		РезультатПоТабличнойЧасти.ХарактеристикаНаименование,
	|		РезультатПоТабличнойЧасти.НомерГТД,
	|		РезультатПоТабличнойЧасти.СтранаПроисхождения,
	|		РезультатПоТабличнойЧасти.СтранаПроисхожденияКод,
	|		НЕОПРЕДЕЛЕНО,
	|		РезультатПоТабличнойЧасти.СтавкаНДС,
	|		РезультатПоТабличнойЧасти.ЕдиницаИзмерения,
	|		РезультатПоТабличнойЧасти.Количество,
	|		РезультатПоТабличнойЧасти.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|		РезультатПоТабличнойЧасти.Цена,
	|		РезультатПоТабличнойЧасти.СуммаБезНДС,
	|		РезультатПоТабличнойЧасти.СуммаНДС,
	|		0,
	|		0,
	|		РезультатПоТабличнойЧасти.СуммаСНДС,
	|		0,
	|		0,
	|		РезультатПоТабличнойЧасти.НомерСтроки,
	|		РезультатПоТабличнойЧасти.НомерСтрокиНаборы,
	|		РезультатПоТабличнойЧасти.ЭтоВозвратнаяТара
	|	ИЗ
	|		РезультатПоТабличнойЧасти КАК РезультатПоТабличнойЧасти
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СчетаФактуры КАК КорректировочныеСФ
	|			ПО КорректировочныеСФ.ИсходныйСчетФактура = РезультатПоТабличнойЧасти.Ссылка
	|	ГДЕ
	|		РезультатПоТабличнойЧасти.Ссылка.Корректировочный
	|	) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Характеристика,
	|	Таблица.НомерГТД,
	|	Таблица.ПолныйНабор,
	|	Таблица.ХарактеристикаНабора,
	|	Таблица.ЭтоНабор,
	|	Таблица.ХарактеристикаНаименование,
	|	Таблица.СтранаПроисхождения,
	|	Таблица.Ссылка,
	|	Таблица.ВариантРасчетаЦеныНабора,
	|	Таблица.ЭтоКомплектующие,
	|	Таблица.Номенклатура,
	|	Таблица.НоменклатураНаименование,
	|	Таблица.ВариантПредставленияНабораВПечатныхФормах,
	|	Таблица.НоменклатураНабора,
	|	Таблица.ЭтоВозвратнаяТара,
	|	Таблица.СтранаПроисхожденияКод,
	|	Таблица.ЕдиницаИзмерения,
	|	Таблица.Цена,
	|	Таблица.Количество,
	|	Таблица.КоличествоПоРНПТ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка,
	|	Таблица.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	Таблица.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	Таблица.НоменклатураНабора КАК НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	Таблица.ЭтоКомплектующие КАК ЭтоКомплектующие,
	|	Таблица.ЭтоНабор КАК ЭтоНабор,
	|	Таблица.ПолныйНабор КАК ПолныйНабор,
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.НоменклатураНаименование КАК НоменклатураНаименование,
	|	Таблица.Характеристика КАК Характеристика,
	|	Таблица.ХарактеристикаНаименование КАК ХарактеристикаНаименование,
	|	Таблица.НомерГТД КАК НомерГТД,
	|	Таблица.СтранаПроисхождения КАК СтранаПроисхождения,
	|	Таблица.СтранаПроисхожденияКод КАК СтранаПроисхожденияКод,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДСДо,
	|	Таблица.СтавкаНДС КАК СтавкаНДС,
	|	Таблица.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Таблица.Количество КАК Количество,
	|	Таблица.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	Таблица.Цена КАК Цена,
	|	Таблица.СуммаБезНДС КАК СуммаБезНДС,
	|	Таблица.СуммаНДС КАК СуммаНДС,
	|	0 КАК СуммаНДСДо,
	|	0 КАК РазницаНДСУвеличение,
	|	Таблица.СуммаСНДС КАК СуммаСНДС,
	|	0 КАК СуммаСНДСДо,
	|	0 КАК РазницаСНДСУвеличение,
	|	Таблица.НомерСтроки КАК НомерСтроки,
	|	Таблица.НомерСтрокиНаборы КАК НомерСтрокиНаборы,
	|	Таблица.ЭтоВозвратнаяТара КАК ЭтоВозвратнаяТара,
	|	0 КАК КоличествоПоРНПТУвеличение,
	|	0 КАК КоличествоПоРНПТУменьшение
	|ИЗ
	|	РезультатПоТабличнойЧасти КАК Таблица
	|ГДЕ
	|	НЕ Таблица.Ссылка.Корректировочный
	|
	|УПОРЯДОЧИТЬ ПО
	|	Таблица.Ссылка,
	|	МАКСИМУМ(Таблица.НомерСтрокиНаборы),
	|	Таблица.ЭтоНабор УБЫВ,
	|	МАКСИМУМ(Таблица.НомерСтроки)
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетФактураВыданныйАванс.Ссылка КАК Ссылка,
	|	СчетФактураПолученныйАванс.Контрагент КАК Поставщик,
	|	СчетФактураПолученныйАванс.ИННКонтрагента КАК ИННПоставщика,
	|	СчетФактураПолученныйАванс.КППКонтрагента КАК КПППоставщика
	|ИЗ
	|	Документ.СчетФактураВыданныйАванс КАК СчетФактураВыданныйАванс
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученныйАванс КАК СчетФактураПолученныйАванс
	|		ПО СчетФактураВыданныйАванс.ДокументОснование = СчетФактураПолученныйАванс.Ссылка
	|ГДЕ
	|	СчетФактураВыданныйАванс.Ссылка В(&МассивДокументов)
	|	И СчетФактураВыданныйАванс.Проведен
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|ИТОГИ ПО
	|	Ссылка";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	РезультатПоШапке          = МассивРезультатов[3];
	РезультатПоТабличнойЧасти = МассивРезультатов[10];
	РезультатПоИсходнымДанным = МассивРезультатов[0];
	РезультатПоПоставщикам    = МассивРезультатов[11];
	
	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("РезультатПоШапке", РезультатПоШапке);
	СтруктураДанныхДляПечати.Вставить("РезультатПоТабличнойЧасти", РезультатПоТабличнойЧасти);
	СтруктураДанныхДляПечати.Вставить("РезультатПоИсходнымДанным", РезультатПоИсходнымДанным);
	СтруктураДанныхДляПечати.Вставить("РезультатПоПоставщикам", РезультатПоПоставщикам);
	
	СтруктураДанныхДляПечати.Вставить("СчетФактураНаАванс", Истина);
	СтруктураДанныхДляПечати.Вставить("РезультатПоПоставщикам", РезультатПоПоставщикам);
	
	Возврат СтруктураДанныхДляПечати;
КонецФункции

#КонецОбласти

//-- Локализация
