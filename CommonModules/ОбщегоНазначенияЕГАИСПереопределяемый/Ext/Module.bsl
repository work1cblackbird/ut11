#Область ПрограммныйИнтерфейс

// Заполняет в переданной таблице значений реквизиты: Крепость, ВидПродукции и Объем.
//
// Параметры:
//  ТаблицаНоменклатуры - ТаблицаЗначений - таблица номенклатуры с колонками: НомерСтроки, Номенклатура.
Процедура ЗаполнитьРеквизитыАлкогольнойПродукции(ТаблицаНоменклатуры) Экспорт
	
	//++ НЕ ГОСИС
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура.Крепость КАК Крепость,
	|	Товары.Номенклатура.ВидАлкогольнойПродукции КАК ВидПродукции,
	|	Товары.Номенклатура.ПроизводительИмпортерКонтрагент КАК Производитель,
	|	ВЫРАЗИТЬ(Товары.Номенклатура.ОбъемДАЛ * 10 КАК ЧИСЛО(10, 3)) КАК Объем,
	|	Производители.ИНН КАК ПроизводительИНН,
	|	Производители.КПП КАК ПроизводительКПП
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Производители
	|		ПО Товары.Номенклатура.ПроизводительИмпортерКонтрагент = Производители.Ссылка
	|");
	
	Запрос.УстановитьПараметр("Товары", ТаблицаНоменклатуры);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаТаблицы = ТаблицаНоменклатуры.Найти(Выборка.НомерСтроки, "НомерСтроки");
		Если СтрокаТаблицы <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка, "Крепость, ВидПродукции, Производитель, Объем, ПроизводительИНН, ПроизводительКПП");
		КонецЕсли;
	КонецЦикла;
	//-- НЕ ГОСИС
	
КонецПроцедуры

//Заполняет текст запроса для формирования временной таблицы коэффициентов пересчета базовых единиц измерения
//   номенклатуры в объем ЕГАИС (декалитры).
//
//Параметры:
//  ТекстЗапроса        - Строка - Заполняемый текст запроса.
//  ИмяТаблицыТовары    - Строка - Имя таблицы с колонками: Номенклатура, Характеристика.
//  ИмяВременнойТаблицы - Строка - Имя результирующей временной таблицы.
Процедура ТекстЗапросаВТОбъемДАЛЕдиницыНоменклатуры(ТекстЗапроса, ИмяТаблицыТовары, ИмяВременнойТаблицы) Экспорт
	
	//++ НЕ ГОСИС
	ТекстЗапроса = СтрШаблон(
	"ВЫБРАТЬ
	|	Таблица.Номенклатура   КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА СправочникНоменклатура.Ссылка ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА СправочникНоменклатура.ОбъемДАЛ <> 0
	|			ТОГДА СправочникНоменклатура.ОбъемДАЛ
	|		КОГДА СправочникНоменклатура.ОбъемИспользовать
	|			И НЕ СправочникНоменклатура.ОбъемЕдиницаИзмерения ЕСТЬ NULL
	|			И СправочникНоменклатура.ОбъемЗнаменатель <> 0
	|			И СправочникНоменклатура.ОбъемЕдиницаИзмерения.Знаменатель <> 0
	|		ТОГДА СправочникНоменклатура.ОбъемЧислитель / СправочникНоменклатура.ОбъемЗнаменатель *
	|			СправочникНоменклатура.ОбъемЕдиницаИзмерения.Числитель / СправочникНоменклатура.ОбъемЕдиницаИзмерения.Знаменатель * 100
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЧИСЛО(12,4)) КАК ОбъемДАЛ
	|ПОМЕСТИТЬ %2
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ Т.Номенклатура, Т.Характеристика ИЗ %1 КАК Т) КАК Таблица
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Таблица.Номенклатура = СправочникНоменклатура.Ссылка
	|",
	ИмяТаблицыТовары,
	ИмяВременнойТаблицы);
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

#КонецОбласти