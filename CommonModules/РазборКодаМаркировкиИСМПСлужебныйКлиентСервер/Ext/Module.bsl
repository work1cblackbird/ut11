#Область СлужебныйПрограммныйИнтерфейс

#Область РазборКодаМаркировки

Функция ЭлементКодаМаркировкиСоответствуетОписанию(Значение, ОписаниеЭлементаКМ, ПараметрыОписанияКодаМаркировки, ДополнительныеПараметры) Экспорт
	
	Если ОписаниеЭлементаКМ.Имя = "GTIN" Тогда
		
		Если Не РазборКодаМаркировкиИССлужебныйКлиентСерверПовтИсп.ЭтоGTIN(Значение) Тогда
			Возврат Ложь;
		КонецЕсли;
		
	ИначеЕсли ОписаниеЭлементаКМ.Имя = "SSCC" Тогда
		
		КонтрольноеЧисло = РазборКодаМаркировкиИССлужебныйКлиентСервер.КонтрольноеЧислоSSCC(Лев(Значение, ОписаниеЭлементаКМ.Длина - 1));
		
		Возврат КонтрольноеЧисло =  Число(Прав(Значение, 1));
		
	ИначеЕсли ОписаниеЭлементаКМ.Имя = "МРЦСтрокой" Тогда
		
		РезультатПроверки = РазборКодаМаркировкиИССлужебныйКлиентСерверПовтИсп.МРЦПоВидуУпаковки(Значение, ПараметрыОписанияКодаМаркировки.ВидУпаковки);
		
		Если Не РезультатПроверки.ЭтоМРЦ Тогда
			Возврат Ложь;
		КонецЕсли;
		
		ДополнительныеПараметры.Вставить("ЗначениеМРЦ", РезультатПроверки.ЗначениеМРЦ);
		
	ИначеЕсли ОписаниеЭлементаКМ.Имя = "ДатаФормированияАТК" Тогда
		
		// дата формирования оператором агрегированного таможенного кода (ДДММГГ)
		
		Год   = Прав(Значение, 2);
		Месяц = Сред(Значение, 3, 2);
		День  = Лев(Значение, 2);
		
		ДатаСтрокой = СтрШаблон("20%1%2%3", Год, Месяц, День);
		
		КвалификаторыДаты = Новый КвалификаторыДаты(ЧастиДаты.Дата);
		ОписаниеТипа      = Новый ОписаниеТипов("Дата",,, КвалификаторыДаты);
		
		ДатаФормированияАТК = ОписаниеТипа.ПривестиЗначение(ДатаСтрокой);
		
		Возврат ДатаФормированияАТК <> '00010101';
		
	ИначеЕсли ОписаниеЭлементаКМ.Имя = "ИНН" Тогда
		
		ТекстСообщения = "";
		
		Если СтрНачинаетсяС(Значение, "00") Тогда
			ИНН = Прав(Значение, 10);
			Если РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, Истина, ТекстСообщения) Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
		
		Возврат РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(Значение, Ложь, ТекстСообщения);
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура ДобавитьЭлементКодаМаркировкиВСоставКодаМаркировки(Значение, ОписаниеЭлементаКМ, СоставКодаМаркировки, ПараметрыОписанияКодаМаркировки, ДополнительныеПараметры) Экспорт
	
	Если ОписаниеЭлементаКМ.Имя = "GTIN" Тогда
		
		РазборКодаМаркировкиИССлужебныйКлиентСервер.ЗаполнитьСоставКодаМаркировки(
			СоставКодаМаркировки,
			Новый Структура("Имя", "EAN"),
			РазборКодаМаркировкиИССлужебныйКлиентСерверПовтИсп.ШтрихкодEANИзGTIN(Значение));
		
	ИначеЕсли ОписаниеЭлементаКМ.Имя = "МРЦСтрокой" Тогда
		
		РазборКодаМаркировкиИССлужебныйКлиентСервер.ЗаполнитьСоставКодаМаркировки(
			СоставКодаМаркировки, Новый Структура("Имя", "МРЦ"), ДополнительныеПараметры["ЗначениеМРЦ"]);
		
		РазборКодаМаркировкиИССлужебныйКлиентСервер.ЗаполнитьСоставКодаМаркировки(
			СоставКодаМаркировки, Новый Структура("Имя", "ВключаетМРЦ"), Истина);
	
	ИначеЕсли ОписаниеЭлементаКМ.Имя = "КодПроверки" Или ОписаниеЭлементаКМ.Имя = "КлючПроверки" Тогда
		
		РазборКодаМаркировкиИССлужебныйКлиентСервер.ЗаполнитьСоставКодаМаркировки(
			СоставКодаМаркировки, Новый Структура("Имя", "ВключаетКриптоХвост"), Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПреобразоватьЗначениеДляЗаполненияСоставаКодаМаркировки(Значение, ОписаниеЭлементаКМ, СоставКодаМаркировки, ПараметрыОписанияКодаМаркировки, ДополнительныеПараметры) Экспорт
	
	Если СоставКодаМаркировки = Неопределено Или Не СоставКодаМаркировки.Свойство(ОписаниеЭлементаКМ.Имя) Тогда
		Возврат;
	КонецЕсли;
	
	Если ОписаниеЭлементаКМ.Имя = "ГоденДо" Тогда
		
		Если ОписаниеЭлементаКМ.Код = "17" Тогда
			// Дата окончания срока годности продукции (срок хранения более 72 часов).
			// Формат: YYMMDD
			КвалификаторыДаты = Новый КвалификаторыДаты(ЧастиДаты.Дата);
		Иначе
			// Дата окончания срока годности продукции (срок хранения менее 72 часов)
			// Формат: YYMMDDHHMM
			КвалификаторыДаты = Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя);
		КонецЕсли;
		
		ОписаниеТипа = Новый ОписаниеТипов("Дата",,, КвалификаторыДаты);
		Значение = ОписаниеТипа.ПривестиЗначение("20" + Значение);
		
	ИначеЕсли ОписаниеЭлементаКМ.Имя = "МассаНеттоВКг" Тогда
		
		ПоложениеДесятичнойТочки = 3;
		
		Значение = СтрокаВЧисло(Значение, ПоложениеДесятичнойТочки);
		
	ИначеЕсли ОписаниеЭлементаКМ.Имя = "ОбъемВЛитрах" Тогда
		
		ПоложениеДесятичнойТочки = Число(Прав(ОписаниеЭлементаКМ.Код, 1));
		
		Значение = СтрокаВЧисло(Значение, ПоложениеДесятичнойТочки);
		
	КонецЕсли;
	
КонецПроцедуры

// Это не формализованный код маркировки.
// 
// Параметры:
//  ПараметрыРазбораКодаМаркировки - см. РазборКодаМаркировкиИССлужебныйКлиентСервер.ИнициализироватьПараметрыРазбораКодаМаркировки.
//  Настройки - см. РазборКодаМаркировкиИССлужебный.НастройкиРазбораКодаМаркировки.
//  ДанныеРезультата - Неопределено - Выходной параметр (см. РазборКодаМаркировкиИССлужебныйКлиентСервер.НовыйРезультатРазбораКодаМаркировки).
//  РезультатБезФильтра - Массив Из см. РазборКодаМаркировкиИССлужебныйКлиентСервер.НовыйРезультатРазбораКодаМаркировки.
// 
// Возвращаемое значение:
//  Булево - Если Ложь, то код маркировки не прошел проверку.
//
Функция ЭтоНеФормализованныйКодМаркировки(ПараметрыРазбораКодаМаркировки, Настройки, ДанныеРезультата, РезультатБезФильтра) Экспорт
	
	Если ПрисутствуетТабачнаяПродукция(Настройки.ДоступныеВидыПродукции) Тогда
		
		Если ЭтоНеФормализованныйКодМаркировкиГрупповойУпаковкиТабака(
				ПараметрыРазбораКодаМаркировки, Настройки, ДанныеРезультата, РезультатБезФильтра) Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Настройки.ДоступныеВидыПродукции.Найти(
			ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КреслаКоляски")) <> Неопределено
		Или Настройки.ДоступныеВидыПродукции.Найти(
			ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ТехническиеСредстваРеабилитации")) <> Неопределено
		Или Настройки.ДоступныеВидыПродукции.Найти(
			ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МедицинскиеИзделия")) <> Неопределено Тогда
		
		Если ЭтоНеФормализованныйКодМаркировкиКреслаКоляски(
				ПараметрыРазбораКодаМаркировки, Настройки, ДанныеРезультата, РезультатБезФильтра) Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЭтоНеФормализованныйКодМаркировкиЛогистическойУпаковкиGS1128(
			ПараметрыРазбораКодаМаркировки, Настройки, ДанныеРезультата, РезультатБезФильтра) Тогда
		Возврат Истина;
	ИначеЕсли ЭтоНеФормализованныйКодМаркировкиЛогистическойУпаковкиCode128(
			ПараметрыРазбораКодаМаркировки, Настройки, ДанныеРезультата, РезультатБезФильтра) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область НастройкиРазбораКодаМаркировки

Функция НовыйСоставКодаМаркировки(ТипШтрихкодаИВидУпаковки) Экспорт
	
	СоставКодаМаркировки = Новый Структура;
	СоставКодаМаркировки.Вставить("ВключаетИдентификаторыПрименения", Ложь);
	
	Если ТипШтрихкодаИВидУпаковки.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.Потребительская")
		Или ТипШтрихкодаИВидУпаковки.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.Групповая")
		Или ТипШтрихкодаИВидУпаковки.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.Набор") Тогда
		
		Если ТипШтрихкодаИВидУпаковки.ТипШтрихкода = ПредопределенноеЗначение("Перечисление.ТипыШтрихкодов.DataMatrix")
			Или ТипШтрихкодаИВидУпаковки.ТипШтрихкода = ПредопределенноеЗначение("Перечисление.ТипыШтрихкодов.GS1_DataMatrix") Тогда
			
			СоставКодаМаркировки.Вставить("GTIN",          "");
			СоставКодаМаркировки.Вставить("EAN",           "");
			СоставКодаМаркировки.Вставить("СерийныйНомер", "");
			
		КонецЕсли;
		
	ИначеЕсли ТипШтрихкодаИВидУпаковки.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.Логистическая")
		И ТипШтрихкодаИВидУпаковки.ТипШтрихкода = ПредопределенноеЗначение("Перечисление.ТипыШтрихкодов.SSCC") Тогда
		
		СоставКодаМаркировки.Вставить("SSCC", "");
		
	ИначеЕсли ТипШтрихкодаИВидУпаковки.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.Логистическая")
		И ТипШтрихкодаИВидУпаковки.ТипШтрихкода = ПредопределенноеЗначение("Перечисление.ТипыШтрихкодов.GS1_128") Тогда
		
		СоставКодаМаркировки.Вставить("GTIN", "");
		СоставКодаМаркировки.Вставить("EAN",  "");
		СоставКодаМаркировки.Вставить("КоличествоВложенныхЕдиниц",                 Неопределено);
		СоставКодаМаркировки.Вставить("Коэффициент",                               Неопределено);
		СоставКодаМаркировки.Вставить("МассаНеттоВКг",                             Неопределено);
		СоставКодаМаркировки.Вставить("ВозможныВариантыКоличестваВложенныхЕдиниц", Ложь);
		
	ИначеЕсли ТипШтрихкодаИВидУпаковки.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.ОбъемноСортовойУчет") Тогда
		
		СоставКодаМаркировки.Вставить("GTIN", "");
		СоставКодаМаркировки.Вставить("EAN",  "");
		СоставКодаМаркировки.Вставить("КоличествоВложенныхЕдиниц", Неопределено);
		СоставКодаМаркировки.Вставить("МассаНеттоВКг",             Неопределено);
	
	ИначеЕсли ТипШтрихкодаИВидУпаковки.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.АгрегированныйТаможенныйКод") Тогда
		
		СоставКодаМаркировки.Вставить("ИНН",                 "");
		СоставКодаМаркировки.Вставить("ДатаФормирования",    "");
		СоставКодаМаркировки.Вставить("ПризнакУникальности", "");
		
	КонецЕсли;
	
	Возврат СоставКодаМаркировки;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РазборКодаМаркировки

Функция ЭтоНеФормализованныйКодМаркировкиГрупповойУпаковкиТабака(ПараметрыРазбораКодаМаркировки, Настройки, ДанныеРезультата, РезультатБезФильтра)
	
	// После идентификатора 93 для блоков и только для них может быть произвольное количество идентификаторов применения
	// 010460620310255621!MmNZo2800514900093Ij5E240FA075486.00
	
	ШаблоныКодаМаркировкиСХвостом = Настройки.ДополнительныеПараметры.ИСМП.Табак.ШаблоныКодаМаркировкиСХвостом;
	
	Если ШаблоныКодаМаркировкиСХвостом.Количество() = 0 Тогда
		Возврат Ложь; // Учет табачной продукции не ведется
	КонецЕсли;
	
	ИсходныйКодМаркировки       = ПараметрыРазбораКодаМаркировки.КодМаркировки;
	ИсходнаяДлинаКодаМаркировки = ПараметрыРазбораКодаМаркировки.ДлинаКодаМаркировки;
	ДанныеРезультата            = Неопределено;
	
	Для Каждого ШаблонКодаМаркировки Из ШаблоныКодаМаркировкиСХвостом Цикл
		
		Если ПараметрыРазбораКодаМаркировки.НачинаетсяСоСкобки Или ПараметрыРазбораКодаМаркировки.СодержитРазделительGS Тогда
			ДлинаКодаМаркировкиИзШаблона = ШаблонКодаМаркировки.ДлинаСоСкобкой;
		Иначе
			ДлинаКодаМаркировкиИзШаблона = ШаблонКодаМаркировки.Длина;
		КонецЕсли;
		
		Если ИсходнаяДлинаКодаМаркировки <= ДлинаКодаМаркировкиИзШаблона Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыРазбораКодаМаркировки.ДлинаКодаМаркировки = ДлинаКодаМаркировкиИзШаблона;
		
		ПараметрыРазбораКодаМаркировки.КодМаркировки = Лев(ИсходныйКодМаркировки, ПараметрыРазбораКодаМаркировки.ДлинаКодаМаркировки);
		
		ДанныеРезультата = РазборКодаМаркировкиИССлужебныйКлиентСервер.КодМаркировкиСоответствуетШаблону(ПараметрыРазбораКодаМаркировки, Настройки, ШаблонКодаМаркировки);
		
		Если ДанныеРезультата = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеРезультата.КодМаркировки = ИсходныйКодМаркировки;
		ДанныеРезультата.ВидУпаковки   = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.Групповая");
		
		Если ПараметрыРазбораКодаМаркировки.ПользовательскиеПараметры.РасширеннаяДетализация Тогда
			ДанныеРезультата.Детализация.ЭтоНеФормализованныйКодМаркировки = Истина;
		КонецЕсли;
		
		ПараметрыРазбораКодаМаркировки.КодМаркировки       = ИсходныйКодМаркировки;
		ПараметрыРазбораКодаМаркировки.ДлинаКодаМаркировки = ИсходнаяДлинаКодаМаркировки;
		
		Возврат Истина;
		
	КонецЦикла;
	
	ПараметрыРазбораКодаМаркировки.КодМаркировки       = ИсходныйКодМаркировки;
	ПараметрыРазбораКодаМаркировки.ДлинаКодаМаркировки = ИсходнаяДлинаКодаМаркировки;
	
	Возврат Ложь;
	
КонецФункции

Функция ЭтоНеФормализованныйКодМаркировкиКреслаКоляски(ПараметрыРазбораКодаМаркировки, Настройки, ДанныеРезультата, РезультатБезФильтра)
	
	// В коде может присутствовать идентификатор 97 и следующий за ним ЗаводскойСерийныйНомер в формате base64
	// 0108717644098684215*bgJiJmK5qTm91FFD092dGVzdFjTvNy+LhiBjpXBVCnWQqs/wt7jWl3EK1aeUV8=97MTIz
	
	ШаблоныКодовМаркировки = Настройки.ДополнительныеПараметры.ИСМП.КреслаКоляски.ШаблоныНеФормализованныхКодовМаркировки;
	
	Если ШаблоныКодовМаркировки.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИсходныйКодМаркировки       = ПараметрыРазбораКодаМаркировки.КодМаркировки;
	ИсходнаяДлинаКодаМаркировки = ПараметрыРазбораКодаМаркировки.ДлинаКодаМаркировки;
	ДанныеРезультата            = Неопределено;
	
	ЕстьСкобки = ПараметрыРазбораКодаМаркировки.НачинаетсяСоСкобки Или ПараметрыРазбораКодаМаркировки.СодержитРазделительGS;
	
	Для Каждого ШаблонКодаМаркировки Из ШаблоныКодовМаркировки Цикл
		
		Если ЕстьСкобки Тогда
			ДлинаКодаМаркировкиИзШаблона = ШаблонКодаМаркировки.ДлинаСоСкобкой;
		Иначе
			ДлинаКодаМаркировкиИзШаблона = ШаблонКодаМаркировки.Длина;
		КонецЕсли;
		
		Если ИсходнаяДлинаКодаМаркировки > ДлинаКодаМаркировкиИзШаблона Тогда
			
			ПолноеОкончаниеКода = Сред(ИсходныйКодМаркировки, ДлинаКодаМаркировкиИзШаблона + 1);
			Если ЕстьСкобки
				И Лев(ПолноеОкончаниеКода, 4) = "(97)" Тогда
				ОкончаниеКода = Сред(ПолноеОкончаниеКода, 5);
			ИначеЕсли Не ЕстьСкобки
				И Лев(ПолноеОкончаниеКода, 2) = "97" Тогда
				ОкончаниеКода = Сред(ПолноеОкончаниеКода, 3);
			Иначе
				ОкончаниеКода = "";
			КонецЕсли;
			
			Если ОкончаниеКода <> "" И Base64Значение(ОкончаниеКода) <> Неопределено Тогда
				
				ПараметрыРазбораКодаМаркировки.ДлинаКодаМаркировки = ДлинаКодаМаркировкиИзШаблона;
				ПараметрыРазбораКодаМаркировки.КодМаркировки = Лев(ИсходныйКодМаркировки, ДлинаКодаМаркировкиИзШаблона);
				
				ДанныеРезультата = РазборКодаМаркировкиИССлужебныйКлиентСервер.КодМаркировкиСоответствуетШаблону(ПараметрыРазбораКодаМаркировки, Настройки, ШаблонКодаМаркировки);
				
				Если ДанныеРезультата <> Неопределено Тогда
					
					ДанныеРезультата.КодМаркировки = ИсходныйКодМаркировки;
					Если ПараметрыРазбораКодаМаркировки.ПользовательскиеПараметры.РасширеннаяДетализация Тогда
						ДанныеРезультата.Детализация.ЭтоНеФормализованныйКодМаркировки = Истина;
					КонецЕсли;
					
					ПараметрыРазбораКодаМаркировки.КодМаркировки       = ИсходныйКодМаркировки;
					ПараметрыРазбораКодаМаркировки.ДлинаКодаМаркировки = ИсходнаяДлинаКодаМаркировки;
					
					Возврат Истина;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыРазбораКодаМаркировки.КодМаркировки       = ИсходныйКодМаркировки;
	ПараметрыРазбораКодаМаркировки.ДлинаКодаМаркировки = ИсходнаяДлинаКодаМаркировки;
	
	Возврат Ложь;
	
КонецФункции

Функция ЭтоНеФормализованныйКодМаркировкиЛогистическойУпаковкиGS1128(ПараметрыРазбораКодаМаркировки, Настройки, ДанныеРезультата, РезультатБезФильтра)
	
	ВозможенКодОСУ = Ложь;
	Если РезультатБезФильтра.Количество() > 0 Тогда
		
		ВозможенКодОСУ = СтрНачинаетсяС(ПараметрыРазбораКодаМаркировки.КодМаркировки, "02")
			И Сред(ПараметрыРазбораКодаМаркировки.КодМаркировки, 17, 2) = "37";
		Если Не ВозможенКодОСУ Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	// Ограничение согласно документации СУЗ
	МинимальнаяДлинаКодаТранспортнойУпаковки    = 18; // В ЦРПТ логистическая упаковка не может быть меньше 18 символов
	МинимальнаяДлинаКодаТранспортнойУпаковкиАТП = 34;
	МаксимальнаяДлинаКодаТранспортнойУпаковки   = 74;
	
	Если ПараметрыРазбораКодаМаркировки.ПользовательскиеПараметры.ВалидироватьШтрихкодЛогистическойУпаковкиGS1128СОшибками Тогда
		МинимальнаяДлинаКодаТранспортнойУпаковкиАТП = МинимальнаяДлинаКодаТранспортнойУпаковки;
	КонецЕсли;

	Если ПараметрыРазбораКодаМаркировки.ДлинаКодаМаркировки < МинимальнаяДлинаКодаТранспортнойУпаковки Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ФильтрПоВидуПродукции = ПараметрыРазбораКодаМаркировки.ФильтрПоВидуПродукции;
	Если Не ФильтрПоВидуПродукции.Использовать Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВидыПродукции = Новый Массив;
	Для Каждого ВидПродукции Из ФильтрПоВидуПродукции.ВидыПродукции Цикл
		Если Не ОбщегоНазначенияИСКлиентСервер.ЭтоПродукцияИСМП(ВидПродукции, Истина) Тогда
			Возврат Ложь;
		КонецЕсли;
		Если Настройки.ДоступныеВидыПродукции.Найти(ВидПродукции) <> Неопределено Тогда
			ВидыПродукции.Добавить(ВидПродукции);
		КонецЕсли;
	КонецЦикла;
	
	КодМаркировки = ПараметрыРазбораКодаМаркировки.КодМаркировки;
	
	Если ПараметрыРазбораКодаМаркировки.ДлинаКодаМаркировки < МинимальнаяДлинаКодаТранспортнойУпаковкиАТП Тогда
		ИндексАТП = ВидыПродукции.Найти(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АльтернативныйТабак"));
		Если ИндексАТП <> Неопределено Тогда
			ВидыПродукции.Удалить(ИндексАТП);
		КонецЕсли;
	КонецЕсли;
	
	Если ВидыПродукции.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВидУпаковки  = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.Логистическая");
	ТипШтрихкода = ПредопределенноеЗначение("Перечисление.ТипыШтрихкодов.GS1_128");
	
	ЗначениеИдентификатораGTIN = Неопределено;
	
	ПрисутствуетИдентификатор02               = Ложь;
	ЗначениеИдентификатора37                  = Неопределено; // КоличествоВложенныхЕдиниц
	ПрисутствуетИдентификатор01               = Ложь;
	ЗначениеИдентификатора30                  = Неопределено; // КоличествоЕдиниц
	ЗначениеИдентификатора310X                = Неопределено; // МассаНеттоВКг
	ВозможныВариантыКоличестваВложенныхЕдиниц = Ложь;
	
	ДлинаКодаМаркировкиБезРазделителей = 0;
	
	Если ПараметрыРазбораКодаМаркировки.НачинаетсяСоСкобки Тогда
		
		Если ПараметрыРазбораКодаМаркировки.ДлинаКодаМаркировки = 22 И СтрНачинаетсяС(КодМаркировки, "(00)") Тогда
			
			// Некорректный SSCC
			
			Если Не ПараметрыРазбораКодаМаркировки.ПользовательскиеПараметры.ВалидироватьШтрихкодЛогистическойУпаковкиGS1128СОшибками Тогда
				Возврат Ложь;
			КонецЕсли;
			
			ТипШтрихкода = ПредопределенноеЗначение("Перечисление.ТипыШтрихкодов.Неопределен");
			
		Иначе
			
			РезультатРазбора = МенеджерОборудованияМаркировкаКлиентСервер.РазобратьСтрокуШтрихкодаGS1(КодМаркировки);
			Если РезультатРазбора.Разобран Тогда
				
				ЭлементКМ = РезультатРазбора.ДанныеШтрихкода["01"];
				Если ЭлементКМ = Неопределено Тогда
					ЭлементКМ = РезультатРазбора.ДанныеШтрихкода["02"];
					Если ЭлементКМ <> Неопределено Тогда
						ЗначениеИдентификатораGTIN = ЭлементКМ.Значение;
						Если РезультатРазбора.ДанныеШтрихкода["37"] <> Неопределено Тогда
							ЗначениеИдентификатора37    = СтрокаВЧисло(РезультатРазбора.ДанныеШтрихкода["37"].Значение);
							ПрисутствуетИдентификатор02 = (ЗначениеИдентификатора37 <> Неопределено);
						КонецЕсли;
					КонецЕсли;
				Иначе
					ЗначениеИдентификатораGTIN = ЭлементКМ.Значение;
					Если РезультатРазбора.ДанныеШтрихкода["30"] <> Неопределено Тогда
						ЗначениеИдентификатора30    = СтрокаВЧисло(РезультатРазбора.ДанныеШтрихкода["30"].Значение);
						ПрисутствуетИдентификатор01 = (ЗначениеИдентификатора30 <> Неопределено);
					КонецЕсли;
				КонецЕсли;
				
				Если РезультатРазбора.ДанныеШтрихкода["310"] <> Неопределено Тогда
					ПоложениеДесятичнойТочки310X = РезультатРазбора.ДанныеШтрихкода["310"].ПоложениеДесятичнойТочки;
					ОписаниеТипаЧисла = Новый ОписаниеТипов("Число");
					ЗначениеИдентификатора310X = ОписаниеТипаЧисла.ПривестиЗначение(РезультатРазбора.ДанныеШтрихкода["310"].Значение);
				КонецЕсли;
				
				Для Каждого КлючЗначение Из РезультатРазбора.ДанныеШтрихкода Цикл
					Идентификатор          = КлючЗначение.Ключ;
					ЗначениеИдентификатора = КлючЗначение.Значение.Значение;
					Если ТипЗнч(ЗначениеИдентификатора) = Тип("Строка")
						И ПустаяСтрока(ЗначениеИдентификатора) Тогда
						РезультатРазбора.Разобран = Ложь;
						Возврат Ложь;
					КонецЕсли;
					ДлинаКодаМаркировкиБезРазделителей = ДлинаКодаМаркировкиБезРазделителей + СтрДлина(Идентификатор) + СтрДлина(ЗначениеИдентификатора);
				КонецЦикла;
				
			Иначе
				
				Если Не ПараметрыРазбораКодаМаркировки.ПользовательскиеПараметры.ВалидироватьШтрихкодЛогистическойУпаковкиGS1128СОшибками Тогда
					Возврат Ложь;
				КонецЕсли;
				
				ИдентификаторGTIN = Лев(КодМаркировки, 4);
				Если ИдентификаторGTIN = "(01)" Тогда
					ЗначениеИдентификатораGTIN = Сред(КодМаркировки, 5, 14);
				ИначеЕсли ИдентификаторGTIN = "(02)" Тогда
					ПараметрыРазбораКодаМаркировки.МодульКонтекста.МодульОбщегоНазначения().СообщитьПользователю(РезультатРазбора.ОписаниеОшибки);
				КонецЕсли;
				
				Если Не РазборКодаМаркировкиИССлужебныйКлиентСерверПовтИсп.ЭтоGTIN(ЗначениеИдентификатораGTIN) Тогда
					Возврат Ложь;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ПараметрыРазбораКодаМаркировки.СодержитРазделительGS Тогда
		
		РезультатРазбора = ПараметрыРазбораКодаМаркировки.РезультатРазбора;
		
		ЭлементКМ = РезультатРазбора.ДанныеШтрихкода["01"];
		Если ЭлементКМ = Неопределено Тогда
			ЭлементКМ = РезультатРазбора.ДанныеШтрихкода["02"];
			Если ЭлементКМ <> Неопределено Тогда
				ЗначениеИдентификатораGTIN = ЭлементКМ.Значение;
				Если РезультатРазбора.ДанныеШтрихкода["37"] <> Неопределено Тогда
					ЗначениеИдентификатора37    = СтрокаВЧисло(РезультатРазбора.ДанныеШтрихкода["37"].Значение);
					ПрисутствуетИдентификатор02 = (ЗначениеИдентификатора37 <> Неопределено);
				КонецЕсли;
			КонецЕсли;
		Иначе
			ЗначениеИдентификатораGTIN = ЭлементКМ.Значение;
			Если РезультатРазбора.ДанныеШтрихкода["30"] <> Неопределено Тогда
				ЗначениеИдентификатора30    = СтрокаВЧисло(РезультатРазбора.ДанныеШтрихкода["30"].Значение);
				ПрисутствуетИдентификатор01 = (ЗначениеИдентификатора30 <> Неопределено);
			КонецЕсли;
		КонецЕсли;
		
		Если РезультатРазбора.ДанныеШтрихкода["310"] <> Неопределено Тогда
			ПоложениеДесятичнойТочки310X = РезультатРазбора.ДанныеШтрихкода["310"].ПоложениеДесятичнойТочки;
			ОписаниеТипаЧисла = Новый ОписаниеТипов("Число");
			ЗначениеИдентификатора310X = ОписаниеТипаЧисла.ПривестиЗначение(РезультатРазбора.ДанныеШтрихкода["310"].Значение);
		КонецЕсли;
		
		Для Каждого КлючЗначение Из РезультатРазбора.ДанныеШтрихкода Цикл
			Идентификатор          = КлючЗначение.Ключ;
			ЗначениеИдентификатора = КлючЗначение.Значение.Значение;
			ДлинаКодаМаркировкиБезРазделителей = ДлинаКодаМаркировкиБезРазделителей + СтрДлина(Идентификатор) + СтрДлина(ЗначениеИдентификатора);
		КонецЦикла;
		
		ДлинаКодаМаркировкиДляПроверки = ДлинаКодаМаркировкиБезРазделителей + РезультатРазбора.ДанныеШтрихкода.Количество() * 2;
		Если ДлинаКодаМаркировкиДляПроверки <> СтрДлина(КодМаркировки) Тогда
			Возврат Ложь; // код маркировки предположительно содержит дубрирующие идентификаторы
		КонецЕсли;
		
	Иначе
		
		Если Не РазборКодаМаркировкиИССлужебныйКлиентСервер.КодСоответствуетАлфавиту(КодМаркировки, Настройки.Алфавит.БуквыЦифрыЗнаки) Тогда
			Возврат Ложь;
		КонецЕсли;
		Если МенеджерОборудованияКлиентСервер.ПроверитьКорректностьGTIN(КодМаркировки) Тогда
			Возврат Ложь;
		КонецЕсли;
		
		ДлинаКодаМаркировкиБезРазделителей = ПараметрыРазбораКодаМаркировки.ДлинаКодаМаркировки;
		
		ВариантыРазбораШтрихкодаGS1 = РазборКодаМаркировкиИССлужебныйКлиентСервер.ВариантыРазбораШтрихкодаGS1БезРазделителей(КодМаркировки);
		Если ВариантыРазбораШтрихкодаGS1 = Неопределено Тогда
			
			Если Не ПараметрыРазбораКодаМаркировки.ПользовательскиеПараметры.ВалидироватьШтрихкодЛогистическойУпаковкиGS1128СОшибками
				Или Не ПараметрыРазбораКодаМаркировки.ПользовательскиеПараметры.ВалидироватьШтрихкодЛогистическойУпаковкиGS1128БезРазделителей Тогда
				Возврат Ложь;
			КонецЕсли;
			
			ТипШтрихкода = ПредопределенноеЗначение("Перечисление.ТипыШтрихкодов.Неопределен");
			
		Иначе
			
			ПроверитьНаличиеВложенныхЕдиниц = Ложь;
			
			ИдентификаторGTIN = Лев(КодМаркировки, 2);
			Если ИдентификаторGTIN = "01" Или ИдентификаторGTIN = "02" Тогда
				ЗначениеИдентификатораGTIN = Сред(КодМаркировки, 3, 14);
				Если РазборКодаМаркировкиИССлужебныйКлиентСерверПовтИсп.ЭтоGTIN(ЗначениеИдентификатораGTIN) Тогда
					ПроверитьНаличиеВложенныхЕдиниц = (ИдентификаторGTIN = "02");
				Иначе
					ЗначениеИдентификатораGTIN = Неопределено;
				КонецЕсли;
			КонецЕсли;
			
			ВариантыКоличестваВложенныхЕдиницПоЗначению   = Новый СписокЗначений;
			ВариантыКоличестваВложенныхЕдиницПоПриоритету = Новый СписокЗначений;
			
			Если ПроверитьНаличиеВложенныхЕдиниц Тогда
				
				Для Каждого КлючИЗначение Из ВариантыРазбораШтрихкодаGS1 Цикл
					
					ИдентификаторВарианта = КлючИЗначение.Ключ;
					ВариантРазбора        = КлючИЗначение.Значение;
					
					Если ВариантРазбора.РезультатРазбора.ДанныеШтрихкода["01"] <> Неопределено Тогда
						
						Если ЗначениеИдентификатораGTIN <> ВариантРазбора.РезультатРазбора.ДанныеШтрихкода["01"].Значение Тогда
							Продолжить;
						ИначеЕсли ВариантРазбора.РезультатРазбора.ДанныеШтрихкода["30"] = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						
						ЗначениеИдентификатора30 = СтрокаВЧисло(ВариантРазбора.РезультатРазбора.ДанныеШтрихкода["30"].Значение);
						
						Если ЗначениеИдентификатора30 <> Неопределено Тогда
							
							ВариантыКоличестваВложенныхЕдиницПоЗначению.Добавить(ЗначениеИдентификатора30, ИдентификаторВарианта);
							ВариантыКоличестваВложенныхЕдиницПоПриоритету.Добавить(ВариантРазбора.Приоритет, ИдентификаторВарианта);
							
							ВариантРазбора.Вставить("Значение", ЗначениеИдентификатора30);
							ВариантРазбора.Вставить("ПрисутствуетИдентификатор02", Ложь);
							ВариантРазбора.Вставить("ПрисутствуетИдентификатор01", Истина);
							
							ПрисутствуетИдентификатор01 = Истина;
							
						КонецЕсли;
						
						ЗначениеИдентификатора30 = Неопределено;
						
					ИначеЕсли ВариантРазбора.РезультатРазбора.ДанныеШтрихкода["02"] <> Неопределено Тогда
						
						Если ЗначениеИдентификатораGTIN <> ВариантРазбора.РезультатРазбора.ДанныеШтрихкода["02"].Значение Тогда
							Продолжить;
						ИначеЕсли ВариантРазбора.РезультатРазбора.ДанныеШтрихкода["37"] = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						
						ЗначениеИдентификатора37 = СтрокаВЧисло(ВариантРазбора.РезультатРазбора.ДанныеШтрихкода["37"].Значение);
						
						Если ЗначениеИдентификатора37 <> Неопределено Тогда
							
							ВариантыКоличестваВложенныхЕдиницПоЗначению.Добавить(ЗначениеИдентификатора37, ИдентификаторВарианта);
							ВариантыКоличестваВложенныхЕдиницПоПриоритету.Добавить(ВариантРазбора.Приоритет, ИдентификаторВарианта);
							
							ВариантРазбора.Вставить("Значение", ЗначениеИдентификатора37);
							ВариантРазбора.Вставить("ПрисутствуетИдентификатор02", Истина);
							ВариантРазбора.Вставить("ПрисутствуетИдентификатор01", Ложь);
							
							ПрисутствуетИдентификатор02 = Истина;
							
						КонецЕсли;
						
						ЗначениеИдентификатора37 = Неопределено;
						
					КонецЕсли;
					
				КонецЦикла;
				
				ВариантыКоличестваВложенныхЕдиницПоЗначению.СортироватьПоЗначению(НаправлениеСортировки.Возр);
				ВариантыКоличестваВложенныхЕдиницПоПриоритету.СортироватьПоЗначению(НаправлениеСортировки.Убыв);
				ВариантыКоличестваВложенныхЕдиницПоЗначениюСвертка = ВариантыКоличестваВложенныхЕдиницПоЗначению.ВыгрузитьЗначения();
				Если ВариантыКоличестваВложенныхЕдиницПоЗначениюСвертка.Количество() > 1 Тогда
					ВариантыКоличестваВложенныхЕдиницПоЗначениюСвертка = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ВариантыКоличестваВложенныхЕдиницПоЗначениюСвертка);
				КонецЕсли;
				
				Если ВариантыКоличестваВложенныхЕдиницПоЗначениюСвертка.Количество() = 0 Тогда
					
					ЗначениеИдентификатораGTIN = Неопределено;
					
				ИначеЕсли ВариантыКоличестваВложенныхЕдиницПоЗначениюСвертка.Количество() = 1 Тогда
					
					Если ПрисутствуетИдентификатор02 Тогда
						ЗначениеИдентификатора37 = ВариантыКоличестваВложенныхЕдиницПоЗначениюСвертка[0];
					ИначеЕсли ПрисутствуетИдентификатор01 Тогда
						ЗначениеИдентификатора30 = ВариантыКоличестваВложенныхЕдиницПоЗначениюСвертка[0];
					КонецЕсли;
					
				Иначе
					
					КоличествоПредполагаемыхКоличеств = 0;
					Для Каждого ЭлементСписка Из ВариантыКоличестваВложенныхЕдиницПоПриоритету Цикл
						
						ВариантРазбора = ВариантыРазбораШтрихкодаGS1[ЭлементСписка.Представление];
						Если ВариантРазбора = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						
						КоличествоПредполагаемыхКоличеств = КоличествоПредполагаемыхКоличеств + 1;
						
						Если ВариантРазбора.ПрисутствуетИдентификатор02 Тогда
							Если ЗначениеИдентификатора37 = Неопределено Тогда
								ЗначениеИдентификатора37 = ВариантРазбора.Значение;
							КонецЕсли;
						ИначеЕсли ВариантРазбора.ПрисутствуетИдентификатор01 Тогда
							Если ЗначениеИдентификатора30 = Неопределено Тогда
								ЗначениеИдентификатора30 = ВариантРазбора.Значение;
							КонецЕсли;
						КонецЕсли;
						
					КонецЦикла;
					
					Если ЗначениеИдентификатора37 = Неопределено И ЗначениеИдентификатора30 = Неопределено Тогда
						ЗначениеИдентификатораGTIN = Неопределено;
					КонецЕсли;
					
					ВозможныВариантыКоличестваВложенныхЕдиниц = (КоличествоПредполагаемыхКоличеств > 1);
					
				КонецЕсли;
				
				ПрисутствуетИдентификатор02 = ЗначениеЗаполнено(ЗначениеИдентификатораGTIN) И ЗначениеЗаполнено(ЗначениеИдентификатора37);
				ПрисутствуетИдентификатор01 = ЗначениеЗаполнено(ЗначениеИдентификатораGTIN) И ЗначениеЗаполнено(ЗначениеИдентификатора30);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	GTIN = "";
	Если ЗначениеЗаполнено(ЗначениеИдентификатораGTIN)
		И ТипЗнч(ЗначениеИдентификатораGTIN) = Тип("Строка")
		И СтрДлина(ЗначениеИдентификатораGTIN) = 14
		И РазборКодаМаркировкиИССлужебныйКлиентСерверПовтИсп.ЭтоGTIN(ЗначениеИдентификатораGTIN) Тогда
		
		GTIN = ЗначениеИдентификатораGTIN;
		
	КонецЕсли;
	
	КоличествоВложенныхЕдиниц = Неопределено;
	Коэффициент               = Неопределено;
	МассаНеттоВКг             = Неопределено;
	
	Если ПрисутствуетИдентификатор02 Тогда
		
		Если ЗначениеЗаполнено(GTIN) Тогда
			КоличествоВложенныхЕдиниц = ЗначениеИдентификатора37;
			МассаНеттоВКг             = ЗначениеИдентификатора310X;
		КонецЕсли;
		
	ИначеЕсли ПрисутствуетИдентификатор01 Тогда
		
		Если ЗначениеЗаполнено(GTIN) Тогда
			Коэффициент   = ЗначениеИдентификатора30;
			МассаНеттоВКг = ЗначениеИдентификатора310X;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПрисутствуетИдентификатор02
		И Не ПараметрыРазбораКодаМаркировки.СодержитРазделительGS
		И ЗначениеЗаполнено(GTIN)
		И ЗначениеЗаполнено(КоличествоВложенныхЕдиниц) Тогда
		
		КоличествоВложенныхЕдиницСтрокой = Формат(КоличествоВложенныхЕдиниц, "ЧН=; ЧГ=0");
		Если ПоложениеДесятичнойТочки310X = Неопределено Тогда
			Если ПараметрыРазбораКодаМаркировки.НачинаетсяСоСкобки Тогда
				КодМаркировкиДляПроверки = СтрШаблон("(02)%1(37)%2", GTIN, КоличествоВложенныхЕдиницСтрокой);
			Иначе
				ЭлементыКМ = Новый Массив;
				ЭлементыКМ.Добавить("02");
				ЭлементыКМ.Добавить(GTIN);
				ЭлементыКМ.Добавить("37");
				ЭлементыКМ.Добавить(КоличествоВложенныхЕдиницСтрокой);
				КодМаркировкиДляПроверки = СтрСоединить(ЭлементыКМ);
			КонецЕсли;
		Иначе
			КодМаркировкиДляПроверки = СтрШаблон(
				"(02)%1(37)%2(%3)%4",
				GTIN,
				КоличествоВложенныхЕдиницСтрокой,
				"310" + ПоложениеДесятичнойТочки310X,
				СтрЗаменить(Формат(МассаНеттоВКг, СтрШаблон("ЧЦ=6; ЧДЦ=%1; ЧН=000000; ЧВН=; ЧГ=0;", ПоложениеДесятичнойТочки310X)), ",", ""));
		КонецЕсли;
		ЭтоОбъемноСортовойКод = (КодМаркировки = КодМаркировкиДляПроверки);
		Если ЭтоОбъемноСортовойКод Тогда
			ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.ОбъемноСортовойУчет");
		КонецЕсли;
		
	Иначе
		ЭтоОбъемноСортовойКод = Ложь;
	КонецЕсли;
	
	Если ЭтоОбъемноСортовойКод Тогда
		Индекс = ВидыПродукции.ВГраница();
		Пока Индекс >= 0 Цикл
			Если Не ОбщегоНазначенияИСКлиентСерверПовтИсп.ВидПродукцииПоддерживаетОбъемноСортовойУчет(ВидыПродукции[Индекс]) Тогда
				ВидыПродукции.Удалить(Индекс);
			КонецЕсли;
			Индекс = Индекс - 1;
		КонецЦикла;
		Если ВидыПродукции.Количество() = 0 Тогда
			Возврат Ложь;
		КонецЕсли;
	ИначеЕсли ВозможенКодОСУ Тогда
		Возврат Ложь;
	ИначеЕсли ДлинаКодаМаркировкиБезРазделителей > 0 Тогда
		Если ДлинаКодаМаркировкиБезРазделителей > МаксимальнаяДлинаКодаТранспортнойУпаковки Тогда
			Возврат Ложь;
		КонецЕсли;
		Если ДлинаКодаМаркировкиБезРазделителей < МинимальнаяДлинаКодаТранспортнойУпаковкиАТП Тогда
			ИндексАТП = ВидыПродукции.Найти(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АльтернативныйТабак"));
			Если ИндексАТП <> Неопределено Тогда
				Если ВидыПродукции.Количество() = 1 Тогда
					Возврат Ложь;
				КонецЕсли;
				ВидыПродукции.Удалить(ИндексАТП);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ТипШтрихкодаИВидУпаковки = РазборКодаМаркировкиИССлужебныйКлиентСервер.ТипШтрихкодаИВидУпаковки();
	ТипШтрихкодаИВидУпаковки.ТипШтрихкода = ТипШтрихкода;
	ТипШтрихкодаИВидУпаковки.ВидУпаковки  = ВидУпаковки;
	
	СоставКодаМаркировки = НовыйСоставКодаМаркировки(ТипШтрихкодаИВидУпаковки);
	
	Если ТипШтрихкодаИВидУпаковки.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.ОбъемноСортовойУчет") Тогда
		
		СоставКодаМаркировки.GTIN                             = GTIN;
		СоставКодаМаркировки.EAN                              = РазборКодаМаркировкиИССлужебныйКлиентСерверПовтИсп.ШтрихкодEANИзGTIN(СоставКодаМаркировки.GTIN);
		СоставКодаМаркировки.КоличествоВложенныхЕдиниц        = КоличествоВложенныхЕдиниц;
		СоставКодаМаркировки.ВключаетИдентификаторыПрименения = Истина;
		СоставКодаМаркировки.МассаНеттоВКг                    = МассаНеттоВКг;
	
	ИначеЕсли ТипШтрихкодаИВидУпаковки.ТипШтрихкода = ПредопределенноеЗначение("Перечисление.ТипыШтрихкодов.GS1_128") Тогда
		
		СоставКодаМаркировки.GTIN = GTIN;
		
		Если ЗначениеЗаполнено(СоставКодаМаркировки.GTIN) Тогда
			СоставКодаМаркировки.EAN = РазборКодаМаркировкиИССлужебныйКлиентСерверПовтИсп.ШтрихкодEANИзGTIN(СоставКодаМаркировки.GTIN);
			СоставКодаМаркировки.ВключаетИдентификаторыПрименения = Истина;
		КонецЕсли;
		
		СоставКодаМаркировки.КоличествоВложенныхЕдиниц                 = КоличествоВложенныхЕдиниц;
		СоставКодаМаркировки.Коэффициент                               = Коэффициент;
		СоставКодаМаркировки.МассаНеттоВКг                             = МассаНеттоВКг;
		СоставКодаМаркировки.ВозможныВариантыКоличестваВложенныхЕдиниц = ВозможныВариантыКоличестваВложенныхЕдиниц;
	
	КонецЕсли;
	
	ДанныеРезультата = РазборКодаМаркировкиИССлужебныйКлиентСервер.НовыйРезультатРазбораКодаМаркировки(ПараметрыРазбораКодаМаркировки.ПользовательскиеПараметры.РасширеннаяДетализация);
	ДанныеРезультата.КодМаркировки        = КодМаркировки;
	ДанныеРезультата.ТипШтрихкода         = ТипШтрихкодаИВидУпаковки.ТипШтрихкода;
	ДанныеРезультата.ВидУпаковки          = ТипШтрихкодаИВидУпаковки.ВидУпаковки;
	ДанныеРезультата.ВидыПродукции        = ВидыПродукции;
	ДанныеРезультата.СоставКодаМаркировки = СоставКодаМаркировки;
	
	Если ПараметрыРазбораКодаМаркировки.ПользовательскиеПараметры.РасширеннаяДетализация Тогда
		ДанныеРезультата.Детализация.НачинаетсяСоСкобки                = ПараметрыРазбораКодаМаркировки.НачинаетсяСоСкобки;
		ДанныеРезультата.Детализация.СодержитРазделительGS             = ПараметрыРазбораКодаМаркировки.СодержитРазделительGS;
		ДанныеРезультата.Детализация.ЭтоНеФормализованныйКодМаркировки = Истина;
	КонецЕсли;
	
	Для Каждого ВидПродукции Из ВидыПродукции Цикл
		ДанныеРезультата.ВидыУпаковокПоВидамПродукции[ВидПродукции] =
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТипШтрихкодаИВидУпаковки.ВидУпаковки);
	КонецЦикла;
	
	НормализованныйКодМаркировки = РазборКодаМаркировкиИССлужебныйКлиентСервер.НормализоватьКодМаркировки(
		ДанныеРезультата, ВидыПродукции[0]); // Тут вид продукции не имеет значения
	
	ДанныеРезультата.НормализованныйКодМаркировки = НормализованныйКодМаркировки;
	
	Возврат Истина;
	
КонецФункции

Функция ЭтоНеФормализованныйКодМаркировкиЛогистическойУпаковкиCode128(ПараметрыРазбораКодаМаркировки, Настройки, ДанныеРезультата, РезультатБезФильтра)
	
	// Ограничение согласно документации СУЗ
	МинимальнаяДлинаКодаТранспортнойУпаковки    = 18; // В ЦРПТ логистическая упаковка не может быть меньше 18 символов
	МинимальнаяДлинаКодаТранспортнойУпаковкиАТП = 34;
	МаксимальнаяДлинаКодаТранспортнойУпаковки   = 74;
	
	Если ПараметрыРазбораКодаМаркировки.ПользовательскиеПараметры.ВалидироватьШтрихкодЛогистическойУпаковкиGS1128СОшибками Тогда
		МинимальнаяДлинаКодаТранспортнойУпаковкиАТП = МинимальнаяДлинаКодаТранспортнойУпаковки;
	КонецЕсли;

	Если ПараметрыРазбораКодаМаркировки.ДлинаКодаМаркировки < МинимальнаяДлинаКодаТранспортнойУпаковки Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ПараметрыРазбораКодаМаркировки.ДлинаКодаМаркировки > МаксимальнаяДлинаКодаТранспортнойУпаковки Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ФильтрПоВидуПродукции = ПараметрыРазбораКодаМаркировки.ФильтрПоВидуПродукции;
	Если Не ФильтрПоВидуПродукции.Использовать Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВидыПродукции = Новый Массив;
	Для Каждого ВидПродукции Из ФильтрПоВидуПродукции.ВидыПродукции Цикл
		Если Не ОбщегоНазначенияИСКлиентСервер.ЭтоПродукцияИСМП(ВидПродукции, Истина) Тогда
			Возврат Ложь;
		КонецЕсли;
		Если Настройки.ДоступныеВидыПродукции.Найти(ВидПродукции) <> Неопределено Тогда
			ВидыПродукции.Добавить(ВидПродукции);
		КонецЕсли;
	КонецЦикла;
	
	КодМаркировки = ПараметрыРазбораКодаМаркировки.КодМаркировки;
	
	Если ПараметрыРазбораКодаМаркировки.ДлинаКодаМаркировки < МинимальнаяДлинаКодаТранспортнойУпаковкиАТП Тогда
		ИндексАТП = ВидыПродукции.Найти(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АльтернативныйТабак"));
		Если ИндексАТП <> Неопределено Тогда
			ВидыПродукции.Удалить(ИндексАТП);
		КонецЕсли;
	КонецЕсли;
	
	Если ВидыПродукции.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЭтоCODE128 = Истина;
	Для Счетчик = 1 По ПараметрыРазбораКодаМаркировки.ДлинаКодаМаркировки Цикл
		ВремКодСимвола = КодСимвола(КодМаркировки, Счетчик);
		Если (ВремКодСимвола > 127) Тогда
			ЭтоCODE128 = Ложь;
		Прервать;
		КонецЕсли;
	КонецЦикла; 
	
	Если Не ЭтоCODE128 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТипШтрихкодаИВидУпаковки = РазборКодаМаркировкиИССлужебныйКлиентСервер.ТипШтрихкодаИВидУпаковки();
	ТипШтрихкодаИВидУпаковки.ТипШтрихкода = ПредопределенноеЗначение("Перечисление.ТипыШтрихкодов.Code128");
	ТипШтрихкодаИВидУпаковки.ВидУпаковки  = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.Логистическая");
	
	СоставКодаМаркировки = НовыйСоставКодаМаркировки(ТипШтрихкодаИВидУпаковки);
	
	ДанныеРезультата = РазборКодаМаркировкиИССлужебныйКлиентСервер.НовыйРезультатРазбораКодаМаркировки(ПараметрыРазбораКодаМаркировки.ПользовательскиеПараметры.РасширеннаяДетализация);
	ДанныеРезультата.КодМаркировки        = КодМаркировки;
	ДанныеРезультата.ТипШтрихкода         = ТипШтрихкодаИВидУпаковки.ТипШтрихкода;
	ДанныеРезультата.ВидУпаковки          = ТипШтрихкодаИВидУпаковки.ВидУпаковки;
	ДанныеРезультата.ВидыПродукции        = ВидыПродукции;
	ДанныеРезультата.СоставКодаМаркировки = СоставКодаМаркировки;
	
	Если ПараметрыРазбораКодаМаркировки.ПользовательскиеПараметры.РасширеннаяДетализация Тогда
		ДанныеРезультата.Детализация.НачинаетсяСоСкобки                = ПараметрыРазбораКодаМаркировки.НачинаетсяСоСкобки;
		ДанныеРезультата.Детализация.СодержитРазделительGS             = ПараметрыРазбораКодаМаркировки.СодержитРазделительGS;
		ДанныеРезультата.Детализация.ЭтоНеФормализованныйКодМаркировки = Истина;
	КонецЕсли;
	
	Для Каждого ВидПродукции Из ВидыПродукции Цикл
		ДанныеРезультата.ВидыУпаковокПоВидамПродукции[ВидПродукции] =
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТипШтрихкодаИВидУпаковки.ВидУпаковки);
	КонецЦикла;
	
	ДанныеРезультата.НормализованныйКодМаркировки = КодМаркировки;
	
	Возврат Истина;
	
КонецФункции

Функция СтрокаВЧисло(ЗначениеСтрокой, ПоложениеДесятичнойТочки = 0)
	
	ЗначениеЧислом = Неопределено;
	
	Если ТипЗнч(ЗначениеСтрокой) = Тип("Строка")
		И Не ПустаяСтрока(ЗначениеСтрокой)
		И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ЗначениеСтрокой) Тогда
		
		Значение = ЗначениеСтрокой;
		
		Если ПоложениеДесятичнойТочки > 0 Тогда
			Для Индекс = 0 По ПоложениеДесятичнойТочки - СтрДлина(Значение) Цикл
				Значение = "0" + Значение;
			КонецЦикла;
			Значение = Лев(Значение, СтрДлина(Значение) - ПоложениеДесятичнойТочки) + "." + Прав(Значение, ПоложениеДесятичнойТочки);
		КонецЕсли;
		
		ОписаниеТипаЧисла = Новый ОписаниеТипов("Число");
		ЗначениеЧислом = ОписаниеТипаЧисла.ПривестиЗначение(Значение);
		
	КонецЕсли;
	
	Возврат ЗначениеЧислом;
	
КонецФункции

#КонецОбласти

#Область ПрочиеСлужебныеПроцедурыИФункции

Функция ПрисутствуетТабачнаяПродукция(ВидыПродукции)
	
	Для Каждого ВидПродукции Из ВидыПродукции Цикл
		Если ОбщегоНазначенияИСКлиентСервер.ЭтоПродукцияМОТП(ВидПродукции) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Функция ДлинаСерийногоНомераКодаМаркировки(ВидПродукции, СтандартныйКМ = Неопределено) Экспорт
	
	Если ОбщегоНазначенияИСКлиентСервер.ЭтоПродукцияМОТП(ВидПродукции)
		Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво")
		Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПивоВПотребительскихУпаковках")
		Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.БезалкогольноеПиво") Тогда
		Результат = 7;
	ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Фотоаппараты") Тогда
		Результат = 20;
	ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС")
		Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС")
		Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МорепродуктыПодконтрольныеВЕТИС")
		Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КормаДляЖивотныхПодконтрольныеВЕТИС")
		Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КормаДляЖивотныхБезВЕТИС")
		Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МясоПодконтрольноеВЕТИС")
		Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КонсервированнаяПродукцияПодконтрольнаяВЕТИС")
		Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КонсервированнаяПродукцияБезВЕТИС")
		Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПарфюмерныеИКосметическиеСредстваИБытоваяХимия")
		Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ИгрыИИгрушкиДляДетей")
			И СтандартныйКМ <> Истина Тогда
		Результат = 6;
	ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПечатнаяПродукция") Тогда
		Результат = ПечатнаяПродукцияДлинаСерийногоНомера();
	ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.СтроительныеМатериалы") Тогда
		Результат = СтроительныеМатериалыДлинаСерийногоНомера();
	ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ОтопительныеПриборы") Тогда
		Результат = ОтопительныеПриборыДлинаСерийногоНомера();
	ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Бакалея") Тогда
		Результат = БакалеяДлинаСерийногоНомера();
	ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АлкогольнаяПродукцияДо9Процентов") Тогда
		Результат = АлкогольнаяПродукцияДо9ПроцентовДлинаСерийногоНомера();
	ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ТелефоныИНоутбуки") Тогда
		Результат = ТелефоныИНоутбукиДлинаСерийногоНомера();
	ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПиротехническиеИзделияИСредстваПожарнойБезопасности") Тогда
		Результат = ПиротехническиеИзделияИСредстваПожарнойБезопасностиДлинаСерийногоНомера();
	ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КабельнаяПродукция") Тогда
		Результат = КабельнаяПродукцияДлинаСерийногоНомера();
	ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МоторныеМасла") Тогда
		Результат = МоторныеМаслаДлинаСерийногоНомера();
	Иначе
		Результат = 13;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область НовыеТоварныеГруппыВнедрение

#Область ДлинаСерийногоНомера

Функция ПечатнаяПродукцияДлинаСерийногоНомера()
	Возврат 13;
КонецФункции

Функция СтроительныеМатериалыДлинаСерийногоНомера()
	Возврат 13;
КонецФункции

Функция ОтопительныеПриборыДлинаСерийногоНомера()
	Возврат 6;
КонецФункции

Функция БакалеяДлинаСерийногоНомера()
	Возврат 13;
КонецФункции

Функция АлкогольнаяПродукцияДо9ПроцентовДлинаСерийногоНомера()
	Возврат 13;
КонецФункции

Функция ТелефоныИНоутбукиДлинаСерийногоНомера()
	Возврат 13;
КонецФункции

Функция ПиротехническиеИзделияИСредстваПожарнойБезопасностиДлинаСерийногоНомера()
	Возврат 6;
КонецФункции

Функция КабельнаяПродукцияДлинаСерийногоНомера()
	Возврат 13;
КонецФункции

Функция МоторныеМаслаДлинаСерийногоНомера()
	Возврат 6;
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти