
// ПартионныйУчет - модуль оффлайнового (неоперативного) расчета регистров партий
// 
// Заметки по концепции:
//
// * Партии.Первичное = Истина означает, что запись не будет разбита, но может быть дополнена данными.
// Если Партии.Первичное = Ложь, то запись может быть перезаписана, разбита на записи, удалена и т.д.
//
// * Партии.ДокументИсточник показывает документ возникновения остатка.
// Для расходов текущего периода это будет документ поступления или перемещения на организацию/склад,
// для приходов перемещений/пересортиц и прочих расходно-приходных документов это будет сам документ
// перемещения, для сторнирующих документов это будет документ реализации.
// При расходах по остаткам прошлого периода это будет документ партии (поступления).
// Реквизит нужен для протягивания партий затрат и партий расходов на с/с по движениям перемещений.
//
// * Записи с типом "Остаток" никогда не пишутся в базу.
////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Выполняет полный расчет партий.
//
// Параметры:
//	ОкончаниеПериодаРасчета - Дата - конец периода (месяца), до которого надо выполнить расчет
//	Организация - СправочникСсылка.Организации - организация, по которым надо выполнить расчет.
//	ВыполняетсяЗакрытиеМесяца - Булево - признак выполнения регламентных операций по закрытию месяца
//
Процедура РассчитатьВсе(Знач ОкончаниеПериодаРасчета, Организация = Неопределено, ВыполняетсяЗакрытиеМесяца = Ложь, АвтоматическоеТестирование = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИдентификаторРасчета = Новый УникальныйИдентификатор;
	
	// Проверим, закончено ли обновление ИБ
	РасчетСебестоимостиПрикладныеАлгоритмы.ПроверитьБлокировкуДанныхПриОбновленииИБ(
		Ложь,
		РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии22(НачалоМесяца(ОкончаниеПериодаРасчета)));
	
	УровеньИнформация = УровеньЖурналаРегистрации.Информация;
	ФорматПериода = "ДЛФ=D";
	КодЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
	
	НомерЗаданияДоРасчета = РасчетСебестоимостиПрикладныеАлгоритмы.УвеличитьНомерЗаданияКРасчетуСебестоимости();
	
	#Область СхемаРасчета
	
	НачатьТранзакцию();
	
	Попытка
		
		// Определим по каким периодам и организациям требуется перерасчет.
		СхемаРасчета = РасчетСебестоимостиПрикладныеАлгоритмы.СхемаРасчетаПартий(
			ОкончаниеПериодаРасчета,
			Организация,
			НомерЗаданияДоРасчета);
			
		Если СхемаРасчета.Количество() > 0 Тогда
			ВременныеТаблицы = РасчетСебестоимостиПрикладныеАлгоритмы.СформироватьВТЗаданияДоРасчета(
				НомерЗаданияДоРасчета,
				СхемаРасчета[СхемаРасчета.Количество() - 1].Организации);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
	КонецПопытки;
		
	Если СхемаРасчета.Количество() = 0 Тогда
		
		// Расчет не требуется (ни в версии 2.1, ни в версии 2.2)
		Комментарий = НСтр("ru = 'Расчет партий по %ОкончаниеПериода% не требуется - партии уже рассчитаны'", КодЯзыка);
		Комментарий = СтрЗаменить(Комментарий, "%ОкончаниеПериода%", Формат(ОкончаниеПериодаРасчета, ФорматПериода));
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Партионный учет.Расчет не требуется'", КодЯзыка),
			УровеньИнформация,
			,
			ОкончаниеПериодаРасчета,
			Комментарий);
		
		Возврат;
		
	КонецЕсли;
	
	ФактическоеОкончаниеПериодаРасчета = ОкончаниеПериодаРасчета;
	
	Если РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии22(НачалоМесяца(ОкончаниеПериодаРасчета)) Тогда
		
		ДатаПереходаНаПартионныйУчетВерсии22 = РасчетСебестоимостиПовтИсп.ДатаПереходаНаПартионныйУчетВерсии22();
		Если ЗначениеЗаполнено(ДатаПереходаНаПартионныйУчетВерсии22) Тогда
			Если ОкончаниеПериодаРасчета >= ДатаПереходаНаПартионныйУчетВерсии22 Тогда
				// Этим механизмом (версии 2.1) можно рассчитать партии только до даты перехода
				ОкончаниеПериодаРасчета = ДатаПереходаНаПартионныйУчетВерсии22 - 1;
			КонецЕсли;
		Иначе
			ОкончаниеПериодаРасчета = Дата(1, 1, 1); // расчет в версии 2.1 не требуется - будет выполнен расчет в версии 2.2
		КонецЕсли;
		
	КонецЕсли;
	
	НеобходимРасчет = (НачалоМесяца(СхемаРасчета[0].Дата) <= НачалоМесяца(ОкончаниеПериодаРасчета));
	РассчитанныеСтрокиСхемы = Новый Массив;
	
	#КонецОбласти
	
	Если НеобходимРасчет Тогда
		
		ПартионныйУчетВключен = РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии21(НачалоМесяца(СхемаРасчета[0].Дата));
		
		ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("ЗакрытиеМесяца.РасчетПартийИСебестоимости");
		КоличествоПовторяющихсяДанных = 0;
		
		Комментарий = НСтр("ru = 'Выполняется расчет партий версии 2.1 по %ОкончаниеПериода%'", КодЯзыка);
		Комментарий = СтрЗаменить(Комментарий, "%ОкончаниеПериода%", Формат(ОкончаниеПериодаРасчета, ФорматПериода));
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Партионный учет.Начало расчета'", КодЯзыка), УровеньИнформация, , ОкончаниеПериодаРасчета, Комментарий);
		
		Для Каждого СтрокаСхемыРасчета Из СхемаРасчета Цикл
			
			Если НЕ РасчетСебестоимостиПовтИсп.ВозможенРасчетСебестоимости(НачалоМесяца(СтрокаСхемыРасчета.Дата)) Тогда
				
				РегистрыСведений.ЗаданияКРасчетуСебестоимости.ОчиститьЗаписиЗаПериод(
					НачалоМесяца(СтрокаСхемыРасчета.Дата),
					КонецМесяца(СтрокаСхемыРасчета.Дата),
					СтрокаСхемыРасчета.Организации);
				
				Продолжить;
				
			КонецЕсли;
			
			Если НачалоМесяца(СтрокаСхемыРасчета.Дата) > НачалоМесяца(ОкончаниеПериодаРасчета) Тогда
				// Дальше необходим расчет партий версии 2.2
				Прервать;
			КонецЕсли;
			
			Если НЕ ВыполняетсяЗакрытиеМесяца Тогда
				// Выполним операции механизма закрытиям месяца, вызываемые до расчета этапа.
				Если НЕ РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьОперацииЗакрытияМесяцаДляПодготовкиКРасчетуЭтапа(СтрокаСхемыРасчета, ИдентификаторРасчета) Тогда
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			НачалоПериода     = НачалоМесяца(СтрокаСхемыРасчета.Дата);
			ОкончаниеПериода  = КонецМесяца(СтрокаСхемыРасчета.Дата);
			МассивОрганизаций = СтрокаСхемыРасчета.Организации;
			
			// Выполним корректировки начальных остатков регистров партионного учета.
			КорректировкаНачальныхОстатковРегистраСебестоимостьТоваров(НачалоПериода, МассивОрганизаций, ПартионныйУчетВключен);
			
			Если АвтоматическоеТестирование Тогда
				ПерепровестиДокументыПоРегистрам(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
			КонецЕсли;
			

			// Исправление некорректных движений
			ИсправитьДвиженияВозвратТоваровМеждуОрганизациямиПоТоварамОрганизаций(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);

			// Устранение проблем первичных движений по себестоимости товаров
			ИсправитьДвиженияПеремещенийТоваровПоСебестоимостиТоваров(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
			ВосстановитьДвиженияПрочегоОприходованияТоваровПоСебестоимостиТоваров(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
			ВосстановитьДвиженияПоСебестоимостиТоваровСформированныеВВерсииПУ22(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
			ЗаполнитьКорОрганизациюВОперацияхРеглУчет(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
			ИсправитьКорОрганизациюВВозвратеМеждуОрганизациями(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
			ИсправитьКорОрганизациюВИсправлениеРазвернутогоСальдо(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
			ЗаполнитьВидыЗапасовИНазначенияВСебестоимостиТоваров(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
			ИсправитьДвиженияПриходовДопРасходовПоПрочимРасходам(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
			
			Если ПартионныйУчетВключен Тогда
				Документы.КорректировкаНалогообложенияНДСПартийТоваров.ОтменитьКорректировкуНалогообложенияНДС(ОкончаниеПериода);
				// Этап 1Х: расчет партий товаров
				РассчитатьПартииТоваров(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, КоличествоПовторяющихсяДанных);
				ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоПовторяющихсяДанных, "РассчитатьПартииТоваров");
				КоличествоПовторяющихсяДанных = 0;
				
				СозданаКорректировка = Ложь;
				Документы.КорректировкаНалогообложенияНДСПартийТоваров.СкорректироватьНалогообложениеНДС(ОкончаниеПериода, СозданаКорректировка);
				Если СозданаКорректировка Тогда
					РассчитатьПартииТоваров(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
				КонецЕсли;
				// Этап 16: расчет партий переданных на комиссию
				РассчитатьПартииПереданные(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, КоличествоПовторяющихсяДанных);
				ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоПовторяющихсяДанных, "РассчитатьПартииПереданные");
				КоличествоПовторяющихсяДанных = 0;
				// Этап 18: расчет партий производственных затрат.
				
				СформироватьДвиженияПоНДС(ОкончаниеПериода, МассивОрганизаций, КоличествоПовторяющихсяДанных);
				ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоПовторяющихсяДанных, "СформироватьДвиженияПоНДС");
				КоличествоПовторяющихсяДанных = 0;
				РассчитатьПартииПроизводства(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, КоличествоПовторяющихсяДанных);
				ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоПовторяющихсяДанных, "РассчитатьПартииПроизводства");
				КоличествоПовторяющихсяДанных = 0;

				// Этап 2Х: расчет партий затрат на выпуск
				РассчитатьПартииЗатрат(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, КоличествоПовторяющихсяДанных);
				ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоПовторяющихсяДанных, "РассчитатьПартииЗатрат");
				КоличествоПовторяющихсяДанных = 0;
				// Этапы 3Х и 4Х: расчет партий прочих расходов на себестоимость товаров, партионный учет включен.
				РассчитатьПартииПрочих(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, КоличествоПовторяющихсяДанных); // ПУ 2.2 этап 11
				ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоПовторяющихсяДанных, "РассчитатьПартииПрочих");
				КоличествоПовторяющихсяДанных = 0;
				// Этап 5Х: расчет партий расходов на себестоимость выбытия товаров
				РассчитатьПартииРасходов(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, КоличествоПовторяющихсяДанных);
				ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоПовторяющихсяДанных, "РассчитатьПартииРасходов");
				КоличествоПовторяющихсяДанных = 0;
				// Этап 64: расчет приходов партий НДС к распределению
				РассчитатьПриходыПартийНДСКРаспределению(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, КоличествоПовторяющихсяДанных);
				ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоПовторяющихсяДанных, "РассчитатьПриходыПартийПрочихРасходов");
				КоличествоПовторяющихсяДанных = 0;
			Иначе
				// Этап 2Х: расчет партий затрат на выпуск
				
				СформироватьДвиженияПоНДС(ОкончаниеПериода, МассивОрганизаций, КоличествоПовторяющихсяДанных);
				ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоПовторяющихсяДанных, "СформироватьДвиженияПоНДС");
				КоличествоПовторяющихсяДанных = 0;
				РассчитатьПартииПроизводства(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, КоличествоПовторяющихсяДанных);
				ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоПовторяющихсяДанных, "РассчитатьПартииПроизводства");
				КоличествоПовторяющихсяДанных = 0;

				// Этапы 3Х и 4Х: расчет партий прочих расходов на себестоимость товаров при выключенном партионном учете.
				РассчитатьПартииПрочих(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, КоличествоПовторяющихсяДанных);
				ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоПовторяющихсяДанных, "РассчитатьПартииПрочих");
				КоличествоПовторяющихсяДанных = 0;
			КонецЕсли;
			
			// Этап 7Х: Выполнение расчета себестоимости
			РассчитатьСебестоимость(ОкончаниеПериода, МассивОрганизаций, СтрокаСхемыРасчета.ИзмененоДокументов, ОписаниеЗамера);
			
			// Этап 8Х: Запись новых границ рассчитанных партий - завершение партионного расчета за период.
			УстановитьГраницыРассчитанногоПериода(НачалоПериода, МассивОрганизаций, НомерЗаданияДоРасчета, ВременныеТаблицы);
			
			ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоПовторяющихсяДанных, "РассчитатьСебестоимость");
			КоличествоПовторяющихсяДанных = 0;
			
			РассчитанныеСтрокиСхемы.Добавить(СтрокаСхемыРасчета);
			
			Если НЕ ВыполняетсяЗакрытиеМесяца Тогда
				// Выполним операции механизма закрытиям месяца, вызываемые после расчета этапа.
				РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьОперацииЗакрытияМесяцаДляЗавершенияРасчетаЭтапа(СтрокаСхемыРасчета, ИдентификаторРасчета);
			КонецЕсли;
			
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Партионный учет.Рассчитан период'", КодЯзыка), УровеньИнформация, , Формат(НачалоПериода, "ДФ='ММ.гггг'"));
			
		КонецЦикла;
		
		ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, 1);
		
		Комментарий = НСтр("ru = 'Выполнен расчет с %НачалоПериода% по %ОкончаниеПериода%'", КодЯзыка);
		Комментарий = СтрЗаменить(Комментарий, "%НачалоПериода%",    Формат(НачалоМесяца(СхемаРасчета[0].Дата), ФорматПериода));
		Комментарий = СтрЗаменить(Комментарий, "%ОкончаниеПериода%", Формат(ОкончаниеПериодаРасчета, ФорматПериода));
		
		Если ЗначениеЗаполнено(Организация) Тогда
			Комментарий = Комментарий + "
				|" + НСтр("ru='Организация: %Организация%'");
			Комментарий = СтрЗаменить(Комментарий,
				"%Организация%", 
				РасчетСебестоимостиПрикладныеАлгоритмы.ПредставлениеОрганизаций(Организация,, Истина));
		КонецЕсли;
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Партионный учет.Окончание расчета'", КодЯзыка), УровеньИнформация, , ОкончаниеПериодаРасчета, Комментарий);
		
	КонецЕсли;
	
	Если ФактическоеОкончаниеПериодаРасчета > ОкончаниеПериодаРасчета Тогда
		
		// Удалим рассчитанные строки из схемы расчета.
		Для Каждого СтрокаСхемыРасчета Из РассчитанныеСтрокиСхемы Цикл
			СхемаРасчета.Удалить(СтрокаСхемыРасчета);
		КонецЦикла;
		
		// Оставшиеся периоды рассчитаем механизмом версии 2.2.
		ПараметрыЗапуска = Новый Структура;
		ПараметрыЗапуска.Вставить("Дата", 				ФактическоеОкончаниеПериодаРасчета);
		ПараметрыЗапуска.Вставить("МассивОрганизаций",  Организация);
		ПараметрыЗапуска.Вставить("СхемаРасчета", 		СхемаРасчета);
		ПараметрыЗапуска.Вставить("ВременныеТаблицы",   ВременныеТаблицы);
		ПараметрыЗапуска.Вставить("МестоВызоваРасчета", "ПартионныйУчет.РассчитатьВсе");
		
		РасчетСебестоимости.РассчитатьВсеВПопыткеИсключении(ПараметрыЗапуска);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЭтапыРасчета

// Этап 1Х: расчет партий товаров
Процедура РассчитатьПартииТоваров(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, КоличествоЗаписей = 0)
	ИмяРегистра = Метаданные.РегистрыНакопления.ПартииТоваровОрганизаций.Имя;
	
	НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	// ФАЗА 0:


	// Восстановление "потерянных" первичных движений по партиям товаров
	ВосстановитьДвиженияПоПартиямТоваров(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
	// ФАЗА 10: Выборка исходных данных для расчета партий товаров организаций
	ДанныеДляПартийТоваров = ДанныеДляПартийТоваров(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
	// ФАЗА 12: Расчет партий товаров по исходным данным
	МассивИсключаемыхТипов = Новый Массив;
	МассивИсключаемыхТипов.Добавить(Тип("ДокументСсылка.КорректировкаРегистров"));
	Регистраторы = Регистраторы(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, ИмяРегистра, МассивИсключаемыхТипов);
	РегистраторыССохраняемымиДвижениями = РегистраторыССохраняемымиДвижениями(ИмяРегистра, ДанныеДляПартийТоваров, Регистраторы, НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
	РасчетныеПартииТоваров = РасчетныеПартииТоваров(ДанныеДляПартийТоваров, Регистраторы);
	// ФАЗА 14: Запись регистра ПартииТоваровОрганизаций
	ЗаписатьРасчетныеПартии(РегистрыНакопления[ИмяРегистра], РасчетныеПартииТоваров, Регистраторы);
	ОчиститьРегистрНаСервере(Регистраторы, ИмяРегистра, РегистраторыССохраняемымиДвижениями);
	ЗаписатьСохраняемыеДвижения(РегистрыНакопления[ИмяРегистра], ДанныеДляПартийТоваров, РегистраторыССохраняемымиДвижениями);
	// Завершение этапа
	КоличествоЗаписей = КоличествоЗаписей(ДанныеДляПартийТоваров);
	ДанныеДляПартийТоваров.Закрыть();
	РасчетныеПартииТоваров = Неопределено;
	Замер = ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЗамера;
	Комментарий = НСтр("ru = 'Рассчитано %КолвоСтрок% стр. за %Замер% сек.'", ОбщегоНазначения.КодОсновногоЯзыка());
	Комментарий = СтрЗаменить(Комментарий, "%КолвоСтрок%", КоличествоЗаписей);
	Комментарий = СтрЗаменить(Комментарий, "%Замер%", Формат(Замер/1000., "ЧДЦ=3; ЧН=; ЧГ="));
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Партионный учет.ПартииТоваровОрганизаций'",	ОбщегоНазначения.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Информация,,, Комментарий);
КонецПроцедуры

// Этап 16: расчет партий переданных на комиссию
Процедура РассчитатьПартииПереданные(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, КоличествоЗаписей = 0)
	ИмяРегистра = Метаданные.РегистрыНакопления.ПартииТоваровПереданныеНаКомиссию.Имя;
	
	// ФАЗА 10: Выборка исходных данных для расчета партий товаров организаций
	ДанныеДляПартийПереданных = ДанныеДляПартийПереданных(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
	// ФАЗА 12: Расчет партий товаров по исходным данным
	Регистраторы = Регистраторы(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, ИмяРегистра);
	РегистраторыПартийВПути(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, Регистраторы);
	РегистраторыССохраняемымиДвижениями = РегистраторыССохраняемымиДвижениями(ИмяРегистра, ДанныеДляПартийПереданных, Регистраторы, НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
	РасчетныеПартииПереданные = РасчетныеПартииПереданные(ДанныеДляПартийПереданных, Регистраторы, Неопределено, Ложь, КоличествоЗаписей);
	// ФАЗА 14: Запись регистра ПартииТоваровОрганизаций
	ЗаписатьРасчетныеПартии(РегистрыНакопления[ИмяРегистра], РасчетныеПартииПереданные, Регистраторы);
	ОчиститьРегистрНаСервере(Регистраторы, ИмяРегистра, РегистраторыССохраняемымиДвижениями);
	ЗаписатьСохраняемыеДвижения(РегистрыНакопления[ИмяРегистра], ДанныеДляПартийПереданных, РегистраторыССохраняемымиДвижениями);
	// Завершение этапа
	ДанныеДляПартийПереданных.Закрыть();
	РасчетныеПартииПереданные = Неопределено;
КонецПроцедуры


// Этап 18-1: расчет партий производственных затрат
Процедура РассчитатьПартииПроизводства(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, КоличествоЗаписей = 0)
	ИмяРегистра = Метаданные.РегистрыНакопления.ПартииПроизводственныхЗатрат.Имя;
	
	НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();

	// ФАЗА 11: Выборка данных партий производственных затрат
	ДанныеДляПартийПроизводства = ДанныеДляПартийПроизводства(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
	// ФАЗА 12: Расчет партий производственных затрат по исходным данным
	Регистраторы = Регистраторы(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, ИмяРегистра);
	РегистраторыССохраняемымиДвижениями = РегистраторыССохраняемымиДвижениями(ИмяРегистра, ДанныеДляПартийПроизводства, Регистраторы, НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
	РасчетныеПартииПроизводства = РасчетныеПартииПроизводства(ДанныеДляПартийПроизводства, Регистраторы);
	// ФАЗА 14: запись партий производственных затрат
	ЗаписатьРасчетныеПартии(РегистрыНакопления[ИмяРегистра], РасчетныеПартииПроизводства, Регистраторы);
	ОчиститьРегистрНаСервере(Регистраторы, ИмяРегистра, РегистраторыССохраняемымиДвижениями);
	ЗаписатьСохраняемыеДвижения(РегистрыНакопления[ИмяРегистра], ДанныеДляПартийПроизводства, РегистраторыССохраняемымиДвижениями);
	// Завершение этапа
	КоличествоЗаписей = КоличествоЗаписей(ДанныеДляПартийПроизводства);
	ДанныеДляПартийПроизводства.Закрыть();
	РасчетныеПартииПроизводства = Неопределено;
	Замер = ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЗамера;
	Комментарий = НСтр("ru = 'Рассчитано %КолвоСтрок% стр. за %Замер% сек.'", ОбщегоНазначения.КодОсновногоЯзыка());
	Комментарий = СтрЗаменить(Комментарий, "%КолвоСтрок%", КоличествоЗаписей);
	Комментарий = СтрЗаменить(Комментарий, "%Замер%", Формат(Замер/1000., "ЧДЦ=3; ЧН=; ЧГ="));
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Партионный учет.ПартииПроизводственныхЗатрат'",	ОбщегоНазначения.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Информация,,, Комментарий);
КонецПроцедуры


// Этап 2Х: расчет партий затрат на выпуск
Процедура РассчитатьПартииЗатрат(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, КоличествоЗаписей = 0)
	ИмяРегистра = Метаданные.РегистрыНакопления.ПартииЗатратНаВыпуск.Имя;
	
	НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	// ФАЗА 20: Выборка исходных данных для расчета партий затрат на выпуск
	ДанныеДляПартийЗатрат = ДанныеДляПартийЗатрат(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
	// ФАЗА 22: Расчет партий затрат по исходным данным
	Регистраторы = Регистраторы(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, ИмяРегистра);
	РегистраторыПартийВПути(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, Регистраторы);
	РегистраторыССохраняемымиДвижениями = РегистраторыССохраняемымиДвижениями(ИмяРегистра, ДанныеДляПартийЗатрат, Регистраторы, НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
	РасчетныеПартииЗатрат = РасчетныеПартииЗатрат(ДанныеДляПартийЗатрат, Регистраторы);
	// ФАЗА 24: Запись регистра ПартииЗатратНаВыпуск
	ЗаписатьРасчетныеПартии(РегистрыНакопления[ИмяРегистра], РасчетныеПартииЗатрат, Регистраторы);
	ОчиститьРегистрНаСервере(Регистраторы, ИмяРегистра, РегистраторыССохраняемымиДвижениями);
	ЗаписатьСохраняемыеДвижения(РегистрыНакопления[ИмяРегистра], ДанныеДляПартийЗатрат, РегистраторыССохраняемымиДвижениями);
	// Завершение этапа
	КоличествоЗаписей = КоличествоЗаписей(ДанныеДляПартийЗатрат);
	ДанныеДляПартийЗатрат.Закрыть();
	РасчетныеПартииЗатрат = Неопределено;
	Замер = ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЗамера;
	Комментарий = НСтр("ru = 'Рассчитано %КолвоСтрок% стр. за %Замер% сек.'", ОбщегоНазначения.КодОсновногоЯзыка());
	Комментарий = СтрЗаменить(Комментарий, "%КолвоСтрок%", КоличествоЗаписей);
	Комментарий = СтрЗаменить(Комментарий, "%Замер%", Формат(Замер/1000., "ЧДЦ=3; ЧН=; ЧГ="));
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Партионный учет.ПартииЗатратНаВыпуск'",ОбщегоНазначения.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Информация,,, Комментарий);
КонецПроцедуры

// Этапы 3Х и 4Х: расчет партий прочих расходов на себестоимость товаров, партионный учет включен.
Процедура РассчитатьПартииПрочих(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, КоличествоЗаписей = 0)
	
	ПартионныйУчетВключен = РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии21(НачалоПериода);
// Область РасчетПартийПрочих
	// ФАЗА 30: Выборка исходных данных для расчета списания партий прочих расходов.
	Регистраторы = РегистраторыПартийПрочих(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
	Если ПартионныйУчетВключен Тогда
		ДанныеДляПартийПрочих = ДанныеДляПартийПрочих(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
	Иначе
		ДанныеДляПартийПрочих = ДанныеДляПартийПрочихСебестоимость(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
	КонецЕсли;
	// ФАЗА 32: Расчет расхода ПартииПрочихРасходов
	РасчетныеПартииПрочих = РасчетныеПартииПрочих(ДанныеДляПартийПрочих, Неопределено, Неопределено, Ложь, КоличествоЗаписей);
	
	ЗаписатьРасчетныеПартии(РегистрыНакопления.ПартииПрочихРасходов, РасчетныеПартииПрочих, Неопределено);
	
// Область РасчетПриходыРасходовСебестоимость
	// ФАЗА 40: Расчет прихода в ПартииРасходовНаСебестоимостьТоваров.
	РасчетныеПриходыПартийРасходов = РасчетныеПриходыПартийРасходов(ДанныеДляПартийПрочих, Неопределено);
	ДанныеДляПартийПрочих = Неопределено;
	
	// Фаза 42: Расчет приходов в прочие расходы
	РасчетныеДвиженияПрочихРасходов = РасчетныеДвиженияПрочихРасходов(НачалоПериода, РасчетныеПриходыПартийРасходов, РасчетныеПартииПрочих);
	
	ЗаписатьРасчетныеПартии(РегистрыНакопления.ПрочиеРасходы, РасчетныеДвиженияПрочихРасходов, Неопределено);
	РасчетныеДвиженияПрочихРасходов = Неопределено;
	РасчетныеПартииПрочих = Неопределено;
	
	// Фаза 43: Расчет приходов в партии НДС к распределению
	РасчетныеПартииНДСКРаспределению = РасчетныеПриходыПартийНДСКРаспределению(НачалоПериода, РасчетныеПриходыПартийРасходов);
	
	ЗаписатьРасчетныеПартии(РегистрыНакопления.ПартииНДСКРаспределению, РасчетныеПартииНДСКРаспределению, Неопределено);
	РасчетныеПартииНДСКРаспределению = Неопределено;
	
	// ФАЗА 44: Расчет (трансляция приходов партий расходов) приходов в себестоимость товаров.
	РасчетныеПриходыСебестоимости = РасчетныеПриходыСебестоимости(НачалоПериода, РасчетныеПриходыПартийРасходов, Регистраторы);
	
	// ФАЗА 45: Запись прихода в СебестоимостьТоваров
	ЗаписатьРасчетныеПартии(РегистрыНакопления.СебестоимостьТоваров, РасчетныеПриходыСебестоимости, Неопределено);
	
	// ФАЗА 46: Запись прихода в ПартииРасходовНаСебестоимостьТоваров
	Если ПартионныйУчетВключен Тогда
		УдалитьНезаписываемыеСтроки("ПартииРасходовНаСебестоимостьТоваров", РасчетныеПриходыПартийРасходов);		
		ЗаписатьРасчетныеПартии(РегистрыНакопления.ПартииРасходовНаСебестоимостьТоваров, РасчетныеПриходыПартийРасходов, Неопределено);
	КонецЕсли;
	РасчетныеПриходыПартийРасходов = Неопределено;
	
	РасчетныеПриходыСебестоимости = Неопределено;
	
// Область РасчетЗаписейНоменклатураДоходыРасходы
	// ФАЗА 47: Выборка исходных данных для движений в НоменклатураДоходыРасходы.
	ДанныеДляНоменклатураДоходыРасходы = ДанныеДляНоменклатураДоходыРасходы(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
	// ФАЗА 48: Запись движений в НоменклатураДоходыРасходы
	ЗаписатьРасчетныеПартии(РегистрыНакопления.ДвиженияНоменклатураДоходыРасходы, ДанныеДляНоменклатураДоходыРасходы, Неопределено);
	КоличествоЗаписей = КоличествоЗаписей + ДанныеДляНоменклатураДоходыРасходы.Количество();
	ДанныеДляНоменклатураДоходыРасходы = Неопределено;
	
	МассивПоддерживаемыхТипов = Новый Массив;
	МассивПоддерживаемыхТипов.Добавить(Тип("ДокументСсылка.СчетФактураНалоговыйАгент"));
	МассивПоддерживаемыхТипов.Добавить(Тип("ДокументСсылка.СчетФактураПолученныйНалоговыйАгент"));
	РегистраторыДвиженияНоменклатураДоходыРасходы = Регистраторы(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, Метаданные.РегистрыНакопления.ДвиженияНоменклатураДоходыРасходы.Имя,,
		МассивПоддерживаемыхТипов);
	ОчиститьРегистрНаСервере(РегистраторыДвиженияНоменклатураДоходыРасходы, "ДвиженияНоменклатураДоходыРасходы");
КонецПроцедуры

// Этап 5Х: расчет партий расходов на себестоимость выбытия товаров
Процедура РассчитатьПартииРасходов(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, КоличествоЗаписей = 0)
	ИмяРегистра = Метаданные.РегистрыНакопления.ПартииРасходовНаСебестоимостьТоваров.Имя;
	
	НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	// ФАЗА 50: Выборка исходных данных для расчета списания партий расходов на себестоимость товаров.
	ДанныеДляПартийРасходов = ДанныеДляПартийРасходов(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
	// ФАЗА 52: Расчет расхода ПартииРасходовНаСебестоимостьТоваров
	Регистраторы = Регистраторы(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, ИмяРегистра);
	РегистраторыПартийВПути(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, Регистраторы);
	РегистраторыССохраняемымиДвижениями = РегистраторыССохраняемымиДвижениями(ИмяРегистра, ДанныеДляПартийРасходов, Регистраторы, НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
	РасчетныеПартииРасходов = РасчетныеПартииРасходов(ДанныеДляПартийРасходов, Регистраторы);
	// ФАЗА 54: Запись расхода ПартииРасходовНаСебестоимостьТоваров
	ЗаписатьРасчетныеПартии(РегистрыНакопления[ИмяРегистра], РасчетныеПартииРасходов, Регистраторы);
	ОчиститьРегистрНаСервере(Регистраторы, ИмяРегистра, РегистраторыССохраняемымиДвижениями);
	ЗаписатьСохраняемыеДвижения(РегистрыНакопления[ИмяРегистра], ДанныеДляПартийРасходов, РегистраторыССохраняемымиДвижениями);
	// Завершение этапа
	КоличествоЗаписей = КоличествоЗаписей(ДанныеДляПартийРасходов);
	ДанныеДляПартийРасходов.Закрыть();
	РасчетныеПартииРасходов = Неопределено;
	Замер = ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЗамера;
	Комментарий = НСтр("ru = 'Рассчитано %КолвоСтрок% стр. за %Замер% сек.'", ОбщегоНазначения.КодОсновногоЯзыка());
	Комментарий = СтрЗаменить(Комментарий, "%КолвоСтрок%", КоличествоЗаписей);
	Комментарий = СтрЗаменить(Комментарий, "%Замер%", Формат(Замер/1000., "ЧДЦ=3; ЧН=; ЧГ="));
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Партионный учет.ПартииРасходовНаСебестоимостьТоваров'",ОбщегоНазначения.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Информация,,, Комментарий);
КонецПроцедуры

// Этап 64: расчет приходов партий прочих расходов
Процедура РассчитатьПриходыПартийНДСКРаспределению(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, КоличествоЗаписей)
	ИмяРегистра = Метаданные.РегистрыНакопления.ПартииНДСКРаспределению.Имя;
	
	// ФАЗА 60: выборка данных для приходов партий прочих расходов
	ДанныеДляПартийНДСКРаспределению = ДанныеДляПартийНДСКРаспределению(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
	// ФАЗА 62: запись книги продаж
	ЗаписатьРасчетныеПартии(РегистрыНакопления[ИмяРегистра], ДанныеДляПартийНДСКРаспределению, Неопределено);
	КоличествоЗаписей = ДанныеДляПартийНДСКРаспределению.Количество();
	// Завершение этапа
	ДанныеДляПартийНДСКРаспределению = Неопределено;
КонецПроцедуры
	
Процедура СформироватьДвиженияПоНДС(ОкончаниеПериода, МассивОрганизаций, КоличествоЗаписей = 0)
	
	// Корректировка и распределение НДС
	РасчетСебестоимостиНДС.СформироватьЗаданияДляМеханизмовУчетаНДСПартионныйУчет21(ОкончаниеПериода, МассивОрганизаций, Ложь);
	
	РезультатРаспределенияНДС = Документы.РаспределениеНДС.РаспределитьНДС(
		ОкончаниеПериода,
		МассивОрганизаций,
		НСтр("ru = 'Партионный учет.Распределение НДС.'", ОбщегоНазначения.КодОсновногоЯзыка()),
		,
		Истина);
	
	// Значение "РезультатРаспределенияНДС.ТекстОшибки" не проверяем - продолжим выполнение в любом случае.
	КоличествоЗаписей = КоличествоЗаписей + РезультатРаспределенияНДС.МассивДокументов.Количество();
	
КонецПроцедуры

Процедура РассчитатьСебестоимость(ОкончаниеПериода, МассивОрганизаций, ИзмененоДокументов, ОписаниеЗамера)
	
	ВосстановитьДвиженияДокументовСДублямиДвиженийПоВыручкеИСебестоимостиПродаж(
		НачалоМесяца(ОкончаниеПериода),
		ОкончаниеПериода,
		МассивОрганизаций);
		
	ВосстановитьДвиженияПоВыручкеИСебестоимостиПродажСформированныеВВерсииПУ22(
		НачалоМесяца(ОкончаниеПериода),
		ОкончаниеПериода,
		МассивОрганизаций);
		
	ЗаполнитьВидыЗапасовИНазначенияВВыручкеИСебестоимостиПродаж(
		НачалоМесяца(ОкончаниеПериода),
		ОкончаниеПериода,
		МассивОрганизаций);
	
	ПараметрыЗапускаРасчетаСебестоимости = Новый Структура;
	ПараметрыЗапускаРасчетаСебестоимости.Вставить("Дата", 					ОкончаниеПериода);
	ПараметрыЗапускаРасчетаСебестоимости.Вставить("ПредварительныйРасчет", 	Ложь);
	ПараметрыЗапускаРасчетаСебестоимости.Вставить("МассивОрганизаций", 		МассивОрганизаций);
	ПараметрыЗапускаРасчетаСебестоимости.Вставить("РегламентноеЗадание", 	Ложь);
	ПараметрыЗапускаРасчетаСебестоимости.Вставить("МестоВызоваРасчета", 	"ПартионныйУчет.РассчитатьСебестоимость");
	ПараметрыЗапускаРасчетаСебестоимости.Вставить("ОписаниеЗамера", 		ОписаниеЗамера);
	
	СебестоимостьРассчитана = РасчетСебестоимостиПартионныйУчет21.РассчитатьВсеВПопыткеИсключении(ПараметрыЗапускаРасчетаСебестоимости);
	
	Если НЕ СебестоимостьРассчитана Тогда
		ВызватьИсключение РасчетСебестоимостиПрикладныеАлгоритмы.ТекстОшибкиНекорректногоЗавершенияРасчетаСебестоимости();
	КонецЕсли;
	
КонецПроцедуры

// По окончании расчета создает записи регистров "Задания к..."
Процедура УстановитьГраницыРассчитанногоПериода(НачалоПериода, МассивОрганизаций, НомерЗаданияДоРасчета, ВременныеТаблицы)
	
	НачатьТранзакцию();
	
	Попытка
		РасчетСебестоимостиПрикладныеАлгоритмы.ЗаблокироватьРегистрЗаданий(НомерЗаданияДоРасчета, МассивОрганизаций);
		РасчетСебестоимостиПрикладныеАлгоритмы.УвеличитьПериодРасчетаСебестоимости(НачалоПериода, МассивОрганизаций, ВременныеТаблицы);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
	КонецПопытки;
	
	РасчетСебестоимостиНДС.СформироватьЗаданияДляМеханизмовУчетаНДСПартионныйУчет21(НачалоПериода, МассивОрганизаций, Истина);
	
КонецПроцедуры

#КонецОбласти


#Область РасчетПартийТоваров

// Текст запроса исходных движений для расчета партий
Функция ДанныеДляПартийТоваров(НачалоПериода, ОкончаниеПериода, МассивОрганизаций)
	// СОРТИРОВКА по аналитикам, приоритету и моменту движения - требование алгоритма обсчета партий.
	ТекстСортировка = "
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура, ПериодПоступления, Приоритет ВОЗР, РазделУчета, Организация, ВидЗапасов, АналитикаУчетаПартий, Регистратор
		|";
	ТекстИндексы = "
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|";
	
	ИсходныйТекстЗапроса =
		ТекстПартииТоваровИнициализация() + ";" // вт ПрошлыеПоступления
		+ ЕстьУведомленияОВвозеПрослеживаемыхТоваров() // вт ЕстьУведомленияОВвозе
		+ ";"
		+ ТекстОписаниеПартийТоваров()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстОстаткиПартийТоваров() // использует ПрошлыеПоступления
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПервичныеПартииТоваров()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПервичныеПеремещенияТоваров()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстРеализацииПрошлыхМесяцев()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПотребленияТоваров()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстСписанияКомиссионныхТоваров()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПриходыПеремещений()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстСписаниеИмпортныхПартий()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстСписаниеТаможеннойДекларацией()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПриходыРастаможенногоТовара()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПотребленияЧужихЗапасов()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстВозвратыЧужихЗапасов()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПотребленияСобственныхЗапасов()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПередачиВозвратыКомиссияМеждуОрганизациями()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстОстаткиПереданныхТоваров()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстВводыОстатковПереданныхТоваров()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПередачиНаКомиссию()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстВозвратыПереданныхТоваров()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстВозвратыНаСкладПереданныхТоваров()
		+ ТекстИндексы;
	МВТ = Новый МенеджерВременныхТаблиц;	
	ИсходныйЗапрос = Новый Запрос(СтрЗаменить(ИсходныйТекстЗапроса, "//ВоВременнуюТаблицу", ""));
	ИсходныйЗапрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	ИсходныйЗапрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);	
	ИсходныйЗапрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	ИсходныйЗапрос.УстановитьПараметр("ТипДокументаИмпорта", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ТаможеннаяДекларацияИмпорт"));		
	ИсходныйЗапрос.УстановитьПараметр("КонецПредыдущегоПериода", НачалоПериода - 1);
	ИсходныйЗапрос.УстановитьПараметр("ГраницаКонецПредыдущегоПериода", Новый Граница(НачалоПериода - 1, ВидГраницы.Включая));
	ИсходныйЗапрос.МенеджерВременныхТаблиц = МВТ;
	ИсходныйЗапрос.Выполнить();
	
	Запрос = Новый Запрос(ТекстОписаниеПартийТоваров());
	РасчетныеПартии = Запрос.Выполнить().Выгрузить();
	
	ВременнаяТаблицаДанныхПоОтбору("Номенклатура", ТекстСортировка, РасчетныеПартии, МВТ);
	
	Возврат МВТ;
КонецФункции

Функция РасчетныеПартииТоваров(ДанныеДляРасчета, Регистраторы, ЦепочкиДвижений = Неопределено, Тест = Ложь, РассчитатьПартии = Истина) // ДанныеДляРасчета будут изменены
	// Шаг 1: создаем буфер накопления рассчитанных партий
	Запрос = Новый Запрос(ТекстОписаниеПартийТоваров());
	РасчетныеПартии = Запрос.Выполнить().Выгрузить();
	
	// Шаг 2: готовим описание обсчета
	КонтекстЦепочек = ОписаниеЦепочек("Потребление, Партия, Остаток, Сторно, Перемещение, Прошлое, Материал, Производство,
		|Возврат, Корректировка, Импорт, Декларация, ВозвратКомиссия, ПеремещениеВПроизводстве");
	
	ПоляПотреблений = "АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, РазделУчета";
	КонтекстДвижений = ОписаниеДвижений(
		"ПартииТоваровОрганизаций",
		"ПартииТоваровОрганизаций",
		ПереченьПолей(РасчетныеПартии.Колонки),
		ПоляПотреблений,
		"Знаменатель, Количество, Стоимость, СтоимостьБезНДС, СтоимостьРегл, НДСРегл, ПостояннаяРазница, ВременнаяРазница",
		"Количество", "Количество", "ДокументИсточник", "ПериодПоступления", "ДокументПоступления", Истина);
		
	// Шаг 3: обсчитываем потребления из партий
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Потребление", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Остаток", 	 ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Партия", 	 ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Импорт", 	 ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Перемещение", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Сторно", 	 ПоляПотреблений);
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Декларация", "АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, РазделУчета, ДокументПоступления");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Декларация", "Остаток", "АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, РазделУчета, ДокументПоступления");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Декларация", "Импорт",  "АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, РазделУчета, ДокументПоступления");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Материал", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Материал", "Остаток", 	  ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Материал", "Партия", 	  ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Материал", "Перемещение", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Материал", "Сторно", 	  ПоляПотреблений);
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Корректировка", "ДокументПоступления, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, РазделУчета");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Корректировка", "Остаток", "ДокументПоступления, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, РазделУчета");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Корректировка", "Партия",  "ДокументПоступления, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, РазделУчета");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Сторно", "ДокументИсточник, КорАналитикаУчетаНоменклатуры, КорВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Сторно", "Потребление", "Регистратор, АналитикаУчетаНоменклатуры, ВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Сторно", "Прошлое", 	"Регистратор, АналитикаУчетаНоменклатуры, ВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Сторно", "Материал", 	"Регистратор, АналитикаУчетаНоменклатуры, ВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Сторно", "Возврат", 	"Регистратор, АналитикаУчетаНоменклатуры, КорВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Сторно", "ВозвратКомиссия", "Регистратор, АналитикаУчетаНоменклатуры, ВидЗапасов");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Перемещение", "ДокументИсточник, АналитикаУчетаНоменклатуры, ВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Перемещение", "Потребление", "Регистратор, КорАналитикаУчетаНоменклатуры, КорВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Перемещение", "Декларация",  "Регистратор, КорАналитикаУчетаНоменклатуры, КорВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Перемещение", "Материал", 	 "Регистратор, КорАналитикаУчетаНоменклатуры, КорВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Перемещение", "ПеремещениеВПроизводстве", "Регистратор, КорАналитикаУчетаНоменклатуры, КорВидЗапасов");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Производство", "ДокументИсточник, КорАналитикаУчетаНоменклатуры, КорВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Производство", "Потребление", "Регистратор, АналитикаУчетаНоменклатуры, ВидЗапасов");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "ПеремещениеВПроизводстве", "АналитикаУчетаНоменклатуры, Организация, РазделУчета, Назначение");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ПеремещениеВПроизводстве", "Производство", "АналитикаУчетаНоменклатуры, Организация, РазделУчета, Назначение");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ПеремещениеВПроизводстве", "Остаток", 	  "АналитикаУчетаНоменклатуры, Организация, РазделУчета, Назначение");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Возврат", "АналитикаУчетаНоменклатуры, Организация, РазделУчета, Назначение");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Возврат", "Производство", "АналитикаУчетаНоменклатуры, Организация, РазделУчета, Назначение");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Возврат", "Остаток", 	  "АналитикаУчетаНоменклатуры, Организация, РазделУчета, Назначение");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Возврат", "Перемещение",  "АналитикаУчетаНоменклатуры, Организация, РазделУчета, Назначение");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "ВозвратКомиссия", "ДокументИсточник, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, РазделУчета");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ВозвратКомиссия", "Перемещение", "Регистратор, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, РазделУчета");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ВозвратКомиссия", "Остаток", "Регистратор, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, РазделУчета");
	
	ЦепочкиДвижений(КонтекстЦепочек, ДанныеДляРасчета);
	Если РассчитатьПартии Тогда
		РассчитатьПартииПоЦепочкам(КонтекстДвижений, ДанныеДляРасчета, РасчетныеПартии, Регистраторы, Тест);
	КонецЕсли;
	ЦепочкиДвижений = ДанныеДляРасчета;
	
	// Шаг 4: Удаляем строки, не требующие записи в базу.
	Если Не Тест Тогда
		УдалитьНезаписываемыеСтроки(КонтекстДвижений.Контекст, РасчетныеПартии);
	КонецЕсли;
	
	// Шаг 5: Сортировка партий для дальнейшей записи
	РасчетныеПартии.Сортировать("Регистратор, Приоритет", Новый СравнениеЗначений);
	Возврат РасчетныеПартии;
КонецФункции

Функция ТекстПартииТоваровИнициализация() // вт ПотребленияЧужихЗапасов, ПрошлыеРеализации, ПрошлыеПоступления, МатериалыИРаботыВПроизводстве, ПрошлыеПоступленияПроизводство, КорректировкиПоступлений
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор,
		|	ДД.ОрганизацияВладелец,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.НомерГТД,
		|	ДД.ВидЗапасовПродавца,
		|	ВидыЗапасов.ВидЗапасовВладельца,
		|	ВидыЗапасов.Организация КАК ОрганизацияПродавец
		|ПОМЕСТИТЬ
		|	ПотребленияЧужихЗапасов
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизацийКПередаче КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
		|		ПО ВидыЗапасов.Ссылка = ДД.ВидЗапасовПродавца
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизаций КАК Расходы
		|		ПО Расходы.Регистратор = ДД.Регистратор
		|		И Расходы.Организация = ДД.ОрганизацияВладелец
		|		И Расходы.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|		И Расходы.НомерГТД = ДД.НомерГТД
		|		И Расходы.ВидЗапасов = ВидыЗапасов.ВидЗапасовВладельца
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ВидыЗапасов.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И Расходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|;
	    |ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ДокументРеализации КАК ДокументРеализации,
		|	ДД.КорАналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов КАК ВидЗапасов
		|ПОМЕСТИТЬ
		|	ПрошлыеРеализации
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизаций КАК Расход
		|		ПО Расход.Период < &НачалоПериода
		|		И Расход.Регистратор = ДД.ДокументРеализации
		|		И Расход.АналитикаУчетаНоменклатуры = ДД.КорАналитикаУчетаНоменклатуры
		|		И Расход.ВидЗапасов = ДД.КорВидЗапасов
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ДокументРеализации <> НЕОПРЕДЕЛЕНО
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ДД.Количество < 0
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ДокументРеализации КАК ДокументРеализации,
		|	ДД.КорАналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов КАК ВидЗапасов
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизацийКПередаче КАК Расход
		|		ПО Расход.Период < &НачалоПериода
		|		И Расход.Регистратор = ДД.ДокументРеализации
		|		И Расход.АналитикаУчетаНоменклатуры = ДД.КорАналитикаУчетаНоменклатуры
		|		И Расход.ВидЗапасовПродавца = ДД.КорВидЗапасов
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Активность
		|	И ДД.ДокументРеализации <> НЕОПРЕДЕЛЕНО
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ДД.Количество < 0
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ДокументПоступления КАК Ссылка,
		|	МАКСИМУМ(Периодика.Период) КАК Период,
		|	(ВЫБОР КОГДА Импорт.Регистратор ЕСТЬ NULL
		|		ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КОНЕЦ) КАК РазделУчета
		|ПОМЕСТИТЬ ПрошлыеПоступления
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровОрганизаций КАК Периодика
		|		ПО Периодика.Период < &НачалоПериода
		|		И Периодика.Регистратор = ДД.ДокументПоступления
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКОформлениюДокументовИмпорта КАК Импорт
		|		ПО Импорт.Период < &НачалоПериода
		|		И Импорт.Регистратор = ДД.ДокументПоступления
		|		И Импорт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И Импорт.ТипДокументаИмпорта = &ТипДокументаИмпорта
		|ГДЕ
		|	ДД.ДокументПоступления <> НЕОПРЕДЕЛЕНО
		|СГРУППИРОВАТЬ ПО
		|	ДД.ДокументПоступления,
		|	(ВЫБОР КОГДА Импорт.Регистратор ЕСТЬ NULL
		|		ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КОНЕЦ)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ДокументПоступления КАК Ссылка,
		|	МАКСИМУМ(Периодика.Период) КАК Период,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровПереданныеНаКомиссию КАК Периодика
		|		ПО Периодика.Период < &НачалоПериода
		|		И Периодика.Регистратор = ДД.ДокументПоступления
		|ГДЕ
		|	ДД.ДокументПоступления <> НЕОПРЕДЕЛЕНО
		|СГРУППИРОВАТЬ ПО
		|	ДД.ДокументПоступления
		|;
	    |ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ
		|	МатериалыИРаботыВПроизводстве
		|ИЗ
		|	РегистрНакопления.МатериалыИРаботыВПроизводстве КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Товары
		|		ПО Товары.Ссылка = ДД.Номенклатура
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И Товары.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|	И ДД.Количество > 0
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ДокументПоступления КАК Ссылка,
		|	ДД.АналитикаУчетаНоменклатуры.МестоХранения КАК Склад,
		|	МАКСИМУМ(Периодика.Регистратор) КАК Регистратор,
		|	МАКСИМУМ(Периодика.Период) КАК Период
		|ПОМЕСТИТЬ
		|	ПрошлыеПоступленияПроизводство
		|ИЗ
		|	РегистрНакопления.ПартииПроизводственныхЗатрат.Остатки(&НачалоПериода) КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииПроизводственныхЗатрат КАК Периодика
		|		ПО Периодика.Период < &НачалоПериода
		|		И Периодика.ДокументПоступления = ДД.ДокументПоступления
		|		И Периодика.АналитикаУчетаНоменклатуры.МестоХранения = ДД.АналитикаУчетаНоменклатуры.МестоХранения
		|ГДЕ
		|	ДД.ДокументПоступления <> НЕОПРЕДЕЛЕНО
		|	И Периодика.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И Периодика.Количество > 0
		|СГРУППИРОВАТЬ ПО
		|	ДД.ДокументПоступления,
		|	ДД.АналитикаУчетаНоменклатуры.МестоХранения
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов
		|ПОМЕСТИТЬ
		|	КорректировкиПоступлений
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровОрганизаций КАК Поступления
		|		ПО Поступления.Период <= &ОкончаниеПериода
		|		И Поступления.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|		И Поступления.ВидЗапасов = ДД.ВидЗапасов
		|		И Поступления.ДокументПоступления = ДД.ДокументПоступления
		|		И Поступления.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И Поступления.Регистратор <> ДД.Регистратор
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.Первичное
		|	И ДД.Регистратор ССЫЛКА Документ.КорректировкаПриобретения
		|	И ДД.Регистратор <> ДД.ДокументПоступления
		|";
КонецФункции

Функция ЕстьУведомленияОВвозеПрослеживаемыхТоваров() // вт ЕстьУведомленияОВвозе
	Возврат "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Остатки.ДокументПоступления КАК Партия,
	|	Остатки.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	НАЧАЛОПЕРИОДА(ЗакупкаВСтранахЕАЭС.Дата, МЕСЯЦ) КАК Дата
	|
	|ПОМЕСТИТЬ ОстаткиЗакупкаВСтранахЕАЭС
	|ИЗ
	|	РегистрНакопления.ПартииТоваровОрганизаций.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&МассивОрганизаций)
	|		И ЕСТЬNULL(АналитикаУчетаНоменклатуры.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
	|	) КАК Остатки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ЗакупкаВСтранахЕАЭС
	|		ПО ЗакупкаВСтранахЕАЭС.Ссылка = Остатки.ДокументПоступления
	|		И ЗакупкаВСтранахЕАЭС.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути))
	|ИНДЕКСИРОВАТЬ ПО
	|	Партия
	|;
	|////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Уведомление.ПервичныйДокумент КАК Партия,
	|	Уведомление.Дата КАК Дата,
	|	УведомлениеТовары.Номенклатура КАК Номенклатура
	|
	|ПОМЕСТИТЬ ЕстьУведомленияОВвозе
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК Уведомление
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОВвозеПрослеживаемыхТоваров.Товары КАК УведомлениеТовары
	|		ПО УведомлениеТовары.Ссылка = Уведомление.Ссылка
	|ГДЕ
	|	Уведомление.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода 
	|	И Уведомление.Организация В (&МассивОрганизаций)
	|	И Уведомление.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Остатки.Партия КАК Партия,
	|	Уведомление.Дата КАК Дата, 
	|	Остатки.Номенклатура КАК Номенклатура
	|ИЗ
	|	ОстаткиЗакупкаВСтранахЕАЭС КАК Остатки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК Уведомление
	|		ПО Уведомление.Дата МЕЖДУ Остатки.Дата И &КонецПредыдущегоПериода
	|		И Уведомление.ПервичныйДокумент = Остатки.Партия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Партия,
	|	Номенклатура
	|";		 
КонецФункции

Функция ТекстОписаниеПартийТоваров()
	Возврат "
		|ВЫБРАТЬ ПЕРВЫЕ 0
		|	ВЫРАЗИТЬ("""" КАК СТРОКА(80)) КАК ЗапросИсточник,
		|	0 КАК Приоритет,
		|	""ХХХХХХХХХХХХХХХХХХХХХХХХ"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК Период,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	0 КАК Знаменатель,
		|	0 КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ПериодПоступления,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	ДД.ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ДД.СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	ДД.СтатьяСписанияНДС,
		|	ДД.АналитикаСписанияНДС
		|//ВоВременнуюТаблицу ПОМЕСТИТЬ ИсходныеДанные
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|";
КонецФункции

// КОНТУР ПартииТоваровОрганизаций
Функция ТекстОстаткиПартийТоваров() // использует ПрошлыеПоступления, ЕстьУведомленияОВвозе
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстОстаткиПартийТоваров"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	""Остаток"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ЕСТЬNULL(Периодика.РазделУчета, ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)) КАК РазделУчета,
		|	ЕСТЬNULL(Периодика.Период, &НачалоПериода) КАК Период,
		|	ДД.ДокументПоступления КАК Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	0 КАК Знаменатель,
		|	ДД.КоличествоОстаток КАК Количество,
		|	ДД.СтоимостьОстаток КАК Стоимость,
		|	ДД.СтоимостьБезНДСОстаток КАК СтоимостьБезНДС,
		|	ДД.СтоимостьРеглОстаток КАК СтоимостьРегл,
		|	ДД.НДСРеглОстаток КАК НДСРегл,
		|	ДД.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
		|	ДД.ВременнаяРазницаОстаток КАК ВременнаяРазница,
		|	ИСТИНА КАК Первичное,
		|	ИСТИНА КАК СохранятьРегл,
		|	(ВЫБОР
		|		КОГДА НЕ ЕстьУведомленияОВвозе.Дата ЕСТЬ NULL ТОГДА ЕстьУведомленияОВвозе.Дата
		|		ИНАЧЕ Периодика.Период КОНЕЦ) КАК ПериодПоступления,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ДД.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|	ДД.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПрошлыеПоступления КАК Периодика
		|		ПО Периодика.Ссылка = ДД.ДокументПоступления
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ЗакупкаВСтранахЕАЭС
		|		ПО ЗакупкаВСтранахЕАЭС.Ссылка = ДД.ДокументПоступления
		|		И ЗакупкаВСтранахЕАЭС.ХозяйственнаяОперация В (
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС),
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути))
		|	ЛЕВОЕ СОЕДИНЕНИЕ ЕстьУведомленияОВвозе КАК ЕстьУведомленияОВвозе
		|		ПО ЕстьУведомленияОВвозе.Партия = ДД.ДокументПоступления
		|		И ЕстьУведомленияОВвозе.Номенклатура = ДД.АналитикаУчетаНоменклатуры.Номенклатура
		|ГДЕ
		|	ДД.КоличествоОстаток > 0
		|	И НЕ (НЕ ЗакупкаВСтранахЕАЭС.Ссылка ЕСТЬ NULL
		|		 И ЕСТЬNULL(ДД.АналитикаУчетаНоменклатуры.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
		|		 И ЕстьУведомленияОВвозе.Номенклатура ЕСТЬ NULL)
		|";
КонецФункции

Функция ТекстПервичныеПартииТоваров() // использует КорректировкиПоступлений, ЕстьУведомленияОВвозе
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПервичныеПартииТоваров"" КАК ЗапросИсточник,
		|	(ВЫБОР
		|		КОГДА НЕ Корректировка.Регистратор ЕСТЬ NULL ТОГДА 15
		|		ИНАЧЕ 10 КОНЕЦ) КАК Приоритет,
		|	(ВЫБОР
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество < 0 ТОГДА ""Сторно""
		|		КОГДА НЕ Корректировка.Регистратор ЕСТЬ NULL ТОГДА ""Корректировка""
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту) ТОГДА ""Импорт""
		|		ИНАЧЕ ""Партия"" КОНЕЦ) КАК ТипЗаписи,
		|	(ВЫБОР
		|		КОГДА НЕ Корректировка.Регистратор ЕСТЬ NULL ТОГДА ЛОЖЬ
		// Приобретение товаров (ввоз из ЕАЭС) для прослеживаемых товаров без уведомления о ввозе не должно быть источником для подбора партий.
		|		КОГДА НЕ ЗакупкаВСтранахЕАЭС.Ссылка ЕСТЬ NULL
		|		 И ЕСТЬNULL(ДД.АналитикаУчетаНоменклатуры.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
		|		 И ЕстьУведомленияОВвозе.Номенклатура ЕСТЬ NULL
		|   		ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА КОНЕЦ) КАК РасчетЗавершен,
		|	ДД.ВидДвижения КАК ВидДвижения,
		|	(ВЫБОР
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента)
		|		 И ДД.ВидЗапасов.РеализацияЗапасовДругойОрганизации
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КОНЕЦ) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	0 КАК Знаменатель,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(ДД.Стоимость) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.НДСРегл) КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	ДД.Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	(ВЫБОР
		|		КОГДА НЕ Корректировка.Регистратор ЕСТЬ NULL
		|		 И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА ДОБАВИТЬКДАТЕ(&НачалоПериода, СЕКУНДА, -1)
		|		КОГДА НЕ ЕстьУведомленияОВвозе.Дата ЕСТЬ NULL ТОГДА ЕстьУведомленияОВвозе.Дата
		|		ИНАЧЕ ДД.Период КОНЕЦ) КАК ПериодПоступления,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов,
		|	ДД.ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	АналитикиПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК АналитикиПартий
		|		ПО АналитикиПартий.Ссылка = ДД.АналитикаУчетаПартий
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК Поступление
		|		ПО Поступление.Ссылка = ДД.ДокументПоступления
		|	ЛЕВОЕ СОЕДИНЕНИЕ КорректировкиПоступлений КАК Корректировка
		|		ПО Корректировка.Регистратор = ДД.Регистратор
		|		И Корректировка.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|		И Корректировка.ВидЗапасов = ДД.ВидЗапасов
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ЗакупкаВСтранахЕАЭС
		|		ПО ЗакупкаВСтранахЕАЭС.Ссылка = ДД.Регистратор
		|		И ЗакупкаВСтранахЕАЭС.ХозяйственнаяОперация В (
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС),
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути))
		|	ЛЕВОЕ СОЕДИНЕНИЕ ЕстьУведомленияОВвозе КАК ЕстьУведомленияОВвозе
		|		ПО ЕстьУведомленияОВвозе.Партия = ДД.Регистратор
		|		И ЕстьУведомленияОВвозе.Номенклатура = ДД.АналитикаУчетаНоменклатуры.Номенклатура
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.Первичное
		|	И (ДД.ДокументИсточник = НЕОПРЕДЕЛЕНО
		|		ИЛИ НЕ Корректировка.Регистратор ЕСТЬ NULL)
		|	И НЕ ДД.Регистратор ССЫЛКА Документ.КорректировкаРеализации
		|СГРУППИРОВАТЬ ПО
		|	(ВЫБОР
		|		КОГДА НЕ Корректировка.Регистратор ЕСТЬ NULL ТОГДА 15
		|		ИНАЧЕ 10 КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество < 0 ТОГДА ""Сторно""
		|		КОГДА НЕ Корректировка.Регистратор ЕСТЬ NULL ТОГДА ""Корректировка""
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту) ТОГДА ""Импорт""
		|		ИНАЧЕ ""Партия"" КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА НЕ Корректировка.Регистратор ЕСТЬ NULL ТОГДА ЛОЖЬ
		|		КОГДА НЕ ЗакупкаВСтранахЕАЭС.Ссылка ЕСТЬ NULL
		|		 И ЕСТЬNULL(ДД.АналитикаУчетаНоменклатуры.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
		|		 И ЕстьУведомленияОВвозе.Номенклатура ЕСТЬ NULL
		|   		ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА КОНЕЦ), // РасчетЗавершен
		|	ДД.ВидДвижения,
		|	(ВЫБОР
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента)
		|		 И ДД.ВидЗапасов.РеализацияЗапасовДругойОрганизации
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КОНЕЦ),
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.Первичное,
		|	(ВЫБОР
		|		КОГДА НЕ Корректировка.Регистратор ЕСТЬ NULL
		|		 И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА ДОБАВИТЬКДАТЕ(&НачалоПериода, СЕКУНДА, -1)
		|		КОГДА НЕ ЕстьУведомленияОВвозе.Дата ЕСТЬ NULL ТОГДА ЕстьУведомленияОВвозе.Дата
		|		ИНАЧЕ ДД.Период КОНЕЦ), // ПериодПоступления
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов,
		|	ДД.ДокументИсточник,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	АналитикиПартий.НалогообложениеНДС,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов
		|";
КонецФункции

Функция ТекстПервичныеПеремещенияТоваров()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПервичныеПеремещенияТоваров"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	(ВЫБОР
		|		КОГДА НЕ Возврат.Ссылка ЕСТЬ NULL ТОГДА ""Сторно""
		|		КОГДА Сборка.Ссылка ЕСТЬ NULL И ПередачаКомиссия.Ссылка ЕСТЬ NULL ТОГДА ""Перемещение"" 
		|		ИНАЧЕ ""Партия"" КОНЕЦ) КАК ТипЗаписи,
		|	(ВЫБОР
		|		КОГДА Сборка.Ссылка ЕСТЬ NULL И ПередачаКомиссия.Ссылка ЕСТЬ NULL ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА КОНЕЦ) КАК РасчетЗавершен,
		|	ДД.ВидДвижения КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	0 КАК Знаменатель,
		|	СУММА(ДД.Количество) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	СУММА(ВЫБОР КОГДА Передача.Ссылка ЕСТЬ NULL И Возврат.Ссылка ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ДД.СтоимостьРегл КОНЕЦ) КАК СтоимостьРегл,
		|	СУММА(ВЫБОР КОГДА Передача.Ссылка ЕСТЬ NULL И Возврат.Ссылка ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ДД.НДСРегл КОНЕЦ) КАК НДСРегл,
		|	СУММА(ВЫБОР КОГДА Передача.Ссылка ЕСТЬ NULL И Возврат.Ссылка ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ДД.ПостояннаяРазница КОНЕЦ) КАК ПостояннаяРазница,
		|	СУММА(ВЫБОР КОГДА Передача.Ссылка ЕСТЬ NULL И Возврат.Ссылка ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ДД.ВременнаяРазница КОНЕЦ) КАК ВременнаяРазница,
		|	ДД.Первичное,
		|	(ВЫБОР КОГДА Передача.Ссылка ЕСТЬ NULL И Возврат.Ссылка ЕСТЬ NULL ТОГДА ЛОЖЬ ИНАЧЕ ИСТИНА КОНЕЦ) КАК СохранятьРегл,
		|	ДД.Период КАК ПериодПоступления,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов,
		|	ДД.ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.СборкаТоваров КАК Сборка
		|		ПО Сборка.Дата = ДД.Период
		|		И Сборка.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровМеждуОрганизациями КАК Передача
		|		ПО Передача.Дата = ДД.Период
		|		И Передача.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровМеждуОрганизациями КАК ПередачаКомиссия
		|		ПО ПередачаКомиссия.Дата = ДД.Период
		|		И ПередачаКомиссия.Ссылка = ДД.Регистратор
		|		И ПередачаКомиссия.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссиюВДругуюОрганизацию)
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровМеждуОрганизациями КАК Возврат
		|		ПО Возврат.Дата = ДД.Период
		|		И Возврат.Ссылка = ДД.Регистратор
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.Регистратор = ДД.ДокументПоступления
		|	И ДД.Первичное
		|	И ДД.ДокументИсточник <> НЕОПРЕДЕЛЕНО
		|	И НЕ ДД.Регистратор ССЫЛКА Документ.ПересортицаТоваров
		|СГРУППИРОВАТЬ ПО
		|	(ВЫБОР
		|		КОГДА НЕ Возврат.Ссылка ЕСТЬ NULL ТОГДА ""Сторно""
		|		КОГДА Сборка.Ссылка ЕСТЬ NULL И ПередачаКомиссия.Ссылка ЕСТЬ NULL ТОГДА ""Перемещение"" 
		|		ИНАЧЕ ""Партия"" КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА Сборка.Ссылка ЕСТЬ NULL И ПередачаКомиссия.Ссылка ЕСТЬ NULL ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА КОНЕЦ),
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.Первичное,
		|	(ВЫБОР КОГДА Передача.Ссылка ЕСТЬ NULL И Возврат.Ссылка ЕСТЬ NULL ТОГДА ЛОЖЬ ИНАЧЕ ИСТИНА КОНЕЦ),
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов,
		|	ДД.ДокументИсточник,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов
		|";
КонецФункции

Функция ТекстРеализацииПрошлыхМесяцев()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстРеализацииПрошлыхМесяцев"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	""Прошлое"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ДД.ВидДвижения КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	0 КАК Знаменатель,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(ДД.Стоимость) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.НДСРегл) КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	ДД.Первичное,
		|	ИСТИНА КАК СохранятьРегл,
		|	ДД.Период КАК ПериодПоступления,
		|	ДД.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов КАК КорВидЗапасов,
		|	ДД.Регистратор КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрошлыеРеализации КАК Прошлые
		|		ПО ДД.Регистратор = Прошлые.ДокументРеализации
		|		И ДД.АналитикаУчетаНоменклатуры = Прошлые.АналитикаУчетаНоменклатуры
		|		И ДД.ВидЗапасов = Прошлые.ВидЗапасов
		|ГДЕ
		|	ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|СГРУППИРОВАТЬ ПО
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.Первичное,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов
		|";
КонецФункции

// КОНТУР ТоварыОрганизаций
Функция ТекстПотребленияТоваров()
	// Потребления товаров - реализации, возвраты, расходы перемещений и передач - без расходов чужих видов запасов.
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПотребленияТоваров"" КАК ЗапросИсточник,
		|	(ВЫБОР КОГДА ДД.Количество > 0 ТОГДА 90 ИНАЧЕ 10 КОНЕЦ) КАК Приоритет,
		|	(ВЫБОР
		|		КОГДА НЕ Разборка.Ссылка ЕСТЬ NULL ТОГДА ""Материал""
		|		КОГДА ДД.Количество > 0 ТОГДА ""Потребление""
		|		ИНАЧЕ ""Сторно"" КОНЕЦ) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор, 
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	(ВЫБОР
		|		КОГДА ДД.Регистратор ССЫЛКА Документ.ВозвратТоваровПоставщику ТОГДА ДД.ДокументРеализации
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ) КАК ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	0 КАК Знаменатель,
		|	СУММА(ДД.Количество) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.Период КАК ПериодПоступления,
		|	(ВЫБОР
		|		КОГДА ДД.Регистратор ССЫЛКА Документ.ПеремещениеТоваров ТОГДА ДД.КорАналитикаУчетаНоменклатуры
		|		КОГДА ДД.Регистратор ССЫЛКА Документ.ИсправлениеРазвернутогоСальдоТоваровОрганизаций ТОГДА ДД.КорАналитикаУчетаНоменклатуры
		|		КОГДА ДД.Регистратор ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями ТОГДА ДД.КорАналитикаУчетаНоменклатуры
		|		КОГДА ДД.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг ТОГДА ДД.КорАналитикаУчетаНоменклатуры
		|		КОГДА ДД.Регистратор ССЫЛКА Документ.КорректировкаОбособленногоУчетаЗапасов ТОГДА ДД.КорАналитикаУчетаНоменклатуры
		|		КОГДА СпрВидыЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
		|			И ДД.КорАналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
		|			ТОГДА ДД.КорАналитикаУчетаНоменклатуры
		|		КОГДА ДД.Количество < 0 ТОГДА ДД.АналитикаУчетаНоменклатуры
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КОНЕЦ) КАК КорАналитикаУчетаНоменклатуры,
		|	(ВЫБОР
		|		КОГДА ДД.КорВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
		|			И ДД.КорАналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
		|			ТОГДА ДД.ВидЗапасов
		|		КОГДА ДД.Количество < 0 И ДД.КорВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) ТОГДА ДД.ВидЗапасов
		|		ИНАЧЕ ДД.КорВидЗапасов КОНЕЦ) КАК КорВидЗапасов,
		|	(ВЫБОР
		|		КОГДА ДД.Количество > 0 ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА ДД.Количество < 0 И ДД.Регистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах ТОГДА ДД.Регистратор
		|		ИНАЧЕ ДД.ДокументРеализации КОНЕЦ) КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.СтатьяРасходов КАК СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.СборкаТоваров КАК Разборка
		|		ПО Разборка.Дата = ДД.Период
		|		И Разборка.Ссылка = ДД.Регистратор
		|		И Разборка.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК СпрВидыЗапасов
		|		ПО СпрВидыЗапасов.Ссылка = ДД.ВидЗапасов
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И (ДД.Организация = ДД.ОрганизацияОтгрузки
		|		ИЛИ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровМеждуОрганизациями))
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И (ДД.Количество > 0
		|		ИЛИ ДД.ДокументРеализации <> НЕОПРЕДЕЛЕНО
		|		ИЛИ ДД.Регистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах)
		|	И ДД.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера)
		|	И НЕ ДД.Регистратор ССЫЛКА Документ.КорректировкаПриобретения

		// сворачиваем строки, различающиеся номерами ГТД
		|СГРУППИРОВАТЬ ПО
		|	(ВЫБОР КОГДА ДД.Количество > 0 ТОГДА 90 ИНАЧЕ 10 КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА НЕ Разборка.Ссылка ЕСТЬ NULL ТОГДА ""Материал""
		|		КОГДА ДД.Количество > 0 ТОГДА ""Потребление""
		|		ИНАЧЕ ""Сторно"" КОНЕЦ),
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	(ВЫБОР
		|		КОГДА ДД.Регистратор ССЫЛКА Документ.ВозвратТоваровПоставщику ТОГДА ДД.ДокументРеализации
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ),
		|	ДД.ВидЗапасов,
		|	ЕСТЬNULL(Разборка.Количество, 0),
		|	(ВЫБОР
		|		КОГДА ДД.Регистратор ССЫЛКА Документ.ПеремещениеТоваров ТОГДА ДД.КорАналитикаУчетаНоменклатуры
		|		КОГДА ДД.Регистратор ССЫЛКА Документ.ИсправлениеРазвернутогоСальдоТоваровОрганизаций ТОГДА ДД.КорАналитикаУчетаНоменклатуры
		|		КОГДА ДД.Регистратор ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями ТОГДА ДД.КорАналитикаУчетаНоменклатуры
		|		КОГДА ДД.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг ТОГДА ДД.КорАналитикаУчетаНоменклатуры
		|		КОГДА ДД.Регистратор ССЫЛКА Документ.КорректировкаОбособленногоУчетаЗапасов ТОГДА ДД.КорАналитикаУчетаНоменклатуры
		|		КОГДА СпрВидыЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
		|			И ДД.КорАналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
		|			ТОГДА ДД.КорАналитикаУчетаНоменклатуры
		|		КОГДА ДД.Количество < 0 ТОГДА ДД.АналитикаУчетаНоменклатуры
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ДД.КорВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
		|			И ДД.КорАналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
		|			ТОГДА ДД.ВидЗапасов
		|		КОГДА ДД.Количество < 0 И ДД.КорВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) ТОГДА ДД.ВидЗапасов
		|		ИНАЧЕ ДД.КорВидЗапасов КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ДД.Количество > 0 ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА ДД.Количество < 0 И ДД.Регистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах ТОГДА ДД.Регистратор
		|		ИНАЧЕ ДД.ДокументРеализации КОНЕЦ),
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.СтатьяРасходов,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов
		|";
КонецФункции

Функция ТекстСписанияКомиссионныхТоваров()
	// списание товаров, полученных на комиссию, по аналитике номенклатуры у комитента
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстСписанияКомиссионныхТоваров"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	(ВЫБОР КОГДА ДД.КоличествоСписано > 0 ТОГДА ""Потребление""
		|		ИНАЧЕ ""Сторно"" КОНЕЦ) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор, 
		|	ДД.ВидЗапасов.Организация КАК Организация,
		|	ДД.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	0 КАК Знаменатель,
		|	СУММА(ДД.КоличествоСписано) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.Период КАК ПериодПоступления,
		|	(ВЫБОР КОГДА ДД.КоличествоСписано > 0 ТОГДА ДД.КорАналитикаУчетаНоменклатуры
		|		ИНАЧЕ ДД.АналитикаУчетаНоменклатуры КОНЕЦ) КАК КорАналитикаУчетаНоменклатуры,
		|	(ВЫБОР КОГДА ДД.КоличествоСписано > 0 ТОГДА ДД.КорВидЗапасов
		|		ИНАЧЕ ДД.КорВидЗапасов КОНЕЦ) КАК КорВидЗапасов,
		|	ТоварыОрганизаций.ДокументРеализации КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ТоварыОрганизаций.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ТоварыОрганизаций.СтатьяРасходов КАК СтатьяРасходовСписания,
		|	ТоварыОрганизаций.АналитикаРасходов,
		|	ТоварыОрганизаций.АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		|		ПО ТоварыОрганизаций.Регистратор = ДД.Регистратор
		|		И ТоварыОрганизаций.Период = ДД.Период
		|		И ТоварыОрганизаций.КорАналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|		И ТоварыОрганизаций.ВидЗапасов = ДД.ВидЗапасов
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.ВидЗапасов.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ДД.КоличествоСписано <> 0
		// сворачиваем строки, различающиеся номерами ГТД
		|СГРУППИРОВАТЬ ПО
		|	(ВЫБОР КОГДА ДД.КоличествоСписано > 0 ТОГДА ""Потребление""
		|		ИНАЧЕ ""Сторно"" КОНЕЦ),
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.ВидЗапасов.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов,
		|	(ВЫБОР КОГДА ДД.КоличествоСписано > 0 ТОГДА ДД.КорАналитикаУчетаНоменклатуры
		|		ИНАЧЕ ДД.АналитикаУчетаНоменклатуры КОНЕЦ),
		|	(ВЫБОР КОГДА ДД.КоличествоСписано > 0 ТОГДА ДД.КорВидЗапасов
		|		ИНАЧЕ ДД.КорВидЗапасов КОНЕЦ),
		|	ТоварыОрганизаций.ДокументРеализации,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ТоварыОрганизаций.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ТоварыОрганизаций.СтатьяРасходов,
		|	ТоварыОрганизаций.АналитикаРасходов,
		|	ТоварыОрганизаций.АналитикаАктивовПассивов
		|";
КонецФункции

Функция ТекстПриходыПеремещений()
	// Приходы перемещений, передач, пересортиц по ценам списания, порч по ценам списания.
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПриходыПеремещений"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Перемещение"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	(ВЫБОР ДД.ХозяйственнаяОперация
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваров) ТОГДА ДД.Регистратор
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ) КАК ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	0 КАК Знаменатель,
		|	СУММА(ДД.Количество) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.Период КАК ПериодПоступления,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	ДД.Регистратор КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровОрганизаций КАК Партии
		|		ПО Партии.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И Партии.Регистратор = ДД.Регистратор
		|		И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И Партии.Первичное
		|		И Партии.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|		И Партии.ВидЗапасов = ДД.ВидЗапасов
		|		И НЕ (Партии.Регистратор ССЫЛКА Документ.ПересортицаТоваров И Партии.ДокументИсточник <> НЕОПРЕДЕЛЕНО)
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ДД.ХозяйственнаяОперация В (
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеТоваров),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеТоваровМеждуФилиалами),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваров),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваров),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОбособленногоУчета),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиентуРеглУчет))
		|	И Партии.Регистратор ЕСТЬ NULL
		// сворачиваем строки, различающиеся номерами ГТД
		|СГРУППИРОВАТЬ ПО
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	(ВЫБОР ДД.ХозяйственнаяОперация
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваров) ТОГДА ДД.Регистратор
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ),
		|	ДД.ВидЗапасов,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.ХозяйственнаяОперация
		|";
КонецФункции
	
// КОНТУР ТоварыКОформлениюДокументовИмпорта -> оформление импорта
Функция ТекстСписаниеИмпортныхПартий()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстСписаниеИмпортныхПартий"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Дата КАК Период,
		|	ДД.Ссылка КАК Регистратор,
		|	ДД.Организация,
		|	Строки.АналитикаУчетаНоменклатуры,
		|	Строки.ДокументПоступления,
		|	Строки.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	0 КАК Знаменатель,
		|	СУММА(Строки.Количество) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.Дата КАК ПериодПоступления,
		|	Строки.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
		|	Строки.ВидЗапасов КАК КорВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	Строки.Номенклатура,
		|	Строки.Характеристика,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	Документ.ТаможеннаяДекларацияИмпорт КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Строки
		|		ПО Строки.Ссылка = ДД.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКОформлениюДокументовИмпорта КАК КОформлению
		|		ПО КОформлению.Период = ДД.Дата
		|		И КОформлению.Регистратор = ДД.Ссылка
		|		И КОформлению.АналитикаУчетаНоменклатуры = Строки.АналитикаУчетаНоменклатуры
		|		И КОформлению.ВидЗапасов = Строки.ВидЗапасов
		|		И КОформлению.ДокументПоступления = Строки.ДокументПоступления
		|		И КОформлению.ДокументПоступления <> ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка)
		|		И КОформлению.ДокументПоступления <> НЕОПРЕДЕЛЕНО
		|		И КОформлению.ТипДокументаИмпорта = &ТипДокументаИмпорта
		|ГДЕ
		|	ДД.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Проведен
		|	И КОформлению.ДокументПоступления ЕСТЬ NULL
		|СГРУППИРОВАТЬ ПО
		|	ДД.Дата,
		|	ДД.Ссылка,
		|	Строки.ДокументПоступления,
		|	ДД.Организация,
		|	Строки.АналитикаУчетаНоменклатуры,
		|	Строки.ВидЗапасов,
		|	Строки.Номенклатура,
		|	Строки.Характеристика
		|";
КонецФункции

Функция ТекстСписаниеТаможеннойДекларацией()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстСписаниеТаможеннойДекларацией"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Декларация"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	0 КАК Знаменатель,
		|	СУММА(ДД.Количество) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.Период КАК ПериодПоступления,
		|	ДД.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов КАК КорВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	Аналитики.Номенклатура,
		|	Аналитики.Характеристика,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ТоварыКОформлениюДокументовИмпорта КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитики
		|		ПО Аналитики.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ДД.ДокументПоступления <> ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка)
		|	И ДД.ДокументПоступления <> НЕОПРЕДЕЛЕНО
		|	И ДД.ТипДокументаИмпорта = &ТипДокументаИмпорта	
		|СГРУППИРОВАТЬ ПО
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.ДокументПоступления,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов,
		|	Аналитики.Номенклатура,
		|	Аналитики.Характеристика
		|";
КонецФункции

Функция ТекстПриходыРастаможенногоТовара()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПриходыРастаможенногоТовара"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	""Перемещение"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.Регистратор КАК ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	0 КАК Знаменатель,
		|	СУММА(ДД.Количество) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.Период КАК ПериодПоступления,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	ДД.Регистратор КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	Аналитики.Номенклатура,
		|	Аналитики.Характеристика,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ТоварыКОформлениюДокументовИмпорта КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитики
		|		ПО Аналитики.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ДД.ТипДокументаИмпорта = &ТипДокументаИмпорта
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов,
		|	Аналитики.Номенклатура,
		|	Аналитики.Характеристика
		|";
КонецФункции

// КОНТУР ТоварыОрганизацийКПередаче -> оформление продаж между организациями по расходу чужих запасов.
Функция ТекстПотребленияЧужихЗапасов()
	// потребления чужих видов запасов (реализации, перемещения, возвраты от клиентов)
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПотребленияЧужихЗапасов"" КАК ЗапросИсточник,
		|	(ВЫБОР КОГДА ДД.Количество > 0 ТОГДА 90 ИНАЧЕ 10 КОНЕЦ) КАК Приоритет,
		|	(ВЫБОР
		|		КОГДА НЕ Разборка.Ссылка ЕСТЬ NULL ТОГДА ""Материал""
		|		КОГДА ДД.Количество > 0 ТОГДА ""Потребление""
		|		ИНАЧЕ ""Сторно"" КОНЕЦ) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.ОрганизацияОтгрузки КАК Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	КПередаче.ВидЗапасовПродавца КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	0 КАК Знаменатель,
		|	ДД.Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.Период КАК ПериодПоступления,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	(ВЫБОР КОГДА ДД.КорВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) ТОГДА КПередаче.ВидЗапасовПродавца
		|		ИНАЧЕ ДД.КорВидЗапасов КОНЕЦ) КАК КорВидЗапасов,
		|	ДД.ДокументРеализации КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.СтатьяРасходов КАК СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПотребленияЧужихЗапасов КАК КПередаче
		|		ПО КПередаче.Регистратор = ДД.Регистратор
		|		И КПередаче.ОрганизацияВладелец = ДД.Организация
		|		И КПередаче.ОрганизацияПродавец = ДД.ОрганизацияОтгрузки
		|		И КПередаче.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|		И КПередаче.НомерГТД = ДД.НомерГТД
		|		И КПередаче.ВидЗапасовВладельца = ДД.ВидЗапасов
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.СборкаТоваров КАК Разборка
		|		ПО Разборка.Дата = ДД.Период
		|		И Разборка.Ссылка = ДД.Регистратор
		|		И Разборка.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.ОрганизацияОтгрузки В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И НЕ ДД.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента
		|	И НЕ (ДД.Регистратор ССЫЛКА Документ.КорректировкаРеализации И ДД.Количество < 0)
		|";
КонецФункции

Функция ТекстВозвратыЧужихЗапасов()
	// возвраты от клиентов чужих видов запасов
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстВозвратыЧужихЗапасов"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	""Сторно"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.ОрганизацияОтгрузки КАК Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	КПередаче.ВидЗапасовПродавца КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	0 КАК Знаменатель,
		|	ДД.Количество КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.Период КАК ПериодПоступления,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов КАК КорВидЗапасов,
		|	(ВЫБОР 
		|		КОГДА ДД.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|		 И Возврат.ВозвратПереданнойМногооборотнойТары
		|			ТОГДА ДД.ДокументРеализации
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера) ТОГДА ДД.Регистратор
		|		ИНАЧЕ ДД.ДокументРеализации КОНЕЦ) КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПотребленияЧужихЗапасов КАК КПередаче
		|		ПО КПередаче.Регистратор = ДД.Регистратор
		|		И КПередаче.ОрганизацияВладелец = ДД.Организация
		|		И КПередаче.ОрганизацияПродавец = ДД.ОрганизацияОтгрузки
		|		И КПередаче.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|		И КПередаче.НомерГТД = ДД.НомерГТД
		|		И КПередаче.ВидЗапасовВладельца = ДД.ВидЗапасов
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК СправочникВидыЗапасов
		|		ПО СправочникВидыЗапасов.Ссылка = ДД.КорВидЗапасов
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента КАК Возврат
		|		ПО Возврат.Ссылка = ДД.Регистратор
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И (НЕ ДД.Первичное
	    |	 ИЛИ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера))
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ДД.Количество < 0
		|	И ДД.Организация <> ДД.ОрганизацияОтгрузки
		|	И (ДД.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента
		|		ИЛИ ДД.Регистратор ССЫЛКА Документ.КорректировкаРеализации)
		|	И СправочникВидыЗапасов.РеализацияЗапасовДругойОрганизации
		|";
КонецФункции

Функция ТекстПотребленияСобственныхЗапасов()
	// Списание под обеспечение потреблений чужих видов запасов (передачи, возвраты передач).
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПотребленияСобственныхЗапасов"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	(ВЫБОР
		|		КОГДА НЕ Возврат.Ссылка ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КОНЕЦ) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	(ВЫБОР КОГДА ДД.Возвращено > 0 ТОГДА СправочникВидыЗапасов.Организация ИНАЧЕ ДД.ОрганизацияВладелец КОНЕЦ) КАК Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	(ВЫБОР КОГДА ДД.Возвращено > 0 ТОГДА ДД.ВидЗапасовПродавца ИНАЧЕ СправочникВидыЗапасов.ВидЗапасовВладельца КОНЕЦ) КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	0 КАК Знаменатель,
		|	СУММА(ДД.Количество + ДД.Возвращено) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.Период КАК ПериодПоступления,
		|	(ВЫБОР
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратПоКомиссииМеждуОрганизациями)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ДД.АналитикаУчетаНоменклатуры КОНЕЦ) КАК КорАналитикаУчетаНоменклатуры,
		|	(ВЫБОР
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратПоКомиссииМеждуОрганизациями)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА ДД.Возвращено > 0 ТОГДА ДД.КорВидЗапасов
		|		ИНАЧЕ ДД.ВидЗапасовПродавца КОНЕЦ) КАК КорВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизацийКПередаче КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК СправочникВидыЗапасов
		|		ПО СправочникВидыЗапасов.Ссылка = ДД.ВидЗапасовПродавца
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровМеждуОрганизациями КАК Возврат
		|		ПО Возврат.Дата = ДД.Период
		|		И Возврат.Ссылка = ДД.Регистратор
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И (ВЫБОР КОГДА ДД.Возвращено > 0 ТОГДА СправочникВидыЗапасов.Организация ИНАЧЕ ДД.ОрганизацияВладелец КОНЕЦ) В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ДД.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссиюВДругуюОрганизацию)
		|		// сворачиваем строки, различающиеся номерами ГТД
		|СГРУППИРОВАТЬ ПО
		|	(ВЫБОР
		|		КОГДА НЕ Возврат.Ссылка ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КОНЕЦ),
		|	ДД.Период,
		|	ДД.Регистратор,
		|	(ВЫБОР КОГДА ДД.Возвращено > 0 ТОГДА СправочникВидыЗапасов.Организация ИНАЧЕ ДД.ОрганизацияВладелец КОНЕЦ),
		|	ДД.АналитикаУчетаНоменклатуры,
		|	(ВЫБОР КОГДА ДД.Возвращено > 0 ТОГДА ДД.ВидЗапасовПродавца ИНАЧЕ СправочникВидыЗапасов.ВидЗапасовВладельца КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратПоКомиссииМеждуОрганизациями)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ДД.АналитикаУчетаНоменклатуры КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратПоКомиссииМеждуОрганизациями)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА ДД.Возвращено > 0 ТОГДА ДД.КорВидЗапасов
		|		ИНАЧЕ ДД.ВидЗапасовПродавца КОНЕЦ),
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация
		|";
КонецФункции

Функция ТекстПередачиВозвратыКомиссияМеждуОрганизациями()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПередачиВозвратыКомиссияМеждуОрганизациями"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	(ВЫБОР
		|		КОГДА ДД.Возвращено = 0 ТОГДА ""Потребление""
		|		ИНАЧЕ ""Перемещение"" КОНЕЦ) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.ОрганизацияВладелец КАК Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	ДД.ВидЗапасовПродавца.ВидЗапасовВладельца КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	0. КАК Знаменатель,
		|	СУММА(ДД.Количество + ДД.Возвращено) КАК Количество,
		|	0. КАК Стоимость,
		|	0. КАК СтоимостьБезНДС,
		|	0. КАК СтоимостьРегл,
		|	0. КАК НДСРегл,
		|	0. КАК ПостояннаяРазница,
		|	0. КАК ВременнаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.Период КАК ПериодПоступления,
		|	АналитикаКомиссионера.КлючАналитики КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасовПродавца.ВидЗапасовВладельца КАК КорВидЗапасов,
		|	(ВЫБОР
		|		КОГДА ДД.Возвращено = 0 ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ДД.Регистратор КОНЕЦ) КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизацийКПередаче КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаКомиссионера
		|		ПО АналитикаКомиссионера.Номенклатура = Аналитика.Номенклатура
		|		И АналитикаКомиссионера.Характеристика = Аналитика.Характеристика
		|		И АналитикаКомиссионера.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|		И АналитикаКомиссионера.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		И АналитикаКомиссионера.МестоХранения = ДД.ВидЗапасовПродавца.Организация
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.ОрганизацияВладелец В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ХозяйственнаяОперация В (
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссиюВДругуюОрганизацию),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратПоКомиссииМеждуОрганизациями))
		// сворачиваем строки, различающиеся номерами ГТД
		|СГРУППИРОВАТЬ ПО
		|	(ВЫБОР
		|		КОГДА ДД.Возвращено = 0 ТОГДА ""Потребление""
		|		ИНАЧЕ ""Перемещение"" КОНЕЦ),
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.ОрганизацияВладелец,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасовПродавца.ВидЗапасовВладельца,
		|	АналитикаКомиссионера.КлючАналитики,
		|	(ВЫБОР
		|		КОГДА ДД.Возвращено = 0 ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ДД.Регистратор КОНЕЦ),
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.ХозяйственнаяОперация
		|";
КонецФункции


// КОНТУР ПартииТоваровПереданныеНаКомиссию
Функция ТекстОстаткиПереданныхТоваров()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстОстаткиПереданныхТоваров"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	""Остаток"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию) КАК РазделУчета,
		|	&НачалоПериода КАК Период,
		|	ДД.ДокументПоступления КАК Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	0 КАК Знаменатель,
		|	ДД.КоличествоОстаток КАК Количество,
		|	ДД.СтоимостьОстаток КАК Стоимость,
		|	ДД.СтоимостьБезНДСОстаток КАК СтоимостьБезНДС,
		|	ДД.СтоимостьРеглОстаток КАК СтоимостьРегл,
		|	ДД.НДСРеглОстаток КАК НДСРегл,
		|	ДД.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
		|	ДД.ВременнаяРазницаОстаток КАК ВременнаяРазница,
		|	ИСТИНА КАК Первичное,
		|	ИСТИНА КАК СохранятьРегл,
		|	&НачалоПериода КАК ПериодПоступления,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	ДД.ДокументПередачиНаКомиссию КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ДД.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|	ДД.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровПереданныеНаКомиссию.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|ГДЕ
		|	ДД.КоличествоОстаток > 0
		|";
КонецФункции

Функция ТекстВводыОстатковПереданныхТоваров()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстВводыОстатковПереданныхТоваров"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	""Остаток"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию) КАК РазделУчета,
		|	&НачалоПериода КАК Период,
		|	ДД.ДокументПоступления КАК Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	0 КАК Знаменатель,
		|	ДД.Количество,
		|	ДД.Стоимость,
		|	ДД.СтоимостьБезНДС,
		|	ДД.СтоимостьРегл,
		|	ДД.НДСРегл,
		|	ДД.ПостояннаяРазница,
		|	ДД.ВременнаяРазница,
		|	ИСТИНА КАК Первичное,
		|	ИСТИНА КАК СохранятьРегл,
		|	&НачалоПериода КАК ПериодПоступления,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	ДД.ДокументПередачиНаКомиссию КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ДД.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|	ДД.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровПереданныеНаКомиссию КАК ДД
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ДД.Регистратор ССЫЛКА Документ.ВводОстатков
		|";
КонецФункции

Функция ТекстПередачиНаКомиссию()
	// передачи товаров на комиссию
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПередачиНаКомиссию"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	""Перемещение"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	0 КАК Знаменатель,
		|	СУММА(ДД.Количество) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.Период КАК ПериодПоступления,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	ДД.Регистратор КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ТоварыПереданныеНаКомиссию КАК ДД
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И НЕ ДД.Регистратор ССЫЛКА Документ.ВводОстатков
		// сворачиваем строки, различающиеся номерами ГТД
		|СГРУППИРОВАТЬ ПО
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.ХозяйственнаяОперация
		|";
КонецФункции

Функция ТекстВозвратыПереданныхТоваров()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстВозвратыПереданныхТоваров"" КАК ЗапросИсточник,
		|	90 Приоритет,
		|	(ВЫБОР
		|		КОГДА Реализация.Дата >= &НачалоПериода ТОГДА ""ВозвратКомиссия""
		|		ИНАЧЕ ""Потребление"" КОНЕЦ) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор, 
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	0 КАК Знаменатель,
		|	СУММА(ДД.Количество) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.Период КАК ПериодПоступления,
		|	ДД.КорАналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов КАК КорВидЗапасов,
		|	ДД.ДокументРеализации КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ТоварыПереданныеНаКомиссию КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК Реализация
		|		ПО Реализация.Ссылка = ДД.ДокументРеализации
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ДД.ХозяйственнаяОперация В (
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратПоКомиссииМеждуОрганизациями))
		// сворачиваем строки, различающиеся номерами ГТД
		|СГРУППИРОВАТЬ ПО
		|	(ВЫБОР
		|		КОГДА Реализация.Дата >= &НачалоПериода ТОГДА ""ВозвратКомиссия""
		|		ИНАЧЕ ""Потребление"" КОНЕЦ),
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ДД.ДокументРеализации,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация
		|";
КонецФункции
	
Функция ТекстВозвратыНаСкладПереданныхТоваров()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстВозвратыНаСкладПереданныхТоваров"" КАК ЗапросИсточник,
		|	90 Приоритет,
		|	""Сторно"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор, 
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	0 КАК Знаменатель,
		|	СУММА(ДД.Количество) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.Период КАК ПериодПоступления,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов,
		|	ДД.Регистратор КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК СправочникВидыЗапасов
		|		ПО СправочникВидыЗапасов.Ссылка = ДД.ВидЗапасов
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ДД.Организация = ДД.ОрганизацияОтгрузки
		|	И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера)
		|	И НЕ СправочникВидыЗапасов.РеализацияЗапасовДругойОрганизации
		// сворачиваем строки, различающиеся номерами ГТД
		|СГРУППИРОВАТЬ ПО
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов,
		|	ДД.ДокументРеализации,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация
		|";
КонецФункции

#КонецОбласти


#Область РасчетПартийПереданных

// Текст запроса исходных движений для расчета партий
// ПРИОРИТЕТ:
//	{10, Партия}, {10, Сторно}, {10, Перемещение}, {10, Перемещение/Собственные},
//	{80, Сторно/Чужие},
//	{90, Потребление}, {90, Потребление/Чужие}, {90, Потребление/Собственные}
// ПРИМЕЧАНИЯ:
//	ДокументПоступления - нужен для партий/остатков, приходных движений перемещений и для сторно
//	КорАналитикаУчетаНоменклатуры, КорВидЗапасов - нужны для расходных движений перемещений и для сторно.
Функция ДанныеДляПартийПереданных(НачалоПериода, ОкончаниеПериода, МассивОрганизаций)
	// СОРТИРОВКА по аналитикам, приоритету и моменту движения - требование алгоритма обсчета партий.
	ТекстСортировка = "
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура, Приоритет ВОЗР, ПериодПоступления, Регистратор
		|";
	ТекстИндексы = "
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|";	
		
	ИсходныйТекстЗапроса =
		ТекстПартииПереданныхИнициализация() + ";" // вт ПрошлыеПоступления
		+ ТекстОписаниеПартийПереданных()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстОстаткиПартийПереданных()  // использует ПрошлыеПоступления
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстНачальныеОстаткиПереданные()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПартииПереданные()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПартииВПути()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПартииВПутиРеализовано()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстВозвратыКомиссионеров()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстВозвратыМеждуОрганизациями()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстОтчетыКомиссионеров()
		+ ТекстИндексы;
	МВТ = Новый МенеджерВременныхТаблиц;	
	ИсходныйЗапрос = Новый Запрос(СтрЗаменить(ИсходныйТекстЗапроса, "//ВоВременнуюТаблицу", ""));
	ИсходныйЗапрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	ИсходныйЗапрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);	
	ИсходныйЗапрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);	
	ИсходныйЗапрос.МенеджерВременныхТаблиц = МВТ;
	ИсходныйЗапрос.Выполнить();
	
	Запрос = Новый Запрос(ТекстОписаниеПартийПереданных());
	РасчетныеПартии = Запрос.Выполнить().Выгрузить();
	
	ВременнаяТаблицаДанныхПоОтбору("Номенклатура", ТекстСортировка, РасчетныеПартии, МВТ);

	Возврат МВТ;
КонецФункции

Функция РасчетныеПартииПереданные(ДанныеДляРасчета, Регистраторы, ЦепочкиДвижений = Неопределено, Тест = Ложь, КоличествоЗаписей = 0) // ДанныеДляРасчета будут изменены
	// Шаг 1: создаем буфер накопления рассчитанных партий
	Запрос = Новый Запрос(ТекстОписаниеПартийПереданных());
	РасчетныеПартии = Запрос.Выполнить().Выгрузить();
	КоличествоЗаписей = РасчетныеПартии.Количество();
	
	// Шаг 2: обсчитываем цепочки движений по передачам, возвратам, перемещениям, порчам, пересортицам и т.д.
	КонтекстЦепочек = ОписаниеЦепочек("Потребление, Партия, Остаток, Сторно, Возврат, ВПути");
	
	ПоляПотреблений = "РазделУчета, Организация, АналитикаУчетаНоменклатуры, ВидЗапасов";
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Потребление", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Партия", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "ВПути", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Остаток", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Сторно", ПоляПотреблений);
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Сторно", "АналитикаУчетаНоменклатуры, ВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Сторно", "Партия", "АналитикаУчетаНоменклатуры, ВидЗапасов");
	
	ПоляВозврата = "РазделУчета, Организация, АналитикаУчетаНоменклатуры, ВидЗапасов, ДокументПоступления, ДокументПередачиНаКомиссию";
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Возврат", ПоляВозврата);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Возврат", "Остаток", ПоляВозврата);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Возврат", "Партия", ПоляВозврата);
	
	// Шаг 3: замена ложных партий на первичные партии в расчетных строках по результатам построение графа замен.
	КонтекстДвижений = ОписаниеДвижений(
		"ПартииТоваровПереданныеНаКомиссию",
		"ПартииТоваровПереданныеНаКомиссию",
		ПереченьПолей(РасчетныеПартии.Колонки),
		ПоляПотреблений,
		"Количество, Стоимость, СтоимостьБезНДС, СтоимостьРегл, НДСРегл, ПостояннаяРазница, ВременнаяРазница",
		"Количество", "Количество", "ДокументПередачиНаКомиссию", "Период");
	ЦепочкиДвижений(КонтекстЦепочек, ДанныеДляРасчета);
	РассчитатьПартииПоЦепочкам(КонтекстДвижений, ДанныеДляРасчета, РасчетныеПартии, Регистраторы, Тест);
	ЦепочкиДвижений = ДанныеДляРасчета;
	
	// Шаг 4: Удаляем нерассчитанные партии
	УдалитьНезаписываемыеСтроки(КонтекстДвижений.Контекст, РасчетныеПартии);
	
	Возврат РасчетныеПартии;
КонецФункции

Функция ТекстПартииПереданныхИнициализация()
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ДокументПоступления КАК Ссылка
		|ПОМЕСТИТЬ ПрошлыеПоступленияОстатки
		|ИЗ
		|	РегистрНакопления.ПартииТоваровПереданныеНаКомиссию.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|ГДЕ
		|	ДД.ДокументПоступления <> НЕОПРЕДЕЛЕНО
		|;
		|ВЫБРАТЬ
		|	ПрошлыеПоступления.Ссылка,
		|	МАКСИМУМ(Периодика.Период) КАК Период
		|ПОМЕСТИТЬ ПрошлыеПоступления
		|ИЗ
		|	ПрошлыеПоступленияОстатки КАК ПрошлыеПоступления
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровПереданныеНаКомиссию КАК Периодика
		|		ПО Периодика.Период < &НачалоПериода
		|		И Периодика.ДокументПоступления = ПрошлыеПоступления.Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ПрошлыеПоступления.Ссылка
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|";
КонецФункции

Функция ТекстОписаниеПартийПереданных()
	Возврат "
		|ВЫБРАТЬ ПЕРВЫЕ 0
		|	""ТекстОписаниеПартийПереданных_ХХХХХХХХХХХХХХ"" КАК ЗапросИсточник,
		|	0 КАК Приоритет,
		|	""ХХХХХХХХХХХХХХХХ"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК Период,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ДД.ДокументПередачиНаКомиссию,
		|	0 КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ПериодПоступления,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаНоменклатуры
		|//ВоВременнуюТаблицу ПОМЕСТИТЬ ИсходныеДанные
		|ИЗ
		|	РегистрНакопления.ПартииТоваровПереданныеНаКомиссию КАК ДД
		|";
КонецФункции

Функция ТекстОстаткиПартийПереданных() // использует ПрошлыеПоступления
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстОстаткиПартийПереданных"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	""Остаток"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию) КАК РазделУчета,
		|	Периодика.Период КАК Период,
		|	ДД.ДокументПоступления КАК Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.ДокументПередачиНаКомиссию,
		|	ДД.КоличествоОстаток КАК Количество,
		|	ДД.СтоимостьОстаток КАК Стоимость,
		|	ДД.СтоимостьБезНДСОстаток КАК СтоимостьБезНДС,
		|	ДД.СтоимостьРеглОстаток КАК СтоимостьРегл,
		|	ДД.НДСРеглОстаток КАК НДСРегл,
		|	ДД.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
		|	ДД.ВременнаяРазницаОстаток КАК ВременнаяРазница,
		|	Периодика.Период КАК ПериодПоступления,
		|	ДД.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|	ДД.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаНоменклатуры
		|ИЗ
		|	РегистрНакопления.ПартииТоваровПереданныеНаКомиссию.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрошлыеПоступления КАК Периодика
		|		ПО Периодика.Ссылка = ДД.ДокументПоступления
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК Отгрузки
		|		ПО Отгрузки.Ссылка = ДД.ДокументПередачиНаКомиссию
		|		И Отгрузки.ХозяйственнаяОперация В (
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности))
		|ГДЕ
		|	ДД.КоличествоОстаток > 0
		|	И Отгрузки.Ссылка ЕСТЬ NULL
		|";
КонецФункции

Функция ТекстНачальныеОстаткиПереданные()
	Возврат "
		// Ввод начальных остатков
		|ВЫБРАТЬ
		|	""ТекстНачальныеОстаткиПереданные"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	""Партия"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ДД.ВидДвижения КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.ДокументПередачиНаКомиссию,
		|	ДД.Количество,
		|	ДД.Стоимость,
		|	ДД.СтоимостьБезНДС,
		|	ДД.СтоимостьРегл,
		|	ДД.НДСРегл,
		|	ДД.ПостояннаяРазница,
		|	ДД.ВременнаяРазница,
		|	ДД.Период КАК ПериодПоступления,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаНоменклатуры
		|ИЗ
		|	РегистрНакопления.ПартииТоваровПереданныеНаКомиссию КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизаций КАК Товары
		|		ПО Товары.Регистратор = ДД.Регистратор
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ДД.Регистратор = ДД.ДокументПоступления
		|	И Товары.Регистратор ЕСТЬ NULL
		|";
КонецФункции

Функция ТекстПартииПереданные()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПартииПереданные"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	""Партия"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	Партии.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	Партии.АналитикаУчетаПартий,
		|	Партии.Регистратор КАК ДокументПередачиНаКомиссию,
		|	СУММА(Партии.Количество) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК Количество,
		|	СУММА(Партии.Стоимость) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК Стоимость,
		|	СУММА(Партии.СтоимостьБезНДС) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК СтоимостьБезНДС,
		|	СУММА(Партии.СтоимостьРегл) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК СтоимостьРегл,
		|	СУММА(Партии.НДСРегл) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК НДСРегл,
		|	СУММА(Партии.ПостояннаяРазница) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК ПостояннаяРазница,
		|	СУММА(Партии.ВременнаяРазница) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК ВременнаяРазница,
		|	ДД.Период КАК ПериодПоступления,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.КорАналитикаУчетаНоменклатуры
		|ИЗ
		|	РегистрНакопления.ТоварыПереданныеНаКомиссию КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровОрганизаций КАК Партии
		|		ПО Партии.Регистратор = ДД.Регистратор
		|		И Партии.АналитикаУчетаНоменклатуры = ДД.КорАналитикаУчетаНоменклатуры
		|		И Партии.ВидЗапасов = ДД.ВидЗапасов
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И Партии.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И Партии.Количество > 0
		|	И НЕ Партии.Первичное
		// сворачиваем строки, различающиеся номерами ГТД
		|СГРУППИРОВАТЬ ПО
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	Партии.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	Партии.АналитикаУчетаПартий,
		|	Партии.Регистратор,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.КорАналитикаУчетаНоменклатуры
		|";
КонецФункции

Функция ТекстПартииВПути()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПартииВПути"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	(ВЫБОР
		|		КОГДА Отгрузки.ДатаПереходаПраваСобственности МЕЖДУ &НачалоПериода И &ОкончаниеПериода ТОГДА ""ВПути""
		|		ИНАЧЕ ""Партия"" КОНЕЦ) КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыВПути) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.КорАналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.Регистратор КАК ДокументПередачиНаКомиссию,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(ДД.Стоимость) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.НДСРегл) КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	ДД.Период КАК ПериодПоступления,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК Отгрузки
		|		ПО Отгрузки.Ссылка = ДД.Регистратор
		|ГДЕ
		|	(ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		ИЛИ Отгрузки.ДатаПереходаПраваСобственности МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		ИЛИ Отгрузки.ДатаПереходаПраваСобственности = ДАТАВРЕМЯ(1,1,1))
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И Отгрузки.ХозяйственнаяОперация В (
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности))
		// сворачиваем строки, различающиеся номерами ГТД
		|СГРУППИРОВАТЬ ПО
		|	(ВЫБОР
		|		КОГДА Отгрузки.ДатаПереходаПраваСобственности МЕЖДУ &НачалоПериода И &ОкончаниеПериода ТОГДА ""ВПути""
		|		ИНАЧЕ ""Партия"" КОНЕЦ),
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.Регистратор,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.КорАналитикаУчетаНоменклатуры
		|";
КонецФункции

Функция ТекстПартииВПутиРеализовано()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПартииВПутиРеализовано"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыВПути) КАК РазделУчета,
		|	Отгрузки.ДатаПереходаПраваСобственности КАК Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.КорАналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.Регистратор КАК ДокументПередачиНаКомиссию,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(ДД.Стоимость) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.НДСРегл) КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	Отгрузки.ДатаПереходаПраваСобственности КАК ПериодПоступления,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	НЕОПРЕДЕЛЕНО КАК НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК Отгрузки
		|		ПО Отгрузки.Ссылка = ДД.Регистратор
		|ГДЕ
		|	(Отгрузки.ДатаПереходаПраваСобственности МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		ИЛИ ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода)
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И Отгрузки.ХозяйственнаяОперация В (
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности))
		|	И Отгрузки.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.Отгружено)
		// сворачиваем строки, различающиеся номерами ГТД
		|СГРУППИРОВАТЬ ПО
		|	Отгрузки.ДатаПереходаПраваСобственности,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.Регистратор,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.КорАналитикаУчетаНоменклатуры
		|";
КонецФункции

Функция ТекстВозвратыКомиссионеров()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстВозвратыКомиссионеров"" КАК ЗапросИсточник,
		|	45 КАК Приоритет,
		|	""Возврат"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	Партии.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	Партии.АналитикаУчетаПартий,
		|	Партии.ДокументИсточник КАК ДокументПередачиНаКомиссию,
		|	- СУММА(Партии.Количество) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК Количество,
		|	- СУММА(Партии.Стоимость) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК Стоимость,
		|	- СУММА(Партии.СтоимостьБезНДС) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК СтоимостьБезНДС,
		|	- СУММА(Партии.СтоимостьРегл) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК СтоимостьРегл,
		|	- СУММА(Партии.НДСРегл) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК НДСРегл,
		|	- СУММА(Партии.ПостояннаяРазница) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК ПостояннаяРазница,
		|	- СУММА(Партии.ВременнаяРазница) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК ВременнаяРазница,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ПериодПоступления,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.КорАналитикаУчетаНоменклатуры
		|ИЗ
		|	РегистрНакопления.ТоварыПереданныеНаКомиссию КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровОрганизаций КАК Партии
		|		ПО Партии.Регистратор = ДД.Регистратор
		|		И Партии.КорАналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|		И Партии.КорВидЗапасов = ДД.ВидЗапасов
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И НЕ ДД.Регистратор ССЫЛКА Документ.ВозвратТоваровМеждуОрганизациями
		|	И Партии.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И Партии.Количество < 0
		// сворачиваем строки, различающиеся номерами ГТД
		|СГРУППИРОВАТЬ ПО
		|	(НЕ Партии.Первичное),
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	Партии.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	Партии.АналитикаУчетаПартий,
		|	Партии.ДокументИсточник,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.КорАналитикаУчетаНоменклатуры
		|";
КонецФункции

Функция ТекстВозвратыМеждуОрганизациями()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстВозвратыМеждуОрганизациями"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Возврат"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	Партии.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	Партии.АналитикаУчетаПартий,
		|	Партии.ДокументИсточник КАК ДокументПередачиНаКомиссию,
		|	СУММА(Партии.Количество) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК Количество,
		|	СУММА(Партии.Стоимость) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК Стоимость,
		|	СУММА(Партии.СтоимостьБезНДС) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК СтоимостьБезНДС,
		|	СУММА(Партии.СтоимостьРегл) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК СтоимостьРегл,
		|	СУММА(Партии.НДСРегл) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК НДСРегл,
		|	СУММА(Партии.ПостояннаяРазница) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК ПостояннаяРазница,
		|	СУММА(Партии.ВременнаяРазница) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.НомерСтроки) КАК ВременнаяРазница,
		|	Партии.ДокументПоступления.Дата КАК ПериодПоступления,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.КорАналитикаУчетаНоменклатуры
		|ИЗ
		|	РегистрНакопления.ТоварыПереданныеНаКомиссию КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровОрганизаций КАК Партии
		|		ПО Партии.Регистратор = ДД.Регистратор
		|		И Партии.КорАналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|		И Партии.КорВидЗапасов = ДД.ВидЗапасов
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ДД.Регистратор ССЫЛКА Документ.ВозвратТоваровМеждуОрганизациями
		// сворачиваем строки, различающиеся номерами ГТД
		|СГРУППИРОВАТЬ ПО
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	Партии.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	Партии.АналитикаУчетаПартий,
		|	Партии.ДокументИсточник,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.КорАналитикаУчетаНоменклатуры
		|";
КонецФункции
	
Функция ТекстОтчетыКомиссионеров()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстОтчетыКомиссионеров"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	ВЫБОР
		|		КОГДА СУММА(ДД.Количество) < 0
		|		ТОГДА ""Сторно""
		|		ИНАЧЕ ""Потребление""
		|	КОНЕЦ КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	ДД.ВидЗапасов КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПередачиНаКомиссию,
		|	СУММА(ДД.Количество) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ДД.Период КАК ПериодПоступления,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.КорАналитикаУчетаНоменклатуры
		|ИЗ
		|	РегистрНакопления.ТоварыПереданныеНаКомиссию КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизаций КАК Товары
		|		ПО Товары.Регистратор = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизацийКПередаче КАК ТоварыКПередаче
		|		ПО ТоварыКПередаче.Регистратор = ДД.Регистратор
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И Товары.Регистратор ЕСТЬ NULL
		|	И ТоварыКПередаче.Регистратор ЕСТЬ NULL
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.КорАналитикаУчетаНоменклатуры
		|";
КонецФункции

#КонецОбласти


#Область РасчетПартийЗатрат

Функция ДанныеДляПартийЗатрат(НачалоПериода, ОкончаниеПериода, МассивОрганизаций)
	ТекстСортировка = "
		|УПОРЯДОЧИТЬ ПО
		|	ДокументВыпуска, Период, Регистратор, Приоритет ВОЗР
		|";
	ТекстИндексы = "
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументВыпуска
		|";	
	// Выбираем приходные движения партий затрат, которые дальше будем двигать по партиям.
	ИсходныйТекстЗапроса =
		ТекстПартииЗатратИнициализация() + ";" // вт Выпуски
		+ ТекстОписаниеПартийЗатрат()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстОстаткиПартийЗатрат() // использует Выпуски
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПриходыСборокТоваров() // использует ПриходыСборок
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПриходыСборокТоваровПолуфабрикаты() // использует Выпуски, ПриходыСборок
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстРасходыПартийЗатратПрошлыхМесяцев() // использует ПрошлыеРеализации
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстИтерацияПартийЗатрат()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстИтерацияПартийЗатратКомиссия()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстИтерацияПартийЗатратКомиссияВозврат()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстДополнениеПартийЗатрат()
		+ ТекстИндексы;
	МВТ = Новый МенеджерВременныхТаблиц;	
	ИсходныйЗапрос = Новый Запрос(СтрЗаменить(ИсходныйТекстЗапроса, "//ВоВременнуюТаблицу", ""));
	ИсходныйЗапрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	ИсходныйЗапрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);	
	ИсходныйЗапрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);	
	ИсходныйЗапрос.УстановитьПараметр("Итерация", Ложь);
	ИсходныйЗапрос.УстановитьПараметр("СобственныеТипыЗапасов", РасчетСебестоимостиПрикладныеАлгоритмы.СобственныеТипыЗапасов());
	ИсходныйЗапрос.УстановитьПараметр("ЕстьФИФОСкользящая", ЕстьФИФОСкользящая(НачалоПериода, ОкончаниеПериода, МассивОрганизаций));
	ИсходныйЗапрос.МенеджерВременныхТаблиц = МВТ;
	ИсходныйЗапрос.Выполнить();
	
	Запрос = Новый Запрос(ТекстОписаниеПартийЗатрат());
	РасчетныеПартии = Запрос.Выполнить().Выгрузить();
	
	ВременнаяТаблицаДанныхПоОтбору("ДокументВыпуска", ТекстСортировка, РасчетныеПартии, МВТ);
	
	Возврат МВТ;
КонецФункции

Функция РасчетныеПартииЗатрат(ДанныеДляРасчета, Регистраторы, ЦепочкиДвижений = Неопределено, Тест = Ложь, РассчитатьПартии = Истина) // ДанныеДляРасчета будут изменены
	// Шаг 1: создаем буфер накопления рассчитанных партий
	Запрос = Новый Запрос(ТекстОписаниеПартийЗатрат());
	РасчетныеПартии = Запрос.Выполнить().Выгрузить();
	
	// Шаг 2: обсчитываем цепочки движений по передачам, возвратам, перемещениям, порчам, пересортицам и т.д.
	КонтекстЦепочек = ОписаниеЦепочек("Потребление, Перемещение, Партия, Остаток, Выпуск, Сторно, Замещение, Продукция, Передача, ПотреблениеВПути,
	|Сборка, Затраты, ЗатратыПрочие, Распределение, ВыпускПрочие, ВПути, Прочее, Списание, Прошлое");
	
	ПоляПотреблений = "РазделУчета, ДокументВыпуска, АналитикаУчетаПродукции, Организация, ДокументИсточник";
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Потребление", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Партия", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Остаток", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Перемещение", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Замещение", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Выпуск", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "ВыпускПрочие", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Передача", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Сборка", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Сторно", "РазделУчета, ДокументВыпуска, АналитикаУчетаПродукции, Организация, Регистратор");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "ПотреблениеВПути", "РазделУчета, ДокументВыпуска, АналитикаУчетаПродукции, Организация, Регистратор");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ПотреблениеВПути", "Передача", "РазделУчета, ДокументВыпуска, АналитикаУчетаПродукции, Организация, Регистратор");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ПотреблениеВПути", "Остаток", ПоляПотреблений);

	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Перемещение", "Регистратор, АналитикаУчетаПродукции, ДокументВыпуска");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Перемещение", "Потребление", "Регистратор, КорАналитикаУчетаПродукции, ДокументВыпуска");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Перемещение", "Продукция", "Регистратор, КорАналитикаУчетаПродукции, ДокументВыпуска");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Замещение", "ДокументИсточник, АналитикаУчетаПродукции");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Замещение", "Потребление", "Регистратор, КорАналитикаУчетаПродукции");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Передача", "Регистратор, КорАналитикаУчетаПродукции, ДокументВыпуска");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Передача", "Потребление", "Регистратор, АналитикаУчетаПродукции, ДокументВыпуска");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Выпуск", "ДокументИсточник, АналитикаУчетаПродукции, АналитикаУчетаНоменклатуры, ДокументПоступления, ВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Выпуск", "Потребление", "Регистратор, КорАналитикаУчетаПродукции, АналитикаУчетаПродукции, ДокументПоступления, КорВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Выпуск", "Продукция", "Регистратор, КорАналитикаУчетаПродукции, АналитикаУчетаПродукции, ДокументВыпуска, КорВидЗапасов");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Сборка", "ДокументИсточник, АналитикаУчетаПродукции");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Сборка", "Потребление", "Регистратор, КорАналитикаУчетаПродукции");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Продукция", "ДокументИсточник, РазделУчета, ДокументВыпуска, АналитикаУчетаПродукции, Организация");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Продукция", "Партия", "ДокументИсточник, РазделУчета, ДокументВыпуска, АналитикаУчетаПродукции, Организация");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Продукция", "Остаток", "ДокументИсточник, РазделУчета, ДокументВыпуска, АналитикаУчетаПродукции, Организация");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Продукция", "Перемещение", "ДокументИсточник, РазделУчета, ДокументВыпуска, АналитикаУчетаПродукции, Организация");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Продукция", "Выпуск", "ДокументИсточник, РазделУчета, ДокументВыпуска, АналитикаУчетаПродукции, Организация");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Продукция", "ВыпускПрочие", "ДокументИсточник, РазделУчета, ДокументВыпуска, АналитикаУчетаПродукции, Организация");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Сторно", "ДокументИсточник, РазделУчета, КорАналитикаУчетаПродукции, ДокументВыпуска");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Сторно", "Потребление", "Регистратор, РазделУчета, АналитикаУчетаПродукции, ДокументВыпуска");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Сторно", "ПотреблениеВПути", "Регистратор, РазделУчета, КорАналитикаУчетаПродукции, ДокументВыпуска");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Сторно", "Прошлое", "Регистратор, РазделУчета, АналитикаУчетаПродукции, ДокументВыпуска");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "ЗатратыПрочие", "ДокументИсточник, СтатьяРасходовСписания, АналитикаРасходов, ПодразделениеРасходов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ЗатратыПрочие", "Потребление", "Регистратор, СтатьяРасходовСписания, АналитикаРасходов, ПодразделениеРасходов");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Распределение", "ДокументИсточник");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Распределение", "Затраты", "Регистратор");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Распределение", "ЗатратыПрочие", "Регистратор");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Прочее", "Организация, СтатьяРасходовСписания, АналитикаРасходов, ПодразделениеРасходов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Прочее", "Затраты", "Организация, СтатьяРасходовСписания, АналитикаРасходов, ПодразделениеРасходов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Прочее", "ЗатратыПрочие", "Организация, СтатьяРасходовСписания, АналитикаРасходов, ПодразделениеРасходов");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Списание", "Регистратор");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Списание", "Прочее", "Регистратор");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "ВыпускПрочие", "ДокументИсточник, АналитикаУчетаПродукции, СтатьяРасходовСписания, АналитикаРасходов, ПодразделениеРасходов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ВыпускПрочие", "Распределение", "Регистратор, КорАналитикаУчетаПродукции, СтатьяРасходовСписания, АналитикаРасходов, ПодразделениеРасходов");
	
	// Шаг 4: замена ложных партий на первичные партии в расчетных строках по результатам построение графа замен.
	КонтекстДвижений = ОписаниеДвижений(
		"ПартииЗатратНаВыпуск",
		"ПартииЗатратНаВыпуск",
		ПереченьПолей(РасчетныеПартии.Колонки),
		ПоляПотреблений,
		"Знаменатель, Количество, Стоимость, СтоимостьБезНДС, СтоимостьРегл, НДСРегл, ПостояннаяРазница, ВременнаяРазница",
		"Знаменатель", "Знаменатель", "ДокументИсточник", "Период");
	КонтекстДвижений.Вставить("ЗаполнятьАналитикуПартий", Истина);
	КонтекстДвижений.Вставить("ПоляСуммирования", "Количество, Стоимость, СтоимостьБезНДС, СтоимостьРегл, НДСРегл, ПостояннаяРазница, ВременнаяРазница");
	КонтекстДвижений.Вставить("ПоляГруппировки", ПереченьПолей(РасчетныеПартии.Колонки, КонтекстДвижений.ПоляСуммирования));
	КонтекстДвижений.Вставить("УменьшатьБазис", Истина);
	ЦепочкиДвижений(КонтекстЦепочек, ДанныеДляРасчета);
	Если РассчитатьПартии Тогда
		РассчитатьПартииПоЦепочкам(КонтекстДвижений, ДанныеДляРасчета, РасчетныеПартии, Регистраторы, Тест);
	КонецЕсли;
	ЦепочкиДвижений = ДанныеДляРасчета;
	
	// Шаг 4: Удаляем нерассчитанные партии
	Если Не Тест Тогда
		УдалитьНезаписываемыеСтроки(КонтекстДвижений.Контекст, РасчетныеПартии);
	КонецЕсли;
	
	Возврат РасчетныеПартии;
КонецФункции

Функция ТекстПартииЗатратИнициализация() // вт ПрошлыеРеализации, Выпуски, ОстаткиПартийСборок, ПроизводственныеЗатраты, ДолиСтоимости, ВыпускиПродукции, ПриходыСборок, ПрошлыеПоступления, ИсключаемыеВыпуски
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ДокументРеализации КАК ДокументРеализации,
		|	ДД.КорАналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
		|ПОМЕСТИТЬ
		|	ПрошлыеРеализации
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизаций КАК Расход
		|		ПО Расход.Период < &НачалоПериода
		|		И Расход.Регистратор = ДД.ДокументРеализации
		|		И Расход.АналитикаУчетаНоменклатуры = ДД.КорАналитикаУчетаНоменклатуры
		|		И Расход.ВидЗапасов = ДД.КорВидЗапасов
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ДокументРеализации <> НЕОПРЕДЕЛЕНО
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ДД.Количество < 0
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ДокументРеализации КАК ДокументРеализации,
		|	ДД.КорАналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизацийКПередаче КАК Расход
		|		ПО Расход.Период < &НачалоПериода
		|		И Расход.Регистратор = ДД.ДокументРеализации
		|		И Расход.АналитикаУчетаНоменклатуры = ДД.КорАналитикаУчетаНоменклатуры
		|		И Расход.ВидЗапасовПродавца = ДД.КорВидЗапасов
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Активность
		|	И ДД.ДокументРеализации <> НЕОПРЕДЕЛЕНО
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ДД.Количество < 0
		|;
		// Документы и даты выпусков и сборок
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор,
		|	Аналитика.Номенклатура КАК Номенклатура,
		|	Аналитика.Характеристика КАК Характеристика,
		|	МАКСИМУМ(ДД.Период) КАК Период
		|ПОМЕСТИТЬ Выпуски
		|ИЗ (
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ДД.ДокументВыпуска КАК Регистратор,
		|		ДД.АналитикаУчетаПродукции КАК АналитикаУчетаПродукции,
		|		Приходы.Период
		|	ИЗ
		|		РегистрНакопления.ПартииЗатратНаВыпуск.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииЗатратНаВыпуск КАК Приходы
		|			ПО Приходы.Период < &НачалоПериода
		|			И Приходы.Регистратор = ДД.ДокументВыпуска
		|			И Приходы.Регистратор = Приходы.ДокументВыпуска
		|
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ДД.Регистратор,
		|		ДД.АналитикаУчетаНоменклатуры,
		|		ДД.Период
		|	ИЗ
		|		РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|	ГДЕ
		|		ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И ДД.Организация В (&МассивОрганизаций)
		|		И ДД.Активность
		|		И ТИПЗНАЧЕНИЯ(ДД.Регистратор) В (ТИП(Документ.СборкаТоваров),
		|										ТИП(Документ.ПересортицаТоваров),
		|										ТИП(Документ.ПорчаТоваров))
		|		И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|) КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаПродукции
		|СГРУППИРОВАТЬ ПО
		|	Аналитика.Номенклатура,
		|	Аналитика.Характеристика,
		|	ДД.Регистратор
		|;
		// Остатки партионных регистров на начало
		|ВЫБРАТЬ
		|	Партии.Организация,
		|	Партии.АналитикаУчетаНоменклатуры,
		|	Партии.ДокументПоступления,
		|	СУММА(Партии.Количество) КАК Количество
		|ПОМЕСТИТЬ
		|	ОстаткиПартийСборок
		|ИЗ (
		|	ВЫБРАТЬ
		|		ДД.Организация,
		|		ДД.АналитикаУчетаНоменклатуры,
		|		ДД.ДокументПоступления,
		|		ДД.КоличествоОстаток КАК Количество
		|	ИЗ
		|		РегистрНакопления.ПартииТоваровОрганизаций.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ
		|		ДД.Организация,
		|		ДД.АналитикаУчетаНоменклатуры,
		|		ДД.ДокументПоступления,
		|		ДД.КоличествоОстаток КАК Количество
		|	ИЗ
		|		РегистрНакопления.ПартииТоваровПереданныеНаКомиссию.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ
		|		ДД.Организация,
		|		ДД.АналитикаУчетаНоменклатуры,
		|		ДД.ДокументПоступления,
		|		ДД.КоличествоОстаток КАК Количество
		|	ИЗ
		|		РегистрНакопления.ПартииПроизводственныхЗатрат.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|
		|	) КАК Партии
		|СГРУППИРОВАТЬ ПО
		|	Партии.Организация,
		|	Партии.АналитикаУчетаНоменклатуры,
		|	Партии.ДокументПоступления
		|;
		// Документы передачи в производство и возврата из производства
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ
		|	МатериалыИРаботыВПроизводстве
		|ИЗ
		|	РегистрНакопления.МатериалыИРаботыВПроизводстве КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Товары
		|		ПО Товары.Ссылка = ДД.АналитикаУчетаНоменклатуры.Номенклатура
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И Товары.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|ИНДЕКСИРОВАТЬ ПО
		|	ДД.Регистратор
		|;

		// Поступления комплектов сборок товаров
		|ВЫБРАТЬ
		|	ДД.Регистратор,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.ДокументИсточник,
		|	СУММА(ДД.Количество) КАК Количество
		|ПОМЕСТИТЬ
		|	ПриходыСборок
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.Регистратор ССЫЛКА Документ.СборкаТоваров
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.ДокументИсточник
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ДокументПоступления КАК Ссылка,
		|	ДД.Склад КАК Склад,
		|	МАКСИМУМ(ДД.Регистратор) КАК Регистратор,
		|	МАКСИМУМ(ДД.Период) КАК Период
		|ПОМЕСТИТЬ
		|	ПрошлыеПоступления
		|ИЗ (
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ДД.ДокументПоступления,
		|		ДД.АналитикаУчетаНоменклатуры.МестоХранения КАК Склад,
		|		МАКСИМУМ(Периодика.Регистратор) КАК Регистратор,
		|		МАКСИМУМ(Периодика.Период) КАК Период
		|	ИЗ
		|		РегистрНакопления.ПартииПроизводственныхЗатрат.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииПроизводственныхЗатрат КАК Периодика
		|			ПО Периодика.Период < &НачалоПериода
		|			И Периодика.ДокументПоступления = ДД.ДокументПоступления
		|			И Периодика.АналитикаУчетаНоменклатуры.МестоХранения = ДД.АналитикаУчетаНоменклатуры.МестоХранения
		|	ГДЕ
		|		ДД.ДокументПоступления <> НЕОПРЕДЕЛЕНО
		|		И Периодика.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И Периодика.Количество > 0
		|	СГРУППИРОВАТЬ ПО
		|		ДД.ДокументПоступления,
		|		ДД.АналитикаУчетаНоменклатуры.МестоХранения
		|
		|	) КАК ДД
		|СГРУППИРОВАТЬ ПО
		|	ДД.ДокументПоступления,
		|	ДД.Склад
		|;
		// Подразделение документов-регистраторов
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор,
		|	ДД.Подразделение
		|ПОМЕСТИТЬ
		|	ПодразделенияРегистраторов
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК ДД
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		ИЛИ ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноСписанияНаРасходы))
		|	И ДД.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|";
КонецФункции

Функция ТекстОписаниеПартийЗатрат()
	Возврат "
		|ВЫБРАТЬ ПЕРВЫЕ 0
		|	ВЫРАЗИТЬ("""" КАК СТРОКА(80)) КАК ЗапросИсточник,
		|	0 КАК Приоритет,
		|	""ХХХХХХХХХХХХХХХХ"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК Период,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаПродукции,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
		|	ДД.ДокументВыпуска,
		|	ДД.ДокументПоступления,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	0 КАК Знаменатель,
		|	0 КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.Регистратор КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаПродукции,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Продукция,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаПродукции,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ДД.СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов,
		|	ДД.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ДД.АналитикаАктивовПассивов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	ДД.СтатьяСписанияНДС,
		|	ДД.АналитикаСписанияНДС
		|//ВоВременнуюТаблицу ПОМЕСТИТЬ ИсходныеДанные
		|ИЗ
		|	РегистрНакопления.ПартииЗатратНаВыпуск КАК ДД
		|";
КонецФункции

Функция ТекстОстаткиПартийЗатрат() // использует Выпуски, ОстаткиПартийСборок, ПрошлыеПоступления
	Возврат "
		// остатки партий затрат на выпуск
		|ВЫБРАТЬ
		|	""ТекстОстаткиПартийЗатрат"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Остаток"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	(ВЫБОР
		|		КОГДА Периодика.Регистратор ЕСТЬ NULL ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КОНЕЦ) КАК РазделУчета,
		|	Выпуски.Период КАК Период,
		|	ЕСТЬNULL(Периодика.Регистратор, ДД.ДокументВыпуска) КАК Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаПродукции,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов,
		|	ДД.ДокументВыпуска,
		|	ДД.ДокументПоступления,
		|	ДД.АналитикаУчетаПартий,
		|	Партии.Количество КАК Знаменатель,
		|	ДД.КоличествоОстаток КАК Количество,
		|	ДД.СтоимостьОстаток КАК Стоимость,
		|	ДД.СтоимостьБезНДСОстаток КАК СтоимостьБезНДС,
		|	ДД.СтоимостьРеглОстаток КАК СтоимостьРегл,
		|	ДД.НДСРеглОстаток КАК НДСРегл,
		|	ДД.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
		|	ДД.ВременнаяРазницаОстаток КАК ВременнаяРазница,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ЕСТЬNULL(Периодика.Регистратор, ДД.ДокументВыпуска) КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаПродукции,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)  КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	"""",
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииЗатратНаВыпуск.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаПродукции
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Выпуски
		|		ПО Выпуски.Регистратор = ДД.ДокументВыпуска
		|		И Выпуски.Номенклатура = Аналитика.Номенклатура
		|		И Выпуски.Характеристика = Аналитика.Характеристика
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОстаткиПартийСборок КАК Партии
		|		ПО Партии.Организация = ДД.Организация
		|		И Партии.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаПродукции
		|		И Партии.ДокументПоступления = ДД.ДокументВыпуска
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПрошлыеПоступления КАК Периодика
		|		ПО Периодика.Ссылка = ДД.ДокументВыпуска
		|		И Периодика.Склад = ДД.АналитикаУчетаПродукции.МестоХранения
		|ГДЕ
		|	Партии.Количество > 0
		|";
КонецФункции

Функция ТекстПриходыСборокТоваров() // использует ПриходыСборок
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПриходыСборокТоваров"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Партия"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.КорАналитикаУчетаНоменклатуры КАК АналитикаУчетаПродукции,
		|	ДД.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов КАК ВидЗапасов,
		|	ДД.Регистратор КАК ДокументВыпуска,
		|	ДД.ДокументПоступления КАК ДокументПоступления,
		|	ДД.АналитикаУчетаПартий,
		|	Приходы.Количество КАК Знаменатель,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(ДД.Стоимость) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.НДСРегл) КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	Приходы.ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаПродукции,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	"""",
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПриходыСборок КАК Приходы
		|		ПО Приходы.Регистратор = ДД.Регистратор
		|		И (Приходы.ВидЗапасов = ДД.КорВидЗапасов
		|			ИЛИ ДД.КорВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
		|			И ДД.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка))
		|		И Приходы.АналитикаУчетаНоменклатуры = ДД.КорАналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
		|		ПО ВидыЗапасов.Ссылка = ДД.ВидЗапасов
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.СборкаТоваров)
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ЕСТЬNULL(ВидыЗапасов.ТипЗапасов, ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)) В (&СобственныеТипыЗапасов)
		|	И (НЕ ДД.Стоимость = 0
		|		ИЛИ НЕ ДД.СтоимостьБезНДС = 0)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов,
		|	ДД.Регистратор,
		|	ДД.ДокументПоступления,
		|	ДД.АналитикаУчетаПартий,
		|	Приходы.Количество,
		|	Приходы.ДокументИсточник,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС,
		|	ДД.НалогообложениеНДС
		|";
КонецФункции

Функция ТекстПриходыСборокТоваровПолуфабрикаты() // использует Выпуски, ПриходыСборок
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПриходыСборокТоваров"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Сборка"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.КорАналитикаУчетаНоменклатуры КАК АналитикаУчетаПродукции,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
		|	ДД.Регистратор КАК ДокументВыпуска,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	ДД.АналитикаУчетаПартий,
		|	Приходы.Количество КАК Знаменатель,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(ДД.Стоимость) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.НДСРегл) КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	Приходы.ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаПродукции,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	"""",
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПриходыСборок КАК Приходы
		|		ПО Приходы.Регистратор = ДД.Регистратор
		|		И (Приходы.ВидЗапасов = ДД.КорВидЗапасов
		|			ИЛИ ДД.КорВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
		|			И ДД.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка))
		|		И Приходы.АналитикаУчетаНоменклатуры = ДД.КорАналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Выпуски
		|		ПО Выпуски.Регистратор = ДД.ДокументПоступления
		|		И Выпуски.Номенклатура = Аналитика.Номенклатура
		|		И Выпуски.Характеристика = Аналитика.Характеристика
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
		|		ПО ВидыЗапасов.Ссылка = ДД.ВидЗапасов
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.СборкаТоваров)
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ЕСТЬNULL(ВидыЗапасов.ТипЗапасов, ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)) В (&СобственныеТипыЗапасов)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ДД.Регистратор,
		|	ДД.АналитикаУчетаПартий,
		|	Приходы.Количество,
		|	Приходы.ДокументИсточник,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС,
		|	ДД.НалогообложениеНДС
		|";
КонецФункции

Функция ТекстРасходыПартийЗатратПрошлыхМесяцев() // использует ПрошлыеРеализации
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстРасходыПартийЗатратПрошлыхМесяцев"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	""Прошлое"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаПродукции,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов,
		|	ДД.ДокументВыпуска,
		|	ДД.ДокументПоступления,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.Знаменатель,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(ДД.Стоимость) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.НДСРегл) КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.ДокументИсточник,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаПродукции,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	"""",
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииЗатратНаВыпуск КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрошлыеРеализации КАК Прошлые
		|		ПО ДД.Регистратор = Прошлые.ДокументРеализации
		|		И ДД.АналитикаУчетаПродукции = Прошлые.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|СГРУППИРОВАТЬ ПО
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаПродукции,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов,
		|	ДД.ДокументВыпуска,
		|	ДД.ДокументПоступления,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.Знаменатель,
		|	ДД.ДокументИсточник,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.НалогообложениеНДС
		|";
КонецФункции

Функция ТекстИтерацияПартийЗатрат()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстИтерацияПартийЗатрат"" КАК ЗапросИсточник,
		|	(ВЫБОР
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА 90
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество > 0 ТОГДА 10
		|		ИНАЧЕ 40 КОНЕЦ) КАК Приоритет,
		|	(ВЫБОР
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество > 0 ТОГДА ""Потребление""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество < 0 ТОГДА ""Сторно""
		|		КОГДА ДД.Регистратор = ДД.ДокументПоступления ТОГДА ""Замещение""
		|		ИНАЧЕ ""Перемещение"" КОНЕЦ) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры КАК АналитикаУчетаПродукции,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
		|	ДД.ДокументПоступления КАК ДокументВыпуска,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	СУММА(ДД.Количество) КАК Знаменатель,
		|	0 КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	(ВЫБОР
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА ЛОЖЬ
		|		КОГДА Передачи.Ссылка ЕСТЬ NULL И Возвраты.Ссылка ЕСТЬ NULL ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА КОНЕЦ) КАК СохранятьРегл,
		|	ДД.ДокументИсточник,
		|	ДД.КорАналитикаУчетаНоменклатуры КАК КорАналитикаУчетаПродукции,
		|	(ВЫБОР
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА ДД.ВидЗапасов
		|		ИНАЧЕ ДД.КорВидЗапасов КОНЕЦ) КАК КорВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	ДД.ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	ДД.СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов,
		|	"""",
		|	ДД.АналитикаАктивовПассивов,
		|	ЕСТЬNULL(ПодразделенияРегистраторов.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПодразделенияРегистраторов КАК ПодразделенияРегистраторов
		|		ПО ПодразделенияРегистраторов.Регистратор = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровМеждуОрганизациями КАК Передачи
		|		ПО Передачи.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И Передачи.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровМеждуОрганизациями КАК Возвраты
		|		ПО Возвраты.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И Возвраты.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента КАК ВозвратыОтКлиента
		|		ПО ВозвратыОтКлиента.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И ВозвратыОтКлиента.Ссылка = ДД.Регистратор
		|		И ВозвратыОтКлиента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера)
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.СборкаТоваров КАК Сборка
		|		ПО Сборка.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И Сборка.Ссылка = ДД.Регистратор
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ДокументИсточник <> НЕОПРЕДЕЛЕНО
		|	И ВозвратыОтКлиента.Ссылка ЕСТЬ NULL
		|	И НЕ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И НЕ Сборка.Ссылка ЕСТЬ NULL)
		|	И НЕ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И НЕ Сборка.Ссылка ЕСТЬ NULL
		|		И ДД.АналитикаУчетаНоменклатуры = ДД.КорАналитикаУчетаНоменклатуры)
		|	И ДД.ДокументПоступления <> НЕОПРЕДЕЛЕНО
		|	И НЕ ДД.ДокументПоступления ССЫЛКА Документ.ВводОстатков
		|	И НЕ ДД.ДокументПоступления ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|	И НЕ ДД.ДокументПоступления ССЫЛКА Документ.ОприходованиеИзлишковТоваров
		|	И НЕ ДД.ДокументПоступления ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
		|	И НЕ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И ЕСТЬNULL(Передачи.ХозяйственнаяОперация, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссиюВДругуюОрганизацию))
		|СГРУППИРОВАТЬ ПО
		|	(ВЫБОР
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА 90
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество > 0 ТОГДА 10
		|		ИНАЧЕ 40 КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество > 0 ТОГДА ""Потребление""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество < 0 ТОГДА ""Сторно""
		|		КОГДА ДД.Регистратор = ДД.ДокументПоступления ТОГДА ""Замещение""
		|		ИНАЧЕ ""Перемещение"" КОНЕЦ),
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	(ВЫБОР
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА ЛОЖЬ
		|		КОГДА Передачи.Ссылка ЕСТЬ NULL И Возвраты.Ссылка ЕСТЬ NULL ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА КОНЕЦ),
		|	ДД.ДокументИсточник,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	(ВЫБОР
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА ДД.ВидЗапасов
		|		ИНАЧЕ ДД.КорВидЗапасов КОНЕЦ),
		|	ДД.ХозяйственнаяОперация,
		|	ДД.НалогообложениеНДС,
		|	ДД.СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	ЕСТЬNULL(ПодразделенияРегистраторов.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|";
КонецФункции

Функция ТекстИтерацияПартийЗатратКомиссия()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстИтерацияПартийЗатратКомиссия"" КАК ЗапросИсточник,
		|	(ВЫБОР
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА 90
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество > 0 ТОГДА 10
		|		ИНАЧЕ 40 КОНЕЦ) КАК Приоритет,
		|	(ВЫБОР
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			И НЕ РеализацияТоварыВПути.Ссылка ЕСТЬ NULL ТОГДА ""ПотреблениеВПути""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество > 0 ТОГДА ""Потребление""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество < 0 ТОГДА ""Сторно""
		|		КОГДА ДД.Регистратор = ДД.ДокументПоступления ТОГДА ""Замещение""
		|		ИНАЧЕ ""Передача"" КОНЕЦ) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры КАК АналитикаУчетаПродукции,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
		|	ДД.ДокументПоступления КАК ДокументВыпуска,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	СУММА(ДД.Количество) КАК Знаменатель,
		|	0 КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	(ВЫБОР
		|		КОГДА Реализация.Дата >= &НачалоПериода ТОГДА ДД.ДокументПередачиНаКомиссию
		|		КОГДА Передача.Дата >= &НачалоПериода ТОГДА ДД.ДокументПередачиНаКомиссию
		|		ИНАЧЕ ДД.ДокументПоступления КОНЕЦ) КАК ДокументИсточник,
		|	ДД.КорАналитикаУчетаНоменклатуры КАК КорАналитикаУчетаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	ДД.ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	"""",
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровПереданныеНаКомиссию КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоварыВПути
		|		ПО РеализацияТоварыВПути.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И РеализацияТоварыВПути.Ссылка = ДД.Регистратор
		|		И РеализацияТоварыВПути.ХозяйственнаяОперация В (
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности))
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК Реализация
		|		ПО Реализация.Ссылка = ДД.ДокументПередачиНаКомиссию
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровМеждуОрганизациями КАК Передача
		|		ПО Передача.Ссылка = ДД.ДокументПередачиНаКомиссию
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И (ДД.Регистратор <> ДД.ДокументПоступления ИЛИ &Итерация)
		|СГРУППИРОВАТЬ ПО
		|	(ВЫБОР
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА 90
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество > 0 ТОГДА 10
		|		ИНАЧЕ 40 КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			И НЕ РеализацияТоварыВПути.Ссылка ЕСТЬ NULL ТОГДА ""ПотреблениеВПути""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество > 0 ТОГДА ""Потребление""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество < 0 ТОГДА ""Сторно""
		|		КОГДА ДД.Регистратор = ДД.ДокументПоступления ТОГДА ""Замещение""
		|		ИНАЧЕ ""Передача"" КОНЕЦ),
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	(ВЫБОР
		|		КОГДА Реализация.Дата >= &НачалоПериода ТОГДА ДД.ДокументПередачиНаКомиссию
		|		КОГДА Передача.Дата >= &НачалоПериода ТОГДА ДД.ДокументПередачиНаКомиссию
		|		ИНАЧЕ ДД.ДокументПоступления КОНЕЦ),
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.НалогообложениеНДС
		|";
КонецФункции

Функция ТекстИтерацияПартийЗатратКомиссияВозврат()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстИтерацияПартийЗатратКомиссияВозврат"" КАК ЗапросИсточник,
		|	40 КАК Приоритет,
		|	""Сторно"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.КорАналитикаУчетаНоменклатуры КАК АналитикаУчетаПродукции,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
		|	ДД.ДокументПоступления КАК ДокументВыпуска,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	СУММА(-ДД.Количество) КАК Знаменатель,
		|	0 КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.Регистратор КАК ДокументИсточник,
		|	ДД.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	ДД.ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	"""",
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровПереданныеНаКомиссию КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента КАК ВозвратыОтКлиента
		|		ПО ВозвратыОтКлиента.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И ВозвратыОтКлиента.Ссылка = ДД.Регистратор
		|		И ВозвратыОтКлиента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера)
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И (ДД.Регистратор <> ДД.ДокументПоступления ИЛИ &Итерация)
		|СГРУППИРОВАТЬ ПО
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ДокументПередачиНаКомиссию,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.НалогообложениеНДС
		|";
КонецФункции

Функция ТекстДополнениеПартийЗатрат()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстДополнениеПартийЗатрат"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""ВПути"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаПродукции,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов,
		|	ДД.ДокументВыпуска,
		|	ДД.ДокументПоступления,
		|	ДД.АналитикаУчетаПартий,
		|	СУММА(ДД.Знаменатель) КАК Знаменатель,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(ДД.Стоимость) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.НДСРегл) КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ДД.КорАналитикаУчетаПродукции,
		|	ДД.КорВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	"""",
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК Отгрузки
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииЗатратНаВыпуск КАК ДД
		|		ПО ДД.Регистратор = Отгрузки.Ссылка
		|ГДЕ
		|	Отгрузки.ХозяйственнаяОперация В (
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности))
		|	И (Отгрузки.ДатаПереходаПраваСобственности МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		ИЛИ Отгрузки.ДатаПереходаПраваСобственности = ДАТАВРЕМЯ(1,1,1))
		|	И ДД.Период < &НачалоПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|СГРУППИРОВАТЬ ПО
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаПродукции,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов,
		|	ДД.ДокументВыпуска,
		|	ДД.ДокументПоступления,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.КорАналитикаУчетаПродукции,
		|	ДД.КорВидЗапасов,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС,
		|	ДД.НалогообложениеНДС
		|";
КонецФункции



#КонецОбласти


#Область РасчетПартийПрочих

Функция ДанныеДляПартийПрочих(НачалоПериода, ОкончаниеПериода, МассивОрганизаций)
	МВТ = Новый МенеджерВременныхТаблиц;
	// Распределяем только по собственным запасам, исключаем комиссионные и давальческие запасы.
	СобственныеТипыЗапасов = РасчетСебестоимостиПрикладныеАлгоритмы.СобственныеТипыЗапасов();
	
	// Определяем базу распределения прочих расходов на заказы поставщикам и заказы перемещений,
	// т.е. распределяем выполненные строки заказов на поступления партий - указываем заказ в партии.
	ТекстСортировкаЗаказов = "
		|УПОРЯДОЧИТЬ ПО
		|	Регистратор, Номенклатура, Характеристика, Склад, Назначение, Приоритет
		|";
	ТекстРасчетаЗаказов = 
		ТекстТаможенныеДекларацииИПоступления() + ";" + // вт Декларации
		ТекстАналитикиРасходовВсе() + ";" + // вт АналитикиРасходовВсе
		ТекстЗаказыИзАналитикРасходов() + ";" + // вт ЗаказыИзАналитикРасходов, использует АналитикиРасходовВсе
		ТекстЗаказыИзАналитикРасходовСводно() + ";" + // вт ЗаказыИзАналитикРасходовСводно, использует ЗаказыИзАналитикРасходов 
		ТекстПоступившиеЗаказы() + // использует ЗаказыИзАналитикРасходов
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстОстаткиПоступленийПоЗаказам() + // использует ЗаказыИзАналитикРасходов
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстПриходыПоступленийПоЗаказам() + // использует ЗаказыИзАналитикРасходов
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстПриходыРаботПоЗаказам() + // использует ЗаказыИзАналитикРасходов
		ТекстСортировкаЗаказов;
	ЗапросРасчетаЗаказов = Новый Запрос(ТекстРасчетаЗаказов);
	ЗапросРасчетаЗаказов.МенеджерВременныхТаблиц = МВТ;
	ЗапросРасчетаЗаказов.УстановитьПараметр("НачалоПериода", НачалоПериода);
	ЗапросРасчетаЗаказов.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	ЗапросРасчетаЗаказов.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);	
	ЗапросРасчетаЗаказов.УстановитьПараметр("СобственныеТипыЗапасов", СобственныеТипыЗапасов);
	ЗапросРасчетаЗаказов.УстановитьПараметр("УчитыватьНазначение",
		РасчетСебестоимостиПовтИсп.СебестоимостьТоваровПоНазначениям(НачалоПериода));
	
	ДанныеРасчетаЗаказов = ЗапросРасчетаЗаказов.Выполнить().Выгрузить();
	РасчетныеПартииПоЗаказам = Новый ТаблицаЗначений;
	ПоляРасчетаЗаказов = СкопироватьКолонки(ДанныеРасчетаЗаказов.Колонки, РасчетныеПартииПоЗаказам.Колонки);
	КонтекстДвижений = ОписаниеДвижений(
		"ЗаказыПоставщикам", "",
		ПоляРасчетаЗаказов,
		"Номенклатура, Регистратор, Характеристика, Склад", "Количество, Стоимость",
		"Количество", "Количество", "Заказ", "Период");
	РаспределитьПриходыНаРасходы(КонтекстДвижений, ДанныеРасчетаЗаказов, РасчетныеПартииПоЗаказам);
	
	// Получаем данные для расчета списания партий прочих расходов и прихода партий расходов на с/с товаров.
	ТекстСортировка = "
		|УПОРЯДОЧИТЬ ПО
		|	Организация, Приоритет, ВидДвижения, Период, Регистратор, АналитикаУчетаНоменклатуры, ВидЗапасов, ДокументПоступления";
	ИсходныйТекстЗапроса =
		ТекстПартииПоЗаказам() + ";" + // вт ПартииПоЗаказам, использует &РасчетныеПартииПоЗаказам
		ТекстПрошлыеПеремещения() + ";" + // вт ПрошлыеПеремещения
		ТекстРегистраторыПрочих() + ";" + // вт РегистраторыПрочих
		ТекстОстаткиПартийСборок() + ";" + // вт ОстаткиЗатратПоСборкам, ОстаткиПартийСборок
		ТекстОстаткиСебестоимости() + ";" + // вт ОстаткиСебестоимости
		ТекстПриходыПартийСборок() + ";" + // вт ПриходыПартийСборок
		ТекстОписаниеПартийПрочих() +
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстОстаткиПартийПрочих() + // использует АналитикиРасходовВсе
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстПриходыПартийПрочих() +
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстДополненияПартийПрочих() + // использует РегистраторыПрочих
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисОстатки() + // использует АналитикиРасходовВсе
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисОстаткиСборок() + // использует ОстаткиПартийСборок, АналитикиРасходовВсе
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисПриходы() + // использует АналитикиРасходовВсе
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисКорректировкиПоступлений() + // использует АналитикиРасходовВсе
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисПриходыСборок() + // использует ПриходыПартийСборок, АналитикиРасходовВсе
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисРаботы() + // использует АналитикиРасходовВсе
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисПеремещения() + // использует АналитикиРасходовВсе
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисПеремещенияСборок() + // использует АналитикиРасходовВсе
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисПрошлыеПеремещения() + // использует ПрошлыеПеремещения
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисЗаказы() + // использует ПартииПоЗаказам, ЗаказыИзАналитикРасходов
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисУслуги() + // использует АналитикиРасходовВсе
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисПрочиеАктивыПассивы() + // использует АналитикиРасходовВсе
		ТекстСортировка;
	ИсходныйТекстЗапроса = СтрЗаменить(ИсходныйТекстЗапроса, "&Вес", 
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки("СпрНоменклатура.ЕдиницаИзмерения", "СпрНоменклатура"));
	ИсходныйТекстЗапроса = СтрЗаменить(ИсходныйТекстЗапроса, "&Объем", 
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаОбъемУпаковки("СпрНоменклатура.ЕдиницаИзмерения", "СпрНоменклатура"));
	ИсходныйЗапрос = Новый Запрос(ИсходныйТекстЗапроса);
	ИсходныйЗапрос.МенеджерВременныхТаблиц = МВТ;
	ИсходныйЗапрос.УстановитьПараметр("РасчетныеПартииПоЗаказам", РасчетныеПартииПоЗаказам);
	ИсходныйЗапрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	ИсходныйЗапрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	ИсходныйЗапрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);	
	ИсходныйЗапрос.УстановитьПараметр("СобственныеТипыЗапасов", СобственныеТипыЗапасов);
	ИсходныйЗапрос.УстановитьПараметр("УчитыватьНазначение", 
		РасчетСебестоимостиПовтИсп.СебестоимостьТоваровПоНазначениям(НачалоПериода));
	ИсходныйЗапрос.УстановитьПараметр("ОрганизацииСФИФОСкользящая",
		НастройкиНалоговУчетныхПолитик.ОрганизацииСЗаданнымПараметромПолитики(
			"УчетнаяПолитикаФинансовогоУчета",
			"МетодОценкиСтоимостиТоваров",
			Перечисления.МетодыОценкиСтоимостиТоваров.ФИФОСкользящаяОценка,
			МассивОрганизаций,
			НачалоПериода));
	
	ДанныеДляРасчета = ИсходныйЗапрос.Выполнить().Выгрузить();
	
	Возврат ДанныеДляРасчета;
	
КонецФункции

Функция РасчетныеПартииПрочих(ДанныеДляРасчета, Регистраторы, УзлыДвижений = Неопределено, Тест = Ложь, КоличествоЗаписей = 0) // ДанныеДляРасчета будут изменены
	
	// Шаг 0: Свертка исходных данных
	ПоляСуммирования = "Базис, Количество, Вес, Объем, Стоимость, СтоимостьБезНДС, СтоимостьРегл, НДСРегл, ПостояннаяРазница, ВременнаяРазница";
	Исключения = "ЗапросИсточник";
	ПоляГруппировки = ПереченьПолей(ДанныеДляРасчета.Колонки, ПоляСуммирования + ", " + Исключения);
	ДанныеДляРасчета.Свернуть(ПоляГруппировки, ПоляСуммирования);
	
	// Движения, которые надо сохранить в расчетных партиях, но не нужно учитывать в расчетах.
	Дополнения = Новый Массив;
	// кэшируем получение пустых ссылок - на множественных итерациях есть выигрыш
	ПустыеСсылки = Новый Соответствие;
	ПропорциональноКоличеству = Перечисления.ТипыБазыРаспределенияРасходов.КоличествоТоваров;
	ПропорциональноСебестоимости = Перечисления.ТипыБазыРаспределенияРасходов.СебестоимостьТоваров;
	ПропорциональноВесу = Перечисления.ТипыБазыРаспределенияРасходов.ВесТоваров;
	ПропорциональноОбъему = Перечисления.ТипыБазыРаспределенияРасходов.ОбъемТоваров;
	// Шаг 1: Рассчитываем базу распределения для каждого прихода прочих расходов с учетом налогообложения партии распределения.
	УзлыДвижений = Новый Соответствие; // [Организация, [АналитикаРасходов, [ИндексСтроки, [НалогообложениеНДС, Базис]]]]
	Ключи = Новый Массив(4); // {Организация, АналитикаРасходов, ИндексСтроки, НалогообложениеНДС}
	УдаляемыеСтроки = новый Массив;
	
	// расчет базы по для каждой строки прихода по налогообложению каждого расхода
	Для Каждого Движение Из ДанныеДляРасчета Цикл
		Ключи[0] = Движение.Организация;
		Ключи[1] = Неопределено;
		Ключи[2] = ДанныеДляРасчета.Индекс(Движение);
		Ключи[3] = Движение.НалогообложениеНДС;
		// фиксируем дополнение и уходим на следующую запись
		Если Движение.ТипЗаписи = "Дополнение" Тогда
			Дополнения.Добавить(Ключи[2]);
			Продолжить;
		// накапливаем приход партий прочих расходов для распределения по налогообложению
		ИначеЕсли ЗначениеЗаполнено(Движение.СтатьяРасходов) Тогда
			Ключи[1] = Движение.АналитикаРаспределения;
			ДобавитьУзелИндекса(УзлыДвижений, Ключи, Неопределено);
			Продолжить;
		КонецЕсли;
		
		Количество = Движение.Количество;
		Стоимость  = Движение.Стоимость;
		СтоимостьРегл  = Движение.СтоимостьРегл;
		Вес = Движение.Вес;
		Объем = Движение.Объем;
		
		// Исключаем строки с отрицательными показателями базиса распределения.
		Если Количество < 0
		 ИЛИ Стоимость < 0
		 ИЛИ СтоимостьРегл < 0
		 ИЛИ Вес < 0
		 ИЛИ Объем < 0 Тогда
			УдаляемыеСтроки.Добавить(Движение);
			Продолжить;
		КонецЕсли;
		
		// непосредственно расчет базисов по налогообложению
		Аналитики = УзелИндекса(УзлыДвижений, Ключи);
		Если Аналитики = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Для Каждого Значение Из АналитикиОбсчетаПартийПрочих(Движение, ПустыеСсылки) Цикл
			ИндексыСтрок = Аналитики[Значение];
			Если ИндексыСтрок = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Для Каждого ИндексСтроки Из ИндексыСтрок Цикл
				Приход = ДанныеДляРасчета[ИндексСтроки.Ключ];
				Если Приход.ПравилоРаспределения = ПропорциональноКоличеству Тогда
					Базис = Количество;
				ИначеЕсли Приход.ПравилоРаспределения = ПропорциональноВесу Тогда
					Базис = Вес;
				ИначеЕсли Приход.ПравилоРаспределения = ПропорциональноОбъему Тогда
					Базис = Объем;	
				ИначеЕсли Приход.ПравилоРаспределения = ПропорциональноСебестоимости Тогда
					Базис = Стоимость;
				Иначе
					Продолжить;
				КонецЕсли;
				Приход.Базис = Приход.Базис + Базис;
				// увеличим базу по налогообложению (Ключи[3])
				Налоги = ИндексСтроки.Значение;
				ОписаниеБазы = ?(Налоги[Ключи[3]] = Неопределено, Новый Структура("Базис", 0), Налоги[Ключи[3]]);
				ОписаниеБазы.Базис = ОписаниеБазы.Базис + Базис;
				Налоги.Вставить(Ключи[3], ОписаниеБазы);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	// Шаг 2: создаем буфер накопления рассчитанных партий
	РасчетныеПартии = Новый ТаблицаЗначений;
	СкопироватьКолонки(ДанныеДляРасчета.Колонки, РасчетныеПартии.Колонки);
	// Шаг 3: Копируем дополнительные движения в расчетные партии
	Для Каждого Дополнение Из Дополнения Цикл
		Движение = ДанныеДляРасчета[Дополнение];
		РасчетнаяПартия = РасчетныеПартии.Добавить();
		ЗаполнитьРасчетнуюПартию("ПартииПрочихРасходов", РасчетнаяПартия, Движение, Неопределено);
	КонецЦикла;
	// Шаг 4: Создаем расчетные партии расходов по каждому накопленному налогообложению
	Для Каждого Организация Из УзлыДвижений Цикл
		Для Каждого Аналитика Из Организация.Значение Цикл
			Для Каждого ИндексСтроки Из Аналитика.Значение Цикл
				Движение = ДанныеДляРасчета[ИндексСтроки.Ключ];
				Если Движение.Базис = 0 Тогда
					Продолжить;
				КонецЕсли;
				Для Каждого РаспределениеБазиса Из ИндексСтроки.Значение Цикл
					Если РаспределениеБазиса.Значение = Неопределено Тогда
						Продолжить;
					КонецЕсли;
					РасчетнаяПартия = РасчетныеПартии.Добавить();
					ЗаполнитьРасчетнуюПартию("ПартииПрочихРасходов", РасчетнаяПартия, Движение, РаспределениеБазиса);
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	// Удаляем строки с отрицательным базисом.
	Для Каждого Строка Из УдаляемыеСтроки Цикл
		ДанныеДляРасчета.Удалить(Строка);
	КонецЦикла;
	УдаляемыеСтроки.Очистить();
	
	// Шаг 4: Удаляем измененные записи ДанныеДляРасчета (партии, остатки, дополнения) - они теперь лишние.
	МинимальныйИндекс = 1 - ДанныеДляРасчета.Количество();
	Для Индекс = МинимальныйИндекс По 0 Цикл
		Движение = ДанныеДляРасчета[-Индекс];
		Если Движение.ТипЗаписи = "Партия" Или Движение.ТипЗаписи = "Остаток" Или Движение.ТипЗаписи = "Дополнение" Тогда
			ДанныеДляРасчета.Удалить(-Индекс);
		КонецЕсли;
	КонецЦикла;
	// Шаг 5: Копируем рассчитанные партии с указанием налогообложения в ДанныеДляРасчета
	// эти движения понадобятся для формирования прихода в партии расходов на себестоимость.
	Для Каждого РасчетнаяПартия Из РасчетныеПартии Цикл
		Если РасчетнаяПартия.ТипЗаписи = "Потребление" Тогда
			Движение = ДанныеДляРасчета.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, РасчетнаяПартия,,"ХозяйственнаяОперация");
			Движение.ТипЗаписи = "Остаток";
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		КонецЕсли;
	КонецЦикла;
	// Шаг 6: Сортировка партий для дальнейшей записи
	РасчетныеПартии.Сортировать("Регистратор, ВидДвижения, Период", Новый СравнениеЗначений);
	КоличествоЗаписей = РасчетныеПартии.Количество();
	Возврат РасчетныеПартии;
КонецФункции

Функция ТекстТаможенныеДекларацииИПоступления() // вт Декларации, КоличествоДеклараций
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ДокументПоступления КАК Ссылка,
		|	Партии.ДокументПоступления КАК Поступление
		|ПОМЕСТИТЬ
		|	Декларации
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровОрганизаций КАК Партии
		|		ПО Партии.Регистратор = ДД.ДокументПоступления
		|		И Партии.Организация = ДД.Организация
		|		И Партии.АналитикаУчетаНоменклатуры.Номенклатура = Аналитика.Номенклатура
		|		И Партии.АналитикаУчетаНоменклатуры.Характеристика = Аналитика.Характеристика
		|ГДЕ
		|	ДД.ДокументПоступления ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
		|	И Партии.Период < &НачалоПериода
		|	И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|ИНДЕКСИРОВАТЬ ПО
		|	Поступление
		|;
		|ВЫБРАТЬ
		|	ДД.Поступление,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.Ссылка) КАК Количество
		|ПОМЕСТИТЬ
		|	КоличествоДеклараций
		|ИЗ
		|	Декларации КАК ДД
		|СГРУППИРОВАТЬ ПО
		|	ДД.Поступление
		|ИНДЕКСИРОВАТЬ ПО
		|	Поступление
		|";
КонецФункции

Функция ТекстАналитикиРасходовВсе() // вт АналитикиРасходовВсе, использует Декларации
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Ключи.Организация,
		|	Ключи.СтатьяРасходов,
		|	Ключи.АналитикаРасходов,
		|	Ключи.НаправлениеДеятельности
		|ПОМЕСТИТЬ
		|	АналитикиРасходовВсе
		|ИЗ (
		|	ВЫБРАТЬ
		|		ДД.Организация,
		|		ДД.СтатьяРасходов,
		|		ЕСТЬNULL(Декларации.Ссылка, ДД.АналитикаРасходов) КАК АналитикаРасходов,
		|		ДД.НаправлениеДеятельности
		|	ИЗ
		|		РегистрНакопления.ПартииПрочихРасходов.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
		|			ПО Статьи.Ссылка = ДД.СтатьяРасходов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Декларации КАК Декларации
		|			ПО Декларации.Поступление = ДД.АналитикаРасходов
		|	ГДЕ
		|		Статьи.ВариантРаспределенияРасходовУпр =
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|		ИЛИ Статьи.ВариантРаспределенияРасходовРегл =
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ
		|		ДД.Организация,
		|		ДД.СтатьяРасходов,
		|		(ВЫБОР
		|			КОГДА ДД.Регистратор ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
		|			 И ДД.АналитикаРасходов = ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка)
		|				ТОГДА ЗНАЧЕНИЕ(Документ.ТаможеннаяДекларацияИмпорт.ПустаяСсылка)
		|			ИНАЧЕ ЕСТЬNULL(Декларации.Ссылка, ДД.АналитикаРасходов) КОНЕЦ) КАК АналитикаРасходов,
		|		ДД.НаправлениеДеятельности
		|	ИЗ
		|		РегистрНакопления.ПартииПрочихРасходов КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
		|			ПО Статьи.Ссылка = ДД.СтатьяРасходов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Декларации КАК Декларации
		|			ПО Декларации.Поступление = ДД.АналитикаРасходов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения КАК Корректировка
		|			ПО Корректировка.Ссылка = ДД.Регистратор
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученныйНалоговыйАгент КАК СчетФактура
		|			ПО СчетФактура.Ссылка = ДД.Регистратор
		|	ГДЕ
		|		ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И ДД.Организация В (&МассивОрганизаций)
		|		И ДД.Активность
		|		И ДД.ДокументПоступленияРасходов В (
		|			ДД.Регистратор,
		|			СчетФактура.СчетФактураОснование,
		|			Корректировка.ДокументОснование)
		|		И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И (Статьи.ВариантРаспределенияРасходовУпр =
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|			ИЛИ Статьи.ВариантРаспределенияРасходовРегл =
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров))
		|) КАК Ключи
		|";
КонецФункции

Функция ТекстПрошлыеПеремещения() // вт ПрошлыеПеремещения
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Организация КАК Организация,
		|	ДД.АналитикаРасходов КАК Перемещение,
		|	Партии.Период,
		|	Партии.ДокументПоступления КАК ДокументПоступления,
		|	Партии.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
		|ПОМЕСТИТЬ
		|	ПрошлыеПеремещения
		|ИЗ
		|	РегистрНакопления.ПартииПрочихРасходов КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров КАК Перемещение
		|		ПО Перемещение.Ссылка = ДД.АналитикаРасходов
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровОрганизаций КАК Партии
		|		ПО Партии.Период = Перемещение.Дата
		|		И Партии.Регистратор = Перемещение.Ссылка
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И Перемещение.Дата < &НачалоПериода
		|	И Партии.ДокументПоступления <> ДД.Регистратор
		|	И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	ДокументПоступления,
		|	АналитикаУчетаНоменклатуры
		|";
КонецФункции

Функция ТекстРегистраторыПрочих() // вт РегистраторыПрочих
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Ключи.Регистратор
		|ПОМЕСТИТЬ
		|	РегистраторыПрочих
		|ИЗ (
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ДД.ДокументПоступленияРасходов КАК Регистратор
		|	ИЗ
		|		РегистрНакопления.ПартииПрочихРасходов.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
		|			ПО Статьи.Ссылка = ДД.СтатьяРасходов
		|	ГДЕ
		|		ДД.СтоимостьОстаток > 0
		|		И (Статьи.ВариантРаспределенияРасходовУпр =
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|			ИЛИ Статьи.ВариантРаспределенияРасходовРегл =
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров))
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ
		|		ДД.Регистратор КАК Регистратор
		|	ИЗ
		|		РегистрНакопления.ПартииПрочихРасходов КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
		|			ПО Статьи.Ссылка = ДД.СтатьяРасходов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученныйНалоговыйАгент КАК СчетФактура
		|			ПО СчетФактура.Ссылка = ДД.Регистратор
		|	ГДЕ
		|		ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И ДД.Организация В (&МассивОрганизаций)
		|		И ДД.Активность
		|		И ДД.ДокументПоступленияРасходов В (
		|			ДД.Регистратор,
		|			СчетФактура.СчетФактураОснование)
		|		И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И (Статьи.ВариантРаспределенияРасходовУпр =
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|			ИЛИ Статьи.ВариантРаспределенияРасходовРегл =
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров))
		|) КАК Ключи
		|";
КонецФункции
	
Функция ТекстОстаткиПартийСборок() // вт ОстаткиЗатратПоСборкам, ОстаткиПартийСборок
	
	Возврат "
		|ВЫБРАТЬ
		|	ДД.ДокументВыпуска
		|ПОМЕСТИТЬ
		|	ОстаткиЗатратПоСборкам
		|ИЗ
		|	РегистрНакопления.ПартииЗатратНаВыпуск.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументВыпуска
		|;
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	СпрАналитикиУчетаПартий.НалогообложениеНДС КАК НалогообложениеНДС,
		|	ДД.КоличествоОстаток КАК Количество
		|ПОМЕСТИТЬ
		|	ОстаткиПартийСборок
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)
		|		И ДокументПоступления В (
		|			ВЫБРАТЬ
		|				Затраты.ДокументВыпуска
		|			ИЗ
		|				ОстаткиЗатратПоСборкам КАК Затраты
		|		)
		|		И ВидЗапасов.ТипЗапасов В (&СобственныеТипыЗапасов)
		|) КАК ДД
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК СпрАналитикиУчетаПартий
		|	ПО СпрАналитикиУчетаПартий.Ссылка = ДД.АналитикаУчетаПартий
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	АналитикаУчетаНоменклатуры,
		|	ДокументПоступления
		|";
КонецФункции

Функция ТекстПриходыПартийСборок() // вт ПриходыПартийСборок
	
	Возврат "
		|ВЫБРАТЬ
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	СпрАналитикиУчетаПартий.НалогообложениеНДС КАК НалогообложениеНДС,
		|	СУММА(ДД.Количество) КАК Количество
		|ПОМЕСТИТЬ
		|	ПриходыПартийСборок
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК СпрАналитикиУчетаПартий
		|	ПО СпрАналитикиУчетаПартий.Ссылка = ДД.АналитикаУчетаПартий
		|
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ДД.Активность
		|	И (ДД.ДокументПоступления ССЫЛКА Документ.СборкаТоваров
		|		ИЛИ ДД.ДокументПоступления ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями)
		|	И (ДД.Регистратор ССЫЛКА Документ.СборкаТоваров
		|		ИЛИ ДД.Регистратор ССЫЛКА Документ.ПеремещениеТоваров
		|		ИЛИ ДД.Регистратор ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	СпрАналитикиУчетаПартий.НалогообложениеНДС,
		|	ДД.ВидЗапасов
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	АналитикаУчетаНоменклатуры,
		|	ДокументПоступления
		|";
КонецФункции

Функция ТекстОстаткиСебестоимости() // вт ОстаткиСебестоимости
	Возврат 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Организация КАК Организация,
	|	Аналитика.Номенклатура КАК Номенклатура,
	|	Аналитика.Характеристика КАК Характеристика,
	|	Аналитика.Серия КАК Серия,
	|	Аналитика.МестоХранения КАК Склад,
	|	ВЫБОР
	|		КОГДА &УчитыватьНазначение
	|			ТОГДА Аналитика.Назначение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Назначение
	|ПОМЕСТИТЬ ОстаткиСебестоимости
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО (Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры)
	|ГДЕ
	|	ДД.КоличествоОстаток > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДД.Организация,
	|	Аналитика.Номенклатура,
	|	Аналитика.Характеристика,
	|	Аналитика.Серия,
	|	Аналитика.МестоХранения,
	|	Назначение";
КонецФункции

Функция ТекстЗаказыИзАналитикРасходов() // вт ЗаказыИзАналитикРасходов, использует АналитикиРасходовВсе
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ДатаПоступления,
		|	ДД.Поступление,
		|	ДД.Заказ,
		|	ДД.Организация,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.Склад,
		|	(ВЫБОР
		|		КОГДА &УчитыватьНазначение ТОГДА ДД.Назначение
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КОНЕЦ) КАК Назначение,
		|	ДД.Количество
		|ПОМЕСТИТЬ
		|	ЗаказыИзАналитикРасходов
		|ИЗ (
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ДД.Период КАК ДатаПоступления,
		|		ДД.Регистратор КАК Поступление,
		|		ДД.ЗаказПоставщику КАК Заказ,
		|		КлючиРасходов.Организация,
		|		ДД.Номенклатура,
		|		ДД.Характеристика,
		|		ТоварыДокумента.Назначение,
		|		(ВЫБОР
		|			КОГДА Заказ.ХозяйственнаяОперация В (
		|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути),
		|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути),
		|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути))
		|				ТОГДА Заказ.Партнер
		|			ИНАЧЕ ДД.Склад КОНЕЦ) КАК Склад,
		|		ДД.КОформлению КАК Количество
		|	ИЗ
		|		РегистрНакопления.ЗаказыПоставщикам КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику КАК Заказ
		|			ПО Заказ.Ссылка = ДД.ЗаказПоставщику
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику.Товары КАК ТоварыДокумента
		|			ПО ТоварыДокумента.Ссылка = ДД.ЗаказПоставщику
		|			И ТоварыДокумента.КодСтроки = ДД.КодСтроки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиРасходовВсе КАК КлючиРасходов
		|			ПО КлючиРасходов.АналитикаРасходов = ДД.ЗаказПоставщику
		|			И КлючиРасходов.НаправлениеДеятельности = 
		|				ЕСТЬNULL(ТоварыДокумента.Назначение.НаправлениеДеятельности,
		|					ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
		|	ГДЕ
		|		ДД.Период <= &ОкончаниеПериода
		|		И ДД.Активность
		|		И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ДД.Период КАК ДатаПоступления,
		|		ДД.Регистратор КАК Поступление,
		|		ДД.ЗаказПоставщику КАК Заказ,
		|		КлючиРасходов.Организация,
		|		ДД.Номенклатура,
		|		ДД.Характеристика,
		|		ТоварыДокумента.Назначение,
		|		(ВЫБОР
		|			КОГДА Заказ.ХозяйственнаяОперация В (
		|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути),
		|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути),
		|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути))
		|				ТОГДА Заказ.Партнер
		|			ИНАЧЕ ДД.Склад КОНЕЦ) КАК Склад,
		|		ДД.КОформлению КАК Количество
		|	ИЗ
		|		РегистрНакопления.ЗаказыПоставщикам КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику КАК Заказ
		|			ПО Заказ.Ссылка = ДД.ЗаказПоставщику
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику.Товары КАК ТоварыДокумента
		|			ПО ДД.ЗаказПоставщику = ТоварыДокумента.Ссылка
		|			И ДД.КодСтроки = ТоварыДокумента.КодСтроки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиРасходовВсе КАК КлючиРасходов
		|			ПО КлючиРасходов.АналитикаРасходов = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
		|			И КлючиРасходов.Организация = Заказ.Организация
		|			И КлючиРасходов.НаправлениеДеятельности = 
		|				ЕСТЬNULL(ТоварыДокумента.Назначение.НаправлениеДеятельности,
		|					ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
		|	ГДЕ
		|		ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И ДД.Активность
		|		И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|
		// Расходы по заказам на перемещение
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ДД.Период КАК ДатаПоступления,
		|		ДД.Регистратор КАК Поступление,
		|		ДД.ЗаказНаПеремещение КАК Заказ,
		|		КлючиРасходов.Организация,
		|		ДД.Номенклатура,
		|		ДД.Характеристика,
		|		ТоварыДокумента.Назначение,
		|		Заказ.СкладПолучатель КАК Склад,
		|		ДД.КОформлению КАК Количество
		|	ИЗ
		|		РегистрНакопления.ЗаказыНаПеремещение КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПеремещение КАК Заказ
		|			ПО Заказ.Ссылка = ДД.ЗаказНаПеремещение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПеремещение.Товары КАК ТоварыДокумента
		|			ПО ДД.ЗаказНаПеремещение = ТоварыДокумента.Ссылка
		|			И ДД.КодСтроки = ТоварыДокумента.КодСтроки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиРасходовВсе КАК КлючиРасходов
		|			ПО КлючиРасходов.АналитикаРасходов = ДД.ЗаказНаПеремещение
		|			И КлючиРасходов.НаправлениеДеятельности = 
		|				ЕСТЬNULL(ТоварыДокумента.Назначение.НаправлениеДеятельности,
		|					ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
		|	ГДЕ
		|		ДД.Период <= &ОкончаниеПериода
		|		И ДД.Активность
		|		И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ДД.Период КАК ДатаПоступления,
		|		ДД.Регистратор КАК Поступление,
		|		ДД.ЗаказНаПеремещение КАК Заказ,
		|		КлючиРасходов.Организация,
		|		ДД.Номенклатура,
		|		ДД.Характеристика,
		|		ТоварыДокумента.Назначение,
		|		Заказ.СкладПолучатель КАК Склад,
		|		ДД.КОформлению КАК Количество
		|	ИЗ
		|		РегистрНакопления.ЗаказыНаПеремещение КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПеремещение КАК Заказ
		|			ПО Заказ.Ссылка = ДД.ЗаказНаПеремещение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПеремещение.Товары КАК ТоварыДокумента
		|			ПО ДД.ЗаказНаПеремещение = ТоварыДокумента.Ссылка
		|			И ДД.КодСтроки = ТоварыДокумента.КодСтроки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиРасходовВсе КАК КлючиРасходов
		|			ПО КлючиРасходов.АналитикаРасходов = ЗНАЧЕНИЕ(Документ.ЗаказНаПеремещение.ПустаяСсылка)
		|			И КлючиРасходов.Организация = Заказ.Организация
		|			И КлючиРасходов.НаправлениеДеятельности = 
		|				ЕСТЬNULL(ТоварыДокумента.Назначение.НаправлениеДеятельности,
		|					ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
		|	ГДЕ
		|		ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И ДД.Активность
		|		И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|
		// Расходы по заказам на сборку
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ДД.Период КАК ДатаПоступления,
		|		ДД.Регистратор КАК Поступление,
		|		ДД.ЗаказНаСборку КАК Заказ,
		|		КлючиРасходов.Организация,
		|		ДД.Номенклатура,
		|		ДД.Характеристика,
		|		(ВЫБОР
		|			КОГДА Заказ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
		|				ТОГДА Заказ.Назначение
		|			ИНАЧЕ ЗаказТовары.Назначение КОНЕЦ) КАК Назначение,
		|		Заказ.Склад КАК Склад,
		|		ДД.КОформлению КАК Количество
		|	ИЗ
		|		РегистрНакопления.ЗаказыНаСборку КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаСборку КАК Заказ
		|			ПО Заказ.Ссылка = ДД.ЗаказНаСборку
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаСборку.Товары КАК ЗаказТовары
		|			ПО ЗаказТовары.Ссылка = ДД.ЗаказНаСборку
		|			И ЗаказТовары.КодСтроки = ДД.КодСтроки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиРасходовВсе КАК КлючиРасходов
		|			ПО КлючиРасходов.АналитикаРасходов = ДД.ЗаказНаСборку
		|			И КлючиРасходов.НаправлениеДеятельности = 
		|			(ВЫБОР
		|				КОГДА Заказ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
		|					ТОГДА ЕСТЬNULL(Заказ.Назначение.НаправлениеДеятельности,
		|						ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
		|				ИНАЧЕ
		|					ЕСТЬNULL(ЗаказТовары.Назначение.НаправлениеДеятельности,
		|						ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
		|				КОНЕЦ)
		|	ГДЕ
		|		ДД.Период <= &ОкончаниеПериода
		|		И ДД.Активность
		|		И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		И ДД.ТипСборки = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияЗапасов.Поступление)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ДД.Период КАК ДатаПоступления,
		|		ДД.Регистратор КАК Поступление,
		|		ДД.ЗаказНаСборку КАК Заказ,
		|		КлючиРасходов.Организация,
		|		ДД.Номенклатура,
		|		ДД.Характеристика,
		|		(ВЫБОР
		|			КОГДА Заказ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
		|				ТОГДА Заказ.Назначение
		|			ИНАЧЕ ЗаказТовары.Назначение КОНЕЦ) КАК Назначение,
		|		Заказ.Склад КАК Склад,
		|		ДД.КОформлению КАК Количество
		|	ИЗ
		|		РегистрНакопления.ЗаказыНаСборку КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаСборку КАК Заказ
		|			ПО Заказ.Ссылка = ДД.ЗаказНаСборку
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаСборку.Товары КАК ЗаказТовары
		|			ПО ЗаказТовары.Ссылка = ДД.ЗаказНаСборку
		|			И ЗаказТовары.КодСтроки = ДД.КодСтроки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиРасходовВсе КАК КлючиРасходов
		|			ПО КлючиРасходов.АналитикаРасходов = ЗНАЧЕНИЕ(Документ.ЗаказНаСборку.ПустаяСсылка)
		|			И КлючиРасходов.Организация = Заказ.Организация
		|			И КлючиРасходов.НаправлениеДеятельности = 
		|			(ВЫБОР
		|				КОГДА Заказ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров)
		|					ТОГДА ЕСТЬNULL(Заказ.Назначение.НаправлениеДеятельности,
		|						ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
		|				ИНАЧЕ
		|					ЕСТЬNULL(ЗаказТовары.Назначение.НаправлениеДеятельности,
		|						ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
		|				КОНЕЦ)
		|	ГДЕ
		|		ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И ДД.Активность
		|		И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		И ДД.ТипСборки = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияЗапасов.Поступление)
		|) КАК ДД
		|";
КонецФункции

Функция ТекстЗаказыИзАналитикРасходовСводно() // вт ЗаказыИзАналитикРасходовСводно, использует ЗаказыИзАналитикРасходов
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Заказы.ДатаПоступления,
		|	Заказы.Поступление,
		|	Заказы.Номенклатура,
		|	Заказы.Характеристика,
		|	Заказы.Склад,
		|	Заказы.Назначение
		|ПОМЕСТИТЬ ЗаказыИзАналитикРасходовСводно
		|ИЗ
		|	ЗаказыИзАналитикРасходов КАК Заказы
		|ГДЕ
		|	Заказы.ДатаПоступления < &ОкончаниеПериода
		|ИНДЕКСИРОВАТЬ ПО
		|	Поступление,
		|	Номенклатура,
		|	Характеристика,
		|	Склад
		|";
КонецФункции

Функция ТекстПоступившиеЗаказы() // использует ЗаказыИзАналитикРасходов
	Возврат "
		|ВЫБРАТЬ
		|	10 КАК Приоритет,
		|	""Остаток"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДД.ДатаПоступления КАК Период,
		|	ДД.Поступление КАК Регистратор,
		|	ДД.Поступление КАК ДокументПоступления,
		|	ДД.Заказ,
		|	ДД.Организация,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.Склад,
		|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
		|	ДД.Назначение,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ДД.Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС
		|ИЗ
		|	ЗаказыИзАналитикРасходов КАК ДД
		|";
КонецФункции

Функция ТекстОстаткиПоступленийПоЗаказам() // использует ЗаказыИзАналитикРасходов
	Возврат "
		|ВЫБРАТЬ
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	Заказы.ДатаПоступления КАК Период,
		|	ДД.ДокументПоступления КАК Регистратор,
		|	ДД.ДокументПоступления КАК ДокументПоступления,
		|	НЕОПРЕДЕЛЕНО КАК Заказ,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.АналитикаУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
		|	ДД.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	ДД.АналитикаУчетаНоменклатуры.МестоХранения  КАК Склад,
		|	ДД.АналитикаУчетаНоменклатуры.Серия          КАК Серия,
		|	ДД.АналитикаУчетаНоменклатуры.Назначение     КАК Назначение,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.КоличествоОстаток КАК Количество,
		|	ДД.СтоимостьОстаток КАК Стоимость,
		|	ДД.СтоимостьБезНДСОстаток КАК СтоимостьБезНДС,
		|	ДД.СтоимостьРеглОстаток КАК СтоимостьРегл,
		|	ДД.НДСРеглОстаток КАК НДСРегл,
		|	ДД.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
		|	ДД.ВременнаяРазницаОстаток КАК ВременнаяРазница,
		|	АналитикиПартий.НалогообложениеНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикиПартий
		|		ПО АналитикиПартий.КлючАналитики = ДД.АналитикаУчетаПартий
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
		|		ПО ВидыЗапасов.Ссылка = ДД.ВидЗапасов
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗаказыИзАналитикРасходовСводно КАК Заказы
		|		ПО Заказы.Поступление = ДД.ДокументПоступления
		|		И Заказы.Номенклатура = ДД.АналитикаУчетаНоменклатуры.Номенклатура
		|		И Заказы.Характеристика = ДД.АналитикаУчетаНоменклатуры.Характеристика
		|		И Заказы.Склад = ДД.АналитикаУчетаНоменклатуры.МестоХранения
		|		И Заказы.Назначение = ДД.АналитикаУчетаНоменклатуры.Назначение
		|		И Заказы.ДатаПоступления < &НачалоПериода
		|ГДЕ
		|	ДД.КоличествоОстаток > 0
		|	И ВидыЗапасов.ТипЗапасов В (&СобственныеТипыЗапасов)
		|";
КонецФункции

Функция ТекстПриходыПоступленийПоЗаказам() // использует ЗаказыИзАналитикРасходов
	Возврат "
		|ВЫБРАТЬ
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	Заказы.ДатаПоступления КАК Период,
		|	ДД.Регистратор КАК Регистратор,
		|	ДД.ДокументПоступления КАК ДокументПоступления,
		|	НЕОПРЕДЕЛЕНО КАК Заказ,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.АналитикаУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
		|	ДД.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	ДД.АналитикаУчетаНоменклатуры.МестоХранения  КАК МестоХранения,
		|	ДД.АналитикаУчетаНоменклатуры.Серия          КАК Серия,
		|	ДД.АналитикаУчетаНоменклатуры.Назначение     КАК Назначение,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.Количество,
		|	ДД.Стоимость,
		|	ДД.СтоимостьБезНДС,
		|	ДД.СтоимостьРегл,
		|	ДД.НДСРегл,
		|	ДД.ПостояннаяРазница,
		|	ДД.ВременнаяРазница,
		|	АналитикиПартий.НалогообложениеНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикиПартий
		|		ПО АналитикиПартий.КлючАналитики = ДД.АналитикаУчетаПартий
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
		|		ПО ВидыЗапасов.Ссылка = ДД.ВидЗапасов
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗаказыИзАналитикРасходовСводно КАК Заказы
		|		ПО Заказы.Поступление = ДД.Регистратор
		|		И Заказы.Номенклатура = ДД.АналитикаУчетаНоменклатуры.Номенклатура
		|		И Заказы.Характеристика = ДД.АналитикаУчетаНоменклатуры.Характеристика
		|		И Заказы.Склад = ДД.АналитикаУчетаНоменклатуры.МестоХранения
		|		И Заказы.Назначение = ДД.АналитикаУчетаНоменклатуры.Назначение
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ВидыЗапасов.ТипЗапасов В (&СобственныеТипыЗапасов)
		|";
КонецФункции

Функция ТекстПриходыРаботПоЗаказам() // использует ЗаказыИзАналитикРасходов
	Возврат "
		|ВЫБРАТЬ
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	Заказы.ДатаПоступления КАК Период,
		|	ДД.Регистратор КАК Регистратор,
		|	ДД.ДокументПоступления КАК ДокументПоступления,
		|	НЕОПРЕДЕЛЕНО КАК Заказ,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.АналитикаУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
		|	ДД.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)     КАК Склад,
		|	ДД.АналитикаУчетаНоменклатуры.Серия          КАК Серия,
		|	ДД.АналитикаУчетаНоменклатуры.Назначение     КАК Назначение,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.Количество,
		|	ДД.Стоимость,
		|	ДД.СтоимостьБезНДС,
		|	ДД.СтоимостьРегл,
		|	ДД.НДСРегл,
		|	ДД.ПостояннаяРазница,
		|	ДД.ВременнаяРазница,
		|	АналитикиПартий.НалогообложениеНДС
		|ИЗ
		|	РегистрНакопления.ПартииПроизводственныхЗатрат КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Работы
		|		ПО Работы.Ссылка = ДД.АналитикаУчетаНоменклатуры.Номенклатура
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикиПартий
		|		ПО АналитикиПартий.КлючАналитики = ДД.АналитикаУчетаПартий
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
		|		ПО ВидыЗапасов.Ссылка = ДД.ВидЗапасов
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗаказыИзАналитикРасходовСводно КАК Заказы
		|		ПО Заказы.Поступление = ДД.Регистратор
		|		И Заказы.Номенклатура = ДД.АналитикаУчетаНоменклатуры.Номенклатура
		|		И Заказы.Характеристика = ДД.АналитикаУчетаНоменклатуры.Характеристика
		|		И Заказы.Назначение = ДД.АналитикаУчетаНоменклатуры.Назначение
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И Работы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|	И ВидыЗапасов.ТипЗапасов В (&СобственныеТипыЗапасов)
		|";
КонецФункции

Функция ТекстПартииПоЗаказам() // вт ПартииПоЗаказам, использует &РасчетныеПартииПоЗаказам
	Возврат "
		|ВЫБРАТЬ
		|	ДД.Приоритет,
		|	ДД.ТипЗаписи,
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Заказ,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.Регистратор,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.Количество,
		|	ДД.Стоимость,
		|	ДД.СтоимостьБезНДС,
		|	ДД.СтоимостьРегл,
		|	ДД.НДСРегл,
		|	ДД.ПостояннаяРазница,
		|	ДД.ВременнаяРазница,
		|	ДД.НалогообложениеНДС
		|ПОМЕСТИТЬ
		|	ПартииПоЗаказам
		|ИЗ
		|	&РасчетныеПартииПоЗаказам КАК ДД
		|";
КонецФункции

Функция ТекстОписаниеПартийПрочих()
	Возврат "
		|ВЫБРАТЬ ПЕРВЫЕ 0
		|	ВЫРАЗИТЬ("""" КАК СТРОКА(80)) КАК ЗапросИсточник,
		|	0 КАК Приоритет,
		|	""ХХХХХХХХХХХХХХХХ"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
		|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.НаправлениеДеятельности,
		|	ДД.АналитикаУчетаПартий.Контрагент КАК Поставщик,
		|	ДД.ДокументПоступленияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка) КАК ВидЦенности,
		|	ДД.АналитикаУчетаПартий.СтавкаНДС КАК СтавкаНДС,
		|	ДД.СтатьяРасходов,
		|	ДД.Подразделение,
		|	ДД.АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	0 КАК Количество,
		|	0 КАК Вес,
		|	0 КАК Объем,
		|	ДД.Стоимость,
		|	ДД.СтоимостьБезНДС,
		|	ДД.СтоимостьРегл,
		|	ДД.НДСРегл,
		|	ДД.ПостояннаяРазница,
		|	ДД.ВременнаяРазница,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	ДД.ДокументРеализации,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ДД.СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	ДД.АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК РасчетныйПериод,
		|	ДД.РасчетПартий
		|ИЗ
		|	РегистрНакопления.ПартииПрочихРасходов КАК ДД
		|";
КонецФункции

Функция ТекстОстаткиПартийПрочих() // использует АналитикиРасходовВсе
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстОстаткиПартийПрочих"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	""Остаток"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	Периоды.Период,
		|	ДД.ДокументПоступленияРасходов КАК Регистратор,
		|	ДД.Организация,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК Склад,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.НаправлениеДеятельности,
		|	ДД.АналитикаУчетаПартий.Контрагент КАК Поставщик,
		|	ДД.ДокументПоступленияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
		|	ДД.АналитикаУчетаПартий.СтавкаНДС КАК СтавкаНДС,
		|	ДД.СтатьяРасходов,
		|	ДД.Подразделение,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаРасходов КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	0 КАК Количество,
		|	0 КАК Вес,
		|	0 КАК Объем,
		|	(ВЫБОР
		|		КОГДА Статьи.ВариантРаспределенияРасходовУпр =
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|			ТОГДА ДД.СтоимостьОстаток
		|		ИНАЧЕ 0 КОНЕЦ) КАК Стоимость,
		|	(ВЫБОР
		|		КОГДА Статьи.ВариантРаспределенияРасходовУпр =
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|			ТОГДА ДД.СтоимостьБезНДСОстаток
		|		ИНАЧЕ 0 КОНЕЦ) КАК СтоимостьБезНДС,
		|	(ВЫБОР
		|		КОГДА Статьи.ВариантРаспределенияРасходовРегл =
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|			ТОГДА ДД.СтоимостьРеглОстаток
		|		ИНАЧЕ 0 КОНЕЦ) КАК СтоимостьРегл,
		|	(ВЫБОР
		|		КОГДА Статьи.ВариантРаспределенияРасходовРегл =
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|			ТОГДА ДД.НДСРеглОстаток
		|		ИНАЧЕ 0 КОНЕЦ) КАК НДСРегл,
		|	(ВЫБОР
		|		КОГДА Статьи.ВариантРаспределенияРасходовРегл =
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|			ТОГДА ДД.ПостояннаяРазницаОстаток
		|		ИНАЧЕ 0 КОНЕЦ) КАК ПостояннаяРазница,
		|	(ВЫБОР
		|		КОГДА Статьи.ВариантРаспределенияРасходовРегл =
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|			ТОГДА ДД.ВременнаяРазницаОстаток
		|		ИНАЧЕ 0 КОНЕЦ) КАК ВременнаяРазница,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаОтраженияРасходов,
		|	Правила.БазаРаспределенияПоПартиям КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	&НачалоПериода КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	РегистрНакопления.ПартииПрочихРасходов.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
		|		ПО Статьи.Ссылка = ДД.СтатьяРасходов
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаРаспределенияРасходов КАК Правила
		|		ПО Правила.Ссылка = Статьи.ПравилоРаспределенияРасходовУпр
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
		|		ВЫБРАТЬ
		|			ДД.Регистратор КАК ДокументПоступленияРасходов,
		|			ДД.Организация,
		|			ДД.СтатьяРасходов,
		|			ДД.АналитикаРасходов,
		|			ДД.НаправлениеДеятельности,
		|			МАКСИМУМ(ДД.Период) КАК Период
		|		ИЗ
		|			РегистрНакопления.ПартииПрочихРасходов КАК ДД
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиРасходовВсе КАК КлючиРасходов
		|				ПО КлючиРасходов.Организация = ДД.Организация
		|				И КлючиРасходов.СтатьяРасходов = ДД.СтатьяРасходов
		|				И КлючиРасходов.АналитикаРасходов = ДД.АналитикаРасходов
		|				И КлючиРасходов.НаправлениеДеятельности = ДД.НаправлениеДеятельности
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения КАК Корректировка
		|				ПО Корректировка.Ссылка = ДД.Регистратор
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученныйНалоговыйАгент КАК СчетФактура
		|				ПО СчетФактура.Ссылка = ДД.Регистратор
		|		ГДЕ
		|			ДД.Период < &НачалоПериода
		|			И ДД.Организация В (&МассивОрганизаций)
		|			И ДД.Активность
		|			И ДД.ДокументПоступленияРасходов В (
		|				ДД.Регистратор,
		|				СчетФактура.СчетФактураОснование,
		|				Корректировка.ДокументОснование)
		|			И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		СГРУППИРОВАТЬ ПО
		|			ДД.Регистратор,
		|			ДД.Организация,
		|			ДД.СтатьяРасходов,
		|			ДД.АналитикаРасходов,
		|			ДД.НаправлениеДеятельности
		|	) КАК Периоды
		|		ПО Периоды.ДокументПоступленияРасходов = ДД.ДокументПоступленияРасходов
		|		И Периоды.Организация = ДД.Организация
		|		И Периоды.СтатьяРасходов = ДД.СтатьяРасходов
		|		И Периоды.АналитикаРасходов = ДД.АналитикаРасходов
		|		И Периоды.НаправлениеДеятельности = ДД.НаправлениеДеятельности
		|ГДЕ
		|	(Статьи.ВариантРаспределенияРасходовУпр =
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|	ИЛИ Статьи.ВариантРаспределенияРасходовРегл =
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров))
		|";
КонецФункции

Функция ТекстПриходыПартийПрочих() // использует Декларации
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПриходыПартийПрочих"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	""Партия"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК Склад,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.НаправлениеДеятельности,
		|	ДД.АналитикаУчетаПартий.Контрагент КАК Поставщик,
		|	ДД.ДокументПоступленияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
		|	ДД.АналитикаУчетаПартий.СтавкаНДС КАК СтавкаНДС,
		|	ДД.СтатьяРасходов,
		|	ДД.Подразделение,
		|	ДД.АналитикаРасходов,
		|	(ВЫБОР
		|		КОГДА ДД.Регистратор ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
		|		 И ДД.АналитикаРасходов = ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка)
		|			ТОГДА ЗНАЧЕНИЕ(Документ.ТаможеннаяДекларацияИмпорт.ПустаяСсылка)
		|		ИНАЧЕ ЕСТЬNULL(Декларации.Ссылка, ДД.АналитикаРасходов) КОНЕЦ) КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	0 КАК Количество,
		|	0 КАК Вес,
		|	0 КАК Объем,
		|	СУММА(ВЫБОР
		|		КОГДА Статьи.ВариантРаспределенияРасходовУпр =
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|			ТОГДА ДД.Стоимость / ЕСТЬNULL(Знаменатели.Количество, 1)
		|		ИНАЧЕ 0 КОНЕЦ) КАК Стоимость,
		|	СУММА(ВЫБОР
		|		КОГДА Статьи.ВариантРаспределенияРасходовУпр =
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|			ТОГДА ДД.СтоимостьБезНДС / ЕСТЬNULL(Знаменатели.Количество, 1)
		|		ИНАЧЕ 0 КОНЕЦ) КАК СтоимостьБезНДС,
		|	СУММА(ВЫБОР
		|		КОГДА Статьи.ВариантРаспределенияРасходовРегл =
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|			ТОГДА ДД.СтоимостьРегл / ЕСТЬNULL(Знаменатели.Количество, 1)
		|		ИНАЧЕ 0 КОНЕЦ) КАК СтоимостьРегл,
		|	СУММА(ВЫБОР
		|		КОГДА Статьи.ВариантРаспределенияРасходовРегл =
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|			ТОГДА ДД.НДСРегл / ЕСТЬNULL(Знаменатели.Количество, 1)
		|		ИНАЧЕ 0 КОНЕЦ) КАК НДСРегл,
		|	СУММА(ВЫБОР
		|		КОГДА Статьи.ВариантРаспределенияРасходовРегл =
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|			ТОГДА ДД.ПостояннаяРазница / ЕСТЬNULL(Знаменатели.Количество, 1)
		|		ИНАЧЕ 0 КОНЕЦ) КАК ПостояннаяРазница,
		|	СУММА(ВЫБОР
		|		КОГДА Статьи.ВариантРаспределенияРасходовРегл =
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|			ТОГДА ДД.ВременнаяРазница / ЕСТЬNULL(Знаменатели.Количество, 1)
		|		ИНАЧЕ 0 КОНЕЦ) КАК ВременнаяРазница,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	ДД.ДокументРеализации,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ДД.СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаОтраженияРасходов,
		|	Правила.БазаРаспределенияПоПартиям КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Период КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	РегистрНакопления.ПартииПрочихРасходов КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
		|		ПО Статьи.Ссылка = ДД.СтатьяРасходов
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаРаспределенияРасходов КАК Правила
		|		ПО Правила.Ссылка = Статьи.ПравилоРаспределенияРасходовУпр
		|	ЛЕВОЕ СОЕДИНЕНИЕ Декларации КАК Декларации
		|		ПО Декларации.Поступление = ДД.АналитикаРасходов
		|	ЛЕВОЕ СОЕДИНЕНИЕ КоличествоДеклараций КАК Знаменатели
		|		ПО Знаменатели.Поступление = ДД.АналитикаРасходов
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения КАК Корректировка
		|		ПО Корректировка.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученныйНалоговыйАгент КАК СчетФактура
		|		ПО СчетФактура.Ссылка = ДД.Регистратор
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ДокументПоступленияРасходов В (
		|		ДД.Регистратор,
		|		СчетФактура.СчетФактураОснование,
		|		Корректировка.ДокументОснование)
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И (Статьи.ВариантРаспределенияРасходовУпр =
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|		ИЛИ Статьи.ВариантРаспределенияРасходовРегл =
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров))
		|СГРУППИРОВАТЬ ПО
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.НаправлениеДеятельности,
		|	ДД.ДокументПоступленияРасходов,
		|	ДД.СтатьяРасходов,
		|	ДД.Подразделение,
		|	ДД.АналитикаРасходов,
		|	(ВЫБОР
		|		КОГДА ДД.Регистратор ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
		|		 И ДД.АналитикаРасходов = ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка)
		|			ТОГДА ЗНАЧЕНИЕ(Документ.ТаможеннаяДекларацияИмпорт.ПустаяСсылка)
		|		ИНАЧЕ ЕСТЬNULL(Декларации.Ссылка, ДД.АналитикаРасходов) КОНЕЦ),
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС,
		|	ДД.НалогообложениеНДС,
		|	ДД.ДокументРеализации,
		|	ДД.СтатьяОтраженияРасходов,
		|	Правила.БазаРаспределенияПоПартиям
		|";
КонецФункции

Функция ТекстДополненияПартийПрочих() // использует РегистраторыПрочих
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстДополненияПартийПрочих"" КАК ЗапросИсточник,
		|	99 КАК Приоритет,
		|	""Дополнение"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК Склад,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК Поставщик,
		|	ДД.ДокументПоступленияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
		|	ДД.СтатьяРасходов,
		|	ДД.Подразделение,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаРасходов КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	0 КАК Количество,
		|	0 КАК Вес,
		|	0 КАК Объем,
		|	ДД.Стоимость,
		|	ДД.СтоимостьБезНДС,
		|	ДД.СтоимостьРегл,
		|	ДД.НДСРегл,
		|	ДД.ПостояннаяРазница,
		|	ДД.ВременнаяРазница,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	ДД.ДокументРеализации,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ДД.СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Период КАК РасчетныйПериод,
		|	ДД.РасчетПартий
		|ИЗ
		|	РегистрНакопления.ПартииПрочихРасходов КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистраторыПрочих КАК Регистраторы
		|		ПО Регистраторы.Регистратор = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
		|		ПО Статьи.Ссылка = ДД.СтатьяРасходов
		|ГДЕ
		|	НЕ ДД.РасчетПартий
		|	И (
		|		НЕ (ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода)
		|		И ДД.Организация В (&МассивОрганизаций)
		|		И ДД.Активность
		|	ИЛИ ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	)
		|";
КонецФункции

Функция ТекстБазисОстатки() // использует АналитикиРасходовВсе, ОстаткиСебестоимости
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстБазисОстатки"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&НачалоПериода КАК Период,
		|	ДД.ДокументПоступления КАК Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	АналитикиТоваров.МестоХранения КАК Склад,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК Поставщик,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	ДД.КоличествоОстаток КАК Количество,
		|	&Вес * ДД.КоличествоОстаток КАК Вес,
		|	&Объем * ДД.КоличествоОстаток КАК Объем,
		|	(ВЫБОР
		|		КОГДА ДД.Организация В (&ОрганизацииСФИФОСкользящая) ТОГДА ДД.СтоимостьОстаток
		|		ИНАЧЕ ДД.СтоимостьРеглОстаток КОНЕЦ) КАК Стоимость,
		|	ДД.СтоимостьБезНДСОстаток КАК СтоимостьБезНДС,
		|	ДД.СтоимостьРеглОстаток КАК СтоимостьРегл,
		|	ДД.НДСРеглОстаток КАК НДСРегл,
		|	ДД.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
		|	ДД.ВременнаяРазницаОстаток КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ЕСТЬNULL(АналитикиПартий.НалогообложениеНДС,
		|		ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
		|	&НачалоПериода КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикиТоваров
		|		ПО АналитикиТоваров.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = АналитикиТоваров.Номенклатура
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК АналитикиПартий
		|		ПО АналитикиПартий.Ссылка = ДД.АналитикаУчетаПартий
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
		|		ПО ВидыЗапасов.Ссылка = ДД.ВидЗапасов
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОстаткиСебестоимости КАК Остатки
		|		ПО Остатки.Организация = ДД.Организация
		|		И Остатки.Номенклатура = АналитикиТоваров.Номенклатура
		|		И Остатки.Характеристика = АналитикиТоваров.Характеристика
		|		И Остатки.Серия = АналитикиТоваров.Серия
		|		И Остатки.Склад = АналитикиТоваров.МестоХранения
		|		И (Остатки.Назначение = АналитикиТоваров.Назначение
		|			ИЛИ НЕ &УчитыватьНазначение)
		|ГДЕ
		|	ДД.СтоимостьОстаток > 0
		|	И (ДД.Организация,
		|		АналитикиТоваров.Номенклатура,
		|		АналитикиТоваров.МестоХранения,
		|		ЕСТЬNULL(АналитикиТоваров.Назначение.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|		ДД.ДокументПоступления) В
		|	(
		|		ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			КлючиРасходов.Организация,
		|			АналитикиТоваров.Номенклатура,
		|			АналитикиТоваров.МестоХранения,
		|			(ВЫБОР
		|				КОГДА КлючиРасходов.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|					ТОГДА ЕСТЬNULL(АналитикиТоваров.Назначение.НаправлениеДеятельности,
		|						ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
		|				ИНАЧЕ КлючиРасходов.НаправлениеДеятельности КОНЕЦ) КАК НаправлениеДеятельности,
		|			ДД.ДокументПоступления
		|		ИЗ
		|			АналитикиРасходовВсе КАК КлючиРасходов
		|		ГДЕ 
		|			КлючиРасходов.АналитикаРасходов В (
		|				АналитикиТоваров.Номенклатура,
		|				ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|				АналитикиТоваров.МестоХранения,
		|				ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
		|				ДД.ДокументПоступления)
		|	)
		|	И ВидыЗапасов.ТипЗапасов В (&СобственныеТипыЗапасов)
		|";
КонецФункции

Функция ТекстБазисОстаткиСборок() // использует ОстаткиПартийСборок, АналитикиРасходовВсе
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстБазисОстаткиСборок"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&НачалоПериода КАК Период,
		|	ДД.ДокументВыпуска КАК Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаПродукции КАК АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	АналитикиТоваров.МестоХранения КАК Склад,
		|	ДД.ДокументВыпуска КАК ДокументПоступления,
		|	Партии.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК Поставщик,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	Партии.Количество,
		|	&Вес * Партии.Количество КАК Вес,
		|	&Объем * Партии.Количество КАК Объем,
		|	(ВЫБОР
		|		КОГДА ДД.Организация В (&ОрганизацииСФИФОСкользящая) ТОГДА ДД.СтоимостьОстаток
		|		ИНАЧЕ ДД.СтоимостьРеглОстаток КОНЕЦ) КАК Стоимость,
		|	ДД.СтоимостьБезНДСОстаток КАК СтоимостьБезНДС,
		|	ДД.СтоимостьРеглОстаток КАК СтоимостьРегл,
		|	ДД.НДСРеглОстаток КАК НДСРегл,
		|	ДД.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
		|	ДД.ВременнаяРазницаОстаток КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	Партии.НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
		|	&НачалоПериода КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	РегистрНакопления.ПартииЗатратНаВыпуск.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОстаткиПартийСборок КАК Партии
		|		ПО Партии.Организация = ДД.Организация
		|		И Партии.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаПродукции
		|		И Партии.ДокументПоступления = ДД.ДокументВыпуска
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикиТоваров
		|		ПО АналитикиТоваров.Ссылка = ДД.АналитикаУчетаПродукции
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = АналитикиТоваров.Номенклатура
		|ГДЕ
		|	(ДД.Организация,
		|		АналитикиТоваров.Номенклатура,
		|		АналитикиТоваров.МестоХранения,
		|		ЕСТЬNULL(АналитикиТоваров.Назначение.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|		ДД.ДокументВыпуска) В
		|	(
		|		ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			КлючиРасходов.Организация,
		|			АналитикиТоваров.Номенклатура,
		|			АналитикиТоваров.МестоХранения,
		|			(ВЫБОР
		|				КОГДА КлючиРасходов.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|					ТОГДА ЕСТЬNULL(АналитикиТоваров.Назначение.НаправлениеДеятельности,
		|						ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
		|				ИНАЧЕ КлючиРасходов.НаправлениеДеятельности КОНЕЦ) КАК НаправлениеДеятельности,
		|			ДД.ДокументВыпуска
		|		ИЗ
		|			АналитикиРасходовВсе КАК КлючиРасходов
		|		ГДЕ 
		|			КлючиРасходов.АналитикаРасходов В (
		|				АналитикиТоваров.Номенклатура,
		|				ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|				АналитикиТоваров.МестоХранения,
		|				ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
		|				ДД.ДокументВыпуска)
		|	)
		|";
КонецФункции

Функция ТекстБазисПриходы() // использует АналитикиРасходовВсе
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстБазисПриходы"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	АналитикиТоваров.МестоХранения КАК Склад,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК Поставщик,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	СУММА(
		|		ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ДД.Количество
		|		ИНАЧЕ -ДД.Количество КОНЕЦ) КАК Количество,
		|	СУММА(&Вес
		|		* ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ДД.Количество
		|		ИНАЧЕ -ДД.Количество КОНЕЦ) КАК Вес,
		|	СУММА(&Объем
		|		* ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ДД.Количество
		|		ИНАЧЕ -ДД.Количество КОНЕЦ) КАК Объем,
		|	СУММА(
		|		ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		 И ДД.Организация В (&ОрганизацииСФИФОСкользящая)
		|			ТОГДА ДД.Стоимость
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		 И НЕ ДД.Организация В (&ОрганизацииСФИФОСкользящая)
		|			ТОГДА ДД.СтоимостьРегл
		|		ИНАЧЕ -ДД.Стоимость КОНЕЦ) КАК Стоимость,
		|	СУММА(
		|		ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ДД.СтоимостьБезНДС
		|		ИНАЧЕ -ДД.СтоимостьБезНДС КОНЕЦ) КАК СтоимостьБезНДС,
		|	СУММА(
		|		ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ДД.СтоимостьРегл
		|		ИНАЧЕ -ДД.СтоимостьРегл КОНЕЦ) КАК СтоимостьРегл,
		|	СУММА(
		|		ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ДД.НДСРегл
		|		ИНАЧЕ -ДД.НДСРегл КОНЕЦ) КАК НДСРегл,
		|	СУММА(
		|		ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ДД.ПостояннаяРазница
		|		ИНАЧЕ -ДД.ПостояннаяРазница КОНЕЦ) КАК ПостояннаяРазница,
		|	СУММА(
		|		ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ДД.ВременнаяРазница
		|		ИНАЧЕ -ДД.ВременнаяРазница КОНЕЦ) КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	АналитикиПартий.НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
		|	ДД.Период КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикиТоваров
		|		ПО АналитикиТоваров.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = АналитикиТоваров.Номенклатура
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
		|		ПО АналитикиТоваров.МестоХранения = Склады.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикиПартий
		|		ПО АналитикиПартий.КлючАналитики = ДД.АналитикаУчетаПартий
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК СпрВидыЗапасов
		|		ПО СпрВидыЗапасов.Ссылка = ДД.ВидЗапасов
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров КАК Перемещения
		|		ПО Перемещения.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И Перемещения.Ссылка = ДД.Регистратор
		|		И Перемещения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеТоваровМеждуФилиалами)
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровМеждуОрганизациями КАК Возвраты
		|		ПО Возвраты.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И Возвраты.Ссылка = ДД.Регистратор
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И (ДД.Регистратор = ДД.ДокументПоступления ИЛИ НЕ Перемещения.Ссылка ЕСТЬ NULL)
		|	И Возвраты.Ссылка ЕСТЬ NULL
		|	И НЕ ДД.Регистратор Ссылка Документ.СборкаТоваров
		|	И НЕ (ДД.Регистратор Ссылка Документ.ПередачаТоваровМеждуОрганизациями И ДД.Стоимость = 0)
		|	И (ДД.Первичное ИЛИ
		|		ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И (ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.ПередачаТоваровМеждуОрганизациями)
		|			ИЛИ ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.ТаможеннаяДекларацияИмпорт)
		|			ИЛИ НЕ Перемещения.Ссылка ЕСТЬ NULL)
		|	)
		|	И (ДД.Организация,
		|		АналитикиТоваров.Номенклатура,
		|		АналитикиТоваров.МестоХранения,
		|		ЕСТЬNULL(АналитикиТоваров.Назначение.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|		ДД.Регистратор) В
		|	(
		|		ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			КлючиРасходов.Организация,
		|			АналитикиТоваров.Номенклатура,
		|			АналитикиТоваров.МестоХранения,
		|			(ВЫБОР
		|				КОГДА КлючиРасходов.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|					ТОГДА ЕСТЬNULL(АналитикиТоваров.Назначение.НаправлениеДеятельности,
		|						ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
		|				ИНАЧЕ КлючиРасходов.НаправлениеДеятельности КОНЕЦ) КАК НаправлениеДеятельности,
		|			ДД.Регистратор
		|		ИЗ
		|			АналитикиРасходовВсе КАК КлючиРасходов
		|		ГДЕ 
		|			КлючиРасходов.АналитикаРасходов В (
		|				АналитикиТоваров.Номенклатура,
		|				ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|				АналитикиТоваров.МестоХранения,
		|				ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
		|				ДД.ДокументПоступления)
		|			ИЛИ
		|			ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИПЗНАЧЕНИЯ(КлючиРасходов.АналитикаРасходов)
		|			И КлючиРасходов.АналитикаРасходов В (
		|				ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка),
		|				ЗНАЧЕНИЕ(Документ.ТаможеннаяДекларацияИмпорт.ПустаяСсылка),
		|				ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка))
		|	)
		|	И СпрВидыЗапасов.ТипЗапасов В (&СобственныеТипыЗапасов)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка,
		|	АналитикиТоваров.МестоХранения,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	АналитикиПартий.НалогообложениеНДС
		|";
КонецФункции

Функция ТекстБазисКорректировкиПоступлений() // использует АналитикиРасходовВсе
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстБазисКорректировкиПоступлений"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	Поступление.Дата КАК Период,
		|	ДД.ДокументПоступления КАК Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	АналитикиТоваров.МестоХранения КАК Склад,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК Поставщик,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРаспределения,
		|	0. КАК Базис,
		|	СУММА(
		|		ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ДД.Количество
		|		ИНАЧЕ -ДД.Количество КОНЕЦ) КАК Количество,
		|	СУММА(&Вес
		|		* ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ДД.Количество
		|		ИНАЧЕ -ДД.Количество КОНЕЦ) КАК Вес,
		|	СУММА(&Объем
		|		* ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ДД.Количество
		|		ИНАЧЕ -ДД.Количество КОНЕЦ) КАК Объем,
		|	СУММА(
		|		ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ДД.Стоимость
		|		ИНАЧЕ -ДД.Стоимость КОНЕЦ) КАК Стоимость,
		|	СУММА(
		|		ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ДД.СтоимостьБезНДС
		|		ИНАЧЕ -ДД.СтоимостьБезНДС КОНЕЦ) КАК СтоимостьБезНДС,
		|	СУММА(
		|		ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ДД.СтоимостьРегл
		|		ИНАЧЕ -ДД.СтоимостьРегл КОНЕЦ) КАК СтоимостьРегл,
		|	СУММА(
		|		ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ДД.НДСРегл
		|		ИНАЧЕ -ДД.НДСРегл КОНЕЦ) КАК НДСРегл,
		|	СУММА(
		|		ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ДД.ПостояннаяРазница
		|		ИНАЧЕ -ДД.ПостояннаяРазница КОНЕЦ) КАК ПостояннаяРазница,
		|	СУММА(
		|		ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ДД.ВременнаяРазница
		|		ИНАЧЕ -ДД.ВременнаяРазница КОНЕЦ) КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	АналитикиПартий.НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
		|	Поступление.Дата КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикиТоваров
		|		ПО АналитикиТоваров.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = АналитикиТоваров.Номенклатура
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
		|		ПО АналитикиТоваров.МестоХранения = Склады.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикиПартий
		|		ПО АналитикиПартий.КлючАналитики = ДД.АналитикаУчетаПартий
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК СпрВидыЗапасов
		|		ПО СпрВидыЗапасов.Ссылка = ДД.ВидЗапасов
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК Поступление
		|		ПО Поступление.Ссылка = ДД.ДокументПоступления
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.Регистратор Ссылка Документ.КорректировкаПриобретения
		|	И ДД.Первичное
		|	И (ДД.Организация, АналитикиТоваров.Номенклатура, АналитикиТоваров.МестоХранения, ДД.ДокументПоступления) В (
		|		ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			КлючиРасходов.Организация, АналитикиТоваров.Номенклатура, АналитикиТоваров.МестоХранения, ДД.ДокументПоступления
		|		ИЗ
		|			АналитикиРасходовВсе КАК КлючиРасходов
		|		ГДЕ 
		|			КлючиРасходов.АналитикаРасходов В (
		|				АналитикиТоваров.Номенклатура,
		|				ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|				АналитикиТоваров.МестоХранения,
		|				ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
		|				ДД.ДокументПоступления)
		|			ИЛИ
		|			ТИПЗНАЧЕНИЯ(ДД.ДокументПоступления) = ТИПЗНАЧЕНИЯ(КлючиРасходов.АналитикаРасходов)
		|			И КлючиРасходов.АналитикаРасходов В (
		|				ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка),
		|				ЗНАЧЕНИЕ(Документ.ТаможеннаяДекларацияИмпорт.ПустаяСсылка),
		|				ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка))
		|	)
		|	И СпрВидыЗапасов.ТипЗапасов В (&СобственныеТипыЗапасов)
		|
		|СГРУППИРОВАТЬ ПО
		|	Поступление.Дата,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка,
		|	АналитикиТоваров.МестоХранения,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	АналитикиПартий.НалогообложениеНДС
		|";
КонецФункции

Функция ТекстБазисПриходыСборок() // использует ПриходыПартийСборок, АналитикиРасходовВсе
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстБазисПриходыСборок"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаПродукции КАК АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	АналитикиТоваров.СкладскаяТерритория КАК Склад,
		|	ДД.ДокументВыпуска КАК ДокументПоступления,
		|	Партии.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК Поставщик,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	Партии.Количество,
		|	&Вес * Партии.Количество КАК Вес,
		|	&Объем * Партии.Количество КАК Объем,
		|	СУММА(ВЫБОР
		|		КОГДА ДД.Организация В (&ОрганизацииСФИФОСкользящая) ТОГДА ДД.Стоимость
		|		ИНАЧЕ ДД.СтоимостьРегл КОНЕЦ) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.НДСРегл) КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	Партии.НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
		|	ДД.Период КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	РегистрНакопления.ПартииЗатратНаВыпуск КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПриходыПартийСборок КАК Партии
		|		ПО Партии.Организация = ДД.Организация
		|		И Партии.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаПродукции
		|		И Партии.ДокументПоступления = ДД.ДокументВыпуска
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикиТоваров
		|		ПО АналитикиТоваров.Ссылка = ДД.АналитикаУчетаПродукции
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = АналитикиТоваров.Номенклатура
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
		|		ПО АналитикиТоваров.СкладскаяТерритория = Склады.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК СпрВидыЗапасов
		|		ПО СпрВидыЗапасов.Ссылка = Партии.ВидЗапасов
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.Регистратор = ДД.ДокументВыпуска
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И (ДД.Организация,
		|		АналитикиТоваров.Номенклатура,
		|		АналитикиТоваров.СкладскаяТерритория,
		|		ЕСТЬNULL(АналитикиТоваров.Назначение.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|		ДД.Регистратор) В
		|	(
		|		ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			КлючиРасходов.Организация,
		|			АналитикиТоваров.Номенклатура,
		|			АналитикиТоваров.СкладскаяТерритория,
		|			(ВЫБОР
		|				КОГДА КлючиРасходов.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|					ТОГДА ЕСТЬNULL(АналитикиТоваров.Назначение.НаправлениеДеятельности,
		|						ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
		|				ИНАЧЕ КлючиРасходов.НаправлениеДеятельности КОНЕЦ) КАК НаправлениеДеятельности,
		|			ДД.Регистратор
		|		ИЗ
		|			АналитикиРасходовВсе КАК КлючиРасходов
		|		ГДЕ 
		|			КлючиРасходов.АналитикаРасходов В (
		|				АналитикиТоваров.Номенклатура,
		|				ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|				АналитикиТоваров.СкладскаяТерритория,
		|				ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
		|				ДД.ДокументВыпуска)
		|			ИЛИ
		|			ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИПЗНАЧЕНИЯ(КлючиРасходов.АналитикаРасходов)
		|			И КлючиРасходов.АналитикаРасходов В (
		|				ЗНАЧЕНИЕ(Документ.СборкаТоваров.ПустаяСсылка),
		|				ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка))
		|	)
		|	И СпрВидыЗапасов.ТипЗапасов В (&СобственныеТипыЗапасов)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаПродукции,
		|	СпрНоменклатура.Ссылка,
		|	АналитикиТоваров.СкладскаяТерритория,
		|	ДД.ДокументВыпуска,
		|	Партии.ВидЗапасов,
		|	Партии.Количество,
		|	&Вес * Партии.Количество,
		|	&Объем * Партии.Количество,
		|	Партии.НалогообложениеНДС
		|";
КонецФункции

Функция ТекстБазисРаботы() // использует АналитикиРасходовВсе
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстБазисРаботы"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	АналитикиТоваров.МестоХранения КАК Склад,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК Поставщик,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(&Вес * ДД.Количество) КАК Вес,
		|	СУММА(&Объем * ДД.Количество) КАК Объем,
		|	СУММА(ВЫБОР
		|		КОГДА ДД.Организация В (&ОрганизацииСФИФОСкользящая) ТОГДА ДД.Стоимость
		|		ИНАЧЕ ДД.СтоимостьРегл КОНЕЦ) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.НДСРегл) КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	АналитикиПартий.НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
		|	ДД.Период КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	РегистрНакопления.ПартииПроизводственныхЗатрат КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикиТоваров
		|		ПО АналитикиТоваров.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = АналитикиТоваров.Номенклатура
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Работы
		|		ПО Работы.Ссылка = АналитикиТоваров.Номенклатура
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикиПартий
		|		ПО АналитикиПартий.КлючАналитики = ДД.АналитикаУчетаПартий
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И Работы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|	И ДД.Регистратор = ДД.ДокументПоступления
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И (ДД.Организация,
		|		АналитикиТоваров.Номенклатура,
		|		АналитикиТоваров.МестоХранения,
		|		ЕСТЬNULL(АналитикиТоваров.Назначение.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|		ДД.Регистратор) В
		|	(
		|		ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			КлючиРасходов.Организация,
		|			АналитикиТоваров.Номенклатура,
		|			АналитикиТоваров.МестоХранения,
		|			(ВЫБОР
		|				КОГДА КлючиРасходов.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|					ТОГДА ЕСТЬNULL(АналитикиТоваров.Назначение.НаправлениеДеятельности,
		|						ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
		|				ИНАЧЕ КлючиРасходов.НаправлениеДеятельности КОНЕЦ) КАК НаправлениеДеятельности,
		|			ДД.Регистратор
		|		ИЗ
		|			АналитикиРасходовВсе КАК КлючиРасходов
		|		ГДЕ 
		|			КлючиРасходов.АналитикаРасходов В (
		|				АналитикиТоваров.Номенклатура,
		|				ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|				АналитикиТоваров.МестоХранения,
		|				ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
		|				ДД.ДокументПоступления)
		|			ИЛИ
		|			ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИПЗНАЧЕНИЯ(КлючиРасходов.АналитикаРасходов)
		|			И КлючиРасходов.АналитикаРасходов В (
		|				ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка),
		|				ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка))
		|	)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка,
		|	АналитикиТоваров.МестоХранения,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	АналитикиПартий.НалогообложениеНДС
		|";
КонецФункции

Функция ТекстБазисПеремещения() // использует АналитикиРасходовВсе
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстБазисПеремещения"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК Склад,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК Поставщик,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	ДД.Регистратор КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	ДД.Количество,
		|	&Вес * ДД.Количество КАК Вес,
		|	&Объем * ДД.Количество КАК Объем,
		|	(ВЫБОР
		|		КОГДА ДД.Организация В (&ОрганизацииСФИФОСкользящая) ТОГДА ДД.Стоимость
		|		ИНАЧЕ ДД.СтоимостьРегл КОНЕЦ) КАК Стоимость,
		|	ДД.СтоимостьБезНДС,
		|	ДД.СтоимостьРегл,
		|	ДД.НДСРегл,
		|	ДД.ПостояннаяРазница,
		|	ДД.ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	АналитикиПартий.НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
		|	ДД.Регистратор КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
		|	ДД.Период КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = ДД.АналитикаУчетаНоменклатуры.Номенклатура
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК АналитикиПартий
		|		ПО АналитикиПартий.Ссылка = ДД.АналитикаУчетаПартий
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
		|		ПО ВидыЗапасов.Ссылка = ДД.ВидЗапасов
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.ПеремещениеТоваров)
		|	И НЕ ДД.ДокументПоступления Ссылка Документ.СборкаТоваров
		|	И (ДД.Организация,
		|		ЕСТЬNULL(ДД.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|		ДД.Регистратор) В
		|	(
		|		ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			КлючиРасходов.Организация,
		|			(ВЫБОР
		|				КОГДА КлючиРасходов.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|					ТОГДА ЕСТЬNULL(ДД.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности,
		|						ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
		|				ИНАЧЕ КлючиРасходов.НаправлениеДеятельности КОНЕЦ) КАК НаправлениеДеятельности,
		|			ДД.Регистратор
		|		ИЗ
		|			АналитикиРасходовВсе КАК КлючиРасходов
		|		ГДЕ 
		|			КлючиРасходов.АналитикаРасходов В (
		|				ДД.Регистратор,
		|				ЗНАЧЕНИЕ(Документ.ПеремещениеТоваров.ПустаяСсылка))
		|	)
		|	И ВидыЗапасов.ТипЗапасов В (&СобственныеТипыЗапасов)
		|";
КонецФункции

Функция ТекстБазисПеремещенияСборок() // использует АналитикиРасходовВсе
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстБазисПеремещенияСборок"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаПродукции КАК АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК Склад,
		|	ДД.ДокументВыпуска КАК ДокументПоступления,
		|	Партии.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК Поставщик,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	ДД.Регистратор КАК АналитикаРаспределения,
		|	0. КАК Базис,
		|	Партии.Количество,
		|	&Вес * Партии.Количество КАК Вес,
		|	&Объем * Партии.Количество КАК Объем,
		|	СУММА(ВЫБОР
		|		КОГДА ДД.Организация В (&ОрганизацииСФИФОСкользящая) ТОГДА ДД.Стоимость
		|		ИНАЧЕ ДД.СтоимостьРегл КОНЕЦ) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.НДСРегл) КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	Партии.НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
		|	ДД.Регистратор КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
		|	ДД.Период КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	РегистрНакопления.ПартииЗатратНаВыпуск КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПриходыПартийСборок КАК Партии
		|		ПО Партии.Организация = ДД.Организация
		|		И Партии.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаПродукции
		|		И Партии.ДокументПоступления = ДД.ДокументВыпуска
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = ДД.АналитикаУчетаПродукции.Номенклатура
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
		|		ПО ВидыЗапасов.Ссылка = ДД.ВидЗапасов
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.ПеремещениеТоваров)
		|	И (ДД.Организация,
		|		ЕСТЬNULL(ДД.АналитикаУчетаПродукции.Назначение.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|		ДД.Регистратор) В
		|	(
		|		ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			КлючиРасходов.Организация,
		|			(ВЫБОР
		|				КОГДА КлючиРасходов.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|					ТОГДА ЕСТЬNULL(ДД.АналитикаУчетаПродукции.Назначение.НаправлениеДеятельности,
		|						ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
		|				ИНАЧЕ КлючиРасходов.НаправлениеДеятельности КОНЕЦ) КАК НаправлениеДеятельности,
		|			ДД.Регистратор
		|		ИЗ
		|			АналитикиРасходовВсе КАК КлючиРасходов
		|		ГДЕ 
		|			КлючиРасходов.АналитикаРасходов В (
		|				ДД.Регистратор,
		|				ЗНАЧЕНИЕ(Документ.ПеремещениеТоваров.ПустаяСсылка))
		|	)
		|	И ВидыЗапасов.ТипЗапасов В (&СобственныеТипыЗапасов)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаПродукции,
		|	СпрНоменклатура.Ссылка,
		|	ДД.ДокументВыпуска,
		|	Партии.ВидЗапасов,
		|	Партии.Количество,
		|	&Вес * Партии.Количество,
		|	&Объем * Партии.Количество,
		|	Партии.НалогообложениеНДС
		|";
КонецФункции

Функция ТекстБазисПрошлыеПеремещения() // использует ПрошлыеПеремещения
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстБазисПрошлыеПеремещения"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ПрошлыеПеремещения.Период,
		|	ПрошлыеПеремещения.Перемещение КАК Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК Склад,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК Поставщик,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	ПрошлыеПеремещения.Перемещение КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	ДД.КоличествоОстаток КАК Количество,
		|	&Вес * ДД.КоличествоОстаток КАК Вес,
		|	&Объем * ДД.КоличествоОстаток КАК Объем,
		|	(ВЫБОР
		|		КОГДА ДД.Организация В (&ОрганизацииСФИФОСкользящая) ТОГДА ДД.СтоимостьОстаток
		|		ИНАЧЕ ДД.СтоимостьРеглОстаток КОНЕЦ) КАК Стоимость,
		|	ДД.СтоимостьБезНДСОстаток КАК СтоимостьБезНДС,
		|	ДД.СтоимостьРеглОстаток КАК СтоимостьРегл,
		|	ДД.НДСРеглОстаток КАК НДСРегл,
		|	ДД.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
		|	ДД.ВременнаяРазницаОстаток КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	АналитикиПартий.НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
		|	ДД.ДокументПоступления КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
		|	&НачалоПериода КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрошлыеПеремещения КАК ПрошлыеПеремещения
		|		ПО ПрошлыеПеремещения.Организация = ДД.Организация
		|		И ПрошлыеПеремещения.ДокументПоступления = ДД.ДокументПоступления
		|		И ПрошлыеПеремещения.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = ДД.АналитикаУчетаНоменклатуры.Номенклатура
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикиПартий
		|		ПО АналитикиПартий.КлючАналитики = ДД.АналитикаУчетаПартий
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
		|		ПО ВидыЗапасов.Ссылка = ДД.ВидЗапасов
		|ГДЕ
		|	ВидыЗапасов.ТипЗапасов В (&СобственныеТипыЗапасов)
		|";
КонецФункции

Функция ТекстБазисЗаказы() // использует ПартииПоЗаказам, ЗаказыИзАналитикРасходов
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстБазисЗаказы"" КАК ЗапросИсточник,
		|	ДД.Приоритет,
		|	ДД.ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Заказ КАК Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК Склад,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК Поставщик,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(&Вес * ДД.Количество) КАК Вес,
		|	СУММА(&Объем * ДД.Количество) КАК Объем,
		|	СУММА(ВЫБОР
		|		КОГДА ДД.Организация В (&ОрганизацииСФИФОСкользящая) ТОГДА ДД.Стоимость
		|		ИНАЧЕ ДД.СтоимостьРегл КОНЕЦ) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.НДСРегл) КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
		|	(ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Заказ) = ТИП(Документ.ЗаказНаПеремещение) ТОГДА ДД.Регистратор
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ) КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК РазделУчета,
		|	(ВЫБОР КОГДА ДД.Период < &НачалоПериода ТОГДА &НачалоПериода ИНАЧЕ ДД.Период КОНЕЦ) КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	ПартииПоЗаказам КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
		|		ПО ВидыЗапасов.Ссылка = ДД.ВидЗапасов
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = ДД.АналитикаУчетаНоменклатуры.Номенклатура
		|ГДЕ
		|	ЕСТЬNULL(ВидыЗапасов.ТипЗапасов, ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)) В (&СобственныеТипыЗапасов)
		|	И (ДД.Регистратор, ДД.Заказ) В (
		|		ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			Заказы.Поступление,
		|			Заказы.Заказ
		|		ИЗ
		|			ЗаказыИзАналитикРасходов КАК Заказы
		|	)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Приоритет,
		|	ДД.ТипЗаписи,
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Заказ,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.НалогообложениеНДС,
		|	(ВЫБОР КОГДА ДД.Период < &НачалоПериода ТОГДА &НачалоПериода ИНАЧЕ ДД.Период КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Заказ) = ТИП(Документ.ЗаказНаПеремещение) ТОГДА ДД.Регистратор
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ)
		|";
КонецФункции

Функция ТекстБазисУслуги() // использует АналитикиРасходовВсе
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстБазисУслуги"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК Склад,
		|	ДД.Регистратор КАК ДокументПоступления,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ДД.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК Поставщик,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	(ВЫБОР
		|		КОГДА ЕСТЬNULL(ДД.СтатьяРасходов.ВидЦенностиНДС, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)) <>
		|				ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
		|			ТОГДА ДД.СтатьяРасходов.ВидЦенностиНДС
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги) КОНЕЦ) КАК ВидЦенности,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	1 КАК Количество,
		|	0 КАК Вес,
		|	0 КАК Объем,
		|	СУММА(ВЫБОР
		|		КОГДА ДД.Организация В (&ОрганизацииСФИФОСкользящая) ТОГДА ДД.Сумма
		|		ИНАЧЕ ДД.СуммаРегл КОНЕЦ) КАК Стоимость,
		|	СУММА(ДД.Сумма) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СуммаРегл) КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ЕСТЬNULL(ПоступлениеТоваров.ЗакупкаПодДеятельность, ПоступлениеУслуг.ЗакупкаПодДеятельность) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ДД.Подразделение КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ДД.СтатьяРасходов КАК СтатьяОтраженияРасходов,
		|	ДД.СтатьяРасходов.ВариантРаздельногоУчетаНДС КАК ВариантРаздельногоУчетаНДС,
		|	ДД.АналитикаРасходов КАК АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
		|	ДД.Период КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	РегистрНакопления.ПрочиеРасходы КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ПоступлениеТоваров
		|		ПО ПоступлениеТоваров.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеУслугПрочихАктивов КАК ПоступлениеУслуг
		|		ПО ПоступлениеУслуг.Ссылка = ДД.Регистратор
		|ГДЕ
		|	ДД.Период <= &ОкончаниеПериода
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И (ДД.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг 
		|		ИЛИ ДД.Регистратор ССЫЛКА Документ.ПриобретениеУслугПрочихАктивов)
		|	И (ДД.Организация, ДД.Регистратор) В (
		|		ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			КлючиРасходов.Организация, ДД.Регистратор
		|		ИЗ
		|			АналитикиРасходовВсе КАК КлючиРасходов
		|		ГДЕ 
		|			КлючиРасходов.АналитикаРасходов = ДД.Регистратор
		|			И КлючиРасходов.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.НДСНалоговогоАгента)
		|		)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ДД.СтатьяРасходов,
		|	ДД.СтатьяРасходов.ВариантРаздельногоУчетаНДС,
		|	ДД.АналитикаРасходов,
		|	ДД.НаправлениеДеятельности,
		|	(ВЫБОР
		|		КОГДА ЕСТЬNULL(ДД.СтатьяРасходов.ВидЦенностиНДС, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)) <>
		|				ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
		|			ТОГДА ДД.СтатьяРасходов.ВидЦенностиНДС
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги) КОНЕЦ),
		|	ЕСТЬNULL(ПоступлениеТоваров.ЗакупкаПодДеятельность, ПоступлениеУслуг.ЗакупкаПодДеятельность)
		|";
КонецФункции

Функция ТекстБазисПрочиеАктивыПассивы() // использует АналитикиРасходовВсе
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстБазисПрочиеАктивыПассивы"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК Склад,
		|	ДД.Регистратор КАК ДокументПоступления,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ДД.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК Поставщик,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги) КАК ВидЦенности,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	1 КАК Количество,
		|	0 КАК Вес,
		|	0 КАК Объем,
		|	СУММА(ДД.Сумма) КАК Стоимость,
		|	СУММА(ДД.Сумма) КАК СтоимостьБезНДС,
		|	СУММА(ДД.Сумма) КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ЕСТЬNULL(ПоступлениеТоваров.ЗакупкаПодДеятельность, ПоступлениеУслуг.ЗакупкаПодДеятельность) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ДД.Подразделение КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ДД.Статья КАК СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ИзДокумента) КАК ВариантРаздельногоУчетаНДС,
		|	ДД.Аналитика КАК АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
		|	ДД.Период КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	РегистрНакопления.ПрочиеАктивыПассивы КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ПоступлениеТоваров
		|		ПО ПоступлениеТоваров.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеУслугПрочихАктивов КАК ПоступлениеУслуг
		|		ПО ПоступлениеУслуг.Ссылка = ДД.Регистратор
		|ГДЕ
		|	ДД.Период <= &ОкончаниеПериода
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ДД.ВидИсточника = ЗНАЧЕНИЕ(Перечисление.ВидыИсточниковДвижений.ПустаяСсылка)
		|	И (ДД.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг 
		|		ИЛИ ДД.Регистратор ССЫЛКА Документ.ПриобретениеУслугПрочихАктивов)
		|	И (ДД.Организация, ДД.Регистратор) В (
		|		ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			КлючиРасходов.Организация, ДД.Регистратор
		|		ИЗ
		|			АналитикиРасходовВсе КАК КлючиРасходов
		|		ГДЕ 
		|			КлючиРасходов.АналитикаРасходов = ДД.Регистратор
		|			И КлючиРасходов.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.НДСНалоговогоАгента)
		|		)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ДД.Статья,
		|	ДД.Аналитика,
		|	ДД.НаправлениеДеятельности,
		|	ЕСТЬNULL(ПоступлениеТоваров.ЗакупкаПодДеятельность, ПоступлениеУслуг.ЗакупкаПодДеятельность)
		|";
КонецФункции

#КонецОбласти


#Область РасчетПриходыРасходовСебестоимость

// В партионном учете версии 2.2 - этап 11, РаспределениеДопРасходовМеждуПартиямиИТоварами.

Функция ДанныеДляПартийПрочихСебестоимость(НачалоПериода, ОкончаниеПериода, МассивОрганизаций)
	МВТ = Новый МенеджерВременныхТаблиц;
	// Распределяем только по собственным запасам, исключаем комиссионные и давальческие запасы.
	СобственныеТипыЗапасов = РасчетСебестоимостиПрикладныеАлгоритмы.СобственныеТипыЗапасов();
	
	// Определяем базу распределения прочих расходов на заказы поставщикам и заказы перемещений,
	// т.е. распределяем выполненные строки заказов на поступления партий - указываем заказ в партии.
	ТекстСортировкаЗаказов = "
		|УПОРЯДОЧИТЬ ПО
		|	Регистратор, Номенклатура, Характеристика, Склад, Приоритет
		|";
	ТекстРасчетаЗаказов = 
		ТекстТаможенныеДекларацииИПоступления() + ";" + // вт Декларации
		ТекстАналитикиРасходовВсе() + ";" + // вт АналитикиРасходовВсе
		ТекстЗаказыИзАналитикРасходов() + ";" + // вт ЗаказыИзАналитикРасходов, использует АналитикиРасходовВсе
		ТекстЗаказыИзАналитикРасходовСводно() + ";" + // вт ЗаказыИзАналитикРасходовСводно, использует ЗаказыИзАналитикРасходов 
		ТекстПоступившиеЗаказы() + // использует ЗаказыИзАналитикРасходов
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстПриходыПоступленийПоЗаказамСебестоимости() + // использует ЗаказыИзАналитикРасходов
		ТекстСортировкаЗаказов;
	ЗапросРасчетаЗаказов = Новый Запрос(ТекстРасчетаЗаказов);
	ЗапросРасчетаЗаказов.МенеджерВременныхТаблиц = МВТ;
	ЗапросРасчетаЗаказов.УстановитьПараметр("НачалоПериода", НачалоПериода);
	ЗапросРасчетаЗаказов.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	ЗапросРасчетаЗаказов.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);	
	ЗапросРасчетаЗаказов.УстановитьПараметр("УчитыватьНазначение",
		РасчетСебестоимостиПовтИсп.СебестоимостьТоваровПоНазначениям(НачалоПериода));
	
	ДанныеРасчетаЗаказов = ЗапросРасчетаЗаказов.Выполнить().Выгрузить();
	РасчетныеПартииПоЗаказам = Новый ТаблицаЗначений;
	ПоляРасчетаЗаказов = СкопироватьКолонки(ДанныеРасчетаЗаказов.Колонки, РасчетныеПартииПоЗаказам.Колонки);
	КонтекстДвижений = ОписаниеДвижений(
		"ЗаказыПоставщикам", "",
		ПоляРасчетаЗаказов,
		"Номенклатура, Регистратор, Характеристика, Склад", "Количество, Стоимость",
		"Количество", "Количество", "Заказ", "Период");
	РаспределитьПриходыНаРасходы(КонтекстДвижений, ДанныеРасчетаЗаказов, РасчетныеПартииПоЗаказам);
	
	// Получаем данные для расчета списания партий прочих расходов и прихода партий расходов на с/с товаров.
	ТекстСортировка = "
		|УПОРЯДОЧИТЬ ПО
		|	Организация, Приоритет, ВидДвижения, Период, Регистратор";
	ИсходныйТекстЗапроса =
		ТекстПартииПоЗаказам() + ";" + // вт ПартииПоЗаказам, использует &РасчетныеПартииПоЗаказам
		ТекстРегистраторыПрочих() + ";" + // вт РегистраторыПрочих
		ТекстОписаниеПартийПрочих() +
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстОстаткиПартийПрочих() + // использует АналитикиРасходовВсе
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстПриходыПартийПрочих() +
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстДополненияПартийПрочих() + // использует РегистраторыПрочих
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисОстаткиСебестоимость() + // использует АналитикиРасходовВсе
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисПриходыТоваровСебестоимость() + // использует АналитикиРасходовВсе
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисПриходыИнтеркампаниСебестоимость() + // использует АналитикиРасходовВсе
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисПриходыИмпортСебестоимость() + // использует АналитикиРасходовВсе
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисПриходыРаботСебестоимость() + // использует АналитикиРасходовВсе
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисПеремещенияСебестоимость() + // использует АналитикиРасходовВсе
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисПрошлыеПриходыТоваровСебестоимость() + // использует АналитикиРасходовВсе
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисЗаказы() + // использует ПартииПоЗаказам, ЗаказыИзАналитикРасходов
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисУслуги() + // использует АналитикиРасходовВсе
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстБазисПрочиеАктивыПассивы() + // использует АналитикиРасходовВсе
		ТекстСортировка;
	ИсходныйТекстЗапроса = СтрЗаменить(ИсходныйТекстЗапроса, "&Вес", 
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки("СпрНоменклатура.ЕдиницаИзмерения", "СпрНоменклатура"));
	ИсходныйТекстЗапроса = СтрЗаменить(ИсходныйТекстЗапроса, "&Объем", 
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаОбъемУпаковки("СпрНоменклатура.ЕдиницаИзмерения", "СпрНоменклатура"));
	ИсходныйЗапрос = Новый Запрос(ИсходныйТекстЗапроса);
	ИсходныйЗапрос.МенеджерВременныхТаблиц = МВТ;
	ИсходныйЗапрос.УстановитьПараметр("РасчетныеПартииПоЗаказам", РасчетныеПартииПоЗаказам);
	ИсходныйЗапрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	ИсходныйЗапрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	ИсходныйЗапрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);	
	ИсходныйЗапрос.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоНазначениям",
		РасчетСебестоимостиПовтИсп.СебестоимостьТоваровПоНазначениям(НачалоПериода));
	ИсходныйЗапрос.УстановитьПараметр("СобственныеТипыЗапасов", СобственныеТипыЗапасов);
	ИсходныйЗапрос.УстановитьПараметр("ОрганизацииСФИФОСкользящая", 
		НастройкиНалоговУчетныхПолитик.ОрганизацииСЗаданнымПараметромПолитики(
			"УчетнаяПолитикаФинансовогоУчета",
			"МетодОценкиСтоимостиТоваров",
			Перечисления.МетодыОценкиСтоимостиТоваров.ФИФОСкользящаяОценка,
			МассивОрганизаций,
			НачалоПериода));
	ИсходныйЗапрос.УстановитьПараметр("ТипДокументаИмпорта", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ТаможеннаяДекларацияИмпорт"));
	
	ДанныеДляРасчета = ИсходныйЗапрос.Выполнить().Выгрузить();
	
	Возврат ДанныеДляРасчета;
	
КонецФункции

Функция РасчетныеПриходыПартийРасходов(ДанныеДляРасчета, Регистраторы, УзлыДвижений = Неопределено, Тест = Ложь) // ДанныеДляРасчета будут изменены
	// кэшируем получение пустых ссылок - на множественных итерациях есть выигрыш
	ПустыеСсылки = Новый Соответствие;
	ПравилаРаспределения = Новый Массив(4);
	ПравилаРаспределения[0] = Перечисления.ТипыБазыРаспределенияРасходов.КоличествоТоваров;
	ПравилаРаспределения[1] = Перечисления.ТипыБазыРаспределенияРасходов.СебестоимостьТоваров;
	ПравилаРаспределения[2] = Перечисления.ТипыБазыРаспределенияРасходов.ВесТоваров;
	ПравилаРаспределения[3] = Перечисления.ТипыБазыРаспределенияРасходов.ОбъемТоваров;
	// Шаг 1: Строим сопоставление приходов с базисом и расходов, по которым базис рассчитан.
	ДанныеДляРасчета.Сортировать("Организация, НалогообложениеНДС, Приоритет, Период, Регистратор, АналитикаУчетаНоменклатуры", Новый СравнениеЗначений);
	УзлыДвижений = Новый Соответствие;
	Ключи = Новый Массив(6); // {Организация, НалогообложениеНДС, ПравилоРаспределения, АналитикаРасходов, ДокументПоступленияРасходов, НомерСтроки}
	// формирование упорядоченных массивов строк таблицы ДанныеДляРасчета
	Для Каждого Движение Из ДанныеДляРасчета Цикл
		Ключи[0] = Движение.Организация;
		Ключи[1] = Движение.НалогообложениеНДС;
		Ключи[2] = Движение.ПравилоРаспределения;
		Ключи[3] = Неопределено;
		Ключи[4] = Неопределено;
		Ключи[5] = Неопределено;
		// накапливаем приход партий прочих расходов для дальнейшего сопоставления
		Если ЗначениеЗаполнено(Движение.СтатьяРасходов) Тогда
			Ключи[3] = Движение.АналитикаРаспределения;
			Ключи[4] = Движение.ДокументПоступленияРасходов;
			Ключи[5] = ДанныеДляРасчета.Индекс(Движение);
			Движения = УзелИндекса(УзлыДвижений, Ключи);
			Если Движения = Неопределено Тогда
				Движения = ДобавитьУзелИндекса(УзлыДвижений, Ключи, Новый Массив);
			КонецЕсли;
			Движения.Добавить(Движение);
			Продолжить;
		КонецЕсли;
		// непосредственно вставка строки расхода в сопоставления по каждой аналитике
		Для Каждого Правило Из ПравилаРаспределения Цикл
			Ключи[2] = Правило;
			Аналитики = УзелИндекса(УзлыДвижений, Ключи);
			Если Аналитики = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			// Аналитики содержат соответствие, в ключах которого значение аналитики расхода,
			// в значениях - соответствие между документом поступления расхода и движениями относящимися к этому расходу.
			Для Каждого Значение Из АналитикиОбсчетаПартийПрочих(Движение, ПустыеСсылки) Цикл
				ПоступленияРасходов = Аналитики[Значение];
				Если ПоступленияРасходов = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				Для Каждого ПоступлениеРасхода Из ПоступленияРасходов Цикл
					Для Каждого НомерСтроки Из  ПоступлениеРасхода.Значение Цикл
						НомерСтроки.Значение.Добавить(Движение);
					КонецЦикла
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	// Шаг 2: создаем буфер накопления рассчитанных партий
	РасчетныеПартии = Новый ТаблицаЗначений;
	ПоляРасчета = СкопироватьКолонки(ДанныеДляРасчета.Колонки, РасчетныеПартии.Колонки);
	// Шаг 3: распределяем каждый массив строк в аналитиках
	КонтекстДвижений = ОписаниеДвижений(
		"ПартииРасходовНаСебестоимостьТоваровПриход",
		"ПартииРасходовНаСебестоимостьТоваров",
		ПоляРасчета,
		"Организация",
		"Базис, Количество, Вес, Объем, Стоимость, СтоимостьБезНДС, СтоимостьРегл, НДСРегл, ПостояннаяРазница, ВременнаяРазница",
		"Базис", "<БАЗИС_РАСХОДА>", "ДокументРеализации", "Период");
	Для Каждого Организации Из УзлыДвижений Цикл
		Для Каждого Налогообложения Из Организации.Значение Цикл
			Для Каждого Правила Из Налогообложения.Значение Цикл
				Если Правила.Ключ = ПравилаРаспределения[0] Тогда
					КонтекстДвижений.БазисРасхода = "Количество";
				ИначеЕсли Правила.Ключ = ПравилаРаспределения[2] Тогда
					КонтекстДвижений.БазисРасхода = "Вес";
				ИначеЕсли Правила.Ключ = ПравилаРаспределения[3] Тогда
					КонтекстДвижений.БазисРасхода = "Объем";	
				Иначе
					КонтекстДвижений.БазисРасхода = "Стоимость";
				КонецЕсли;
				Для Каждого Аналитики Из Правила.Значение Цикл
					Для Каждого ПоступлениеРасхода Из Аналитики.Значение Цикл
						Для Каждого НомерСтроки Из  ПоступлениеРасхода.Значение Цикл
							РаспределитьПриходыНаРасходы(КонтекстДвижений, НомерСтроки.Значение, РасчетныеПартии);
						КонецЦикла;
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	// Шаг 4: Дополнение расчетных партий данными прямого прихода в партии расходов на с/с.
	ИндексРегистраторов = Новый Соответствие;
	Регистраторы = Новый Массив;
	ВсегоРегистраторов = 0;
	НачалоПериода = Дата(1,1,1);
	Для Каждого РасчетнаяПартия Из РасчетныеПартии Цикл
		Ключ = РасчетнаяПартия.Регистратор;
		Если ИндексРегистраторов[Ключ] = Неопределено Тогда
			Регистраторы.Добавить(Ключ);
			ИндексРегистраторов.Вставить(Ключ, ВсегоРегистраторов);
			ВсегоРегистраторов = ВсегоРегистраторов + 1;
			Если НачалоПериода = Дата(1,1,1) Тогда
				НачалоПериода = НачалоМесяца(Движение.Период);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ЗапросИсходныхПартий = Новый Запрос(ТекстИсходныеПриходыПартийРасходов());
	ЗапросИсходныхПартий.УстановитьПараметр("Регистраторы", Регистраторы);
	ЗапросИсходныхПартий.УстановитьПараметр("НачалоПериода", НачалоПериода);
	ВыборкаИсходныхПартий = ЗапросИсходныхПартий.Выполнить().Выбрать();
	Пока ВыборкаИсходныхПартий.Следующий() Цикл
		РасчетнаяПартия = РасчетныеПартии.Добавить();
		ЗаполнитьЗначенияСвойств(РасчетнаяПартия, ВыборкаИсходныхПартий);
	КонецЦикла;
	// Шаг 5: Сортировка партий для дальнейшей записи
	РасчетныеПартии.Сортировать("Регистратор, ВидДвижения, Период, АналитикаУчетаНоменклатуры", Новый СравнениеЗначений);
	Возврат РасчетныеПартии;
КонецФункции

Функция РасчетныеПриходыСебестоимости(НачалоПериода, РасчетныеПриходыПартийРасходов, ИндексРегистраторов, АналитикиПартнеров = Неопределено, Тест = Ложь)
	// Шаг 1: получаем аналитики учета по партнерам и регистраторы расчетных приходов
	АналитикиПартнеров = Новый Соответствие;
	Поступления = Новый Массив;
	
	Если ИндексРегистраторов = Неопределено Тогда
		ИндексРегистраторов = Новый Соответствие;
		Регистраторы = Новый Массив;
		ВсегоРегистраторов = 0;
	Иначе
		Регистраторы = ОбщегоНазначенияУТКлиентСервер.ПреобразоватьСоответствиеИлиСтруктуруВМассив(ИндексРегистраторов);
		ВсегоРегистраторов = Регистраторы.Количество();
	КонецЕсли;
	
	АналитикиБезНазначения = Новый Соответствие;
	АналитикиНоменклатуры = Новый Массив;
	РасчетныеПриходыПартийРасходов.Сортировать("Регистратор", Новый СравнениеЗначений);
	Для Каждого Движение Из РасчетныеПриходыПартийРасходов Цикл
		Если Движение.ТипЗаписи = "Дополнение" Тогда
			Продолжить;
		КонецЕсли;
		// Аналитики учета по партнерам для дальнейшего заполнения по документам поступлений.
		Если АналитикиПартнеров[Движение.ДокументПоступления] = Неопределено Тогда
			Поступления.Добавить(Движение.ДокументПоступления);
			АналитикиПартнеров.Вставить(Движение.ДокументПоступления, Неопределено);
		КонецЕсли;
		// Регистраторы для дальнейшей выборки онлайн-движений из регистра себестоимости товаров.
		Если ИндексРегистраторов[Движение.Регистратор] = Неопределено Тогда
			Регистраторы.Добавить(Движение.Регистратор);
			ИндексРегистраторов.Вставить(Движение.Регистратор, ВсегоРегистраторов);
			ВсегоРегистраторов = ВсегоРегистраторов + 1;
		КонецЕсли;
		
		Если АналитикиБезНазначения[Движение.АналитикаУчетаНоменклатуры] = Неопределено Тогда
			АналитикиБезНазначения.Вставить(Движение.АналитикаУчетаНоменклатуры, Неопределено);
			АналитикиНоменклатуры.Добавить(Движение.АналитикаУчетаНоменклатуры);
		КонецЕсли;
	КонецЦикла;
	// заполняем аналитики учета по партнерам по документам поступлений товаров
	ЗапросАналитик = Новый Запрос(ТекстАналитикиПартнеров()); // использует &Поступления
	ЗапросАналитик.УстановитьПараметр("Поступления", Поступления);
	ВыборкаАналитик = ЗапросАналитик.Выполнить().Выбрать();
	Пока ВыборкаАналитик.Следующий() Цикл
		АналитикиПартнеров[ВыборкаАналитик.ДокументПоступления] = ВыборкаАналитик.АналитикаУчетаПоПартнерам;
	КонецЦикла;
	Поступления.Очистить();
	
	// Заполняем аналитики учета номенклатуры без назначений.
	ЗапросАналитикБезНазначений = Новый Запрос(ТекстАналитикиБезНазначений()); // использует &АналитикиНоменклатуры
	ЗапросАналитикБезНазначений.УстановитьПараметр("АналитикиНоменклатуры", АналитикиНоменклатуры);
	ВыборкаАналитикБезНазначений = ЗапросАналитикБезНазначений.Выполнить().Выбрать();
	Пока ВыборкаАналитикБезНазначений.Следующий() Цикл
		АналитикиБезНазначения[ВыборкаАналитикБезНазначений.Аналитика] = ВыборкаАналитикБезНазначений.АналитикаБезНазначения;
	КонецЦикла;
	АналитикиНоменклатуры.Очистить();
	
	// Шаг 2: создаем буфер накопления рассчитанных приходов в себестоимость
	Запрос = Новый Запрос(ТекстОписаниеСебестоимость());
	РасчетныеПартии = Запрос.Выполнить().Выгрузить();
	
	ТекстСортировка = "УПОРЯДОЧИТЬ ПО Регистратор";
	ЗапросОписания = Новый Запрос(
		ТекстОписаниеСебестоимость() +
		"ОБЪЕДИНИТЬ ВСЕ" + ТекстИсходныеДвиженияСебестоимость() +
		ТекстСортировка
		); // использует &Регистраторы
	ЗапросОписания.УстановитьПараметр("Регистраторы", Регистраторы);
	ЗапросОписания.УстановитьПараметр("НачалоПериода", НачалоПериода);
	ЗапросОписания.УстановитьПараметр("ОкончаниеПериода", КонецМесяца(НачалоПериода));
	Выборка = ЗапросОписания.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РасчетнаяПартия = РасчетныеПартии.Добавить();
		ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Выборка);
		Если Не Тест И РасчетныеПартии.Количество() > 1000 Тогда
			ЗаписатьРасчетныеПартии(РегистрыНакопления.СебестоимостьТоваров, РасчетныеПартии, ИндексРегистраторов);
			РасчетныеПартии.Очистить();
		КонецЕсли;
	КонецЦикла;
	Регистраторы.Очистить();
	// кэшируем константы
	СебестоимостьПоНазначениям = РасчетСебестоимостиПовтИсп.СебестоимостьТоваровПоНазначениям(НачалоПериода);
	// Шаг 3: Заполняем буфер накопления
	Для Каждого Движение Из РасчетныеПриходыПартийРасходов Цикл
		Если Не ЗначениеЗаполнено(Движение.СтатьяРасходов)
		 ИЛИ ЗначениеЗаполнено(Движение.ВариантРаздельногоУчетаНДС)
		 ИЛИ Движение.ТипЗаписи = "Дополнение" Тогда
			Продолжить;
		КонецЕсли;
		РасчетнаяПартия = РасчетныеПартии.Добавить();
		ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Движение,
			"Период, Регистратор, ВидЗапасов, Организация, РазделУчета, Подразделение, АналитикаРасходов");
		РасчетнаяПартия.ВидДвижения = ВидДвиженияНакопления.Приход;
		Если СебестоимостьПоНазначениям Тогда
			РасчетнаяПартия.АналитикаУчетаНоменклатуры = Движение.АналитикаУчетаНоменклатуры;
		Иначе
			АналитикаНоменклатурыБезНазначения = АналитикиБезНазначения[Движение.АналитикаУчетаНоменклатуры];
			Если ЗначениеЗаполнено(АналитикаНоменклатурыБезНазначения) Тогда
				РасчетнаяПартия.АналитикаУчетаНоменклатуры = АналитикаНоменклатурыБезНазначения;
			Иначе
				РасчетнаяПартия.АналитикаУчетаНоменклатуры = Движение.АналитикаУчетаНоменклатуры;
			КонецЕсли;
		КонецЕсли;
		РасчетнаяПартия.ДопРасходы = Движение.Стоимость;
		РасчетнаяПартия.ДопРасходыБезНДС = Движение.СтоимостьБезНДС;
		РасчетнаяПартия.СтоимостьРегл = Движение.СтоимостьРегл;
		Если Движение.НалогообложениеПартии = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД
		 ИЛИ Движение.НалогообложениеПартии = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС Тогда
			РасчетнаяПартия.СтоимостьРегл = РасчетнаяПартия.СтоимостьРегл + Движение.НДСРегл;
		КонецЕсли;
		РасчетнаяПартия.АналитикаУчетаПоПартнерам = АналитикиПартнеров[Движение.ДокументПоступления];
		РасчетнаяПартия.СтатьяРасходовСписания = Движение.СтатьяРасходов;
		РасчетнаяПартия.КорНаправлениеДеятельности = Движение.НаправлениеДеятельности;
		РасчетнаяПартия.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика;
		
		Если Не Тест И РасчетныеПартии.Количество() > 1000 Тогда
			ЗаписатьРасчетныеПартии(РегистрыНакопления.СебестоимостьТоваров, РасчетныеПартии, ИндексРегистраторов);
			РасчетныеПартии.Очистить();
		КонецЕсли;
	КонецЦикла;
	АналитикиПартнеров.Очистить();
	Если Не Тест Тогда
		ЗаписатьРасчетныеПартии(РегистрыНакопления.СебестоимостьТоваров, РасчетныеПартии, ИндексРегистраторов);
		ОчиститьРегистрНаСервере(ИндексРегистраторов, "СебестоимостьТоваров");
		РасчетныеПартии.Очистить();
	КонецЕсли;
	// Шаг 5: Сортировка партий для дальнейшей записи
	РасчетныеПартии.Сортировать("Регистратор, ВидДвижения, Период", Новый СравнениеЗначений);
	Возврат РасчетныеПартии;
КонецФункции

Функция РасчетныеДвиженияПрочихРасходов(НачалоПериода, РасчетныеПриходыПартийРасходов, РасчетныеПартииПрочих, Тест = Ложь)
	
	// Шаг 1: формируем движения по регистру ПрочиеРасходы
	ТекстСортировка = "УПОРЯДОЧИТЬ ПО Регистратор";
	Запрос = Новый Запрос(
		ТекстОписаниеДанныхДляПрочихРасходов()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстИсходныеДвиженияПрочиеРасходы()
		+ ТекстСортировка); // использует &Регистраторы
	Регистраторы = РасчетныеПартииПрочих.ВыгрузитьКолонку("Регистратор"); 
	Запрос.УстановитьПараметр("Регистраторы", Регистраторы);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", КонецМесяца(НачалоПериода));
	РасчетныеПартии = Запрос.Выполнить().Выгрузить();
	
	ИспользоватьУчетПрочихДоходовРасходовРегл = ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходовРегл");
	
	Для Каждого Движение Из РасчетныеПриходыПартийРасходов Цикл
		Если Не ЗначениеЗаполнено(Движение.СтатьяРасходов)
		 ИЛИ НЕ ЗначениеЗаполнено(Движение.ВариантРаздельногоУчетаНДС)
		 ИЛИ Движение.ВариантРаздельногоУчетаНДС = Перечисления.ВариантыРаздельногоУчетаНДС.Распределение
		 ИЛИ Движение.ТипЗаписи = "Дополнение" Тогда
			Продолжить;
		КонецЕсли;
		РасчетнаяПартия = РасчетныеПартии.Добавить();
		ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Движение,
			"Период, Регистратор, Организация, НаправлениеДеятельности");
		РасчетнаяПартия.ВидДвижения = ВидДвиженияНакопления.Приход;
		
		РасчетнаяПартия.Подразделение 			= Движение.ПодразделениеРасходов;
		РасчетнаяПартия.СтатьяРасходов 			= Движение.СтатьяОтраженияРасходов;
		РасчетнаяПартия.АналитикаРасходов 		= Движение.АналитикаОтраженияРасходов;
		
		РасчетнаяПартия.ДокументДвижения 		= Движение.Регистратор;
		
		Если Движение.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД
		 ИЛИ Движение.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС Тогда
			РасчетнаяПартия.СуммаРегл = Движение.НДСРегл;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Движение Из РасчетныеПартииПрочих Цикл
		Если Движение.ВидДвижения = ВидДвиженияНакопления.Расход Тогда
			РасчетнаяПартия = РасчетныеПартии.Добавить();
			ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Движение,
				"ВидДвижения, Период, Регистратор, Организация, Подразделение, НаправлениеДеятельности,
				|СтатьяРасходов, АналитикаРасходов, ХозяйственнаяОперация");
			РасчетнаяПартия.ДокументДвижения = Движение.Регистратор;
			
			РасчетнаяПартия.Сумма       = Движение.Стоимость;
			РасчетнаяПартия.СуммаБезНДС = Движение.СтоимостьБезНДС;
			Если ИспользоватьУчетПрочихДоходовРасходовРегл Тогда
				РасчетнаяПартия.СуммаРегл   = Движение.СтоимостьРегл;
				Если Движение.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД
				 ИЛИ Движение.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС Тогда
					РасчетнаяПартия.СуммаРегл = РасчетнаяПартия.СуммаРегл + Движение.НДСРегл;
				КонецЕсли;
				РасчетнаяПартия.ПостояннаяРазница = Движение.ПостояннаяРазница;
				РасчетнаяПартия.ВременнаяРазница  = Движение.ВременнаяРазница;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Шаг 2: Сортировка партий для дальнейшей записи
	РасчетныеПартии.Сортировать("Регистратор, ВидДвижения, Период", Новый СравнениеЗначений);
	Возврат РасчетныеПартии;
КонецФункции

Функция ТекстИсходныеДвиженияПрочиеРасходы()
	Возврат "
	|ВЫБРАТЬ
	|	90 КАК Приоритет,
	|	""Дополнение"" КАК ТипЗаписи,
	|	ИСТИНА КАК РасчетЗавершен,
	|	НЕОПРЕДЕЛЕНО КАК РазделУчета,
	|	Т.ВидДвижения КАК ВидДвижения,
	|	Т.Период,
	|	Т.Регистратор КАК Регистратор,
	|	Т.Организация,
	|	Т.Подразделение,
	|	Т.НаправлениеДеятельности,
	|	Т.СтатьяРасходов,
	|	Т.АналитикаРасходов,
	|	Т.ХозяйственнаяОперация,
	|	Т.ДокументДвижения,
	|	Т.КорСтатьяРасходов,
	|	Т.КорАналитикаРасходов,
	|	Т.Подразделение КАК КорПодразделение,
	|	Т.ГруппаПродукции КАК ГруппаПродукции,
	|	0. КАК Знаменатель,
	|	0. КАК ДоляСтоимости,
	|	Т.Сумма КАК Сумма,
	|	Т.СуммаБезНДС КАК СуммаБезНДС,
	|	Т.СуммаРегл КАК СуммаРегл,
	|	Т.ПостояннаяРазница КАК ПостояннаяРазница,
	|	Т.ВременнаяРазница КАК ВременнаяРазница
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И Т.Регистратор В (&Регистраторы)
	|	И Т.Активность
	|	И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|";
КонецФункции

Функция РасчетныеПриходыПартийНДСКРаспределению(НачалоПериода, РасчетныеПриходыПартийРасходов)
	
	Запрос = Новый Запрос(ТекстОписаниеДанныхДляПартийНДСКРаспределению());
	РасчетныеПартии = Запрос.Выполнить().Выгрузить();
	
	// Шаг 1: формируем движения по регистру ПрочиеРасходы
	Для Каждого Движение Из РасчетныеПриходыПартийРасходов Цикл
		Если ЗначениеЗаполнено(Движение.СтатьяРасходов)
		 И ЗначениеЗаполнено(Движение.ВариантРаздельногоУчетаНДС)
		 И Движение.ВариантРаздельногоУчетаНДС = Перечисления.ВариантыРаздельногоУчетаНДС.Распределение Тогда
			РасчетнаяПартия = РасчетныеПартии.Добавить();
			ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Движение,
				"Период, Регистратор, Организация, ДокументПоступленияРасходов, НаправлениеДеятельности,
				|Поставщик, ВидЦенности, СтавкаНДС,
				|СтоимостьРегл, НДСРегл");
			РасчетнаяПартия.ВидДвижения = ВидДвиженияНакопления.Приход;
			
			РасчетнаяПартия.Подразделение 			= Движение.ПодразделениеРасходов;
			РасчетнаяПартия.СтатьяРасходов 			= Движение.СтатьяОтраженияРасходов;
			РасчетнаяПартия.АналитикаРасходов 		= Движение.АналитикаОтраженияРасходов;
			РасчетнаяПартия.ВидДеятельностиНДС 		= Движение.НалогообложениеНДС;
			
		ИначеЕсли Движение.НалогообложениеПартии = Перечисления.ТипыНалогообложенияНДС.ОпределяетсяРаспределением
		 И Движение.НДСРегл <> 0 Тогда
			РасчетнаяПартия = РасчетныеПартии.Добавить();
			ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Движение,
				"Период, Регистратор, Организация, ДокументПоступленияРасходов,
				|Поставщик, ВидЦенности, СтавкаНДС, НДСРегл");
			РасчетнаяПартия.ВидДвижения = ВидДвиженияНакопления.Приход;
			РасчетнаяПартия.ВидДеятельностиНДС = Движение.НалогообложениеПартии;
		КонецЕсли;
	КонецЦикла;
	
	// Шаг 2: Сортировка партий для дальнейшей записи
	РасчетныеПартии.Сортировать("Регистратор, Период", Новый СравнениеЗначений);
	Возврат РасчетныеПартии;
КонецФункции

Функция ТекстИсходныеПриходыПартийРасходов() // использует &Регистраторы
	Возврат "
		|ВЫБРАТЬ
		|	10 КАК Приоритет,
		|	""Дополнение"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.ДокументПоступленияРасходов,
		|	ДД.СтатьяРасходов,
		|	ДД.Количество,
		|	ДД.Стоимость,
		|	ДД.СтоимостьБезНДС,
		|	ДД.СтоимостьРегл,
		|	ДД.НДСРегл,
		|	ДД.ПостояннаяРазница,
		|	ДД.ВременнаяРазница,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.ПодразделениеРасходов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов
		|ИЗ
		|	РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров КАК ДД
		|ГДЕ
		|	ДД.Регистратор В (&Регистраторы)
		|	И ДД.ДокументПоступленияРасходов = ДД.Регистратор
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И (ДД.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|		ИЛИ ДД.Период < &НачалоПериода)
		|";
КонецФункции

Функция ТекстАналитикиПартнеров() // использует &Поступления
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор КАК Декларация,
		|	ДД.ДокументПоступления КАК Поступление
		|ПОМЕСТИТЬ
		|	Импорт
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|ГДЕ
		|	ДД.Регистратор ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
		|	И ДД.Регистратор В (&Поступления)
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|;
		|ВЫБРАТЬ
		|	ДД.Регистратор КАК ДокументПоступления,
		|	МИНИМУМ(ДД.АналитикаУчетаПоПартнерам) КАК АналитикаУчетаПоПартнерам
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК ДД
		|ГДЕ
		|	ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ДД.Количество > 0
		|	И ДД.АналитикаУчетаПоПартнерам <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
		|	И ДД.Регистратор В (&Поступления)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Регистратор
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	Импорт.Декларация КАК ДокументПоступления,
		|	МИНИМУМ(ДД.АналитикаУчетаПоПартнерам) КАК АналитикаУчетаПоПартнерам
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Импорт КАК Импорт
		|		ПО Импорт.Поступление = ДД.Регистратор
		|ГДЕ
		|	ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ДД.Количество > 0
		|	И ДД.АналитикаУчетаПоПартнерам <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
		|СГРУППИРОВАТЬ ПО
		|	Импорт.Декларация
		|";
КонецФункции

Функция ТекстАналитикиБезНазначений() // использует &АналитикиНоменклатуры
	Возврат "
		|ВЫБРАТЬ
		|	Аналитики.Ссылка КАК Аналитика,
		|	АналитикиБезНазначения.КлючАналитики КАК АналитикаБезНазначения
		|ИЗ
		|	Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитики
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикиБезНазначения
		|		ПО АналитикиБезНазначения.Номенклатура = Аналитики.Номенклатура
		|		И АналитикиБезНазначения.Характеристика = Аналитики.Характеристика
		|		И АналитикиБезНазначения.Серия = Аналитики.Серия
		|		И АналитикиБезНазначения.МестоХранения = Аналитики.МестоХранения
		|		И АналитикиБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|ГДЕ
		|	Аналитики.Ссылка В (&АналитикиНоменклатуры)
		|";
КонецФункции

Функция ТекстОписаниеДанныхДляПрочихРасходов()
	Возврат "
	|ВЫБРАТЬ ПЕРВЫЕ 0
	|	0 КАК Приоритет,
	|	""ХХХХХХХХХХХХХХХХХХХХ"" КАК ТипЗаписи,
	|	ЛОЖЬ КАК РасчетЗавершен,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК Период,
	|	Т.Регистратор КАК Регистратор,
	|	Т.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
	|	Т.АналитикаРасходов КАК АналитикаРасходов,
	|	Т.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Т.Регистратор КАК ДокументДвижения,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК КорСтатьяРасходов,
	|	Т.АналитикаРасходов КАК КорАналитикаРасходов,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК КорПодразделение,
	|	ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) КАК ГруппаПродукции,
	|	0 КАК Знаменатель,
	|	0 КАК ДоляСтоимости,
	|	0 КАК Сумма,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК Т
	|";
КонецФункции

Функция ТекстОписаниеСебестоимость()
	Возврат "
		|ВЫБРАТЬ ПЕРВЫЕ 0
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК Период,
		|	НЕОПРЕДЕЛЕНО КАК Регистратор,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
		|	0 КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК ДопРасходы,
		|	0 КАК ДопРасходыБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК КорРазделУчета,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК КорОрганизация,
		|	0 КАК КорСтоимость,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка) КАК АналитикаУчетаПоПартнерам,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказКлиента,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка) КАК СтатьяДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ПериодПродажи,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка) КАК СтатьяАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументДвижения,
		|	""ХХ"" КАК ИдентификаторСтроки,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК КорНаправлениеДеятельности,
		|	ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) КАК ГруппаПродукции
		|//ВоВременнуюТаблицу ПОМЕСТИТЬ ДанныеПредварительные
		|";
КонецФункции

Функция ТекстИсходныеДвиженияСебестоимость() // использует &Регистраторы
	Возврат "
		|ВЫБРАТЬ
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.РазделУчета,
		|	ДД.ВидЗапасов,
		|	ДД.Организация,
		|	ДД.Количество,
		|	ДД.Стоимость,
		|	ДД.СтоимостьБезНДС,
		|	ДД.ДопРасходы,
		|	ДД.ДопРасходыБезНДС,
		|	ДД.СтоимостьРегл,
		|	ДД.ПостояннаяРазница,
		|	ДД.ВременнаяРазница,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ДД.КорРазделУчета,
		|	ДД.КорВидЗапасов,
		|	ДД.КорОрганизация,
		|	ДД.КорСтоимость,
		|	ДД.АналитикаУчетаПоПартнерам,
		|	ДД.ЗаказКлиента,
		|	ДД.Подразделение,
		|	ДД.АналитикаРасходов,
		|	ДД.СтатьяРасходовСписания,
		|	ДД.СтатьяДоходов,
		|	ДД.АналитикаДоходов,
		|	ДД.ПериодПродажи,
		|	ДД.СтатьяАктивовПассивов,
		|	ДД.АналитикаАктивовПассивов,
		|	ДД.ДокументДвижения,
		|	ДД.ИдентификаторСтроки,
		|	ДД.КорНаправлениеДеятельности,
		|	ДД.ГруппаПродукции
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК ДД
		|ГДЕ
		|	ДД.Регистратор В (&Регистраторы)
		|	И НЕ ДД.РасчетСебестоимости
		|	И (ДД.ДопРасходы = 0 И ДД.Количество <> 0
		|		ИЛИ ДД.ДопРасходы <> 0 И ДД.Период < &НачалоПериода
		|		ИЛИ ДД.ДопРасходы <> 0 И ДД.Период > &ОкончаниеПериода
		|		ИЛИ ДД.СтатьяРасходовСписания = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка))
		|";
КонецФункции

Функция ТекстПриходыПоступленийПоЗаказамСебестоимости() // использует ЗаказыИзАналитикРасходов
	Возврат "
		|ВЫБРАТЬ
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	Заказы.ДатаПоступления КАК Период,
		|	ДД.Регистратор КАК Регистратор,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	НЕОПРЕДЕЛЕНО КАК Заказ,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.АналитикаУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
		|	ДД.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	ДД.АналитикаУчетаНоменклатуры.МестоХранения  КАК Склад,
		|	ДД.АналитикаУчетаНоменклатуры.Серия          КАК Серия,
		|	ДД.АналитикаУчетаНоменклатуры.Назначение     КАК Назначение,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка),
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(ДД.Стоимость) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	0,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗаказыИзАналитикРасходовСводно КАК Заказы
		|		ПО Заказы.Поступление = ДД.Регистратор
		|		И Заказы.Номенклатура = ДД.АналитикаУчетаНоменклатуры.Номенклатура
		|		И Заказы.Характеристика = ДД.АналитикаУчетаНоменклатуры.Характеристика
		|		И Заказы.Склад = ДД.АналитикаУчетаНоменклатуры.МестоХранения
		|		И Заказы.Назначение = ДД.АналитикаУчетаНоменклатуры.Назначение
		|ГДЕ
		|	ДД.Период <= &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|СГРУППИРОВАТЬ ПО
		|	Заказы.ДатаПоступления,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.АналитикаУчетаНоменклатуры.Номенклатура,
		|	ДД.АналитикаУчетаНоменклатуры.Характеристика,
		|	ДД.АналитикаУчетаНоменклатуры.МестоХранения,
		|	ДД.АналитикаУчетаНоменклатуры.Серия,
		|	ДД.АналитикаУчетаНоменклатуры.Назначение,
		|	ДД.ВидЗапасов
		|";
КонецФункции

Функция ТекстБазисОстаткиСебестоимость() // использует АналитикиРасходовВсе
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстБазисОстаткиСебестоимость"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&НачалоПериода КАК Период,
		|	ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка) КАК Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	АналитикиТоваров.МестоХранения КАК Склад,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК Поставщик,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	ДД.КоличествоОстаток КАК Количество,
		|	&Вес * ДД.КоличествоОстаток КАК Вес,
		|	&Объем * ДД.КоличествоОстаток КАК Объем,
		|	ДД.СтоимостьОстаток КАК Стоимость,
		|	ДД.СтоимостьБезНДСОстаток КАК СтоимостьБезНДС,
		|	ДД.СтоимостьРеглОстаток КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	ДД.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
		|	ДД.ВременнаяРазницаОстаток КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ДД.РазделУчета,
		|	&НачалоПериода КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)
		|		И РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
		|		И РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
		|	) КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикиТоваров
		|		ПО АналитикиТоваров.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = АналитикиТоваров.Номенклатура
		|ГДЕ
		|	ДД.СтоимостьОстаток > 0
		|	И (ДД.Организация,
		|		АналитикиТоваров.Номенклатура,
		|		АналитикиТоваров.МестоХранения,
		|		ЕСТЬNULL(АналитикиТоваров.Назначение.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))) В
		|	(
		|		ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			КлючиРасходов.Организация,
		|			АналитикиТоваров.Номенклатура,
		|			АналитикиТоваров.МестоХранения,
		|			КлючиРасходов.НаправлениеДеятельности
		|		ИЗ
		|			АналитикиРасходовВсе КАК КлючиРасходов
		|		ГДЕ 
		|			КлючиРасходов.АналитикаРасходов В (
		|				АналитикиТоваров.Номенклатура,
		|				ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|				АналитикиТоваров.МестоХранения,
		|				ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|				)
		|	)
		|";
КонецФункции

Функция ТекстБазисПриходыТоваровСебестоимость() // использует АналитикиРасходовВсе
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстБазисПриходыТоваровСебестоимость"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	АналитикиТоваров.МестоХранения КАК Склад,
		|	ДД.Регистратор,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК Поставщик,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	СУММА(ДД.Количество) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК Количество,
		|	СУММА(&Вес * ДД.Количество) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК Вес,
		|	СУММА(&Объем * ДД.Количество) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК Объем,
		|	СУММА(ДД.Стоимость) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ДД.РазделУчета,
		|	ДД.Период КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикиТоваров
		|		ПО АналитикиТоваров.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = АналитикиТоваров.Номенклатура
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
		|		ПО АналитикиТоваров.МестоХранения = Склады.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизаций КАК Товары
		|		ПО Товары.Регистратор = ДД.Регистратор
		|		И Товары.АналитикаУчетаНоменклатуры.Номенклатура = АналитикиТоваров.Номенклатура
		|		И Товары.АналитикаУчетаНоменклатуры.Характеристика = АналитикиТоваров.Характеристика
		|		И Товары.АналитикаУчетаНоменклатуры.Серия = АналитикиТоваров.Серия
		|		И Товары.АналитикаУчетаНоменклатуры.МестоХранения = АналитикиТоваров.МестоХранения
		|		И Товары.Организация = ДД.Организация
		|		И Товары.ВидЗапасов = ДД.ВидЗапасов
		|		И Товары.Первичное = ИСТИНА
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И НЕ ДД.РазделУчета В (
		|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
		|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку))
		|	И (ДД.Организация,
		|		АналитикиТоваров.Номенклатура,
		|		АналитикиТоваров.МестоХранения,
		|		ЕСТЬNULL(АналитикиТоваров.Назначение.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|		ДД.Регистратор) В
		|	(
		|		ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			КлючиРасходов.Организация,
		|			АналитикиТоваров.Номенклатура,
		|			АналитикиТоваров.МестоХранения,
		|			КлючиРасходов.НаправлениеДеятельности,
		|			ДД.Регистратор
		|		ИЗ
		|			АналитикиРасходовВсе КАК КлючиРасходов
		|		ГДЕ 
		|			КлючиРасходов.АналитикаРасходов В (
		|				АналитикиТоваров.Номенклатура,
		|				ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|				АналитикиТоваров.МестоХранения,
		|				ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
		|				ДД.Регистратор)
		|			ИЛИ
		|			ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИПЗНАЧЕНИЯ(КлючиРасходов.АналитикаРасходов)
		|			И КлючиРасходов.АналитикаРасходов В (
		|				ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка),
		|				ЗНАЧЕНИЕ(Документ.СборкаТоваров.ПустаяСсылка),
		|				ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка))
		|	)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка,
		|	АналитикиТоваров.МестоХранения,
		|	ДД.Регистратор,
		|	ДД.ВидЗапасов,
		|	ДД.РазделУчета
		|";
КонецФункции

Функция ТекстБазисПриходыИнтеркампаниСебестоимость() // использует АналитикиРасходовВсе
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстБазисПриходыИнтеркампаниСебестоимость"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	АналитикиТоваров.МестоХранения КАК Склад,
		|	ДД.Регистратор,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК Поставщик,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	СУММА(ДД.Количество) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК Количество,
		|	СУММА(&Вес * ДД.Количество) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК Вес,
		|	СУММА(&Объем * ДД.Количество) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК Объем,
		|	СУММА(ДД.Стоимость) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ДД.РазделУчета,
		|	ДД.Период КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикиТоваров
		|		ПО АналитикиТоваров.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = АналитикиТоваров.Номенклатура
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизацийКПередаче КАК Товары
		|		ПО Товары.Регистратор = ДД.Регистратор
		|		И Товары.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|		И Товары.ВидЗапасовПродавца.Организация = ДД.Организация
		|		И Товары.ВидЗапасовПродавца = ДД.ВидЗапасов
		|		И Товары.Первичное = ИСТИНА
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И НЕ ДД.РазделУчета В (
		|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
		|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку))
		|	И (ДД.Организация,
		|		АналитикиТоваров.Номенклатура,
		|		АналитикиТоваров.МестоХранения,
		|		ЕСТЬNULL(АналитикиТоваров.Назначение.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|		ДД.Регистратор) В
		|	(
		|		ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			КлючиРасходов.Организация,
		|			АналитикиТоваров.Номенклатура,
		|			АналитикиТоваров.МестоХранения,
		|			КлючиРасходов.НаправлениеДеятельности,
		|			ДД.Регистратор
		|		ИЗ
		|			АналитикиРасходовВсе КАК КлючиРасходов
		|		ГДЕ 
		|			КлючиРасходов.АналитикаРасходов В (
		|				АналитикиТоваров.Номенклатура,
		|				ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|				АналитикиТоваров.МестоХранения,
		|				ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
		|				ДД.Регистратор)
		|			ИЛИ
		|			ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИПЗНАЧЕНИЯ(КлючиРасходов.АналитикаРасходов)
		|			И КлючиРасходов.АналитикаРасходов В (
		|				ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка),
		|				ЗНАЧЕНИЕ(Документ.СборкаТоваров.ПустаяСсылка),
		|				ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка))
		|	)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка,
		|	АналитикиТоваров.МестоХранения,
		|	ДД.Регистратор,
		|	ДД.ВидЗапасов,
		|	ДД.РазделУчета
		|";
КонецФункции

Функция ТекстБазисПриходыРаботСебестоимость() // использует АналитикиРасходовВсе
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстБазисПриходыРаботСебестоимость"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	АналитикиТоваров.МестоХранения КАК Склад,
		|	ДД.Регистратор,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК Поставщик,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	СУММА(ДД.Количество) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Затраты.НомерСтроки) КАК Количество,
		|	СУММА(&Вес * ДД.Количество) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Затраты.НомерСтроки) КАК Вес,
		|	СУММА(&Объем * ДД.Количество) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Затраты.НомерСтроки) КАК Объем,
		|	СУММА(ДД.Стоимость) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Затраты.НомерСтроки) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Затраты.НомерСтроки) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Затраты.НомерСтроки) КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Затраты.НомерСтроки) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Затраты.НомерСтроки) КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ДД.РазделУчета,
		|	ДД.Период КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикиТоваров
		|		ПО АналитикиТоваров.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = АналитикиТоваров.Номенклатура
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.МатериалыИРаботыВПроизводстве КАК Затраты
		|		ПО Затраты.Регистратор = ДД.Регистратор
		|		И Затраты.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|		И Затраты.Организация = ДД.Организация
		|		И (Затраты.Назначение = АналитикиТоваров.Назначение
		|			ИЛИ НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
		|			)
		|		И Затраты.Первичное = ИСТИНА
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И (ДД.Организация,
		|		АналитикиТоваров.Номенклатура,
		|		АналитикиТоваров.МестоХранения,
		|		ЕСТЬNULL(АналитикиТоваров.Назначение.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|		ДД.Регистратор) В
		|	(
		|		ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			КлючиРасходов.Организация,
		|			АналитикиТоваров.Номенклатура,
		|			АналитикиТоваров.МестоХранения,
		|			КлючиРасходов.НаправлениеДеятельности,
		|			ДД.Регистратор
		|		ИЗ
		|			АналитикиРасходовВсе КАК КлючиРасходов
		|		ГДЕ 
		|			КлючиРасходов.АналитикаРасходов В (
		|				АналитикиТоваров.Номенклатура,
		|				ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|				АналитикиТоваров.МестоХранения,
		|				ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
		|				ДД.Регистратор)
		|			ИЛИ
		|			ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИПЗНАЧЕНИЯ(КлючиРасходов.АналитикаРасходов)
		|			И КлючиРасходов.АналитикаРасходов В (
		|				ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка),
		|				ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка))
		|	)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка,
		|	АналитикиТоваров.МестоХранения,
		|	ДД.Регистратор,
		|	ДД.ВидЗапасов,
		|	ДД.РазделУчета
		|";
КонецФункции

Функция ТекстБазисПриходыИмпортСебестоимость() // использует АналитикиРасходовВсе
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстБазисПриходыИмпортСебестоимость"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	АналитикиТоваров.МестоХранения КАК Склад,
		|	ДД.Регистратор,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК Поставщик,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	СУММА(ДД.Количество) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК Количество,
		|	СУММА(&Вес * ДД.Количество) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК Вес,
		|	СУММА(&Объем * ДД.Количество) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК Объем,
		|	СУММА(ДД.Стоимость) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.НомерСтроки) КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ДД.РазделУчета,
		|	ДД.Период КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикиТоваров
		|		ПО АналитикиТоваров.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = АналитикиТоваров.Номенклатура
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКОформлениюДокументовИмпорта КАК Товары
		|		ПО Товары.Регистратор = ДД.Регистратор
		|		И Товары.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|		И Товары.Организация = ДД.Организация
		|		И Товары.ВидЗапасов = ДД.ВидЗапасов
		|		И Товары.Первичное = ИСТИНА
		|		И Товары.ТипДокументаИмпорта = &ТипДокументаИмпорта
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И НЕ ДД.РазделУчета В (
		|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
		|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку))
		|	И (ДД.Организация,
		|		АналитикиТоваров.Номенклатура,
		|		АналитикиТоваров.МестоХранения,
		|		ЕСТЬNULL(АналитикиТоваров.Назначение.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|		ДД.Регистратор) В
		|	(
		|		ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			КлючиРасходов.Организация,
		|			АналитикиТоваров.Номенклатура,
		|			АналитикиТоваров.МестоХранения,
		|			КлючиРасходов.НаправлениеДеятельности,
		|			ДД.Регистратор
		|		ИЗ
		|			АналитикиРасходовВсе КАК КлючиРасходов
		|		ГДЕ 
		|			КлючиРасходов.АналитикаРасходов В (
		|				АналитикиТоваров.Номенклатура,
		|				ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|				АналитикиТоваров.МестоХранения,
		|				ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
		|				ДД.Регистратор)
		|			ИЛИ
		|			ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИПЗНАЧЕНИЯ(КлючиРасходов.АналитикаРасходов)
		|			И КлючиРасходов.АналитикаРасходов В (
		|				ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка),
		|				ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка))
		|	)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка,
		|	АналитикиТоваров.МестоХранения,
		|	ДД.Регистратор,
		|	ДД.ВидЗапасов,
		|	ДД.РазделУчета
		|";
КонецФункции

Функция ТекстБазисПеремещенияСебестоимость() // использует АналитикиРасходовВсе
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстБазисПеремещенияСебестоимость"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК Склад,
		|	ДД.Регистратор,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК Поставщик,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	ДД.Количество,
		|	&Вес * ДД.Количество КАК Вес,
		|	&Объем * ДД.Количество КАК Объем,
		|	ДД.Стоимость,
		|	ДД.СтоимостьБезНДС,
		|	ДД.СтоимостьРегл,
		|	0,
		|	ДД.ПостояннаяРазница,
		|	ДД.ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ДД.РазделУчета,
		|	ДД.Период КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикиТоваров
		|		ПО АналитикиТоваров.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = АналитикиТоваров.Номенклатура
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.ПеремещениеТоваров)
		|	И НЕ ДД.РазделУчета В (
		|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
		|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку))
		|	И (ДД.Организация,
		|		ЕСТЬNULL(АналитикиТоваров.Назначение.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|		ДД.Регистратор) В
		|	(
		|		ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			КлючиРасходов.Организация,
		|			КлючиРасходов.НаправлениеДеятельности,
		|			ДД.Регистратор
		|		ИЗ
		|			АналитикиРасходовВсе КАК КлючиРасходов
		|		ГДЕ 
		|			КлючиРасходов.АналитикаРасходов В (
		|				ДД.Регистратор,
		|				ЗНАЧЕНИЕ(Документ.ПеремещениеТоваров.ПустаяСсылка))
		|	)
		|";
КонецФункции

Функция ТекстБазисПрошлыеПриходыТоваровСебестоимость() // использует АналитикиРасходовВсе
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстБазисПрошлыеПриходыТоваровСебестоимость"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Потребление"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	АналитикиТоваров.МестоХранения КАК Склад,
		|	ДД.Регистратор,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК Поставщик,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
		|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРаспределения,
		|	0 КАК Базис,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(&Вес * ДД.Количество) КАК Вес,
		|	СУММА(&Объем * ДД.Количество) КАК Объем,
		|	СУММА(ДД.Стоимость) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)	КАК ВариантРаздельногоУчетаНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаОтраженияРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка) КАК ПравилоРаспределения,
		|	ДД.РазделУчета,
		|	ДД.Период КАК РасчетныйПериод,
		|	ЛОЖЬ КАК РасчетПартий
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров.Остатки(&НачалоПериода) КАК Остатки
		|		ПО Остатки.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|		И Остатки.РазделУчета = ДД.РазделУчета
		|		И Остатки.ВидЗапасов = ДД.ВидЗапасов
		|		И Остатки.Организация = ДД.Организация
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикиТоваров
		|		ПО АналитикиТоваров.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = АналитикиТоваров.Номенклатура
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
		|		ПО АналитикиТоваров.МестоХранения = Склады.Ссылка
		|ГДЕ
		|	ДД.Период < &НачалоПериода
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И НЕ ДД.РазделУчета В (
		|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
		|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку))
		|	И (ДД.Организация,
		|		АналитикиТоваров.Номенклатура,
		|		АналитикиТоваров.МестоХранения,
		|		ЕСТЬNULL(АналитикиТоваров.Назначение.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|		ДД.Регистратор) В
		|	(
		|		ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			КлючиРасходов.Организация,
		|			АналитикиТоваров.Номенклатура,
		|			АналитикиТоваров.МестоХранения,
		|			КлючиРасходов.НаправлениеДеятельности,
		|			ДД.Регистратор
		|		ИЗ
		|			АналитикиРасходовВсе КАК КлючиРасходов
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Поступление
		|				ПО Поступление.Регистратор = КлючиРасходов.АналитикаРасходов
		|		ГДЕ 
		|			КлючиРасходов.АналитикаРасходов = ДД.Регистратор
		|			И Поступление.Период < &НачалоПериода
		|	)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	СпрНоменклатура.Ссылка,
		|	АналитикиТоваров.МестоХранения,
		|	ДД.Регистратор,
		|	ДД.ВидЗапасов,
		|	ДД.РазделУчета
		|";
КонецФункции

#КонецОбласти


#Область РасчетЗаписейНоменклатураДоходыРасходы

// В партионном учете версии 2.2 - этап 11, РаспределениеДопРасходовМеждуПартиямиИТоварами.

Функция ДанныеДляНоменклатураДоходыРасходы(НачалоПериода, ОкончаниеПериода, МассивОрганизаций)
	ТекстСортировка = "";
	ИсходныйТекстЗапроса = 
		ТекстРегистраторыДляНоменклатураДоходыРасходы() + ";" + // вт Регистраторы
		ТекстОписаниеДанныхДляНоменклатураДоходыРасходы()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстНоменклатураДоходыРасходыИзСебестоимости() // использует Регистраторы
		+ ТекстСортировка;
	ИсходныйЗапрос = Новый Запрос(ИсходныйТекстЗапроса);
	ИсходныйЗапрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	ИсходныйЗапрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	ИсходныйЗапрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);	
	ДанныеДляРасчета = ИсходныйЗапрос.Выполнить().Выгрузить();
	ДанныеДляРасчета.Сортировать("Регистратор", Новый СравнениеЗначений);
	
	Возврат ДанныеДляРасчета;
КонецФункции

Функция ТекстРегистраторыДляНоменклатураДоходыРасходы() // вт Регистраторы
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор
		|ПОМЕСТИТЬ
		|	Регистраторы
		|ИЗ
		|	РегистрНакопления.ПартииПрочихРасходов КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Партии
		|		ПО Партии.Период = ДД.Период
		|		И Партии.Регистратор = ДД.Регистратор
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|ИНДЕКСИРОВАТЬ ПО
		|	ДД.Регистратор
		|";
	
КонецФункции

Функция ТекстОписаниеДанныхДляНоменклатураДоходыРасходы()
	Возврат "
		|ВЫБРАТЬ ПЕРВЫЕ 0
		|	0 КАК Приоритет,
		|	""ХХХХХХХХХХХХХХХХ"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК Период,
		|	НЕОПРЕДЕЛЕНО КАК Регистратор,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)		  КАК НаправлениеДеятельностиНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПустаяСсылка) КАК ТипЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
		|
		|	НЕОПРЕДЕЛЕНО КАК СтатьяДоходовРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|
		|	0 КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|
		|	НЕОПРЕДЕЛЕНО КАК ИсточникГФУНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ДокументДвижения
		|";
КонецФункции

Функция ТекстНоменклатураДоходыРасходыИзСебестоимости() // использует Регистраторы
	Возврат "
		|ВЫБРАТЬ
		|	90 КАК Приоритет,
		|	""Партия"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ДД.Период,
		|	ДД.Регистратор,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовНаСебестоимость),
		|	ДД.Организация,
		|	ДД.Подразделение,
		|
		|	Аналитика.Назначение.НаправлениеДеятельности,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	Аналитика.МестоХранения КАК Склад,
		|	ЕСТЬNULL(СпрВидыЗапасов.ТипЗапасов, ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)) КАК ТипЗапасов,
		|	ДД.ВидЗапасов,
		|
		|	ДД.СтатьяРасходовСписания КАК СтатьяДоходовРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
		|	ДД.АналитикаРасходов КАК АналитикаРасходов,
		|
		|	0 КАК Количество,
		|	СУММА(ДД.ДопРасходы) КАК Стоимость,
		|	СУММА(ДД.ДопРасходыБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|
		|	ДД.ВидЗапасов КАК ИсточникГФУНоменклатуры,
		|	ДД.Регистратор КАК ДокументДвижения
		|
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК Регистраторы
		|		ПО Регистраторы.Регистратор = ДД.Регистратор
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК СпрВидыЗапасов
		|		ПО СпрВидыЗапасов.Ссылка = ДД.ВидЗапасов
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И (ДД.ДопРасходы <> 0 ИЛИ ДД.ДопРасходы <> 0)
		|	И ДД.Количество = 0
		|	И НЕ ДД.ХозяйственнаяОперация В
		|			(ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка),
		|			 ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОформлениеГТДБрокером),
		|			 ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОформлениеГТДСамостоятельно),
		|			 ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ДополнительныеРасходыБезПартии),
		|			 ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ДополнительныеРасходыСПартией))
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	Аналитика.Назначение.НаправлениеДеятельности,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	Аналитика.МестоХранения,
		|	ЕСТЬNULL(СпрВидыЗапасов.ТипЗапасов, ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)),
		|	ДД.ВидЗапасов,
		|	ДД.СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов
		|";
КонецФункции

#КонецОбласти


#Область РасчетПартийРасходов

Функция ДанныеДляПартийРасходов(НачалоПериода, ОкончаниеПериода, МассивОрганизаций)
	ТекстСортировка = "
		|УПОРЯДОЧИТЬ ПО
		|	ДокументПоступления, Период, Регистратор, Приоритет УБЫВ,
		|	КорАналитикаУчетаНоменклатуры, КорВидЗапасов, АналитикаУчетаНоменклатуры, ВидЗапасов,
		|	ДокументИсточник, ПодразделениеРасходов, СтатьяРасходовСписания, АналитикаРасходов, АналитикаАктивовПассивов
		|";
	ТекстИндексы = "
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументПоступления
		|";
	// Выбираем приходные движения партий расходов и дальнейшие движения партий товаров
	ИсходныйТекстЗапроса =
		ТекстПартииРасходовИнициализация() + ";" + // вт ПрошлыеРеализации, Отработано, Аналитики, ОстаткиПартий, ДолиСтоимости, ПрошлыеПоступления
		ТекстОписаниеПартийРасходов()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстОстаткиПартийРасходов() // использует ОстаткиПартий, ПрошлыеПоступления
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПриходыПартийРасходов()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстДополненияПартийРасходов()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстРасходыПартийРасходовПрошлыхМесяцев() // использует ПрошлыеРеализации
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстИтерацияПартийТоваров()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстИтерацияПартийПереданных()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПартииРасходовВПути()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстИтерацияПартийПроизводства()

		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстИтерацияПартийРаботИПеремещенияВПроизводстве()
		+ ТекстИндексы;
	МВТ = Новый МенеджерВременныхТаблиц;	
	ИсходныйЗапрос = Новый Запрос(СтрЗаменить(ИсходныйТекстЗапроса, "//ВоВременнуюТаблицу", ""));
	ИсходныйЗапрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	ИсходныйЗапрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);	
	ИсходныйЗапрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);	
	ИсходныйЗапрос.УстановитьПараметр("ЕстьФИФОСкользящая", ЕстьФИФОСкользящая(НачалоПериода, ОкончаниеПериода, МассивОрганизаций));
	ИсходныйЗапрос.МенеджерВременныхТаблиц = МВТ;
	ИсходныйЗапрос.Выполнить();
	
	Запрос = Новый Запрос(ТекстОписаниеПартийРасходов());
	РасчетныеПартии = Запрос.Выполнить().Выгрузить();
	
	ВременнаяТаблицаДанныхПоОтбору("ДокументПоступления", ТекстСортировка, РасчетныеПартии, МВТ);
	
	Возврат МВТ;
КонецФункции

Функция РасчетныеПартииРасходов(ДанныеДляРасчета, Регистраторы, ЦепочкиДвижений = Неопределено, Тест = Ложь, РассчитатьПартии = Истина)
	// Шаг 1: создаем буфер накопления рассчитанных партий
	Запрос = Новый Запрос(ТекстОписаниеПартийРасходов());
	РасчетныеПартии = Запрос.Выполнить().Выгрузить();
	
	// Шаг 2: обсчитываем цепочки движений по передачам, возвратам, перемещениям, порчам, пересортицам и т.д.
	КонтекстЦепочек = ОписаниеЦепочек("Потребление, Перемещение, Партия, Остаток, Сторно, Прошлое, Замещение,
		|Продукция, Выпуск, Затраты, Распределение, ВыпускПрочие, ВПути, Дополнение, Прочее, Списание, ПотреблениеВПути");
	
	ПоляПотреблений = "РазделУчета, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, ДокументПоступления, ДокументИсточник";
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Потребление", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Партия", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Остаток", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Перемещение", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Замещение", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Выпуск", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "ВыпускПрочие",
		"РазделУчета, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, ДокументПоступления, Регистратор");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Сторно",
		"РазделУчета, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, ДокументПоступления, Регистратор");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "ПотреблениеВПути", "РазделУчета, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, ДокументПоступления, Регистратор");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ПотреблениеВПути", "Перемещение", "РазделУчета, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, ДокументПоступления, Регистратор");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ПотреблениеВПути", "Остаток", ПоляПотреблений);
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Перемещение",
		"Регистратор, АналитикаУчетаНоменклатуры, ВидЗапасов, ДокументПоступления");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Перемещение", "Потребление",
		"Регистратор, КорАналитикаУчетаНоменклатуры, КорВидЗапасов, ДокументПоступления");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Перемещение", "Продукция",
		"Регистратор, АналитикаУчетаНоменклатуры, ВидЗапасов, ДокументПоступления");	
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Замещение",
		"Регистратор, АналитикаУчетаНоменклатуры, ВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Замещение", "Потребление",
		"Регистратор, КорАналитикаУчетаНоменклатуры, КорВидЗапасов");
		
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Продукция", "ДокументИсточник, РазделУчета, АналитикаУчетаНоменклатуры, ДокументПоступления, ВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Продукция", "Перемещение", "ДокументИсточник, РазделУчета, АналитикаУчетаНоменклатуры, ДокументПоступления, ВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Продукция", "Выпуск", "ДокументИсточник, РазделУчета, АналитикаУчетаНоменклатуры, ДокументПоступления, ВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Продукция", "ВыпускПрочие", "Регистратор, РазделУчета, АналитикаУчетаНоменклатуры, ДокументПоступления, ВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Продукция", "Партия", "ДокументИсточник, РазделУчета, АналитикаУчетаНоменклатуры, ДокументПоступления, ВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Продукция", "Остаток", "ДокументИсточник, РазделУчета, АналитикаУчетаНоменклатуры, ДокументПоступления, ВидЗапасов");
		
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Выпуск", "Регистратор, АналитикаУчетаНоменклатуры, ВидЗапасов, КорАналитикаУчетаНоменклатуры, КорВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Выпуск", "Продукция", "Регистратор, КорАналитикаУчетаНоменклатуры, КорВидЗапасов, АналитикаУчетаНоменклатуры, ВидЗапасов");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Сторно",
		"ДокументИсточник, КорАналитикаУчетаНоменклатуры, КорВидЗапасов, ДокументПоступления");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Сторно", "Потребление",
		"Регистратор, АналитикаУчетаНоменклатуры, ВидЗапасов, ДокументПоступления");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Сторно", "ПотреблениеВПути",
		"Регистратор, КорАналитикаУчетаНоменклатуры, ВидЗапасов, ДокументПоступления");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Сторно", "Прошлое",
		"Регистратор, АналитикаУчетаНоменклатуры, ВидЗапасов, ДокументПоступления");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Сторно", "Перемещение",
		"Регистратор, АналитикаУчетаНоменклатуры, ВидЗапасов, ДокументПоступления");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Сторно", "Сторно",
		"Регистратор, АналитикаУчетаНоменклатуры, ВидЗапасов, ДокументПоступления");
		
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Затраты", "Организация, АналитикаУчетаНоменклатуры, СтатьяРасходовСписания, АналитикаРасходов, ПодразделениеРасходов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Затраты", "Потребление", "Организация, АналитикаУчетаНоменклатуры, СтатьяРасходовСписания, АналитикаРасходов, ПодразделениеРасходов");
		
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Распределение", "Организация, СтатьяРасходовСписания, АналитикаРасходов, ПодразделениеРасходов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Распределение", "Затраты", "Организация, СтатьяРасходовСписания, АналитикаРасходов, ПодразделениеРасходов");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Прочее", "Организация, СтатьяРасходов, АналитикаРасходов, ПодразделениеРасходов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Прочее", "Затраты", "Организация, СтатьяРасходовСписания, АналитикаРасходов, ПодразделениеРасходов");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Списание", "Регистратор");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Списание", "Прочее", "Регистратор");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "ВыпускПрочие",
		"Регистратор, ДокументИсточник, АналитикаУчетаНоменклатуры, ВидЗапасов, СтатьяРасходовСписания, АналитикаРасходов, ПодразделениеРасходов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ВыпускПрочие", "Распределение",
		"Регистратор, ДокументИсточник, КорАналитикаУчетаНоменклатуры, КорВидЗапасов, СтатьяРасходовСписания, АналитикаРасходов, ПодразделениеРасходов");
		
	// Шаг 3: замена ложных партий на первичные партии в расчетных строках по результатам построение графа замен.
	КонтекстДвижений = ОписаниеДвижений(
		"ПартииРасходовНаСебестоимостьТоваров",
		"ПартииРасходовНаСебестоимостьТоваров",
		ПереченьПолей(РасчетныеПартии.Колонки),
		ПоляПотреблений,
		"Количество, Знаменатель, Стоимость, СтоимостьБезНДС, СтоимостьРегл, НДСРегл, ПостояннаяРазница, ВременнаяРазница",
		"Количество", "Количество", "ДокументИсточник", "Период");
	КонтекстДвижений.Вставить("ПоляСуммирования", "Стоимость, СтоимостьБезНДС, СтоимостьРегл, НДСРегл, ПостояннаяРазница, ВременнаяРазница");
	КонтекстДвижений.Вставить("ПоляГруппировки", ПереченьПолей(РасчетныеПартии.Колонки, КонтекстДвижений.ПоляСуммирования));
	ЦепочкиДвижений(КонтекстЦепочек, ДанныеДляРасчета);
	Если РассчитатьПартии Тогда
		РассчитатьПартииПоЦепочкам(КонтекстДвижений, ДанныеДляРасчета, РасчетныеПартии, Регистраторы, Тест);
	КонецЕсли;
	ЦепочкиДвижений = ДанныеДляРасчета;
	
	// Шаг 4: Удаляем нерассчитанные партии
	УдалитьНезаписываемыеСтроки(КонтекстДвижений.Контекст, РасчетныеПартии);
	
	Возврат РасчетныеПартии;
КонецФункции

Функция ТекстПартииРасходовИнициализация() // вт ПрошлыеРеализации, ОстаткиПартий, ПроизводственныеЗатраты, ПрошлыеРегистраторы, Выпуски, ДолиСтоимости, ПрошлыеПоступления
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ДокументРеализации КАК ДокументРеализации,
		|	ДД.КорАналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов КАК ВидЗапасов
		|ПОМЕСТИТЬ
		|	ПрошлыеРеализации
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизаций КАК Расход
		|		ПО Расход.Период < &НачалоПериода
		|		И Расход.Регистратор = ДД.ДокументРеализации
		|		И Расход.АналитикаУчетаНоменклатуры = ДД.КорАналитикаУчетаНоменклатуры
		|		И Расход.ВидЗапасов = ДД.КорВидЗапасов
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ДокументРеализации <> НЕОПРЕДЕЛЕНО
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ДД.Количество < 0
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ДокументРеализации КАК ДокументРеализации,
		|	ДД.КорАналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов КАК ВидЗапасов
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизацийКПередаче КАК Расход
		|		ПО Расход.Период < &НачалоПериода
		|		И Расход.Регистратор = ДД.ДокументРеализации
		|		И Расход.АналитикаУчетаНоменклатуры = ДД.КорАналитикаУчетаНоменклатуры
		|		И Расход.ВидЗапасовПродавца = ДД.КорВидЗапасов
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Активность
		|	И ДД.ДокументРеализации <> НЕОПРЕДЕЛЕНО
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ДД.Количество < 0
		|;
		// Потенциальные источники, потребления которых надо списывать из знаменателя
		|ВЫБРАТЬ
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	СУММА(ДД.Количество) КАК Знаменатель
		|ПОМЕСТИТЬ
		|	Знаменатели
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ДокументИсточник <> НЕОПРЕДЕЛЕНО
		|	И НЕ ДД.Первичное
		|	И (ДД.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента
		|		ИЛИ ДД.Регистратор ССЫЛКА Документ.ПрочееОприходованиеТоваров
		|		ИЛИ ДД.Регистратор ССЫЛКА Документ.КорректировкаРеализации)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий
		|;
		// Остатки партионных регистров на начало по аналитикам доп.расходов
		|ВЫБРАТЬ
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	СУММА(ДД.Количество) КАК Количество
		|ПОМЕСТИТЬ
		|	ОстаткиПартий
		|ИЗ (
		|	ВЫБРАТЬ
		|		ДД.Организация,
		|		ДД.АналитикаУчетаНоменклатуры,
		|		ДД.ДокументПоступления,
		|		ДД.ВидЗапасов,
		|		ДД.КоличествоОстаток КАК Количество
		|	ИЗ
		|		РегистрНакопления.ПартииТоваровОрганизаций.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ
		|		ДД.Организация,
		|		ДД.АналитикаУчетаНоменклатуры,
		|		ДД.ДокументПоступления,
		|		ДД.ВидЗапасов,
		|		ДД.КоличествоОстаток КАК Количество
		|	ИЗ
		|		РегистрНакопления.ПартииТоваровПереданныеНаКомиссию.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ
		|		ДД.Организация,
		|		ДД.АналитикаУчетаНоменклатуры,
		|		ДД.ДокументПоступления,
		|		ДД.ВидЗапасов,
		|		ДД.КоличествоОстаток КАК Количество
		|	ИЗ
		|		РегистрНакопления.ПартииПроизводственныхЗатрат.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|
		|	) КАК ДД
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов
		|;
		// Документы передачи в производство и возврата из производства
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ
		|	МатериалыИРаботыВПроизводстве
		|ИЗ
		|	РегистрНакопления.МатериалыИРаботыВПроизводстве КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Товары
		|		ПО Товары.Ссылка = ДД.АналитикаУчетаНоменклатуры.Номенклатура
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И Товары.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|;
		// Документы распределения расходов, возникших в прошлых периодах
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ДокументПоступленияРасходов КАК Ссылка
		|ПОМЕСТИТЬ
		|	ПрошлыеРегистраторы
		|ИЗ
		|	РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров КАК Партии
		|		ПО Партии.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И Партии.Регистратор = ДД.ДокументПоступленияРасходов
		|		И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|СГРУППИРОВАТЬ ПО
		|	ДД.ДокументПоступленияРасходов
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ДокументПоступления КАК Ссылка,
		|	ДД.Склад КАК Склад,
		|	МАКСИМУМ(ДД.Регистратор) КАК Регистратор,
		|	МАКСИМУМ(ДД.Период) КАК Период
		|ПОМЕСТИТЬ
		|	ПрошлыеПоступления
		|ИЗ (
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ДД.ДокументПоступления,
		|		ДД.АналитикаУчетаНоменклатуры.МестоХранения КАК Склад,
		|		МАКСИМУМ(Периодика.Регистратор) КАК Регистратор,
		|		МАКСИМУМ(Периодика.Период) КАК Период
		|	ИЗ
		|		РегистрНакопления.ПартииПроизводственныхЗатрат.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииПроизводственныхЗатрат КАК Периодика
		|			ПО Периодика.Период < &НачалоПериода
		|			И Периодика.ДокументПоступления = ДД.ДокументПоступления
		|			И Периодика.АналитикаУчетаНоменклатуры.МестоХранения = ДД.АналитикаУчетаНоменклатуры.МестоХранения
		|	ГДЕ
		|		ДД.ДокументПоступления <> НЕОПРЕДЕЛЕНО
		|		И Периодика.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И Периодика.Количество > 0
		|	СГРУППИРОВАТЬ ПО
		|		ДД.ДокументПоступления,
		|		ДД.АналитикаУчетаНоменклатуры.МестоХранения
		|
		|	) КАК ДД
		|СГРУППИРОВАТЬ ПО
		|	ДД.ДокументПоступления,
		|	ДД.Склад
		|;
		// Подразделение документов-регистраторов
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор,
		|	ДД.Подразделение
		|ПОМЕСТИТЬ
		|	ПодразделенияРегистраторов
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК ДД
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		ИЛИ ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноСписанияНаРасходы))
		|	И ДД.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Корректировка.ДокументОснование КАК Поступление
		|ПОМЕСТИТЬ
		|	КорректируемыеПоступления
		|ИЗ
		|	РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения КАК Корректировка
		|		ПО Корректировка.Ссылка = ДД.Регистратор
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ДД.ДокументПоступленияРасходов = ДД.Регистратор
		|;
		// Остатки партионных регистров на начало по аналитикам доп.расходов
		|ВЫБРАТЬ
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	СУММА(ДД.Количество) КАК Количество
		|ПОМЕСТИТЬ
		|	ПоступленияПартий
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ КорректируемыеПоступления КАК Поступления
		|		ПО Поступления.Поступление = ДД.Регистратор
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.СтатьяРасходов
		|ПОМЕСТИТЬ
		|	ПартииПрочихРасходов
		|ИЗ
		|	РегистрНакопления.ПартииПрочихРасходов КАК ДД
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И (ДД.СтатьяРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|		ИЛИ ДД.СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров))
		|";
КонецФункции

Функция ТекстОписаниеПартийРасходов()
	Возврат "
		// Соединения и ЕСТЬNULL использованы для расширения типов полей таблицы ДанныеДляПартийХХХ.
		|ВЫБРАТЬ ПЕРВЫЕ 0
		|	ВЫРАЗИТЬ("""" КАК СТРОКА(80)) КАК ЗапросИсточник,
		|	0 КАК Приоритет,
		|	""ХХХХХХХХХХХХХХХХ"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК Период,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	ДД.ДокументПоступленияРасходов,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	0 КАК Количество,
		|	0 КАК Знаменатель,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	ДД.ДокументИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Продукция,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаПродукции,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	ДД.СтатьяРасходовСписания,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка) КАК ВариантРаспределенияРасходовУпр,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка) КАК ВариантРаспределенияРасходовРегл,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	ДД.СтатьяСписанияНДС,
		|	ДД.АналитикаСписанияНДС
		|//ВоВременнуюТаблицу ПОМЕСТИТЬ ИсходныеДанные
		|ИЗ
		|	РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров КАК ДД
		|";
КонецФункции

Функция ТекстОстаткиПартийРасходов() // использует ОстаткиПартий, ПрошлыеПоступления
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстОстаткиПартийРасходов"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	""Остаток"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	(ВЫБОР
		|		КОГДА Периодика.Регистратор ЕСТЬ NULL ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КОНЕЦ) КАК РазделУчета,
		|	&НачалоПериода КАК Период,
		|	ЕСТЬNULL(Периодика.Регистратор, ДД.ДокументПоступления) КАК Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.ДокументПоступленияРасходов,
		|	ДД.СтатьяРасходов,
		|	Партии.Количество,
		|	0 КАК Знаменатель,
		|	ДД.СтоимостьОстаток КАК Стоимость,
		|	ДД.СтоимостьБезНДСОстаток КАК СтоимостьБезНДС,
		|	ДД.СтоимостьРеглОстаток КАК СтоимостьРегл,
		|	ДД.НДСРеглОстаток КАК НДСРегл,
		|	ДД.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
		|	ДД.ВременнаяРазницаОстаток КАК ВременнаяРазница,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	ЕСТЬNULL(Периодика.Регистратор, ДД.ДокументПоступления) КАК ДокументИсточник,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК ВариантРаспределенияРасходовУпр,
		|	НЕОПРЕДЕЛЕНО КАК ВариантРаспределенияРасходовРегл,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОстаткиПартий КАК Партии
		|		ПО Партии.Организация = ДД.Организация
		|		И Партии.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|		И Партии.ДокументПоступления = ДД.ДокументПоступления
		|		И Партии.ВидЗапасов = ДД.ВидЗапасов
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПрошлыеПоступления КАК Периодика
		|		ПО Периодика.Ссылка = ДД.ДокументПоступления
		|		И Периодика.Склад = Аналитика.МестоХранения
		|ГДЕ
		|	Партии.Количество > 0
		|";
КонецФункции
	
Функция ТекстПриходыПартийРасходов()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПриходыПартийРасходов"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	""Партия"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.ДокументПоступленияРасходов,
		|	ДД.СтатьяРасходов,
		|	СУММА(ЕСТЬNULL(ПоступленияПартий.Количество, ДД.Количество)) КАК Количество,
		|	0 КАК Знаменатель,
		|	СУММА(ДД.Стоимость) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.НДСРегл) КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	(ВЫБОР 
		|		КОГДА ТИПЗНАЧЕНИЯ(ДД.АналитикаРасходов) = ТИП(Документ.ПеремещениеТоваров)
		|			И ДД.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПеремещениеТоваров.ПустаяСсылка) ТОГДА ДД.ДокументИсточник
		|		КОГДА ТИПЗНАЧЕНИЯ(ДД.АналитикаРасходов) = ТИП(Документ.ПередачаТоваровМеждуОрганизациями)
		|			И ДД.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка) ТОГДА ДД.АналитикаРасходов
		|		КОГДА ТИПЗНАЧЕНИЯ(ДД.АналитикаРасходов) = ТИП(Документ.ЗаказНаПеремещение) ТОГДА ДД.ДокументИсточник
		|		КОГДА ДД.ДокументИсточник <> НЕОПРЕДЕЛЕНО ТОГДА ДД.ДокументИсточник
		|		ИНАЧЕ ДД.ДокументПоступления КОНЕЦ) КАК ДокументИсточник,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.ПодразделениеРасходов,
		|	ДД.СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК ВариантРаспределенияРасходовУпр,
		|	НЕОПРЕДЕЛЕНО КАК ВариантРаспределенияРасходовРегл,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПоступленияПартий КАК ПоступленияПартий
		|		ПО ПоступленияПартий.Организация = ДД.Организация
		|		И ПоступленияПартий.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|		И ПоступленияПартий.ДокументПоступления = ДД.ДокументПоступления
		|		И ПоступленияПартий.ВидЗапасов = ДД.ВидЗапасов
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПартииПрочихРасходов КАК Партии
		|		ПО Партии.Регистратор = ДД.Регистратор
		|		И Партии.Организация = ДД.Организация
		|		И Партии.СтатьяРасходов = ДД.СтатьяРасходов
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученныйНалоговыйАгент КАК СчетФактура
		|		ПО СчетФактура.Ссылка = ДД.Регистратор
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ДокументПоступленияРасходов В (
		|		ДД.Регистратор,
		|		СчетФактура.СчетФактураОснование)
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И (НЕ Партии.Регистратор ЕСТЬ NULL
		|		ИЛИ ДД.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка))
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.ДокументПоступленияРасходов,
		|	ДД.СтатьяРасходов,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	(ВЫБОР 
		|		КОГДА ТИПЗНАЧЕНИЯ(ДД.АналитикаРасходов) = ТИП(Документ.ПеремещениеТоваров)
		|			И ДД.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПеремещениеТоваров.ПустаяСсылка) ТОГДА ДД.ДокументИсточник
		|		КОГДА ТИПЗНАЧЕНИЯ(ДД.АналитикаРасходов) = ТИП(Документ.ПередачаТоваровМеждуОрганизациями)
		|			И ДД.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка) ТОГДА ДД.АналитикаРасходов
		|		КОГДА ТИПЗНАЧЕНИЯ(ДД.АналитикаРасходов) = ТИП(Документ.ЗаказНаПеремещение) ТОГДА ДД.ДокументИсточник
		|		КОГДА ДД.ДокументИсточник <> НЕОПРЕДЕЛЕНО ТОГДА ДД.ДокументИсточник
		|		ИНАЧЕ ДД.ДокументПоступления КОНЕЦ),
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.ПодразделениеРасходов,
		|	ДД.СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов
		|";
КонецФункции

Функция ТекстДополненияПартийРасходов() // использует ПрошлыеРегистраторы
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстДополненияПартийРасходов"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	""Дополнение"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.ДокументПоступленияРасходов,
		|	ДД.СтатьяРасходов,
		|	ДД.Количество,
		|	0 КАК Знаменатель,
		|	ДД.Стоимость,
		|	ДД.СтоимостьБезНДС,
		|	ДД.СтоимостьРегл,
		|	ДД.НДСРегл,
		|	ДД.ПостояннаяРазница,
		|	ДД.ВременнаяРазница,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	ДД.ДокументИсточник,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.ПодразделениеРасходов,
		|	ДД.СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК ВариантРаспределенияРасходовУпр,
		|	НЕОПРЕДЕЛЕНО КАК ВариантРаспределенияРасходовРегл,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрошлыеРегистраторы КАК ПрошлыеРегистраторы
		|		ПО ПрошлыеРегистраторы.Ссылка = ДД.ДокументПоступленияРасходов
		|ГДЕ
		|	ДД.Период < &НачалоПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|";
КонецФункции

Функция ТекстРасходыПартийРасходовПрошлыхМесяцев() // использует ПрошлыеРеализации
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстРасходыПартийРасходовПрошлыхМесяцев"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	""Прошлое"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.ДокументПоступленияРасходов,
		|	ДД.СтатьяРасходов,
		|	МАКСИМУМ(ДД.Количество) КАК Количество,
		|	0 КАК Знаменатель,
		|	СУММА(ДД.Стоимость) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.НДСРегл) КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов КАК КорВидЗапасов,
		|	ДД.Регистратор КАК ДокументИсточник,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.ПодразделениеРасходов,
		|	ДД.СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК ВариантРаспределенияРасходовУпр,
		|	НЕОПРЕДЕЛЕНО КАК ВариантРаспределенияРасходовРегл,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрошлыеРеализации КАК Прошлые
		|		ПО ДД.Регистратор = Прошлые.ДокументРеализации
		|		И ДД.АналитикаУчетаНоменклатуры = Прошлые.АналитикаУчетаНоменклатуры
		|		И ДД.ВидЗапасов = Прошлые.ВидЗапасов
		|ГДЕ
		|	ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|СГРУППИРОВАТЬ ПО
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.ДокументПоступленияРасходов,
		|	ДД.СтатьяРасходов,
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.ПодразделениеРасходов,
		|	ДД.СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов
		|";
КонецФункции

Функция ТекстИтерацияПартийТоваров()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстИтерацияПартийТоваров"" КАК ЗапросИсточник,
		|	(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество > 0 ТОГДА 90
		|		ИНАЧЕ 40 КОНЕЦ) КАК Приоритет,
		|	(ВЫБОР
		|		КОГДА ДД.Регистратор = ДД.ДокументПоступления ТОГДА ""Замещение""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество > 0 ТОГДА ""Потребление""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество < 0 ТОГДА ""Сторно""
		|		ИНАЧЕ ""Перемещение"" КОНЕЦ) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	(ВЫБОР
		|		КОГДА ДД.Регистратор = ДД.ДокументПоступления ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ДД.ВидДвижения КОНЕЦ) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Период КАК Период,
		|	ДД.Регистратор КАК Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	СУММА(ВЫБОР
		|		КОГДА ДД.Регистратор = ДД.ДокументПоступления И ДД.Количество < 0 ТОГДА -ДД.Количество
		|		ИНАЧЕ ДД.Количество КОНЕЦ) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(Комиссия.НомерСтроки, -1)) КАК Количество,
		|	ЕСТЬNULL(Знаменатели.Знаменатель, 0) КАК Знаменатель,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	(ВЫБОР
		|		КОГДА НЕ Возвраты.Ссылка ЕСТЬ NULL И ДД.Количество < 0 ТОГДА ИСТИНА
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА ЛОЖЬ
		|		КОГДА Передачи.Ссылка ЕСТЬ NULL И Возвраты.Ссылка ЕСТЬ NULL ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА КОНЕЦ) КАК СохранятьРегл,
		|	ЕСТЬNULL(Комиссия.АналитикаУчетаНоменклатуры, ДД.КорАналитикаУчетаНоменклатуры) КАК КорАналитикаУчетаНоменклатуры,
		|	ЕСТЬNULL(Комиссия.ВидЗапасов, ДД.КорВидЗапасов) КАК КорВидЗапасов,
		|	(ВЫБОР
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера) ТОГДА ДД.Регистратор
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратИзПроизводства) ТОГДА ДД.Регистратор
		|		ИНАЧЕ ДД.ДокументИсточник КОНЕЦ) КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	ДД.ХозяйственнаяОперация,
		|	ЕСТЬNULL(ПодразделенияРегистраторов.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК ПодразделениеРасходов,
		|	ДД.СтатьяРасходовСписания,
		|	ДД.СтатьяРасходовСписания.ВариантРаспределенияРасходовУпр КАК ВариантРаспределенияРасходовУпр,
		|	ДД.СтатьяРасходовСписания.ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияРасходовРегл,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПодразделенияРегистраторов КАК ПодразделенияРегистраторов
		|		ПО ПодразделенияРегистраторов.Регистратор = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ Знаменатели КАК Знаменатели
		|		ПО Знаменатели.Регистратор = ДД.Регистратор
		|		И Знаменатели.Организация = ДД.Организация
		|		И Знаменатели.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|		И Знаменатели.ДокументПоступления = ДД.ДокументПоступления
		|		И Знаменатели.ВидЗапасов = ДД.ВидЗапасов
		|		И Знаменатели.АналитикаУчетаПартий = ДД.АналитикаУчетаПартий
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровМеждуОрганизациями КАК Передачи
		|		ПО Передачи.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И Передачи.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровМеждуОрганизациями КАК Возвраты
		|		ПО Возвраты.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И Возвраты.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровПереданныеНаКомиссию КАК Комиссия
		|		ПО Комиссия.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И Комиссия.Регистратор = ДД.Регистратор
		|		И Комиссия.Организация = ДД.Организация
		|		И Комиссия.ДокументПоступления = ДД.ДокументПоступления
		|		И Комиссия.ВидЗапасов = ДД.ВидЗапасов
		|		И Комиссия.КорАналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидЗапасовПрихода
		|		ПО ВидЗапасовПрихода.Ссылка = ДД.ВидЗапасов
		|		И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ДокументИсточник <> НЕОПРЕДЕЛЕНО
		|	И ЕСТЬNULL(ВидЗапасовПрихода.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар), ИСТИНА)
		|	И НЕ ДД.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		|СГРУППИРОВАТЬ ПО
		|	(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество > 0 ТОГДА 90
		|		ИНАЧЕ 40 КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ДД.Регистратор = ДД.ДокументПоступления ТОГДА ""Замещение""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество > 0 ТОГДА ""Потребление""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество < 0 ТОГДА ""Сторно""
		|		ИНАЧЕ ""Перемещение"" КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ДД.Регистратор = ДД.ДокументПоступления ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ДД.ВидДвижения КОНЕЦ),
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ЕСТЬNULL(Знаменатели.Знаменатель, 0),
		|	(ВЫБОР
		|		КОГДА НЕ Возвраты.Ссылка ЕСТЬ NULL И ДД.Количество < 0 ТОГДА ИСТИНА
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА ЛОЖЬ
		|		КОГДА Передачи.Ссылка ЕСТЬ NULL И Возвраты.Ссылка ЕСТЬ NULL ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА КОНЕЦ),
		|	ЕСТЬNULL(Комиссия.АналитикаУчетаНоменклатуры, ДД.КорАналитикаУчетаНоменклатуры),
		|	ЕСТЬNULL(Комиссия.ВидЗапасов, ДД.КорВидЗапасов),
		|	(ВЫБОР
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера) ТОГДА ДД.Регистратор
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратИзПроизводства) ТОГДА ДД.Регистратор
		|		ИНАЧЕ ДД.ДокументИсточник КОНЕЦ),
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	ЕСТЬNULL(ПодразделенияРегистраторов.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|";
КонецФункции

Функция ТекстИтерацияПартийПереданных()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстИтерацияПартийПереданных"" КАК ЗапросИсточник,
		|	(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество > 0 ТОГДА 90
		|		ИНАЧЕ 40 КОНЕЦ) КАК Приоритет,
		|	(ВЫБОР
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			И НЕ РеализацияТоварыВПути.Ссылка ЕСТЬ NULL ТОГДА ""ПотреблениеВПути""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество > 0 ТОГДА ""Потребление""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество < 0 ТОГДА ""Сторно""
		|		КОГДА ДД.Регистратор = ДД.ДокументПоступления ТОГДА ""Замещение""
		|		ИНАЧЕ ""Перемещение"" КОНЕЦ) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Период КАК Период,
		|	ДД.Регистратор КАК Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	СУММА(ДД.Количество) КАК Количество,
		|	0 КАК Знаменатель,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	(ВЫБОР
		|		КОГДА Реализация.Дата >= &НачалоПериода ТОГДА ДД.ДокументПередачиНаКомиссию
		|		КОГДА Передача.Дата >= &НачалоПериода ТОГДА ДД.ДокументПередачиНаКомиссию
		|		ИНАЧЕ ДД.ДокументПоступления КОНЕЦ) КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	ДД.ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК ВариантРаспределенияРасходовУпр,
		|	НЕОПРЕДЕЛЕНО КАК ВариантРаспределенияРасходовРегл,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровПереданныеНаКомиссию КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК Реализация
		|		ПО Реализация.Ссылка = ДД.ДокументПередачиНаКомиссию
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровМеждуОрганизациями КАК Передача
		|		ПО Передача.Ссылка = ДД.ДокументПередачиНаКомиссию
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоварыВПути
		|		ПО РеализацияТоварыВПути.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И РеализацияТоварыВПути.Ссылка = ДД.Регистратор
		|		И РеализацияТоварыВПути.ХозяйственнаяОперация В (
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности))
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|СГРУППИРОВАТЬ ПО
		|	(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество > 0 ТОГДА 90
		|		ИНАЧЕ 40 КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			И НЕ РеализацияТоварыВПути.Ссылка ЕСТЬ NULL ТОГДА ""ПотреблениеВПути""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество > 0 ТОГДА ""Потребление""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Количество < 0 ТОГДА ""Сторно""
		|		КОГДА ДД.Регистратор = ДД.ДокументПоступления ТОГДА ""Замещение""
		|		ИНАЧЕ ""Перемещение"" КОНЕЦ),
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	(ВЫБОР
		|		КОГДА Реализация.Дата >= &НачалоПериода ТОГДА ДД.ДокументПередачиНаКомиссию
		|		КОГДА Передача.Дата >= &НачалоПериода ТОГДА ДД.ДокументПередачиНаКомиссию
		|		ИНАЧЕ ДД.ДокументПоступления КОНЕЦ),
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация
		|";
КонецФункции

Функция ТекстПартииРасходовВПути()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПартииРасходовВПути"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	""ВПути"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.ДокументПоступленияРасходов,
		|	ДД.СтатьяРасходов,
		|	ДД.Количество,
		|	0 КАК Знаменатель,
		|	ДД.Стоимость,
		|	ДД.СтоимостьБезНДС,
		|	ДД.СтоимостьРегл,
		|	ДД.НДСРегл,
		|	ДД.ПостояннаяРазница,
		|	ДД.ВременнаяРазница,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	ДД.ДокументИсточник,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.ПодразделениеРасходов,
		|	ДД.СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК ВариантРаспределенияРасходовУпр,
		|	НЕОПРЕДЕЛЕНО КАК ВариантРаспределенияРасходовРегл,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК Отгрузки
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ  РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров КАК ДД
		|		ПО ДД.Регистратор = Отгрузки.Ссылка
		|ГДЕ
		|	Отгрузки.ХозяйственнаяОперация В (
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности))
		|	И (Отгрузки.ДатаПереходаПраваСобственности МЕЖДУ &НачалоПериода И &ОкончаниеПериода // расчет в периоде перехода права собственности
		|		ИЛИ Отгрузки.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода // расчет в периоде отгрузки
		|		ИЛИ Отгрузки.ДатаПереходаПраваСобственности = ДАТАВРЕМЯ(1,1,1))
		|	И (ДД.Период < &НачалоПериода И Отгрузки.Дата < &НачалоПериода // сохранение движений в периоде отгрузки
		|		// сохранение движений в периоде перехода права собственности
		|		ИЛИ ДД.Период > &ОкончаниеПериода И Отгрузки.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода)
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|";
КонецФункции

Функция ТекстИтерацияПартийПроизводства()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстИтерацияПартийПроизводства"" КАК ЗапросИсточник,
		|	(ВЫБОР 
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА 10
		|		ИНАЧЕ 90 КОНЕЦ) КАК Приоритет,
		|	(ВЫБОР 
		|		КОГДА ДД.Количество < 0 ТОГДА ""Сторно""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА ""Потребление""
		|		ИНАЧЕ ""Перемещение"" КОНЕЦ) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	(ВЫБОР 
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КОНЕЦ) КАК ВидДвижения,
		|	(ВЫБОР ДД.ХозяйственнаяОперация
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПереработчику)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеПереработчику)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПереработчику2_5)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеПереработчику)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КОНЕЦ) КАК РазделУчета,
		|	ДД.Период КАК Период,
		|	ДД.Регистратор КАК Регистратор,
		|	ДД.Организация,
		|	(ВЫБОР
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтПереработчика)
		|			ТОГДА ДД.АналитикаУчетаНоменклатуры
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтПереработчика2_5)
		|			ТОГДА ДД.АналитикаУчетаНоменклатуры
		|		ИНАЧЕ ДД.КорАналитикаУчетаНоменклатуры КОНЕЦ) КАК АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.КорВидЗапасов КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(ДД.Количество) КАК Знаменатель,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	(ВЫБОР 
		|		КОГДА ДД.Количество < 0 ТОГДА ДД.КорАналитикаУчетаНоменклатуры
		|		ИНАЧЕ ДД.АналитикаУчетаНоменклатуры КОНЕЦ) КАК КорАналитикаУчетаНоменклатуры,
		|	(ВЫБОР 
		|		КОГДА ДД.Количество < 0 ТОГДА ДД.КорВидЗапасов
		|		ИНАЧЕ ДД.ВидЗапасов КОНЕЦ) КАК КорВидЗапасов,
		|	(ВЫБОР
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтПереработчика)
		|			ТОГДА ДД.Регистратор
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтПереработчика2_5)
		|			ТОГДА ДД.Регистратор
		|		КОГДА ДД.Количество < 0 ТОГДА ДД.ДокументИсточник
		|		ИНАЧЕ ДД.Регистратор КОНЕЦ) КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	ДД.ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК ВариантРаспределенияРасходовУпр,
		|	НЕОПРЕДЕЛЕНО КАК ВариантРаспределенияРасходовРегл,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ МатериалыИРаботыВПроизводстве КАК Затраты
		|		ПО Затраты.Регистратор = ДД.Регистратор
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|СГРУППИРОВАТЬ ПО
		|	(ВЫБОР 
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА 10
		|		ИНАЧЕ 90 КОНЕЦ),
		|	(ВЫБОР 
		|		КОГДА ДД.Количество < 0 ТОГДА ""Сторно""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА ""Потребление""
		|		ИНАЧЕ ""Перемещение"" КОНЕЦ),
		|	(ВЫБОР 
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КОНЕЦ),
		|	(ВЫБОР ДД.ХозяйственнаяОперация
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПереработчику)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеПереработчику)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПереработчику2_5)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеПереработчику)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КОНЕЦ),
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	(ВЫБОР
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтПереработчика)
		|			ТОГДА ДД.АналитикаУчетаНоменклатуры
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтПереработчика2_5)
		|			ТОГДА ДД.АналитикаУчетаНоменклатуры
		|		ИНАЧЕ ДД.КорАналитикаУчетаНоменклатуры КОНЕЦ),
		|	ДД.ДокументПоступления,
		|	ДД.КорВидЗапасов,
		|	(ВЫБОР 
		|		КОГДА ДД.Количество < 0 ТОГДА ДД.КорАналитикаУчетаНоменклатуры
		|		ИНАЧЕ ДД.АналитикаУчетаНоменклатуры КОНЕЦ),
		|	(ВЫБОР 
		|		КОГДА ДД.Количество < 0 ТОГДА ДД.КорВидЗапасов
		|		ИНАЧЕ ДД.ВидЗапасов КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтПереработчика)
		|			ТОГДА ДД.Регистратор
		|		КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтПереработчика2_5)
		|			ТОГДА ДД.Регистратор
		|		КОГДА ДД.Количество < 0 ТОГДА ДД.ДокументИсточник
		|		ИНАЧЕ ДД.Регистратор КОНЕЦ),
		|	ДД.НалогообложениеНДС,
		|	ДД.ХозяйственнаяОперация
		|";
КонецФункции



Функция ТекстИтерацияПартийРаботИПеремещенияВПроизводстве()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстИтерацияПартийРаботИПеремещенияВПроизводстве"" КАК ЗапросИсточник,
		|	(ВЫБОР 
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА 90
		|		ИНАЧЕ 10 КОНЕЦ) КАК Приоритет,
		|	(ВЫБОР 
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА ""Потребление""
		|		ИНАЧЕ ""Перемещение"" КОНЕЦ) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Период КАК Период,
		|	ДД.Регистратор КАК Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступленияРасходов,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|	СУММА(ДД.Количество) КАК Количество,
		|	0 КАК Знаменатель,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	(ВЫБОР
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			ТОГДА ДД.КорАналитикаУчетаНоменклатуры
		|		ИНАЧЕ ДД.АналитикаУчетаНоменклатуры КОНЕЦ) КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов КАК КорВидЗапасов,
		|	(ВЫБОР
		|		КОГДА ДД.ДокументИсточник = НЕОПРЕДЕЛЕНО ТОГДА ДД.ДокументПоступления
		|		ИНАЧЕ ДД.ДокументИсточник КОНЕЦ) КАК ДокументИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ДД.ПодразделениеРасходов КАК ПодразделениеРасходов,
		|	ДД.СтатьяРасходовСписания КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК ВариантРаспределенияРасходовУпр,
		|	НЕОПРЕДЕЛЕНО КАК ВариантРаспределенияРасходовРегл,
		|	ДД.АналитикаРасходов КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииПроизводственныхЗатрат КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Работы
		|		ПО Работы.Ссылка = ДД.АналитикаУчетаНоменклатуры.Номенклатура
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И (Работы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|		)
		|	И НЕ ДД.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|СГРУППИРОВАТЬ ПО
		|	(ВЫБОР 
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА 90
		|		ИНАЧЕ 10 КОНЕЦ),
		|	(ВЫБОР 
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА ""Потребление""
		|		ИНАЧЕ ""Перемещение"" КОНЕЦ),
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	(ВЫБОР
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			ТОГДА ДД.КорАналитикаУчетаНоменклатуры
		|		ИНАЧЕ ДД.АналитикаУчетаНоменклатуры КОНЕЦ),
		|	(ВЫБОР 
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА ДД.ДокументПоступления
		|		ИНАЧЕ ДД.Регистратор КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ДД.ДокументИсточник = НЕОПРЕДЕЛЕНО ТОГДА ДД.ДокументПоступления
		|		ИНАЧЕ ДД.ДокументИсточник КОНЕЦ),
		|	ДД.НалогообложениеНДС,
		|	ДД.ПодразделениеРасходов,
		|	ДД.СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов
		|";
КонецФункции

#КонецОбласти



#Область РасчетПартийПроизводственныхЗатрат

Функция ДанныеДляПартийПроизводства(НачалоПериода, ОкончаниеПериода, МассивОрганизаций)
	ТекстСортировка = "
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура, Приоритет ВОЗР, ПериодПоступления, Регистратор, Назначение
		|";
	ТекстИндексы = "
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|";	
	ИсходныйТекстЗапроса =
		ТекстПартииПроизводстваИнициализация() + ";" // вт МатериалыИРаботыВПроизводстве, ПрошлыеПоступления, ПрошлыеРеализации
		+ ТекстОписаниеДанныхДляПартийПроизводства()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстОстаткиПартийПроизводства() // использует ПрошлыеПоступления
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПервичныеПартииПроизводства()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПервичныеПеремещенияПартийПроизводства()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПоступленияВПроизводство()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПотребленияВПроизводстве()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстРеализацииРаботПрошлыхМесяцев() // использует ПрошлыеРеализации

		+ ТекстИндексы;
	МВТ = Новый МенеджерВременныхТаблиц;	
	ИсходныйЗапрос = Новый Запрос(СтрЗаменить(ИсходныйТекстЗапроса, "//ВоВременнуюТаблицу", ""));
	ИсходныйЗапрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	ИсходныйЗапрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);	
	ИсходныйЗапрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);	
	ИсходныйЗапрос.УстановитьПараметр("ПартионныйУчетВключен", РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии21(НачалоПериода));
	ИсходныйЗапрос.МенеджерВременныхТаблиц = МВТ;
	ИсходныйЗапрос.Выполнить();
	
	Запрос = Новый Запрос(ТекстОписаниеДанныхДляПартийПроизводства());
	РасчетныеПартии = Запрос.Выполнить().Выгрузить();
	
	ВременнаяТаблицаДанныхПоОтбору("Номенклатура", ТекстСортировка, РасчетныеПартии, МВТ);
	
	Возврат МВТ;
КонецФункции

Функция РасчетныеПартииПроизводства(ДанныеДляРасчета, Регистраторы, ВнутренниеДанные = Неопределено, Тест = Ложь)
	
	// Шаг 1: создаем буфер накопления рассчитанных партий
	Запрос = Новый Запрос(ТекстОписаниеДанныхДляПартийПроизводства());
	РасчетныеПартии = Запрос.Выполнить().Выгрузить();
	
	// Шаг 2: обсчитываем цепочки движений по передачам, возвратам, перемещениям, порчам, пересортицам и т.д.
	КонтекстЦепочек = ОписаниеЦепочек("Потребление, ПотреблениеАвто, Партия, Остаток, ОстатокСторно, Сторно, ПартияСторно, Перемещение, Списание, Дополнение, ВозвратСторно, Корректировка,
									|Прошлое, Возврат");
	
	ПоляПотреблений = "Организация, АналитикаУчетаНоменклатуры, Назначение";
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Потребление", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Партия", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Остаток", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Потребление", "Перемещение", ПоляПотреблений);
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "ПотреблениеАвто", "Организация, АналитикаУчетаНоменклатуры, ВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ПотреблениеАвто", "Партия", "Организация, АналитикаУчетаНоменклатуры, ВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ПотреблениеАвто", "Остаток", "Организация, АналитикаУчетаНоменклатуры, ВидЗапасов");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ПотреблениеАвто", "Перемещение", "Организация, АналитикаУчетаНоменклатуры, ВидЗапасов");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Корректировка", "ДокументПоступления, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Корректировка", "Остаток", "ДокументПоступления, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Корректировка", "Партия", "ДокументПоступления, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "ВозвратСторно", "ДокументИсточник, ДокументПоступления, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ВозвратСторно", "Остаток", "Регистратор, ДокументПоступления, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ВозвратСторно", "Партия", "Регистратор, ДокументПоступления, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Возврат", "ДокументИсточник, АналитикаУчетаНоменклатуры, Организация");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Возврат", "Прошлое", "Регистратор, АналитикаУчетаНоменклатуры, Организация");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Возврат", "Потребление", "Регистратор, АналитикаУчетаНоменклатуры, Организация");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Списание", "Регистратор, ДокументПоступления, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Списание", "Партия", "Регистратор, ДокументПоступления, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация");
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Сторно", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Сторно", "ОстатокСторно", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Сторно", "ПартияСторно", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Сторно", "Остаток", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Сторно", "Партия", ПоляПотреблений);
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Перемещение", "ДокументИсточник, АналитикаУчетаНоменклатуры");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Перемещение", "Потребление", "Регистратор, КорАналитикаУчетаНоменклатуры");
	
	// Шаг 3: замена ложных партий на первичные партии в расчетных строках по результатам построение графа замен.
	КонтекстДвижений = ОписаниеДвижений(
		"ПартииПроизводственныхЗатрат",
		"ПартииПроизводственныхЗатрат",
		ПереченьПолей(РасчетныеПартии.Колонки),
		ПоляПотреблений,
		"Количество, Стоимость, СтоимостьБезНДС, СтоимостьРегл, НДСРегл, ПостояннаяРазница, ВременнаяРазница",
		"Количество", "Количество", "ДокументИсточник", "Период"
		);
	КонтекстДвижений.Вставить("ЗаполнятьАналитикуПартий", Истина);
	ЦепочкиДвижений(КонтекстЦепочек, ДанныеДляРасчета);
	РассчитатьПартииПоЦепочкам(КонтекстДвижений, ДанныеДляРасчета, РасчетныеПартии, Регистраторы, Тест);
	ВнутренниеДанные = ДанныеДляРасчета;
	
	// Шаг 4: Удаляем нерассчитанные партии
	УдалитьНезаписываемыеСтроки(КонтекстДвижений.Контекст, РасчетныеПартии);
	
	Возврат РасчетныеПартии;
	
КонецФункции

Функция ТекстПартииПроизводстваИнициализация() // вт МатериалыИРаботыВПроизводстве, МатериалыИРаботыРегистраторы, ПрошлыеПоступления, ПрошлыеРеализации
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор КАК Регистратор,
		|	ДД.АналитикаУчетаНоменклатуры
		|ПОМЕСТИТЬ
		|	МатериалыИРаботыВПроизводстве
		|ИЗ
		|	РегистрНакопления.МатериалыИРаботыВПроизводстве КАК ДД
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ
		|	МатериалыИРаботыРегистраторы
		|ИЗ
		|	РегистрНакопления.МатериалыИРаботыВПроизводстве КАК ДД
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ДокументПоступления КАК Ссылка,
		|	ДД.АналитикаУчетаНоменклатуры.МестоХранения КАК Склад,
		|	МАКСИМУМ(Периодика.Регистратор) КАК Регистратор,
		|	МАКСИМУМ(Периодика.Период) КАК Период
		|ПОМЕСТИТЬ
		|	ПрошлыеПоступления
		|ИЗ
		|	РегистрНакопления.ПартииПроизводственныхЗатрат.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииПроизводственныхЗатрат КАК Периодика
		|		ПО Периодика.Период < &НачалоПериода
		|		И Периодика.ДокументПоступления = ДД.ДокументПоступления
		|		И Периодика.АналитикаУчетаНоменклатуры.МестоХранения = ДД.АналитикаУчетаНоменклатуры.МестоХранения
		|ГДЕ
		|	ДД.ДокументПоступления <> НЕОПРЕДЕЛЕНО
		|	И Периодика.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И Периодика.Количество > 0
		|СГРУППИРОВАТЬ ПО
		|	ДД.ДокументПоступления,
		|	ДД.АналитикаУчетаНоменклатуры.МестоХранения
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Корректировка.ДокументОснование КАК ДокументРеализации,
		|	ДД.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
		|ПОМЕСТИТЬ
		|	ПрошлыеРеализации
		|ИЗ
		|	РегистрНакопления.МатериалыИРаботыВПроизводстве КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК Корректировка
		|		ПО Корректировка.Ссылка = ДД.Регистратор
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.МатериалыИРаботыВПроизводстве КАК Расход
		|		ПО Расход.Период < &НачалоПериода
		|		И Расход.Регистратор = Корректировка.ДокументОснование
		|		И Расход.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ДД.Количество < 0
		|";
КонецФункции

Функция ТекстОписаниеДанныхДляПартийПроизводства()
	Возврат "
		|ВЫБРАТЬ ПЕРВЫЕ 0
		|	ВЫРАЗИТЬ("""" КАК СТРОКА(80)) КАК ЗапросИсточник,
		|	0 КАК Приоритет,
		|	""ХХХХХХХХХХХХХХХХ"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК Период,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	0 КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ПериодПоступления,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК КорРазделУчета,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДатаРегистратора,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ДД.ДокументИсточник,
		|	ДД.СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов,
		|	ДД.ПодразделениеРасходов,
		|	0 КАК НомерГруппыЗатрат,
		|	ДД.АналитикаАктивовПассивов,
		|	ДД.ИдентификаторСтроки,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
		|	ДД.СтатьяСписанияНДС,
		|	ДД.АналитикаСписанияНДС
		|//ВоВременнуюТаблицу ПОМЕСТИТЬ ИсходныеДанные
		|ИЗ
		|	РегистрНакопления.ПартииПроизводственныхЗатрат КАК ДД
		|";
КонецФункции

Функция ТекстОстаткиПартийПроизводства() // использует ПрошлыеПоступления
	// передачи материалов в производство и в переработку, возвраты материалов из производства и от переработчика.
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстОстаткиПартийПроизводства"" КАК ЗапросИсточник,
		|	(ВЫБОР
		|		КОГДА ДД.КоличествоОстаток < 0 ТОГДА 5
		|		ИНАЧЕ 10 КОНЕЦ) КАК Приоритет,
		|	(ВЫБОР
		|		КОГДА ДД.КоличествоОстаток < 0 ТОГДА ""ОстатокСторно""
		|		ИНАЧЕ ""Остаток"" КОНЕЦ) КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ЕСТЬNULL(Периодика.Период, &НачалоПериода) КАК Период,
		|	ЕСТЬNULL(Периодика.Регистратор, ДД.ДокументПоступления) КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.КоличествоОстаток КАК Количество,
		|	ДД.СтоимостьОстаток КАК Стоимость,
		|	ДД.СтоимостьБезНДСОстаток КАК СтоимостьБезНДС,
		|	ДД.СтоимостьРеглОстаток КАК СтоимостьРегл,
		|	ДД.НДСРеглОстаток КАК НДСРегл,
		|	ДД.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
		|	ДД.ВременнаяРазницаОстаток КАК ВременнаяРазница,
		|	ИСТИНА КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ЕСТЬNULL(Периодика.Период, &НачалоПериода) КАК ПериодПоступления,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК КорРазделУчета,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДатаРегистратора,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	ЕСТЬNULL(ДД.АналитикаУчетаНоменклатуры.Назначение, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)) КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	0 КАК НомерГруппыЗатрат,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
		|	Аналитика.Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииПроизводственныхЗатрат.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПрошлыеПоступления КАК Периодика
		|		ПО Периодика.Ссылка = ДД.ДокументПоступления
		|		И Периодика.Склад = ДД.АналитикаУчетаНоменклатуры.МестоХранения
		|ГДЕ
		|	ДД.КоличествоОстаток <> 0
		|";
КонецФункции

Функция ТекстПоступленияВПроизводство()
	// Передачи материалов в производство и в переработку, возвраты материалов из производства и от переработчика.
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПоступленияВПроизводство"" КАК ЗапросИсточник,
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА 5
		|		ИНАЧЕ 10 КОНЕЦ) КАК Приоритет,
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА ""ВозвратСторно""
		|		ИНАЧЕ ""Партия"" КОНЕЦ) КАК ТипЗаписи,
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА КОНЕЦ) КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
		|	ДД.Организация,
		|	ДД.КорАналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	(ВЫБОР
		|		КОГДА ДД.КорВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
		|		 И ДД.КорВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
		|			ТОГДА ДД.КорВидЗапасов
		|		ИНАЧЕ ДД.ВидЗапасов КОНЕЦ) КАК ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(ДД.Стоимость) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ВЫБОР
		|			КОГДА ДД.СтатьяСписанияНДС <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|			ТОГДА 0
		|			ИНАЧЕ ДД.НДСРегл
		|		КОНЕЦ) КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	ИСТИНА КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА &НачалоПериода
		|		ИНАЧЕ ДД.Период КОНЕЦ) КАК ПериодПоступления,
		|	ДД.ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КАК КорРазделУчета,
		|	ДД.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов КАК КорВидЗапасов,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДатаРегистратора,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	ЕСТЬNULL(ДД.КорАналитикаУчетаНоменклатуры.Назначение, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)) КАК Назначение,
		|	ДД.ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	0 КАК НомерГруппыЗатрат,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
		|	Аналитика.Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ МатериалыИРаботыВПроизводстве КАК Затраты
		|		ПО Затраты.Регистратор = ДД.Регистратор
		|		И Затраты.АналитикаУчетаНоменклатуры = ДД.КорАналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК СпрВидыЗапасов
		|		ПО СпрВидыЗапасов.Ссылка = ДД.ВидЗапасов
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И НЕ ДД.Первичное
		|	И ЕСТЬNULL(СпрВидыЗапасов.ТипЗапасов, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
		|	И &ПартионныйУчетВключен
		|СГРУППИРОВАТЬ ПО
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА 5
		|		ИНАЧЕ 10 КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА ""ВозвратСторно""
		|		ИНАЧЕ ""Партия"" КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА КОНЕЦ),
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	(ВЫБОР
		|		КОГДА ДД.КорВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
		|		 И ДД.КорВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
		|			ТОГДА ДД.КорВидЗапасов
		|		ИНАЧЕ ДД.ВидЗапасов КОНЕЦ),
		|	ДД.АналитикаУчетаПартий,
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА &НачалоПериода
		|		ИНАЧЕ ДД.Период КОНЕЦ),
		|	ДД.ХозяйственнаяОперация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов,
		|	ДД.НалогообложениеНДС,
		|	ЕСТЬNULL(ДД.КорАналитикаУчетаНоменклатуры.Назначение, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)),
		|	ДД.ДокументИсточник,
		|	Аналитика.Номенклатура
		|";
КонецФункции

Функция ТекстПервичныеПартииПроизводства()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПервичныеПартииПроизводства"" КАК ЗапросИсточник,
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА 5
		|		ИНАЧЕ 10 КОНЕЦ) КАК Приоритет,
		|	(ВЫБОР
		|		КОГДА НЕ Корректировка.Ссылка ЕСТЬ NULL И ДД.Количество <= 0 ТОГДА ""Корректировка""
		|		КОГДА ДД.Количество < 0 ТОГДА ""ПартияСторно""
		|		ИНАЧЕ ""Партия"" КОНЕЦ) КАК ТипЗаписи,
		|	(ВЫБОР
		|		КОГДА НЕ Корректировка.Ссылка ЕСТЬ NULL И ДД.Количество <= 0 ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА КОНЕЦ) КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(ДД.Стоимость) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.НДСРегл) КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	ИСТИНА КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	(ВЫБОР
		|		КОГДА НЕ Корректировка.Ссылка ЕСТЬ NULL ТОГДА Поступление.Дата
		|		ИНАЧЕ ДД.Период КОНЕЦ) КАК ПериодПоступления,
		|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасов,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДатаРегистратора,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	НЕОПРЕДЕЛЕНО КАК НалогообложениеНДС,
		|	ЕСТЬNULL(ДД.АналитикаУчетаНоменклатуры.Назначение, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)) КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	0 КАК НомерГруппыЗатрат,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
		|	Аналитика.Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииПроизводственныхЗатрат КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения КАК Корректировка
		|		ПО Корректировка.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК Поступление
		|		ПО Поступление.Ссылка = ДД.ДокументПоступления
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И (ДД.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|		ИЛИ ДД.Регистратор ССЫЛКА Документ.КорректировкаПриобретения
		|		ИЛИ ДД.Регистратор ССЫЛКА Документ.ВводОстатков
		|		)
		|СГРУППИРОВАТЬ ПО
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА 5
		|		ИНАЧЕ 10 КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА НЕ Корректировка.Ссылка ЕСТЬ NULL И ДД.Количество <= 0 ТОГДА ""Корректировка""
		|		КОГДА ДД.Количество < 0 ТОГДА ""ПартияСторно""
		|		ИНАЧЕ ""Партия"" КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА НЕ Корректировка.Ссылка ЕСТЬ NULL И ДД.Количество <= 0 ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА КОНЕЦ),
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ЕСТЬNULL(ДД.АналитикаУчетаНоменклатуры.Назначение, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)),
		|	(ВЫБОР
		|		КОГДА НЕ Корректировка.Ссылка ЕСТЬ NULL ТОГДА Поступление.Дата
		|		ИНАЧЕ ДД.Период КОНЕЦ),
		|	ДД.АналитикаУчетаПартий,
		|	Аналитика.Номенклатура
		|";
КонецФункции

Функция ТекстПервичныеПеремещенияПартийПроизводства()
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПервичныеПеремещенияПартийПроизводства"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	""Перемещение"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	СУММА(ДД.Количество) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.НДСРегл) КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ИСТИНА КАК Первичное,
		|	ИСТИНА КАК СохранятьРегл,
		|	ДД.Период КАК ПериодПоступления,
		|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасов,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДатаРегистратора,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	НЕОПРЕДЕЛЕНО КАК НалогообложениеНДС,
		|	ЕСТЬNULL(ДД.АналитикаУчетаНоменклатуры.Назначение, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)) КАК Назначение,
		|	ДД.Регистратор КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	0 КАК НомерГруппыЗатрат,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
		|	Аналитика.Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииПроизводственныхЗатрат КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ДД.Регистратор ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ЕСТЬNULL(ДД.АналитикаУчетаНоменклатуры.Назначение, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)),
		|	ДД.АналитикаУчетаПартий,
		|	Аналитика.Номенклатура
		|";
КонецФункции

Функция ТекстПотребленияВПроизводстве()
	// распределение материалов на выпуск, списание
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстПотребленияВПроизводстве"" КАК ЗапросИсточник,
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА 35
		|		ИНАЧЕ 40 КОНЕЦ) КАК Приоритет,
		|	(ВЫБОР
		|		КОГДА НЕ Корректировка.Ссылка ЕСТЬ NULL И ДД.Количество < 0
		|			ТОГДА ""Возврат""
		|		КОГДА ДД.Количество < 0 ТОГДА ""Сторно""
		|		ИНАЧЕ ""Потребление"" КОНЕЦ) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	СУММА(ДД.Количество) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА ДОБАВИТЬКДАТЕ(&НачалоПериода, СЕКУНДА, -1)
		|		ИНАЧЕ ДД.Период КОНЕЦ) КАК ПериодПоступления,
		|	(ВЫБОР
		|		КОГДА НЕ АналитикаПолучатель.КлючАналитики ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеТоваров)
		|		КОГДА ДД.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|		 И ДД.СтатьяРасходов <> НЕОПРЕДЕЛЕНО
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваров)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КОНЕЦ) КАК ХозяйственнаяОперация,
		|	(ВЫБОР
		|		КОГДА НЕ АналитикаПолучатель.КлючАналитики ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ) КАК КорРазделУчета,
		|	(ВЫБОР КОГДА НЕ Передача.Ссылка ЕСТЬ NULL ТОГДА ДД.АналитикаУчетаНоменклатуры
		|		ИНАЧЕ ЕСТЬNULL(АналитикаПолучатель.КлючАналитики, ДД.АналитикаУчетаПродукции) КОНЕЦ) КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДатаРегистратора,
		|	ДД.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	ДД.Назначение,
		|	ЕСТЬNULL(Корректировка.ДокументОснование, НЕОПРЕДЕЛЕНО) КАК ДокументИсточник,
		|	ДД.СтатьяРасходов КАК СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов,
		|	ДД.ПодразделениеПолучатель КАК ПодразделениеРасходов,
		|	ВЫБОР
		|		КОГДА ИСТИНА
		|			ТОГДА 0
		|	КОНЕЦ КАК НомерГруппыЗатрат,
		|	ДД.АналитикаАктивовПассивов,
		|	ДД.ИдентификаторСтроки,
		|	ДД.Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.МатериалыИРаботыВПроизводстве КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПолучатель
		|		ПО АналитикаПолучатель.Номенклатура = ДД.АналитикаУчетаНоменклатуры.Номенклатура
		|		И АналитикаПолучатель.Характеристика = ДД.АналитикаУчетаНоменклатуры.Характеристика
		|		И АналитикаПолучатель.Серия = ДД.АналитикаУчетаНоменклатуры.Серия
		|		И АналитикаПолучатель.МестоХранения = ДД.ПодразделениеПолучатель
		|		И АналитикаПолучатель.Назначение = ДД.Назначение
		|		И ДД.ПодразделениеПолучатель <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровМеждуОрганизациями КАК Передача
		|		ПО Передача.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК Корректировка
		|		ПО Корректировка.Ссылка = ДД.Регистратор

		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|СГРУППИРОВАТЬ ПО
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА 35
		|		ИНАЧЕ 40 КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА НЕ Корректировка.Ссылка ЕСТЬ NULL И ДД.Количество < 0
		|			ТОГДА ""Возврат""
		|		КОГДА ДД.Количество < 0 ТОГДА ""Сторно""
		|		ИНАЧЕ ""Потребление"" КОНЕЦ),
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА ДОБАВИТЬКДАТЕ(&НачалоПериода, СЕКУНДА, -1)
		|		ИНАЧЕ ДД.Период КОНЕЦ,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	(ВЫБОР
		|		КОГДА НЕ АналитикаПолучатель.КлючАналитики ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеТоваров)
		|		КОГДА ДД.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|		 И ДД.СтатьяРасходов <> НЕОПРЕДЕЛЕНО
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваров)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА НЕ АналитикаПолучатель.КлючАналитики ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ),
		|	(ВЫБОР КОГДА НЕ Передача.Ссылка ЕСТЬ NULL ТОГДА ДД.АналитикаУчетаНоменклатуры
		|		ИНАЧЕ ЕСТЬNULL(АналитикаПолучатель.КлючАналитики, ДД.АналитикаУчетаПродукции) КОНЕЦ),
		|	ДД.НалогообложениеНДС,
		|	ДД.Назначение,
		|	ЕСТЬNULL(Корректировка.ДокументОснование, НЕОПРЕДЕЛЕНО),
		|	ВЫБОР
		|		КОГДА ИСТИНА
		|			ТОГДА 0
		|	КОНЕЦ,
		|	ДД.СтатьяРасходов,
		|	ДД.АналитикаРасходов,
		|	ДД.ПодразделениеПолучатель,
		|	ДД.АналитикаАктивовПассивов,
		|	ДД.ИдентификаторСтроки,
		|	ДД.Номенклатура
		|";
КонецФункции

Функция ТекстРеализацииРаботПрошлыхМесяцев() // использует ПрошлыеРеализации
	Возврат "
		|ВЫБРАТЬ
		|	""ТекстРеализацииРаботПрошлыхМесяцев"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	""Прошлое"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ДД.ВидДвижения КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(ДД.Стоимость) КАК Стоимость,
		|	СУММА(ДД.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.НДСРегл) КАК НДСРегл,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	ИСТИНА КАК Первичное,
		|	ИСТИНА КАК СохранятьРегл,
		|	ДД.Период КАК ПериодПоступления,
		|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
		|	ДД.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов КАК КорВидЗапасов,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДатаРегистратора,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ДД.НалогообложениеНДС,
		|	Аналитика.Назначение КАК Назначение,
		|	ДД.Регистратор КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеРасходов,
		|	0. КАК НомерГруппыЗатрат,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
		|	Аналитика.Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяСписанияНДС,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаСписанияНДС
		|ИЗ
		|	РегистрНакопления.ПартииПроизводственныхЗатрат КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрошлыеРеализации КАК Прошлые
		|		ПО ДД.Регистратор = Прошлые.ДокументРеализации
		|		И ДД.АналитикаУчетаНоменклатуры = Прошлые.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|СГРУППИРОВАТЬ ПО
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.АналитикаУчетаПартий,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС,
		|	ДД.НалогообложениеНДС,
		|	Аналитика.Назначение,
		|	Аналитика.Номенклатура
		|";
КонецФункции

#КонецОбласти


#Область РасчетПриходыПартийНДСКРаспределению

Функция ДанныеДляПартийНДСКРаспределению(НачалоПериода, ОкончаниеПериода, МассивОрганизаций)
	ТекстСортировка = "";
	ИсходныйТекстЗапроса =
		ТекстОписаниеДанныхДляПартийНДСКРаспределению()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПоступленияПартийНДСКРаспределениюИзПартий()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПоступленияПартийНДСКРаспределениюИзДопРасходов()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПоступленияПартийНДСКРаспределениюИзЗатратНаВыпуск()
		+ ТекстСортировка;
	ИсходныйЗапрос = Новый Запрос(ИсходныйТекстЗапроса);
	ИсходныйЗапрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	ИсходныйЗапрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	ИсходныйЗапрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);	
	ДанныеДляРасчета = ИсходныйЗапрос.Выполнить().Выгрузить();
	ДанныеДляРасчета.Сортировать("Регистратор", Новый СравнениеЗначений);
	
	Возврат ДанныеДляРасчета;
КонецФункции

Функция ТекстОписаниеДанныхДляПартийНДСКРаспределению()
	Возврат "
		|ВЫБРАТЬ ПЕРВЫЕ 0
		|	0 КАК Приоритет,
		|	""ХХХХХХХХХХХХХХХХ"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ДД.НаправлениеДеятельности,
		|	ДД.СтатьяРасходов,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	ДД.Поставщик,
		|	ДД.ДокументПоступленияРасходов,
		|	ДД.ВидДеятельностиНДС,
		|	ДД.ВидЦенности,
		|	ДД.СтавкаНДС,
		|	ДД.СтоимостьРегл,
		|	ДД.НДСРегл
		|ИЗ
		|	РегистрНакопления.ПартииНДСКРаспределению КАК ДД
		|";
КонецФункции

Функция ТекстПоступленияПартийНДСКРаспределениюИзПартий()
	Возврат "
		|ВЫБРАТЬ
		|	10 КАК Приоритет,
		|	""Партия"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ВЫБОР
		|		КОГДА НЕ Потребление.Подразделение ЕСТЬ NULL
		|			ТОГДА Потребление.Подразделение
		|		ИНАЧЕ Сторно.Подразделение
		|	КОНЕЦ КАК Подразделение,
		|	ЕСТЬNULL(Потребление.НаправлениеДеятельности,
		|		ЕСТЬNULL(Назначения.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))) КАК НаправлениеДеятельности,
		|	ДД.СтатьяРасходовСписания КАК СтатьяРасходов,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	ДД.АналитикаУчетаПартий.Контрагент КАК Поставщик,
		|	ДД.ДокументПоступления КАК ДокументПоступленияРасходов,
		|	(ВЫБОР
		|		КОГДА ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию)
		|			ТОГДА ДД.НалогообложениеНДС
		|		КОГДА Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
		|		 И Статья.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС) ТОГДА
		|			(ВЫБОР
		|				КОГДА Статья.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ИзДокумента)
		|				 И ДД.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|					ТОГДА ДД.НалогообложениеНДС
		|				КОГДА Статья.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ИзДокумента)
		|				 И ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|					ТОГДА ДД.АналитикаУчетаПартий.НалогообложениеНДС
		|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию) КОНЕЦ)
		|		КОГДА Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
		|		 И Статья.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НМА)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию)
		|		КОГДА ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|			ТОГДА ДД.АналитикаУчетаПартий.НалогообложениеНДС
		|		ИНАЧЕ ДД.НалогообложениеНДС КОНЕЦ) КАК ВидДеятельностиНДС,
		|	(ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ДД.ДокументПоступления) = ТИП(Документ.ТаможеннаяДекларацияИмпорт)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТаможенныеПлатежи)
		|		КОГДА Аналитика.Номенклатура.ТипНоменклатуры В (
		|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
		|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|			ТОГДА (ВЫБОР
		|				КОГДА ЕСТЬNULL(Аналитика.Номенклатура.ГруппаФинансовогоУчета.ВидЦенностиНДС,
		|						ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
		|					ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
		|				ИНАЧЕ Аналитика.Номенклатура.ГруппаФинансовогоУчета.ВидЦенностиНДС КОНЕЦ)
		|		КОГДА ЕСТЬNULL(Статья.ВидЦенностиНДС, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)) <>
		|				ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
		|			ТОГДА Статья.ВидЦенностиНДС
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги) КОНЕЦ) КАК ВидЦенности,
		|	ДД.АналитикаУчетаПартий.СтавкаНДС КАК СтавкаНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.НДСРегл) КАК НДСРегл
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК Назначения
		|		ПО Назначения.Ссылка = Аналитика.Назначение
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВнутреннееПотребление КАК Потребление
		|		ПО Потребление.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПрочееОприходованиеТоваров КАК Сторно
		|		ПО Сторно.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статья
		|		ПО Статья.Ссылка = ДД.СтатьяРасходовСписания
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиАктивовПассивов КАК СтатьяАктивов
		|		ПО СтатьяАктивов.Ссылка = ДД.СтатьяРасходовСписания
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И (Статья.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.Распределение)
		|		ИЛИ Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
		|			И Статья.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС)
		|		ИЛИ ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
		|		ИЛИ НЕ СтатьяАктивов.Ссылка ЕСТЬ NULL
		|			И ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))
		|	И ДД.НДСРегл <> 0
		|	И ДД.АналитикаУчетаПартий <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ВЫБОР
		|		КОГДА НЕ Потребление.Подразделение ЕСТЬ NULL
		|			ТОГДА Потребление.Подразделение
		|		ИНАЧЕ Сторно.Подразделение
		|	КОНЕЦ,
		|	ЕСТЬNULL(Потребление.НаправлениеДеятельности,
		|		ЕСТЬNULL(Назначения.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))),
		|	ДД.СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	ДД.АналитикаУчетаПартий.Контрагент,
		|	ДД.ДокументПоступления,
		|	(ВЫБОР
		|		КОГДА ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию)
		|			ТОГДА ДД.НалогообложениеНДС
		|		КОГДА Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
		|		 И Статья.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС) ТОГДА
		|			(ВЫБОР
		|				КОГДА Статья.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ИзДокумента)
		|				 И ДД.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|					ТОГДА ДД.НалогообложениеНДС
		|				КОГДА Статья.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ИзДокумента)
		|				 И ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|					ТОГДА ДД.АналитикаУчетаПартий.НалогообложениеНДС
		|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию) КОНЕЦ)
		|		КОГДА Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
		|		 И Статья.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НМА)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию)
		|		КОГДА ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|			ТОГДА ДД.АналитикаУчетаПартий.НалогообложениеНДС
		|		ИНАЧЕ ДД.НалогообложениеНДС КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ДД.ДокументПоступления) = ТИП(Документ.ТаможеннаяДекларацияИмпорт)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТаможенныеПлатежи)
		|		КОГДА Аналитика.Номенклатура.ТипНоменклатуры В (
		|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
		|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|			ТОГДА (ВЫБОР
		|				КОГДА ЕСТЬNULL(Аналитика.Номенклатура.ГруппаФинансовогоУчета.ВидЦенностиНДС,
		|						ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
		|					ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
		|				ИНАЧЕ Аналитика.Номенклатура.ГруппаФинансовогоУчета.ВидЦенностиНДС КОНЕЦ)
		|		КОГДА ЕСТЬNULL(Статья.ВидЦенностиНДС, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)) <>
		|				ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
		|			ТОГДА Статья.ВидЦенностиНДС
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги) КОНЕЦ),
		|	ДД.АналитикаУчетаПартий.СтавкаНДС
		|";
КонецФункции
	
Функция ТекстПоступленияПартийНДСКРаспределениюИзДопРасходов()
	Возврат "
		|ВЫБРАТЬ
		|	10 КАК Приоритет,
		|	""Партия"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ВЫБОР
		|		КОГДА НЕ Потребление.Подразделение ЕСТЬ NULL
		|			ТОГДА Потребление.Подразделение
		|		ИНАЧЕ Сторно.Подразделение
		|	КОНЕЦ КАК Подразделение,
		|	ЕСТЬNULL(Потребление.НаправлениеДеятельности,
		|		ЕСТЬNULL(Назначения.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))) КАК НаправлениеДеятельности,
		|	ДД.СтатьяРасходовСписания КАК СтатьяРасходов,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	ДД.АналитикаУчетаПартий.Контрагент КАК Поставщик,
		|	ДД.ДокументПоступленияРасходов,
		|	(ВЫБОР
		|		КОГДА ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию)
		|			ТОГДА ДД.НалогообложениеНДС
		|		КОГДА Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
		|		 И Статья.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС) ТОГДА
		|			(ВЫБОР
		|				КОГДА Статья.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ИзДокумента)
		|				 И ДД.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|					ТОГДА ДД.НалогообложениеНДС
		|				КОГДА Статья.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ИзДокумента)
		|				 И ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|					ТОГДА ДД.АналитикаУчетаПартий.НалогообложениеНДС
		|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию) КОНЕЦ)
		|		КОГДА Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
		|		 И Статья.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НМА)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию)
		|		КОГДА ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|			ТОГДА ДД.АналитикаУчетаПартий.НалогообложениеНДС
		|		ИНАЧЕ ДД.НалогообложениеНДС КОНЕЦ) КАК ВидДеятельностиНДС,
		|	(ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ДД.ДокументПоступления) = ТИП(Документ.ТаможеннаяДекларацияИмпорт)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТаможенныеПлатежи)
		|		КОГДА Аналитика.Номенклатура.ТипНоменклатуры В (
		|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
		|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|			ТОГДА (ВЫБОР
		|				КОГДА ЕСТЬNULL(Аналитика.Номенклатура.ГруппаФинансовогоУчета.ВидЦенностиНДС,
		|						ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
		|					ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
		|				ИНАЧЕ Аналитика.Номенклатура.ГруппаФинансовогоУчета.ВидЦенностиНДС КОНЕЦ)
		|		КОГДА ЕСТЬNULL(Статья.ВидЦенностиНДС, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)) <>
		|				ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
		|			ТОГДА Статья.ВидЦенностиНДС
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги) КОНЕЦ) КАК ВидЦенности,
		|	ДД.АналитикаУчетаПартий.СтавкаНДС КАК СтавкаНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.НДСРегл) КАК НДСРегл
		|ИЗ
		|	РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК Назначения
		|		ПО Назначения.Ссылка = Аналитика.Назначение
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВнутреннееПотребление КАК Потребление
		|		ПО Потребление.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПрочееОприходованиеТоваров КАК Сторно
		|		ПО Сторно.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статья
		|		ПО Статья.Ссылка = ДД.СтатьяРасходовСписания
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиАктивовПассивов КАК СтатьяАктивов
		|		ПО СтатьяАктивов.Ссылка = ДД.СтатьяРасходовСписания
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И (Статья.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.Распределение)
		|		ИЛИ Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
		|			И Статья.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС)
		|		ИЛИ ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
		|		ИЛИ НЕ СтатьяАктивов.Ссылка ЕСТЬ NULL
		|			И ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))
		|	И ДД.НДСРегл <> 0
		|	И ДД.АналитикаУчетаПартий <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)

		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ВЫБОР
		|		КОГДА НЕ Потребление.Подразделение ЕСТЬ NULL
		|			ТОГДА Потребление.Подразделение
		|		ИНАЧЕ Сторно.Подразделение
		|	КОНЕЦ,
		|	ЕСТЬNULL(Потребление.НаправлениеДеятельности,
		|		ЕСТЬNULL(Назначения.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))),
		|	ДД.СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	ДД.АналитикаУчетаПартий.Контрагент,
		|	ДД.ДокументПоступленияРасходов,
		|	(ВЫБОР
		|		КОГДА ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию)
		|			ТОГДА ДД.НалогообложениеНДС
		|		КОГДА Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
		|		 И Статья.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС) ТОГДА
		|			(ВЫБОР
		|				КОГДА Статья.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ИзДокумента)
		|				 И ДД.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|					ТОГДА ДД.НалогообложениеНДС
		|				КОГДА Статья.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ИзДокумента)
		|				 И ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|					ТОГДА ДД.АналитикаУчетаПартий.НалогообложениеНДС
		|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию) КОНЕЦ)
		|		КОГДА Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
		|		 И Статья.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НМА)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию)
		|		КОГДА ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|			ТОГДА ДД.АналитикаУчетаПартий.НалогообложениеНДС
		|		ИНАЧЕ ДД.НалогообложениеНДС КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ДД.ДокументПоступления) = ТИП(Документ.ТаможеннаяДекларацияИмпорт)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТаможенныеПлатежи)
		|		КОГДА Аналитика.Номенклатура.ТипНоменклатуры В (
		|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
		|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|			ТОГДА (ВЫБОР
		|				КОГДА ЕСТЬNULL(Аналитика.Номенклатура.ГруппаФинансовогоУчета.ВидЦенностиНДС,
		|						ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
		|					ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
		|				ИНАЧЕ Аналитика.Номенклатура.ГруппаФинансовогоУчета.ВидЦенностиНДС КОНЕЦ)
		|		КОГДА ЕСТЬNULL(Статья.ВидЦенностиНДС, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)) <>
		|				ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
		|			ТОГДА Статья.ВидЦенностиНДС
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги) КОНЕЦ),
		|	ДД.АналитикаУчетаПартий.СтавкаНДС
		|";
КонецФункции

Функция ТекстПоступленияПартийНДСКРаспределениюИзЗатратНаВыпуск()
	Возврат "
		|ВЫБРАТЬ
		|	10 КАК Приоритет,
		|	""Партия"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ЕСТЬNULL(Потребление.Подразделение, Сторно.Подразделение) КАК Подразделение,
		|	ЕСТЬNULL(Потребление.НаправлениеДеятельности,
		|		ЕСТЬNULL(Назначения.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))) КАК НаправлениеДеятельности,
		|	ДД.СтатьяРасходовСписания КАК СтатьяРасходов,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	ДД.АналитикаУчетаПартий.Контрагент КАК Поставщик,
		|	ДД.ДокументПоступления,
		|	(ВЫБОР
		|		КОГДА ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию)
		|			ТОГДА ДД.НалогообложениеНДС
		|		КОГДА Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
		|		 И Статья.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС) ТОГДА
		|			(ВЫБОР
		|				КОГДА Статья.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ИзДокумента)
		|				 И ДД.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|					ТОГДА ДД.НалогообложениеНДС
		|				КОГДА Статья.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ИзДокумента)
		|				 И ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|					ТОГДА ДД.АналитикаУчетаПартий.НалогообложениеНДС
		|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию) КОНЕЦ)
		|		КОГДА Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
		|		 И Статья.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НМА)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию)
		|		КОГДА ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|			ТОГДА ДД.АналитикаУчетаПартий.НалогообложениеНДС
		|		ИНАЧЕ ДД.НалогообложениеНДС КОНЕЦ) КАК ВидДеятельностиНДС,
		|	(ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ДД.ДокументПоступления) = ТИП(Документ.ТаможеннаяДекларацияИмпорт)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТаможенныеПлатежи)
		|		КОГДА Аналитика.Номенклатура.ТипНоменклатуры В (
		|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
		|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|			ТОГДА (ВЫБОР
		|				КОГДА ЕСТЬNULL(Аналитика.Номенклатура.ГруппаФинансовогоУчета.ВидЦенностиНДС,
		|						ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
		|					ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
		|				ИНАЧЕ Аналитика.Номенклатура.ГруппаФинансовогоУчета.ВидЦенностиНДС КОНЕЦ)
		|		КОГДА ЕСТЬNULL(Статья.ВидЦенностиНДС, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)) <>
		|				ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
		|			ТОГДА Статья.ВидЦенностиНДС
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги) КОНЕЦ) КАК ВидЦенности,
		|	ДД.АналитикаУчетаПартий.СтавкаНДС КАК СтавкаНДС,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.НДСРегл) КАК НДСРегл
		|ИЗ
		|	РегистрНакопления.ПартииЗатратНаВыпуск КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК Назначения
		|		ПО Назначения.Ссылка = Аналитика.Назначение
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВнутреннееПотребление КАК Потребление
		|		ПО Потребление.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПрочееОприходованиеТоваров КАК Сторно
		|		ПО Сторно.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статья
		|		ПО Статья.Ссылка = ДД.СтатьяРасходовСписания
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиАктивовПассивов КАК СтатьяАктивов
		|		ПО СтатьяАктивов.Ссылка = ДД.СтатьяРасходовСписания
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И (Статья.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.Распределение)
		|		ИЛИ Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
		|			И Статья.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС)
		|		ИЛИ ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
		|		ИЛИ НЕ СтатьяАктивов.Ссылка ЕСТЬ NULL
		|			И ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))
		|	И ДД.НДСРегл <> 0
		|	И ДД.АналитикаУчетаПартий <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)

		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ЕСТЬNULL(Потребление.Подразделение, Сторно.Подразделение),
		|	ЕСТЬNULL(Потребление.НаправлениеДеятельности,
		|		ЕСТЬNULL(Назначения.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))),
		|	ДД.СтатьяРасходовСписания,
		|	ДД.АналитикаРасходов,
		|	ДД.АналитикаАктивовПассивов,
		|	ДД.АналитикаУчетаПартий.Контрагент,
		|	ДД.ДокументПоступления,
		|	(ВЫБОР
		|		КОГДА ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию)
		|			ТОГДА ДД.НалогообложениеНДС
		|		КОГДА Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
		|		 И Статья.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС) ТОГДА
		|			(ВЫБОР
		|				КОГДА Статья.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ИзДокумента)
		|				 И ДД.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|					ТОГДА ДД.НалогообложениеНДС
		|				КОГДА Статья.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ИзДокумента)
		|				 И ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|					ТОГДА ДД.АналитикаУчетаПартий.НалогообложениеНДС
		|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию) КОНЕЦ)
		|		КОГДА Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
		|		 И Статья.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НМА)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию)
		|		КОГДА ДД.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|			ТОГДА ДД.АналитикаУчетаПартий.НалогообложениеНДС
		|		ИНАЧЕ ДД.НалогообложениеНДС КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ДД.ДокументПоступления) = ТИП(Документ.ТаможеннаяДекларацияИмпорт)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТаможенныеПлатежи)
		|		КОГДА Аналитика.Номенклатура.ТипНоменклатуры В (
		|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
		|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|			ТОГДА (ВЫБОР
		|				КОГДА ЕСТЬNULL(Аналитика.Номенклатура.ГруппаФинансовогоУчета.ВидЦенностиНДС,
		|						ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
		|					ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
		|				ИНАЧЕ Аналитика.Номенклатура.ГруппаФинансовогоУчета.ВидЦенностиНДС КОНЕЦ)
		|		КОГДА ЕСТЬNULL(Статья.ВидЦенностиНДС, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)) <>
		|				ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
		|			ТОГДА Статья.ВидЦенностиНДС
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги) КОНЕЦ),
		|	ДД.АналитикаУчетаПартий.СтавкаНДС
		|";
КонецФункции

#КонецОбласти


#Область Тестирование

Функция ТестТекстОписание(ИмяМетода) Экспорт
	Если ИмяМетода = "ТекстОписаниеПартийТоваров" Тогда
		Возврат ТекстОписаниеПартийТоваров();
		
	ИначеЕсли ИмяМетода = "ТекстОписаниеПартийПереданных" Тогда
		Возврат ТекстОписаниеПартийПереданных();
		
	ИначеЕсли ИмяМетода = "ТекстОписаниеПартийЗатрат" Тогда
		Возврат ТекстОписаниеПартийЗатрат();
		
	ИначеЕсли ИмяМетода = "ТекстОписаниеПартийПрочих" Тогда
		Возврат ТекстОписаниеПартийПрочих();
		
	ИначеЕсли ИмяМетода = "ТекстОписаниеСебестоимость" Тогда
		Возврат ТекстОписаниеСебестоимость();
		
	ИначеЕсли ИмяМетода = "ТекстОписаниеПартийРасходов" Тогда
		Возврат ТекстОписаниеПартийРасходов();
		
	ИначеЕсли ИмяМетода = "ТекстОписаниеДанныхДляПартийНДСКРаспределению" Тогда
		Возврат ТекстОписаниеДанныхДляПартийНДСКРаспределению();	
		
	ИначеЕсли ИмяМетода = "ТекстОписаниеДанныхДляПартийПроизводства" Тогда
		Возврат ТекстОписаниеДанныхДляПартийПроизводства();
		
		
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

Функция ВыгрузитьИзВременнойТаблицы(МВТ)
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	Данные КАК ДД
	|");
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция ТестДанныеДляПартий(ИмяМетода, НачалоПериода, ОкончаниеПериода, Организация = Неопределено) Экспорт
	
	МассивОрганизаций = РасчетСебестоимостиПрикладныеАлгоритмы.СвязиОрганизацийПоСхемеИнтеркампани(ОкончаниеПериода, Организация);
	
	Если ИмяМетода = "ДанныеДляПартийТоваров" Тогда
		Возврат ВыгрузитьИзВременнойТаблицы(ДанныеДляПартийТоваров(НачалоПериода, ОкончаниеПериода, МассивОрганизаций));
		
	ИначеЕсли ИмяМетода = "ДанныеДляПартийПереданных" Тогда
		Возврат ВыгрузитьИзВременнойТаблицы(ДанныеДляПартийПереданных(НачалоПериода, ОкончаниеПериода, МассивОрганизаций));
		
	ИначеЕсли ИмяМетода = "ДанныеДляПартийЗатрат" Тогда
		Возврат ВыгрузитьИзВременнойТаблицы(ДанныеДляПартийЗатрат(НачалоПериода, ОкончаниеПериода, МассивОрганизаций));
		
	ИначеЕсли ИмяМетода = "ДанныеДляПартийПрочих" Тогда
		Возврат ДанныеДляПартийПрочих(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
		
	ИначеЕсли ИмяМетода = "ДанныеДляПартийПрочихСебестоимость" Тогда
		Возврат ДанныеДляПартийПрочихСебестоимость(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
		
	ИначеЕсли ИмяМетода = "ДанныеДляПартийРасходов" Тогда
		Возврат ВыгрузитьИзВременнойТаблицы(ДанныеДляПартийРасходов(НачалоПериода, ОкончаниеПериода, МассивОрганизаций));
		
	ИначеЕсли ИмяМетода = "ДанныеДляПартийНДСКРаспределению" Тогда
		Возврат ДанныеДляПартийНДСКРаспределению(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);	
		
	ИначеЕсли ИмяМетода = "ДанныеДляПартийПроизводства" Тогда
		Возврат ВыгрузитьИзВременнойТаблицы(ДанныеДляПартийПроизводства(НачалоПериода, ОкончаниеПериода, МассивОрганизаций));
		

	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции


Функция ЗагрузитьВоВременнуюТаблицу(ДанныеДляПартий)
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	*
	|ПОМЕСТИТЬ
	|	Данные
	|ИЗ
	|	&ДанныеДляПартий КАК ДД
	|ИНДЕКСИРОВАТЬ ПО
	|	ДД.К
	|");
	МВТ = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.УстановитьПараметр("ДанныеДляПартий", ДанныеДляПартий);
	Запрос.Выполнить();
	Возврат МВТ;
КонецФункции

Функция ТестРасчетныеПартии(ИмяМетода, ДанныеДляПартий, ВнутренниеДанные = Неопределено) Экспорт
	Если ИмяМетода = "РасчетныеПартииТоваров" Тогда
		МВТ = ЗагрузитьВоВременнуюТаблицу(ДанныеДляПартий);
		РасчетныеПартии = РасчетныеПартииТоваров(МВТ, Неопределено, ВнутренниеДанные, Истина);
		ДанныеДляПартий = ВыгрузитьИзВременнойТаблицы(МВТ); 
		Возврат РасчетныеПартии;
		
	ИначеЕсли ИмяМетода = "РасчетныеПартииПереданные" Тогда
		МВТ = ЗагрузитьВоВременнуюТаблицу(ДанныеДляПартий);
		РасчетныеПартии = РасчетныеПартииПереданные(МВТ, Неопределено, ВнутренниеДанные, Истина);
		ДанныеДляПартий = ВыгрузитьИзВременнойТаблицы(МВТ); 
		Возврат РасчетныеПартии;
		
	ИначеЕсли ИмяМетода = "РасчетныеПартииЗатрат" Тогда
		МВТ = ЗагрузитьВоВременнуюТаблицу(ДанныеДляПартий);
		РасчетныеПартии = РасчетныеПартииЗатрат(МВТ, Неопределено, ВнутренниеДанные, Истина);
		ДанныеДляПартий = ВыгрузитьИзВременнойТаблицы(МВТ); 
		Возврат РасчетныеПартии;
		
	ИначеЕсли ИмяМетода = "РасчетныеПартииПрочих" Тогда
		Возврат РасчетныеПартииПрочих(ДанныеДляПартий, Неопределено, ВнутренниеДанные, Истина);
		
	ИначеЕсли ИмяМетода = "РасчетныеПриходыПартийРасходов" Тогда
		Возврат РасчетныеПриходыПартийРасходов(ДанныеДляПартий, Неопределено, ВнутренниеДанные, Истина);
		
	ИначеЕсли ИмяМетода = "РасчетныеПриходыСебестоимости" Тогда
		Возврат РасчетныеПриходыСебестоимости(Дата(1,1,1), ДанныеДляПартий, Неопределено, ВнутренниеДанные, Истина);
		
	ИначеЕсли ИмяМетода = "РасчетныеПартииРасходов" Тогда
		МВТ = ЗагрузитьВоВременнуюТаблицу(ДанныеДляПартий);
		РасчетныеПартии = РасчетныеПартииРасходов(МВТ, Неопределено, ВнутренниеДанные, Истина);
		ДанныеДляПартий = ВыгрузитьИзВременнойТаблицы(МВТ); 
		Возврат РасчетныеПартии;
		
	ИначеЕсли ИмяМетода = "РасчетныеПартииПроизводства" Тогда
		Возврат РасчетныеПартииПроизводства(ЗагрузитьВоВременнуюТаблицу(ДанныеДляПартий), Неопределено, ВнутренниеДанные, Истина);
		
		
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

Функция ТестРассчитать(ИмяМетода, НачалоПериода, ОкончаниеПериода, Организация = Неопределено) Экспорт
	Перем РасчетныеПартии;
	
	МассивОрганизаций = РасчетСебестоимостиПрикладныеАлгоритмы.СвязиОрганизацийПоСхемеИнтеркампани(ОкончаниеПериода, Организация);
	
	Если ИмяМетода = "РассчитатьПартииТоваров" Тогда
		РассчитатьПартииТоваров(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
		
	ИначеЕсли ИмяМетода = "РассчитатьПартииПереданные" Тогда
		РассчитатьПартииПереданные(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
		
	ИначеЕсли ИмяМетода = "РассчитатьПартииЗатрат" Тогда
		РассчитатьПартииЗатрат(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
		
	ИначеЕсли ИмяМетода = "РассчитатьПартииПрочих" Тогда
		РассчитатьПартииПрочих(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
		
	ИначеЕсли ИмяМетода = "РассчитатьПартииРасходов" Тогда
		РассчитатьПартииРасходов(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
		
	ИначеЕсли ИмяМетода = "РассчитатьПартииПроизводства" Тогда
		РассчитатьПартииПроизводства(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);		
		
	
	КонецЕсли;
	
	Возврат РасчетныеПартии;
	
КонецФункции

Процедура ТестЗаписатьРасчет(МенеджерРегистра, РасчетныеПартии) Экспорт
	ЗаписатьРасчетныеПартии(МенеджерРегистра, РасчетныеПартии, Неопределено);
КонецПроцедуры

Функция ТестРассчитатьЦепочкиДвижений(ИмяМетода, НачалоПериода, ОкончаниеПериода, Организация = Неопределено, ВнутренниеДанные = Неопределено) Экспорт
	Перем РасчетныеПартии;
	
	МассивОрганизаций = РасчетСебестоимостиПрикладныеАлгоритмы.СвязиОрганизацийПоСхемеИнтеркампани(ОкончаниеПериода, Организация);
	
	Если ИмяМетода = "ПартииТоваров" Тогда
		ДанныеДляПартийТоваров = ДанныеДляПартийТоваров(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
		РасчетныеПартии = РасчетныеПартииТоваров(ДанныеДляПартийТоваров, Неопределено, ВнутренниеДанные, Истина, Ложь);
	ИначеЕсли ИмяМетода = "ПартииЗатрат" Тогда
		ДанныеДляПартийЗатрат = ДанныеДляПартийЗатрат(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
		РасчетныеПартии = РасчетныеПартииЗатрат(ДанныеДляПартийЗатрат, Неопределено, ВнутренниеДанные, Истина, Ложь);
	ИначеЕсли ИмяМетода = "ПартииРасходов" Тогда
		ДанныеДляПартийРасходов = ДанныеДляПартийРасходов(НачалоПериода, ОкончаниеПериода, МассивОрганизаций);
		РасчетныеПартии = РасчетныеПартииРасходов(ДанныеДляПартийРасходов, Неопределено, ВнутренниеДанные, Истина, Ложь);
	КонецЕсли;
	Возврат РасчетныеПартии;
КонецФункции

// Позволяет отключить все операции записи регистров накопления (для тестирования расчета).
//
Функция ТестированиеБезЗаписи()
	Возврат Ложь;
КонецФункции

#КонецОбласти


#Область КорректировкаОстатков

#Область СебестоимостьТоваров

Процедура КорректировкаНачальныхОстатковРегистраСебестоимостьТоваров(НачалоПериода, МассивОрганизаций, ПартионныйУчетВключен)
	
	// Инициализация параметров расчета.
	МетаданныеРегистра = Метаданные.РегистрыНакопления.СебестоимостьТоваров;
	
	ПараметрыРасчета = РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьОсновныеПараметрыРасчета(НачалоПериода, КонецМесяца(НачалоПериода), МассивОрганизаций);
	РасчетСебестоимостиПрикладныеАлгоритмы.УстановитьТехнологическиеПараметрыРасчета(ПараметрыРасчета);
	
	ПараметрыРасчета.Вставить("ОрганизацииСОстаткамиПоСебестоимости",
		РасчетСебестоимостиПрикладныеАлгоритмы.ОрганизацииСОстаткамиПоСебестоимости(НачалоПериода, МассивОрганизаций));
	
	ПараметрыРасчета.Вставить("ПартионныйУчетВключен",   ПартионныйУчетВключен);
	ПараметрыРасчета.Вставить("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
	ПараметрыРасчета.Вставить("НомерЗаданияДоРасчета", 	 -1);
	
	ПараметрыРасчета.Вставить("РегистрыСРасчетнымиОборотами", Новый Структура);
	ПараметрыРасчета.Вставить("РегистрыСРасчетнымиОстатками", Новый Структура);
	ПараметрыРасчета.Вставить("Движения", 					  Новый Структура);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьДанныеРегистра(ПараметрыРасчета, МетаданныеРегистра);
	ИнициализироватьДокументыРасчетаСебестоимостиПрошлогоПериода(ПараметрыРасчета);
		
	// Инициализация параметров корректировки.
	ПараметрыКорректировки = ИнициализироватьКорректировкуОстатковРегистра(ПараметрыРасчета, МетаданныеРегистра.ПолноеИмя());
		
	КорректировкаВключитьУчетПоВидамЗапасовСебестоимостьТоваров(ПараметрыРасчета, ПараметрыКорректировки);
	
	Если РасчетСебестоимостиПовтИсп.СебестоимостьТоваровПоНазначениям(НачалоПериода) Тогда
		Если РасчетСебестоимостиПовтИсп.ДатаВключенияОбособленногоУчетаСебестоимостиПоНазначениям() = НачалоПериода Тогда
			КорректировкаВключитьУчетПоНазначениямСебестоимостьТоваров(ПараметрыРасчета, ПараметрыКорректировки);
		КонецЕсли;
	Иначе
		КорректировкаОтключитьУчетПоНазначениямСебестоимостьТоваров(ПараметрыРасчета, ПараметрыКорректировки);
	КонецЕсли;
	
	ЗавершитьКорректировкуОстатковРегистра(ПараметрыРасчета, ПараметрыКорректировки);
	
КонецПроцедуры

Процедура КорректировкаВключитьУчетПоВидамЗапасовСебестоимостьТоваров(ПараметрыРасчета, ПараметрыКорректировки)
	
	НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		Перечисления.ТипыЗаписейПартий.СлужебноеВключитьУчетПоВидамЗапасов,
		Истина);
	
	Если НЕ ПараметрыКорректировки.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	#Область ПодготовкаДанных
	
	ОписаниеАналитики = СформироватьОписаниеКлючейАналитикУчетаНоменклатуры(, "Назначение");
	
	Запрос = Новый Запрос;
	ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	// Выберем данные для распределения
	
	Запрос.Текст = ТекстЗапросаКорректировкиПолучитьДанныеПартионныхРегистров(); // ВТОстаткиПартионныхРегистровПредварительная для ФИФО скользящая
	
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&ИзмеренияРегистра,
	|	&РесурсыРегистра,
	|	&ПоляАналитикиИменованные
	|ПОМЕСТИТЬ ВТОстаткиСебестоимостиБезВидовЗапасов
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И НЕ (Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		  И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения В
	|			(ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер),
	|			 ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Организация)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация						КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов						КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|		КОГДА ЕСТЬNULL(Т.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	КОНЕЦ								КАК РазделУчета,
	|	Т.КоличествоОстаток					КАК Количество
	|ПОМЕСТИТЬ ВТОстаткиСВидамиЗапасовПредварительная
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииНеФИФОСкользящая)
	|	) КАК Т
	|ГДЕ
	|	Т.КоличествоОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Организация						КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов						КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию)
	|	КОНЕЦ								КАК РазделУчета,
	|	Т.КоличествоОстаток					КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеНаКомиссию.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииНеФИФОСкользящая)
	|	) КАК Т
	|ГДЕ
	|	Т.КоличествоОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.ВидЗапасов.Организация			КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов						ВидЗапасов,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Т.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	КОНЕЦ								КАК РазделУчета,
	|	Т.КоличествоОстаток					КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюДокументовИмпорта.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		ВидЗапасов.Организация В (&ОрганизацииНеФИФОСкользящая)
	|		И ТипДокументаИмпорта = &ТипДокументаИмпорта
	|	) КАК Т
	|ГДЕ
	|	Т.КоличествоОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Организация						КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов						ВидЗапасов,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
	|	Т.КоличествоОстаток					КАК Количество
	|ИЗ
	|	РегистрНакопления.ПартииПроизводственныхЗатрат.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииНеФИФОСкользящая)
	|	) КАК Т
	|ГДЕ
	|	Т.КоличествоОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументРеализации.Организация														  КАК Организация,
	|	ЕСТЬNULL(АналитикаПереданнойНоменклатуры.КлючАналитики, Т.АналитикаУчетаНоменклатуры) КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов																		  КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыВПути) 				  КАК РазделУчета,
	|	Т.Количество																		  КАК Количество
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ВидыЗапасов КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК ДокументРеализации
	|		ПО Т.Ссылка = ДокументРеализации.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО Т.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПереданнойНоменклатуры
	|	ПО Аналитика.Номенклатура = АналитикаПереданнойНоменклатуры.Номенклатура
	|		И Аналитика.Характеристика = АналитикаПереданнойНоменклатуры.Характеристика
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаПереданнойНоменклатуры.Назначение
	|		И ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = АналитикаПереданнойНоменклатуры.Серия
	|		И ДокументРеализации.Партнер = АналитикаПереданнойНоменклатуры.МестоХранения
	|ГДЕ
	|	ДокументРеализации.Организация В (&ОрганизацииНеФИФОСкользящая)
	|	И ДокументРеализации.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности))
	|	И ДокументРеализации.Проведен
	|	И ДокументРеализации.Дата < &НачалоПериода
	|	И ДокументРеализации.ДатаПереходаПраваСобственности >= &НачалоПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.РазделУчета,
	|	&ПоляАналитикиИменованные,
	|	СУММА(Т.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТОстаткиСВидамиЗапасов
	|ИЗ
	|	ВТОстаткиСВидамиЗапасовПредварительная КАК Т
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.РазделУчета,
	|	&ПоляАналитикиНеИменованные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.РазделУчета,
	|	&ПоляАналитикиИменованные,
	|	СУММА(Т.Количество) КАК Количество
	|ИЗ
	|	ВТОстаткиПартионныхРегистровПредварительная КАК Т
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.РазделУчета,
	|	&ПоляАналитикиНеИменованные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТОстаткиСВидамиЗапасовПредварительная";
	
	РаспределяемыеИзмерения = РасчетСебестоимостиУниверсальныеАлгоритмы.УдалитьЭлементыИзСтрокиШаблона(
		ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра,
		"%1ВидЗапасов");
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИзмеренияРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(РаспределяемыеИзмерения, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&РесурсыРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляАналитикиИменованные",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеАналитики.ПоляИменованные, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляАналитикиНеИменованные",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеАналитики.ПоляНеИменованные, "Т."));

	Запрос.УстановитьПараметр("ТипДокументаИмпорта", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ТаможеннаяДекларацияИмпорт"));
	
	Запрос.Выполнить();
	
	#КонецОбласти 
	
	#Область РаспределениеДанных
	
	УчетПоНазначениям = РасчетСебестоимостиПовтИсп.СебестоимостьТоваровПоНазначениям(ПараметрыРасчета.РасчетныйПериод.НачалоПериода);
	
	// Инициализируем распределение
	ПараметрыРаспределения = РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПараметрыРаспределенияМетодомУменьшаемогоОстатка(
		"ВТОстаткиСебестоимостиБезВидовЗапасов",
		"ВТОстаткиСВидамиЗапасов",
		РасчетСебестоимостиУниверсальныеАлгоритмы.СоединитьСтроки("Организация, РазделУчета", ОписаниеАналитики.ПереченьПолей));
	
	РаспределяемыеРесурсы = РасчетСебестоимостиУниверсальныеАлгоритмы.УдалитьЭлементыИзСтрокиШаблона(
		ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра,
		"%1Количество");
	
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьЧисловыеПоляРаспределенияМетодомУменьшаемогоОстатка(
		ПараметрыРаспределения,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(РаспределяемыеРесурсы, ""));
		
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПрочиеПоляРаспределенияМетодомУменьшаемогоОстатка(
		ПараметрыРаспределения,
		"Партия, АналитикаУчетаПартий, АналитикаФинансовогоУчета, ВидДеятельностиНДС, АналитикаУчетаНоменклатуры",
		"ВидЗапасов" + ?(УчетПоНазначениям, ", АналитикаУчетаНоменклатуры", ""));
		
	// Распределяем
	РасчетСебестоимостиУниверсальныеАлгоритмы.РаспределитьМетодомУменьшаемогоОстатка(Запрос.МенеджерВременныхТаблиц, ПараметрыРаспределения);
	
	Если НЕ УчетПоНазначениям Тогда
		ЕстьКорректировки = СформироватьАналитикиНоменклатурыБезНазначения(
			ПараметрыРасчета,
			"ВТРезультатРаспределения",
			"ВТРезультатРаспределенияСкорректированный");
	Иначе
		ЕстьКорректировки = Ложь;
	КонецЕсли;
	
	// Добавляем результаты распределения к корректировочным движениям
	ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		?(ЕстьКорректировки, "ВТРезультатРаспределенияСкорректированный", "ВТРезультатРаспределения") + ", ВТНераспределенныеДанныеИсточника",
		"ВТОстаткиСебестоимостиБезВидовЗапасов",
		Истина,
		Истина);
	
	#КонецОбласти

КонецПроцедуры

Процедура КорректировкаВключитьУчетПоНазначениямСебестоимостьТоваров(ПараметрыРасчета, ПараметрыКорректировки)
	
	НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		Перечисления.ТипыЗаписейПартий.СлужебноеВключитьУчетПоНазначениям,
		Истина);
	
	Если НЕ ПараметрыКорректировки.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	#Область ПодготовкаДанныхНеФИФОСкользящая
	
	ОписаниеАналитики = СформироватьОписаниеКлючейАналитикУчетаНоменклатуры(, "Назначение");
	
	Запрос = Новый Запрос;
	ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	// Выберем данные для распределения
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&ИзмеренияРегистра,
	|	&РесурсыРегистра,
	|	&ПоляАналитикиИменованные
	|ПОМЕСТИТЬ ВТОстаткиСебестоимостиБезНазначений
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И Т.Организация В (&ОрганизацииНеФИФОСкользящая)
	|	И Т.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Ключи.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТАналитикиСНазначениями
	|ИЗ
	|	Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
	|ГДЕ
	|	Ключи.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация						КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов 						КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|		КОГДА ЕСТЬNULL(Т.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	КОНЕЦ								КАК РазделУчета,
	|	Т.КоличествоОстаток					КАК Количество
	|ПОМЕСТИТЬ ВТОстаткиСНазначениямиПредварительная
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииНеФИФОСкользящая)
	|		И АналитикаУчетаНоменклатуры В (ВЫБРАТЬ Т.Ссылка ИЗ ВТАналитикиСНазначениями КАК Т)
	|	) КАК Т
	|ГДЕ
	|	Т.КоличествоОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Организация						КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов 						КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию)
	|	КОНЕЦ								КАК РазделУчета,
	|	Т.КоличествоОстаток					КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеНаКомиссию.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииНеФИФОСкользящая)
	|		И АналитикаУчетаНоменклатуры В (ВЫБРАТЬ Т.Ссылка ИЗ ВТАналитикиСНазначениями КАК Т)
	|	) КАК Т
	|ГДЕ
	|	Т.КоличествоОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.ВидЗапасов.Организация			КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов 						КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Т.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	КОНЕЦ								КАК РазделУчета,
	|	Т.КоличествоОстаток					КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюДокументовИмпорта.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		ВидЗапасов.Организация В (&ОрганизацииНеФИФОСкользящая)
	|		И ТипДокументаИмпорта = &ТипДокументаИмпорта
	|		И АналитикаУчетаНоменклатуры В (ВЫБРАТЬ Т.Ссылка ИЗ ВТАналитикиСНазначениями КАК Т)
	|	) КАК Т
	|ГДЕ
	|	Т.КоличествоОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.РазделУчета,
	|	&ПоляАналитикиИменованные,
	|	СУММА(Т.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТОстаткиСНазначениями
	|ИЗ
	|	ВТОстаткиСНазначениямиПредварительная КАК Т
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.РазделУчета,
	|	&ПоляАналитикиНеИменованные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.Номенклатура КАК АналитикаУчетаНоменклатуры_Номенклатура,
	|	Т.Характеристика КАК АналитикаУчетаНоменклатуры_Характеристика,
	|	Т.Серия КАК АналитикаУчетаНоменклатуры_Серия,
	|	Т.Назначение КАК АналитикаУчетаНоменклатуры_Назначение,
	|	Т.Подразделение КАК АналитикаУчетаНоменклатуры_МестоХранения,
	|	&ПустаяСтатьяКалькуляции КАК АналитикаУчетаНоменклатуры_СтатьяКалькуляции,
	|	Т.КоличествоОстаток КАК Количество
	|ПОМЕСТИТЬ ВТОстаткиМатериалыИРаботыБезАналитик
	|ИЗ
	|	РегистрНакопления.МатериалыИРаботыВПроизводстве.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииНеФИФОСкользящая)
	|		И Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	) КАК Т
	|ГДЕ
	|	Т.КоличествоОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТОстаткиСНазначениямиПредварительная";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИзмеренияРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&РесурсыРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляАналитикиИменованные",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеАналитики.ПоляИменованные, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляАналитикиНеИменованные",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеАналитики.ПоляНеИменованные, "Т."));
	
	Запрос.УстановитьПараметр("ПустаяСтатьяКалькуляции", "");

	Запрос.УстановитьПараметр("ТипДокументаИмпорта", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ТаможеннаяДекларацияИмпорт"));
	
	Запрос.Выполнить();
	
	#КонецОбласти 
	
	#Область РаспределениеДанныхНеФИФОСкользящая
	
	// Инициализируем распределение
	ПараметрыРаспределения = РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПараметрыРаспределенияМетодомУменьшаемогоОстатка(
		"ВТОстаткиСебестоимостиБезНазначений",
		"ВТОстаткиСНазначениями",
		РасчетСебестоимостиУниверсальныеАлгоритмы.СоединитьСтроки("Организация, РазделУчета, ВидЗапасов", ОписаниеАналитики.ПереченьПолей));
		
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьТаблицыРезультатовРаспределения(
		ПараметрыРаспределения,
		"ВТРезультатРаспределения",
		"НераспределенныеОстаткиСебестоимости");
		
	РаспределяемыеРесурсы = РасчетСебестоимостиУниверсальныеАлгоритмы.УдалитьЭлементыИзСтрокиШаблона(
		ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра,
		"%1Количество");
	
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьЧисловыеПоляРаспределенияМетодомУменьшаемогоОстатка(
		ПараметрыРаспределения,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(РаспределяемыеРесурсы, ""));
		
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПрочиеПоляРаспределенияМетодомУменьшаемогоОстатка(
		ПараметрыРаспределения,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра, ""),
		"АналитикаУчетаНоменклатуры");
	
	// Распределяем
	РасчетСебестоимостиУниверсальныеАлгоритмы.РаспределитьМетодомУменьшаемогоОстатка(Запрос.МенеджерВременныхТаблиц, ПараметрыРаспределения);
	
	ТребуетсяРаспределениеМатериаловИРабот =
		РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТОстаткиМатериалыИРаботыБезАналитик") > 0
		И РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "НераспределенныеОстаткиСебестоимости") > 0;
	
	// Добавляем результаты распределения к корректировочным движениям
	ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		"ВТРезультатРаспределения" + ?(ТребуетсяРаспределениеМатериаловИРабот, "", ", НераспределенныеОстаткиСебестоимости"),
		"ВТОстаткиСебестоимостиБезНазначений",
		Истина,
		Истина);
		
	#КонецОбласти
	
	#Область ПодготовкаДанныхМатериалыИРаботыНеФИФОСкользящая
	
	СформироватьАналитикиНоменклатурыПоЗначениямПолей(
		ПараметрыРасчета,
		"ВТОстаткиМатериалыИРаботыБезАналитик",
		"ВТОстаткиМатериалыИРаботыСАналитиками",
		"АналитикаУчетаНоменклатуры");
	
	#КонецОбласти
	
	#Область РаспределениеДанныхДанныхМатериалыИРаботыНеФИФОСкользящая
	
	Если ТребуетсяРаспределениеМатериаловИРабот Тогда
		
		// Инициализируем распределение
		ПараметрыРаспределения = РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПараметрыРаспределенияМетодомУменьшаемогоОстатка(
			"НераспределенныеОстаткиСебестоимости",
			"ВТОстаткиМатериалыИРаботыСАналитиками",
			РасчетСебестоимостиУниверсальныеАлгоритмы.СоединитьСтроки("Организация", ОписаниеАналитики.ПереченьПолей));
			
		РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьЧисловыеПоляРаспределенияМетодомУменьшаемогоОстатка(
			ПараметрыРаспределения,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(РаспределяемыеРесурсы, ""));
			
		РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПрочиеПоляРаспределенияМетодомУменьшаемогоОстатка(
			ПараметрыРаспределения,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра, ""),
			"АналитикаУчетаНоменклатуры");
			
		// Распределяем
		РасчетСебестоимостиУниверсальныеАлгоритмы.РаспределитьМетодомУменьшаемогоОстатка(Запрос.МенеджерВременныхТаблиц, ПараметрыРаспределения);
		
		// Добавляем результаты распределения к корректировочным движениям
		ДобавитьКорректировочныеДвиженияРегистра(
			ПараметрыРасчета,
			ПараметрыКорректировки,
			"ВТРезультатРаспределения, ВТНераспределенныеДанныеИсточника",
			,
			Истина);
		
	КонецЕсли;
	
	#КонецОбласти

	#Область ПодготовкаДанныхФИФОСкользящая
	
	Запрос.Текст = ТекстЗапросаКорректировкиПолучитьДанныеПартионныхРегистров();
	
	Запрос.Выполнить();
	
	ЕстьКорректировки = СформироватьАналитикиНоменклатурыБезНазначения(
		ПараметрыРасчета,
		"ВТОстаткиПартионныхРегистровПредварительная",
		"ВТОстаткиСебестоимостиБезНазначений");
		
	Если ЕстьКорректировки Тогда
		
		// Добавляем результаты распределения к корректировочным движениям
		ДобавитьКорректировочныеДвиженияРегистра(
			ПараметрыРасчета,
			ПараметрыКорректировки,
			"ВТОстаткиПартионныхРегистровПредварительная",
			"ВТОстаткиСебестоимостиБезНазначений",
			Истина,
			Истина);
		
	КонецЕсли;
	
	#КонецОбласти

КонецПроцедуры

Процедура КорректировкаОтключитьУчетПоНазначениямСебестоимостьТоваров(ПараметрыРасчета, ПараметрыКорректировки)
	
	НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		Перечисления.ТипыЗаписейПартий.СлужебноеОтключитьУчетПоНазначениям,
		Истина);
	
	Если НЕ ПараметрыКорректировки.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	#Область ПодготовкаДанных
	
	Запрос = Новый Запрос;
	ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	// Выберем данные для корректировки
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&ИзмеренияРегистра,
	|	&РесурсыРегистра
	|ПОМЕСТИТЬ ВТОстаткиСебестоимостиСНазначением
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИзмеренияРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&РесурсыРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра, "Т."));
	
	Запрос.Выполнить();
	
	ЕстьКорректировки = СформироватьАналитикиНоменклатурыБезНазначения(
		ПараметрыРасчета,
		"ВТОстаткиСебестоимостиСНазначением",
		"ВТОстаткиСебестоимостиБезНазначения");
	
	Если НЕ ЕстьКорректировки Тогда
		Возврат;
	КонецЕсли;
	
	#КонецОбласти 
	
	// Добавляем результаты распределения к корректировочным движениям
	ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		"ВТОстаткиСебестоимостиБезНазначения",
		"ВТОстаткиСебестоимостиСНазначением",
		Истина,
		Истина);
	
КонецПроцедуры

Функция ТекстЗапросаКорректировкиПолучитьДанныеПартионныхРегистров()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.ДокументПоступления,
	|	Т.АналитикаУчетаПартий,
	|	Т.КоличествоОстаток,
	|	Т.СтоимостьОстаток,
	|	Т.СтоимостьБезНДСОстаток,
	|	Т.ПостояннаяРазницаОстаток,
	|	Т.ВременнаяРазницаОстаток,
	|	ВЫБОР
	|		КОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС В
	|		  (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|		   ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			 ТОГДА Т.СтоимостьРеглОстаток + Т.НДСРеглОстаток
	|		ИНАЧЕ Т.СтоимостьРеглОстаток
	|	КОНЕЦ КАК СтоимостьРеглОстаток
	|ПОМЕСТИТЬ ВТОстаткиПартииТоваровОрганизаций
	|ИЗ
	|	РегистрНакопления.ПартииТоваровОрганизаций.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииФИФОСкользящая)) КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.ДокументПоступления,
	|	Т.АналитикаУчетаПартий,
	|	Т.КоличествоОстаток,
	|	Т.СтоимостьОстаток,
	|	Т.СтоимостьБезНДСОстаток,
	|	Т.ПостояннаяРазницаОстаток,
	|	Т.ВременнаяРазницаОстаток,
	|	ВЫБОР
	|		КОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС В
	|		  (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|		   ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			 ТОГДА Т.СтоимостьРеглОстаток + Т.НДСРеглОстаток
	|		ИНАЧЕ Т.СтоимостьРеглОстаток
	|	КОНЕЦ КАК СтоимостьРеглОстаток
	|ПОМЕСТИТЬ ВТОстаткиПартииТоваровПереданныеНаКомиссию
	|ИЗ
	|	РегистрНакопления.ПартииТоваровПереданныеНаКомиссию.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииФИФОСкользящая)) КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.ДокументПоступления,
	|	Т.СтатьяРасходов,
	|	Т.СтоимостьОстаток,
	|	Т.СтоимостьБезНДСОстаток,
	|	Т.СтоимостьРеглОстаток,
	|	Т.ПостояннаяРазницаОстаток,
	|	Т.ВременнаяРазницаОстаток,
	|	ВЫБОР
	|		КОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС В
	|		  (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|		   ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			 ТОГДА Т.СтоимостьРеглОстаток + Т.НДСРеглОстаток
	|		ИНАЧЕ Т.СтоимостьРеглОстаток
	|	КОНЕЦ КАК СтоимостьРеглОстаток
	|ПОМЕСТИТЬ ВТОстаткиПартииРасходовНаСебестоимостьТоваров
	|ИЗ
	|	РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииФИФОСкользящая)) КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаПродукции,
	|	Т.ДокументВыпуска,
	|	Т.КоличествоОстаток,
	|	Т.СтоимостьОстаток,
	|	Т.СтоимостьБезНДСОстаток,
	|	Т.ПостояннаяРазницаОстаток,
	|	Т.ВременнаяРазницаОстаток,
	|	ВЫБОР
	|		КОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС В
	|		  (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|		   ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			 ТОГДА Т.СтоимостьРеглОстаток + Т.НДСРеглОстаток
	|		ИНАЧЕ Т.СтоимостьРеглОстаток
	|	КОНЕЦ КАК СтоимостьРеглОстаток
	|ПОМЕСТИТЬ ВТОстаткиПартииЗатратНаВыпуск
	|ИЗ
	|	РегистрНакопления.ПартииЗатратНаВыпуск.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииФИФОСкользящая)
	|		И АналитикаУчетаПродукции <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)) КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.ДокументПоступления,
	|	Т.АналитикаУчетаПартий,
	|	Т.КоличествоОстаток,
	|	Т.СтоимостьОстаток,
	|	Т.СтоимостьБезНДСОстаток,
	|	Т.ПостояннаяРазницаОстаток,
	|	Т.ВременнаяРазницаОстаток,
	|	ВЫБОР
	|		КОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС В
	|		  (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|		   ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			 ТОГДА Т.СтоимостьРеглОстаток + Т.НДСРеглОстаток
	|		ИНАЧЕ Т.СтоимостьРеглОстаток
	|	КОНЕЦ КАК СтоимостьРеглОстаток
	|ПОМЕСТИТЬ ВТОстаткиПартииПроизводственныхЗатрат
	|ИЗ
	|	РегистрНакопления.ПартииПроизводственныхЗатрат.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииФИФОСкользящая)) КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////

	|ВЫБРАТЬ
	|	Т.РазделУчета 					КАК РазделУчета,
	|	Т.Организация 					КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры 	КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов 					КАК ВидЗапасов,
	|	СУММА(Т.Количество) 			КАК Количество,
	|	СУММА(Т.Стоимость) 				КАК Стоимость,
	|	СУММА(Т.СтоимостьБезНДС) 		КАК СтоимостьБезНДС,
	|	СУММА(Т.ПостояннаяРазница) 		КАК ПостояннаяРазница,
	|	СУММА(Т.ВременнаяРазница) 		КАК ВременнаяРазница,
	|	СУММА(Т.СтоимостьРегл) 			КАК СтоимостьРегл,
	|	СУММА(Т.ДопРасходы) 			КАК ДопРасходы,
	|	СУММА(Т.ДопРасходыБезНДС) 		КАК ДопРасходыБезНДС
	|ПОМЕСТИТЬ ВТОстаткиПартионныхРегистровПредварительная
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|			КОГДА Т.ВидЗапасов.ТипЗапасов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца), ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца))
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		КОНЕЦ КАК РазделУчета,
	|		Т.Организация КАК Организация,
	|		Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		Т.ВидЗапасов КАК ВидЗапасов,
	|		Т.КоличествоОстаток КАК Количество,
	|		Т.СтоимостьОстаток КАК Стоимость,
	|		Т.СтоимостьБезНДСОстаток КАК СтоимостьБезНДС,
	|		Т.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
	|		Т.ВременнаяРазницаОстаток КАК ВременнаяРазница,
	|		Т.СтоимостьРеглОстаток КАК СтоимостьРегл,
	|		0 КАК ДопРасходы,
	|		0 КАК ДопРасходыБезНДС
	|	ИЗ
	|		ВТОстаткиПартииТоваровОрганизаций КАК Т
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию),
	|		Т.Организация,
	|		Т.АналитикаУчетаНоменклатуры,
	|		Т.ВидЗапасов,
	|		Т.КоличествоОстаток,
	|		Т.СтоимостьОстаток,
	|		Т.СтоимостьБезНДСОстаток,
	|		Т.ПостояннаяРазницаОстаток,
	|		Т.ВременнаяРазницаОстаток,
	|		Т.СтоимостьРеглОстаток,
	|		0,
	|		0
	|	ИЗ
	|		ВТОстаткиПартииТоваровПереданныеНаКомиссию КАК Т
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|			КОГДА Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ,
	|		Т.Организация,
	|		Т.АналитикаУчетаНоменклатуры,
	|		Т.ВидЗапасов,
	|		0,
	|		Т.СтоимостьОстаток,
	|		Т.СтоимостьБезНДСОстаток,
	|		Т.ПостояннаяРазницаОстаток,
	|		Т.ВременнаяРазницаОстаток,
	|		Т.СтоимостьРеглОстаток,
	|		0,
	|		0
	|	ИЗ
	|		ВТОстаткиПартииРасходовНаСебестоимостьТоваров КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиПартииТоваровОрганизаций КАК ПартииТоваров
	|			ПО Т.Организация = ПартииТоваров.Организация
	|			И Т.АналитикаУчетаНоменклатуры = ПартииТоваров.АналитикаУчетаНоменклатуры
	|			И Т.ВидЗапасов = ПартииТоваров.ВидЗапасов
	|			И Т.ДокументПоступления = ПартииТоваров.ДокументПоступления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиПартииПроизводственныхЗатрат КАК ПартииПроизводства
	|			ПО Т.Организация = ПартииПроизводства.Организация
	|			И Т.АналитикаУчетаНоменклатуры = ПартииПроизводства.АналитикаУчетаНоменклатуры
	|			И Т.ВидЗапасов = ПартииПроизводства.ВидЗапасов
	|			И Т.ДокументПоступления = ПартииПроизводства.ДокументПоступления
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА Т.АналитикаУчетаПродукции.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|			КОГДА Т.АналитикаУчетаПродукции.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ,
	|		Т.Организация,
	|		Т.АналитикаУчетаПродукции,
	|		ЕСТЬNULL(ПартииТоваров.ВидЗапасов, ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)),
	|		0 КАК КоличествоОстаток,
	|		Т.СтоимостьОстаток,
	|		Т.СтоимостьБезНДСОстаток,
	|		Т.ПостояннаяРазницаОстаток,
	|		Т.ВременнаяРазницаОстаток,
	|		Т.СтоимостьРеглОстаток,
	|		0,
	|		0
	|	ИЗ
	|		ВТОстаткиПартииЗатратНаВыпуск КАК Т
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиПартииТоваровОрганизаций КАК ПартииТоваров
	|			ПО Т.Организация = ПартииТоваров.Организация
	|				И Т.АналитикаУчетаПродукции = ПартииТоваров.АналитикаУчетаНоменклатуры
	|				И Т.ДокументВыпуска = ПартииТоваров.ДокументПоступления
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеПереработчику)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		КОНЕЦ,
	|		Т.Организация,
	|		Т.АналитикаУчетаНоменклатуры,
	|		Т.ВидЗапасов,
	|		Т.КоличествоОстаток,
	|		Т.СтоимостьОстаток,
	|		Т.СтоимостьБезНДСОстаток,
	|		Т.ПостояннаяРазницаОстаток,
	|		Т.ВременнаяРазницаОстаток,
	|		Т.СтоимостьРеглОстаток,
	|		0,
	|		0
	|	ИЗ
	|		ВТОстаткиПартииПроизводственныхЗатрат КАК Т
	|	
	|	) КАК Т
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.РазделУчета,
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов
	|
	|ИМЕЮЩИЕ
	|	СУММА(Т.Количество) <> 0
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ВспомогательныеПроцедурыКорректировки

Функция ИнициализироватьКорректировкуОстатковРегистра(ПараметрыРасчета, ИмяРегистра)
	
	ПараметрыКорректировки = Новый Структура;
	
	МетаданныеРегистра = Метаданные.НайтиПоПолномуИмени(ИмяРегистра);
	
	// Описание регистра
	ПараметрыКорректировки.Вставить("МетаданныеРегистра", 	 МетаданныеРегистра); // объект метаданных регистра
	ПараметрыКорректировки.Вставить("ПолноеИмяРегистра", 	 ИмяРегистра); // полное имя регистра из метаданных
	ПараметрыКорректировки.Вставить("ИмяРегистра", 			 МетаданныеРегистра.Имя); // имя регистра из объекта метаданных
	ПараметрыКорректировки.Вставить("ПредставлениеРегистра", МетаданныеРегистра.Синоним); // представление регистра из объекта метаданных
	ПараметрыКорректировки.Вставить("ОписаниеРегистра", 	 ПараметрыРасчета.Движения[ПараметрыКорректировки.ИмяРегистра]); // описание регистра
	
	// Описание временных таблиц
	ПараметрыКорректировки.Вставить("ИмяТаблицыОстатков", "ВТРасчетныеНачальныеОстатки" + ПараметрыКорректировки.ИмяРегистра); // количество таблиц сформированных движений вида ВТКорректировка<ИмяРегистра><НомерТаблицы>
	ПараметрыКорректировки.Вставить("КоличествоСформированныхТаблиц", 0); // количество таблиц сформированных движений вида ВТКорректировка<ИмяРегистра><НомерТаблицы>
	ПараметрыКорректировки.Вставить("ИменаСформированныхТаблиц", ""); // имена всех таблиц вида ВТКорректировка<ИмяРегистра><НомерТаблицы>
	ПараметрыКорректировки.Вставить("СуществующиеТаблицы",
		РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(ПараметрыРасчета)); // имена всех таблиц, существовавших до начала корректировки
	
	// Описание корректировки
	ПараметрыКорректировки.Вставить("ТипЗаписиКорректировки", Неопределено); // тип записи текущей корректировки
	
	Возврат ПараметрыКорректировки;
	
КонецФункции

Процедура НачатьСледующуюКорректировкуОстатковРегистра(ПараметрыРасчета, ПараметрыКорректировки,
			ТипЗаписиКорректировки, ОчищатьКорректировкиСледующихПериодов = Ложь) Экспорт
	
	ПараметрыКорректировки.ТипЗаписиКорректировки = ТипЗаписиКорректировки;
	
	// Очистим временные таблицы предыдущих корректировок.
	ИменаУдаляемыхТаблиц = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(
		ПараметрыРасчета,
		РасчетСебестоимостиУниверсальныеАлгоритмы.СоединитьСтроки(ПараметрыКорректировки.СуществующиеТаблицы, ПараметрыКорректировки.ИменаСформированныхТаблиц));
	
	МассивСтрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаУдаляемыхТаблиц, ",", Истина, Истина);
	ИменаУдаляемыхТаблиц = "";
	
	// Не удаляем таблицы расчетных остатков и таблицы сформированных корректировок
	// (в случае, если несколько корректировок разных регистров выполняются одновременно).
	Для Каждого ИмяТаблицы Из МассивСтрок Цикл
		
		Если СтрНачинаетсяС(ИмяТаблицы, "ВТРасчетныеНачальныеОстатки") Тогда
			Продолжить;
		ИначеЕсли СтрНачинаетсяС(ИмяТаблицы, "ВТКорректировка") Тогда
			Продолжить;
		КонецЕсли;
		
		РасчетСебестоимостиУниверсальныеАлгоритмы.ДополнитьСтроку(ИменаУдаляемыхТаблиц, ИмяТаблицы);
		
	КонецЦикла;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета, ИменаУдаляемыхТаблиц);
	
	// Обслуживание регистра РасчетСебестоимостиДатыКорректировокОстатков
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьОписаниеОрганизацийДляВыполненияКорректировки(ПараметрыРасчета, ПараметрыКорректировки);
	
	ПараметрыРасчета.Вставить("ОрганизацииФИФОСкользящая", Новый Массив);
	ПараметрыРасчета.Вставить("ОрганизацииНеФИФОСкользящая", Новый Массив);
	
	Для Каждого ТекущаяОрганизация Из ПараметрыКорректировки.ОрганизацииДляКорректировки Цикл
			
		ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату("УчетнаяПолитикаФинансовогоУчета",
			ТекущаяОрганизация,
			НачалоМесяца(ПараметрыРасчета.РасчетныйПериод.НачалоПериода - 1));
		Если ПараметрыРасчета.ПартионныйУчетВключен
		 И ПараметрыУчетнойПолитики <> Неопределено
		 И ПараметрыУчетнойПолитики.МетодОценкиСтоимостиТоваров = Перечисления.МетодыОценкиСтоимостиТоваров.ФИФОСкользящаяОценка Тогда
			ПараметрыРасчета.ОрганизацииФИФОСкользящая.Добавить(ТекущаяОрганизация);
		Иначе
			ПараметрыРасчета.ОрганизацииНеФИФОСкользящая.Добавить(ТекущаяОрганизация);
		КонецЕсли;	
		
	КонецЦикла;
	
	Если ОчищатьКорректировкиСледующихПериодов Тогда
		// Очистим движения корректировок в следующих периодах.
		ОчиститьКорректировкиРегистровВСледующихПериодах(ПараметрыРасчета, ПараметрыКорректировки);
	КонецЕсли;
	
	// Инициализируем таблицу расчетных начальных остатков регистра.
	СформироватьТаблицуРасчетныхНачальныхОстатковРегистра(ПараметрыРасчета, ПараметрыКорректировки);
	
КонецПроцедуры

Процедура ДобавитьКорректировочныеДвиженияРегистра(ПараметрыРасчета, ПараметрыКорректировки,
			ИменаТаблицКорректировки, ИменаТаблицСторно = "", УдалятьТаблицыКорректировки = Ложь, УдалятьТаблицыСторно = Ложь) Экспорт
	
	// Подготовим вспомогательные данные.
	ИменаКолонокРегистра  = Новый Структура(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ПоляОсновнойТаблицыРегистра, ""));
	ИменаРесурсовРегистра = Новый Структура(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра, ""));
	ИменаСлужебныхКолонок = Новый Структура("Регистратор, Период, ВидДвижения, ТипЗаписи");
	
	Если ПараметрыКорректировки.ОписаниеРегистра.ЕстьРасчетПартий Тогда
		ИменаСлужебныхКолонок.Вставить("РасчетПартий");
	КонецЕсли;
	
	ИменаТаблиц = РасчетСебестоимостиУниверсальныеАлгоритмы.СоединитьСтроки(ИменаТаблицКорректировки, ИменаТаблицСторно);
	СтруктураТаблицСторно = Новый Структура(ИменаТаблицСторно);
	
	Запрос = Новый Запрос;
	ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	Запрос.УстановитьПараметр("ТипЗаписиКорректировки", ПараметрыКорректировки.ТипЗаписиКорректировки);
	
	ШаблонЗапроса =
	"ВЫБРАТЬ
	|	&ПоляПриемника
	|ПОМЕСТИТЬ ИмяПриемника
	|ИЗ
	|	&ИмяИсточника КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТДокументыРасчетаСебестоимостиПрошлогоПериода КАК ДокументыРасчетаСебестоимости
	|		ПО Т.Организация = ДокументыРасчетаСебестоимости.Организация";
	
	Для Каждого КлючИЗначение Из Новый Структура(ИменаТаблиц) Цикл // для каждой таблицы корректировки и сторно
		
		// Источник
		ИмяТаблицыИсточника = КлючИЗначение.Ключ;
		
		СтруктураТаблицыИсточника = РасчетСебестоимостиПрикладныеАлгоритмы.ВыгрузитьВременнуюТаблицу(ПараметрыРасчета, ИмяТаблицыИсточника, 1);
		Если СтруктураТаблицыИсточника.Количество() = 0 Тогда
			Продолжить; // в источнике нет данных
		КонецЕсли;
		
		КолонкиТаблицыИсточника = СтруктураТаблицыИсточника.Колонки;
		
		// Приемник
		ПараметрыКорректировки.КоличествоСформированныхТаблиц = ПараметрыКорректировки.КоличествоСформированныхТаблиц + 1;
		
		ИмяТаблицыПриемника = "ВТКорректировка" + ПараметрыКорректировки.ИмяРегистра + "_" + РасчетСебестоимостиУниверсальныеАлгоритмы.ЧислоВСтрокуБезПробелов(ПараметрыКорректировки.КоличествоСформированныхТаблиц);
		РасчетСебестоимостиУниверсальныеАлгоритмы.ДополнитьСтроку(ПараметрыКорректировки.ИменаСформированныхТаблиц, ИмяТаблицыПриемника);
		
		ТекстПоляПриемника =
		"	ДокументыРасчетаСебестоимости.Ссылка КАК Регистратор,
		|	&КонецПредыдущегоПериода КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&ТипЗаписиКорректировки КАК ТипЗаписи";
		
		Если ПараметрыКорректировки.ОписаниеРегистра.ЕстьРасчетПартий Тогда
			ТекстПоляПриемника = ТекстПоляПриемника + ",
				|	ИСТИНА КАК РасчетПартий";
		КонецЕсли;
		
		Для Каждого КлючИЗначение2 Из ИменаКолонокРегистра Цикл // колонки регистра-приемника
			
			ИмяКолонки = КлючИЗначение2.Ключ;
			
			Если ИменаСлужебныхКолонок.Свойство(ИмяКолонки) Тогда // служебная
				
				Продолжить;// текст уже сформирован
				
			ИначеЕсли КолонкиТаблицыИсточника.Найти(ИмяКолонки) <> Неопределено Тогда // взять колонку из временной таблицы
				
				Если ИменаРесурсовРегистра.Свойство(ИмяКолонки) И СтруктураТаблицСторно.Свойство(ИмяТаблицыИсточника) Тогда
					ТекстЗнак = "-"; // ресурсы сторно со знаком минус
				Иначе
					ТекстЗнак = "";
				КонецЕсли;
				
				ТекстПоле = ТекстЗнак + "Т." + ИмяКолонки;
				
			Иначе // установить пустое значение колонки
				
				ТипЗначенияКолонки = РасчетСебестоимостиУниверсальныеАлгоритмы.ОписанияТиповБезТипаNull(
					ПараметрыКорректировки.ОписаниеРегистра.Таблица.Колонки.Найти(ИмяКолонки).ТипЗначения);
				
				ТекстПоле = "&ПустоеЗначение_" + ИмяКолонки;
				
				Запрос.УстановитьПараметр(
					"ПустоеЗначение_" + ИмяКолонки,
					ТипЗначенияКолонки.ПривестиЗначение(Неопределено));
				
			КонецЕсли;
			
			РасчетСебестоимостиУниверсальныеАлгоритмы.ДополнитьСтроку(
				ТекстПоляПриемника,
				"	" + ТекстПоле + " КАК " + ИмяКолонки);
			
		КонецЦикла; // колонки регистра-приемника
		
		Запрос.Текст = ШаблонЗапроса;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяПриемника",  ИмяТаблицыПриемника);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяИсточника",  ИмяТаблицыИсточника);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляПриемника", ТекстПоляПриемника);
		
		Запрос.Выполнить();
		
	КонецЦикла; // добавляемые таблицы
	
	Если УдалятьТаблицыКорректировки Тогда
		РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета, ИменаТаблицКорректировки);
	КонецЕсли;
	
	Если УдалятьТаблицыСторно Тогда
		РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета, ИменаТаблицСторно);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗавершитьКорректировкуОстатковРегистра(ПараметрыРасчета, ПараметрыКорректировки) Экспорт
	
	#Область ФормированиеТаблицыНовыхДвижений
	
	Запрос = Новый Запрос;
	ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	Запрос.УстановитьПараметр("ОрганизацииДляКорректировки", ПараметрыКорректировки.ОрганизацииДляКорректировки);
	
	// Если нет сформированных корректировок, то поместим в <ИмяТаблицыНовыхДвижений> только "старые" движения документов, не являющиеся корректировками.
	// Если корректировки есть, то поместим "старые" движения в ВТПрочиеДвиженияДокументовРасчетаСебестоимости и объединим ее с "новыми" корректировочными движениями в <ИмяТаблицыНовыхДвижений>.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&ПоляРегистра
	|ПОМЕСТИТЬ ВТПрочиеДвиженияДокументовРасчетаСебестоимости
	|ИЗ
	|	&ТаблицаРегистра КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыРасчетаСебестоимостиПрошлогоПериода КАК ДокументыРасчетаСебестоимости
	|		ПО Т.Регистратор = ДокументыРасчетаСебестоимости.Ссылка
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПредыдущегоПериода И &КонецПредыдущегоПериода
	|	И Т.Организация В (&ОрганизацииДляКорректировки)
	|	И &ОтборПоТипуЗаписиНеКорректировки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	1 КАК Количество
	|ИЗ
	|	ВТПрочиеДвиженияДокументовРасчетаСебестоимости КАК Т";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТаблицаРегистра", СтрШаблон("РегистрНакопления.%1", ПараметрыКорректировки.ИмяРегистра));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ПоляОсновнойТаблицыРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоТипуЗаписиНеКорректировки",
		?(ПараметрыКорректировки.ОписаниеРегистра.ЕстьТипЗаписи, "НЕ (Т.ТипЗаписи В (&ТипыЗаписейКонвертацииДанных))", "ИСТИНА"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если ПараметрыКорректировки.КоличествоСформированныхТаблиц = 0 Тогда
		
		ИмяТаблицыНовыхДвижений = "ВТПрочиеДвиженияДокументовРасчетаСебестоимости"; // могут быть только "прочие" движения документа расчета себестоимости
		
	Иначе
		
		Если ПараметрыКорректировки.КоличествоСформированныхТаблиц = 1 И РезультатЗапроса.Пустой() Тогда
			
			ИмяТаблицыНовыхДвижений = ПараметрыКорректировки.ИменаСформированныхТаблиц; // есть только одна таблица корректировочных движений
			
		Иначе
			
			// Объединим все новые корректировочные движения и "прочие" движения документа расчета себестоимости.
			ИмяТаблицыНовыхДвижений = "ВТНовыеДвиженияПоРегистру" + ПараметрыКорректировки.ИмяРегистра;
			
			РасчетСебестоимостиПрикладныеАлгоритмы.ОбъединитьВременныеТаблицы(
				ПараметрыРасчета,
				РасчетСебестоимостиУниверсальныеАлгоритмы.СоединитьСтроки(
					ПараметрыКорректировки.ИменаСформированныхТаблиц,
					"ВТПрочиеДвиженияДокументовРасчетаСебестоимости"),
				ИмяТаблицыНовыхДвижений,
				ПараметрыКорректировки.ОписаниеРегистра.ПоляОсновнойТаблицыРегистра,
				ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра,
				,
				Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ФормированиеТаблицыИзмененныхДокументов
	
	// Сравним движения в <ИмяТаблицыНовыхДвижений> с движениями тех же документов в ИБ.
	ТекстЗапросаВТИзмененныеДокументы = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&ПолеРегистратора
	|ПОМЕСТИТЬ ВТИзмененныеДокументы
	|ИЗ
	|	(ВЫБРАТЬ // новые движения, с плюсом
	|		&ПоляРегистра
	|	ИЗ
	|		&ТаблицаНовыхДвижений КАК Т
	|	ГДЕ
	|		Т.Организация В (&ОрганизацииДляКорректировки)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ // старые движения, с минусом
	|		&ПоляРегистраСМинусом
	|	ИЗ
	|		&ТаблицаРегистра КАК Т
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыРасчетаСебестоимостиПрошлогоПериода КАК ДокументыРасчетаСебестоимости
	|			ПО Т.Регистратор = ДокументыРасчетаСебестоимости.Ссылка
	|	ГДЕ
	|		Т.Период МЕЖДУ &НачалоПредыдущегоПериода И &КонецПредыдущегоПериода
	|		И Т.Организация В (&ОрганизацииДляКорректировки)
	|	) КАК Т
	|";
	
	ТекстЗапросаВТДокументыСДвижениями = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор
	|ПОМЕСТИТЬ ВТДокументыСДвижениями
	|ИЗ
	|	&ТаблицаНовыхДвижений КАК Т
	|";
	
	ТекстЗапросаВТДокументыДляЗаписи = "
	|ВЫБРАТЬ
	|	Т.Регистратор КАК Регистратор,
	|	ВЫБОР КОГДА НовыеДвижения.Регистратор ЕСТЬ NULL ТОГДА ЛОЖЬ ИНАЧЕ ИСТИНА КОНЕЦ КАК ЕстьНовыеДвижения
	|ПОМЕСТИТЬ ВТДокументыДляЗаписи
	|ИЗ
	|	ВТИзмененныеДокументы КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДокументыСДвижениями КАК НовыеДвижения
	|		ПО Т.Регистратор = НовыеДвижения.Регистратор
	|";
	
	ТекстПоляРегистраСМинусом  = "";
	ТекстОтборНепустыхРесурсов = "";
	ТекстГруппировкаПолей 	   = "";
	
	МассивПолей = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
		СтрЗаменить(ПараметрыКорректировки.ОписаниеРегистра.ПоляОсновнойТаблицыРегистра, "%1", "Т."),
		",",
		Истина,
		Истина);
	МассивРесурсов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
		СтрЗаменить(ПараметрыКорректировки.ОписаниеРегистра.ПоляОтбораНепустыхДвижений, "%1", "Т."),
		",",
		Истина,
		Истина);
	
	Для Каждого ТекущееПоле Из МассивПолей Цикл
		
		ЭтоРесурсРегистра = (МассивРесурсов.Найти(ТекущееПоле) <> Неопределено);
		
		Если ЭтоРесурсРегистра Тогда
			// Ресурс
			ТекстОтборНепустыхРесурсов = ТекстОтборНепустыхРесурсов 
				+ ?(ТекстОтборНепустыхРесурсов = "", "", "
				|	ИЛИ ") + "СУММА(" + ТекущееПоле + ") <> 0";
		Иначе
			// Измерение или реквизит
			ТекстГруппировкаПолей = ТекстГруппировкаПолей 
				+ ?(ТекстГруппировкаПолей = "", "", ",
				|	") + ТекущееПоле;
		КонецЕсли;
		
		ТекстПоляРегистраСМинусом = ТекстПоляРегистраСМинусом 
			+ ?(ТекстПоляРегистраСМинусом = "", "", ",
			|		") + ?(ЭтоРесурсРегистра, "-", "") + ТекущееПоле;
		
	КонецЦикла;
	
	// Дополним текст запроса ТекстЗапросаВТИзмененныеДокументы группировками и отбором
	Если ЗначениеЗаполнено(ТекстГруппировкаПолей) Тогда 
		ТекстЗапросаВТИзмененныеДокументы = ТекстЗапросаВТИзмененныеДокументы + "
			|СГРУППИРОВАТЬ ПО
			|	" + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстГруппировкаПолей, "Т.");
	КонецЕсли;
	Если ЗначениеЗаполнено(ТекстОтборНепустыхРесурсов) Тогда 
		ТекстЗапросаВТИзмененныеДокументы = ТекстЗапросаВТИзмененныеДокументы + "
			|ИМЕЮЩИЕ
			|	" + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОтборНепустыхРесурсов, "Т.");
	КонецЕсли;
	
	ТекстыЗапроса = Новый Массив; //Массив из Строка
	
	ТекстыЗапроса.Добавить(ТекстЗапросаВТИзмененныеДокументы);
	ТекстыЗапроса.Добавить(ТекстЗапросаВТДокументыСДвижениями);
	ТекстыЗапроса.Добавить(ТекстЗапросаВТДокументыДляЗаписи);
	
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТаблицаРегистра", СтрШаблон("РегистрНакопления.%1", ПараметрыКорректировки.ИмяРегистра));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТаблицаНовыхДвижений", ИмяТаблицыНовыхДвижений);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляРегистраСМинусом",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПоляРегистраСМинусом, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПолеРегистратора", "Т.Регистратор");
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ПоляОсновнойТаблицыРегистра, "Т."));
	
	Запрос.Выполнить();
	
	#КонецОбласти
	
	#Область ПодготовкаЗаписиДвижений
	
	ПараметрыЗаписи = РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьПараметрыЗаписи(
		ПараметрыРасчета.РасчетныйПериод.НачалоПериода,
		ПараметрыРасчета.НомерЗаданияДоРасчета);
	
	#КонецОбласти
	
	#Область ОчисткаСтарыхДвижений
	
	// Очистим движения документов, у которых нет движений в <ИмяТаблицыНовыхДвижений>, но есть движения в ИБ
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.Регистратор
	|ИЗ
	|	ВТДокументыДляЗаписи КАК Т
	|ГДЕ
	|	НЕ Т.ЕстьНовыеДвижения";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаписатьДвиженияПоРегистру(
		Запрос,
		ПараметрыКорректировки.ОписаниеРегистра.МенеджерРегистра,
		ПараметрыЗаписи);
	
	#КонецОбласти
	
	#Область ЗаписьНовыхДвижений
	
	// Запишем только документы с измененными движениями у которых есть движения в <ИмяТаблицыНовыхДвижений>.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&ПоляРегистра
	|ИЗ
	|	&ТаблицаНовыхДвижений КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДокументыДляЗаписи КАК ДокументыДляЗаписи
	|		ПО Т.Регистратор = ДокументыДляЗаписи.Регистратор
	|ГДЕ
	|	ЕСТЬNULL(ДокументыДляЗаписи.ЕстьНовыеДвижения, ЛОЖЬ)
	|УПОРЯДОЧИТЬ ПО
	|	&УпорядочиваниеПолей";
	
	ТекстУпорядочиваниеПолей  = РасчетСебестоимостиУниверсальныеАлгоритмы.СоединитьСтроки(
		"%1Регистратор, %1Период",
		?(ПараметрыКорректировки.ОписаниеРегистра.ЕстьТипЗаписи, "%1ТипЗаписи", ""),
		ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра,
		ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТаблицаНовыхДвижений", ИмяТаблицыНовыхДвижений);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УпорядочиваниеПолей",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстУпорядочиваниеПолей, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ПоляОсновнойТаблицыРегистра, "Т."));
	
	Попытка
		
		РасчетСебестоимостиПрикладныеАлгоритмы.ЗаписатьДвиженияПоРегистру(
			Запрос,
			ПараметрыКорректировки.ОписаниеРегистра.МенеджерРегистра,
			ПараметрыЗаписи);
		
	Исключение
		
		// Информацию об ошибке добавим в протокол расчета.
		// Затем, по окончании расчета, запишем протокол расчета и только потом вызовем исключение.
		ТекстДляПротокола = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстДляПротокола = СтрЗаменить(ТекстДляПротокола, РасчетСебестоимостиПрикладныеАлгоритмы.СлужебныйСимвол_ПроблемаУжеЗарегистрирована(), "");
		
		РасчетСебестоимостиПрикладныеАлгоритмы.ЗарегистрироватьПроблемуВыполненияРасчета(
			ПараметрыРасчета,
			,
			НСтр("ru='При записи движений диагностированы ошибки'", ОбщегоНазначения.КодОсновногоЯзыка()),
			ТекстДляПротокола);
		
	КонецПопытки;
	
	#КонецОбласти
	
	#Область ОкончаниеЗаписиДвижений
	
	// Все данные записаны в ИБ, вспомогательные таблицы больше не нужны.
	ИменаУдаляемыхТаблиц = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(
		ПараметрыРасчета,
		ПараметрыКорректировки.СуществующиеТаблицы);
	
	МассивСтрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаУдаляемыхТаблиц, ",", Истина, Истина);
	ИменаУдаляемыхТаблиц = "";
	
	// Не удаляем таблицы расчетных остатков и таблицы сформированных корректировок
	// (в случае, если несколько корректировок разных регистров выполняются одновременно).
	Для Каждого ИмяТаблицы Из МассивСтрок Цикл
		
		Если СтрНачинаетсяС(ИмяТаблицы, "ВТРасчетныеНачальныеОстатки")
		 И ИмяТаблицы <> ПараметрыКорректировки.ИмяТаблицыОстатков Тогда
			Продолжить;
		ИначеЕсли СтрНачинаетсяС(ИмяТаблицы, "ВТКорректировка")
		 И НЕ СтрНачинаетсяС(ИмяТаблицы, "ВТКорректировка" + ПараметрыКорректировки.ИмяРегистра + "_") Тогда
			Продолжить;
		КонецЕсли;
		
		РасчетСебестоимостиУниверсальныеАлгоритмы.ДополнитьСтроку(ИменаУдаляемыхТаблиц, ИмяТаблицы);
		
	КонецЦикла;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета, ИменаУдаляемыхТаблиц);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗафиксироватьФактВыполненияКорректировки(ПараметрыРасчета, ПараметрыКорректировки);
	
	#КонецОбласти
	
КонецПроцедуры


Процедура СформироватьТаблицуРасчетныхНачальныхОстатковРегистра(ПараметрыРасчета, ПараметрыКорректировки)
	
	Запрос = Новый Запрос;
	ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	Запрос.УстановитьПараметр("ОрганизацииДляКорректировки", ПараметрыКорректировки.ОрганизацииДляКорректировки);
	
	// Поля для подстановки в шаблоны.
	ИзмеренияРегистра = СтрЗаменить(ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра, "%1", "Т.");
	РесурсыРегистраСумма = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистраССуффиксом,
		"СУММА(Т.Знак * Т.",
		")");
	РесурсыРегистраОстатки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистраССуффиксом,
		"Т.",
		"Остаток");
	РесурсыРегистраОбороты = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистраССуффиксом,
		"Т.",
		"");
		
	ТекстОтборПоТипуЗаписи = ?(ПараметрыКорректировки.ОписаниеРегистра.ЕстьТипЗаписи, "И Т.ТипЗаписи В (&ТипыЗаписейКонвертацииДанных)", "");
	
	// Сформируем отбор по непустым ресурсам регистра.
	ТекстОтборНепустыхРесурсов = "";
	МассивРесурсов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
		СтрЗаменить(ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра, "%1", "Т."),
		",",
		Истина,
		Истина);
	
	Для Каждого ТекущееПоле Из МассивРесурсов Цикл
		
		ТекстОтборНепустыхРесурсов = ТекстОтборНепустыхРесурсов 
			+ ?(ТекстОтборНепустыхРесурсов = "", "", "
			|	ИЛИ ") + "СУММА(Т.Знак * " + ТекущееПоле + ") <> 0";
		
	КонецЦикла;
	
	ТекстЗапросаОстатков = "
	|ВЫБРАТЬ
	|	&ИзмеренияРегистра,
	|	1 КАК Знак,
	|	&РесурсыРегистраОстатки
	|ПОМЕСТИТЬ ТаблицаОстатковИсточники
	|ИЗ
	|	&ИмяТаблицыОстатковРегистраНакопления КАК Т
	|";
	
	ТекстЗапросаОстатков = СтрЗаменить(ТекстЗапросаОстатков, 
								"&ИмяТаблицыОстатковРегистраНакопления",
								СтрШаблон("РегистрНакопления.%1.Остатки(&ГраницаКонецПредыдущегоПериода,
											|			Организация В (&ОрганизацииДляКорректировки))", 
											ПараметрыКорректировки.ИмяРегистра));
	
	ТекстаЗапросаДвижений = "
	|ВЫБРАТЬ
	|	&ИзмеренияРегистра,
	|	ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА -1 ИНАЧЕ 1 КОНЕЦ КАК Знак,
	|	&РесурсыРегистраОбороты
	|ИЗ
	|	&ИмяТаблицыРегистраНакопления КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПредыдущегоПериода И &КонецПредыдущегоПериода
	|	И Т.Организация В (&ОрганизацииДляКорректировки)
	|	И Т.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
	|	И Т.Активность
	|	И &ОтборПоТипуЗаписи
	|";
	
	ТекстаЗапросаДвижений = СтрЗаменить(ТекстаЗапросаДвижений, 
								"&ИмяТаблицыРегистраНакопления",
								СтрШаблон("РегистрНакопления.%1", 
											ПараметрыКорректировки.ИмяРегистра));
	
	ТекстыЗапроса = Новый Массив; // Массив из Строка
	ТекстыЗапроса.Добавить(ТекстЗапросаОстатков);
	ТекстыЗапроса.Добавить(ТекстаЗапросаДвижений);
	
	// Сформируем тексты запросов для выборки из таблиц корректировки, уже сформированных при текущем расчете.
	ТекстШаблонТекущейКорректировки = "
	|ВЫБРАТЬ
	|	&ИзмеренияРегистра,
	|	ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА -1 ИНАЧЕ 1 КОНЕЦ КАК Знак,
	|	&РесурсыРегистраОбороты
	|ИЗ
	|	&ИмяТаблицыРезультатовКорректировки КАК Т";
	
	Для НомерТаблицы = 1 По ПараметрыКорректировки.КоличествоСформированныхТаблиц Цикл
		
		ТекстВыборкиИзСформированныхКорректировок = СтрЗаменить(ТекстШаблонТекущейКорректировки,
			"&ИзмеренияРегистра", ИзмеренияРегистра);
		ТекстВыборкиИзСформированныхКорректировок = СтрЗаменить(ТекстВыборкиИзСформированныхКорректировок,
			"&РесурсыРегистраОбороты", РесурсыРегистраОбороты);
		ТекстВыборкиИзСформированныхКорректировок = СтрЗаменить(ТекстВыборкиИзСформированныхКорректировок,
			"&ИмяТаблицыРезультатовКорректировки", "ВТКорректировка" + ПараметрыКорректировки.ИмяРегистра + "_"
			+ РасчетСебестоимостиУниверсальныеАлгоритмы.ЧислоВСтрокуБезПробелов(НомерТаблицы));
		
		ТекстыЗапроса.Добавить(ТекстВыборкиИзСформированныхКорректировок);
		
	КонецЦикла;
	
	ТекстЗапросаИсточники = СтрСоединить(ТекстыЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстЗапросаТаблицыОстатков =
	"ВЫБРАТЬ
	|	&ИзмеренияРегистра,
	|	&РесурсыРегистраСумма
	|ПОМЕСТИТЬ ИмяТаблицыОстатков
	|ИЗ
	|	ТаблицаОстатковИсточники КАК Т
	|
	|СГРУППИРОВАТЬ ПО
	|	&ИзмеренияРегистра
	|
	|ИМЕЮЩИЕ
	|	&ОтборНепустыхРесурсов
	|";
	
	ТекстыЗапроса = Новый Массив; // Массив из Строка
	ТекстыЗапроса.Добавить(ТекстЗапросаИсточники);
	ТекстыЗапроса.Добавить(ТекстЗапросаТаблицыОстатков);
	
	ШаблонЗапроса = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	// Поля для подстановки в шаблоны.
	ИзмеренияРегистра = СтрЗаменить(ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра, "%1", "Т.");
	РесурсыРегистраСумма = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистраССуффиксом,
		"СУММА(Т.Знак * Т.",
		")");
	РесурсыРегистраОстатки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистраССуффиксом,
		"Т.",
		"Остаток");
	РесурсыРегистраОбороты = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистраССуффиксом,
		"Т.",
		"");
		
	ТекстОтборПоТипуЗаписи = ?(ПараметрыКорректировки.ОписаниеРегистра.ЕстьТипЗаписи, "Т.ТипЗаписи В (&ТипыЗаписейКонвертацииДанных)", "ИСТИНА");
	
	// Сформируем отбор по непустым ресурсам регистра.
	ТекстОтборНепустыхРесурсов = "";
	МассивРесурсов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
		СтрЗаменить(ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра, "%1", "Т."),
		",",
		Истина,
		Истина);
	
	Для Каждого ТекущееПоле Из МассивРесурсов Цикл
		
		ТекстОтборНепустыхРесурсов = ТекстОтборНепустыхРесурсов 
			+ ?(ТекстОтборНепустыхРесурсов = "", "", "
			|	ИЛИ ") + "СУММА(Т.Знак * " + ТекущееПоле + ") <> 0";
		
	КонецЦикла;
	
	ТекстОтборНепустыхРесурсов = ?(ТекстОтборНепустыхРесурсов = "", "ИСТИНА", ТекстОтборНепустыхРесурсов);
	
	// Подставим параметры в текстЗапроса
	Запрос.Текст = ШаблонЗапроса;
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.ВременнаяТаблицаСуществует(ПараметрыРасчета, ПараметрыКорректировки.ИмяТаблицыОстатков) Тогда
		Запрос.Текст =
		"УНИЧТОЖИТЬ ИмяТаблицыОстатков
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|" + Запрос.Текст;
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяТаблицыОстатков", 		ПараметрыКорректировки.ИмяТаблицыОстатков);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИзмеренияРегистра", 		ИзмеренияРегистра);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&РесурсыРегистраСумма", 	РесурсыРегистраСумма);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&РесурсыРегистраОстатки", РесурсыРегистраОстатки);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&РесурсыРегистраОбороты", РесурсыРегистраОбороты);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоТипуЗаписи",		ТекстОтборПоТипуЗаписи);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборНепустыхРесурсов",	ТекстОтборНепустыхРесурсов);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ОчиститьКорректировкиРегистровВСледующихПериодах(ПараметрыРасчета, ПараметрыКорректировки)
	
	ПараметрыКорректировки.ОписаниеРегистра = ПараметрыРасчета.Движения[ПараметрыКорректировки.ИмяРегистра];
	
	Запрос = Новый Запрос;
	ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	Запрос.УстановитьПараметр("ТипЗаписиКорректировки", 	 ПараметрыКорректировки.ТипЗаписиКорректировки);
	Запрос.УстановитьПараметр("ОрганизацииДляКорректировки", ПараметрыКорректировки.ОрганизацииДляКорректировки);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор КАК Регистратор
	|ИЗ
	|	&ИмяРегистра КАК Т
	|ГДЕ
	|	Т.Организация В(&ОрганизацииДляКорректировки)
	|	И Т.Период > &КонецПредыдущегоПериода
	|	И Т.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
	|	И &ОтборПоТипуЗаписи";
	
	ТекстОтборПоТипуЗаписи = ?(ПараметрыКорректировки.ОписаниеРегистра.ЕстьТипЗаписи,
		"Т.ТипЗаписи = &ТипЗаписиКорректировки
		|	И НЕ Т.ТипЗаписи В (&ТипыЗаписейМногократнойКонвертацииДанных)", "");
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяРегистра", "РегистрНакопления." + ПараметрыКорректировки.ИмяРегистра);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоТипуЗаписи", ТекстОтборПоТипуЗаписи);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		ПараметрыЗаписи = РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьПараметрыЗаписи(
			ПараметрыРасчета.РасчетныйПериод.НачалоПериода,
			ПараметрыРасчета.НомерЗаданияДоРасчета);
		
		РасчетСебестоимостиПрикладныеАлгоритмы.ЗаписатьДвиженияПоРегистру(
			Выборка,
			ПараметрыКорректировки.ОписаниеРегистра.МенеджерРегистра,
			ПараметрыЗаписи);
			
	КонецЕсли;
	
КонецПроцедуры


Процедура ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета)
	
	РасчетныйПериод = ПараметрыРасчета.РасчетныйПериод;
	
	// Общие параметры
	Запрос.УстановитьПараметр("НачалоПериода",				  			РасчетныйПериод.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",				  			РасчетныйПериод.КонецПериода);
	Запрос.УстановитьПараметр("НачалоПредыдущегоПериода",	  			НачалоМесяца(РасчетныйПериод.НачалоПериода - 1));
	Запрос.УстановитьПараметр("КонецПредыдущегоПериода",	  			РасчетныйПериод.НачалоПериода - 1);
	Запрос.УстановитьПараметр("НачалоСледующегоПериода",	  			РасчетныйПериод.КонецПериода + 1);
	Запрос.УстановитьПараметр("ГраницаНачалоПериода",		  			Новый Граница(РасчетныйПериод.НачалоПериода, ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("ГраницаКонецПериода",		  			Новый Граница(РасчетныйПериод.КонецПериода, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ГраницаКонецПредыдущегоПериода",			Новый Граница(РасчетныйПериод.НачалоПериода - 1, ВидГраницы.Включая));
	
	Запрос.УстановитьПараметр("МассивОрганизаций",						ПараметрыРасчета.МассивОрганизаций);
	Запрос.УстановитьПараметр("ОрганизацииСОстаткамиПоСебестоимости",	ПараметрыРасчета.ОрганизацииСОстаткамиПоСебестоимости);
	
	Если ПараметрыРасчета.Свойство("ОрганизацииФИФОСкользящая") Тогда
		Запрос.УстановитьПараметр("ОрганизацииФИФОСкользящая",			ПараметрыРасчета.ОрганизацииФИФОСкользящая);
		Запрос.УстановитьПараметр("ОрганизацииНеФИФОСкользящая",		ПараметрыРасчета.ОрганизацииНеФИФОСкользящая);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТипыЗаписейКонвертацииДанных",			  РасчетСебестоимостиПрикладныеАлгоритмы.ТипыЗаписейКонвертацииДанных());
	Запрос.УстановитьПараметр("ТипыЗаписейМногократнойКонвертацииДанных", РасчетСебестоимостиПрикладныеАлгоритмы.ТипыЗаписейМногократнойКонвертацииДанных());
	
	Запрос.УстановитьПараметр("УправленческийУчетОрганизаций",
		РасчетСебестоимостиПовтИсп.УправленческийУчетОрганизаций(РасчетныйПериод.НачалоПериода));
	
	Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	
КонецПроцедуры

Процедура ИнициализироватьДокументыРасчетаСебестоимостиПрошлогоПериода(ПараметрыРасчета) Экспорт
	
	Запрос = Новый Запрос;
	ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);

	// Выберем все не удаленные документы расчета себестоимости за прошлый период.
	// Если по организации уже существует подходящий документ расчета себестоимости, то новый документ создаваться не будет.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СпрОрганизации.Ссылка КАК Организация,
	|	МАКСИМУМ(ЕСТЬNULL(РасчетСебестоимостиТоваров.Ссылка, НЕОПРЕДЕЛЕНО)) КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК СпрОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров КАК РасчетСебестоимостиТоваров
	|		ПО (СпрОрганизации.Ссылка = РасчетСебестоимостиТоваров.Организация)
	|			И (РасчетСебестоимостиТоваров.Дата МЕЖДУ &НачалоПредыдущегоПериода И &КонецПредыдущегоПериода)
	|			И (РасчетСебестоимостиТоваров.Проведен)
	|			И (НЕ РасчетСебестоимостиТоваров.ПредварительныйРасчет)
	|ГДЕ
	|	СпрОрганизации.Ссылка В(&ОрганизацииСОстаткамиПоСебестоимости)
	|
	|СГРУППИРОВАТЬ ПО
	|	СпрОрганизации.Ссылка";
	
	ДокументыРасчетаПоОрганизациям = Новый Соответствие;
	ДокументыРасчетаСебестоимости = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДокументыРасчетаПоОрганизациям.Вставить(Выборка.Организация, Выборка.Ссылка);
	КонецЦикла;
	
	Попытка
		
		// Создадим недостающие документы расчета себестоимости по организациям.
		Для Каждого КлючИЗначение Из ДокументыРасчетаПоОрганизациям Цикл
			
			Если ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
				ДокументыРасчетаСебестоимости.Добавить(КлючИЗначение.Значение);
				Продолжить;
			КонецЕсли;

			ДокументОбъект = Документы.РасчетСебестоимостиТоваров.СоздатьДокумент();
			
			ДанныеЗаполненияДокумента = Новый Структура;
			ДанныеЗаполненияДокумента.Вставить("Дата", 					ПараметрыРасчета.РасчетныйПериод.НачалоПериода - 1);
			ДанныеЗаполненияДокумента.Вставить("Организация", 			КлючИЗначение.Ключ);
			ДанныеЗаполненияДокумента.Вставить("ПредварительныйРасчет", Ложь);
			
			ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату("УчетнаяПолитикаФинансовогоУчета",
				КлючИЗначение.Ключ,
				НачалоМесяца(ПараметрыРасчета.РасчетныйПериод.НачалоПериода - 1)); 
			
			Если ПараметрыУчетнойПолитики <> Неопределено Тогда
				ДанныеЗаполненияДокумента.Вставить("МетодОценки", ПараметрыУчетнойПолитики.МетодОценкиСтоимостиТоваров);
			Иначе
				ДанныеЗаполненияДокумента.Вставить("МетодОценки", Перечисления.МетодыОценкиСтоимостиТоваров.СредняяЗаМесяц);
			КонецЕсли;
			
			ДокументОбъект.Заполнить(ДанныеЗаполненияДокумента);
			
			ДокументОбъект.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения", Истина);
				
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			
			// Запомним документ
			ДокументыРасчетаСебестоимости.Добавить(ДокументОбъект.Ссылка);
			ДокументыРасчетаПоОрганизациям[КлючИЗначение.Ключ] = ДокументОбъект.Ссылка;
			
		КонецЦикла;
		
		Запрос.УстановитьПараметр("ДокументыРасчетаСебестоимости", ДокументыРасчетаСебестоимости);
		
	Исключение
		
		ТекстДляПротокола = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не удалось инициализировать документы расчета себестоимости прошлого периода по причине:
				|%1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение РасчетСебестоимостиПрикладныеАлгоритмы.ТекстИсключениеДляРегистрацииПроблемы(ТекстДляПротокола);
		
	КонецПопытки;
	
	// Сформируем временную таблицу ВТДокументыРасчетаСебестоимости
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.Ссылка,
	|	Т.МоментВремени,
	|	Т.Организация КАК Организация
	|ПОМЕСТИТЬ ВТДокументыРасчетаСебестоимостиПрошлогоПериода
	|ИЗ
	|	Документ.РасчетСебестоимостиТоваров КАК Т
	|ГДЕ
	|	Т.Ссылка В (&ДокументыРасчетаСебестоимости)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация";
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#Область ОперацииСКлючамиАналитик

Функция СформироватьОписаниеКлючейАналитикУчетаНоменклатуры(ИмяРеквизитаАналитики = "АналитикаУчетаНоменклатуры",
			ИсключатьПоля = "", ИсключатьПоляДетализацииМестаХранения = Истина)
	
	ОписаниеКлючей = Новый Структура("ПоляИменованные, ПоляНеИменованные, ПереченьПолей");
	СтруктураИсключемыхПолей = Новый Структура(
		РасчетСебестоимостиУниверсальныеАлгоритмы.СоединитьСтроки(
			ИсключатьПоля,
			?(ИсключатьПоляДетализацииМестаХранения, "ТипМестаХранения, СкладскаяТерритория, Подразделение, Партнер, Контрагент, Договор, Организация", "")));
	
	Для Каждого МетаРеквизиты Из Метаданные.Справочники.КлючиАналитикиУчетаНоменклатуры.Реквизиты Цикл
		
		Если СтруктураИсключемыхПолей.Свойство(МетаРеквизиты.Имя) Тогда
			Продолжить;
		КонецЕсли;
		
		РасчетСебестоимостиУниверсальныеАлгоритмы.ДополнитьСтроку(
			ОписаниеКлючей.ПоляИменованные,
			"%1" + ИмяРеквизитаАналитики + "." + МетаРеквизиты.Имя + " КАК " + ИмяРеквизитаАналитики + "_" + МетаРеквизиты.Имя,,,,
			"," + Символы.ПС + "	");
		
		РасчетСебестоимостиУниверсальныеАлгоритмы.ДополнитьСтроку(
			ОписаниеКлючей.ПоляНеИменованные,
			"%1" + ИмяРеквизитаАналитики + "." + МетаРеквизиты.Имя,,,,
			"," + Символы.ПС + "	");
		
		РасчетСебестоимостиУниверсальныеАлгоритмы.ДополнитьСтроку(
			ОписаниеКлючей.ПереченьПолей,
			ИмяРеквизитаАналитики + "_" + МетаРеквизиты.Имя);
		
	КонецЦикла;
	
	Возврат ОписаниеКлючей;
	
КонецФункции

Функция СформироватьАналитикиНоменклатурыБезНазначения(ПараметрыРасчета,
			ИмяИсходнойТаблицы, ИмяТаблицыПриемника, ИмяПоляАналитики = "АналитикаУчетаНоменклатуры")
	
	Запрос = Новый Запрос;
	ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&ИмяПоляАналитики КАК АналитикаУчетаНоменклатуры
	|ПОМЕСТИТЬ ВТКлючиАналитикиНоменклатурыСНазначениями
	|ИЗ
	|	&ИмяИсходнойТаблицы КАК Т
	|ГДЕ
	|	&ИмяПоляАналитики <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|	И &ИмяПоляНазначениеАналитики <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Т.АналитикаУчетаНоменклатуры
	|ИЗ
	|	ВТКлючиАналитикиНоменклатурыСНазначениями КАК Т
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяИсходнойТаблицы", ИмяИсходнойТаблицы);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяПоляАналитики",   СтрШаблон("Т.%1",ИмяПоляАналитики));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяПоляНазначениеАналитики", СтрШаблон("Т.%1.Назначение",ИмяПоляАналитики));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета, "ВТКлючиАналитикиНоменклатурыСНазначениями");
		Возврат Ложь; // корректировка аналитик не требуется
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Т.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	Т.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	Т.АналитикаУчетаНоменклатуры.Серия КАК Серия,
	|	Т.АналитикаУчетаНоменклатуры.МестоХранения КАК МестоХранения,
	|	Т.АналитикаУчетаНоменклатуры.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ЕСТЬNULL(Ключи.Ссылка, НЕОПРЕДЕЛЕНО) КАК АналитикаУчетаНоменклатурыБезНазначения
	|ПОМЕСТИТЬ ВТКлючиАналитикиНоменклатурыБезНазначения
	|ИЗ
	|	ВТКлючиАналитикиНоменклатурыСНазначениями КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
	|		ПО Т.АналитикаУчетаНоменклатуры.Номенклатура = Ключи.Номенклатура
	|			И Т.АналитикаУчетаНоменклатуры.Характеристика = Ключи.Характеристика
	|			И Т.АналитикаУчетаНоменклатуры.Серия = Ключи.Серия
	|			И Т.АналитикаУчетаНоменклатуры.МестоХранения = Ключи.МестоХранения
	|			И Т.АналитикаУчетаНоменклатуры.СтатьяКалькуляции = Ключи.СтатьяКалькуляции
	|			И (Ключи.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
	|			И (Ключи.ПометкаУдаления = ЛОЖЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Т.Номенклатура КАК Номенклатура,
	|	Т.Характеристика КАК Характеристика,
	|	Т.Серия КАК Серия,
	|	Т.МестоХранения КАК МестоХранения,
	|	Т.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение
	|ИЗ
	|	ВТКлючиАналитикиНоменклатурыБезНазначения КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатурыБезНазначения = НЕОПРЕДЕЛЕНО";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ТаблицаАналитик = Новый ТаблицаЗначений;
	ТаблицаАналитик.Колонки.Добавить("АналитикаУчетаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаНоменклатуры"));
	ТаблицаАналитик.Колонки.Добавить("АналитикаУчетаНоменклатурыБезНазначения", Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаНоменклатуры"));
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаАналитик.Добавить();
		НоваяСтрока.АналитикаУчетаНоменклатуры = Выборка.АналитикаУчетаНоменклатуры;
		
		НоваяСтрока.АналитикаУчетаНоменклатурыБезНазначения = РегистрыСведений.АналитикаУчетаНоменклатуры.СоздатьКлючАналитики(Выборка);
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ТаблицаАналитик", ТаблицаАналитик);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Т.АналитикаУчетаНоменклатурыБезНазначения КАК АналитикаУчетаНоменклатурыБезНазначения
	|ПОМЕСТИТЬ ВТНовыеКлючиАналитикиНоменклатурыБезНазначения
	|ИЗ
	|	&ТаблицаАналитик КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА Т.АналитикаУчетаНоменклатурыБезНазначения = НЕОПРЕДЕЛЕНО
	|			ТОГДА НовыеКлючи.АналитикаУчетаНоменклатурыБезНазначения
	|		ИНАЧЕ Т.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ КАК АналитикаУчетаНоменклатурыБезНазначения
	|ПОМЕСТИТЬ ВТКлючиДляЗамены
	|ИЗ
	|	ВТКлючиАналитикиНоменклатурыБезНазначения КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНовыеКлючиАналитикиНоменклатурыБезНазначения КАК НовыеКлючи
	|		ПО Т.АналитикаУчетаНоменклатуры = НовыеКлючи.АналитикаУчетаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ПоляИсходнойТаблицы
	|ПОМЕСТИТЬ ИмяТаблицыПриемника
	|ИЗ
	|	&ИмяИсходнойТаблицы КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКлючиДляЗамены КАК НовыеКлючи
	|		ПО &ИмяПоляАналитики = НовыеКлючи.АналитикаУчетаНоменклатуры
	|";
	
	ПоляИсходнойТаблицы    = "";
	КолонкиИсходнойТаблицы = РасчетСебестоимостиПрикладныеАлгоритмы.ВыгрузитьВременнуюТаблицу(
		ПараметрыРасчета,
		ИмяИсходнойТаблицы,
		0).Колонки;
	
	Для Каждого ТекущаяКолонка Из КолонкиИсходнойТаблицы Цикл
		
		Если НРег(ТекущаяКолонка.Имя) = НРег(ИмяПоляАналитики) Тогда
			РасчетСебестоимостиУниверсальныеАлгоритмы.ДополнитьСтроку(
				ПоляИсходнойТаблицы,
				"ЕСТЬNULL(НовыеКлючи.АналитикаУчетаНоменклатурыБезНазначения, Т." + ИмяПоляАналитики + ") КАК " + ИмяПоляАналитики,,,,
				"," + Символы.ПС + "	");
		Иначе
			РасчетСебестоимостиУниверсальныеАлгоритмы.ДополнитьСтроку(
				ПоляИсходнойТаблицы,
				"Т." + ТекущаяКолонка.Имя,,,,
				"," + Символы.ПС + "	");
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяИсходнойТаблицы",  ИмяИсходнойТаблицы);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяТаблицыПриемника", ИмяТаблицыПриемника);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяПоляАналитики",    СтрШаблон("Т.%1", ИмяПоляАналитики));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляИсходнойТаблицы", ПоляИсходнойТаблицы);
	
	Запрос.Выполнить();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета,
		"ВТКлючиАналитикиНоменклатурыСНазначениями, ВТКлючиАналитикиНоменклатурыБезНазначения,
		|ВТНовыеКлючиАналитикиНоменклатурыБезНазначения, ВТКлючиДляЗамены");
	
	Возврат Истина;
	
КонецФункции

Функция СформироватьАналитикиНоменклатурыПоЗначениямПолей(ПараметрыРасчета,
			ИмяИсходнойТаблицы, ИмяТаблицыПриемника, ИмяПоляАналитики = "АналитикаУчетаНоменклатуры")
	
	Запрос = Новый Запрос;
	ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.АналитикаУчетаНоменклатуры_Номенклатура КАК Номенклатура,
	|	Т.АналитикаУчетаНоменклатуры_Характеристика КАК Характеристика,
	|	Т.АналитикаУчетаНоменклатуры_Серия КАК Серия,
	|	Т.АналитикаУчетаНоменклатуры_МестоХранения КАК МестоХранения,
	|	Т.АналитикаУчетаНоменклатуры_Назначение КАК Назначение,
	|	Т.АналитикаУчетаНоменклатуры_СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ЕСТЬNULL(Ключи.Ссылка, НЕОПРЕДЕЛЕНО) КАК АналитикаУчетаНоменклатуры
	|ПОМЕСТИТЬ ВТПоляКлючейАналитикиНоменклатуры
	|ИЗ
	|	&ИмяИсходнойТаблицы КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
	|		ПО Т.АналитикаУчетаНоменклатуры_Номенклатура = Ключи.Номенклатура
	|			И Т.АналитикаУчетаНоменклатуры_Характеристика = Ключи.Характеристика
	|			И Т.АналитикаУчетаНоменклатуры_Серия = Ключи.Серия
	|			И Т.АналитикаУчетаНоменклатуры_МестоХранения = Ключи.МестоХранения
	|			И Т.АналитикаУчетаНоменклатуры_Назначение = Ключи.Назначение
	|			И Т.АналитикаУчетаНоменклатуры_СтатьяКалькуляции = Ключи.СтатьяКалькуляции
	|			И (Ключи.ПометкаУдаления = ЛОЖЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	Т.Серия,
	|	Т.МестоХранения,
	|	Т.Назначение,
	|	Т.СтатьяКалькуляции
	|ИЗ
	|	ВТПоляКлючейАналитикиНоменклатуры КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры = НЕОПРЕДЕЛЕНО";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяИсходнойТаблицы", ИмяИсходнойТаблицы);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	МетаРеквизиты = Метаданные.Справочники.КлючиАналитикиУчетаНоменклатуры.Реквизиты;
	
	ТаблицаАналитик = Новый ТаблицаЗначений;
	ТаблицаАналитик.Колонки.Добавить("Номенклатура", 	  МетаРеквизиты.Номенклатура.Тип);
	ТаблицаАналитик.Колонки.Добавить("Характеристика", 	  МетаРеквизиты.Характеристика.Тип);
	ТаблицаАналитик.Колонки.Добавить("Серия", 			  МетаРеквизиты.Серия.Тип);
	ТаблицаАналитик.Колонки.Добавить("МестоХранения", 	  МетаРеквизиты.МестоХранения.Тип);
	ТаблицаАналитик.Колонки.Добавить("Назначение", 		  МетаРеквизиты.Назначение.Тип);
	ТаблицаАналитик.Колонки.Добавить("СтатьяКалькуляции", МетаРеквизиты.СтатьяКалькуляции.Тип);
	ТаблицаАналитик.Колонки.Добавить("АналитикаУчетаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаНоменклатуры"));
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаАналитик.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		НоваяСтрока.АналитикаУчетаНоменклатуры = РегистрыСведений.АналитикаУчетаНоменклатуры.СоздатьКлючАналитики(Выборка);
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ТаблицаАналитик", ТаблицаАналитик);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	Т.Серия,
	|	Т.МестоХранения,
	|	Т.Назначение,
	|	Т.СтатьяКалькуляции
	|ПОМЕСТИТЬ ВТНовыеКлючиАналитикиНоменклатуры
	|ИЗ
	|	&ТаблицаАналитик КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ПоляИсходнойТаблицы
	|ПОМЕСТИТЬ ИмяТаблицыПриемника
	|ИЗ
	|	&ИмяИсходнойТаблицы КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНовыеКлючиАналитикиНоменклатуры КАК НовыеКлючи
	|		ПО Т.АналитикаУчетаНоменклатуры_Номенклатура = НовыеКлючи.Номенклатура
	|			И Т.АналитикаУчетаНоменклатуры_Характеристика = НовыеКлючи.Характеристика
	|			И Т.АналитикаУчетаНоменклатуры_Серия = НовыеКлючи.Серия
	|			И Т.АналитикаУчетаНоменклатуры_МестоХранения = НовыеКлючи.МестоХранения
	|			И Т.АналитикаУчетаНоменклатуры_Назначение = НовыеКлючи.Назначение
	|			И Т.АналитикаУчетаНоменклатуры_СтатьяКалькуляции = НовыеКлючи.СтатьяКалькуляции
	|";
	
	ПоляИсходнойТаблицы    = "";
	КолонкиИсходнойТаблицы = РасчетСебестоимостиПрикладныеАлгоритмы.ВыгрузитьВременнуюТаблицу(
		ПараметрыРасчета,
		ИмяИсходнойТаблицы,
		0).Колонки;
	
	РасчетСебестоимостиУниверсальныеАлгоритмы.ДополнитьСтроку(
		ПоляИсходнойТаблицы,
		"ЕСТЬNULL(НовыеКлючи.АналитикаУчетаНоменклатуры, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)) КАК " + ИмяПоляАналитики,,,,
		"," + Символы.ПС + "	");
	
	Для Каждого ТекущаяКолонка Из КолонкиИсходнойТаблицы Цикл
		
		РасчетСебестоимостиУниверсальныеАлгоритмы.ДополнитьСтроку(
				ПоляИсходнойТаблицы,
				"Т." + ТекущаяКолонка.Имя,,,,
				"," + Символы.ПС + "	");
		
	КонецЦикла;
	
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяИсходнойТаблицы",  ИмяИсходнойТаблицы);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяТаблицыПриемника", ИмяТаблицыПриемника);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляИсходнойТаблицы", ПоляИсходнойТаблицы);
	
	Запрос.Выполнить();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета,
		"ВТПоляКлючейАналитикиНоменклатуры, ВТНовыеКлючиАналитикиНоменклатуры");
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ВосстановлениеДвижений

// Процедура выполняет перепроведение всех документов за период по регистрам накопления:
// "Себестоимость товаров", "Выручка и себестоимость продаж", "Прочие расходы", "Партии прочих расходов"ю
// Выполняется только при работе механизма автотестирования себестоимости.
// Предназначена для выявления ошибок, связанных с проведением документов по регистрам.
//
Процедура ПерепровестиДокументыПоРегистрам(НачалоПериода, КонецПериода, МассивОрганизаций)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",      КонецПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	
	#Область СебестоимостьТоваров
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Регистратор КАК Ссылка,
	|	ДД.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДД
	|ГДЕ
	|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И НЕ ДД.РасчетПартий
	|	И НЕ ДД.РасчетСебестоимости
	|";
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
		НачалоПериода,
		КонецПериода,
		МассивОрганизаций,
		ВыборкаДокументов,
		Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя);
		
	#КонецОбласти
	
	#Область ВыручкаИСебестоимостьПродаж
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Регистратор КАК Ссылка,
	|	ДД.АналитикаУчетаПоПартнерам.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ДД
	|ГДЕ
	|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.АналитикаУчетаПоПартнерам.Организация В (&МассивОрганизаций)
	|	И НЕ ДД.РасчетПартий
	|	И НЕ ДД.РасчетСебестоимости
	|";
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
		НачалоПериода,
		КонецПериода,
		МассивОрганизаций,
		ВыборкаДокументов,
		Метаданные.РегистрыНакопления.ВыручкаИСебестоимостьПродаж.Имя);
		
	#КонецОбласти
	
	#Область ПрочиеРасходы
	
	Запрос.УстановитьПараметр("ТипыДокументовОСиНМА", РасчетСебестоимостиПрикладныеАлгоритмы.ТипыДокументовОСиНМА());
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Регистратор КАК Ссылка,
	|	ДД.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК ДД
	|ГДЕ
	|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И НЕ ДД.РасчетПартий
	|	И НЕ ДД.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(ДД.Регистратор) В (&ТипыДокументовОСиНМА)
	|";
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
		НачалоПериода,
		КонецПериода,
		МассивОрганизаций,
		ВыборкаДокументов,
		Метаданные.РегистрыНакопления.ПрочиеРасходы.Имя);
		
	#КонецОбласти
	
	#Область ПартииПрочихРасходов
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Регистратор КАК Ссылка,
	|	ДД.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК ДД
	|ГДЕ
	|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И НЕ ДД.РасчетПартий
	|	И НЕ ТИПЗНАЧЕНИЯ(ДД.Регистратор) В (&ТипыДокументовОСиНМА)
	|";
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
		НачалоПериода,
		КонецПериода,
		МассивОрганизаций,
		ВыборкаДокументов,
		Метаданные.РегистрыНакопления.ПартииПрочихРасходов.Имя);
		
	#КонецОбласти
	
КонецПроцедуры

#Область РегистрНакопления_ТоварыОрганизаций

Процедура ИсправитьДвиженияВозвратТоваровМеждуОрганизациямиПоТоварамОрганизаций(НачалоПериода, ОкончаниеПериода, МассивОрганизаций)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Регистратор КАК Ссылка,
	|	ДД.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций КАК ДД
	|ГДЕ
	|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И ДД.Регистратор ССЫЛКА Документ.ВозвратТоваровМеждуОрганизациями
	|	И ДД.Количество < 0
	|	И ДД.КорАналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|";
	
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода",  ОкончаниеПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
	    НачалоПериода,
	    ОкончаниеПериода,
	    МассивОрганизаций,
		Выборка,
		Метаданные.РегистрыНакопления.ТоварыОрганизаций.Имя);
	
КонецПроцедуры

#КонецОбласти


#Область РегистрНакопления_ПартииТоваровОрганизаций

Процедура ВосстановитьДвиженияПоПартиямТоваров(НачалоПериода, ОкончаниеПериода, МассивОрганизаций) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Ссылка,
	|	ДД.Организация,
	|	СУММА(ДД.Количество)
	|ИЗ (
	|	ВЫБРАТЬ
	|		ДД.Регистратор КАК Ссылка,
	|		ДД.Организация КАК Организация,
	|		СУММА(ДД.Количество) КАК Количество
	|	ИЗ 
	|		РегистрНакопления.ТоварыОрганизаций КАК ДД
	|	ГДЕ
	|		ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И ДД.Организация В (&МассивОрганизаций)
	|		И ДД.Первичное
	|		И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	СГРУППИРОВАТЬ ПО
	|		ДД.Регистратор,
	|		ДД.Организация
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДД.Регистратор КАК Ссылка,
	|		ДД.Организация КАК Организация,
	|		СУММА(ДД.Количество) КАК Количество
	|	ИЗ 
	|		РегистрНакопления.ТоварыКОформлениюДокументовИмпорта КАК ДД
	|	ГДЕ
	|		ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И ДД.Организация В (&МассивОрганизаций)
	|		И ДД.Первичное
	|		И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И ДД.ТипДокументаИмпорта = &ТипДокументаИмпорта
	|	СГРУППИРОВАТЬ ПО
	|		ДД.Регистратор,
	|		ДД.Организация
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|		
	|	ВЫБРАТЬ
	|		ДД.Регистратор КАК Ссылка,
	|		ДД.ОрганизацияВладелец КАК Организация,
	|		СУММА(ДД.Количество) КАК Количество
	|	ИЗ 
	|		РегистрНакопления.ТоварыОрганизацийКПередаче КАК ДД
	|	ГДЕ
	|		ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И ДД.ВидЗапасовПродавца.Организация В (&МассивОрганизаций)
	|		И ДД.Первичное
	|		И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	СГРУППИРОВАТЬ ПО
	|		ДД.Регистратор,
	|		ДД.ОрганизацияВладелец
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|		
	|	ВЫБРАТЬ
	|		ДД.Регистратор КАК Ссылка,
	|		ДД.ВидЗапасов.Организация КАК Организация,
	|		СУММА(ДД.КоличествоСписано) КАК Количество
	|	ИЗ 
	|		РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту КАК ДД
	|	ГДЕ
	|		ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И ДД.ВидЗапасов.Организация В (&МассивОрганизаций)
	|		И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	СГРУППИРОВАТЬ ПО
	|		ДД.Регистратор,
	|		ДД.ВидЗапасов.Организация
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|		
	|	ВЫБРАТЬ
	|		ДД.Регистратор КАК Ссылка,
	|		ДД.Организация КАК Организация,
	|		СУММА(-ДД.Количество) КАК Количество
	|	ИЗ 
	|		РегистрНакопления.ПартииТоваровОрганизаций КАК ДД
	|	ГДЕ
	|		ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И ДД.Организация В (&МассивОрганизаций)
	|		И ДД.Первичное
	|		И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	СГРУППИРОВАТЬ ПО
	|		ДД.Регистратор,
	|		ДД.Организация
	|	) КАК ДД
	|СГРУППИРОВАТЬ ПО
	|	ДД.Ссылка,
	|	ДД.Организация
	|ИМЕЮЩИЕ
	|	СУММА(ДД.Количество) <> 0
	|");
	
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода",  ОкончаниеПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	Запрос.УстановитьПараметр("ТипДокументаИмпорта", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ТаможеннаяДекларацияИмпорт"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
	    НачалоПериода,
	    ОкончаниеПериода,
	    МассивОрганизаций,
		Выборка,
		Метаданные.РегистрыНакопления.ПартииТоваровОрганизаций.Имя);
	
КонецПроцедуры

#КонецОбласти

#Область РегистрНакопления_СебестоимостьТоваров

Процедура ИсправитьДвиженияПеремещенийТоваровПоСебестоимостиТоваров(НачалоПериода, ОкончаниеПериода, МассивОрганизаций)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДД.Регистратор КАК Ссылка,
	|	ДД.Организация КАК Организация
	|ИЗ 
	|	РегистрНакопления.СебестоимостьТоваров КАК ДД
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров КАК Перемещение
	|		ПО Перемещение.Ссылка = ДД.Регистратор
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК СпрВидыЗапасов
	|		ПО СпрВидыЗапасов.Ссылка = ДД.ВидЗапасов
	|ГДЕ
	|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И СпрВидыЗапасов.РеализацияЗапасовДругойОрганизации
	|";
	
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода",  ОкончаниеПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
	    НачалоПериода,
	    ОкончаниеПериода,
	    МассивОрганизаций,
		Выборка,
		Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя);
	
КонецПроцедуры

Процедура ЗаполнитьКорОрганизациюВОперацияхРеглУчет(НачалоПериода, ОкончаниеПериода, МассивОрганизаций)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода",  ОкончаниеПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Регистратор				КАК Ссылка,
		|	Т.Организация				КАК Организация
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК Т
		|ГДЕ
		|	Т.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И Т.Активность
		|	И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И Т.ХозяйственнаяОперация В (
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиентуРеглУчет))
		|	И Т.КорОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
	    НачалоПериода,
	    ОкончаниеПериода,
	    МассивОрганизаций,
		Выборка,
		"СебестоимостьТоваров");
			
КонецПроцедуры

Процедура ИсправитьКорОрганизациюВВозвратеМеждуОрганизациями(НачалоПериода, ОкончаниеПериода, МассивОрганизаций)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода",  ОкончаниеПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Регистратор				КАК Ссылка,
		|	Т.Организация				КАК Организация
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК Т
		|ГДЕ
		|	Т.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И Т.Активность
		|	И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровМеждуОрганизациями)
		|	И Т.КорОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
	    НачалоПериода,
	    ОкончаниеПериода,
	    МассивОрганизаций,
		Выборка,
		"СебестоимостьТоваров");
			
КонецПроцедуры

Процедура ИсправитьКорОрганизациюВИсправлениеРазвернутогоСальдо(НачалоПериода, ОкончаниеПериода, МассивОрганизаций)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода",  ОкончаниеПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор				КАК Ссылка,
	|	Т.Организация				КАК Организация
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И Т.Регистратор ССЫЛКА Документ.ИсправлениеРазвернутогоСальдоТоваровОрганизаций
	|	И Т.ОрганизацияОтгрузки = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И Т.Активность
	|";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
	    НачалоПериода,
	    ОкончаниеПериода,
	    МассивОрганизаций,
		Выборка,
		"ТоварыОрганизаций");
			
КонецПроцедуры

Процедура ВосстановитьДвиженияПрочегоОприходованияТоваровПоСебестоимостиТоваров(НачалоПериода, ОкончаниеПериода, МассивОрганизаций) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПрочиеОприходованияТоваровСДублямиДвиженийСебестоимостиТоваров();
	
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода",  ОкончаниеПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
	    НачалоПериода,
	    ОкончаниеПериода,
	    МассивОрганизаций,
		Выборка,
		Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя);
	
	КонецПроцедуры
	
// Возвращает текст запроса, позволяющего получить список документов ПрочееОприходованиеТоваров
// с некорректными (задублированными) движениями по себестоимости товаров.
//
//	Возвращаемое значение:
//		Строка - текст запроса
//
Функция ТекстЗапросаПрочиеОприходованияТоваровСДублямиДвиженийСебестоимостиТоваров() Экспорт
	Возврат "
		|ВЫБРАТЬ
		|	ДД.Ссылка,
		|	ДД.Организация
		|ИЗ (
		|	ВЫБРАТЬ
		|		ДД.Регистратор КАК Ссылка,
		|		ДД.Организация КАК Организация,
		|		СУММА(ДД.Количество) КАК Количество
		|	ИЗ 
		|		РегистрНакопления.СебестоимостьТоваров КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрочееОприходованиеТоваров КАК Оприходование
		|			ПО Оприходование.Ссылка = ДД.Регистратор
		|	ГДЕ
		|		ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И ДД.Организация В (&МассивОрганизаций)
		|	СГРУППИРОВАТЬ ПО
		|		ДД.Регистратор,
		|		ДД.Организация
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДД.Регистратор КАК Ссылка,
		|		ДД.Организация КАК Организация,
		|		СУММА(-ДД.Количество) КАК Количество
		|	ИЗ 
		|		РегистрНакопления.ТоварыОрганизаций КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрочееОприходованиеТоваров КАК Оприходование
		|			ПО Оприходование.Ссылка = ДД.Регистратор
		|	ГДЕ
		|		ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И ДД.Организация В (&МассивОрганизаций)
		|	СГРУППИРОВАТЬ ПО
		|		ДД.Регистратор,
		|		ДД.Организация
		|		
		|	) КАК ДД
		|СГРУППИРОВАТЬ ПО
		|	ДД.Ссылка,
		|	ДД.Организация
		|	
		|ИМЕЮЩИЕ
		|	СУММА(ДД.Количество) <> 0
		|";
КонецФункции

Процедура ВосстановитьДвиженияПоСебестоимостиТоваровСформированныеВВерсииПУ22(НачалоПериода, ОкончаниеПериода, МассивОрганизаций) Экспорт
	
	ПартионныйУчетВключен = РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии21(НачалоПериода);
	
	Если ПартионныйУчетВключен Тогда
		Возврат; // поддерживается только переход ПУ 2.2 -> ПУ выключен
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	// Перепроведем документы с заполненными полями партионного учета версии 2.2.
	Запрос.Текст = ТекстЗапросаДвиженияПоСебестоимостиТоваровСформированныеВВерсииПУ22();
	
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода",  ОкончаниеПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	Запрос.УстановитьПараметр("ТипыЗаписейКонвертацииДанных",
		РасчетСебестоимостиПрикладныеАлгоритмы.ТипыЗаписейКонвертацииДанных());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
	    НачалоПериода,
	    ОкончаниеПериода,
	    МассивОрганизаций,
		Выборка,
		Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя);
	
	// Распроведем документы формирования начальных остатков партионного учета версии 2.2.
	Запрос.Текст = ТекстЗапросаДвиженияПоФормированиюОстатковСебестоимостиТоваровПУ22();
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДокументОстатков = Выборка.Ссылка.ПолучитьОбъект();
		ДокументОстатков.Записать(РежимЗаписиДокумента.ОтменаПроведения);
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаДвиженияПоСебестоимостиТоваровСформированныеВВерсииПУ22() Экспорт
	Возврат
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор КАК Ссылка,
		|	ДД.Организация КАК Организация
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК ДД
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В(&МассивОрганизаций)
		|	И НЕ ДД.РасчетПартий
		|	И НЕ ДД.РасчетСебестоимости
		|	И ДД.Активность
		|	И НЕ ДД.Регистратор ССЫЛКА Документ.КорректировкаРегистров
		|	И (ДД.Партия <> НЕОПРЕДЕЛЕНО
		|		ИЛИ ДД.АналитикаУчетаПартий <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
		|		ИЛИ ДД.АналитикаФинансовогоУчета <> НЕОПРЕДЕЛЕНО
		|		ИЛИ ДД.ВидДеятельностиНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка))
		|";
КонецФункции

Функция ТекстЗапросаДвиженияПоФормированиюОстатковСебестоимостиТоваровПУ22() Экспорт
	Возврат
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор КАК Ссылка
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК ДД
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В(&МассивОрганизаций)
		|	И ДД.ТипЗаписи В (&ТипыЗаписейКонвертацииДанных)
		|	И ДД.Активность
		|	И ДД.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		|";
КонецФункции


#КонецОбласти


#Область РегистрНакопления_ВыручкаИСебестоимостьПродаж

Процедура ВосстановитьДвиженияДокументовСДублямиДвиженийПоВыручкеИСебестоимостиПродаж(НачалоПериода, ОкончаниеПериода, МассивОрганизаций) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		РасчетСебестоимостиПодготовкаДанных.ТекстЗапросаДокументыСДублямиДвиженийПоВыручкеИСебестоимостиПродаж()
		+ "
		|;
		|ВЫБРАТЬ
		|	Т.Ссылка,
		|	Т.Организация
		|ИЗ
		|	ВТРегистраторыСНекорректнымиДвижениями КАК Т";
	
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  	   ОкончаниеПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
	    НачалоПериода,
	    ОкончаниеПериода,
	    МассивОрганизаций,
		Выборка,
		Метаданные.РегистрыНакопления.ВыручкаИСебестоимостьПродаж.Имя);
	
КонецПроцедуры

Процедура ВосстановитьДвиженияПоВыручкеИСебестоимостиПродажСформированныеВВерсииПУ22(НачалоПериода, ОкончаниеПериода, МассивОрганизаций) Экспорт
	
	ПартионныйУчетВключен = РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии21(НачалоПериода);
	
	Если ПартионныйУчетВключен Тогда
		Возврат; // поддерживается только переход ПУ 2.2 -> ПУ выключен
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	// Перепроведем документы с заполненными полями партионного учета версии 2.2.
	Запрос.Текст = ТекстЗапросаДвиженияПоВыручкеИСебестоимостиПродажСформированныеВВерсииПУ22();
	
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода",  ОкончаниеПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
	    НачалоПериода,
	    ОкончаниеПериода,
	    МассивОрганизаций,
		Выборка,
		Метаданные.РегистрыНакопления.ВыручкаИСебестоимостьПродаж.Имя);
	
КонецПроцедуры

Функция ТекстЗапросаДвиженияПоВыручкеИСебестоимостиПродажСформированныеВВерсииПУ22() Экспорт
	Возврат
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор КАК Ссылка,
		|	Аналитики.Организация КАК Организация
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО ДД.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И Аналитики.Организация В(&МассивОрганизаций)
		|	И НЕ ДД.РасчетСебестоимости
		|	И ДД.Активность
		|	И НЕ ДД.Регистратор ССЫЛКА Документ.КорректировкаРегистров
		|	И (ДД.Партия <> НЕОПРЕДЕЛЕНО
		|		ИЛИ ДД.АналитикаУчетаПартий <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка))
		|";
КонецФункции

#КонецОбласти

#Область УчетСебестоимостиПоВидамЗапасовИНазначениям

// Определяется перечень документов, движения которых не соответствуют условиям:
//	- в движениях не заполнен вид запасов кроме исключений, описанных ниже
//	- движения не соответствуют состоянию функциональной опция учета себестоимости по назначениям

Процедура ЗаполнитьВидыЗапасовИНазначенияВСебестоимостиТоваров(НачалоПериода, ОкончаниеПериода, МассивОрганизаций)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  	   ОкончаниеПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	Запрос.УстановитьПараметр("ПартионныйУчетВерсии22", Ложь);
	Запрос.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоНазначениям",
		РасчетСебестоимостиПовтИсп.СебестоимостьТоваровПоНазначениям(НачалоПериода));
	Запрос.УстановитьПараметр("ИспользоватьУчетСебестоимости",
		РасчетСебестоимостиПовтИсп.ФормироватьДвиженияПоРегистрамСебестоимости(НачалоПериода));
	
	// Включение учета по видам запасов.
	Запрос.Текст =
		ТекстЗапросаВключениеУчетаСебестоимостиПоВидамЗапасовПУ21()
		+ "
		|;
		|ВЫБРАТЬ
		|	Т.Ссылка,
		|	Т.Организация
		|ИЗ
		|	ВТРегистраторыСНекорректнымиДвижениями КАК Т";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
	    НачалоПериода,
	    ОкончаниеПериода,
	    МассивОрганизаций,
		Выборка,
		Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя);
		
	// Выключение учета по назначениям.	
	Запрос.Текст =
		 РасчетСебестоимостиПодготовкаДанных.ТекстЗапросаВыключениеУчетаСебестоимостиПоНазначениям()
		+ "
		|;
		|ВЫБРАТЬ
		|	Т.Ссылка,
		|	Т.Организация
		|ИЗ
		|	ВТРегистраторыСНекорректнымиДвижениями КАК Т";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
	    НачалоПериода,
	    ОкончаниеПериода,
	    МассивОрганизаций,
		Выборка,
		Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя);
		
	// Включение учета по назначениям.
	Запрос.Текст =
		РасчетСебестоимостиПодготовкаДанных.ТекстЗапросаВключениеУчетаСебестоимостиПоНазначениям()
		+ "
		|;
		|ВЫБРАТЬ
		|	Т.Ссылка,
		|	Т.Организация
		|ИЗ
		|	ВТРегистраторыСНекорректнымиДвижениями КАК Т";
	Запрос.УстановитьПараметр("ТипДокументаИмпорта", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ТаможеннаяДекларацияИмпорт"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
	    НачалоПериода,
	    ОкончаниеПериода,
	    МассивОрганизаций,
		Выборка,
		Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя);
	
	
КонецПроцедуры

Процедура ЗаполнитьВидыЗапасовИНазначенияВВыручкеИСебестоимостиПродаж(НачалоПериода, ОкончаниеПериода, МассивОрганизаций)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  	   ОкончаниеПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	Запрос.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоНазначениям",
		РасчетСебестоимостиПовтИсп.СебестоимостьТоваровПоНазначениям(НачалоПериода));
	Запрос.УстановитьПараметр("ИспользоватьУчетСебестоимости",
		РасчетСебестоимостиПовтИсп.ФормироватьДвиженияПоРегистрамСебестоимости(НачалоПериода));	
	
	// Включение учета по видам запасов.
	Запрос.Текст =
		РасчетСебестоимостиПодготовкаДанных.ТекстЗапросаВключениеУчетаВыручкиИСебестоимостиПродажПоВидамЗапасов()
		+ "
		|;
		|ВЫБРАТЬ
		|	Т.Ссылка,
		|	Т.Организация
		|ИЗ
		|	ВТРегистраторыСНекорректнымиДвижениями КАК Т";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
	    НачалоПериода,
	    ОкончаниеПериода,
	    МассивОрганизаций,
		Выборка,
		Метаданные.РегистрыНакопления.ВыручкаИСебестоимостьПродаж.Имя);
		
	// Выключение учета по назначениям.	
	Запрос.Текст =
		РасчетСебестоимостиПодготовкаДанных.ТекстЗапросаВыключениеУчетаВыручкиИСебестоимостиПродажПоНазначениям()
		+ "
		|;
		|ВЫБРАТЬ
		|	Т.Ссылка,
		|	Т.Организация
		|ИЗ
		|	ВТРегистраторыСНекорректнымиДвижениями КАК Т";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
	    НачалоПериода,
	    ОкончаниеПериода,
	    МассивОрганизаций,
		Выборка,
		Метаданные.РегистрыНакопления.ВыручкаИСебестоимостьПродаж.Имя);
		
	// Включение учета по назначениям.
	Запрос.Текст =
		РасчетСебестоимостиПодготовкаДанных.ТекстЗапросаВключениеУчетаВыручкиИСебестоимостиПродажПоНазначениям()
		+ "
		|;
		|ВЫБРАТЬ
		|	Т.Ссылка,
		|	Т.Организация
		|ИЗ
		|	ВТРегистраторыСНекорректнымиДвижениями КАК Т";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
	    НачалоПериода,
	    ОкончаниеПериода,
	    МассивОрганизаций,
		Выборка,
		Метаданные.РегистрыНакопления.ВыручкаИСебестоимостьПродаж.Имя);
	
	
КонецПроцедуры

Функция ТекстЗапросаВключениеУчетаСебестоимостиПоВидамЗапасовПУ21()
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""СебестоимостьТоваров""			КАК ИмяРегистра,
		|	СебестоимостьТоваров.Регистратор	КАК Ссылка,
		|	СебестоимостьТоваров.Организация	КАК Организация,
		|	1									КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|ГДЕ
		|	СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СебестоимостьТоваров.Организация В(&МассивОрганизаций)
		|	И СебестоимостьТоваров.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
		// Выбираем движения с ненулевым количеством, что бы исключеить оффлайные движения
		// т.к. при партионном учета 2.1 нет формального признака, выделяющего оффлайновые движения (например, распределение доп. расходов)
		|	И (СебестоимостьТоваров.Количество <> 0
		// Выбираем движения документа "Таможенная декларация на импорт"
		|		ИЛИ СебестоимостьТоваров.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыСПартией)
		|		ИЛИ СебестоимостьТоваров.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыБезПартии)
		// Выбираем движения документов "Заявление о ввозе товаров с ЕАЭС" и "Корректировка приобретения"
		|		ИЛИ СебестоимостьТоваров.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаСтоимости)
		|		ИЛИ СебестоимостьТоваров.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретенияПрошлогоПериода))
		|	И СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		// Товары, выкупленные у комитентов
		|	И НЕ (
		|		СебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
		|		И (СебестоимостьТоваров.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
		|			ИЛИ СебестоимостьТоваров.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Организация)))
		|	И НЕ СебестоимостьТоваров.РасчетСебестоимости
		|	И НЕ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		|";
КонецФункции

#КонецОбласти

#Область РегистрНакопления_ПрочиеРасходы

Процедура ИсправитьДвиженияПриходовДопРасходовПоПрочимРасходам(НачалоПериода, ОкончаниеПериода, МассивОрганизаций)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДД.Регистратор КАК Ссылка,
	               |	ДД.Организация КАК Организация
	               |ИЗ
	               |	РегистрНакопления.ПрочиеРасходы КАК ДД
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииПрочихРасходов КАК ПартииПрочихРасходов
	               |		ПО ДД.Регистратор = ПартииПрочихРасходов.ДокументПоступленияРасходов
	               |			И ДД.Организация = ПартииПрочихРасходов.Организация
	               |			И ДД.Подразделение = ПартииПрочихРасходов.Подразделение
	               |			И ДД.НаправлениеДеятельности = ПартииПрочихРасходов.НаправлениеДеятельности
	               |			И ДД.СтатьяРасходов = ПартииПрочихРасходов.СтатьяРасходов
	               |			И ДД.АналитикаРасходов = ПартииПрочихРасходов.АналитикаРасходов
	               |			И ДД.ХозяйственнаяОперация = ПартииПрочихРасходов.ХозяйственнаяОперация
	               |			И ДД.Сумма = ПартииПрочихРасходов.Стоимость
	               |			И ДД.СуммаРегл <> ПартииПрочихРасходов.СтоимостьРегл
	               |ГДЕ
	               |	ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	               |	И ДД.Организация В(&МассивОрганизаций)
	               |	И ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	               |	И ТИПЗНАЧЕНИЯ(ДД.Регистратор) В (ТИП(Документ.ПриобретениеТоваровУслуг), ТИП(Документ.ПриобретениеУслугПрочихАктивов))";
	
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода",  ОкончаниеПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВосстановитьДвиженияДокументовПоРегиструНакопления(
	    НачалоПериода,
	    ОкончаниеПериода,
	    МассивОрганизаций,
		Выборка,
		Метаданные.РегистрыНакопления.ПрочиеРасходы.Имя);
	
КонецПроцедуры

#КонецОбласти

// Исправление "внештатных" ситуаций в движениях документов
// По своей сути эти процедуры аналогичны обработчикам обновлений.
// Отличие в том, что обработчики должны поправить все данные в ИБ,
// а эти процедуры исправляют данные только в том периоде, по которому выполняется расчет партий.
// Такой подход позволяет не "портить" данные в уже рассчитанных и закрытых периодах.

Процедура ВосстановитьДвиженияДокументовПоРегиструНакопления(НачалоПериода, КонецПериода, МассивОрганизаций, ВыборкаДокументов, ИмяРегистраНакопления)
	
	ПараметрыРасчета = РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьОсновныеПараметрыРасчета(НачалоПериода, КонецПериода,  МассивОрганизаций);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВосстановитьДвиженияДокументовПоРегиструНакопления(
	    ПараметрыРасчета,
		ВыборкаДокументов,
		ИмяРегистраНакопления);
	
КонецПроцедуры

#КонецОбласти


#Область ОписаниеДанныхМеханизма

// Возвращает перечень объектов метаданных, на основании данных которых выполняется расчет партий.
//
// Параметры:
//	ВходящиеДанные - Соответствие - уже инициализированное хранилище для описания входящих данных
//	ТолькоТребующиеПерерасчета - Булево - если установлен, то будет возвращен перечень только тех данных,
//		изменение которых влечет за собой необходимость перерасчета партий и себестоимости
//		При изменении этих данных должна создаваться запись в регистре сведений ЗаданияКРасчетуСебестоимости.
//
// Возвращаемое значение:
//	Соответствие - Ключ - ОбъектМетаданных.
//
Функция ВходящиеДанныеМеханизма(ВходящиеДанные = Неопределено, ТолькоТребующиеПерерасчета = Ложь) Экспорт
	
	Если ВходящиеДанные = Неопределено Тогда
		ВходящиеДанные = Новый Соответствие;
	КонецЕсли;
	
	Если ТолькоТребующиеПерерасчета Тогда
		Значение = Истина; // чтобы можно было проверить вхождение объекта метаданных в это соответствие
	Иначе
		Значение = Неопределено;
	КонецЕсли;
	
	Если НЕ ТолькоТребующиеПерерасчета Тогда
		
		ВходящиеДанные.Вставить(Метаданные.Документы.ВнутреннееПотребление, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.ВозвратТоваровМеждуОрганизациями, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.ВозвратТоваровОтКлиента, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.ЗаказНаПеремещение, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.ЗаказПоставщику, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.КорректировкаПриобретения, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.ПередачаТоваровМеждуОрганизациями, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.ПеремещениеТоваров, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.ПриобретениеТоваровУслуг, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.ПрочееОприходованиеТоваров, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.РасчетСебестоимостиТоваров, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.РеализацияТоваровУслуг, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.СборкаТоваров, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.ТаможеннаяДекларацияИмпорт, Значение);
		

		
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.АналитикаУчетаНоменклатуры, Значение);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.АналитикаУчетаПартий, Значение);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.ЗаданияКРасчетуСебестоимости, Значение);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.СтоимостьТоваров, Значение);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.УчетнаяПолитикаФинансовогоУчета, Значение);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.НастройкиУчетаНДС, Значение);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.ЦеныНоменклатуры, Значение);
		
		
	КонецЕсли;
	
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ДвиженияНоменклатураДоходыРасходы, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ЗаказыНаПеремещение, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ЗаказыПоставщикам, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.МатериалыИРаботыВПроизводстве, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииЗатратНаВыпуск, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииПроизводственныхЗатрат, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииПрочихРасходов, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииРасходовНаСебестоимостьТоваров, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииТоваровОрганизаций, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииТоваровПереданныеНаКомиссию, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПрочиеРасходы, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.СебестоимостьТоваров, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ТоварыКОформлениюОтчетовКомитенту, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ТоварыОрганизаций, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ТоварыКОформлениюДокументовИмпорта, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ТоварыОрганизацийКПередаче, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.РезервыТоваровОрганизаций, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ТоварыПереданныеНаКомиссию, Значение);
	

	
	Возврат ВходящиеДанные;
	
КонецФункции

// Возвращает перечень регистров, обслуживаемых механизмом расчета партий.
//
// Возвращаемое значение:
//	Соответствие - Ключ - ОбъектМетаданных.
//
Функция ИсходящиеДанныеМеханизма(ИсходящиеДанные = Неопределено) Экспорт
	
	// Перечень метаданных регистров, по которым формируются движения по партиям.
	Если ИсходящиеДанные = Неопределено Тогда
		ИсходящиеДанные = Новый Соответствие;
	КонецЕсли;
	
	Значение = Истина; // чтобы можно было проверить вхождение объекта метаданных в это соответствие
	
	ИсходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ДвиженияНоменклатураДоходыРасходы, 		Значение);
	ИсходящиеДанные.Вставить(Метаданные.РегистрыНакопления.МатериалыИРаботыВПроизводстве, 			Значение);
	ИсходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииЗатратНаВыпуск, 					Значение);
	ИсходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииПроизводственныхЗатрат, 			Значение);
	ИсходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииПрочихРасходов, 					Значение);
	ИсходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииРасходовНаСебестоимостьТоваров, 	Значение);
	ИсходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииТоваровОрганизаций, 				Значение);
	ИсходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииТоваровПереданныеНаКомиссию, 		Значение);
	ИсходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПрочиеРасходы, 							Значение);
	ИсходящиеДанные.Вставить(Метаданные.РегистрыНакопления.СебестоимостьТоваров, 					Значение);
	ИсходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииНДСКРаспределению,					Значение);
	
	
	Возврат ИсходящиеДанные;
	
КонецФункции

#КонецОбласти

// Контекстные методы заполнения новой расчетной партии по строке расхода и строке прихода.
#Область ЗаполнитьРасчетнуюПартию

// Используется для ВСЕХ вызовов заполнения расчетной партии
Процедура ЗаполнитьРасчетнуюПартию(Контекст, РасчетнаяПартия, Расход, Приход)
	Если Контекст = "ПартииТоваровОрганизаций" Тогда
		ЗаполнитьПартиюТоваров(РасчетнаяПартия, Расход, Приход);
		
	ИначеЕсли Контекст = "ПартииТоваровПереданныеНаКомиссию" Тогда
		ЗаполнитьПартиюПереданную(РасчетнаяПартия, Расход, Приход);
		
	ИначеЕсли Контекст = "ПартииЗатратНаВыпуск" Тогда
		ЗаполнитьПартиюЗатрат(РасчетнаяПартия, Расход, Приход);
		
	ИначеЕсли Контекст = "ЗаказыПоставщикам" Тогда
		ЗаполнитьЗаказВПриходе(РасчетнаяПартия, Расход, Приход);
		
	ИначеЕсли Контекст = "ПартииПрочихРасходов" Тогда
		ЗаполнитьПартиюПрочих(РасчетнаяПартия, Расход, Приход);
		
	ИначеЕсли Контекст = "ПартииРасходовНаСебестоимостьТоваровПриход" Тогда
		ЗаполнитьПриходПартииРасходов(РасчетнаяПартия, Расход, Приход);
		
	ИначеЕсли Контекст = "ПартииРасходовНаСебестоимостьТоваров" Тогда
		ЗаполнитьПартиюРасходов(РасчетнаяПартия, Расход, Приход);
		
		
	ИначеЕсли Контекст = "ПартииПроизводственныхЗатрат" Тогда
		ЗаполнитьПартиюПроизводственныхЗатрат(РасчетнаяПартия, Расход, Приход);
		
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьПартиюТоваров(РасчетнаяПартия, Расход, Приход) // ПартииТоваровОрганизаций
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Расход);
	
	Если ТипЗнч(РасчетнаяПартия.Регистратор) = Тип("ДокументСсылка.КорректировкаНалогообложенияНДСПартийТоваров")
	 И РасчетнаяПартия.ВидДвижения = ВидДвиженияНакопления.Расход
	 И Приход.НалогообложениеПартии <> Перечисления.ТипыНалогообложенияНДС.ПоФактическомуИспользованию Тогда
		Возврат;
	КонецЕсли;
	
	Если Расход.ТипЗаписи = "Корректировка" Тогда
		Если Расход.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
			Приход.Количество = Приход.Количество + Расход.Количество;
			Приход.Стоимость = Приход.Стоимость + Расход.Стоимость;
			Приход.СтоимостьБезНДС = Приход.СтоимостьБезНДС + Расход.СтоимостьБезНДС;
			Приход.СтоимостьРегл = Приход.СтоимостьРегл + Расход.СтоимостьРегл;
			Приход.НДСРегл = Приход.НДСРегл + Расход.НДСРегл;
			Приход.ПостояннаяРазница = Приход.ПостояннаяРазница + Расход.ПостояннаяРазница;
			Приход.ВременнаяРазница = Приход.ВременнаяРазница + Расход.ВременнаяРазница;
		ИначеЕсли Приход.Количество = Расход.Количество Тогда
			РасчетнаяПартия.Стоимость = Приход.Стоимость;
			РасчетнаяПартия.СтоимостьБезНДС = Приход.СтоимостьБезНДС;
			РасчетнаяПартия.СтоимостьРегл = Приход.СтоимостьРегл;
			РасчетнаяПартия.НДСРегл = Приход.НДСРегл;
			РасчетнаяПартия.ПостояннаяРазница = Приход.ПостояннаяРазница;
			РасчетнаяПартия.ВременнаяРазница = Приход.ВременнаяРазница;
			
			Приход.Количество = 0;
			Приход.Стоимость = 0;
			Приход.СтоимостьБезНДС = 0;
			Приход.СтоимостьРегл = 0;
			Приход.НДСРегл = 0;
			Приход.ПостояннаяРазница = 0;
			Приход.ВременнаяРазница = 0;
		Иначе
			РасчетнаяПартия.Стоимость = Мин(Расход.Стоимость, Приход.Стоимость);
			РасчетнаяПартия.СтоимостьБезНДС = Мин(Расход.СтоимостьБезНДС, Приход.СтоимостьБезНДС);
			РасчетнаяПартия.СтоимостьРегл = Мин(Расход.СтоимостьРегл, Приход.СтоимостьРегл);
			РасчетнаяПартия.НДСРегл = Мин(Расход.НДСРегл, Приход.НДСРегл);
			РасчетнаяПартия.ПостояннаяРазница = Мин(Расход.ПостояннаяРазница, Приход.ПостояннаяРазница);
			РасчетнаяПартия.ВременнаяРазница = Мин(Расход.ВременнаяРазница, Приход.ВременнаяРазница);

			Приход.Количество = Приход.Количество - Расход.Количество;
			Приход.Стоимость = Приход.Стоимость - Мин(Расход.Стоимость, Приход.Стоимость);
			Приход.СтоимостьБезНДС = Приход.СтоимостьБезНДС - Мин(Расход.СтоимостьБезНДС, Приход.СтоимостьБезНДС);
			Приход.СтоимостьРегл = Приход.СтоимостьРегл - Мин(Расход.СтоимостьРегл, Приход.СтоимостьРегл);
			Приход.НДСРегл = Приход.НДСРегл - Мин(Расход.НДСРегл, Приход.НДСРегл);
			Приход.ПостояннаяРазница = Приход.ПостояннаяРазница - Мин(Расход.ПостояннаяРазница, Приход.ПостояннаяРазница);
			Приход.ВременнаяРазница = Приход.ВременнаяРазница - Мин(Расход.ВременнаяРазница, Приход.ВременнаяРазница);
		КонецЕсли;
		Расход.Стоимость = 0;
		Расход.СтоимостьБезНДС = 0;
		Расход.СтоимостьРегл = 0;
		Расход.НДСРегл = 0;
		Расход.ПостояннаяРазница = 0;
		Расход.ВременнаяРазница = 0;
		Расход.Количество = 0;
	Иначе	
		// расчетная партия заполняется на наибольшее общее вычитаемое
		Если Расход.ТипЗаписи = "Материал" И Приход.Количество <= Расход.Знаменатель Тогда
			Приход.Знаменатель = ?(Приход.Знаменатель = 0, Приход.Количество, Приход.Знаменатель);
			КоличествоСписания = Приход.Знаменатель / Расход.Знаменатель * Расход.Количество;
			РасчетнаяПартия.Знаменатель = Приход.Знаменатель;
			Расход.Знаменатель = Расход.Знаменатель - Приход.Знаменатель;
		Иначе
			Приход.Знаменатель = 0;
			КоличествоСписания = Мин(Расход.Количество, Приход.Количество);
		КонецЕсли;
		РасходБазис = ?(Расход.Количество <> 0, КоличествоСписания / Расход.Количество, 0);
		ПриходБазис = ?(Приход.Количество <> 0, КоличествоСписания / Приход.Количество, 0);
		
		РасходСтоимостьРегл = Окр(РасходБазис * Расход.СтоимостьРегл, 2);
		РасходНДСРегл = Окр(РасходБазис * Расход.НДСРегл, 2);
		РасходПостояннаяРазница = Окр(РасходБазис * Расход.ПостояннаяРазница, 2);
		РасходВременнаяРазница = Окр(РасходБазис * Расход.ВременнаяРазница, 2);
		
		ПриходСтоимостьРегл = Окр(ПриходБазис * Приход.СтоимостьРегл, 2);
		ПриходНДСРегл = Окр(ПриходБазис * Приход.НДСРегл, 2);
		ПриходПостояннаяРазница = Окр(ПриходБазис * Приход.ПостояннаяРазница, 2);
		ПриходВременнаяРазница = Окр(ПриходБазис * Приход.ВременнаяРазница, 2);
		
		// заполняем показатели расчетной партии
		РасчетнаяПартия.Стоимость = Окр(ПриходБазис * Приход.Стоимость, 2);
		РасчетнаяПартия.СтоимостьБезНДС = Окр(ПриходБазис * Приход.СтоимостьБезНДС, 2);
		РасчетнаяПартия.Количество = Окр(КоличествоСписания, 3);
		
		Если РасчетнаяПартия.СохранятьРегл Тогда
			РасчетнаяПартия.СтоимостьРегл = РасходСтоимостьРегл;
			РасчетнаяПартия.НДСРегл = РасходНДСРегл;
			РасчетнаяПартия.ПостояннаяРазница = РасходПостояннаяРазница;
			РасчетнаяПартия.ВременнаяРазница = РасходВременнаяРазница;
		Иначе
			Если РасчетнаяПартия.ВидДвижения = ВидДвиженияНакопления.Приход
			  И Приход.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОпределяетсяРаспределением Тогда
				РасчетнаяПартия.СтоимостьРегл = ПриходСтоимостьРегл;
				РасчетнаяПартия.НДСРегл = 0;
			ИначеЕсли РасчетнаяПартия.ТипЗаписи = "Перемещение"
			 И ТипЗнч(РасчетнаяПартия.Регистратор) = Тип("ДокументСсылка.ПересортицаТоваров") Тогда
			 	Если Приход.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС
				 ИЛИ Приход.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД Тогда
					РасчетнаяПартия.СтоимостьРегл = ПриходСтоимостьРегл + ПриходНДСРегл;
				Иначе
					РасчетнаяПартия.СтоимостьРегл = ПриходСтоимостьРегл;
				КонецЕсли;
				РасчетнаяПартия.НДСРегл = 0;
		 	Иначе
			 	РасчетнаяПартия.СтоимостьРегл = ПриходСтоимостьРегл;
				РасчетнаяПартия.НДСРегл = ПриходНДСРегл;
			КонецЕсли;
			РасчетнаяПартия.ПостояннаяРазница = ПриходПостояннаяРазница;
			РасчетнаяПартия.ВременнаяРазница = ПриходВременнаяРазница;
		КонецЕсли;
		
		// корректируем базу расчета в потреблении
		Расход.Стоимость = Расход.Стоимость - Окр(РасходБазис * Расход.Стоимость, 2);
		Расход.СтоимостьБезНДС = Расход.СтоимостьБезНДС - Окр(РасходБазис * Расход.СтоимостьБезНДС, 2);
		Расход.СтоимостьРегл = Расход.СтоимостьРегл - РасходСтоимостьРегл;
		Расход.НДСРегл = Расход.НДСРегл - РасходНДСРегл;
		Расход.ПостояннаяРазница = Расход.ПостояннаяРазница - РасходПостояннаяРазница;
		Расход.ВременнаяРазница = Расход.ВременнаяРазница - РасходВременнаяРазница;
		Расход.Количество = Расход.Количество - РасчетнаяПартия.Количество;
		
		// корректируем базу расчета в приходе
		Если НЕ (Расход.ТипЗаписи = "Перемещение"
		 И (Расход.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиентуРеглУчет
		  ИЛИ Расход.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию)) Тогда
			Приход.Стоимость = Приход.Стоимость - РасчетнаяПартия.Стоимость;
			Приход.СтоимостьБезНДС = Приход.СтоимостьБезНДС - РасчетнаяПартия.СтоимостьБезНДС;
			Приход.СтоимостьРегл = Приход.СтоимостьРегл - ПриходСтоимостьРегл;
			Приход.НДСРегл = Приход.НДСРегл - ПриходНДСРегл;
			Приход.ПостояннаяРазница = Приход.ПостояннаяРазница - ПриходПостояннаяРазница;
			Приход.ВременнаяРазница = Приход.ВременнаяРазница - ПриходВременнаяРазница;
			Приход.Количество = Приход.Количество - РасчетнаяПартия.Количество;
		КонецЕсли;
	
		// Заполняем партионную идентификацию в расчетной партии
		Если Не ЗначениеЗаполнено(РасчетнаяПартия.ДокументПоступления)
		 ИЛИ Не ЗначениеЗаполнено(Расход.РазделУчета)
		 ИЛИ Расход.ТипЗаписи = "Потребление" Тогда
			РасчетнаяПартия.ДокументПоступления = Приход.ДокументПоступления;
			Если (РасчетнаяПартия.ТипЗаписи <> "Сторно") Тогда
				РасчетнаяПартия.ПериодПоступления = Приход.ПериодПоступления;
			КонецЕсли;
		КонецЕсли;
		Если Приход.ТипЗаписи = "Возврат"
		 ИЛИ Приход.РазделУчета = Перечисления.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию Тогда
			РасчетнаяПартия.ДокументИсточник = Приход.ДокументИсточник;
		КонецЕсли;
		Если Приход.РазделУчета = Перечисления.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты
		 И Расход.РазделУчета = Перечисления.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты Тогда
			РасчетнаяПартия.ДокументИсточник = Приход.ДокументИсточник;
		КонецЕсли;
		Если РасчетнаяПартия.ТипЗаписи = "Сторно"
		 И (Приход.ТипЗаписи = "Потребление" ИЛИ Приход.ТипЗаписи = "ВозвратКомиссия" ИЛИ Приход.ТипЗаписи = "Перемещение")
		 И (Приход.РазделУчета = Перечисления.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты
			ИЛИ Приход.РазделУчета = Перечисления.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию) Тогда
			РасчетнаяПартия.ДокументИсточник = Приход.ДокументИсточник;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(РасчетнаяПартия.ДокументИсточник) Тогда
			РасчетнаяПартия.ДокументИсточник = Приход.Регистратор;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(РасчетнаяПартия.ВидЗапасов) Тогда
			РасчетнаяПартия.ВидЗапасов = Приход.ВидЗапасов;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(РасчетнаяПартия.КорВидЗапасов) И РасчетнаяПартия.ТипЗаписи <> "Перемещение" Тогда
			РасчетнаяПартия.КорВидЗапасов = Приход.ВидЗапасов;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(РасчетнаяПартия.АналитикаУчетаПартий) Тогда
			Если (РасчетнаяПартия.ВидДвижения = ВидДвиженияНакопления.Приход ИЛИ РасчетнаяПартия.ТипЗаписи = "Сторно")
			 И ТипЗнч(РасчетнаяПартия.Регистратор) <> Тип("ДокументСсылка.КорректировкаРеализации")
			Тогда
				РасчетнаяПартия.АналитикаУчетаПартий =
					АналитикаУчетаПартий(Приход.АналитикаУчетаПартий, Приход.НалогообложениеПартии, Приход.НалогообложениеНДС);
				Если ЗначениеЗаполнено(Приход.НалогообложениеНДС) Тогда
					РасчетнаяПартия.НалогообложениеПартии = Приход.НалогообложениеНДС;
				Иначе
					РасчетнаяПартия.НалогообложениеПартии = Приход.НалогообложениеПартии;
				КонецЕсли;
			Иначе
				РасчетнаяПартия.АналитикаУчетаПартий = Приход.АналитикаУчетаПартий;
				РасчетнаяПартия.НалогообложениеПартии = Приход.НалогообложениеПартии;
			КонецЕсли;
		КонецЕсли;
		
		ЗаполнитьСтатьюИАналитикуСписанияНДС(РасчетнаяПартия, Расход, Приход);
		
	КонецЕсли;
	// По результатам партионной идентификации определяем завершенность расчета
	РасчетнаяПартия.РасчетЗавершен = Приход.РасчетЗавершен;
КонецПроцедуры

Процедура ЗаполнитьПартиюПереданную(РасчетнаяПартия, Расход, Приход) // ПартииТоваровПереданныеНаКомиссию
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Расход);
	
	Если Расход.ТипЗаписи = "Возврат" Тогда
		Приход.Количество = Приход.Количество - Расход.Количество;
		Приход.Стоимость = Приход.Стоимость - Расход.Стоимость;
		Приход.СтоимостьБезНДС = Приход.СтоимостьБезНДС - Расход.СтоимостьБезНДС;
		Приход.СтоимостьРегл = Приход.СтоимостьРегл - Расход.СтоимостьРегл;
		Приход.НДСРегл = Приход.НДСРегл - Расход.НДСРегл;
		Приход.ПостояннаяРазница = Приход.ПостояннаяРазница - Расход.ПостояннаяРазница;
		Приход.ВременнаяРазница = Приход.ВременнаяРазница - Расход.ВременнаяРазница;
		
		Расход.Количество = 0;
		Расход.Стоимость = 0;
		Расход.СтоимостьБезНДС = 0;
		Расход.СтоимостьРегл = 0;
		Расход.НДСРегл = 0;
		Расход.ПостояннаяРазница = 0;
		Расход.ВременнаяРазница = 0;
	Иначе	
		Если Расход.Количество = 0 ИЛИ Приход.Количество = 0 Тогда
			Возврат;
		КонецЕсли;
	
		// расчетная партия заполняется на наибольшее общее вычитаемое
		КоличествоСписания = Мин(Расход.Количество, Приход.Количество);
		
		ПотреблениеСтоимость = Окр(КоличествоСписания * Расход.Стоимость / Расход.Количество, 2);
		ПотреблениеСтоимостьБезНДС = Окр(КоличествоСписания * Расход.СтоимостьБезНДС / Расход.Количество, 2);
		ПотреблениеСтоимостьРегл = Окр(КоличествоСписания * Расход.СтоимостьРегл / Расход.Количество, 2);
		ПотреблениеНДСРегл = Окр(КоличествоСписания * Расход.НДСРегл / Расход.Количество, 2);
		ПотреблениеПостояннаяРазница = Окр(КоличествоСписания * Расход.ПостояннаяРазница / Расход.Количество, 2);
		ПотреблениеВременнаяРазница = Окр(КоличествоСписания * Расход.ВременнаяРазница / Расход.Количество, 2);

		ПартияСтоимость = Окр(КоличествоСписания * Приход.Стоимость / Приход.Количество, 2);
		ПартияСтоимостьБезНДС = Окр(КоличествоСписания * Приход.СтоимостьБезНДС / Приход.Количество, 2);
		ПартияСтоимостьРегл = Окр(КоличествоСписания * Приход.СтоимостьРегл / Приход.Количество, 2);
		ПартияНДСРегл = Окр(КоличествоСписания * Приход.НДСРегл / Приход.Количество, 2);
		ПартияПостояннаяРазница = Окр(КоличествоСписания * Приход.ПостояннаяРазница / Приход.Количество, 2);
		ПартияВременнаяРазница = Окр(КоличествоСписания * Приход.ВременнаяРазница / Приход.Количество, 2);

		// заполняем показатели расчетной партии
		РасчетнаяПартия.Количество = КоличествоСписания;
		РасчетнаяПартия.Стоимость = ПартияСтоимость;
		РасчетнаяПартия.СтоимостьБезНДС = ПартияСтоимостьБезНДС;
		РасчетнаяПартия.СтоимостьРегл = ПартияСтоимостьРегл;
		РасчетнаяПартия.НДСРегл = ПартияНДСРегл;
		РасчетнаяПартия.ПостояннаяРазница = ПартияПостояннаяРазница;
		РасчетнаяПартия.ВременнаяРазница = ПартияВременнаяРазница;
		
		// корректируем базу расчета в расходе
		Если Расход <> РасчетнаяПартия Тогда
			Расход.Количество = Расход.Количество - КоличествоСписания;
			Расход.Стоимость = Расход.Стоимость - ПотреблениеСтоимость;
			Расход.СтоимостьБезНДС = Расход.СтоимостьБезНДС - ПотреблениеСтоимостьБезНДС;
			Расход.СтоимостьРегл = Расход.СтоимостьРегл - ПотреблениеСтоимостьРегл;
			Расход.НДСРегл = Расход.НДСРегл - ПотреблениеНДСРегл;
			Расход.ПостояннаяРазница = Расход.ПостояннаяРазница - ПотреблениеПостояннаяРазница;
			Расход.ВременнаяРазница = Расход.ВременнаяРазница - ПотреблениеВременнаяРазница;
		КонецЕсли;
		
		// корректируем базу расчета в партии
		Приход.Количество = Приход.Количество - КоличествоСписания;
		Приход.Стоимость = Приход.Стоимость - ПартияСтоимость;
		Приход.СтоимостьБезНДС = Приход.СтоимостьБезНДС - ПартияСтоимостьБезНДС;
		Приход.СтоимостьРегл = Приход.СтоимостьРегл - ПартияСтоимостьРегл;
		Приход.НДСРегл = Приход.НДСРегл - ПартияНДСРегл;
		Приход.ПостояннаяРазница = Приход.ПостояннаяРазница - ПартияПостояннаяРазница;
		Приход.ВременнаяРазница = Приход.ВременнаяРазница - ПартияВременнаяРазница;
		
		// Заполняем партионную идентификацию в расчетной партии
		РасчетнаяПартия.ДокументПередачиНаКомиссию = Приход.ДокументПередачиНаКомиссию;
		РасчетнаяПартия.АналитикаУчетаПартий = Приход.АналитикаУчетаПартий;
		РасчетнаяПартия.ДокументПоступления = Приход.ДокументПоступления;
	КонецЕсли;
	
	// По результатам партионной идентификации определяем завершенность расчета
	РасчетнаяПартия.РасчетЗавершен = Приход.РасчетЗавершен И
		ЗначениеЗаполнено(РасчетнаяПартия.АналитикаУчетаПартий) И ЗначениеЗаполнено(РасчетнаяПартия.ДокументПоступления);
	
КонецПроцедуры

Процедура ЗаполнитьПартиюЗатрат(РасчетнаяПартия, Расход, Приход) // ПартииЗатратНаВыпуск
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Расход);
	
	
	// расчетная партия заполняется на наибольшее общее вычитаемое
	Если Расход.ТипЗаписи = "Выпуск" ИЛИ Расход.ТипЗаписи = "Сборка" ИЛИ Расход.ТипЗаписи = "ВыпускПрочие" Тогда
		ЗнаменательСписания = Мин(Расход.Количество, Приход.Знаменатель);
		РасчетнаяПартия.Знаменатель = Расход.Знаменатель;
	Иначе
		ЗнаменательСписания = Мин(Расход.Знаменатель, Приход.Знаменатель);
		Если Расход.ТипЗаписи = "Перемещение"
		 И Расход.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеТоваров Тогда
			РасчетнаяПартия.Знаменатель = 0;
		ИначеЕсли Расход.ТипЗаписи <> "Перемещение" И Расход.ТипЗаписи <> "Замещение" Тогда
			РасчетнаяПартия.Знаменатель = ЗнаменательСписания;
		КонецЕсли;
	КонецЕсли;
	
	// заполняем показатели расчетной партии
	РасчетнаяПартия.Количество = Окр(ЗнаменательСписания * Приход.Количество / Приход.Знаменатель, 3);
	Если ЗначениеЗаполнено(Приход.АналитикаУчетаНоменклатуры) И РасчетнаяПартия.Количество = 0 Тогда
		РасчетнаяПартия.Стоимость = 0;
		РасчетнаяПартия.СтоимостьБезНДС = 0;
		ПриходСтоимостьРегл = 0;
		ПриходНДСРегл = 0;
		ПриходПостояннаяРазница = 0;
		ПриходВременнаяРазница = 0;
	ИначеЕсли ЗначениеЗаполнено(Приход.АналитикаУчетаНоменклатуры) И РасчетнаяПартия.Количество = Приход.Количество Тогда
		РасчетнаяПартия.Стоимость = Приход.Стоимость;
		РасчетнаяПартия.СтоимостьБезНДС = Приход.СтоимостьБезНДС;
		
		ПриходСтоимостьРегл = Приход.СтоимостьРегл;
		ПриходНДСРегл = Приход.НДСРегл;
		ПриходПостояннаяРазница = Приход.ПостояннаяРазница;
		ПриходВременнаяРазница = Приход.ВременнаяРазница;
	Иначе
		РасчетнаяПартия.Стоимость = Окр(ЗнаменательСписания * Приход.Стоимость / Приход.Знаменатель, 2);
		РасчетнаяПартия.СтоимостьБезНДС = Окр(ЗнаменательСписания * Приход.СтоимостьБезНДС / Приход.Знаменатель, 2);
		
		ПриходСтоимостьРегл = Окр(ЗнаменательСписания * Приход.СтоимостьРегл / Приход.Знаменатель, 2);
		ПриходНДСРегл = Окр(ЗнаменательСписания * Приход.НДСРегл / Приход.Знаменатель, 2);
		ПриходПостояннаяРазница = Окр(ЗнаменательСписания * Приход.ПостояннаяРазница / Приход.Знаменатель, 2);
		ПриходВременнаяРазница = Окр(ЗнаменательСписания * Приход.ВременнаяРазница / Приход.Знаменатель, 2);
	КонецЕсли;
	
	Если РасчетнаяПартия.СохранятьРегл Тогда
		РасчетнаяПартия.СтоимостьРегл = 0;
		РасчетнаяПартия.НДСРегл = 0;
		РасчетнаяПартия.ПостояннаяРазница = 0;
		РасчетнаяПартия.ВременнаяРазница = 0;
	Иначе
		Если РасчетнаяПартия.ВидДвижения = ВидДвиженияНакопления.Приход
		 И Приход.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОпределяетсяРаспределением Тогда
			РасчетнаяПартия.НДСРегл = 0;
	 	Иначе
			РасчетнаяПартия.НДСРегл = ПриходНДСРегл;
		КонецЕсли;
		РасчетнаяПартия.СтоимостьРегл = ПриходСтоимостьРегл;
		РасчетнаяПартия.ПостояннаяРазница = ПриходПостояннаяРазница;
		РасчетнаяПартия.ВременнаяРазница = ПриходВременнаяРазница;
	КонецЕсли;
	
	// корректируем базу расчета в приходе
	Приход.Знаменатель = Приход.Знаменатель - ЗнаменательСписания;
	Приход.Количество = Приход.Количество - РасчетнаяПартия.Количество;
	Приход.Стоимость = Приход.Стоимость - РасчетнаяПартия.Стоимость;
	Приход.СтоимостьБезНДС = Приход.СтоимостьБезНДС - РасчетнаяПартия.СтоимостьБезНДС;
	Приход.СтоимостьРегл = Приход.СтоимостьРегл - ПриходСтоимостьРегл;
	Приход.НДСРегл = Приход.НДСРегл - ПриходНДСРегл;
	Приход.ПостояннаяРазница = Приход.ПостояннаяРазница - ПриходПостояннаяРазница;
	Приход.ВременнаяРазница = Приход.ВременнаяРазница - ПриходВременнаяРазница;
	
	Если Не ЗначениеЗаполнено(Приход.АналитикаУчетаНоменклатуры) Тогда
		Приход.Количество = 0;
		РасчетнаяПартия.Количество = 0;
	КонецЕсли;
	
	Если Приход.Количество = 0
	 И Приход.Стоимость = 0
	 И Приход.СтоимостьБезНДС = 0
	 И Приход.СтоимостьРегл = 0
	 И Приход.НДСРегл = 0
	 И Приход.ПостояннаяРазница = 0
	 И Приход.ВременнаяРазница = 0 Тогда
		Приход.Знаменатель = 0;
	КонецЕсли;
	
	// Заполняем партионную идентификацию в расчетной партии
	Если РасчетнаяПартия.ВидДвижения = ВидДвиженияНакопления.Приход ИЛИ РасчетнаяПартия.ТипЗаписи = "Сторно" Тогда
		РасчетнаяПартия.АналитикаУчетаПартий =
			АналитикаУчетаПартий(Приход.АналитикаУчетаПартий, Приход.НалогообложениеПартии, Приход.НалогообложениеНДС);
		Если ЗначениеЗаполнено(Приход.НалогообложениеНДС) Тогда
			РасчетнаяПартия.НалогообложениеПартии = Приход.НалогообложениеНДС;
		Иначе
			РасчетнаяПартия.НалогообложениеПартии = Приход.НалогообложениеПартии;
		КонецЕсли;
	Иначе
		РасчетнаяПартия.АналитикаУчетаПартий = Приход.АналитикаУчетаПартий;
		РасчетнаяПартия.НалогообложениеПартии = Приход.НалогообложениеПартии;
	КонецЕсли;
	РасчетнаяПартия.АналитикаУчетаНоменклатуры = Приход.АналитикаУчетаНоменклатуры;
	РасчетнаяПартия.ВидЗапасов = Приход.ВидЗапасов;
	РасчетнаяПартия.ДокументПоступления = Приход.ДокументПоступления;
	Если Приход.ТипЗаписи = "Затраты" Тогда
		РасчетнаяПартия.АналитикаУчетаПродукции = Приход.АналитикаУчетаПродукции;
	ИначеЕсли Приход.ТипЗаписи = "Распределение" Тогда
		РасчетнаяПартия.СтатьяРасходовСписания = Приход.СтатьяРасходовСписания;
		РасчетнаяПартия.АналитикаРасходов = Приход.АналитикаРасходов;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.ПодразделениеРасходов) Тогда 
		РасчетнаяПартия.ПодразделениеРасходов = Приход.ПодразделениеРасходов;
	КонецЕсли;
	Если Расход.ТипЗаписи = "Продукция" Тогда
		РасчетнаяПартия.ДокументИсточник = Неопределено;
	КонецЕсли;
	Если РасчетнаяПартия.Количество = 0
	 И РасчетнаяПартия.Стоимость = 0
	 И РасчетнаяПартия.СтоимостьБезНДС = 0
	 И РасчетнаяПартия.СтоимостьРегл = 0
	 И РасчетнаяПартия.НДСРегл = 0
	 И РасчетнаяПартия.ПостояннаяРазница = 0
	 И РасчетнаяПартия.ВременнаяРазница = 0 Тогда
		РасчетнаяПартия.Знаменатель = 0;
		РасчетнаяПартия.РасчетЗавершен = Ложь;
	Иначе
		РасчетнаяПартия.РасчетЗавершен = Приход.РасчетЗавершен;
	КонецЕсли;
	
	ЗаполнитьСтатьюИАналитикуСписанияНДС(РасчетнаяПартия, Расход, Приход);
	
КонецПроцедуры

Процедура ЗаполнитьЗаказВПриходе(РасчетнаяПартия, Расход, Приход) // ЗаказыПоставщикам
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Расход);
	РасчетнаяПартия.РасчетЗавершен = Истина;
	Если Неопределено = Приход Тогда
		Возврат;
	КонецЕсли;
	
	// расчетная партия заполняется на наибольшее общее вычитаемое
	КоличествоСписания = ?(Расход.Количество >= Приход.Количество, Приход.Количество, Расход.Количество);
	// заполняем показатели расчетной партии
	РасчетнаяПартия.Стоимость = Окр(КоличествоСписания * Расход.Стоимость / Расход.Количество, 2);
	РасчетнаяПартия.СтоимостьБезНДС = Окр(КоличествоСписания * Расход.СтоимостьБезНДС / Расход.Количество, 2);
	РасчетнаяПартия.СтоимостьРегл = Окр(КоличествоСписания * Расход.СтоимостьРегл / Расход.Количество, 2);
	РасчетнаяПартия.НДСРегл = Окр(КоличествоСписания * Расход.НДСРегл / Расход.Количество, 2);
	РасчетнаяПартия.ПостояннаяРазница = Окр(КоличествоСписания * Расход.ПостояннаяРазница / Расход.Количество, 2);
	РасчетнаяПартия.ВременнаяРазница = Окр(КоличествоСписания * Расход.ВременнаяРазница / Расход.Количество, 2);
	РасчетнаяПартия.Количество = КоличествоСписания;
	// корректируем базу расчета в расходе
	Если Расход <> РасчетнаяПартия Тогда
		Расход.Стоимость = Расход.Стоимость - РасчетнаяПартия.Стоимость;
		Расход.СтоимостьБезНДС = Расход.СтоимостьБезНДС - РасчетнаяПартия.СтоимостьБезНДС;
		Расход.СтоимостьРегл = Расход.СтоимостьРегл - РасчетнаяПартия.СтоимостьРегл;
		Расход.НДСРегл = Расход.НДСРегл - РасчетнаяПартия.НДСРегл;
		Расход.ПостояннаяРазница = Расход.ПостояннаяРазница - РасчетнаяПартия.ПостояннаяРазница;
		Расход.ВременнаяРазница = Расход.ВременнаяРазница - РасчетнаяПартия.ВременнаяРазница;
		Расход.Количество = Расход.Количество - КоличествоСписания;
	КонецЕсли;
	// корректируем базу расчета в приходе
	Приход.Стоимость = Приход.Стоимость - Окр(КоличествоСписания * Приход.Стоимость / Приход.Количество, 2);
	Приход.СтоимостьБезНДС = Приход.СтоимостьБезНДС - Окр(КоличествоСписания * Приход.СтоимостьБезНДС / Приход.Количество, 2);
	Приход.СтоимостьРегл = Приход.СтоимостьРегл - Окр(КоличествоСписания * Приход.СтоимостьРегл / Приход.Количество, 2);
	Приход.НДСРегл = Приход.НДСРегл - Окр(КоличествоСписания * Приход.НДСРегл / Приход.Количество, 2);
	Приход.ПостояннаяРазница = Приход.ПостояннаяРазница - Окр(КоличествоСписания * Приход.ПостояннаяРазница / Приход.Количество, 2);
	Приход.ВременнаяРазница = Приход.ВременнаяРазница - Окр(КоличествоСписания * Приход.ВременнаяРазница / Приход.Количество, 2);
	Приход.Количество = Приход.Количество - КоличествоСписания;
	
	// Заполняем партионную идентификацию в расчетной партии
	РасчетнаяПартия.Заказ = Приход.Заказ;
КонецПроцедуры

Функция АналитикиОбсчетаПартийПрочих(Движение, ПустыеСсылки)
	Результат = Новый Массив;
	Результат.Добавить(Движение.Номенклатура);
	Если Движение.Номенклатура <> Неопределено Тогда
		Результат.Добавить(ПустаяСсылка(Движение.Номенклатура, ПустыеСсылки));
	КонецЕсли;
	Результат.Добавить(Движение.Склад);
	Если Движение.Склад <> Неопределено Тогда
		Результат.Добавить(ПустаяСсылка(Движение.Склад, ПустыеСсылки));
	КонецЕсли;
	Результат.Добавить(Движение.Регистратор);
	Результат.Добавить(ПустаяСсылка(Движение.Регистратор, ПустыеСсылки));
	Возврат Результат;
КонецФункции

Процедура ЗаполнитьПартиюПрочих(РасчетнаяПартия, Расход, Приход) // ПартииПрочихРасходов
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Расход);
	Если Неопределено = Приход Тогда
		Возврат;
	КонецЕсли;
	
	РасчетнаяПартия.ТипЗаписи = "Потребление";
	РасчетнаяПартия.ВидДвижения = ВидДвиженияНакопления.Расход;
	РасчетнаяПартия.Период = Макс(Расход.Период, Расход.РасчетныйПериод);
	
	Если Расход.Базис = 0 Тогда
		Возврат;
	КонецЕсли;
	// расчетная партия заполняется на наибольшее общее вычитаемое
	БазисСписания = ?(Расход.Базис >= Приход.Значение.Базис, Приход.Значение.Базис, Расход.Базис);
	// заполняем показатели расчетной партии
	РасчетнаяПартия.Стоимость = Окр(БазисСписания * Расход.Стоимость / Расход.Базис, 2);
	РасчетнаяПартия.СтоимостьБезНДС = Окр(БазисСписания * Расход.СтоимостьБезНДС / Расход.Базис, 2);
	РасчетнаяПартия.СтоимостьРегл = Окр(БазисСписания * Расход.СтоимостьРегл / Расход.Базис, 2);
	РасчетнаяПартия.НДСРегл = Окр(БазисСписания * Расход.НДСРегл / Расход.Базис, 2);
	РасчетнаяПартия.ПостояннаяРазница = Окр(БазисСписания * Расход.ПостояннаяРазница / Расход.Базис, 2);
	РасчетнаяПартия.ВременнаяРазница = Окр(БазисСписания * Расход.ВременнаяРазница / Расход.Базис, 2);
	РасчетнаяПартия.Базис = БазисСписания;
	// корректируем базу расчета в расходе
	Расход.Стоимость = Расход.Стоимость - РасчетнаяПартия.Стоимость;
	Расход.СтоимостьБезНДС = Расход.СтоимостьБезНДС - РасчетнаяПартия.СтоимостьБезНДС;
	Расход.СтоимостьРегл = Расход.СтоимостьРегл - РасчетнаяПартия.СтоимостьРегл;
	Расход.НДСРегл = Расход.НДСРегл - РасчетнаяПартия.НДСРегл;
	Расход.ПостояннаяРазница = Расход.ПостояннаяРазница - РасчетнаяПартия.ПостояннаяРазница;
	Расход.ВременнаяРазница = Расход.ВременнаяРазница - РасчетнаяПартия.ВременнаяРазница;
	Расход.Базис = Расход.Базис - БазисСписания;
	// корректируем базу расчета в приходе
	Приход.Значение.Базис = Приход.Значение.Базис - БазисСписания;
	
	// Заполняем партионную идентификацию в расчетной партии
	Если ТипЗнч(Приход.Ключ) = Тип("ПеречислениеСсылка.ТипыНалогообложенияНДС") Тогда
		РасчетнаяПартия.НалогообложениеНДС = Приход.Ключ;
	Иначе
		РасчетнаяПартия.СтатьяОтраженияРасходов = Приход.Ключ;
	КонецЕсли;
	РасчетнаяПартия.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РаспределениеРасходовНаСебестоимость;
КонецПроцедуры

Процедура ЗаполнитьПриходПартииРасходов(РасчетнаяПартия, Расход, Приход) // ПартииРасходовНаСебестоимостьТоваровПриход
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Расход);
	Если Неопределено = Приход Тогда
		Возврат;
	КонецЕсли;
	
	// расчетная партия заполняется на наибольшее общее вычитаемое
	Если Приход.ПравилоРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.КоличествоТоваров Тогда
		Расход.Базис = Расход.Количество;
	ИначеЕсли Приход.ПравилоРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.ВесТоваров Тогда
		Расход.Базис = Расход.Вес;
	ИначеЕсли Приход.ПравилоРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.ОбъемТоваров Тогда
		Расход.Базис = Расход.Объем;
	Иначе
		Расход.Базис = Расход.Стоимость;
	КонецЕсли;
	БазисСписания = ?(Расход.Базис >= Приход.Базис, Приход.Базис, Расход.Базис);
	// заполняем показатели расчетной партии
	РасчетнаяПартия.Количество = Окр(БазисСписания * Расход.Количество / Расход.Базис, 3);
	РасчетнаяПартия.Вес = Окр(БазисСписания * Расход.Вес / Расход.Базис, 7);
	РасчетнаяПартия.Объем = Окр(БазисСписания * Расход.Объем / Расход.Базис, 7);
	РасчетнаяПартия.Стоимость = Окр(БазисСписания * Приход.Стоимость / Приход.Базис, 2);
	РасчетнаяПартия.СтоимостьБезНДС = Окр(БазисСписания * Приход.СтоимостьБезНДС / Приход.Базис, 2);
	РасчетнаяПартия.СтоимостьРегл = Окр(БазисСписания * Приход.СтоимостьРегл / Приход.Базис, 2);
	РасчетнаяПартия.НДСРегл = Окр(БазисСписания * Приход.НДСРегл / Приход.Базис, 2);
	РасчетнаяПартия.ПостояннаяРазница = Окр(БазисСписания * Приход.ПостояннаяРазница / Приход.Базис, 2);
	РасчетнаяПартия.ВременнаяРазница = Окр(БазисСписания * Приход.ВременнаяРазница / Приход.Базис, 2);
	РасчетнаяПартия.Базис = БазисСписания;
	// корректируем базу расчета в приходе
	Приход.Стоимость = Приход.Стоимость - РасчетнаяПартия.Стоимость;
	Приход.СтоимостьБезНДС = Приход.СтоимостьБезНДС - РасчетнаяПартия.СтоимостьБезНДС;
	Приход.СтоимостьРегл = Приход.СтоимостьРегл - РасчетнаяПартия.СтоимостьРегл;
	Приход.НДСРегл = Приход.НДСРегл - РасчетнаяПартия.НДСРегл;
	Приход.ПостояннаяРазница = Приход.ПостояннаяРазница - РасчетнаяПартия.ПостояннаяРазница;
	Приход.ВременнаяРазница = Приход.ВременнаяРазница - РасчетнаяПартия.ВременнаяРазница;
	Приход.Базис = Приход.Базис - БазисСписания;
	// корректируем базу расчета в расходе
	Расход.Количество = Расход.Количество - РасчетнаяПартия.Количество;
	Расход.Вес = Расход.Вес - РасчетнаяПартия.Вес;
	Расход.Объем = Расход.Объем - РасчетнаяПартия.Объем;
	Расход.Стоимость = Расход.Стоимость - Окр(БазисСписания * Расход.Стоимость / Расход.Базис, 2);
	Расход.СтоимостьБезНДС = Расход.СтоимостьБезНДС - Окр(БазисСписания * Расход.СтоимостьБезНДС / Расход.Базис, 2);
	Расход.СтоимостьРегл = Расход.СтоимостьРегл - Окр(БазисСписания * Расход.СтоимостьРегл / Расход.Базис, 2);
	Расход.НДСРегл = Расход.НДСРегл - Окр(БазисСписания * Расход.НДСРегл / Расход.Базис, 2);
	Расход.ПостояннаяРазница = Расход.ПостояннаяРазница - Окр(БазисСписания * Расход.ПостояннаяРазница / Расход.Базис, 2);
	Расход.ВременнаяРазница = Расход.ВременнаяРазница - Окр(БазисСписания * Расход.ВременнаяРазница / Расход.Базис, 2);
	Расход.Базис = Расход.Базис - БазисСписания;
	
	// Заполняем партионную идентификацию в расчетной партии
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Приход,
		"ВидДвижения, Период, Регистратор, ДокументПоступленияРасходов, СтатьяРасходов, ХозяйственнаяОперация,
		|Подразделение, ПравилоРаспределения, НаправлениеДеятельности");
		
	Если ЗначениеЗаполнено(Приход.АналитикаРасходов) Тогда
		РасчетнаяПартия.АналитикаРасходов = Приход.АналитикаРасходов;
	КонецЕсли;
	Если РасчетнаяПартия.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
		РасчетнаяПартия.АналитикаУчетаПартий =
			АналитикаУчетаПартий(Приход.АналитикаУчетаПартий, Приход.НалогообложениеПартии, Приход.НалогообложениеНДС);
		РасчетнаяПартия.НалогообложениеПартии = Приход.НалогообложениеНДС;
	Иначе
		РасчетнаяПартия.АналитикаУчетаПартий = Приход.АналитикаУчетаПартий;
		РасчетнаяПартия.НалогообложениеПартии = Приход.НалогообложениеПартии;
	КонецЕсли;
	Если ЗначениеЗаполнено(РасчетнаяПартия.ВариантРаздельногоУчетаНДС) Тогда
		РасчетнаяПартия.ТипЗаписи = "Услуги";
		РасчетнаяПартия.НалогообложениеНДС = Приход.НалогообложениеПартии;
	Иначе
		РасчетнаяПартия.ТипЗаписи = "Партия";
		РасчетнаяПартия.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПустаяСсылка();
	КонецЕсли;
	РасчетнаяПартия.РасчетЗавершен = Истина;
КонецПроцедуры

Процедура ЗаполнитьПартиюРасходов(РасчетнаяПартия, Расход, Приход) // ПартииРасходовНаСебестоимостьТоваров
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Расход);
	
	
	// расчетная партия заполняется на наибольшее общее вычитаемое
	ВключаяКоличество = (Расход.ТипЗаписи <> "Перемещение" И Расход.ТипЗаписи <> "Замещение" И Расход.ТипЗаписи <> "Выпуск" И Расход.ТипЗаписи <> "Затраты");
	СписыватьЗнаменатель = (Приход.Знаменатель = Приход.Количество) И (Приход.Знаменатель <> 0);
	ПриходКоличество = Приход.Количество;
	Если Приход.ТипЗаписи = "Сторно" Тогда
		КоличествоСписания = Мин(Расход.Количество, Приход.Количество);
	ИначеЕсли Приход.ТипЗаписи = "ВыпускПрочие" Тогда
		КоличествоСписания = Мин(Расход.Количество, Приход.Знаменатель);
		ПриходКоличество = Приход.Знаменатель;
		СписыватьЗнаменатель = Истина;
	ИначеЕсли ВключаяКоличество Тогда
		КоличествоСписания = Мин(Расход.Количество, Приход.Количество);
	Иначе
		КоличествоСписания = Приход.Количество;
	КонецЕсли;
	
	Если ПриходКоличество <> 0 Тогда
		ПриходСтоимостьРегл = Окр(КоличествоСписания * Приход.СтоимостьРегл / ПриходКоличество, 2);
		ПриходНДСРегл = Окр(КоличествоСписания * Приход.НДСРегл / ПриходКоличество, 2);
		ПриходПостояннаяРазница = Окр(КоличествоСписания * Приход.ПостояннаяРазница / ПриходКоличество, 2);
		ПриходВременнаяРазница = Окр(КоличествоСписания * Приход.ВременнаяРазница / ПриходКоличество, 2);
	
		// заполняем показатели расчетной партии
		РасчетнаяПартия.Стоимость = Окр(КоличествоСписания * Приход.Стоимость / ПриходКоличество, 2);
		РасчетнаяПартия.СтоимостьБезНДС = Окр(КоличествоСписания * Приход.СтоимостьБезНДС / ПриходКоличество, 2);
	Иначе
		ПриходСтоимостьРегл = 0;
		ПриходНДСРегл = 0;
		ПриходПостояннаяРазница = 0;
		ПриходВременнаяРазница = 0;
		РасчетнаяПартия.Стоимость = 0;
		РасчетнаяПартия.СтоимостьБезНДС = 0;
	КонецЕсли;
	Если РасчетнаяПартия.СохранятьРегл Тогда
		РасчетнаяПартия.СтоимостьРегл = 0;
		РасчетнаяПартия.НДСРегл = 0;
		РасчетнаяПартия.ПостояннаяРазница = 0;
		РасчетнаяПартия.ВременнаяРазница = 0;
	Иначе
		Если РасчетнаяПартия.ВидДвижения = ВидДвиженияНакопления.Приход
		  И Приход.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОпределяетсяРаспределением Тогда
			РасчетнаяПартия.СтоимостьРегл = ПриходСтоимостьРегл;
			РасчетнаяПартия.НДСРегл = 0;
		ИначеЕсли РасчетнаяПартия.ТипЗаписи = "Замещение"
	     И ТипЗнч(РасчетнаяПартия.Регистратор) = Тип("ДокументСсылка.ПересортицаТоваров") Тогда
			Если Приход.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС
			 ИЛИ Приход.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД Тогда
				РасчетнаяПартия.СтоимостьРегл = ПриходСтоимостьРегл + ПриходНДСРегл;
			Иначе
				РасчетнаяПартия.СтоимостьРегл = ПриходСтоимостьРегл;
			КонецЕсли;
			РасчетнаяПартия.НДСРегл = 0;
	 	Иначе
		 	РасчетнаяПартия.СтоимостьРегл = ПриходСтоимостьРегл;
			РасчетнаяПартия.НДСРегл = ПриходНДСРегл;
		КонецЕсли;
		РасчетнаяПартия.ПостояннаяРазница = ПриходПостояннаяРазница;
		РасчетнаяПартия.ВременнаяРазница = ПриходВременнаяРазница;
	КонецЕсли;
	Если ВключаяКоличество Тогда
		РасчетнаяПартия.Количество = КоличествоСписания;
		Если Расход.Количество <> 0 И РасчетнаяПартия.ТипЗаписи <> "ВыпускПрочие" Тогда
			РасчетнаяПартия.Знаменатель = РасчетнаяПартия.Знаменатель * РасчетнаяПартия.Количество / Расход.Количество;
		КонецЕсли;
	КонецЕсли;
	
	// корректируем базу расчета в приходе
	Приход.Стоимость = Приход.Стоимость - РасчетнаяПартия.Стоимость;
	Приход.СтоимостьБезНДС = Приход.СтоимостьБезНДС - РасчетнаяПартия.СтоимостьБезНДС;
	Приход.СтоимостьРегл = Приход.СтоимостьРегл - ПриходСтоимостьРегл;
	Приход.НДСРегл = Приход.НДСРегл - ПриходНДСРегл;
	Приход.ПостояннаяРазница = Приход.ПостояннаяРазница - ПриходПостояннаяРазница;
	Приход.ВременнаяРазница = Приход.ВременнаяРазница - ПриходВременнаяРазница;
	
	Если Приход.ТипЗаписи <> "ВыпускПрочие" Тогда
		Приход.Количество = Приход.Количество - КоличествоСписания;
	КонецЕсли;
	
	Если СписыватьЗнаменатель Тогда
		Приход.Знаменатель = Приход.Знаменатель - КоличествоСписания;
	КонецЕсли;
	
	Если РасчетнаяПартия.ТипЗаписи = "Затраты" Тогда
		Если РасчетнаяПартия.ВариантРаспределенияРасходовУпр <> Перечисления.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты Тогда
			РасчетнаяПартия.Стоимость = 0;
			РасчетнаяПартия.СтоимостьБезНДС = 0;
		КонецЕсли;
		Если РасчетнаяПартия.ВариантРаспределенияРасходовРегл <> Перечисления.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты Тогда
			РасчетнаяПартия.СтоимостьРегл = 0;
			РасчетнаяПартия.НДСРегл = 0;
			РасчетнаяПартия.ПостояннаяРазница = 0;
			РасчетнаяПартия.ВременнаяРазница = 0;
		КонецЕсли;
	КонецЕсли;
	
	Если Приход.Стоимость = 0
	 И Приход.СтоимостьБезНДС = 0
	 И Приход.СтоимостьРегл = 0
	 И Приход.НДСРегл = 0
	 И Приход.ПостояннаяРазница = 0
	 И Приход.ВременнаяРазница = 0 Тогда
	 	Приход.Количество = 0;
		Приход.Знаменатель = 0;
	КонецЕсли;
	
	Если РасчетнаяПартия.ВидДвижения = ВидДвиженияНакопления.Приход ИЛИ РасчетнаяПартия.ТипЗаписи = "Сторно" Тогда
		РасчетнаяПартия.АналитикаУчетаПартий =
			АналитикаУчетаПартий(Приход.АналитикаУчетаПартий, Приход.НалогообложениеПартии, Приход.НалогообложениеНДС);
		Если ЗначениеЗаполнено(Приход.НалогообложениеНДС) Тогда
			РасчетнаяПартия.НалогообложениеПартии = Приход.НалогообложениеНДС;
		Иначе
			РасчетнаяПартия.НалогообложениеПартии = Приход.НалогообложениеПартии;
		КонецЕсли;
	Иначе
		РасчетнаяПартия.АналитикаУчетаПартий = Приход.АналитикаУчетаПартий;
		РасчетнаяПартия.НалогообложениеПартии = Приход.НалогообложениеПартии;
	КонецЕсли;

	// Заполняем партионную идентификацию в расчетной партии
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Приход,
		"РасчетЗавершен, ДокументПоступленияРасходов, СтатьяРасходов");
	Если РасчетнаяПартия.Стоимость = 0
	 И РасчетнаяПартия.СтоимостьБезНДС = 0
	 И РасчетнаяПартия.СтоимостьРегл = 0
	 И РасчетнаяПартия.НДСРегл = 0
	 И РасчетнаяПартия.ПостояннаяРазница = 0
	 И РасчетнаяПартия.ВременнаяРазница = 0 Тогда
	 	РасчетнаяПартия.Количество = 0;
		РасчетнаяПартия.Знаменатель = 0;
		РасчетнаяПартия.РасчетЗавершен = Ложь;
	КонецЕсли;		
	
	ЗаполнитьСтатьюИАналитикуСписанияНДС(РасчетнаяПартия, Расход, Приход);
	
КонецПроцедуры


Процедура ЗаполнитьПартиюПроизводственныхЗатрат(РасчетнаяПартия, Расход, Приход) // ПартииПроизводственныхЗатрат
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Расход);
	
	Если Расход.ТипЗаписи = "Корректировка" Тогда
		Приход.Стоимость = Приход.Стоимость + Расход.Стоимость;
		Приход.СтоимостьБезНДС = Приход.СтоимостьБезНДС + Расход.СтоимостьБезНДС;
		Приход.СтоимостьРегл = Приход.СтоимостьРегл + Расход.СтоимостьРегл;
		Приход.НДСРегл = Приход.НДСРегл + Расход.НДСРегл;
		Приход.ПостояннаяРазница = Приход.ПостояннаяРазница + Расход.ПостояннаяРазница;
		Приход.ВременнаяРазница = Приход.ВременнаяРазница + Расход.ВременнаяРазница;
	ИначеЕсли Расход.ТипЗаписи = "ВозвратСторно" Тогда
		РасчетнаяПартия.Количество = Мин(Приход.Количество, Расход.Количество);
		РасчетнаяПартия.Стоимость = Мин(Приход.Стоимость, Расход.Стоимость);
		РасчетнаяПартия.СтоимостьБезНДС = Мин(Приход.СтоимостьБезНДС, Расход.СтоимостьБезНДС);
		РасчетнаяПартия.СтоимостьРегл = Мин(Приход.СтоимостьРегл, Расход.СтоимостьРегл);
		РасчетнаяПартия.НДСРегл = Мин(Приход.НДСРегл, Расход.НДСРегл);
		РасчетнаяПартия.ПостояннаяРазница = Мин(Приход.ПостояннаяРазница, Расход.ПостояннаяРазница);
		РасчетнаяПартия.ВременнаяРазница = Мин(Приход.ВременнаяРазница, Расход.ВременнаяРазница);
		
		Приход.Количество = Приход.Количество - РасчетнаяПартия.Количество;
		Приход.Стоимость = Приход.Стоимость - РасчетнаяПартия.Стоимость;
		Приход.СтоимостьБезНДС = Приход.СтоимостьБезНДС - РасчетнаяПартия.СтоимостьБезНДС;
		Приход.СтоимостьРегл = Приход.СтоимостьРегл - РасчетнаяПартия.СтоимостьРегл;
		Приход.НДСРегл = Приход.НДСРегл - РасчетнаяПартия.НДСРегл;
		Приход.ПостояннаяРазница = Приход.ПостояннаяРазница - РасчетнаяПартия.ПостояннаяРазница;
		Приход.ВременнаяРазница = Приход.ВременнаяРазница - РасчетнаяПартия.ВременнаяРазница;
		
		Расход.Количество = Расход.Количество - РасчетнаяПартия.Количество;
		Расход.Стоимость = Расход.Стоимость - РасчетнаяПартия.Стоимость;
		Расход.СтоимостьБезНДС = Расход.СтоимостьБезНДС - РасчетнаяПартия.СтоимостьБезНДС;
		Расход.СтоимостьРегл = Расход.СтоимостьРегл - РасчетнаяПартия.СтоимостьРегл;
		Расход.НДСРегл = Расход.НДСРегл - РасчетнаяПартия.НДСРегл;
		Расход.ПостояннаяРазница = Расход.ПостояннаяРазница - РасчетнаяПартия.ПостояннаяРазница;
		Расход.ВременнаяРазница = Расход.ВременнаяРазница - РасчетнаяПартия.ВременнаяРазница;
		
		Если Не ЗначениеЗаполнено(РасчетнаяПартия.ДокументИсточник) Тогда
			РасчетнаяПартия.ДокументИсточник = Приход.Регистратор;
		КонецЕсли;
	Иначе
		Если Приход.Количество = 0 Тогда
			Возврат;
		КонецЕсли;
		
		// расчетная партия заполняется на наибольшее общее вычитаемое
		КоличествоСписания = Мин(Расход.Количество, Приход.Количество);
		
		РасходБазис = ?(Расход.Количество <> 0, КоличествоСписания / Расход.Количество, 0);
		ПриходБазис = ?(Приход.Количество <> 0, КоличествоСписания / Приход.Количество, 0);
		
		РасходСтоимостьРегл = Окр(РасходБазис * Расход.СтоимостьРегл, 2);
		РасходНДСРегл = Окр(РасходБазис * Расход.НДСРегл, 2);
		РасходПостояннаяРазница = Окр(РасходБазис * Расход.ПостояннаяРазница, 2);
		РасходВременнаяРазница = Окр(РасходБазис * Расход.ВременнаяРазница, 2);
		
		ПриходСтоимостьРегл = Окр(ПриходБазис * Приход.СтоимостьРегл, 2);
		ПриходНДСРегл = Окр(ПриходБазис * Приход.НДСРегл, 2);
		ПриходПостояннаяРазница = Окр(ПриходБазис * Приход.ПостояннаяРазница, 2);
		ПриходВременнаяРазница = Окр(ПриходБазис * Приход.ВременнаяРазница, 2);
		
		// заполняем показатели расчетной партии
		РасчетнаяПартия.Стоимость = Окр(КоличествоСписания * Приход.Стоимость / Приход.Количество, 2);
		РасчетнаяПартия.СтоимостьБезНДС = Окр(КоличествоСписания * Приход.СтоимостьБезНДС / Приход.Количество, 2);
		Если РасчетнаяПартия.СохранятьРегл Тогда
			РасчетнаяПартия.СтоимостьРегл = РасходСтоимостьРегл;
			РасчетнаяПартия.НДСРегл = РасходНДСРегл;
			РасчетнаяПартия.ПостояннаяРазница = РасходПостояннаяРазница;
			РасчетнаяПартия.ВременнаяРазница = РасходВременнаяРазница;
		Иначе
			Если РасчетнаяПартия.ВидДвижения = ВидДвиженияНакопления.Приход
			 И Приход.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОпределяетсяРаспределением Тогда
				РасчетнаяПартия.НДСРегл = 0;
		 	Иначе
				РасчетнаяПартия.НДСРегл = ПриходНДСРегл;
			КонецЕсли;
			РасчетнаяПартия.СтоимостьРегл = ПриходСтоимостьРегл;
			РасчетнаяПартия.ПостояннаяРазница = ПриходПостояннаяРазница;
			РасчетнаяПартия.ВременнаяРазница = ПриходВременнаяРазница;
		КонецЕсли;
		РасчетнаяПартия.Количество = КоличествоСписания;
		
		// корректируем базу расчета в потреблении
		Расход.Стоимость = Расход.Стоимость - Окр(РасходБазис * Расход.Стоимость, 2);
		Расход.СтоимостьБезНДС = Расход.СтоимостьБезНДС - Окр(РасходБазис * Расход.СтоимостьБезНДС, 2);
		Расход.СтоимостьРегл = Расход.СтоимостьРегл - РасходСтоимостьРегл;
		Расход.НДСРегл = Расход.НДСРегл - РасходНДСРегл;
		Расход.ПостояннаяРазница = Расход.ПостояннаяРазница - РасходПостояннаяРазница;
		Расход.ВременнаяРазница = Расход.ВременнаяРазница - РасходВременнаяРазница;
		Расход.Количество = Расход.Количество - РасчетнаяПартия.Количество;
		
		// корректируем базу расчета в приходе
		Если Расход.ТипЗаписи = "Сторно" И (Приход.ТипЗаписи = "Партия" ИЛИ Приход.ТипЗаписи = "Остаток") Тогда
			Приход.Стоимость = Приход.Стоимость + РасчетнаяПартия.Стоимость;
			Приход.СтоимостьБезНДС = Приход.СтоимостьБезНДС + РасчетнаяПартия.СтоимостьБезНДС;
			Приход.СтоимостьРегл = Приход.СтоимостьРегл + ПриходСтоимостьРегл;
			Приход.НДСРегл = Приход.НДСРегл + ПриходНДСРегл;
			Приход.ПостояннаяРазница = Приход.ПостояннаяРазница + ПриходПостояннаяРазница;
			Приход.ВременнаяРазница = Приход.ВременнаяРазница + ПриходВременнаяРазница;
			Приход.Количество = Приход.Количество + КоличествоСписания;
		Иначе
			Приход.Стоимость = Приход.Стоимость - РасчетнаяПартия.Стоимость;
			Приход.СтоимостьБезНДС = Приход.СтоимостьБезНДС - РасчетнаяПартия.СтоимостьБезНДС;
			Приход.СтоимостьРегл = Приход.СтоимостьРегл - ПриходСтоимостьРегл;
			Приход.НДСРегл = Приход.НДСРегл - ПриходНДСРегл;
			Приход.ПостояннаяРазница = Приход.ПостояннаяРазница - ПриходПостояннаяРазница;
			Приход.ВременнаяРазница = Приход.ВременнаяРазница - ПриходВременнаяРазница;
			Приход.Количество = Приход.Количество - КоличествоСписания;
		КонецЕсли;

		// Заполняем партионную идентификацию в расчетной партии
		Если Не ЗначениеЗаполнено(РасчетнаяПартия.ДокументПоступления) Тогда
			РасчетнаяПартия.ДокументПоступления = Приход.ДокументПоступления;
		КонецЕсли;
		Если Расход.ТипЗаписи = "Перемещение" ИЛИ Приход.ТипЗаписи = "Перемещение" Тогда
			РасчетнаяПартия.ДокументИсточник = Приход.ДокументИсточник;
		ИначеЕсли Не ЗначениеЗаполнено(РасчетнаяПартия.ДокументИсточник) Тогда
			РасчетнаяПартия.ДокументИсточник = Приход.Регистратор;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(РасчетнаяПартия.ВидЗапасов) Тогда
			РасчетнаяПартия.ВидЗапасов = Приход.ВидЗапасов;
		КонецЕсли;
		Если ЗначениеЗаполнено(РасчетнаяПартия.КорАналитикаУчетаНоменклатуры) Тогда
			РасчетнаяПартия.КорВидЗапасов = РасчетнаяПартия.ВидЗапасов;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(РасчетнаяПартия.АналитикаУчетаПартий) Тогда
			Если РасчетнаяПартия.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
				РасчетнаяПартия.АналитикаУчетаПартий =
					АналитикаУчетаПартий(Приход.АналитикаУчетаПартий, Приход.НалогообложениеПартии, Приход.НалогообложениеНДС);
				РасчетнаяПартия.НалогообложениеПартии = Приход.НалогообложениеНДС;
			Иначе
				РасчетнаяПартия.АналитикаУчетаПартий = Приход.АналитикаУчетаПартий;
				РасчетнаяПартия.НалогообложениеПартии = Приход.НалогообложениеПартии;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.ПодразделениеРасходов) Тогда 
			РасчетнаяПартия.ПодразделениеРасходов = Приход.ПодразделениеРасходов;
		КонецЕсли;
		
		ЗаполнитьСтатьюИАналитикуСписанияНДС(РасчетнаяПартия, Расход, Приход);
		
		Если Расход.ТипЗаписи = "Списание" Тогда
			РасчетнаяПартия.ИдентификаторСтроки = Приход.ИдентификаторСтроки;
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Приход, "РасчетЗавершен");
КонецПроцедуры


Процедура ЗаполнитьСтатьюИАналитикуСписанияНДС(РасчетнаяПартия, Расход, Приход)
	
	Период = НачалоМесяца(РасчетнаяПартия.Период);
	
	ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату("НастройкиУчетаНДС",
		РасчетнаяПартия.Организация,
		НачалоМесяца(Период));
		
	Если ПараметрыУчетнойПолитики <> Неопределено
		И ПараметрыУчетнойПолитики.ВариантУчетаНДСПриИзмененииВидаДеятельности = Перечисления.ВариантыУчетаНДСПриИзмененииВидаДеятельностиНаНеоблагаемую.ВключатьВСтоимость Тогда
		Возврат;
	КонецЕсли;
	
	Если УчетНДСУП.ВозможноСписаниеНДС(РасчетнаяПартия.НалогообложениеПартии, РасчетнаяПартия.НалогообложениеНДС)
	 ИЛИ (РасчетнаяПартия.ВидДвижения = ВидДвиженияНакопления.Приход
	  И УчетНДСУП.ВозможноСписаниеНДС(Приход.НалогообложениеПартии, Приход.НалогообложениеНДС)) Тогда
		
		Если ПараметрыУчетнойПолитики <> Неопределено
			И ПараметрыУчетнойПолитики.ВариантУчетаНДСПриИзмененииВидаДеятельности = Перечисления.ВариантыУчетаНДСПриИзмененииВидаДеятельностиНаНеоблагаемую.ВключатьВСтоимостьИлиРасходыВЗависимостиОтПериода Тогда
			
			Если ЗначениеЗаполнено(РасчетнаяПартия.ДокументПоступления) Тогда
				ДатаПартии = НачалоМесяца(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РасчетнаяПартия.ДокументПоступления, "Дата"));
			Иначе
				ДатаПартии = Дата(1,1,1);
			КонецЕсли;
			
			Если ДатаПартии = Период Тогда
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПараметрыУчетнойПолитики <> Неопределено
			И УчетНДСУП.ВозможноСписаниеНДС(РасчетнаяПартия.НалогообложениеПартии, РасчетнаяПартия.НалогообложениеНДС) Тогда
			
			Если РасчетнаяПартия.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС Тогда
				РасчетнаяПартия.СтатьяСписанияНДС 	 = ПараметрыУчетнойПолитики.СтатьяРасходовНеНДС;
				РасчетнаяПартия.АналитикаСписанияНДС = ПараметрыУчетнойПолитики.АналитикаРасходовНеНДС;
			Иначе
				РасчетнаяПартия.СтатьяСписанияНДС 	 = ПараметрыУчетнойПолитики.СтатьяРасходовЕНВД;
				РасчетнаяПартия.АналитикаСписанияНДС = ПараметрыУчетнойПолитики.АналитикаРасходовЕНВД;
			КонецЕсли;
			
		Иначе
			
			РасчетнаяПартия.НДСРегл = 0;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Общие алгоритмы расчета партий
#Область ОбщиеРасчетныеМетоды

Процедура УдалитьНезаписываемыеСтроки(Контекст, РасчетныеПартии)
	
	УдаляемыеТипы = Новый Структура;
	УдаляемыеРазделы = Новый Соответствие;
	Если Контекст = "ПартииТоваровОрганизаций" Тогда
		УдаляемыеТипы.Вставить("Остаток");
		УдаляемыеТипы.Вставить("Прошлое");
		УдаляемыеРазделы.Вставить(Перечисления.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты, "");
		УдаляемыеРазделы.Вставить(Перечисления.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию, "");
		УдалятьНезавершенные = Ложь;
	ИначеЕсли Контекст = "ПартииТоваровПереданныеНаКомиссию" Тогда
		УдаляемыеТипы.Вставить("Остаток");
		УдалятьНезавершенные = Истина;
	ИначеЕсли Контекст = "ПартииРасходовНаСебестоимостьТоваров" Тогда
		УдаляемыеТипы.Вставить("Остаток");
		УдаляемыеТипы.Вставить("Прочее");
		УдаляемыеТипы.Вставить("Прошлое");
		УдаляемыеТипы.Вставить("Услуги");
		УдалятьНезавершенные = Истина;
	ИначеЕсли Контекст = "ПартииЗатратНаВыпуск" Тогда
		УдаляемыеТипы.Вставить("Остаток");
		УдаляемыеТипы.Вставить("Прочее");
		УдаляемыеТипы.Вставить("Прошлое");
		УдалятьНезавершенные = Истина;
	ИначеЕсли Контекст = "ПартииПроизводственныхЗатрат" Тогда
		УдаляемыеТипы.Вставить("Остаток");
		УдаляемыеТипы.Вставить("ОстатокСторно");
		УдалятьНезавершенные = Ложь;	
	ИначеЕсли Контекст = "ПартииНезавершенногоПроизводства" Тогда
		УдаляемыеТипы.Вставить("Остаток");
		УдаляемыеТипы.Вставить("ОстатокСторно");
		УдаляемыеРазделы.Вставить(Перечисления.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка(), "");
		УдалятьНезавершенные = Истина;
	ИначеЕсли Контекст = "ТрудозатратыНезавершенногоПроизводства" Тогда
		УдаляемыеТипы.Вставить("Остаток");
		УдаляемыеТипы.Вставить("НЗП");
		УдаляемыеТипы.Вставить("МаршрутныйЛист");
		УдаляемыеТипы.Вставить("ОстатокБезЗаказа");
		УдаляемыеТипы.Вставить("ПартияБезЗаказа");
		УдаляемыеТипы.Вставить("Потребление");
		УдаляемыеТипы.Вставить("Перемещение");
		УдаляемыеТипы.Вставить("Распределение");
		УдаляемыеТипы.Вставить("ПолуфабрикатДавальца");
		УдаляемыеРазделы.Вставить(Перечисления.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка(), "");
		УдалятьНезавершенные = Истина;
	ИначеЕсли Контекст = "ПрочиеРасходыНезавершенногоПроизводства" Тогда
		УдаляемыеТипы.Вставить("Остаток");
		УдаляемыеТипы.Вставить("Распределение");
		УдаляемыеТипы.Вставить("ПоБазеБезЗаказа");
		УдаляемыеТипы.Вставить("ПолуфабрикатДавальца");
		УдаляемыеРазделы.Вставить(Перечисления.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку, "");
		УдалятьНезавершенные = Истина;
	ИначеЕсли Контекст = "ПрочиеРасходыНезавершенногоПроизводстваКРаспределению" Тогда
		УдаляемыеТипы.Вставить("Партия");
		УдалятьНезавершенные = Истина;
	ИначеЕсли Контекст = "ПрочиеРасходыНезавершенногоПроизводстваПоБазе" Тогда
		УдаляемыеТипы.Вставить("Партия");
		УдалятьНезавершенные = Истина;
	ИначеЕсли Контекст = "ПрочиеРасходыПоБазе" Тогда
		УдаляемыеТипы.Вставить("Партия");
		УдалятьНезавершенные = Истина;
	ИначеЕсли Контекст = "РаспределениеМатериаловИРабот" Тогда
		УдаляемыеТипы.Вставить("Партия");
		УдалятьНезавершенные = Истина;	
	КонецЕсли;
	
	МинимальныйИндекс = 1 - РасчетныеПартии.Количество();
	Для Индекс = МинимальныйИндекс По 0 Цикл
		РасчетнаяПартия = РасчетныеПартии[-Индекс];
		Если УдалятьНезавершенные И РасчетнаяПартия.РасчетЗавершен <> Истина
		 Или УдаляемыеТипы.Свойство(РасчетнаяПартия.ТипЗаписи)
		 Или УдаляемыеРазделы[РасчетнаяПартия.РазделУчета] <> Неопределено
		Тогда
			РасчетныеПартии.Удалить(-Индекс);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция Регистраторы(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, ИмяРегистра,
			МассивИсключаемыхТипов = Неопределено, МассивПоддерживаемыхТипов = Неопределено)
	
	Если МассивИсключаемыхТипов = Неопределено Тогда
		МассивИсключаемыхТипов = Новый Массив;
	КонецЕсли;
	Если МассивПоддерживаемыхТипов = Неопределено Тогда
		МассивПоддерживаемыхТипов = Новый Массив;
	КонецЕсли;
	
	ШаблонЗапроса = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор КАК Регистратор
		|ИЗ
		|	&ШаблонРегистра КАК ДД
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И НЕ ТипЗначения(ДД.Регистратор) В (&МассивИсключаемыхТипов)
		|	И (&ПоддерживаютсяВсеТипы ИЛИ ТипЗначения(ДД.Регистратор) В (&МассивПоддерживаемыхТипов))
		|";
	
	Запрос = Новый Запрос(СтрЗаменить(ШаблонЗапроса, "&ШаблонРегистра", "РегистрНакопления." + ИмяРегистра));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);	
	Запрос.УстановитьПараметр("МассивИсключаемыхТипов", МассивИсключаемыхТипов);
	Запрос.УстановитьПараметр("МассивПоддерживаемыхТипов", МассивПоддерживаемыхТипов);
	Запрос.УстановитьПараметр("ПоддерживаютсяВсеТипы", НЕ ЗначениеЗаполнено(МассивПоддерживаемыхТипов));
	
	Регистраторы = Новый Соответствие;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Регистраторы.Вставить(Выборка.Регистратор, Истина);
	КонецЦикла;
	
	Возврат Регистраторы;
	
КонецФункции

Процедура РегистраторыПартийВПути(НачалоПериода, ОкончаниеПериода, МассивОрганизаций, Регистраторы)
	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ДД.Ссылка КАК Регистратор
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК ДД
		|ГДЕ
		|	(ДД.ДатаПереходаПраваСобственности МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		ИЛИ ДД.ДатаПереходаПраваСобственности = ДАТАВРЕМЯ(1,1,1))
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Проведен
		|	И ДД.ХозяйственнаяОперация В (
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности))
		|";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Регистраторы.Вставить(Выборка.Регистратор, Истина);
	КонецЦикла;
КонецПроцедуры

Функция РегистраторыПартийПрочих(НачалоПериода, ОкончаниеПериода, МассивОрганизаций)
	Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.ПартииПрочихРасходов КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
		|		ПО Себестоимость.Регистратор = ДД.Регистратор
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
		|		ПО Статьи.Ссылка = ДД.СтатьяРасходов
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.Регистратор = ДД.ДокументПоступленияРасходов
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И Себестоимость.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И НЕ Себестоимость.РасчетСебестоимости
		|	И НЕ Себестоимость.СтатьяРасходовСписания = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|	И (Статьи.ВариантРаспределенияРасходовУпр =
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|		ИЛИ Статьи.ВариантРаспределенияРасходовРегл =
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров))
		|");
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);	
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Регистраторы = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		Регистраторы.Вставить(Выборка.Регистратор, Истина);
	КонецЦикла;
	
	Возврат Регистраторы;
	
КонецФункции


Процедура ЗаписатьРасчетныеПартии(МенеджерРегистра, РасчетныеПартии, Регистраторы) Экспорт
	
	Если ТестированиеБезЗаписи() Тогда
		Возврат;
	КонецЕсли;
	
	Если РасчетныеПартии.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	КоличествоНенулевыхЗаписей = 0;
	КоличествоРегистраторов = 0;
	
	РасчетныеПартии.Сортировать("Регистратор", Новый СравнениеЗначений);
	
	Движения = МенеджерРегистра.СоздатьНаборЗаписей();
	Движения.ДополнительныеСвойства.Вставить(ИмяДополнительногоСвойстваЗаписываемогоНабора());
	ДатыЗапретаИзмененияУТ.ОтключитьПроверкуДатыЗапретаИзменения(Движения);
	
	Если ИсходящиеДанныеМеханизма().Получить(Движения.Метаданные()) = Неопределено Тогда
		
		ТекстДляПротокола = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Движения по регистру ""%1"" не должны формироваться механизмом расчета партий.'"),
			Движения.Метаданные().Имя);
		
		ВызватьИсключение РасчетСебестоимостиПрикладныеАлгоритмы.ТекстИсключениеДляРегистрацииПроблемы(ТекстДляПротокола);
		
	КонецЕсли;
	
	// Получим структуру с ключами, соответствующими ресурсам регистра.
	МетаданныеРегистра = Движения.Метаданные();
	СтруктураРесурсов = Новый Структура;
	Для Каждого МетаРесурс Из МетаданныеРегистра.Ресурсы Цикл
		СтруктураРесурсов.Вставить(МетаРесурс.Имя, 0);
	КонецЦикла;
	
	Регистратор = Неопределено;
	Замещать = Истина;
	Счетчик = 0;
	МаксимумСчетчика = РасчетныеПартии.Количество() - 1;
	
	Пока Счетчик <= МаксимумСчетчика Цикл
		
		РасчетнаяПартия = РасчетныеПартии[Счетчик];
		Если Регистратор <> РасчетнаяПартия.Регистратор Тогда
			Регистратор = РасчетнаяПартия.Регистратор;
			
			Движения.Очистить();
			Движения.Отбор.Регистратор.Установить(Регистратор);
			
			Если Регистраторы <> Неопределено Тогда
				Если Регистраторы[Регистратор] <> Неопределено Тогда
					Замещать = Истина;
					Регистраторы.Удалить(Регистратор);
				Иначе
					Замещать = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// Проверим заполненность ресурсов в расчетной партии.
		ЗаполнитьЗначенияСвойств(СтруктураРесурсов, РасчетнаяПартия);
		
		Если НЕ ОбщегоНазначенияУТКлиентСервер.ЗначенияСтруктурыНеЗаполнены(СтруктураРесурсов) Тогда
			Движение = Движения.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, РасчетнаяПартия);
		КонецЕсли;
		
		Счетчик = Счетчик + 1;
		
		Если Счетчик > МаксимумСчетчика Или (Регистратор <> РасчетныеПартии[Счетчик].Регистратор) Тогда
			Если Замещать ИЛИ Движения.Количество() > 0 Тогда
				КоличествоНенулевыхЗаписей = КоличествоНенулевыхЗаписей + Движения.Количество();
				КоличествоРегистраторов = КоличествоРегистраторов + 1;
				Движения.Записать(Замещать);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Замер = ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЗамера;
	Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Записано движений: %1 шт. по %2 док., длительность: %3 сек.'"),
		СокрЛП(КоличествоНенулевыхЗаписей),
		СокрЛП(КоличествоРегистраторов),
		СокрЛП(Замер/1000));
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Партионный учет.ЗаписатьРасчетныеПартии'", ОбщегоНазначения.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Информация,,, Комментарий);
	
КонецПроцедуры

Функция ТекстОтборПоТипамРегистраторовДляСохраненияДвижений()
	
	// Получим перечень метаданные документов, формирующих движения по нескольким организациям.
	МетаданныеСохраняемыхРегистраторов = РасчетСебестоимостиПрикладныеАлгоритмы.ДокументыСРазнымиПериодамиИлиОрганизациямиВДвижениях(Ложь, Истина);
	
	// Сформируем текст для отбора по типу регистратора
	ТекстОтборПоТипамРегистраторов = "";
	
	Для Каждого КлючИЗначение Из МетаданныеСохраняемыхРегистраторов Цикл
		ТекстОтборПоТипамРегистраторов = ТекстОтборПоТипамРегистраторов
			+ ?(ТекстОтборПоТипамРегистраторов = "", "", ", ") + "ТИП(" + КлючИЗначение.Ключ.ПолноеИмя() + ")";
	КонецЦикла;
	
	Возврат "ТИПЗНАЧЕНИЯ(Т.Регистратор) В (" + ТекстОтборПоТипамРегистраторов + ")";
	
КонецФункции

Функция РегистраторыССохраняемымиДвижениями(ИмяРегистра, МенеджерВременныхТаблиц, СтарыеРегистраторы, НачалоПериода, ОкончаниеПериода, МассивОрганизаций)
	
	// Подготовим запрос
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НачалоПериода",  	НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода",  	ОкончаниеПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций",  МассивОрганизаций);
	Запрос.УстановитьПараметр("СтарыеРегистраторы",
		ОбщегоНазначенияУТКлиентСервер.ПреобразоватьСоответствиеИлиСтруктуруВМассив(СтарыеРегистраторы));
	
	// Надо сохранить движения по "другим" организациям из тех регистраторов, которые
	//	- входят в регистраторы таблицы данных для расчета партий
	//  - могут формировать движения по нескольким организациям
	//  - входят в список "старых" регистраторов (для них запись расчетных движений будет выполняться с замещением).
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор
	|ПОМЕСТИТЬ ВТСохраняемыеРегистраторы
	|ИЗ
	|	Данные КАК Т
	|ГДЕ
	|	%1
	|	И Т.Регистратор В (&СтарыеРегистраторы)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	*
	|ПОМЕСТИТЬ ВТСохраняемыеДвижения
	|ИЗ
	|	РегистрНакопления.%2 КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И Т.Регистратор В (ВЫБРАТЬ Т.Регистратор ИЗ ВТСохраняемыеРегистраторы КАК Т)
	|	И НЕ (Т.Организация В (&МассивОрганизаций))
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор
	|ИЗ
	|	ВТСохраняемыеДвижения КАК Т
	|";
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Запрос.Текст,
		ТекстОтборПоТипамРегистраторовДляСохраненияДвижений(),
		ИмяРегистра);
		
	// Получим перечень регистраторов, у которых нужно сохранить движения
	РегистраторыССохраняемымиДвижениями = Новый Соответствие;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		// При восстановлении сохраненных движений их надо добавлять к сформированным расчетным движениям (без замещения).
		РегистраторыССохраняемымиДвижениями.Вставить(Выборка.Регистратор, Ложь);
	КонецЦикла;
	
	Возврат РегистраторыССохраняемымиДвижениями;
	
КонецФункции

Процедура ЗаписатьСохраняемыеДвижения(МенеджерРегистра, МенеджерВременныхТаблиц, РегистраторыССохраняемымиДвижениями)
	
	Если ТестированиеБезЗаписи() Тогда
		Возврат;
	КонецЕсли;
	
	Если РегистраторыССохраняемымиДвижениями.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	*
	|ИЗ
	|	ВТСохраняемыеДвижения КАК Т
	|
	|УПОРЯДОЧИТЬ ПО
	|	Т.Регистратор,
	|	Т.НомерСтроки
	|";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Движения = МенеджерРегистра.СоздатьНаборЗаписей();
	Движения.ДополнительныеСвойства.Вставить(ИмяДополнительногоСвойстваЗаписываемогоНабора());
	
	Регистратор = Неопределено;
	Замещать = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		Если Регистратор <> Выборка.Регистратор Тогда
			
			Если Регистратор <> Неопределено Тогда
				Движения.Записать(Замещать);
			КонецЕсли;
			
			Регистратор = Выборка.Регистратор;
			Замещать = РегистраторыССохраняемымиДвижениями.Получить(Регистратор);
			
			Движения.Очистить();
			Движения.Отбор.Регистратор.Установить(Регистратор);
			
		КонецЕсли;
		
		Движение = Движения.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		
	КонецЦикла;
	
	Если Регистратор <> Неопределено Тогда
		Движения.Записать(Замещать);
	КонецЕсли;
	
КонецПроцедуры


Функция ЕстьФИФОСкользящая(НачалоПериода, ОкончаниеПериода, МассивОрганизаций)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	НастройкиНалоговУчетныхПолитик.ДополнитьМенеджерВременныхТаблицДействующимиПараметрамиНалоговУчетныхПолитик(
		Метаданные.РегистрыСведений.УчетнаяПолитикаФинансовогоУчета.Имя,
		Запрос.МенеджерВременныхТаблиц,
		НачалоПериода,
		"ВТУчетнаяПолитикаФинансовогоУчета",
		МассивОрганизаций);
		
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.МетодОценкиСтоимостиТоваров
	|ИЗ
	|	ВТУчетнаяПолитикаФинансовогоУчета КАК ДД
	|ГДЕ
	|	ДД.МетодОценкиСтоимостиТоваров = ЗНАЧЕНИЕ(Перечисление.МетодыОценкиСтоимостиТоваров.ФИФОСкользящаяОценка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Расчет.Ссылка.МетодОценки
	|ИЗ
	|	Документ.РасчетСебестоимостиТоваров.Организации КАК Расчет
	|ГДЕ
	|	Расчет.Ссылка.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И Расчет.Организация В (&МассивОрганизаций)
	|	И Расчет.Ссылка.МетодОценки = ЗНАЧЕНИЕ(Перечисление.МетодыОценкиСтоимостиТоваров.ФИФОСкользящаяОценка)
	|	И Расчет.Ссылка.Проведен";
	
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода",  ОкончаниеПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);	
	
	Результат = Запрос.Выполнить();
	
	Возврат НЕ Результат.Пустой();
	
КонецФункции

Функция ИмяДополнительногоСвойстваЗаписываемогоНабора()
	Возврат "ПартионныйУчет_ЗаписьПартии";
КонецФункции

Процедура ВременнаяТаблицаДанныхПоОтбору(ПолеОтбора, ТекстСортировка, Таблица, МВТ)
	
	// Подготовим необходимые параметры расчета (аналогично параметрам партионного учета версии 2.2).
	ПараметрыРасчета = Новый Структура;
	ПараметрыРасчета.Вставить("МенеджерВременныхТаблиц", МВТ);
	ПараметрыРасчета.Вставить("ОграниченияВыборки", Новый Структура);
	ПараметрыРасчета.ОграниченияВыборки.Вставить("КоличествоСтрокВТЗ", 100000);
	
	// Подготовим параметры нумерации.
	ТекстСортировка = СтрЗаменить(ТекстСортировка, "	", " ");
	ТекстСортировка = СтрЗаменить(ТекстСортировка, Символы.ПС, " ");
	ТекстСортировка = СтрЗаменить(НРег(ТекстСортировка), "упорядочить по", "");
	
	ПараметрыНумерации = РасчетСебестоимостиПрикладныеАлгоритмы.СформироватьПараметрыНумерацииСтрокВременнойТаблицы(
		ПолеОтбора, // разделитель
		"", // ресурсы
		СокрЛП(ТекстСортировка)); // порядок
	
	// Выполним нумерацию строк.
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьНомераСтрокВременнойТаблицы(
		ПараметрыРасчета,
		ПараметрыНумерации,
		"ИсходныеДанные",
		"Данные");
	
КонецПроцедуры

Функция КоличествоЗаписей(МВТ)
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДД.К) КАК Количество
	|ИЗ
	|	Данные КАК ДД
	|");
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Количество = Выборка.Количество;
	Иначе
		Количество = 0;
	КонецЕсли;
	Возврат Количество;
КонецФункции

Функция КоличествоСвязейМеждуЗаписями(МВТ)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(*) КАК Количество
	|ИЗ
	|	Источники КАК ДД // в таблице Приемники такое же количество записей
	|";
	Запрос.МенеджерВременныхТаблиц = МВТ;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Количество = Выборка.Количество;
	Иначе
		Количество = 0;
	КонецЕсли;
	
	Возврат Количество;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Распределение приходов на расходы

// Основной расчетный метод - заполняет расчетные партии из данных для расчета
// Поля прямого обращения: ТипЗаписи, ВидДвижения, Регистратор
// Обращения к остальным полям происходит через структуру ОписаниеДвижений.
Процедура РаспределитьПриходыНаРасходы(ОписаниеДвижений, ДанныеДляРасчета, РасчетныеПартии)
	ПредыдущееДвижение = Неопределено;
	ИнвертироватьПоказатели = Ложь;
	// Буфер партий по аналитике - очередь копий партионных движений
	Приходы = Новый Структура("Очередь, Указатель, УказательРестарта", Новый Массив, 0, -1);
	// Буфер потреблений по аналитике - очередь копий движений потребления
	Расходы = Новый Структура("Очередь, Указатель, УказательРестарта", Новый Массив, 0, -1);
	
	Для Каждого Движение Из ДанныеДляРасчета Цикл
		// сменилась аналитика
		Если Не ПоляЗаписейРавны(ПредыдущееДвижение, Движение, ОписаниеДвижений.КлючиСравнения) Тогда
			// сбрасываем оставшийся буфер потреблений (партии не подобраны) в расчетные партии
			Расходы.Указатель = ?(Расходы.УказательРестарта > 0, Расходы.УказательРестарта, Расходы.Указатель);
			Пока Расходы.Указатель < Расходы.Очередь.Количество() Цикл
				Расход = Расходы.Очередь[Расходы.Указатель];
				Если Расход[ОписаниеДвижений.БазисРасхода] > 0 Тогда
					// перемещения, что приведет к очистке документа поступления у перемещения.
					Распределение = РасчетныеПартии.Добавить();
					ЗаполнитьРасчетнуюПартию(ОписаниеДвижений.Контекст, Распределение, Расход, Неопределено);
					ИнвертироватьПоказатели(Распределение, ОписаниеДвижений.Показатели, ИнвертироватьПоказатели);
				КонецЕсли;
				Расходы.Указатель = Расходы.Указатель + 1;
			КонецЦикла;
			// инициализируемся
			ПредыдущееДвижение = Движение;
			Приходы.Очередь.Очистить();
			Приходы.Указатель = 0;
			Приходы.УказательРестарта = -1;
			Расходы.Очередь.Очистить();
			Расходы.Указатель = 0;
			Расходы.УказательРестарта = -1;
		КонецЕсли;
		
		ЭтоПриход = ("Остаток" = Движение.ТипЗаписи) Или ("Партия" = Движение.ТипЗаписи)
			Или ("Перемещение" = Движение.ТипЗаписи) Или ("Сторно" = Движение.ТипЗаписи);
		ИнвертироватьПоказатели = ЭтоПриход И Движение[ОписаниеДвижений.БазисПрихода] < 0
			Или НЕ ЭтоПриход И Движение[ОписаниеДвижений.БазисРасхода] < 0;
		
		Если ЭтоПриход Тогда
			Приход = КопияЗаписиСтруктурой(Движение, ОписаниеДвижений.ПоляРасчета);
			ИнвертироватьПоказатели(Приход, ОписаниеДвижений.Показатели, ИнвертироватьПоказатели);
			// если ранее пропустили расход, то текущий приход начинаем распределять на него
			Если Расходы.УказательРестарта >= 0 Тогда
				Расходы.Указатель = Расходы.УказательРестарта;
				Расходы.УказательРестарта = -1;
			КонецЕсли;
			// покрываем расходы из буфера расходов по очереди (FIFO)
			Пока Приход[ОписаниеДвижений.БазисПрихода] > 0 И Расходы.Указатель < Расходы.Очередь.Количество() Цикл
				Расход = Расходы.Очередь[Расходы.Указатель];
				// приход, сделанный по расходу не может быть распределен на этот расход
				Если Расход.Регистратор = Приход[ОписаниеДвижений.КлючРасхода] Тогда
					Расходы.УказательРестарта = Расходы.Указатель;
					Расходы.Указатель = Расходы.Указатель + 1;
					Продолжить;
				КонецЕсли;
				// добавляем в партии покрытую часть расхода
				Если Расход[ОписаниеДвижений.БазисРасхода] > 0 Тогда
					Распределение = РасчетныеПартии.Добавить();
					ЗаполнитьРасчетнуюПартию(ОписаниеДвижений.Контекст, Распределение, Расход, Приход);
					ИнвертироватьПоказатели(Распределение, ОписаниеДвижений.Показатели, ИнвертироватьПоказатели);
				КонецЕсли;
				// если расход покрыт, то ставим на покрытие следующий расход
				Если Расход[ОписаниеДвижений.БазисРасхода] <= 0 Тогда
					Расходы.Указатель = Расходы.Указатель + 1;
				КонецЕсли;
			КонецЦикла;
			// если приход еще остался, то регистрируемся в буфере приходов
			Если Приход[ОписаниеДвижений.БазисПрихода] > 0 Тогда
				Приходы.Очередь.Добавить(Приход);
			КонецЕсли;
			// приход надо добавить в партии - код должен быть здесь для сохранения сортировки
			Если "Остаток" <> Движение.ТипЗаписи Тогда
				Распределение = РасчетныеПартии.Добавить();
				ЗаполнитьРасчетнуюПартию(ОписаниеДвижений.Контекст, Распределение, Движение, Неопределено);
				ИнвертироватьПоказатели(Распределение, ОписаниеДвижений.Показатели, ИнвертироватьПоказатели);
			КонецЕсли;
		КонецЕсли;
		
		Если "Потребление" = Движение.ТипЗаписи Тогда
			Расход = КопияЗаписиСтруктурой(Движение, ОписаниеДвижений.ПоляРасчета);
			ИнвертироватьПоказатели(Расход, ОписаниеДвижений.Показатели, ИнвертироватьПоказатели);
			// если ранее пропустили приход, то текущий расход начинаем потреблять с него
			Если Приходы.УказательРестарта >= 0 Тогда
				Приходы.Указатель = Приходы.УказательРестарта;
				Приходы.УказательРестарта = -1;
			КонецЕсли;
			// списываем приходы из буфера приходов по очереди (FIFO)
			Пока Расход[ОписаниеДвижений.БазисРасхода] > 0 И Приходы.Указатель < Приходы.Очередь.Количество() Цикл
				Приход = Приходы.Очередь[Приходы.Указатель];
				// приход, сделанный по расходу не может быть распределен на этот расход
				Если Расход.Регистратор = Приход[ОписаниеДвижений.КлючРасхода] Тогда
					Приходы.УказательРестарта = Приходы.Указатель;
					Приходы.Указатель = Приходы.Указатель + 1;
					Продолжить;
				КонецЕсли;
				// добавляем в партии покрытую часть расхода
				Если Приход[ОписаниеДвижений.БазисПрихода] > 0 Тогда
					Распределение = РасчетныеПартии.Добавить();
					ЗаполнитьРасчетнуюПартию(ОписаниеДвижений.Контекст, Распределение, Расход, Приход);
					ИнвертироватьПоказатели(Распределение, ОписаниеДвижений.Показатели, ИнвертироватьПоказатели);
				КонецЕсли;
				// если приход потреблен, то выбираем следующий приход
				Если Приход[ОписаниеДвижений.БазисПрихода] <= 0 Тогда
					Приходы.Указатель = Приходы.Указатель + 1;
				КонецЕсли;
			КонецЦикла;
			// если расход еще остался, то регистрируемся в буфере расходов
			Если Расход[ОписаниеДвижений.БазисРасхода] > 0 Тогда
				Расходы.Очередь.Добавить(Расход);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
КонецПроцедуры

Функция ОписаниеДвижений(Контекст, ИмяРегистра, ПоляРасчета, КлючиСравнения, Показатели,
						 БазисПрихода, БазисРасхода, КлючРасхода, ПолеПорядка, ПоляСортировки = "", СортировкаПоУсловию = "")
			
	Результат = Новый Структура;
	
	Результат.Вставить("Контекст", 			  		Контекст);
	Результат.Вставить("ИмяРегистра", 		  		ИмяРегистра);
	Результат.Вставить("ПоляРасчета", 		 	 	ПоляРасчета);
	Результат.Вставить("КлючиСравнения", 	  		КлючиСравнения);
	Результат.Вставить("Показатели", 		  		Показатели);
	Результат.Вставить("БазисПрихода", 		  		БазисПрихода);
	Результат.Вставить("БазисРасхода", 		  		БазисРасхода);
	Результат.Вставить("КлючРасхода", 		  		КлючРасхода);
	Результат.Вставить("ПолеПорядка", 		  		ПолеПорядка);
	Результат.Вставить("ПоляСортировки", 	  		ПоляСортировки);
	Результат.Вставить("СортировкаПоУсловию", 		СортировкаПоУсловию);
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Построение цепочек движений для проброса партий

Функция ОписаниеЦепочек(ПереченьТиповЗаписей)
	ОписаниеЦепочек = Новый Структура(ПереченьТиповЗаписей);
	Для Каждого Описание Из ОписаниеЦепочек Цикл
		ОписаниеЦепочек[Описание.Ключ] = Новый Структура("ПоляСвязи, ТипыПриемников, ТипыИсточников");
		ОписаниеЦепочек[Описание.Ключ].ПоляСвязи = Новый Массив;
		ОписаниеЦепочек[Описание.Ключ].ТипыПриемников = Новый Соответствие;
		ОписаниеЦепочек[Описание.Ключ].ТипыИсточников = Новый Соответствие;
	КонецЦикла;
	Возврат ОписаниеЦепочек;
КонецФункции

Процедура ДобавитьОписаниеПриемника(ОписаниеЦепочек, ТипПриемника, ПоляПриемника)
	Для Каждого ОписаниеПоля Из Новый Структура(ПоляПриемника) Цикл
		ОписаниеЦепочек[ТипПриемника].ПоляСвязи.Добавить(ОписаниеПоля.Ключ);
	КонецЦикла;
КонецПроцедуры

Процедура ДобавитьОписаниеИсточника(ОписаниеЦепочек, ТипПриемника, ТипИсточника, ПоляИсточника)
	ПоляСвязи = Новый Массив;
	Для Каждого ОписаниеПоля Из Новый Структура(ПоляИсточника) Цикл
		ПоляСвязи.Добавить(ОписаниеПоля.Ключ);
	КонецЦикла;
	ОписаниеЦепочек[ТипИсточника].ТипыПриемников.Вставить(ТипПриемника, ПоляСвязи);
	ОписаниеЦепочек[ТипПриемника].ТипыИсточников.Вставить(ТипИсточника, ПоляСвязи);
КонецПроцедуры

// Ждем, что источников будем менее десятка и они будут почти всегда уже упорядочены, поэтому делаем сортировку вставками.
Процедура СортироватьИсточники(Источники, ПолеПорядка, РасчетныеПартии)
	Для НомерИсточника1 = 1 По Источники.ВГраница() Цикл
		Если Неопределено = РасчетныеПартии[Источники[НомерИсточника1]] Тогда
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(РасчетныеПартии[Источники[НомерИсточника1]]) = Тип("Массив") Тогда
			Если РасчетныеПартии[Источники[НомерИсточника1]].Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			ИсточникУ = РасчетныеПартии[Источники[НомерИсточника1]][0];
		Иначе
			ИсточникУ = РасчетныеПартии[Источники[НомерИсточника1]];
		КонецЕсли;
		Элемент = Новый Структура("Порядок, Источник", ИсточникУ[ПолеПорядка], Источники[НомерИсточника1]);
		НомерИсточника2 = НомерИсточника1 - 1;
		Пока НомерИсточника2 >= 0 Цикл
			Если ТипЗнч(РасчетныеПартии[Источники[НомерИсточника2]]) = Тип("Массив") Тогда
				Если РасчетныеПартии[Источники[НомерИсточника2]].Количество() = 0 Тогда
					НомерИсточника2 = НомерИсточника2 - 1;
					Продолжить;
				КонецЕсли;
				ИсточникХ = РасчетныеПартии[Источники[НомерИсточника2]][0];
			Иначе
				ИсточникХ = РасчетныеПартии[Источники[НомерИсточника2]];
			КонецЕсли;
			Если Неопределено = ИсточникХ Тогда
				НомерИсточника2 = НомерИсточника2 - 1;
				Продолжить;
			КонецЕсли;
			Если ИсточникХ[ПолеПорядка] <= Элемент.Порядок Тогда
				Прервать;
			КонецЕсли;
			Источники[НомерИсточника2 + 1] = Источники[НомерИсточника2];
			НомерИсточника2 = НомерИсточника2 - 1;
		КонецЦикла;
		Источники[НомерИсточника2 + 1] = Элемент.Источник;
	КонецЦикла;
КонецПроцедуры

Процедура СортироватьПриходыПоЛИФО(ПолеПорядка, МассивПриходов)
	Список = Новый СписокЗначений;
	Для Каждого Приход Из МассивПриходов Цикл
		Список.Добавить(Приход, Приход[ПолеПорядка]);
	КонецЦикла;
	Список.СортироватьПоПредставлению(НаправлениеСортировки.Убыв);
	МассивПриходов = Список.ВыгрузитьЗначения();
КонецПроцедуры

Процедура СортироватьИсточникиПоЗначениямПолей(Источники, ЗначенияПолей, Приходы)
	
	Список = Новый СписокЗначений;
	ИндексЭлементаМассива = -1;
	
	Для Каждого ИндексИсточника Из Источники Цикл
		
		ИндексЭлементаМассива = ИндексЭлементаМассива + 1;
		МассивПриходов = Приходы[ИндексИсточника];
		
		Если МассивПриходов = Неопределено ИЛИ МассивПриходов.Количество() = 0 Тогда
			Сдвиг = 0;
		Иначе
			Сдвиг = ЗначенияПолей.Количество();
			Для Каждого ПолеСортировки Из ЗначенияПолей Цикл
				Если МассивПриходов[0][ПолеСортировки.Ключ] = ПолеСортировки.Значение Тогда
					Сдвиг = Сдвиг - 1;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Список.Добавить(ИндексИсточника, Формат(ИндексЭлементаМассива + Сдвиг * Источники.Количество(), "ЧЦ=15; ЧДЦ=; ЧВН=; ЧГ="));
		
	КонецЦикла;
	
	Если Список.Количество() > 0 Тогда
		Список.СортироватьПоПредставлению(НаправлениеСортировки.Возр);
		Источники = Список.ВыгрузитьЗначения();
	КонецЕсли;
	
КонецПроцедуры

Функция ТребуетсяСортировка(Контекст, Расход)
	ТребуетсяСортировка = Истина;
	Если Контекст = "ПартииТоваровОрганизаций" Тогда
		ТребуетсяСортировка = (ТипЗнч(Расход.Регистратор) = Тип("ДокументСсылка.ТаможеннаяДекларацияИмпорт") И Расход.ТипЗаписи = "Потребление")
			ИЛИ (ТипЗнч(Расход.Регистратор) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") И Расход.ТипЗаписи = "Потребление");
	КонецЕсли;
	Возврат ТребуетсяСортировка;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Построение цепочек движений с помощью запроса

Процедура ЦепочкиДвижений(ОписаниеЦепочек, МВТ)
	ШаблонЗапроса = "
	|ВЫБРАТЬ
	|	Приемники.К КАК Приемник,
	|	Источники.К КАК Источник
	|ПОМЕСТИТЬ
	|	&Результат
	|ИЗ
	|	Данные КАК Приемники
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Данные КАК Источники
	|		ПО &Условия
	|ГДЕ
	|	Приемники.К <> Источники.К
	|	И Приемники.ТипЗаписи = &ТипПриемника
	|	И Источники.ТипЗаписи = &ТипИсточника
	|;
	|";
	ТекстЗапроса = "";
	Результаты = Новый Массив;
	Для Каждого Описание Из ОписаниеЦепочек Цикл
		ПоляПриемника = Описание.Значение.ПоляСвязи;
		Если ПоляПриемника.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		Для Каждого ОписаниеИсточника Из Описание.Значение.ТипыИсточников Цикл
			ПоляИсточника = ОписаниеИсточника.Значение;
			Если ПоляИсточника.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			НомерПоля = -1;
			Для Каждого ПолеПриемника Из ПоляПриемника Цикл
				НомерПоля = НомерПоля + 1;
				Если НомерПоля = 0 Тогда
					Условия = "Приемники." + ПолеПриемника + " = " + "Источники." + ПоляИсточника[НомерПоля];
				Иначе
					Условия = Условия + Символы.ПС + " И " + "Приемники." + ПолеПриемника + " = " + "Источники." + ПоляИсточника[НомерПоля];
				КонецЕсли;
			КонецЦикла;
			ТекстЗапроса = СтрЗаменить(ШаблонЗапроса, "&Результат", Описание.Ключ + ОписаниеИсточника.Ключ);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условия", Условия);
			Результаты.Добавить(Описание.Ключ + ОписаниеИсточника.Ключ);
			Запрос = Новый Запрос(ТекстЗапроса);
			Запрос.МенеджерВременныхТаблиц = МВТ;
			Запрос.УстановитьПараметр("ТипПриемника", Описание.Ключ);
			Запрос.УстановитьПараметр("ТипИсточника", ОписаниеИсточника.Ключ);
			Запрос.Выполнить();
		КонецЦикла;
	КонецЦикла;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ ПЕРВЫЕ 0
	|	-1 КАК Приемник,
	|	-1 КАК Источник
	|ПОМЕСТИТЬ
	|	Цепочки
	|";
	ШаблонЗапроса = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	ДД.Приемник,
	|	ДД.Источник
	|ИЗ
	|	&ИмяТаблицы КАК ДД
	|";
	Для Каждого Результат Из Результаты Цикл
		ТекстЗапроса = ТекстЗапроса + СтрЗаменить(ШаблонЗапроса, "&ИмяТаблицы", Результат);
	КонецЦикла;
	ТекстЗапроса = ТекстЗапроса + ";";
	Для Каждого Результат Из Результаты Цикл
		ТекстЗапроса = ТекстЗапроса + "УНИЧТОЖИТЬ " + Результат + ";";
	КонецЦикла;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Приемник КАК Ключ,
	|	ДД.Источник КАК Источник,
	|	0 			КАК Порядок
	|ПОМЕСТИТЬ Источники
	|ИЗ
	|	Цепочки КАК ДД
	|ИНДЕКСИРОВАТЬ ПО
	|	Ключ
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Источник КАК Ключ,
	|	ДД.Приемник	КАК Приемник,
	|	0 			КАК Порядок
	|ПОМЕСТИТЬ Приемники
	|ИЗ
	|	Цепочки КАК ДД
	|ИНДЕКСИРОВАТЬ ПО
	|	Ключ
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Цепочки
	|";
	
	Запрос.Выполнить();
	
	ОптимизироватьНумерациюВЦепочках(МВТ);
	
КонецПроцедуры

Процедура ОптимизироватьНумерациюВЦепочках(МенеджерВременныхТаблиц)
	
	РазмерТаблицыДанных = РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(МенеджерВременныхТаблиц, "Данные");
	
	Если РазмерТаблицыДанных = 0 Тогда
		Возврат;
	КонецЕсли;
	
	#Область Инициализация
	
	НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	// Подготовим необходимые параметры расчета (аналогично параметрам партионного учета версии 2.2).
	ПараметрыРасчета = Новый Структура;
	ПараметрыРасчета.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
	ПараметрыРасчета.Вставить("ОграниченияВыборки", Новый Структура);
	ПараметрыРасчета.ОграниченияВыборки.Вставить("КоличествоСтрокВТЗ", 100000);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("РазмерТаблицыДанных", РазмерТаблицыДанных);
	Запрос.УстановитьПараметр("КоличествоСтрокВТЗ",  ПараметрыРасчета.ОграниченияВыборки.КоличествоСтрокВТЗ);
	
	// Получим описание структуры временной таблицы Данные.
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 0
	|	*
	|ИЗ
	|	Данные КАК Т";
	
	ПустаяТаблицаДанных    = Запрос.Выполнить().Выгрузить();
	КолонкиТаблицыДанных   = ПустаяТаблицаДанных.Колонки;
	
	ЕстьПериодПоступленияВДанных = (КолонкиТаблицыДанных.Найти("ПериодПоступления") <> Неопределено);
	ЕстьПериодВДанных 	   		 = (КолонкиТаблицыДанных.Найти("Период") <> Неопределено);
	ЕстьРегистраторВДанных 		 = (КолонкиТаблицыДанных.Найти("Регистратор") <> Неопределено);
	
	ИменаКолонокТаблицыДанные = "";
	Для Каждого ТекущаяКолонка Из КолонкиТаблицыДанных Цикл
		
		ИмяКолонки = ?(ТекущаяКолонка.Имя = "К", "НовыеНомера.НовыйНомерУзла КАК К", "Т." + ТекущаяКолонка.Имя);
		
		ИменаКолонокТаблицыДанные = ИменаКолонокТаблицыДанные + ?(ИменаКолонокТаблицыДанные = "", "", ",
			|	") + ИмяКолонки;
		
	КонецЦикла;
	
	#КонецОбласти
	
	#Область Упорядочивание
	
	// Подготовим данные для упорядочивания записей.
	// Упорядочивание выполняется по полям ПериодПоступления, Период, Регистратор
	// Упорядочивание используется для сортировки узлов одной волны подграфа и для сортировки источников и приемников узла.
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.К КАК НомерУзла,
	|	%1 КАК ПериодПоступления,
	|	%2 КАК Период,
	|	%3 КАК Регистратор
	|ПОМЕСТИТЬ ВТДанныеДляУпорядочивания
	|ИЗ
	|	Данные КАК Т
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПериодПоступления,
	|	Период,
	|	Регистратор";
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Запрос.Текст,
		?(ЕстьПериодПоступленияВДанных, "ЕСТЬNULL(Т.ПериодПоступления, ДАТАВРЕМЯ(1,1,1))", "ДАТАВРЕМЯ(1,1,1)"),
		?(ЕстьПериодВДанных, "ЕСТЬNULL(Т.Период, ДАТАВРЕМЯ(1,1,1))", "ДАТАВРЕМЯ(1,1,1)"),
		?(ЕстьРегистраторВДанных, "ЕСТЬNULL(Т.Регистратор, НЕОПРЕДЕЛЕНО)", "ДАТАВРЕМЯ(1,1,1)"));
	
	Запрос.Выполнить(); // создание ВТДанныеДляУпорядочивания
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	%1 КАК ЗначениеРазделителя,
	|	Т.ПериодПоступления КАК ПериодПоступления,
	|	Т.Период КАК Период,
	|	Т.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТПоляУпорядочивания
	|ИЗ
	|	ВТДанныеДляУпорядочивания КАК Т
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПериодПоступления,
	|	Период,
	|	Регистратор";
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Запрос.Текст,
		?(ЕстьПериодПоступленияВДанных,
			"НАЧАЛОПЕРИОДА(Т.ПериодПоступления, ДЕНЬ)",
			?(ЕстьПериодВДанных,
				"НАЧАЛОПЕРИОДА(Т.Период, ДЕНЬ)",
				"1")));
	
	Запрос.Выполнить(); // создание ВТПоляУпорядочивания
	
	ПараметрыНумерации = РасчетСебестоимостиПрикладныеАлгоритмы.СформироватьПараметрыНумерацииСтрокВременнойТаблицы(
		"ЗначениеРазделителя", // разделитель
		"", // ресурсы
		"ПериодПоступления, Период, Регистратор", // порядок
		"Порядок", // номер
		"ПериодПоступления, Период, Регистратор", // индекс
		"", // накопление
		Ложь); // не подбирать разделитель
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьНомераСтрокВременнойТаблицы(
		ПараметрыРасчета,
		ПараметрыНумерации,
		"ВТПоляУпорядочивания");
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.НомерУзла 			   КАК НомерУзла,
	|	ПоляУпорядочивания.Порядок КАК Порядок
	|ПОМЕСТИТЬ ВТПорядокУзлов
	|ИЗ
	|	ВТДанныеДляУпорядочивания КАК Т
	|   ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПоляУпорядочивания КАК ПоляУпорядочивания
	|		ПО Т.ПериодПоступления = ПоляУпорядочивания.ПериодПоступления
	|		 И Т.Период = ПоляУпорядочивания.Период
	|		 И Т.Регистратор = ПоляУпорядочивания.Регистратор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерУзла";
	
	Запрос.Выполнить(); // создание ВТПорядокУзлов
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета,
		"ВТДанныеДляУпорядочивания, ВТПоляУпорядочивания");
		
	#КонецОбласти
	
	#Область РазбиениеГрафаНаПодграфы
	
	// Выполним разбиение исходного графа данных на несвязанные между собой подграфы.
	// Каждый найденный подграф будет пронумерован от 0 до (количество подграфов - 1) в порядку убывания количества узлов в подграфе.
	// Т.е. подграф с номером 0 будет самым большим, а последние подграфы будут вырожденные (содержат по одному узлу).
	//
	// Разбиение выполняем следующим образов:
	// - для начала каждой вершине присваиваем номер подграфа, равный номеру самой вершины
	// - для каждой вершины i определяем номера подграфов всех ее источников и приемников
	// - если минимальный номер подграфов связанных вершин меньше, чем номер подграфа самой вершины i,
	//	 то присваиваем вершине i этот минимальный номер подграфа
	// - повторяем в цикле действия 2 и 3 до тех пор, пока на очередной итерации ни у одной вершины не изменится номер ее подграфа.
	// Т.е. минимальный номер вершины в каждом подграфе как-бы начинает "расползаться" по этому подграфу - от этой вершины
	// сначала "переходит" на смежные вершины, потом на их смежные вершины и т.д, пока не "займет" весь подграф.
	// В результате каждый подграф будет иметь свой уникальный номер, равный номеру минимальной вершины, входящий в него.
	// Затем сделаем сквозную нумерацию этих подграфов (как написано выше).
	// Например, есть исходный граф ((4-2-1-7) (6) (5-3)):
	// - найдем в нем три подграфа с условными номерами 1, 6 и 3
	// - затем присвоим им номера 0, 2 и 1 соответственно.
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.К КАК НомерУзла,
	|	Т.К КАК НомерПодграфа
	|ПОМЕСТИТЬ ВТУзлыПодграфов
	|ИЗ
	|	Данные КАК Т
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерУзла
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Ключ КАК НомерУзла,
	|	Т.Источник КАК СвязанныйУзел,
	|	ИСТИНА КАК ЭтоИсточник
	|ПОМЕСТИТЬ ВТСвязиУзлов
	|ИЗ
	|	Источники КАК Т
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Ключ,
	|	Т.Приемник,
	|	ЛОЖЬ
	|ИЗ
	|	Приемники КАК Т
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СвязанныйУзел,
	|	ЭтоИсточник";
	
	Запрос.Выполнить(); // создание ВТУзлыПодграфов и ВТСвязиУзлов
	
	ЕстьИзменения 	   = Истина;
	КоличествоИтерацийПоискаПодграфов = 0;
	
	Пока ЕстьИзменения Цикл
		
		КоличествоИтерацийПоискаПодграфов = КоличествоИтерацийПоискаПодграфов + 1;
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СвязиУзлов.НомерУзла КАК НомерУзла,
		|	МИНИМУМ(Т.НомерПодграфа) КАК НомерПодграфа
		|ПОМЕСТИТЬ ВТПодграфыСвязанныхУзлов
		|ИЗ
		|	ВТУзлыПодграфов КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСвязиУзлов КАК СвязиУзлов
		|		ПО Т.НомерУзла = СвязиУзлов.СвязанныйУзел
		|
		|СГРУППИРОВАТЬ ПО
		|	СвязиУзлов.НомерУзла
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерУзла
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.НомерУзла КАК НомерУзла,
		|	ПодграфыСвязанныхУзлов.НомерПодграфа КАК НомерПодграфа
		|ПОМЕСТИТЬ ВТИзмененныеПодграфы
		|ИЗ
		|	ВТУзлыПодграфов КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПодграфыСвязанныхУзлов КАК ПодграфыСвязанныхУзлов
		|		ПО Т.НомерУзла = ПодграфыСвязанныхУзлов.НомерУзла
		|ГДЕ
		|	ПодграфыСвязанныхУзлов.НомерПодграфа < Т.НомерПодграфа
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерУзла
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПодграфыСвязанныхУзлов";
		
		Запрос.Выполнить(); // создание ВТИзмененныеПодграфы
		
		ЕстьИзменения = (РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТИзмененныеПодграфы") > 0);
		
		Если ЕстьИзменения Тогда
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	Т.НомерУзла,
			|	ВЫБОР КОГДА ИзмененныеПодграфы.НомерУзла ЕСТЬ NULL 
			|		ТОГДА Т.НомерПодграфа
			|		ИНАЧЕ ИзмененныеПодграфы.НомерПодграфа
			|	КОНЕЦ КАК НомерПодграфа
			|ПОМЕСТИТЬ ВТНовыеУзлыПодграфов
			|ИЗ
			|	ВТУзлыПодграфов КАК Т
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИзмененныеПодграфы КАК ИзмененныеПодграфы
			|		ПО Т.НомерУзла = ИзмененныеПодграфы.НомерУзла
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТУзлыПодграфов
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Т.НомерУзла,
			|	Т.НомерПодграфа
			|ПОМЕСТИТЬ ВТУзлыПодграфов
			|ИЗ
			|	ВТНовыеУзлыПодграфов КАК Т
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	НомерУзла
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТНовыеУзлыПодграфов";
			
			Запрос.Выполнить(); // обновление ВТУзлыПодграфов
			
		КонецЕсли;
		
		РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета, "ВТИзмененныеПодграфы");
		
	КонецЦикла;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.НомерПодграфа КАК НомерПодграфа,
	|	СУММА(1) КАК КоличествоУзлов,
	|	СУММА(1) КАК НовыйНомерПервогоУзлаПодграфа
	|ПОМЕСТИТЬ ВТПодграфы
	|ИЗ
	|	ВТУзлыПодграфов КАК Т
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.НомерПодграфа";
	
	Запрос.Выполнить(); // создание ВТПодграфы
	
	ПараметрыНумерации = РасчетСебестоимостиПрикладныеАлгоритмы.СформироватьПараметрыНумерацииСтрокВременнойТаблицы(
		"", // разделитель
		"", // ресурсы
		"КоличествоУзлов УБЫВ, НомерПодграфа", // порядок
		"НовыйНомерПодграфа", // номер
		"НомерПодграфа", // индекс
		"НовыйНомерПервогоУзлаПодграфа"); // накопление
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьНомераСтрокВременнойТаблицы(
		ПараметрыРасчета,
		ПараметрыНумерации,
		"ВТПодграфы");
	
	#КонецОбласти
	
	#Область ВолновойОбходГрафа
	
	// Для того, чтобы пронумеровать вершины графа от начальных (не имеющих входов) до конечных (не имеющих выходов),
	// необходимо для каждой вершины определить ее "удаленность" от начальной вершины.
	// Вершины с меньшей "удаленностью" от начальных должны иметь меньшие номера чем вершины с большей "удаленность".
	// Т.е. цепочки графа должен иметь вид 1->2->3->4, а не 2->4->1->3.
	// При соблюдении этого условию механизм расчета партий по цепочкам будет работать наиболее оптимальным образом.
	//
	// Для расчета удаленности вершин используем волновой метод:
	// - начальным вершинам присваиваем номер волны 1
	// - вершинам, в которые есть путь из начальных вершин - номер волны 2
	// - и т.д., пока не достигнем конечных вершин.
	// Если после этого останутся вершины, которым не присвоен номер волны,
	// значит эти вершины образуют цикл, в который нет пути из начальных вершин.
	// Обрабатываем такие циклы следующим образом:
	// - выбираем произвольную вершину, принадлежащую этому циклу
	// - присваиваем ей очередной номер волны N и от нее продолжаем обход оставшихся вершин
	// В результате всем вершинам исходного графа будет присвоен некий номер волны.
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.НомерУзла КАК НомерУзла,
	|	Т.КоличествоПриемников КАК КоличествоПриемников,
	|	Т.КоличествоИсточников КАК КоличествоИсточников,
	|	ВЫБОР
	|		КОГДА Т.КоличествоПриемников = 0 И Т.КоличествоИсточников = 0
	|			ТОГДА &РазмерТаблицыДанных
	|		КОГДА Т.КоличествоИсточников = 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НомерВолны
	|ПОМЕСТИТЬ ВТОписаниеУзлов
	|ИЗ
	|	(ВЫБРАТЬ
	|		Т.НомерУзла КАК НомерУзла,
	|		СУММА(Т.КоличествоПриемников) КАК КоличествоПриемников,
	|		СУММА(Т.КоличествоИсточников) КАК КоличествоИсточников
	|	ИЗ
	|		(ВЫБРАТЬ
	|			Т.НомерУзла КАК НомерУзла,
	|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Приемники.Приемник) КАК КоличествоПриемников,
	|			0 КАК КоличествоИсточников
	|		ИЗ
	|			ВТУзлыПодграфов КАК Т
	|				ЛЕВОЕ СОЕДИНЕНИЕ Приемники КАК Приемники
	|				ПО Т.НомерУзла = Приемники.Ключ
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Т.НомерУзла
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			Т.НомерУзла,
	|			0,
	|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Источники.Источник)
	|		ИЗ
	|			ВТУзлыПодграфов КАК Т
	|				ЛЕВОЕ СОЕДИНЕНИЕ Источники КАК Источники
	|				ПО Т.НомерУзла = Источники.Ключ
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Т.НомерУзла) КАК Т
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Т.НомерУзла) КАК Т
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерУзла";
	
	Запрос.Выполнить(); // создание ВТОписаниеУзлов
	
	ЕстьИзменения 	    = Истина;
	НомерВолны    	    = 1;
	НомерВолныДляЦиклов = 0;
	
	Пока ЕстьИзменения Цикл
		
		НомерВолны = НомерВолны + 1;
		Запрос.УстановитьПараметр("НомерВолны", НомерВолны);
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Приемники.Приемник КАК НомерУзла
		|ПОМЕСТИТЬ ВТИзменениеОписанияУзлов
		|ИЗ
		|	ВТОписаниеУзлов КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Приемники КАК Приемники
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОписаниеУзлов КАК ОписаниеУзловПриемников
		|			ПО Приемники.Приемник = ОписаниеУзловПриемников.НомерУзла
		|				И (ОписаниеУзловПриемников.НомерВолны = 0)
		|		ПО Т.НомерУзла = Приемники.Ключ
		|ГДЕ
		|	Т.НомерВолны = &НомерВолны - 1
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерУзла";
		
		Запрос.Выполнить(); // создание ВТИзменениеОписанияУзлов
		
		ЕстьИзменения = (РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТИзменениеОписанияУзлов") > 0);
		
		Если НЕ ЕстьИзменения Тогда
			
			// Может получиться, что некоторые узла обойти не удалось - эти узлы могут располагаться в циклах, в которые нет пути
			// из начальных вершин ( с номером волны = 1). В этом случае из цикла берем вершину с минимальным номером и
			// минимальным количеством исходящих дуг и продолжаем обход из нее.
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	КОЛИЧЕСТВО(*) КАК КоличествоУзловВЦиклах
			|ИЗ
			|	ВТОписаниеУзлов КАК Т
			|ГДЕ
			|	Т.НомерВолны = 0
			|";
			
			Выборка = Запрос.Выполнить().Выбрать();
			Выборка.Следующий();
			
			ЕстьИзменения = (Выборка.КоличествоУзловВЦиклах > 0); // если Ложь, то обход графа полностью завершен
				
			Если ЕстьИзменения Тогда
				
				НомерВолныДляЦиклов = ?(ЗначениеЗаполнено(НомерВолныДляЦиклов), НомерВолныДляЦиклов, НомерВолны);
				
				Запрос.Текст =
				"ВЫБРАТЬ
				|	Т.НомерПодграфа КАК НомерПодграфа,
				|	Т.НомерУзла,
				|	ОписаниеУзлов.КоличествоПриемников КАК КоличествоПриемников
				|ПОМЕСТИТЬ ВТУзлыДляОбхода
				|ИЗ
				|	ВТУзлыПодграфов КАК Т
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОписаниеУзлов КАК ОписаниеУзлов
				|		ПО Т.НомерУзла = ОписаниеУзлов.НомерУзла
				|ГДЕ
				|	ОписаниеУзлов.НомерВолны = 0
				|
				|ИНДЕКСИРОВАТЬ ПО
				|	НомерПодграфа,
				|	КоличествоПриемников
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	Т.НомерПодграфа КАК НомерПодграфа,
				|	МИНИМУМ(Т.КоличествоПриемников) КАК КоличествоПриемников
				|ПОМЕСТИТЬ ВТПодграфыДляОбхода
				|ИЗ
				|	ВТУзлыДляОбхода КАК Т
				|
				|СГРУППИРОВАТЬ ПО
				|	Т.НомерПодграфа
				|
				|ИНДЕКСИРОВАТЬ ПО
				|	НомерПодграфа,
				|	КоличествоПриемников
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|УНИЧТОЖИТЬ ВТИзменениеОписанияУзлов
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ВТУзлыДляОбхода.НомерПодграфа,
				|	МИНИМУМ(ВТУзлыДляОбхода.НомерУзла) КАК НомерУзла
				|ПОМЕСТИТЬ ВТИзменениеОписанияУзлов
				|ИЗ
				|	ВТУзлыДляОбхода КАК ВТУзлыДляОбхода
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПодграфыДляОбхода КАК ВТПодграфыДляОбхода
				|		ПО ВТУзлыДляОбхода.НомерПодграфа = ВТПодграфыДляОбхода.НомерПодграфа
				|			И ВТУзлыДляОбхода.КоличествоПриемников = ВТПодграфыДляОбхода.КоличествоПриемников
				|
				|СГРУППИРОВАТЬ ПО
				|	ВТУзлыДляОбхода.НомерПодграфа
				|
				|ИНДЕКСИРОВАТЬ ПО
				|	НомерУзла";
				
				Запрос.Выполнить(); // создание ВТИзменениеОписанияУзлов
				
				РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета, "ВТУзлыДляОбхода, ВТПодграфыДляОбхода");
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЕстьИзменения Тогда
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	Т.НомерУзла,
			|	Т.КоличествоПриемников,
			|	Т.КоличествоИсточников,
			|	ВЫБОР КОГДА ИзменениеОписанияУзлов.НомерУзла ЕСТЬ NULL 
			|		ТОГДА Т.НомерВолны
			|		ИНАЧЕ &НомерВолны
			|	КОНЕЦ КАК НомерВолны
			|ПОМЕСТИТЬ ВТНовоеОписаниеУзлов
			|ИЗ
			|	ВТОписаниеУзлов КАК Т
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИзменениеОписанияУзлов КАК ИзменениеОписанияУзлов
			|		ПО Т.НомерУзла = ИзменениеОписанияУзлов.НомерУзла
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТОписаниеУзлов
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Т.НомерУзла,
			|	Т.КоличествоПриемников,
			|	Т.КоличествоИсточников,
			|	Т.НомерВолны
			|ПОМЕСТИТЬ ВТОписаниеУзлов
			|ИЗ
			|	ВТНовоеОписаниеУзлов КАК Т
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	НомерУзла
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТНовоеОписаниеУзлов";
			
			Запрос.Выполнить(); // обновление ВТОписаниеУзлов
			
		КонецЕсли;
		
		РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета, "ВТИзменениеОписанияУзлов");
			
	КонецЦикла;
	
	#КонецОбласти
	
	#Область РасчетНовыхНомеровУзлов
	
	// Рассчитаем новые номера вершин исходного графа:
	// - упорядочим все вершины
	// - обойдем получившийся результат и последовательно пронумеруем вершины от 0 до (количество вершин - 1).
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Подграфы.НовыйНомерПервогоУзлаПодграфа / &КоличествоСтрокВТЗ КАК ЧИСЛО(15,0)) КАК ЗначениеРазделителя,
	|	Подграфы.НомерПодграфа 			КАК НомерПодграфа,
	|	Подграфы.НовыйНомерПодграфа 	КАК НовыйНомерПодграфа,
	|	Т.НомерВолны 					КАК НомерВолны,
	|	Т.НомерУзла 					КАК НомерУзла,
	|	Т.НомерУзла 					КАК НовыйНомерУзла,
	|	ПорядокУзлов.Порядок 			КАК Порядок
	|ПОМЕСТИТЬ ВТНовыеНомераУзлов
	|ИЗ
	|	ВТОписаниеУзлов КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТУзлыПодграфов КАК УзлыПодграфов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПодграфы КАК Подграфы
	|			ПО УзлыПодграфов.НомерПодграфа = Подграфы.НомерПодграфа
	|		ПО Т.НомерУзла = УзлыПодграфов.НомерУзла
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПорядокУзлов КАК ПорядокУзлов
	|		ПО Т.НомерУзла = ПорядокУзлов.НомерУзла";
	
	Запрос.Выполнить(); // создание ВТНовыеНомераУзлов
	
	ПараметрыНумерации = РасчетСебестоимостиПрикладныеАлгоритмы.СформироватьПараметрыНумерацииСтрокВременнойТаблицы(
		"ЗначениеРазделителя", // разделитель
		"", // ресурсы
		"НовыйНомерПодграфа, НомерВолны, Порядок, НомерУзла", // порядок
		"НовыйНомерУзла", // номер
		"НомерУзла", // индекс
		"", // накопление
		Ложь); // не подбирать разделитель
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьНомераСтрокВременнойТаблицы(
		ПараметрыРасчета,
		ПараметрыНумерации,
		"ВТНовыеНомераУзлов");
		
	#КонецОбласти
	
	#Область ИзменениеНумерацииВТаблицах
	
	// Заменим старые номера вершин на новые в служебных временных таблицах Данные, Источники, Приемники.
	// В таблицах Источники и Приемники заполним поле Порядок.
	// Это поле относятся к колонкам "Источник" и "Приемник" и предназначено для их сортировки по ФИФО.
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.НомерУзла 			 КАК Ключ,
	|	Т.СвязанныйУзел 		 КАК Источник,
	|	ПорядокУзлов.Порядок 	 КАК Порядок
	|ПОМЕСТИТЬ Источники_Временная
	|ИЗ
	|	ВТСвязиУзлов КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПорядокУзлов КАК ПорядокУзлов
	|		ПО Т.СвязанныйУзел = ПорядокУзлов.НомерУзла
	|			И Т.ЭтоИсточник = ИСТИНА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ключ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Источники
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеНомераКлючей.НовыйНомерУзла 	 КАК Ключ,
	|	НовыеНомераИсточников.НовыйНомерУзла КАК Источник,
	|	Т.Порядок 				 			 КАК Порядок
	|ПОМЕСТИТЬ Источники
	|ИЗ
	|	Источники_Временная КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНовыеНомераУзлов КАК НовыеНомераКлючей
	|		ПО Т.Ключ = НовыеНомераКлючей.НомерУзла
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНовыеНомераУзлов КАК НовыеНомераИсточников
	|		ПО Т.Источник = НовыеНомераИсточников.НомерУзла
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ключ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Источники_Временная";
	
	Запрос.Выполнить(); // обновление Источники
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.НомерУзла 			 КАК Ключ,
	|	Т.СвязанныйУзел 		 КАК Приемник,
	|	ПорядокУзлов.Порядок 	 КАК Порядок
	|ПОМЕСТИТЬ Приемники_Временная
	|ИЗ
	|	ВТСвязиУзлов КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПорядокУзлов КАК ПорядокУзлов
	|		ПО Т.СвязанныйУзел = ПорядокУзлов.НомерУзла
	|			И (Т.ЭтоИсточник = ЛОЖЬ)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ключ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Приемники
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеНомераКлючей.НовыйНомерУзла 	 КАК Ключ,
	|	НовыеНомераПриемников.НовыйНомерУзла КАК Приемник,
	|	Т.Порядок 				 			 КАК Порядок
	|ПОМЕСТИТЬ Приемники
	|ИЗ
	|	Приемники_Временная КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНовыеНомераУзлов КАК НовыеНомераКлючей
	|		ПО Т.Ключ = НовыеНомераКлючей.НомерУзла
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНовыеНомераУзлов КАК НовыеНомераПриемников
	|		ПО Т.Приемник = НовыеНомераПриемников.НомерУзла
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ключ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Приемники_Временная";
	
	Запрос.Выполнить(); // обновление Приемники
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК ЕстьРазличия
	|ИЗ
	|	ВТНовыеНомераУзлов КАК Т
	|ГДЕ
	|	Т.НомерУзла <> Т.НовыйНомерУзла";
	
	ЕстьИзменения = НЕ Запрос.Выполнить().Пустой();
	
	Если ЕстьИзменения Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	*
		|ПОМЕСТИТЬ Данные_Временная
		|ИЗ
		|	Данные КАК Т
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Данные
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	%1
		|ПОМЕСТИТЬ Данные
		|ИЗ
		|	Данные_Временная КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНовыеНомераУзлов КАК НовыеНомера
		|		ПО Т.К = НовыеНомера.НомерУзла
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	К
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Данные_Временная";
		
		Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Запрос.Текст, ИменаКолонокТаблицыДанные);
		
		Запрос.Выполнить(); // обновление Данные
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ФормированиеСтатистикиГрафа
	
	// Создадим новые служебные таблицы ОписаниеПодграфов и ОписаниеУзловПодграфов.
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.НовыйНомерУзла 					КАК НомерУзла,
	|	Т.НовыйНомерПодграфа 				КАК НомерПодграфа,
	|	Т.НомерВолны 						КАК НомерВолны,
	|	Т.Порядок							КАК Порядок,
	|	ОписаниеУзлов.КоличествоПриемников 	КАК КоличествоПриемников,
	|	ОписаниеУзлов.КоличествоИсточников 	КАК КоличествоИсточников
	|ПОМЕСТИТЬ ОписаниеУзловПодграфов
	|ИЗ
	|	ВТНовыеНомераУзлов КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОписаниеУзлов КАК ОписаниеУзлов
	|		ПО Т.НомерУзла = ОписаниеУзлов.НомерУзла
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерУзла
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	Т.НомерПодграфа 					КАК НомерПодграфа,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Т.НомерВолны) 	КАК КоличествоВолн,
	|	СУММА(Т.КоличествоИсточников) 		КАК КоличествоСвязей,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Т.НомерУзла) 	КАК КоличествоУзлов,
	|	МИНИМУМ(Т.НомерУзла) 				КАК МинимальныйНомерУзла,
	|	МАКСИМУМ(Т.НомерУзла) 				КАК МаксимальныйНомерУзла,
	|	МАКСИМУМ(Т.КоличествоПриемников) 	КАК МаксимумПриемниковУзла,
	|	МАКСИМУМ(Т.КоличествоИсточников) 	КАК МаксимумИсточниковУзла
	|ПОМЕСТИТЬ ОписаниеПодграфов
	|ИЗ
	|	ОписаниеУзловПодграфов КАК Т
	|СГРУППИРОВАТЬ ПО
	|	Т.НомерПодграфа
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерПодграфа";
	
	Запрос.Выполнить(); // создание ОписаниеУзловПодграфов и ОписаниеПодграфов
	
	// Выведем в журнал регистрации подробное описание графа.
	
	ОписаниеГрафа = НСтр("ru='Описание цепочек (графа) для расчета:
	|	узлов - %01; дуг - %02; макс. исходящих дуг - %03; макс. входящих дуг - %04;
	|	содержит несвязанных подграфов - %05, в т.ч. вырожденных (из одного узла) - %06;
	|	макс. узлов в одном подграфе - %07; макс. дуг в одном подграфе - %08;
	|	выполнено итераций поиска подграфов - %09, итераций нумерации - %10, в т.ч. из-за циклов - %11'");
	
	Запрос.УстановитьПараметр("КоличествоИтерацийПоискаПодграфов", 	   КоличествоИтерацийПоискаПодграфов);
	Запрос.УстановитьПараметр("КоличествоИтерацийНумерации", 	   	   НомерВолны - 1);
	Запрос.УстановитьПараметр("КоличествоИтерацийНумерацииИзЗаЦиклов", ?(ЗначениеЗаполнено(НомерВолныДляЦиклов), НомерВолны - НомерВолныДляЦиклов, 0));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 						 КАК НомерПараметра,
	|	СУММА(Т.КоличествоУзлов) КАК ЗначениеПараметра
	|ИЗ
	|	ОписаниеПодграфов КАК Т
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	СУММА(Т.КоличествоСвязей)
	|ИЗ
	|	ОписаниеПодграфов КАК Т
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3,
	|	МАКСИМУМ(Т.МаксимумПриемниковУзла)
	|ИЗ
	|	ОписаниеПодграфов КАК Т
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4,
	|	МАКСИМУМ(Т.МаксимумИсточниковУзла)
	|ИЗ
	|	ОписаниеПодграфов КАК Т
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	5,
	|	КОЛИЧЕСТВО(*)
	|ИЗ
	|	ОписаниеПодграфов КАК Т
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	6,
	|	КОЛИЧЕСТВО(*)
	|ИЗ
	|	ОписаниеПодграфов КАК Т
	|ГДЕ
	|	Т.КоличествоУзлов = 1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	7,
	|	МАКСИМУМ(Т.КоличествоУзлов)
	|ИЗ
	|	ОписаниеПодграфов КАК Т
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	8,
	|	МАКСИМУМ(Т.КоличествоСвязей)
	|ИЗ
	|	ОписаниеПодграфов КАК Т
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	9,
	|	&КоличествоИтерацийПоискаПодграфов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	10,
	|	&КоличествоИтерацийНумерации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	11,
	|	&КоличествоИтерацийНумерацииИзЗаЦиклов
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерПараметра";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОписаниеГрафа = СтрЗаменить(
			ОписаниеГрафа,
			"%" + Формат(Выборка.НомерПараметра, "ЧЦ=2; ЧВН="),
			СокрЛП(Выборка.ЗначениеПараметра));
	КонецЦикла;
	
	ОкончаниеЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	ОписаниеГрафа = ОписаниеГрафа + "
		|	" + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Время подготовки графа: %1 сек.'"),
					Формат((ОкончаниеЗамера - НачалоЗамера)/1000, "ЧГ="));
	
	ИмяСобытия = НСтр("ru = 'Партионный учет.ИнформацияОЦепочках'", ОбщегоНазначения.КодОсновногоЯзыка());
	ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Информация,,, ОписаниеГрафа);
	
	#КонецОбласти
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета,
		"ВТПорядокУзлов, ВТУзлыПодграфов, ВТСвязиУзлов, ВТПодграфы, ВТОписаниеУзлов, ВТНовыеНомераУзлов");
	
КонецПроцедуры


Функция ЦепочкиДвиженийПоИндексу(МВТ, НачальныйИндекс, КонечныйИндекс, ЦепочкиДвижений, СтрокиЦепочек)
	
	Колонки = СтрокиЦепочек.Колонки;
	Если Колонки.Количество() = 0 Тогда
		Колонки.Добавить("Источники", 	   Новый ОписаниеТипов("Массив"));
		Колонки.Добавить("Приемники", 	   Новый ОписаниеТипов("Массив"));
		Колонки.Добавить("ПройденныйПуть", Новый ОписаниеТипов("Соответствие"));
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.УстановитьПараметр("НачальныйИндекс", НачальныйИндекс);
	Запрос.УстановитьПараметр("КонечныйИндекс",  КонечныйИндекс);
	
	ТаблицыСвязейУзлов = Новый Структура;
	ТаблицыСвязейУзлов.Вставить("Источники", "Источник");
	ТаблицыСвязейУзлов.Вставить("Приемники", "Приемник");
	
	ШаблонЗапроса =
	"ВЫБРАТЬ
	|	ДД.Ключ КАК Ключ,
	|	ДД.%2 КАК %2
	|ИЗ
	|	%1 КАК ДД
	|ГДЕ
	|	ДД.Ключ >= &НачальныйИндекс
	|	И ДД.Ключ <= &КонечныйИндекс
	|	И ДД.Ключ <> ДД.%2
	|	
	|УПОРЯДОЧИТЬ ПО
	|	Ключ,
	|	ДД.Порядок,
	|	%2";
	
	Для Каждого ОписаниеТаблицы Из ТаблицыСвязейУзлов Цикл
		
		Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонЗапроса,
			ОписаниеТаблицы.Ключ,
			ОписаниеТаблицы.Значение);
		
		ТекущийКлюч = -1;
		Строка = Неопределено;
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Ключ = Выборка.Ключ;
			
			Если ТекущийКлюч <> Ключ Тогда
				
				Если ТекущийКлюч <> -1 Тогда
					ЦепочкиДвижений.Вставить(ТекущийКлюч, Строка);
				КонецЕсли;
				
				ТекущийКлюч = Ключ;
				
				Строка = ЦепочкиДвижений[ТекущийКлюч];
				Если Строка = Неопределено Тогда
					Строка = СтрокиЦепочек.Добавить();
				КонецЕсли;
				
			КонецЕсли;
			
			Строка[ОписаниеТаблицы.Ключ].Добавить(Выборка[ОписаниеТаблицы.Значение]);
			
		КонецЦикла;
		
		Если ТекущийКлюч <> -1 Тогда
			ЦепочкиДвижений.Вставить(ТекущийКлюч, Строка);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Выборка.Количество(); // размер выборки из источников и приемников одинаковый
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Расчет партий по цепочкам движений и выборки из результата запроса

Функция ВыборкаДанныеПоИндексу(МВТ, НачальныйИндекс, КонечныйИндекс)
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	Данные КАК ДД
	|ГДЕ
	|	ДД.К >= &НачальныйИндекс
	|	И ДД.К <= &КонечныйИндекс
	|УПОРЯДОЧИТЬ ПО
	|	ДД.К ВОЗР
	|");
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.УстановитьПараметр("НачальныйИндекс", НачальныйИндекс);
	Запрос.УстановитьПараметр("КонечныйИндекс", КонечныйИндекс);
	Выборка = Запрос.Выполнить().Выбрать();
	Возврат Выборка;
КонецФункции

Процедура РассчитатьПартииПоЦепочкам(ОписаниеДвижений, ДанныеДляРасчета, РасчетныеПартии, Регистраторы, Тест)
	
	Приходы = Новый Соответствие; // буфер копий партий для покрытия расходов
	Расходы = Новый Соответствие; // буфер не рассчитанных партий для расчета на следующих итерациях
	СтрокиПриходов = РасчетныеПартии.СкопироватьКолонки();
	СтрокиРасходов = РасчетныеПартии.СкопироватьКолонки();
	ПройденныйПуть = Новый Соответствие; // используется для прерывания циклов
	ИндексыРасходов = Новый Массив;
	ЦепочкиДвижений = Новый Соответствие;
	СтрокиЦепочек = Новый ТаблицаЗначений;
	
	ЗаполнятьАналитикуПартий = ОписаниеДвижений.Свойство("ЗаполнятьАналитикуПартий");
	
	Данные = Новый Структура;
	Данные.Вставить("РасчетныеПартии", РасчетныеПартии);
	Данные.Вставить("ЦепочкиДвижений", ЦепочкиДвижений);
	Данные.Вставить("СтрокиЦепочек", СтрокиЦепочек);
	Данные.Вставить("Приходы", Приходы);
	Данные.Вставить("Расходы", Расходы);
	Данные.Вставить("СтрокиПриходов", СтрокиПриходов);
	Данные.Вставить("СтрокиРасходов", СтрокиРасходов);
	Данные.Вставить("ИндексыРасходов", ИндексыРасходов);
	Данные.Вставить("ПройденныйПуть", ПройденныйПуть);
	
	КоличествоЗаписей = КоличествоЗаписей(ДанныеДляРасчета);
	КоличествоСвязей  = КоличествоСвязейМеждуЗаписями(ДанныеДляРасчета);

	Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'К расчету: строк - %1, связей - %2'", ОбщегоНазначения.КодОсновногоЯзыка()),
		СокрЛП(КоличествоЗаписей),
		СокрЛП(КоличествоСвязей));
	ИмяСобытия = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Партионный учет.%1'", ОбщегоНазначения.КодОсновногоЯзыка()),
		ОписаниеДвижений.Контекст);
	ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Информация,,, Комментарий);
	
	ИндексСтроки = -1;
	КонечнаяЦепочка = -1;
	КоличествоДуг = 0;
	
	Пока ИндексСтроки <= КоличествоЗаписей Цикл
		
		ИндексСтроки = ИндексСтроки + 1;
		Если ИндексСтроки > КонечнаяЦепочка Тогда
			НачальнаяЦепочка = ИндексСтроки;
			КонечнаяЦепочка = КонечнаяЦепочка + 10000;
			Выборка = ВыборкаДанныеПоИндексу(ДанныеДляРасчета, НачальнаяЦепочка, КонечнаяЦепочка);
			КоличествоДуг = ЦепочкиДвиженийПоИндексу(ДанныеДляРасчета, НачальнаяЦепочка, КонечнаяЦепочка, ЦепочкиДвижений, СтрокиЦепочек);
		КонецЕсли;
		
		Если Не Выборка.Следующий() Тогда
			Прервать;
		КонецЕсли;
		
		ЦепочкаДвижений = ЦепочкиДвижений[ИндексСтроки];
		Если ЦепочкаДвижений = Неопределено Тогда
			РасчетнаяПартия = РасчетныеПартии.Добавить();
			ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Выборка);
			Если ЗаполнятьАналитикуПартий И Выборка.ТипЗаписи = "Партия" И Выборка.РасчетЗавершен Тогда
				РасчетнаяПартия.АналитикаУчетаПартий =
					АналитикаУчетаПартий(Выборка.АналитикаУчетаПартий, Выборка.НалогообложениеПартии, Выборка.НалогообложениеНДС);
			КонецЕсли;
		ИначеЕсли Выборка.РасчетЗавершен Тогда
			РасчетнаяПартия = РасчетныеПартии.Добавить();
			ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Выборка);
			Если ЗаполнятьАналитикуПартий И Выборка.ТипЗаписи = "Партия" Тогда
				РасчетнаяПартия.АналитикаУчетаПартий =
					АналитикаУчетаПартий(Выборка.АналитикаУчетаПартий, Выборка.НалогообложениеПартии, Выборка.НалогообложениеНДС);
				Если ЗначениеЗаполнено(Выборка.НалогообложениеПартии) И ЗначениеЗаполнено(Выборка.НалогообложениеНДС) Тогда
					РасчетнаяПартия.НалогообложениеПартии = Выборка.НалогообложениеНДС;
				КонецЕсли;
			КонецЕсли;
			Если ЦепочкаДвижений.Приемники.Количество() > 0 Тогда
				Приход = СтрокиПриходов.Добавить();
				ЗаполнитьЗначенияСвойств(Приход, РасчетнаяПартия);
				ИнвертироватьПоказатели(Приход, ОписаниеДвижений.Показатели, ("Сторно" = Приход.ТипЗаписи ИЛИ "ПартияСторно" = Приход.ТипЗаписи ИЛИ "ОстатокСторно" = Приход.ТипЗаписи));
				Приходы.Вставить(ИндексСтроки, Новый Массив);
				Приходы[ИндексСтроки].Добавить(Приход);
			КонецЕсли;
			ЦепочкиДвижений.Удалить(ИндексСтроки);
			СтрокиЦепочек.Удалить(ЦепочкаДвижений);
		Иначе
			Расход = СтрокиРасходов.Добавить();
			ЗаполнитьЗначенияСвойств(Расход, Выборка);
			Расходы.Вставить(ИндексСтроки, Расход);
			ИндексыРасходов.Добавить(ИндексСтроки);
		КонецЕсли;
		
		// Запишем порцию движений в регистр.
		Если Не Тест И РасчетныеПартии.Количество() > 100000 Тогда
			Если ОписаниеДвижений.Свойство("ПоляГруппировки") Тогда
				РасчетныеПартии.Свернуть(ОписаниеДвижений.ПоляГруппировки, ОписаниеДвижений.ПоляСуммирования);
			КонецЕсли;
			УдалитьНезаписываемыеСтроки(ОписаниеДвижений.Контекст, РасчетныеПартии);
			ЗаписатьРасчетныеПартии(РегистрыНакопления[ОписаниеДвижений.ИмяРегистра], РасчетныеПартии, Регистраторы);
			РасчетныеПартии.Очистить();
		КонецЕсли;
		
		Если ИндексСтроки > 0 И ИндексСтроки % 10000 = 0 Тогда
			Данные.Вставить("ИндексСтроки", ИндексСтроки);
			Если Приходы.Количество() > 0 Тогда // есть записи по которым расчет завершен
				Пока ИндексыРасходов.Количество() > 0 Цикл
					ИндексРасхода = ИндексыРасходов.Получить(0);
					ИндексыРасходов.Удалить(0);
					Если Расходы[ИндексРасхода] = Неопределено Тогда
						Продолжить; // строка к обсчету НЕ зарегистрирована
					КонецЕсли;
					БазисРасхода = Расходы[ИндексРасхода][ОписаниеДвижений.БазисРасхода];
					ПройденныйПуть.Очистить();
					РассчитатьПартиюПоЦепочкеИзВыборки(ОписаниеДвижений, Данные, ИндексРасхода, Регистраторы, Тест);
					
					Если Расходы[ИндексРасхода] <> Неопределено
					 И Расходы[ИндексРасхода][ОписаниеДвижений.БазисРасхода] > 0
					 И Расходы[ИндексРасхода][ОписаниеДвижений.БазисРасхода] <> БазисРасхода Тогда
						ИндексыРасходов.Добавить(ИндексРасхода);
					КонецЕсли;
					
					// Запишем порцию движений в регистр.
					Если Не Тест И РасчетныеПартии.Количество() > 100000 Тогда
						Если ОписаниеДвижений.Свойство("ПоляГруппировки") Тогда
							РасчетныеПартии.Свернуть(ОписаниеДвижений.ПоляГруппировки, ОписаниеДвижений.ПоляСуммирования);
						КонецЕсли;
						УдалитьНезаписываемыеСтроки(ОписаниеДвижений.Контекст, РасчетныеПартии);
						ЗаписатьРасчетныеПартии(РегистрыНакопления[ОписаниеДвижений.ИмяРегистра], РасчетныеПартии, Регистраторы);
						РасчетныеПартии.Очистить();
					КонецЕсли;
				КонецЦикла;
				
				Для Каждого Элемент Из Расходы Цикл
					ИндексРасхода = Элемент.Ключ;
					ИндексыРасходов.Добавить(ИндексРасхода);
				КонецЦикла;
				Список = Новый СписокЗначений;
				Список.ЗагрузитьЗначения(ИндексыРасходов);
				Список.СортироватьПоЗначению();
				ИндексыРасходов = Список.ВыгрузитьЗначения();
			КонецЕсли;
			
			Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Рассчитано: %1 стр., %2 связей'", ОбщегоНазначения.КодОсновногоЯзыка()),
				ИндексСтроки,
				КоличествоДуг);
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Партионный учет.'",	ОбщегоНазначения.КодОсновногоЯзыка()) + ОписаниеДвижений.Контекст,
				УровеньЖурналаРегистрации.Информация,,, Комментарий);
		КонецЕсли;
	КонецЦикла;
	
	Если ИндексСтроки > КонечнаяЦепочка Тогда
		НачальнаяЦепочка = ИндексСтроки;
		КонечнаяЦепочка = КонечнаяЦепочка + 10000;
		КоличествоДуг = ЦепочкиДвиженийПоИндексу(ДанныеДляРасчета, НачальнаяЦепочка, КонечнаяЦепочка, ЦепочкиДвижений, СтрокиЦепочек);
	КонецЕсли;
	
	Данные.Вставить("ИндексСтроки", ИндексСтроки);
	Пока ИндексыРасходов.Количество() > 0 Цикл
		ИндексРасхода = ИндексыРасходов.Получить(0);
		ИндексыРасходов.Удалить(0);
		Если Расходы[ИндексРасхода] = Неопределено Тогда
			Продолжить; // строка к обсчету НЕ зарегистрирована
		КонецЕсли;
		БазисРасхода = Расходы[ИндексРасхода][ОписаниеДвижений.БазисРасхода];
		ПройденныйПуть.Очистить();
		РассчитатьПартиюПоЦепочкеИзВыборки(ОписаниеДвижений, Данные, ИндексРасхода, Регистраторы, Тест);
		
		Если Расходы[ИндексРасхода] <> Неопределено
		 И Расходы[ИндексРасхода][ОписаниеДвижений.БазисРасхода] > 0
		 И Расходы[ИндексРасхода][ОписаниеДвижений.БазисРасхода] <> БазисРасхода Тогда
			ИндексыРасходов.Добавить(ИндексРасхода);
		КонецЕсли;
		
		// Запишем порцию движений в регистр.
		Если Не Тест И РасчетныеПартии.Количество() > 100000 Тогда
			Если ОписаниеДвижений.Свойство("ПоляГруппировки") Тогда
				РасчетныеПартии.Свернуть(ОписаниеДвижений.ПоляГруппировки, ОписаниеДвижений.ПоляСуммирования);
			КонецЕсли;
			УдалитьНезаписываемыеСтроки(ОписаниеДвижений.Контекст, РасчетныеПартии);
			ЗаписатьРасчетныеПартии(РегистрыНакопления[ОписаниеДвижений.ИмяРегистра], РасчетныеПартии, Регистраторы);
			РасчетныеПартии.Очистить();
		КонецЕсли;
	КонецЦикла;

	Для Каждого Строка Из Расходы Цикл
		РасчетнаяПартия = РасчетныеПартии.Добавить();
		ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Строка.Значение);
		Если Не Тест И РасчетныеПартии.Количество() > 100000 Тогда
			Если ОписаниеДвижений.Свойство("ПоляГруппировки") Тогда
				РасчетныеПартии.Свернуть(ОписаниеДвижений.ПоляГруппировки, ОписаниеДвижений.ПоляСуммирования);
			КонецЕсли;
			УдалитьНезаписываемыеСтроки(ОписаниеДвижений.Контекст, РасчетныеПартии);
			ЗаписатьРасчетныеПартии(РегистрыНакопления[ОписаниеДвижений.ИмяРегистра], РасчетныеПартии, Регистраторы);
			РасчетныеПартии.Очистить();
		КонецЕсли;
	КонецЦикла;
	
	Если ОписаниеДвижений.Свойство("ПоляГруппировки") Тогда
		РасчетныеПартии.Свернуть(ОписаниеДвижений.ПоляГруппировки, ОписаниеДвижений.ПоляСуммирования);
	КонецЕсли;
	
	Если КоличествоЗаписей % 10000 <> 0 Тогда
		Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Рассчитано: %1 стр., %2 связей'", ОбщегоНазначения.КодОсновногоЯзыка()),
			ИндексСтроки,
			КоличествоДуг);
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Партионный учет.'",	ОбщегоНазначения.КодОсновногоЯзыка()) + ОписаниеДвижений.Контекст,
			УровеньЖурналаРегистрации.Информация,,, Комментарий);
	КонецЕсли;
	
	Выборка = Неопределено;
	Приходы.Очистить();
	Расходы.Очистить();
	СтрокиПриходов.Очистить();
	СтрокиРасходов.Очистить();
	Если Не Тест Тогда
		ЦепочкиДвижений.Очистить();
		СтрокиЦепочек.Очистить();
	КонецЕсли;
	
КонецПроцедуры


Функция РассчитатьПартиюПоЦепочкеИзВыборки(ОписаниеДвижений, Данные, ИндексРасхода, Регистраторы, Тест)
	
	РасчетныеПартии = Данные.РасчетныеПартии;
	ПройденныйПуть = Данные.ПройденныйПуть;
	ЦепочкиДвижений = Данные.ЦепочкиДвижений;
	СтрокиЦепочек = Данные.СтрокиЦепочек;
	ЦепочкаРасхода = ЦепочкиДвижений[ИндексРасхода];
	
	Если ЦепочкаРасхода = Неопределено Тогда
		Возврат "НеТребуется";
	КонецЕсли;
	
	Источники = ЦепочкаРасхода.Источники;
	ЕстьЗацикливание = Ложь;
	Если ИндексРасхода = ПройденныйПуть[ИндексРасхода] Или (Источники.Количество() = 0) Тогда
		ЕстьЗацикливание = Истина;
	КонецЕсли;
	
	ИндексСтроки = Данные.ИндексСтроки;
	Для Каждого ИндексПрихода Из Источники Цикл
		Если ИндексПрихода > ИндексСтроки Тогда
			Возврат "НетДанных"; // еще нет всех строк для распределения
		КонецЕсли;
	КонецЦикла;
	
	ПройденныйПуть.Вставить(ИндексРасхода, ИндексРасхода);
	
	Приходы = Данные.Приходы;
	Расходы = Данные.Расходы;
	СтрокиПриходов = Данные.СтрокиПриходов;
	НовыеПриходы = Неопределено;
	СтрокиРасходов = Данные.СтрокиРасходов;
	
	Расход = Расходы[ИндексРасхода];
	ЭтоСторно = ("Сторно" = Расход.ТипЗаписи ИЛИ "ВозвратСторно" = Расход.ТипЗаписи ИЛИ "СторноПрошлое" = Расход.ТипЗаписи );
	
	БазисПрихода = 0;
	Если ОписаниеДвижений.Свойство("ПоляГруппировки")
	 ИЛИ ЭтоСторно Тогда
		Для Каждого ИндексИсточника Из Источники Цикл
			Если НЕ ЕстьЗацикливание
			 И Данные.Приходы[ИндексИсточника] = Неопределено
			 И Данные.Расходы[ИндексИсточника] <> Неопределено Тогда // данный источник еще не рассчитан
				Результат = РассчитатьПартиюПоЦепочкеИзВыборки(ОписаниеДвижений, Данные, ИндексИсточника, Регистраторы, Тест);
				Если Результат = "НетДанных" Тогда
					Возврат Результат;
				ИначеЕсли Результат = "Зацикливание" Тогда
					БазисПрихода = БазисПрихода + Расходы[ИндексИсточника][ОписаниеДвижений.БазисПрихода];
				КонецЕсли;
				Если Данные.Расходы[ИндексРасхода] = Неопределено Тогда
					Возврат "НеТребуется";
				КонецЕсли;
			ИначеЕсли ЕстьЗацикливание И Данные.Расходы[ИндексИсточника] <> Неопределено Тогда
				БазисПрихода = БазисПрихода + Расходы[ИндексИсточника][ОписаниеДвижений.БазисПрихода];
			КонецЕсли;
		КонецЦикла;
		Если ЭтоСторно Тогда
			СортироватьИсточники(Источники, ОписаниеДвижений.ПолеПорядка, Приходы);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОписаниеДвижений.ПоляСортировки) Тогда
		ЗначенияПолей = Новый Структура(ОписаниеДвижений.ПоляСортировки);
		ЗаполнитьЗначенияСвойств(ЗначенияПолей, Расход);
		ЗаполненыПоляСортировки = Ложь;
		Для Каждого ПолеСортировки Из ЗначенияПолей Цикл
			Если ЗначениеЗаполнено(ПолеСортировки.Значение) Тогда
				ЗаполненыПоляСортировки = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		ТребуетсяСортировка = Истина;
		Если ЗначениеЗаполнено(ОписаниеДвижений.СортировкаПоУсловию) Тогда
			ТребуетсяСортировка = ТребуетсяСортировка(ОписаниеДвижений.Контекст, Расход); 
		КонецЕсли;
		Если ЗаполненыПоляСортировки И ТребуетсяСортировка Тогда
			СортироватьИсточникиПоЗначениямПолей(Источники, ЗначенияПолей, Приходы);
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоСторно И ОписаниеДвижений.Контекст = "ПартииПроизводственныхЗатрат" Тогда
		// Сортировка для списания сторно записей в первую очередь.
		СортироватьИсточники(Источники, "Приоритет", Приходы);
	КонецЕсли;
	
	Счетчик = 0;
	ВГраница = Источники.ВГраница();
	РазрывЦикла = Ложь;
	
	УменьшатьБазисРасхода = ОписаниеДвижений.Свойство("УменьшатьБазис");
	
	ИнвертироватьПоказатели(Расход, ОписаниеДвижений.Показатели, ЭтоСторно);
	Пока Счетчик <= ВГраница Цикл
		Если ОписаниеДвижений.Контекст = "ПартииПроизводственныхЗатрат" Тогда
			ИндексИсточника = Источники[Счетчик];
		Иначе
			ИндексИсточника = Источники[?(ЭтоСторно, ВГраница - Счетчик, Счетчик)];
		КонецЕсли;
		Счетчик = Счетчик + 1;
		
		Если ОписаниеДвижений.Контекст = "ПартииТоваровОрганизаций"
		 И Расход.СохранятьРегл
		 И ИндексИсточника = ПройденныйПуть[ИндексИсточника] Тогда
			РазрывЦикла = Истина;
			Продолжить; // данный источник еще рассчитан не до конца
		КонецЕсли;
		
		МассивПриходов = Приходы[ИндексИсточника];
		Если Неопределено = МассивПриходов Тогда
			Если Неопределено = Расходы[ИндексИсточника] Тогда
				Продолжить;
			КонецЕсли;
			Источник = Расходы[ИндексИсточника];
			Если НЕ ЕстьЗацикливание И НЕ Источник.РасчетЗавершен Тогда
				РассчитатьПартиюПоЦепочкеИзВыборки(ОписаниеДвижений, Данные, ИндексИсточника, Регистраторы, Тест);
				ВГраница = Источники.ВГраница();
				Если Данные.Расходы[ИндексРасхода] = Неопределено Тогда
					Возврат "НеТребуется";
				КонецЕсли;
				МассивПриходов = Приходы[ИндексИсточника];
				Если Неопределено = МассивПриходов Тогда
					Продолжить;
				КонецЕсли;
			Иначе
				МассивПриходов = Приходы[ИндексИсточника];
				Если Неопределено = МассивПриходов Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ЭтоСторно Тогда
			СортироватьПриходыПоЛИФО(ОписаниеДвижений.ПолеПорядка, МассивПриходов);
		КонецЕсли;
		
		УменьшениеБазисаРасхода = 0;
		
		ПриходыКУдалению = Новый Массив;
		Для Каждого Приход Из МассивПриходов Цикл
			
			Если УменьшатьБазисРасхода
			 И УменьшениеБазисаРасхода = 0
			 И Расход.ТипЗаписи = "Сторно"
			 И Приход.ТипЗаписи <> "Прошлое" Тогда
				УменьшениеБазисаРасхода = Мин(Расход[ОписаниеДвижений.БазисРасхода], Приход[ОписаниеДвижений.БазисРасхода]);
			КонецЕсли;
			
			РасчетнаяПартия = РасчетныеПартии.Добавить();
			ЗаполнитьРасчетнуюПартию(ОписаниеДвижений.Контекст, РасчетнаяПартия, Расход, Приход);
			ИнвертироватьПоказатели(РасчетнаяПартия, ОписаниеДвижений.Показатели, ЭтоСторно);
			
			Если Приход[ОписаниеДвижений.БазисПрихода] <= 0 Тогда
				ПриходыКУдалению.Добавить(Приход);
			КонецЕсли;
			
			Если РасчетнаяПартия.РасчетЗавершен Тогда
				Если ЦепочкаРасхода.Приемники.Количество() > 0 Тогда
					Если ОписаниеДвижений.Свойство("ПоляГруппировки") Тогда
						Если НовыеПриходы = Неопределено Тогда
							НовыеПриходы = СтрокиПриходов.СкопироватьКолонки();
						КонецЕсли;
						НовыйПриход = НовыеПриходы.Добавить();
						ЗаполнитьЗначенияСвойств(НовыйПриход, РасчетнаяПартия);
						ИнвертироватьПоказатели(НовыйПриход, ОписаниеДвижений.Показатели,
							("Сторно" = НовыйПриход.ТипЗаписи ИЛИ "ВозвратСторно" = НовыйПриход.ТипЗаписи ИЛИ "СторноПрошлое" = НовыйПриход.ТипЗаписи));
					Иначе
						НовыйПриход = СтрокиПриходов.Добавить();
						ЗаполнитьЗначенияСвойств(НовыйПриход, РасчетнаяПартия);
						ИнвертироватьПоказатели(НовыйПриход, ОписаниеДвижений.Показатели,
							("Сторно" = НовыйПриход.ТипЗаписи ИЛИ "ВозвратСторно" = НовыйПриход.ТипЗаписи ИЛИ "СторноПрошлое" = НовыйПриход.ТипЗаписи));
						Если Приходы[ИндексРасхода] = Неопределено Тогда
							Приходы.Вставить(ИндексРасхода, Новый Массив);
						КонецЕсли;
						Приходы[ИндексРасхода].Добавить(НовыйПриход);
					КонецЕсли;
				КонецЕсли;
			Иначе
				РасчетныеПартии.Удалить(РасчетнаяПартия);
			КонецЕсли;
						
			// Запишем порцию движений в регистр.
			Если Не Тест И РасчетныеПартии.Количество() > 100000 Тогда
				Если ОписаниеДвижений.Свойство("ПоляГруппировки") Тогда
					РасчетныеПартии.Свернуть(ОписаниеДвижений.ПоляГруппировки, ОписаниеДвижений.ПоляСуммирования);
				КонецЕсли;
				УдалитьНезаписываемыеСтроки(ОписаниеДвижений.Контекст, РасчетныеПартии);
				ЗаписатьРасчетныеПартии(РегистрыНакопления[ОписаниеДвижений.ИмяРегистра], РасчетныеПартии, Регистраторы);
				РасчетныеПартии.Очистить();
			КонецЕсли;
			Если Расход[ОписаниеДвижений.БазисРасхода] <= 0 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если УменьшатьБазисРасхода
		 И УменьшениеБазисаРасхода <> 0 Тогда
			Расход[ОписаниеДвижений.БазисРасхода] = Расход[ОписаниеДвижений.БазисРасхода] - УменьшениеБазисаРасхода;
		КонецЕсли;
		
		Для Каждого Приход Из ПриходыКУдалению Цикл
			Индекс = МассивПриходов.Найти(Приход);
			Если Индекс <> Неопределено Тогда
				МассивПриходов.Удалить(Индекс);
			КонецЕсли;
			СтрокиПриходов.Удалить(Приход);
		КонецЦикла;
		Если МассивПриходов.Количество() = 0 Тогда
			Приходы.Удалить(ИндексИсточника);
			Если Расходы[ИндексИсточника] = Неопределено Тогда
				ЦепочкаПрихода = ЦепочкиДвижений[ИндексИсточника];
				Если ЦепочкаПрихода <> Неопределено Тогда
					ЦепочкиДвижений.Удалить(ИндексИсточника);
					СтрокиЦепочек.Удалить(ЦепочкаПрихода);
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ПриходыКУдалению.Количество() > 0 Тогда
			Приходы.Вставить(ИндексИсточника, МассивПриходов);
		КонецЕсли;
		Если Расход[ОписаниеДвижений.БазисРасхода] <= 0 Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	БазисРасхода = 0;
	Если ОписаниеДвижений.Свойство("ПоляГруппировки")
	 И НовыеПриходы <> Неопределено
	 И НовыеПриходы.Количество() > 0 Тогда
		НовыеПриходы.Свернуть(ОписаниеДвижений.ПоляГруппировки, ОписаниеДвижений.ПоляСуммирования);
		Если Приходы[ИндексРасхода] = Неопределено Тогда
			Приходы.Вставить(ИндексРасхода, Новый Массив);
		КонецЕсли;
		Для Каждого Строка Из НовыеПриходы Цикл
			НовыйПриход = СтрокиПриходов.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйПриход, Строка);
			Если НовыйПриход[ОписаниеДвижений.БазисРасхода] = 0 Тогда
				НовыйПриход[ОписаниеДвижений.БазисРасхода] = Расход[ОписаниеДвижений.БазисРасхода];
			КонецЕсли;
			Приходы[ИндексРасхода].Добавить(НовыйПриход);
			Если БазисРасхода < НовыйПриход[ОписаниеДвижений.БазисРасхода] Тогда
				БазисРасхода = НовыйПриход[ОписаниеДвижений.БазисРасхода];
			КонецЕсли;
		КонецЦикла;
		НовыеПриходы = Неопределено;
	КонецЕсли;
	Если ОписаниеДвижений.Свойство("ПоляГруппировки") Тогда
		Если Расход.ТипЗаписи = "Перемещение" И БазисПрихода <> 0 Тогда
			Расход[ОписаниеДвижений.БазисРасхода] = БазисПрихода;
		ИначеЕсли БазисРасхода > 0 И Расход[ОписаниеДвижений.БазисРасхода] - БазисРасхода > 0 Тогда
			Расход[ОписаниеДвижений.БазисРасхода] = Расход[ОписаниеДвижений.БазисРасхода] - БазисРасхода;	
		Иначе
			Расход[ОписаниеДвижений.БазисРасхода] = 0;
		КонецЕсли;
	КонецЕсли;
	
	Если Расход[ОписаниеДвижений.БазисРасхода] > 0 Тогда
		ИнвертироватьПоказатели(Расход, ОписаниеДвижений.Показатели,
			("Сторно" = Расход.ТипЗаписи ИЛИ "ВозвратСторно" = Расход.ТипЗаписи ИЛИ "СторноПрошлое" = Расход.ТипЗаписи));
		Если ОписаниеДвижений.Контекст = "ПартииТоваровОрганизаций"
		 И РазрывЦикла
		 И Расход.СохранятьРегл Тогда
		 	Расход.РасчетЗавершен = Истина;
		 	НоваяПартия = РасчетныеПартии.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяПартия, Расход);
			Если ЦепочкаРасхода <> Неопределено
			 И ЦепочкаРасхода.Приемники.Количество() > 0 Тогда
				Приход = СтрокиПриходов.Добавить();
				ЗаполнитьЗначенияСвойств(Приход, НоваяПартия);
				ИнвертироватьПоказатели(Приход, ОписаниеДвижений.Показатели, ("Сторно" = Приход.ТипЗаписи));
				Если Приходы[ИндексРасхода] = Неопределено Тогда
					Приходы.Вставить(ИндексРасхода, Новый Массив);
				КонецЕсли;
				Приходы[ИндексРасхода].Добавить(Приход);
			КонецЕсли;
			Расходы.Удалить(ИндексРасхода);
			СтрокиРасходов.Удалить(Расход);
		КонецЕсли;
	Иначе
		Расходы.Удалить(ИндексРасхода);
		ЦепочкиДвижений.Удалить(ИндексРасхода);
		СтрокиРасходов.Удалить(Расход);
		СтрокиЦепочек.Удалить(ЦепочкаРасхода);
	КонецЕсли;
	
	Возврат "Выполнено";
	
КонецФункции

#КонецОбласти


#Область ОчисткаДвиженийРегистров

Процедура ОчиститьРегистрНаСервере(Регистраторы, ИмяРегистра, РегистраторыССохраняемымиДвижениями = Неопределено)
	
	Если ТестированиеБезЗаписи() Тогда
		Возврат;
	КонецЕсли;
	
	НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	Для Каждого Элемент Из Регистраторы Цикл 
		
		Если РегистраторыССохраняемымиДвижениями <> Неопределено
		 И РегистраторыССохраняемымиДвижениями.Получить(Элемент.Ключ) <> Неопределено Тогда
			// При восстановлении сохраненных движений надо удалять устаревшие движения
			РегистраторыССохраняемымиДвижениями.Вставить(Элемент.Ключ, Истина);
			Продолжить;
		КонецЕсли;

		Движения = РегистрыНакопления[ИмяРегистра].СоздатьНаборЗаписей();
		Движения.Отбор.Регистратор.Установить(Элемент.Ключ);
		Движения.Записать();
		
	КонецЦикла;
	
	Замер = ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЗамера;
	Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Очистка устаревших движений: %1 док., длительность: %2 сек.'"),
		СокрЛП(Регистраторы.Количество()),
		СокрЛП(Замер/1000));
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Партионный учет.ОчиститьРегистрНаСервере'", ОбщегоНазначения.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Информация,,, Комментарий);
	
КонецПроцедуры

#КонецОбласти

// Вспомогательные универсальные методы
#Область СлужебныеПроцедурыИФункции

Функция ПоляЗаписейРавны(Запись1, Запись2, ПереченьКлючей)
	Если Неопределено = Запись1 Или Неопределено = Запись2 Тогда
		Возврат (Запись1 = Запись2);
	КонецЕсли;
	
	Для Каждого Поле Из Новый Структура(ПереченьКлючей) Цикл
		Если Запись1[Поле.Ключ] <> Запись2[Поле.Ключ] Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
КонецФункции

Функция КопияЗаписиСтруктурой(Запись, ПереченьПолей)
	КопияСтрокиСтруктурой = Новый Структура(ПереченьПолей);
	ЗаполнитьЗначенияСвойств(КопияСтрокиСтруктурой, Запись);
	Возврат КопияСтрокиСтруктурой;
КонецФункции

Процедура ИнвертироватьПоказатели(Запись, ПереченьПоказателей, Инвертировать)
	Если Не Инвертировать Тогда
		Возврат;
	КонецЕсли;
	Для Каждого Поле Из Новый Структура(ПереченьПоказателей) Цикл
		Запись[Поле.Ключ] = -Запись[Поле.Ключ];
	КонецЦикла;
КонецПроцедуры

Функция СкопироватьКолонки(КолонкиИсточника, КолонкиПриемника)
	ПоляИсточника = "";
	Для Каждого Колонка Из КолонкиИсточника Цикл
		КолонкиПриемника.Добавить(Колонка.Имя, Колонка.ТипЗначения);
		ПоляИсточника = ПоляИсточника + ?(ЗначениеЗаполнено(ПоляИсточника), ", ", "") + Колонка.Имя;
	КонецЦикла;
	Возврат ПоляИсточника;
КонецФункции

Функция ПереченьПолей(КоллекцияКолонок, Исключения = "")
	ИсключаемыеПоля = Новый Структура(Исключения);
	ПереченьПолей = "";
	Для Каждого Колонка Из КоллекцияКолонок Цикл
		Если НЕ ИсключаемыеПоля.Свойство(Колонка.Имя) Тогда
			ПереченьПолей = ПереченьПолей + ?(ЗначениеЗаполнено(ПереченьПолей), ", ", "") + Колонка.Имя;
		КонецЕсли;
	КонецЦикла;
	Возврат ПереченьПолей;
КонецФункции

Функция ПустаяСсылка(Ссылка, КэшСсылок)
	Результат = КэшСсылок[ТипЗнч(Ссылка)];
	Если Результат = Неопределено Тогда
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Ссылка);
		Если МенеджерОбъекта = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		Результат = МенеджерОбъекта.ПустаяСсылка();
		КэшСсылок.Вставить(ТипЗнч(Ссылка), Результат);
	КонецЕсли;
	Возврат Результат;
КонецФункции

Функция УзелИндекса(Индекс, КлючиИндекса)
	УзелИндекса = Индекс;
	Счетчик = 0;
	Пока Счетчик <= КлючиИндекса.ВГраница() И КлючиИндекса[Счетчик] <> Неопределено Цикл
		УзелИндекса = УзелИндекса[КлючиИндекса[Счетчик]];
		Если УзелИндекса = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		Счетчик = Счетчик + 1;
	КонецЦикла;
	Возврат УзелИндекса;
КонецФункции

Функция ДобавитьУзелИндекса(Индекс, КлючиИндекса, Значение)
	ВГраница = КлючиИндекса.ВГраница();
	Если ВГраница < 0 Или КлючиИндекса[0] = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	УзелИндекса = Индекс;
	Счетчик = 0;
	Пока Счетчик < ВГраница Цикл
		ПодузелИндекса = УзелИндекса[КлючиИндекса[Счетчик]];
		Если ПодузелИндекса = Неопределено Тогда
			ПодузелИндекса = Новый Соответствие;
			УзелИндекса.Вставить(КлючиИндекса[Счетчик], ПодузелИндекса);
		КонецЕсли;
		УзелИндекса = ПодузелИндекса;
		Счетчик = Счетчик + 1;
		Если КлючиИндекса[Счетчик] = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЦикла;
	УзелИндекса.Вставить(КлючиИндекса[Счетчик], Значение);
	Возврат Значение;
КонецФункции

Функция АналитикаУчетаПартий(СтараяАналитика, НалогообложениеПартии, НалогообложениеНДС)
	
	Если ЗначениеЗаполнено(СтараяАналитика) И ТипЗнч(СтараяАналитика) = Тип("СправочникСсылка.КлючиАналитикиУчетаПартий")
	 И ЗначениеЗаполнено(НалогообложениеПартии) И ЗначениеЗаполнено(НалогообложениеНДС)
	 И НалогообложениеПартии <> НалогообложениеНДС Тогда
	 
		РеквизитыНовойАналитики = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			СтараяАналитика,
			"Поставщик, Контрагент, СтавкаНДС, ВидЦенности");
		
		РеквизитыНовойАналитики.Вставить("НалогообложениеНДС", НалогообложениеНДС); // новое налогообложение
		РеквизитыНовойАналитики.Вставить("Дата", Дата(1, 1, 1)); // для того, чтобы режим ПУ точно определился как "не равен ПУ 2.2"
		
		НоваяАналитика = Справочники.КлючиАналитикиУчетаПартий.ПолучитьКлючАналитики(РеквизитыНовойАналитики);
		
	Иначе
		
		НоваяАналитика = СтараяАналитика;
		
	КонецЕсли;
	
	Возврат НоваяАналитика;
	
КонецФункции

#КонецОбласти

#КонецОбласти
