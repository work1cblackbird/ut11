////////////////////////////////////////////////////////////////////////////////
// Содержит процедуры и функции для работы с отчетами.
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Добавляет период отчета в значения отбора.
//
// Параметры:
//  НастройкиКомпоновкиДанных - НастройкиКомпоновкиДанных - Настройки СКД.
//  ЗначенияОтбораДанных - Структура - Значения отбора.
//
Процедура ПериодОтчета(НастройкиКомпоновкиДанных, ЗначенияОтбораДанных) Экспорт

	ПараметрОтчета = ЗначениеПараметра(НастройкиКомпоновкиДанных, "ПериодОтчета"); // СтандартныйПериод - 
	
	Если ПараметрОтчета <> Неопределено Тогда
		ЗначенияОтбораДанных.Вставить("НачалоПериода", ПараметрОтчета.ДатаНачала);
		ЗначенияОтбораДанных.Вставить("ОкончаниеПериода", ПараметрОтчета.ДатаОкончания);
	Иначе
		ЗначенияОтбораДанных.Вставить("НачалоПериода", '000101010000');
		ЗначенияОтбораДанных.Вставить("ОкончаниеПериода", '000101010000');
	КонецЕсли; 
	
КонецПроцедуры

// Получает значение параметра СКД.
//
// Параметры:
//  НастройкиКомпоновкиДанных - НастройкиКомпоновкиДанных - Настройки СКД.
//  Параметр - Строка, ПараметрКомпоновкиДанных	 - поле или имя поля, для которого нужно вернуть параметр.
//  ПустоеЗначение - Произвольный - Значение, которое нужно вернуть, если параметр не используется.
// 
// Возвращаемое значение:
//  Произвольный - Значение параметра СКД.
//
Функция ЗначениеПараметра(НастройкиКомпоновкиДанных, Параметр, ПустоеЗначение = Неопределено) Экспорт

	ПараметрОтчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиКомпоновкиДанных, Параметр);
	
	Если ПараметрОтчета <> Неопределено И ПараметрОтчета.Использование Тогда
		Возврат ПараметрОтчета.Значение;
	Иначе
		Возврат ПустоеЗначение;
	КонецЕсли;

КонецФункции

// Возвращает значение отбора установленного в отчете
//
// Параметры:
//  ИмяПоля	- Строка - Имя отбора.
//  ЭлементыОтбора- КоллекцияЭлементовОтбораКомпоновкиДанных - Отбор компоновки данных.
// 
// Возвращаемое значение:
//  Массив из Произвольный - Установленный отбор.
//
Функция ЗначениеОтбора(ИмяПоля, ЭлементыОтбора) Экспорт

	МассивЗначенийОтбора = Новый Массив;
	
	Если ЭлементыОтбора = Неопределено Тогда
		Возврат МассивЗначенийОтбора;
	КонецЕсли;
	
	Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
	
	ЗначениеОтбора = Неопределено;
	Для каждого ЭлементКоллекции Из ЭлементыОтбора Цикл
		Если ТипЗнч(ЭлементКоллекции) = Тип("ЭлементОтбораКомпоновкиДанных") 
			И ЭлементКоллекции.ЛевоеЗначение = Поле 
			И ЭлементКоллекции.Использование Тогда
			
			ЗначениеОтбора = ЭлементКоллекции.ПравоеЗначение;
			Прервать;
		КонецЕсли; 
	КонецЦикла; 
	
	Если ЗначениеОтбора <> Неопределено И ТипЗнч(ЗначениеОтбора) = Тип("СписокЗначений") Тогда
		МассивЗначенийОтбора = ЗначениеОтбора.ВыгрузитьЗначения();
	ИначеЕсли ЗначениеОтбора <> Неопределено Тогда
		МассивЗначенийОтбора.Добавить(ЗначениеОтбора);
	КонецЕсли;
	
	Возврат МассивЗначенийОтбора;

КонецФункции

#КонецОбласти
