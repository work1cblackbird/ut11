#Область СлужебныйПрограммныйИнтерфейс

#Область СерииНоменклатуры

// Процедура обновляет кеш ключевых реквизитов текущей строки товаров. По ключевым реквизитам осуществляется связь
//  между ТЧ серий и ТЧ товаров.
//
// Параметры:
// 
//  Элемент - ТаблицаФормы - таблица, в которой отображается ТЧ с товарами.
//  КэшированныеЗначения - Структура - переменная модуля формы, в которой хранятся кешируемые значения.
//  ПараметрыУказанияСерий - ФормаКлиентскогоПриложения, Структура - 
//     управляемая форма содержащая структуру или непосредственно структура параметров указания серий.
//  Копирование - Булево - признак, что кешированная строка скопирована (параметр события ПриНачалеРедактирования).
Процедура ОбновитьКешированныеЗначенияДляУчетаСерий(Элемент, КэшированныеЗначения, ПараметрыУказанияСерий = "", Копирование = Ложь) Экспорт
	
	//++ НЕ ГОСИС
	НоменклатураКлиент.ОбновитьКешированныеЗначенияДляУчетаСерий(
		Элемент,
		КэшированныеЗначения,
		?(ТипЗнч(ПараметрыУказанияСерий) = Тип("ФормаКлиентскогоПриложения"),ПараметрыУказанияСерий.ПараметрыУказанияСерий,ПараметрыУказанияСерий),
		Копирование);
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

// Процедура проверяет необходимость обновления статусов указания серий при окончании редактирования строки товаров.
//
// Параметры:
//  Обновить - Булево - (исходящий) - необходимость обновления статусов указания серий;
//  Форма   - ФормаКлиентскогоПриложения - форма-источник вызова;
//  Элемент - ТаблицаФормы	 - таблица формы, отображающая ТЧ товаров;
//  КэшированныеЗначения - Структура   - переменная модуля формы, в которой хранятся кешируемые значения;
//  ПараметрыУказанияСерий - Структура - параметры указания серий, возвращаемые соответствующей процедурой
//                                       модуля менеджера документа;
//  Удаление - Булево - Истина, если проверка вызывается при удалении строки ТЧ.
//
Процедура УстановитьОбновитьСтатусыСерий(Обновить, Форма, Элемент, КэшированныеЗначения, ПараметрыУказанияСерий = "", Удаление) Экспорт
	
	//++ НЕ ГОСИС
	
	Если ПараметрыУказанияСерий = "" Тогда
		ПараметрыУказанияСерий = Форма.ПараметрыУказанияСерий;
	КонецЕсли;
	
	Обновить = НоменклатураКлиент.НеобходимоОбновитьСтатусыСерий(Элемент, КэшированныеЗначения, ПараметрыУказанияСерий, Удаление);
	
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

// процедура получает параметры указания серий в форме конфигурации.
//
// Параметры:
//  ПараметрыУказанияСерий - Произвольный               - (исходящий) параметры указания серий формы.
//  Форма                  - ФормаКлиентскогоПриложения - форма, для которой инициировано получение параметров указания серий.
//
Процедура ПриПолученииПараметровУказанияСерий(ПараметрыУказанияСерий, Форма) Экспорт
	
	//++ НЕ ГОСИС
	ПараметрыУказанияСерий = Форма.ПараметрыУказанияСерий;
	Если Форма.ИмяФормы = "Документ.РасходныйОрдерНаТовары.Форма.ФормаДокумента" Тогда
		ПараметрыУказанияСерий = Форма.ПараметрыУказанияСерий.ОтгружаемыеТовары;
	ИначеЕсли Форма.ИмяФормы = "Документ.ОтборРазмещениеТоваров.Форма.ФормаДокумента" Тогда
		ПараметрыУказанияСерий = Форма.ПараметрыУказанияСерий.Отбор;
	КонецЕсли; 
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

// процедура проверяет необходимость указания серий в строке, если возможно, открывает форму указания,
//  если форма указания не требует контекстного вызова сервера.
//
// Параметры:
//  Нужен                  - Булево              - (исходящий) признак необходимости контекстного вызова сервера;
//  Форма                  - ФормаКлиентскогоПриложения    - форма документа, в которой инициировано указание серий;
//  ПараметрыУказанияСерий - Произвольный        - параметры указания серий строки;
//  Текст                  - Строка              - текст, введенный в поле ввода (параметр событий ОкончаниеВводаТекста 
//                                                 и АвтоПодборВводаТекста).
//  ТекущиеДанные          - Структура, ДанныеФормыЭлементКоллекции - данные строки, в которой указывается серия,
//                         - Неопределено        - текущие данные текущего табличного поля;
//  СтандартнаяОбработка   - Булево              - открытие формы выбора серий по умолчанию.
//
Процедура ЗаполнитьДляУказанияСерийНуженСерверныйВызов(
	Нужен, Форма, ПараметрыУказанияСерий, Текст, ТекущиеДанные, СтандартнаяОбработка) Экспорт
	
	//++ НЕ ГОСИС
	СтандартнаяОбработка = Ложь;
	ПараметрыУказанияСерийКопия = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(ПараметрыУказанияСерий, Ложь);
	Если Форма.ИмяФормы = "Документ.ОтчетОПроизводствеЕГАИС.Форма.ФормаДокумента" Тогда
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущиеДанные, "ИдентификаторСтрокиТовары") Тогда
			ПараметрыУказанияСерийКопия = ПараметрыУказанияСерийКопия.Сырье;
		Иначе
			ПараметрыУказанияСерийКопия = ПараметрыУказанияСерийКопия.Товары;
		КонецЕсли;
	ИначеЕсли Форма.ИмяФормы = "Документ.ФормированиеПартийПриПроизводствеЗЕРНО.Форма.ФормаДокумента" Тогда
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущиеДанные, "Объект") Тогда
			ПараметрыУказанияСерийКопия = ПараметрыУказанияСерийКопия.Шапка;
		Иначе
			ПараметрыУказанияСерийКопия = ПараметрыУказанияСерийКопия.Сырье;
		КонецЕсли;
	ИначеЕсли Форма.ИмяФормы = "Документ.ПроизводственнаяОперацияСАТУРН.Форма.ФормаДокумента" Тогда
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущиеДанные, "НомерПартии") Тогда
			ПараметрыУказанияСерийКопия = ПараметрыУказанияСерийКопия.Товары;
		Иначе
			ПараметрыУказанияСерийКопия = ПараметрыУказанияСерийКопия.Сырье;
		КонецЕсли;
	КонецЕсли;
	Если Форма.ИмяФормы = "Обработка.ПроверкаИПодборПродукцииИСМП.Форма.ПроверкаИПодбор"
			Или Форма.ИмяФормы = "Обработка.ПроверкаИПодборТабачнойПродукцииМОТП.Форма.ПроверкаИПодбор" Тогда
		Если ПараметрыУказанияСерийКопия <> Неопределено Тогда
			Если ПараметрыУказанияСерийКопия.Свойство("ВыходныеИзделия") Тогда
				ПараметрыУказанияСерийКопия = ПараметрыУказанияСерийКопия.ВыходныеИзделия;
			ИначеЕсли ПараметрыУказанияСерийКопия.Свойство("Товары") Тогда
				ПараметрыУказанияСерийКопия = ПараметрыУказанияСерийКопия.Товары;
			КонецЕсли;
			ПараметрыУказанияСерийКопия.ИмяИсточникаЗначенийВФормеОбъекта = "ТекущиеДанные";
			ТекущиеДанные.Ссылка = Форма.ПроверяемыйДокумент;
		КонецЕсли;
	ИначеЕсли Форма.ИмяФормы = "Обработка.ПроверкаИПодборАлкогольнойПродукцииЕГАИС.Форма.ПроверкаИПодбор" Тогда
		Если ПараметрыУказанияСерийКопия <> Неопределено Тогда
			Если ПараметрыУказанияСерийКопия.Свойство("Сырье") Тогда
				ПараметрыУказанияСерийКопия = ПараметрыУказанияСерийКопия.Сырье;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(ТекущиеДанные) = Тип("ФормаКлиентскогоПриложения") Тогда 
		ТекущиеДанныеСтруктура = Новый Структура("Номенклатура,Характеристика,Серия,
		|	Склад,Помещение,ХарактеристикиИспользуются,Действие,Подразделение,
		|	НоменклатураОприходование,ХарактеристикаОприходование,
		|	Назначение,НазначениеОтправителя,НазначениеПолучателя,
		|	СтатусУказанияСерийНаСкладах,СтатусУказанияСерийПереданныхТоваров,СтатусУказанияСерий,
		|	СтатусУказанияСерийОтправитель,СтатусУказанияСерийПолучатель");
		ЗаполнитьЗначенияСвойств(ТекущиеДанныеСтруктура, Форма);
		ТекущиеДанныеСтруктура.Вставить("ИдентификаторТекущейСтроки", 0);
		ТекущиеДанныеСтруктура.Вставить("Количество", 1);
	Иначе 
		ТекущиеДанныеСтруктура = ТекущиеДанные;
	КонецЕсли;
	
	Нужен = НоменклатураКлиент.ДляУказанияСерийНуженСерверныйВызов(Форма, ПараметрыУказанияСерийКопия, Текст, ТекущиеДанныеСтруктура);
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

// Процедура проверяет, что переданная форма является формой указания серий, выставляя результат в первый параметр и
//  нормализуя в этом случае результат выбора в структуру с параметром Значение = выбранная серия.
// 
// Параметры:
//  ЭтоФормаУказанияСерий - Булево - (исходящий) Это форма указания серий.
//  Форма - ФормаКлиентскогоПриложения - Проверяемая форма.
//  ВыбранноеЗначение - Произвольный - (входящий/исходящий) результат выбора до/после нормализации, можно модифицировать
//    если это форма указания серии (должна быть структура с ключем Значение = выбранная серия)
Процедура ЗаполнитьЭтоФормаУказанияСерий(ЭтоФормаУказанияСерий, Форма, ВыбранноеЗначение) Экспорт
	
	//++ НЕ ГОСИС
	ЭтоФормаУказанияСерий = НоменклатураКлиент.ЭтоУказаниеСерий(Форма);
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ПрикладныеКлассификаторы

// Открывает форму списка видов номенклатуры.
//
Процедура ОткрытьФормуСпискаВидыНоменклатуры(ВладелецФормы) Экспорт
	
	//++ НЕ ГОСИС
	ОткрытьФорму("Справочник.ВидыНоменклатуры.ФормаСписка", , ВладелецФормы);
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

// Открывает форму списка номенклатуры.
//
Процедура ОткрытьФормуСпискаНоменклатуры(ВладелецФормы) Экспорт
	
	//++ НЕ ГОСИС
	ОткрытьФорму("Справочник.Номенклатура.ФормаСписка", , ВладелецФормы);
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

// Открывает форму списка земельных участков.
//
Процедура ОткрытьФормуСпискаЗемельныхУчастков(ВладелецФормы) Экспорт
	
	//++ НЕ ГОСИС
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

// Обработчик специфических сценариев записи объекта в форме (например, после дополнительных ответов пользователя)
//   При переопределении действия:
//     ** Вызвать обработчик ДействиеПослеЗаписи после окончания записи
//     ** Установить признак СтандартнаяОбработка в значение Ложь
//
// Параметры:
//   Форма                - ФормаКлиентскогоПриложения     - источник события записи
//   Объект               - ДанныеФормыСтруктура - записываемый из формы объект
//   ДействиеПослеЗаписи  - ОписаниеОповещения   - действие которое требуется выполнить после записи объекта из формы
//   СтандартнаяОбработка - Булево               - признак стандартной обработки события (запись без блокирующих вызовов)
//
Процедура ВыполнитьЗаписьОбъектаВФорме(Форма, Объект, ДействиеПослеЗаписи, СтандартнаяОбработка) Экспорт
	
	//++ НЕ ГОСИС
	СтандартнаяОбработка = Ложь;
	Если Объект.Проведен Тогда
		ОбщегоНазначенияУТКлиент.Провести(Форма, Истина, ДействиеПослеЗаписи);
	Иначе
		ОбщегоНазначенияУТКлиент.Записать(Форма, Истина, ДействиеПослеЗаписи);
	КонецЕсли;
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
