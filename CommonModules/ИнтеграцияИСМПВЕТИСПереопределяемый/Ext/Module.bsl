/////////////////////////////////////////////////////////////////////////////
// Совместная работа подсистем ВетИС и ИСМП.
//   * Процедуры взаимодействия с внешним модулем ВетИС
//

#Область ПрограммныйИнтерфейс

// Проверяет наличие внешней подсистемы работы с ВетИС
//
// Параметры:
//  ИспользуетсяВнешняяПодсистемаВетИС - Булево - Истина, если используется внешняя подсистема работы с ВетИС
//
Процедура ПриПроверкеИспользованияВнешнейПодсистемыВетИС(ИспользуетсяВнешняяПодсистемаВетИС) Экспорт
	
	Возврат;
	
КонецПроцедуры

#Область ОбщийМодульШтрихкодированиеИС

// Заполняет данные документа-основания из подсистемы ВетИС для документа ИСМП
// 
// Параметры:
//   ДанныеОснования    - см. ШтрихкодированиеОбщегоНазначенияИС.ИнициализицияТаблицыДанныхДокумента.
//   ДокументОснование  - ДокументСсылка  - документ-основание.
//   ДанныеСформированы - Булево          - заполнение произведено.
//
Процедура СформироватьДанныеДокументаОснования(ДанныеОснования, ДокументОснование, ДанныеСформированы) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ОбщийМодульШтрихкодированиеИСМПСлужебный

// Проверяет код маркировки молочной продукции на соответствие документу-основанию
// 
// Параметры:
//   СтрокаДанных          - Структура - известные данные кода маркировки.
//   ПравилоПроверки       - Структура - текущее правило проверки кода маркировки:
//     * ЕстьОшибка - Булево - код маркировки не соответствует документу-основанию.
//   ПараметрыСканирования - см. ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования.
//   СтандартнаяОбработка  - Булево    - признак библиотечной обработки
//
Процедура ПроверитьДанныеСтрокиПоСрокуГодностиДокументаОснования(СтрокаДанных, ПравилоПроверки, ПараметрыСканирования, СтандартнаяОбработка) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Дополняет дерево вложенных штрихкодов сохраненными данными ВетИС табличной части "Штрихкоды упаковок"
//   для документа МаркировкаТоваровИСМП:
//     * Заполняет признаки ИдентификаторПроисхожденияВЕТИС, ГоденДо, СкоропортящаясяПродукция по заполненным данным
//      табличной части "ШтрихкодыУпаковок".
//     * Заполняет эти же признаки для всех дочерних узлов дерева.
//     * Заполняет эти же признаки для таблицы МаркированныеТовары вложенных штрихкодов.
// 
// Параметры:
//   ВложенныеШтрихкоды    - см. ШтрихкодированиеИС.ВложенныеШтрихкодыУпаковокПоДокументу.
//   ПараметрыСканирования - см. ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования.
//   СтандартнаяОбработка  - Булево - признак библиотечной обработки
//
Процедура ДополнитьВложенныеШтрихкодыДаннымиВЕТИС(ВложенныеШтрихкоды, ПараметрыСканирования, СтандартнаяОбработка) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ОбщийМодульИнтеграцияИСМПСлужебный

// Заполняет ссылку на идентификатор происхождения ВетИС по его строковому идентификатору
// 
// Параметры:
//   ИдентификаторПроисхожденияВЕТИССтрокой - Строка - строковое представление идентификатора ВетИС
//   ИдентификаторПроисхожденияВЕТИССсылка - ОпределяемыйТип.ИдентификаторПроисхожденияВЕТИС - ссылочное представление идентификатора ВетИС
//   Кеш - Структура - ранее найденные значения:
//    ПараметрыПреобразования - Структура - :
//      НайденныеСсылки- Структура - :
//        ВетеринарноСопроводительныйДокументВЕТИС - Соответствие - :
//          Ключ - Строка - строковое представление идентификатора ВетИС
//          Значение - ОпределяемыйТип.ИдентификаторПроисхожденияВЕТИС - ссылочное представление идентификатора ВетИС
//   СтандартнаяОбработка  - Булево    - признак библиотечной обработки
//
Процедура ПолучитьСсылкуПоСтроковомуИдентификаторуПроисхождения(ИдентификаторПроисхожденияВЕТИССтрокой, ИдентификаторПроисхожденияВЕТИССсылка, Кеш, СтандартнаяОбработка) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ДокументЗаказНаЭмиссиюКодовМаркировкиСУЗ

// Определяет ожидаемый шаблон кода маркировки молочной продукции по идентификатору происхождения ВетИС. Ожидаемое поведение:
//   * Для подконтрольной ВетИС продукции выставление актуального шаблона
// 
// Параметры:
//  Запрос                - Запрос - запрос обработчика заполнения табличной части заказа на эмиссию по маркировке
//   СтандартнаяОбработка - Булево - признак библиотечной обработки
//
Процедура ДоработатьЗапросЗаполненияЗаказаНаЭмиссиюПоМаркировке(Запрос, СтандартнаяОбработка) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ДокументМаркировкаТоваровИСМП

// Обработка заполнения документа "Маркировка товаров ИСМП" по документам ВетИС.
//   
// Параметры:
//   ДокументОбъект - ДокументОбъект.МаркировкаТоваровИСМП - заполняемый документ
//   ДанныеЗаполнения - Произвольный - данные заполнения
//   ТекстЗаполнения - Строка - текст заполнения
//   СтандартнаяОбработка - Булево - признак стандартной обработки события
Процедура ОбработкаЗаполненияМаркировкиТоваровИСМП(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	//++ НЕ ГОСИС
	
	ПроизводственнаяОперация = Тип("ДокументСсылка.ПроизводственнаяОперацияВЕТИС");
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) <> ПроизводственнаяОперация Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА Товары.ДатаПроизводстваНачалоПериода = ДАТАВРЕМЯ(1,1,1)
	|			ТОГДА ДАТАВРЕМЯ(1,1,1)
	|		КОГДА НАЧАЛОПЕРИОДА(Товары.ДатаПроизводстваНачалоПериода, ДЕНЬ) <> НАЧАЛОПЕРИОДА(Товары.ДатаПроизводстваКонецПериода, ДЕНЬ)
	|			И Товары.ДатаПроизводстваКонецПериода <> ДАТАВРЕМЯ(1,1,1)
	|			ТОГДА ДАТАВРЕМЯ(1,1,1)
	|		КОГДА Товары.ДатаПроизводстваТочностьЗаполнения = ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ДДММГГГГ)
	|			ТОГДА Товары.ДатаПроизводстваНачалоПериода
	|		КОГДА Товары.ДатаПроизводстваТочностьЗаполнения = ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ДДММГГГГЧЧ)
	|			ТОГДА НАЧАЛОПЕРИОДА(Товары.ДатаПроизводстваНачалоПериода, ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1,1,1)
	|	КОНЕЦ КАК ДатаПроизводства
	|ПОМЕСТИТЬ ДатыПроизводства
	|ИЗ
	|	Документ.ПроизводственнаяОперацияВЕТИС.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|;
	|ВЫБРАТЬ
	|	ДокументСсылка.Ссылка                                КАК ДокументОснование,
	|	ДокументСсылка.ХозяйствующийСубъект.Контрагент       КАК Организация,
	|	НЕ ДокументСсылка.Проведен                           КАК ЕстьОшибкиПроведен,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ОтчетОВерификацииНанесенныхКМ) КАК ОперацияНанесения,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВводВОборотПроизводствоРФ)      КАК Операция,
	|	Истина КАК ОперацияНанесенияТолькоДляНаборов,
	|	ВЫБОР
	|		КОГДА КОЛИЧЕСТВО(ДатыПроизводства.ДатаПроизводства) <> 1
	|			ТОГДА ДАТАВРЕМЯ(1,1,1)
	|		ИНАЧЕ МАКСИМУМ(ДатыПроизводства.ДатаПроизводства)
	|	КОНЕЦ КАК ДатаПроизводства,
	|	МАКСИМУМ(ОписаниеСборки.ЭтоНабор) КАК ЭтоАгрегацияНабора,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС) КАК ВидПродукции
	|ИЗ
	|	Документ.ПроизводственнаяОперацияВЕТИС КАК ДокументСсылка,
	|	(ВЫБРАТЬ
	|		МАКСИМУМ(ВЫБОР
	|			КОГДА ОписаниеGTINИС.ВидУпаковки = ЗНАЧЕНИЕ(Перечисление.ВидыУпаковокИС.Набор)
	|				И ОписаниеGTINИС.КоличествоПотребительскихУпаковок < 2
	|			ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЭтоНабор
	|	Из Документ.ПроизводственнаяОперацияВЕТИС.Товары КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК Штрихкоды
	|			ПО Штрихкоды.Номенклатура = ТаблицаДокумента.Номенклатура
	|			И Штрихкоды.Характеристика = ТаблицаДокумента.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОписаниеGTINИС КАК ОписаниеGTINИС
	|			ПО  ОписаниеGTINИС.GTIN = Штрихкоды.Штрихкод
	|			Или ОписаниеGTINИС.GTIN = ""0""+Штрихкоды.Штрихкод
	|			Или ОписаниеGTINИС.GTIN = ""00""+Штрихкоды.Штрихкод
	|			Или ОписаниеGTINИС.GTIN = ""000000""+Штрихкоды.Штрихкод
	|		ГДЕ
	|	ТаблицаДокумента.Ссылка = &ДокументОснование) КАК ОписаниеСборки,
	|	ДатыПроизводства КАК ДатыПроизводства
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|СГРУППИРОВАТЬ ПО
	|	ДокументСсылка.Ссылка,
	|	ДокументСсылка.ХозяйствующийСубъект.Контрагент,
	|	НЕ ДокументСсылка.Проведен
	|");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Шапка = Запрос.Выполнить().Выгрузить()[0];
	Если Не Шапка.ЭтоАгрегацияНабора Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	Если Не ЭтоПерезаполнение И Не ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		ИнтеграцияИСПереопределяемый.ПроверитьВозможностьВводаНаОсновании(
			Шапка.ДокументОснование,,
			Шапка.ЕстьОшибкиПроведен);
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Шапка);
	КонецЕсли;
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьТабличнуюЧасть") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|	ТаблицаТовары.Характеристика    КАК Характеристика,
	|	ТаблицаТовары.Серия             КАК Серия,
	|	СУММА(ТаблицаТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.ПроизводственнаяОперацияВЕТИС.Сырье КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &ДокументОснование
	|	И ТаблицаТовары.Номенклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаТовары.Количество) > 0
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Товары КАК Товары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МаркировкаТоваровИСМПТовары.Номенклатура,
	|	МаркировкаТоваровИСМПТовары.Характеристика,
	|	МаркировкаТоваровИСМПТовары.Серия,
	|	-МаркировкаТоваровИСМПТовары.Количество
	|ИЗ
	|	Документ.МаркировкаТоваровИСМП КАК МаркировкаТоваровИСМП
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаркировкаТоваровИСМП.Товары КАК МаркировкаТоваровИСМПТовары
	|		ПО МаркировкаТоваровИСМП.Ссылка = МаркировкаТоваровИСМПТовары.Ссылка
	|ГДЕ
	|	МаркировкаТоваровИСМП.Проведен
	|	И МаркировкаТоваровИСМП.Ссылка <> &ЭтаСсылка
	|	И МаркировкаТоваровИСМП.ДокументОснование = &ДокументОснование
	|	И МаркировкаТоваровИСМП.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.Агрегация)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("ОтчетПроизводственнойЛинии") Тогда
		Запрос.Текст = Запрос.Текст + "ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.Серия КАК Серия
		|ПОМЕСТИТЬ СгруппированныеТоварыКОформлению
		|ИЗ
		|	ТоварыСерии КАК Товары
		|СГРУППИРОВАТЬ ПО
		|	Товары.Номенклатура,
		|	Товары.Характеристика,
		|	Товары.Серия
		|ИМЕЮЩИЕ
		|	СУММА(Товары.Количество) > 0
		|;
		|
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СгруппированныеТоварыКОформлению.Номенклатура       КАК Номенклатура,
		|	СгруппированныеТоварыКОформлению.Характеристика     КАК Характеристика,
		|	СгруппированныеТоварыКОформлению.Серия              КАК Серия
		|ИЗ
		|	СгруппированныеТоварыКОформлению КАК СгруппированныеТоварыКОформлению
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СгруппированныеТоварыКОформлению.Номенклатура) > 1
		|			ТОГДА Неопределено
		|		ИНАЧЕ МАКСИМУМ(СгруппированныеТоварыКОформлению.Номенклатура)
		|	КОНЕЦ КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СгруппированныеТоварыКОформлению.Номенклатура) > 1
		|			ТОГДА Неопределено
		|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СгруппированныеТоварыКОформлению.Характеристика) > 1
		|			ТОГДА Неопределено
		|		ИНАЧЕ МАКСИМУМ(СгруппированныеТоварыКОформлению.Характеристика)
		|	КОНЕЦ КАК Характеристика,
		|	ВЫБОР
		|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СгруппированныеТоварыКОформлению.Номенклатура) > 1
		|			ТОГДА Неопределено
		|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СгруппированныеТоварыКОформлению.Характеристика) > 1
		|			ТОГДА Неопределено
		|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СгруппированныеТоварыКОформлению.Серия) > 1
		|			ТОГДА Неопределено
		|		ИНАЧЕ МАКСИМУМ(СгруппированныеТоварыКОформлению.Серия)
		|	КОНЕЦ КАК Серия
		|ИЗ
		|	СгруппированныеТоварыКОформлению КАК СгруппированныеТоварыКОформлению
		|";
		
		Пакет = Запрос.ВыполнитьПакет();
		ДанныеНоменклатуры = Пакет[Пакет.Количество()-1].Выгрузить();
		
		Если ДанныеНоменклатуры.Количество() = 0 Тогда
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'В %1 отсутствует продукция для заполнения.'"),
				ДанныеЗаполнения);
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ДанныеНоменклатуры[0]);
		
	Иначе
		
		Запрос.Текст = Запрос.Текст +
		"ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.Серия КАК Серия,
		|	СУММА(Товары.Количество) КАК Количество,
		|	СУММА(Товары.Количество) КАК КоличествоУпаковок,
		|	0 КАК КоличествоПотребительскихУпаковок
		|ИЗ
		|	ТоварыСерии КАК Товары
		|СГРУППИРОВАТЬ ПО
		|	Товары.Номенклатура,
		|	Товары.Характеристика,
		|	Товары.Серия
		|ИМЕЮЩИЕ
		|	СУММА(Товары.Количество) > 0";
		
		Товары = Запрос.Выполнить().Выгрузить();
		Если Товары.Количество() = 0 Тогда
			
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'В %1 отсутствует продукция для заполнения.'"),
				ДанныеЗаполнения);
			
		КонецЕсли;
		
		ДокументОбъект.Товары.Загрузить(Товары);
	
	КонецЕсли;
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

// Определяет признак "Скоропортящаяся продукция" по идентификатору происхождения ВетИС. Ожидаемое поведение:
//   * Для скоропортящейся продукции выставление Истина.
//   * Для прочей продукции (в т. ч. при незаполненном идентификаторе) выставление Ложь.
// 
// Параметры:
//  Запрос                - Запрос - запрос обработчика заполнения маркируемой продукции документов ИСМП.
//   СтандартнаяОбработка - Булево - признак библиотечной обработки
//
Процедура ДоработатьЗапросЗаполненияМаркируемойПродукцииДокументаМаркировка(Запрос, СтандартнаяОбработка) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

// Возвращает данные ВетИС по идентификаторам происхождения:
//   * СрокГодности    - Дата   - дата начала последнего периода срока годности.
//   * СкоропортящаясяПродукция - Булево - признак скоропорта.
//   * Представление   - Строка - представление идентификатора (без имени), ожидается "Дата (срок годности)"
//   * Идентификатор   - ОпределяемыйТип.УникальныйИдентификаторИС - GUID объекта ВетИС.
//
// Параметры:
//   ИдентификаторыПроисхождения - Массив Из ОпределяемыйТип.ИдентификаторПроисхожденияВЕТИС - идентификаторы происхождения.
//   ДанныеПоСрокамГодности - Соответствие - данные ВетИС по идентификаторам происхождения, где ключ - ссылка 
//      на идентификатор, значение - полученные данные.
//   СтандартнаяОбработка - Булево - признак библиотечной обработки
//
Процедура ПриПолученииДанныхИдентификаторовПроисхождения(ИдентификаторыПроисхождения, ДанныеПоСрокамГодности, СтандартнаяОбработка) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Проверяет продукцию идентификатора происхождения на соответствие сопоставленной продукции по переданной структуре данных товара.
//
// Параметры:
//  ИдентификаторПроисхождения   - ОпределяемыйТип.УникальныйИдентификаторИС - GUID объекта ВетИС.
//  ДанныеСопоставления - Структура - со свойствами:
//   * Номенклатура   - ОпределяемыйТип.Номенклатура               - номенклатура.
//   * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика.
//   * Серия          - ОпределяемыйТип.СерияНоменклатуры          - серия.
//  Соответствует - Булево - переопределяемый параметр, Истина, если продукция идентификатора совпадает с сопоставленной продукцией данных для сопоставления.
//  СтандартнаяОбработка - Булево - признак библиотечной обработки.
Процедура ПриПроверкеСоответствияНоменклатурыИдентификаторуПроисхождения(ИдентификаторПроисхождения, ДанныеСопоставления, Соответствует, СтандартнаяОбработка) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Возвращает идентификатор производственной площадки ВетИС.
//
// Параметры:
//   ПроизводственнаяПлощадкаВЕТИС - ОпределяемыйТип.ПроизводственнаяПлощадкаВЕТИС - Ссылка на производственную площадку ВетИС.
//   ИдентификаторПроизводственнойПлощадкиВЕТИС - ОпределяемыйТип.УникальныйИдентификаторИС - Идентификатор производственной площадки ВетИС.
//   СтандартнаяОбработка - Булево - Признак библиотечной обработки.
//
Процедура ПриПолученииИдентификатораПроизводственнойПлощадкиВЕТИС(ПроизводственнаяПлощадкаВЕТИС, ИдентификаторПроизводственнойПлощадкиВЕТИС, СтандартнаяОбработка) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти
