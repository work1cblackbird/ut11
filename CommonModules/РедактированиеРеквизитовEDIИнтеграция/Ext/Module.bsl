
#Область СлужебныйПрограммныйИнтерфейс

#Область КлиентскаяБиблиотека

// см. СервисEDIПереопределяемый.ЗаполнитьПараметрыЗапретаРедактированияРеквизитовEDI
Процедура ЗаполнитьПараметрыЗапретаРедактированияРеквизитовEDI(ТаблицаПараметров, ТипДокумента) Экспорт
	
	СервисEDIПереопределяемый.ЗаполнитьПараметрыЗапретаРедактированияРеквизитовEDI(ТаблицаПараметров, ТипДокумента);
	
КонецПроцедуры

// Заполняет таблицу реквизитов сервиса для документа
// 
// Параметры:
// 	ТаблицаПараметров - ТаблицаЗначений - таблица со следующей структурой:
// 	* ИмяРеквизита        - Строка         - имя реквизита сервиса.
// 	* БлокируемыеЭлементы - СписокЗначений - список имен элементов формы, связанных с реквизитом сервиса.
Процедура ЗаполнитьТаблицуПараметровЗапретаРедактированияБлокируемымиРеквизитами(ТаблицаПараметров, ТипДокумента) Экспорт
	
	Если ТипДокумента = Перечисления.ТипыДокументовEDI.ЗаказКлиента Тогда
		
		РедактированиеРеквизитовEDI.ЗаполнитьТаблицуПараметровЗаказКлиента(ТаблицаПараметров);
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовEDI.ЗаказПоставщику Тогда
		
		РедактированиеРеквизитовEDI.ЗаполнитьТаблицуПараметровЗаказПоставщику(ТаблицаПараметров);
		
	КонецЕсли;
	
КонецПроцедуры

// Добавляет на форму реквизиты для хранения состояния доступности реквизитов
//
// Параметры:
//  Форма                - ФормаКлиентскогоПриложения            - форма документа
//  ДобавляемыеРеквизиты - Массив из РеквизитФормы, Неопределено - добавленные реквизиты формы.
//    Если передано Неопределено, то реквизиты будут созданы на форме, если передан массив,
//    то параметр заполняется добавляемыми реквизитами, при этом реквизиты на форме созданы
//    не будут.
//
Процедура ПодготовитьРеквизитыФормы(Форма, ДобавляемыеРеквизиты = Неопределено) Экспорт
	
	// Определение, форма уже подготовлена при предыдущем вызове.
	ФормаПодготовлена = Ложь;
	РеквизитыФормы = Форма.ПолучитьРеквизиты();
	Для Каждого РеквизитФормы Из РеквизитыФормы Цикл
		Если РеквизитФормы.Имя = "ПараметрыЗапретаРедактированияРеквизитовEDI" Тогда
			ФормаПодготовлена = Истина;
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	ВыполнятьИзменениеРеквизитов = Ложь;
	Если ДобавляемыеРеквизиты = Неопределено Тогда
		
		ДобавляемыеРеквизиты         = Новый Массив;
		ВыполнятьИзменениеРеквизитов = Истина;
		
	КонецЕсли;
	
	ОписаниеТиповСтрока100       = Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100));
	ОписаниеТиповБулево          = Новый ОписаниеТипов("Булево");
	ОписаниеТиповСписокЗначений  = Новый ОписаниеТипов("СписокЗначений");
	ОписаниеТиповТаблицаЗначений = Новый ОписаниеТипов("ТаблицаЗначений");
	ОписаниеТиповВариантыЗапрета = Новый ОписаниеТипов("ПеречислениеСсылка.ВариантыЗапретаРедактированияРеквизитовEDI");
	
	// Добавление реквизитов на форму.
	ДобавляемыеРеквизиты = Новый Массив;
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ПараметрыЗапретаРедактированияРеквизитовEDI", ОписаниеТиповТаблицаЗначений));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ИмяРеквизита",            ОписаниеТиповСтрока100,      "ПараметрыЗапретаРедактированияРеквизитовEDI"));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("БлокируемыеЭлементы",     ОписаниеТиповСписокЗначений, "ПараметрыЗапретаРедактированияРеквизитовEDI"));
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ДоступностьЭлементовБлокируемыхEDI", ОписаниеТиповТаблицаЗначений));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ИмяЭлемента", ОписаниеТиповСтрока100, "ДоступностьЭлементовБлокируемыхEDI"));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Доступен",    ОписаниеТиповБулево,    "ДоступностьЭлементовБлокируемыхEDI"));
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ВариантЗапретаРедактированияEDI", ОписаниеТиповВариантыЗапрета));
	
	Если ВыполнятьИзменениеРеквизитов Тогда
		Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти 

