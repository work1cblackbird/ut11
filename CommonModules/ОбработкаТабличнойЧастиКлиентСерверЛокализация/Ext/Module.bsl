#Область ПрограммныйИнтерфейс

Процедура ДополнитьСтруктуруДействийПриИзмененииЭлемента(Форма, Элемент, СтруктураДействий) Экспорт
	
	//++ Локализация
	Если Форма.ИмяФормы = "Документ.ОтчетОРозничныхПродажах.Форма.ФормаДокумента" Тогда
		СтруктураДействий.Вставить("ЗаполнитьПризнакПодакцизныйТовар", Новый Структура("Номенклатура", "ПодакцизныйТовар"));
	КонецЕсли;
	
	Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ПараметрыИнтеграцииГосИС") Тогда
		Возврат;
	КонецЕсли;
	
	Если Форма.ИмяФормы = "Документ.ЧекККМ.Форма.ФормаДокументаРМК"
		Или Форма.ИмяФормы = "Документ.ЧекККМВозврат.Форма.ФормаДокументаРМК" Тогда
		
		Если Элемент = "Номенклатура" Тогда
			
			Если Форма.ПараметрыИнтеграцииГосИС.Получить("ЕГАИС")<>Неопределено
				Или Форма.ПараметрыИнтеграцииГосИС.Получить("ИСМП")<>Неопределено Тогда
				СтруктураДействий.Вставить("ЗаполнитьПризнакМаркируемаяПродукция", Новый Структура("Номенклатура", "МаркируемаяПродукция"));
				СтруктураДействий.Вставить("ЗаполнитьВидПродукцииИС", Новый Структура("Номенклатура", "ВидПродукцииИС"));
				СтруктураДействий.Вставить("ПроверитьСериюРассчитатьСтатус",
					Новый Структура("Склад, ПараметрыУказанияСерий", Форма.Объект.Склад, Форма.ПараметрыУказанияСерий));
			КонецЕсли;
			
			Если Форма.ПараметрыИнтеграцииГосИС.Получить("ЕГАИС")<>Неопределено Тогда
				СтруктураДействий.Вставить("ЗаполнитьНоменклатуруЕГАИС", ПараметрыЗаполненияНоменклатурыЕГАИСДляЧека(Форма.Объект));
			КонецЕсли;
			
		ИначеЕсли Элемент = "НоменклатураПризнаки" Тогда
			
			Если Форма.ПараметрыИнтеграцииГосИС.Получить("ЕГАИС")<>Неопределено Тогда
				СтруктураДействий.Вставить("ЗаполнитьПризнакМаркируемаяПродукция", Новый Структура("Номенклатура", "МаркируемаяПродукция"));
				СтруктураДействий.Вставить("ЗаполнитьВидПродукцииИС", Новый Структура("Номенклатура", "ВидПродукцииИС"));
			КонецЕсли;
			Если Форма.ПараметрыИнтеграцииГосИС.Получить("ИСМП")<>Неопределено Тогда
				СтруктураДействий.Вставить("ЗаполнитьПризнакМаркируемаяПродукция", Новый Структура("Номенклатура", "МаркируемаяПродукция"));
				СтруктураДействий.Вставить("ЗаполнитьВидПродукцииИС", Новый Структура("Номенклатура", "ВидПродукцииИС"));
			КонецЕсли;
			
			Если ИнтеграцияИСВызовСервера.ВидыПродукцииИспользующиеВскрытыеПотребительскихУпаковок(,, Ложь).Количество() Тогда
				СтруктураДействий.Вставить(
					"ЗаполнитьЕстьВскрытыеПотребительскиеУпаковкиИС",
					Новый Структура("Номенклатура", "ЕстьВскрытыеПотребительскиеУпаковкиИС"));
			КонецЕсли;
			
		ИначеЕсли Элемент = "Характеристика"
			Или Элемент = "Серия" Тогда
			
			Если Форма.ПараметрыИнтеграцииГосИС.Получить("ЕГАИС")<>Неопределено Тогда
				СтруктураДействий.Вставить("ЗаполнитьНоменклатуруЕГАИС", ПараметрыЗаполненияНоменклатурыЕГАИСДляЧека(Форма.Объект));
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ИнтеграцияИСУТКлиентСервер.ЕстьПострочныйСтатусПроверкиВФорме(Форма) Тогда
		
		Если Форма.ПараметрыИнтеграцииГосИС.Получить("ИСМП")<>Неопределено Тогда
			СтруктураДействий.Вставить("ЗаполнитьВидПродукцииИС", Новый Структура("Номенклатура", "ВидПродукцииИС"));
			СтруктураДействий.Вставить("ЗаполнитьПризнакМаркируемаяПродукция", Новый Структура("Номенклатура", "МаркируемаяПродукция"));
			СтруктураДействий.Вставить("ЗаполнитьПризнакАвтоматическийОСУИС", Новый Структура);
		КонецЕсли;
		
	ИначеЕсли (Форма.ИмяФормы = "Документ.ВозвратТоваровОтПокупателя.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ПриобретениеТоваровУслуг.Форма.ФормаДокумента") Тогда
		
		Если Форма.ПараметрыИнтеграцииГосИС.Получить("ИСМП")<>Неопределено 
			И Форма.ПараметрыИнтеграцииГосИС.Получить("ИСМП").ВидыПродукции.Количество() > 0 
			И Форма.ПараметрыИнтеграцииГосИС.Получить(
				Форма.ПараметрыИнтеграцииГосИС.Получить("ИСМП").ВидыПродукции[0])[0].ИспользоватьКолонкуСтатусаПроверкиПодбора Тогда
			СтруктураДействий.Вставить("ЗаполнитьВидПродукцииИС", Новый Структура("Номенклатура", "ВидПродукцииИС"));
			СтруктураДействий.Вставить("ЗаполнитьПризнакМаркируемаяПродукция", Новый Структура("Номенклатура", "МаркируемаяПродукция"));
			СтруктураДействий.Вставить("СнятьПризнакМаркируемаяПродукцияАлкоголь", Новый Структура);
		КонецЕсли;
			
	КонецЕсли;
	//-- Локализация
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область Локализация

//++ Локализация

// Пересчитывает количество товара ВЕТИС в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока			- Структура - Структура со свойствами строки документа.
//	СтруктураДействий		- Структура - Структура с действиями, которые нужно произвести.
//	КэшированныеЗначения	- Структура - Сохраненные значения параметров, используемых при обработке строки таблицы.
//
Процедура ПересчитатьКоличествоЕдиницВЕТИСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Перем ПараметрыПересчетаКоличестваВЕТИС, ТекстОшибки;
	
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоЕдиницВЕТИС", ПараметрыПересчетаКоличестваВЕТИС) Тогда
		
		ПараметрыПересчета = ОбработкаТабличнойЧастиКлиентСервер.НормализоватьПараметрыПересчетаЕдиниц(ТекущаяСтрока, ПараметрыПересчетаКоличестваВЕТИС);
		
		КоличествоВЕТИС = ИнтеграцияВЕТИСУТКлиентСервер.ПересчитатьКоличествоЕдиницВЕТИС(
											ТекущаяСтрока["Количество"+ПараметрыПересчетаКоличестваВЕТИС.Суффикс],
											ПараметрыПересчета.Номенклатура,
											ПараметрыПересчета.Упаковка,
											ПараметрыПересчета.НужноОкруглять,
											КэшированныеЗначения,
											ТекстОшибки);
		
		Если КоличествоВЕТИС <> Неопределено Тогда
			ИмяКоличестваВЕТИС = "Количество" + ПараметрыПересчетаКоличестваВЕТИС.Суффикс + "ВЕТИС";
			
			ТекущаяСтрока[ИмяКоличестваВЕТИС] = КоличествоВЕТИС;
		ИначеЕсли ТекстОшибки <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает количество товара (в единицах хранения) в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока			- Структура - Структура со свойствами строки документа.
//	СтруктураДействий		- Структура - Структура с действиями, которые нужно произвести.
//	КэшированныеЗначения	- Структура - Сохраненные значения параметров, используемых при обработке строки таблицы.
//
Процедура ПересчитатьКоличествоЕдиницПоВЕТИСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Перем ПараметрыПересчетаКоличества, ТекстОшибки;
	
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоЕдиницПоВЕТИС", ПараметрыПересчетаКоличества) Тогда
		
		ПараметрыПересчета = ОбработкаТабличнойЧастиКлиентСервер.НормализоватьПараметрыПересчетаЕдиниц(ТекущаяСтрока, ПараметрыПересчетаКоличества);
		ИмяКоличестваВЕТИС = "Количество" + ПараметрыПересчетаКоличества.Суффикс + "ВЕТИС";
		
		Количество = ИнтеграцияВЕТИСУТКлиентСервер.ПересчитатьКоличествоЕдиниц(
											ТекущаяСтрока[ИмяКоличестваВЕТИС],
											ПараметрыПересчета.Номенклатура,
											ПараметрыПересчета.Упаковка,
											ПараметрыПересчета.НужноОкруглять,
											КэшированныеЗначения,
											ТекстОшибки);
											
		Если Количество <> Неопределено Тогда
			ТекущаяСтрока["Количество" + ПараметрыПересчетаКоличества.Суффикс] = Количество;
		ИначеЕсли ТекстОшибки <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает сведения о коэффициенте пересчета единицы измерения ВЕТИС.
//
// Параметры:
//	ЕдиницаИзмеренияВЕТИС	- СправочникСсылка.ЕдиницыИзмеренияВЕТИС	- Единица измерения ВЕТИС, коэффициент которой нужно 
//																		получить.
//	КэшированныеЗначения	- Структура									- Сохраненные значения параметров, используемых при обработке 
//																		строки таблицы.
//	Номенклатура			- СправочникСсылка.Номенклатура				- Номенклатура для единицы хранения, которой осуществляется 
//																		получение коэффициента пересчета.
//
// Возвращаемое значение:
//	Структура - Структура со свойствами:
//		* КодОшибки						- Число				- Код ошибки получения коэффициента.
//																0 - Нет ошибок;
//																1 - Не заполнена единица измерения в справочнике 'ЕдиницыИзмеренияВЕТИС';
//																2 - В справочнике 'Номенклатура' выключена возможность пересчета количества 
//																	в соответствующую мерную единицу измерения;
//																3 - Не удалось сопоставить единицу хранения справочника 'Номенклатура' 
//																	с единицей измерения справочника 'ЕдиницыИзмеренияВЕТИС'.
//		* Коэффициент					- Число				- Коэффициент пересчета единицы измерения ВЕТИС.
//		* ТипИзмеряемойВеличины			- ПеречислениеСсылка.ТипыИзмеряемыхВеличин - Тип измеряемой величины единицы измерения 
//																						справочника 'ЕдиницыИзмеренияВЕТИС'.
//		* НужноОкруглятьКоличество		- Булево - Признак необходимости округления количества при пересчете.
//		* НужноОкруглятьКоличествоВЕТИС	- Булево - Признак необходимости округления количества ВетИС при пересчете.
//
Функция ПолучитьКоэффициентЕдиницыИзмеренияВЕТИС(ЕдиницаИзмеренияВЕТИС, КэшированныеЗначения, Номенклатура = Неопределено) Экспорт
	
	ИменаКлючей = "КодОшибки, Коэффициент, ТипИзмеряемойВеличины, НужноОкруглятьКоличество,"
					+ "НужноОкруглятьКоличествоВЕТИС";
	
	Результат = Новый Структура(ИменаКлючей);
	
	Если ЗначениеЗаполнено(ЕдиницаИзмеренияВЕТИС) Тогда
		
		КлючКоэффициента = ОбработкаТабличнойЧастиКлиентСервер.КлючКэшаУпаковки(Номенклатура, ЕдиницаИзмеренияВЕТИС);
		Кэш              = КэшированныеЗначения.КоэффициентыУпаковок[КлючКоэффициента];
		
		Если Кэш = Неопределено Тогда
			#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
				ЗначенияРеквизитов = ОбработкаТабличнойЧастиСерверЛокализация.ДанныеЕдиницыИзмеренияВЕТИС(
										ЕдиницаИзмеренияВЕТИС, 
										Номенклатура,
										КэшированныеЗначения);
				
				ЗаполнитьЗначенияСвойств(Результат, ЗначенияРеквизитов);
			#Иначе
				ТекстИсключения = НСтр("ru='Попытка получения коэффициента единицы измерения ВетИС на клиенте.'");
				ВызватьИсключение ТекстИсключения;
			#КонецЕсли
		Иначе
			Результат = Кэш;
		КонецЕсли;
	Иначе
		Результат.КодОшибки                     = 0;
		Результат.Коэффициент                   = 1;
		Результат.ТипИзмеряемойВеличины         = Неопределено;
		Результат.НужноОкруглятьКоличество      = Ложь;
		Результат.НужноОкруглятьКоличествоВЕТИС = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Пересчитывает количество товара (в единицах хранения ФГИС Зерно) в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока			- Структура - Структура со свойствами строки документа.
//	СтруктураДействий		- Структура - Структура с действиями, которые нужно произвести.
//	КэшированныеЗначения	- Структура - Сохраненные значения параметров, используемых при обработке строки таблицы.
//
Процедура ПересчитатьКоличествоЕдиницПоЗЕРНОВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Перем ПараметрыПересчетаКоличества;
	
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоЕдиницПоЗЕРНО", ПараметрыПересчетаКоличества) Тогда
		ПараметрыПересчета = ОбработкаТабличнойЧастиКлиентСервер.НормализоватьПараметрыПересчетаЕдиниц(ТекущаяСтрока, ПараметрыПересчетаКоличества);
		Если ТекущаяСтрока.Количество > 0 Тогда
			Если ЗначениеЗаполнено(ПараметрыПересчета.Номенклатура) Тогда
				Коэффициент = КоэффициентУпаковкиЗерно(ПараметрыПересчета, КэшированныеЗначения);
				Если Не ЗначениеЗаполнено(Коэффициент) Тогда
					Коэффициент = 1;
				КонецЕсли;
				ТекущаяСтрока.КоличествоЗЕРНО = ТекущаяСтрока.Количество / Коэффициент;
			Иначе
				ТекущаяСтрока.Количество = 0;
			КонецЕсли;
		ИначеЕсли ЗначениеЗаполнено(ПараметрыПересчета.Номенклатура)
			И ТекущаяСтрока.КоличествоЗЕРНО > 0 Тогда
			Коэффициент = КоэффициентУпаковкиЗерно(ПараметрыПересчета, КэшированныеЗначения);
			ТекущаяСтрока.Количество = ТекущаяСтрока.КоличествоЗЕРНО * Коэффициент;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает тип измеряемой величины и рассчитывает зависимые поля.
//
// Параметры:
//	ТекущаяСтрока			- Структура - Структура со свойствами строки документа.
//	СтруктураДействий		- Структура - Структура с действиями, которые нужно произвести.
//	КэшированныеЗначения	- Структура - Сохраненные значения параметров, используемых при обработке строки таблицы.
//
Процедура ПересчитатьТипИзмеряемойВеличиныСАТУРН(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	ПараметрыВыполнения = Неопределено;
	
	Если СтруктураДействий.Свойство("ПересчитатьТипИзмеряемойВеличиныСАТУРН", ПараметрыВыполнения) Тогда
		
		ПараметрыЗаполнения = ПараметрыВыполнения.ПараметрыЗаполнения;
		
		ДанныеУпаковки = ДанныеУпаковкиСАТУРН(ТекущаяСтрока.Номенклатура, ТекущаяСтрока.Упаковка, КэшированныеЗначения);
		
		// ТипИзмеряемойВеличины
		// 0 - Неизвестен
		// 1 - Объем
		// 2 - Вес
		// 3 - Вес или Объем
		ТипИзмеряемойВеличины = ТипИзмеряемойВеличиныПоДаннымУпаковки(ДанныеУпаковки);
		ТекущийТипИзмеряемойВеличины = ТекущийТипИзмеряемойВеличиныПоДаннымСтроки(ТекущаяСтрока, ТипИзмеряемойВеличины);
		
		Если ТипИзмеряемойВеличины <> 0 Тогда
			
			Если ПараметрыЗаполнения = Неопределено Тогда
				ПараметрыЗаполнения = ИнтеграцияСАТУРНКлиентСервер.ПараметрыЗаполненияТабличнойЧасти();
			КонецЕсли;
			
			Если ПараметрыЗаполнения.ПересчитатьКоличествоВУпаковкеСАТУРН Тогда
				Если ТекущийТипИзмеряемойВеличины = 1 Тогда
					ТекущаяСтрока.КоличествоВУпаковкеСАТУРН = ДанныеУпаковки.Объем;
				ИначеЕсли ТекущийТипИзмеряемойВеличины = 2 Тогда
					ТекущаяСтрока.КоличествоВУпаковкеСАТУРН = ДанныеУпаковки.Вес;
				КонецЕсли;
			КонецЕсли;
			
			Суффиксы = СтрРазделить(ПараметрыЗаполнения.КоличествоСуффикс, ",");
			Если Суффиксы.Количество() = 0 Тогда
				Суффиксы.Добавить("");
			КонецЕсли;
			
			Для Каждого Суффикс Из Суффиксы Цикл
				ИмяПоляКоличествоУпаковок = СтрШаблон("%1%2","КоличествоУпаковок", Суффикс);
				ИмяПоляКоличествоСАТУРН   = СтрШаблон("%1%2%3", "Количество", Суффикс,"САТУРН");
			Если ПараметрыЗаполнения.ПересчитатьКоличествоУпаковокПоОстаткуСАТУРН Тогда
				Если ТекущаяСтрока.КоличествоВУпаковкеСАТУРН = 0 Тогда
					ТекущаяСтрока[ИмяПоляКоличествоУпаковок] = 0;
				Иначе
					ТекущаяСтрока[ИмяПоляКоличествоУпаковок] = ТекущаяСтрока[ИмяПоляКоличествоСАТУРН] / ТекущаяСтрока.КоличествоВУпаковкеСАТУРН;
				КонецЕсли;
			ИначеЕсли ПараметрыЗаполнения.ПересчитатьКоличествоУпаковокПоСАТУРН Тогда
				Если ТекущийТипИзмеряемойВеличины = 1 Тогда
					Если ДанныеУпаковки.Объем = 0 Тогда
						ТекущаяСтрока[ИмяПоляКоличествоУпаковок] = 0;
					Иначе
						ТекущаяСтрока[ИмяПоляКоличествоУпаковок] = ТекущаяСтрока[ИмяПоляКоличествоСАТУРН] * ТекущаяСтрока.КоличествоВУпаковкеСАТУРН / ДанныеУпаковки.Объем;
					КонецЕсли;
				ИначеЕсли ТекущийТипИзмеряемойВеличины = 2 Тогда
					Если ДанныеУпаковки.Вес = 0 Тогда
						ТекущаяСтрока[ИмяПоляКоличествоУпаковок] = 0;
					Иначе
						ТекущаяСтрока[ИмяПоляКоличествоУпаковок] = ТекущаяСтрока[ИмяПоляКоличествоСАТУРН] * ТекущаяСтрока.КоличествоВУпаковкеСАТУРН / ДанныеУпаковки.Вес;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если ПараметрыЗаполнения.ПересчитатьКоличествоСАТУРН
				И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "КоличествоСАТУРН") Тогда
				ТекущаяСтрока[ИмяПоляКоличествоСАТУРН] = ТекущаяСтрока[ИмяПоляКоличествоУпаковок];
			КонецЕсли;
			
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет действия при изменении подобранного количества (поле КоличествоУпаковок) в строке таблицы формы.
//
// Параметры:
//	ТекущаяСтрока			- Структура - Структура со свойствами строки документа.
//	СтруктураДействий		- Структура - Структура с действиями, которые нужно произвести.
//	КэшированныеЗначения	- Структура - Сохраненные значения параметров, используемых при обработке строки таблицы.
//
Процедура ПересчитатьКоличествоДляСАТУРН(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	ПараметрыЗаполнения = Неопределено;
	
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоДляСАТУРН", ПараметрыЗаполнения) Тогда
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "КоличествоУпаковок") 
			И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "Упаковка") Тогда
			
			ДанныеУпаковки = ДанныеУпаковкиСАТУРН(ТекущаяСтрока.Номенклатура, ТекущаяСтрока.Упаковка, КэшированныеЗначения);
		
			Коэффициент = ДанныеУпаковки.Коэффициент;
			Если Не ЗначениеЗаполнено(Коэффициент) Тогда
				Коэффициент = 1;
			КонецЕсли;
			
			Если ПараметрыЗаполнения = Неопределено Тогда
				Суффиксы = Новый Массив;
				Суффиксы.Добавить("");
			Иначе
				Суффиксы = СтрРазделить(ПараметрыЗаполнения.КоличествоСуффикс, ",");
				Если ЗначениеЗаполнено(ПараметрыЗаполнения.КоличествоСуффикс) Тогда
					// ТипИзмеряемойВеличины
					// 0 - Неизвестен
					// 1 - Объем
					// 2 - Вес
					// 3 - Вес или Объем
					ТипИзмеряемойВеличины = ТипИзмеряемойВеличиныПоДаннымУпаковки(ДанныеУпаковки);
					ТекущийТипИзмеряемойВеличины = ТекущийТипИзмеряемойВеличиныПоДаннымСтроки(ТекущаяСтрока, ТипИзмеряемойВеличины);
				Иначе
					Суффиксы.Добавить("");
					ТипИзмеряемойВеличины = Неопределено;
				КонецЕсли;
			КонецЕсли;
			
			Для Каждого Суффикс Из Суффиксы Цикл
				
				ИмяПоляКоличествоУпаковок = СтрШаблон("%1%2","КоличествоУпаковок", Суффикс);
				ИмяПоляКоличествоСАТУРН   = СтрШаблон("%1%2%3", "Количество", Суффикс,"САТУРН");
				
				ТекущаяСтрока["Количество" + Суффикс] = ТекущаяСтрока[ИмяПоляКоличествоУпаковок] * Коэффициент;
				
				Если ПараметрыЗаполнения <> Неопределено И ПараметрыЗаполнения.ПересчитатьКоличествоСАТУРН
					И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, ИмяПоляКоличествоСАТУРН) Тогда
					Если ЗначениеЗаполнено(Суффикс) И ТипИзмеряемойВеличины <> 0 Тогда
						Если ТекущаяСтрока.КоличествоВУпаковкеСАТУРН = 0 Тогда
							ТекущаяСтрока[ИмяПоляКоличествоСАТУРН] = 0;
						ИначеЕсли ТекущийТипИзмеряемойВеличины = 1 Тогда
							ТекущаяСтрока[ИмяПоляКоличествоСАТУРН] = ТекущаяСтрока[ИмяПоляКоличествоУпаковок] * ДанныеУпаковки.Объем / ТекущаяСтрока.КоличествоВУпаковкеСАТУРН;
						ИначеЕсли ТекущийТипИзмеряемойВеличины = 2 Тогда
							ТекущаяСтрока[ИмяПоляКоличествоСАТУРН] =  ТекущаяСтрока[ИмяПоляКоличествоУпаковок] * ДанныеУпаковки.Вес/ ТекущаяСтрока.КоличествоВУпаковкеСАТУРН;
						КонецЕсли;
					Иначе
						ТекущаяСтрока[ИмяПоляКоличествоСАТУРН] = ТекущаяСтрока[ИмяПоляКоличествоУпаковок];
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

//-- Локализация

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ Локализация

// Возвращает структуру параметров для действия ЗаполнитьНоменклатуруЕГАИС.
//
// Параметры:
//  Объект - ДанныеФормыСтруктураСКоллекцией - объект чека на форме:
//   * Серии - ДанныеФормыКоллекция - ТЧ серий
//
// Возвращаемое значение:
//  Структура:
//   *ИмяКолонки - Строка
//   *Серии - ТабличнаяЧасть, ДанныеФормыКоллекция -
//
Функция ПараметрыЗаполненияНоменклатурыЕГАИСДляЧека(Объект) Экспорт
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("ИмяКолонки", "НоменклатураЕГАИС");
	ПараметрыЗаполнения.Вставить("Серии", Объект.Серии);
	Возврат ПараметрыЗаполнения;
	
КонецФункции

Функция КоэффициентУпаковкиЗерно(ПараметрыПересчета, КэшированныеЗначения)
	
	Коэффициент = КэшированныеЗначения.КоэффициентыУпаковок[ПараметрыПересчета.Номенклатура];
	Если Коэффициент = Неопределено Тогда
		Коэффициент = ИнтеграцияИСВызовСервераУТ.КоэффициентУпаковки(ПараметрыПересчета.Упаковка, ПараметрыПересчета.Номенклатура);
		КэшированныеЗначения.КоэффициентыУпаковок.Вставить(ПараметрыПересчета.Номенклатура, Коэффициент);
	КонецЕсли;
	
	Возврат Коэффициент;
	
КонецФункции

// Данные упаковки САТУРН из кэшированных значений
// 
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура
//  Упаковка - СправочникСсылка.УпаковкиЕдиницыИзмерения - Упаковка
//  КэшированныеЗначения - Неопределено, Структура - Кэшированные значения
// 
// Возвращаемое значение:
//  Неопределено - Данные упаковки САТУРНИз кэшированных значений
Функция ДанныеУпаковкиСАТУРНИзКэшированныхЗначений(Номенклатура, Упаковка = Неопределено, КэшированныеЗначения = Неопределено) Экспорт
	
	ИспользоватьКэшированныеЗначения = КэшированныеЗначения <> Неопределено;
	
	Если ИспользоватьКэшированныеЗначения Тогда
		
		Если Не КэшированныеЗначения.Свойство("КоэффициентВесОбъемУпаковки") Тогда
			КэшированныеЗначения.Вставить("КоэффициентВесОбъемУпаковки", Новый Соответствие);
		КонецЕсли;
		
		ДанныеНоменклатуры = КэшированныеЗначения.КоэффициентВесОбъемУпаковки[Номенклатура];
		Если ДанныеНоменклатуры = Неопределено Тогда
			КэшированныеЗначения.КоэффициентВесОбъемУпаковки.Вставить(Номенклатура, Новый Соответствие);
			ДанныеНоменклатуры = КэшированныеЗначения.КоэффициентВесОбъемУпаковки[Номенклатура];
		КонецЕсли;
		
		Если Упаковка = Неопределено Тогда
			КлючУпаковки = ИнтеграцияИСКлиентСерверПовтИсп.ПустоеЗначениеУпаковки();
			Если КлючУпаковки = Неопределено Тогда
				КлючУпаковки = "";
			КонецЕсли;
		Иначе
			КлючУпаковки = Упаковка;
		КонецЕсли;
		
		ДанныеУпаковки = ДанныеНоменклатуры[КлючУпаковки];
		
	Иначе
		ДанныеУпаковки = Неопределено;
	КонецЕсли;
	
	Возврат ДанныеУпаковки;
	
КонецФункции

Функция ДанныеУпаковкиСАТУРН(Номенклатура, Упаковка = Неопределено, КэшированныеЗначения = Неопределено)
	
	Результат = Новый Структура;
	Результат.Вставить("Коэффициент", 0);
	Результат.Вставить("Вес",         0);
	Результат.Вставить("Объем",       0);
	Результат.Вставить("ТипИзмеряемойВеличиныВес",   Ложь);
	Результат.Вставить("ТипИзмеряемойВеличиныОбъем", Ложь);
	
	Если Не ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ДанныеУпаковки = ДанныеУпаковкиСАТУРНИзКэшированныхЗначений(Номенклатура, Упаковка, КэшированныеЗначения);
	
	Если ДанныеУпаковки = Неопределено Тогда
		Результат = ИнтеграцияИСВызовСервера.КоэффициентВесОбъемУпаковки(Номенклатура, Упаковка, КэшированныеЗначения);
	Иначе
		ЗаполнитьЗначенияСвойств(Результат, ДанныеУпаковки);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Тип измеряемой величины по данным упаковки.
// 
// Параметры:
//  ДанныеУпаковки - Структура - Данные упаковки
// 
// Возвращаемое значение:
//  Число - Тип измеряемой величины по данным упаковки
Функция ТипИзмеряемойВеличиныПоДаннымУпаковки(ДанныеУпаковки)
	
	// ТипИзмеряемойВеличины
	// 0 - Неизвестен
	// 1 - Объем
	// 2 - Вес
	// 3 - Вес или Объем
	Если ДанныеУпаковки.ТипИзмеряемойВеличиныОбъем Тогда
		ТипИзмеряемойВеличины = 1;
	ИначеЕсли ДанныеУпаковки.ТипИзмеряемойВеличиныВес Тогда
		ТипИзмеряемойВеличины = 2;
	ИначеЕсли ДанныеУпаковки.Вес > 0 И ДанныеУпаковки.Объем > 0 Тогда
		ТипИзмеряемойВеличины = 3;
	ИначеЕсли ДанныеУпаковки.Объем > 0 Тогда
		ТипИзмеряемойВеличины = 1;
	ИначеЕсли ДанныеУпаковки.Вес > 0 Тогда
		ТипИзмеряемойВеличины = 2;
	Иначе
		ТипИзмеряемойВеличины = 0;
	КонецЕсли;
	
	Возврат ТипИзмеряемойВеличины;
	
КонецФункции

// Текущий тип измеряемой величины по данным строки.
// 
// Параметры:
//  ТекущаяСтрока - Структура - Текущая строка
//  ТипИзмеряемойВеличины -Число - Тип измеряемой величины
// 
// Возвращаемое значение:
//  Число - Текущий тип измеряемой величины по данным строки
Функция ТекущийТипИзмеряемойВеличиныПоДаннымСтроки(ТекущаяСтрока, ТипИзмеряемойВеличины)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "ТипИзмеряемойВеличиныСАТУРН") Тогда
		
		ТипИзмеряемойВеличиныОбъем = ПредопределенноеЗначение("Перечисление.ТипыИзмеряемыхВеличинСАТУРН.Объем");
		ТипИзмеряемойВеличиныВес   = ПредопределенноеЗначение("Перечисление.ТипыИзмеряемыхВеличинСАТУРН.Вес");
	
		Если Не ЗначениеЗаполнено(ТекущаяСтрока.ТипИзмеряемойВеличиныСАТУРН) Тогда
			Если ТипИзмеряемойВеличины = 1 Или ТипИзмеряемойВеличины = 3 Тогда
				ТекущаяСтрока.ТипИзмеряемойВеличиныСАТУРН = ТипИзмеряемойВеличиныОбъем;
			ИначеЕсли ТипИзмеряемойВеличины = 2 Или ТипИзмеряемойВеличины = 0 Тогда
				ТекущаяСтрока.ТипИзмеряемойВеличиныСАТУРН = ТипИзмеряемойВеличиныВес;
			КонецЕсли;
		КонецЕсли;
		
		Если ТекущаяСтрока.ТипИзмеряемойВеличиныСАТУРН = ТипИзмеряемойВеличиныОбъем Тогда
			ТекущийТипИзмеряемойВеличины = 1;
		ИначеЕсли ТекущаяСтрока.ТипИзмеряемойВеличиныСАТУРН = ТипИзмеряемойВеличиныВес  Тогда
			ТекущийТипИзмеряемойВеличины = 2;
		Иначе
			ТекущийТипИзмеряемойВеличины = 0;
		КонецЕсли;
	Иначе
		ТекущийТипИзмеряемойВеличины = 0;
	КонецЕсли;
	
	Возврат ТекущийТипИзмеряемойВеличины;
	
КонецФункции

//-- Локализация
#КонецОбласти