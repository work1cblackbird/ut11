
#Область ПрограммныйИнтерфейс

#Область ОбработчикиСобытий

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив из Строка - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация


	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект.ОтчетКомитентуОЗакупках - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив из Строка- Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	//++ Локализация
	УчетПрослеживаемыхТоваровЛокализация.ПроверитьДанныеПрослеживаемостиНомеровГТД(Объект, Объект.Товары, Объект.Дата);
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект.ОтчетКомитентуОЗакупках - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	//++ Локализация
	
	// Счет-фактура
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "СчетФактура";
	КомандаПечати.Представление = НСтр("ru = 'Счет-фактура'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВВалюте", Ложь);

	// УПД (Универсальный передаточный документ)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "УПД";
	КомандаПечати.Представление = НСтр("ru = 'УПД (Комиссионное вознаграждение)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// УПД (Универсальный передаточный документ)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "УПД";
	КомандаПечати.Представление = НСтр("ru = 'УПД (Сведения о закупки комиссионером)'");
	КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьТовары", Истина);
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;

	// Универсальный корректировочный документ (УКД)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "УКД";
	КомандаПечати.Представление = НСтр("ru = 'УКД (Сведения о закупки комиссионером)'");
	КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьТовары", Истина);
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	//++ Локализация
	//-- Локализация
КонецПроцедуры

//++ Локализация

// Формирует текст запроса для получения данных основания при печати Счет-фактуры.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстЗапросаДанныхОснованияДляПечатнойФормыСчетФактура() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка                                   КАК Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
	|	ДанныеДокументов.Валюта                                   КАК Валюта,
	|	ДанныеДокументов.Организация                              КАК Организация,
	|	ДанныеДокументов.НалогообложениеНДС                       КАК НалогообложениеНДС,
	|	ДанныеДокументов.Подразделение                            КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)                  КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)             КАК Грузоотправитель,
	|	ДанныеДокументов.Организация                              КАК Грузополучатель,
	|	ЛОЖЬ                                                      КАК РасчетыЧерезОтдельногоКонтрагента,
	|	НЕОПРЕДЕЛЕНО                                              КАК Номенклатура,
	|	""""                                                      КАК Содержание,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)             КАК Комиссионер,
	|	НЕОПРЕДЕЛЕНО                                              КАК Основание,
	|	НЕОПРЕДЕЛЕНО                                              КАК ОснованиеДата,
	|	НЕОПРЕДЕЛЕНО                                              КАК ОснованиеНомер,
	|	НЕОПРЕДЕЛЕНО                                              КАК БанковскийСчетОрганизации,
	|	НЕОПРЕДЕЛЕНО                                              КАК БанковскийСчетКонтрагента,
	|	НЕОПРЕДЕЛЕНО                                              КАК БанковскийСчетГрузоотправителя,
	|	НЕОПРЕДЕЛЕНО                                              КАК БанковскийСчетГрузополучателя,
	|	НЕОПРЕДЕЛЕНО                                              КАК ДоверенностьНомер,
	|	НЕОПРЕДЕЛЕНО                                              КАК ДоверенностьДата,
	|	НЕОПРЕДЕЛЕНО                                              КАК ДоверенностьВыдана,
	|	НЕОПРЕДЕЛЕНО                                              КАК ДоверенностьЛицо,
	|	ДанныеДокументов.Менеджер                                 КАК Менеджер,
	|	НЕОПРЕДЕЛЕНО                                              КАК Кладовщик,
	|	НЕОПРЕДЕЛЕНО                                              КАК ДолжностьКладовщика,
	|	ДанныеДокументов.Дата                                     КАК Период
	|
	|//ОператорПОМЕСТИТЬ
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках КАК ДанныеДокументов
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&ДокументОснование_ОтчетКомитентуОЗакупках)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает данные для формирования печатной формы УПД
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.ОтчетКомитентуОЗакупках - ссылки на документы, по которым необходимо
//																		получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемая при печати УПД:
//		* РезультатПоШапке - РезультатЗапроса - общие данные накладной
//		* РезультатПоТабличнойЧасти - РезультатЗапроса - данные табличной части накладной.
//
Функция ПолучитьДанныеДляПечатнойФормыУПД(ПараметрыПечати, МассивОбъектов) Экспорт
	
	Если Не ПараметрыПечати = Неопределено
		И ПараметрыПечати.Свойство("ВыводитьТовары")
		И ПараметрыПечати.ВыводитьТовары Тогда
		ДанныеПечатнойФормы = ПолучитьДанныеДляПечатнойФормыУПД_Товары(ПараметрыПечати, МассивОбъектов);
	Иначе
		ДанныеПечатнойФормы = ПолучитьДанныеДляПечатнойФормыУПД_Вознаграждение(ПараметрыПечати, МассивОбъектов);
	КонецЕсли;
	
	Возврат ДанныеПечатнойФормы;
	
КонецФункции

// Функция получает данные для формирования печатной формы УКД
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.ОтчетКомитентуОЗакупках - Массив ссылок на документы, 
//																	по которым необходимо получить данные.
//
// Возвращаемое значение:
//	- Неопределено.
// 	- Структура:
// 		* РезультатПоШапке - РезультатЗапроса
// 		* РезультатПоИсходнымДанным - РезультатЗапроса
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса
// 		* ПрослеживаемыеКомплектующие - см. УчетПрослеживаемыхТоваровЛокализация.ПрослеживаемыеКомплектующиеДляПечатиДанных
// 		* Маркировка - см. ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое
// 		* МаркировкаДо - см. ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое
// 		* РезультатПоПоставщикам - РезультатЗапроса
// 		* РезультатПоКонтрагентам - РезультатЗапроса.
//
Функция ПолучитьДанныеДляПечатнойФормыУКД(ПараметрыПечати, МассивОбъектов) Экспорт
	
	Если Не ПараметрыПечати = Неопределено
		И ПараметрыПечати.Свойство("ВыводитьТовары")
		И ПараметрыПечати.ВыводитьТовары Тогда
		ДанныеПечатнойФормы = ПолучитьДанныеДляПечатнойФормыУКД_Товары(ПараметрыПечати, МассивОбъектов);
	Иначе
		ДанныеПечатнойФормы = Неопределено;
	КонецЕсли;
	
	Возврат ДанныеПечатнойФормы;

КонецФункции

// Формирует временную таблицу, содержащую табличную часть по таблице данных документов.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц, содержащий таблицу ТаблицаДанныхДокументов с полями:
//		Ссылка.
//
//	ПараметрыЗаполнения - Структура - структура, возвращаемая функцией ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров.
//
Процедура ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения = Неопределено) Экспорт
	
	Если ПараметрыЗаполнения = Неопределено Тогда
		ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета",     Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ПересчитыватьВВалютуРегл",       ПараметрыЗаполнения.ПересчитыватьВВалютуРегл);
	
	Если ПараметрыЗаполнения.ПересчитыватьВВалютуРегл Тогда
		Если ПараметрыЗаполнения.АктуализироватьРасчеты И НЕ ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
			Запрос.Текст = "
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
			|ИЗ
			|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
			|
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|		ТаблицаДанныхДокументов КАК ДанныеДокументов
			|	ПО
			|		РасчетыСКлиентами.Регистратор = ДанныеДокументов.Ссылка
			|
			|ГДЕ
			|	ДанныеДокументов.Валюта <> ДанныеДокументов.Ссылка.Организация.ВалютаРегламентированногоУчета
			|	И РасчетыСКлиентами.Активность
			|";
			ТаблицаАналитик = Запрос.Выполнить().Выгрузить();
			МассивАналитикУчетаПоПартнерам = ТаблицаАналитик.ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
			
			Если МассивАналитикУчетаПоПартнерам.Количество() > 0 Тогда
				ОкончаниеПериодаРасчета = КонецМесяца(ВзаиморасчетыСервер.ПолучитьМаксимальнуюДатуВКоллекцииДокументов(МенеджерВременныхТаблиц)) + 1;
				АналитикиРасчета = РаспределениеВзаиморасчетовВызовСервера.АналитикиРасчета();
				АналитикиРасчета.АналитикиУчетаПоПартнерам = МассивАналитикУчетаПоПартнерам;
				Попытка
					РаспределениеВзаиморасчетовВызовСервера.РаспределитьВсеРасчетыСКлиентами(ОкончаниеПериодаРасчета, АналитикиРасчета);
				Исключение
					ТекстСообщения = НСтр("ru ='Печатная форма сформирована по неактуальным данным.
					|Необходимо актуализировать взаиморасчеты вручную и переформировать печатную форму.'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				КонецПопытки;
			КонецЕсли;
		Иначе
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДанныеДокументов.Ссылка КАК Ссылка
			|ИЗ
			|	ТаблицаДанныхДокументов КАК ДанныеДокументов
			|ГДЕ 
			|	ДанныеДокументов.Валюта <> ДанныеДокументов.Ссылка.Организация.ВалютаРегламентированногоУчета
			|	ИЛИ ДанныеДокументов.Валюта <> &ВалютаУправленческогоУчета";
			МассивДокументов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
			РегистрыСведений.СуммыДокументовВВалютахУчета.РассчитатьСуммыДокументовВВалютахУчета(МассивДокументов);
			
		КонецЕсли;
	КонецЕсли;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                      КАК Ссылка,
	|	СчетаФактурыОснования_Шапка.Ссылка                           КАК СсылкаСФ,
	|	1                                                            КАК НомерСтроки,
	|	ТаблицаДокумента.Ссылка.Услуга                                      КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)          КАК Серия,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)                  КАК НомерГТД,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)       КАК Упаковка,
	|	1                                                            КАК Количество,
	|	1                                                            КАК КоличествоУпаковок,
	|	0                                                            КАК КоличествоПоРНПТ,
	|	ТаблицаДокумента.Ссылка.СтавкаНДСВознаграждения              КАК СтавкаНДС,
	|
	|	СУММА(ЕСТЬNULL(
	|		СуммыДокументовВВалютахУчета.БазаНДСРегл,
	|		ТаблицаДокумента.СуммаВознаграждения - ТаблицаДокумента.СуммаНДСВознаграждения
	|	))                                                            КАК СуммаБезНДС,
	|	СУММА(ЕСТЬNULL(
	|		СуммыДокументовВВалютахУчета.СуммаНДСРегл,
	|		ТаблицаДокумента.СуммаНДСВознаграждения
	|	))                                                            КАК СуммаНДС,
	|	
	|	ЛОЖЬ                                                         КАК ЭтоТовар,
	|	ЛОЖЬ                                                         КАК ВернутьМногооборотнуюТару
	|
	|ПОМЕСТИТЬ ОтчетКомитентуОЗакупкахТаблицаТоваров
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках.Товары КАК ТаблицаДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО
	|		ТаблицаДокумента.Ссылка = ДанныеДокументов.Ссылка
	|	
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		СчетаФактурыОснования_Шапка КАК СчетаФактурыОснования_Шапка
	|	ПО
	|		ТаблицаДокумента.Ссылка = СчетаФактурыОснования_Шапка.ДокументОснование
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|	ПО
	|		ТаблицаДокумента.Ссылка = СуммыДокументовВВалютахУчета.Регистратор
	|		И СуммыДокументовВВалютахУчета.ИдентификаторСтроки = ТаблицаДокумента.ИдентификаторСтрокиВознаграждения
	|		И СуммыДокументовВВалютахУчета.Активность
	|		И &ПересчитыватьВВалютуРегл
	|
	|	ГДЕ
	|		НЕ СчетаФактурыОснования_Шапка.Перевыставленный
	|
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаДокумента.Ссылка,
	|		СчетаФактурыОснования_Шапка.Ссылка,
	|		ТаблицаДокумента.Ссылка.Услуга,
	|		ТаблицаДокумента.Ссылка.СтавкаНДСВознаграждения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка                                      КАК Ссылка,
	|	СчетаФактурыОснования_Шапка.Ссылка                           КАК СсылкаСФ,
	|	ТаблицаДокумента.НомерСтроки                                 КАК НомерСтроки,
	|	ТаблицаДокумента.Номенклатура                                КАК Номенклатура,
	|	ТаблицаДокумента.Характеристика                              КАК Характеристика,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Серия            КАК Серия,
	|	ТаблицаДокумента.НомерГТД                                    КАК НомерГТД,
	|	ТаблицаДокумента.Упаковка                                    КАК Упаковка,
	|	ТаблицаДокумента.Количество                                  КАК Количество,
	|	ТаблицаДокумента.КоличествоУпаковок                          КАК КоличествоУпаковок,
	|	ТаблицаДокумента.КоличествоПоРНПТ                            КАК КоличествоПоРНПТ,
	|	ТаблицаДокумента.СтавкаНДС                                   КАК СтавкаНДС,
	|
	|	ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС       КАК СуммаБезНДС,
	|	ТаблицаДокумента.СуммаНДС                                    КАК СуммаНДС,
	|	
	|	ВЫБОР КОГДА ТаблицаДокумента.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                                        КАК ЭтоТовар,
	|	ЛОЖЬ                                                         КАК ВернутьМногооборотнуюТару
	|
	|
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках.Товары КАК ТаблицаДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО
	|		ТаблицаДокумента.Ссылка = ДанныеДокументов.Ссылка
	|	
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		СчетаФактурыОснования_Шапка КАК СчетаФактурыОснования_Шапка
	|	ПО
	|		ТаблицаДокумента.Ссылка = СчетаФактурыОснования_Шапка.ДокументОснование
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.СчетФактураВыданный.Товары КАК ТоварыСчетаФактуры
	|	ПО
	|		СчетаФактурыОснования_Шапка.Ссылка = ТоварыСчетаФактуры.Ссылка
	|		И ТоварыСчетаФактуры.СчетФактураПолученныйОтПродавца = ТаблицаДокумента.СчетФактураПолученный
	|
	|	ГДЕ
	|		 СчетаФактурыОснования_Шапка.Перевыставленный
	|;
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

//-- Локализация

Процедура СформироватьКомплектПечатныхФорм(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, КомплектПечатныхФорм) Экспорт
	
	//++ Локализация
		
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"СчетФактура");
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.ОтчетКомитентуОЗакупках", КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуСчетФактура(СтруктураТипов, ОбъектыПечати, Новый Структура("ПечатьВВалюте", Ложь)));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"УПД");
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.ОтчетКомитентуОЗакупках", КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуУПД(СтруктураТипов, ОбъектыПечати, Новый Структура("ПечатьВВалюте, ВыводитьТовары", Ложь, Ложь)));
	КонецЦикла;
		
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"УПДТовары");
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.ОтчетКомитентуОЗакупках", КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуУПД(СтруктураТипов, ОбъектыПечати, Новый Структура("ПечатьВВалюте, ВыводитьТовары", Ложь, Истина)));
		КонецЦикла;
		
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"УКДТовары");
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.ОтчетКомитентуОЗакупках", КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуУКД(СтруктураТипов, ОбъектыПечати, Новый Структура("ПечатьВВалюте, ВыводитьТовары", Ложь, Истина)));
		КонецЦикла;
	//-- Локализация
	
КонецПроцедуры

Процедура КомплектПечатныхФорм(КомплектПечатныхФорм) Экспорт
	
	//++ Локализация
	
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм, "СчетФактура", НСтр("ru = 'Счет-фактуры по отчету'"),            1);
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм, "УПД", НСтр("ru = 'УПД (Комиссионное вознаграждение)'"), 0);
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм, "УПДТовары", НСтр("ru = 'УПД (Сведения о закупки комиссионером)'"), 0);
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм, "УКДТовары", НСтр("ru = 'УКД (Сведения о закупки комиссионером)'"), 0);
	
	//-- Локализация
	
КонецПроцедуры

//++ Локализация
#Область СчетНаОплату_1_01

// Возвращает данные для формирования электронного Счета на оплату.
//
// Параметры:
//	МассивОбъектов - Массив из ДокументСсылка.ОтчетКомитентуОЗакупках - ссылки на документы, по которым необходимо
//																		получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемая для формирования электронного Счета на оплату, содержащая следующие свойства:
//		* РезультатПоШапке - РезультатЗапроса - общие данные документа.
//		* РезультатПоЭтапамОплаты - РезультатЗапроса - данные графика оплаты по документу.
//		* РезультатПоТабличнойЧасти - РезультатЗапроса - данные табличной части документа.
//
Функция ДанныеДокументовДляСчетаНаОплату_1_01(МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДанныхДокументовДляСчетаНаОплату();
	
	Запрос.УстановитьПараметр("МассивОбъектов",	МассивОбъектов);
	Запрос.УстановитьПараметр("КолонкаКодов",	КолонкаКодов);
	
	ПакетРезультатаЗапроса		= Запрос.ВыполнитьПакет();
	МаксимальныйИндексПакета	= ПакетРезультатаЗапроса.ВГраница();
	РезультатПоШапке			= ПакетРезультатаЗапроса[МаксимальныйИндексПакета - 2];
	РезультатПоЭтапамОплаты		= ПакетРезультатаЗапроса[МаксимальныйИндексПакета - 1];
	РезультатПоТабличнойЧасти	= ПакетРезультатаЗапроса[МаксимальныйИндексПакета];
	
	ДанныеДокументов = Новый Структура;
	ДанныеДокументов.Вставить("РезультатПоШапке",			РезультатПоШапке);
	ДанныеДокументов.Вставить("РезультатПоЭтапамОплаты",	РезультатПоЭтапамОплаты);
	ДанныеДокументов.Вставить("РезультатПоТабличнойЧасти",	РезультатПоТабличнойЧасти);
	
	Возврат ДанныеДокументов;
	
КонецФункции

// Возвращает текст запроса для получения данных о товарах отчета-основания, используемого при формировании ЭД
// Счета на оплату.
//
// Параметры:
//	ЭтоТекстВТ - Булево - признак необходимости формирования текста запроса временной таблицы. Если значение Истина,
//							тогда формируется текст временной таблицы.
//
// Возвращаемое значение:
//	Строка - текст запроса для получения данных.
//
Функция ТекстЗапросаВТТоваровОснованияСчетаНаОплату_1_01(ЭтоТекстВТ = Истина) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТоварыДокумента.Ссылка				КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО						КАК ХозяйственнаяОперация,
	|	ТоварыДокумента.НомерСтроки			КАК НомерСтроки,
	|	NULL								КАК ВариантКомплектацииНоменклатуры,
	|	&ПустойВариантПредставленияНаборов	КАК ВариантПредставленияНабораВПечатныхФормах,
	|	&ПустойВариантРасчетаЦенНаборов		КАК ВариантРасчетаЦеныНабора,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)	КАК НоменклатураНабора,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)	КАК ХарактеристикаНабора,
	|	ТоварыДокумента.Номенклатура		КАК Номенклатура,
	|	ТоварыДокумента.Характеристика		КАК Характеристика,
	|	ТоварыДокумента.Упаковка			КАК Упаковка,
	|	ТоварыДокумента.Количество			КАК Количество,
	|	ТоварыДокумента.КоличествоУпаковок	КАК КоличествоУпаковок,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ЦенаВключаетНДС
	|			ТОГДА (ТоварыДокумента.Сумма - ТоварыДокумента.СуммаНДС) / ТоварыДокумента.КоличествоУпаковок
	|		ИНАЧЕ ТоварыДокумента.Цена
	|	КОНЕЦ								КАК Цена,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ЦенаВключаетНДС
	|			ТОГДА ТоварыДокумента.Сумма - ТоварыДокумента.СуммаНДС
	|		ИНАЧЕ ТоварыДокумента.Сумма
	|	КОНЕЦ								КАК СтоимостьБезНДС,
	|	ТоварыДокумента.СтавкаНДС			КАК СтавкаНДС,
	|	ТоварыДокумента.СуммаСНДС			КАК СтоимостьСНДС,
	|	0									КАК СуммаСкидки,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ЦенаВключаетНДС
	|			ТОГДА (ТоварыДокумента.Сумма - ТоварыДокумента.СуммаНДС) / ТоварыДокумента.КоличествоУпаковок
	|		ИНАЧЕ ТоварыДокумента.Цена
	|	КОНЕЦ								КАК ЦенаБезСкидки,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ЦенаВключаетНДС
	|			ТОГДА ТоварыДокумента.Сумма - ТоварыДокумента.СуммаНДС
	|		ИНАЧЕ ТоварыДокумента.Сумма
	|	КОНЕЦ								КАК СтоимостьБезНДСБезСкидки,
	|	ТоварыДокумента.СуммаСНДС			КАК СтоимостьСНДСБезСкидки,
	|	ТоварыДокумента.СуммаНДС			КАК СуммаНДС,
	|	""""								КАК Содержание,
	|	ВЫБОР
	|		КОГДА ТоварыДокумента.Номенклатура.ТипНоменклатуры В(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|															ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ								КАК ЭтоТовар,
	|	ЛОЖЬ								КАК ЭтоВозвратнаяТара
	|ПОМЕСТИТЬ ТоварыОснования
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках.Товары КАК ТоварыДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОснований КАК ТаблицаОснований
	|		ПО ТоварыДокумента.Ссылка = ТаблицаОснований.ДокументОснование
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомитентуОЗакупках КАК ДанныеДокумента
	|		ПО ТоварыДокумента.Ссылка = ДанныеДокумента.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыДокумента.Ссылка				КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО						КАК ХозяйственнаяОперация,
	|	99999								КАК НомерСтроки,
	|	NULL								КАК ВариантКомплектацииНоменклатуры,
	|	&ПустойВариантПредставленияНаборов	КАК ВариантПредставленияНабораВПечатныхФормах,
	|	&ПустойВариантРасчетаЦенНаборов		КАК ВариантРасчетаЦеныНабора,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)	КАК НоменклатураНабора,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)	КАК ХарактеристикаНабора,
	|	ТоварыДокумента.Услуга				КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)	КАК Характеристика,
	|	&ПустаяУпаковка						КАК Упаковка,
	|	1									КАК Количество,
	|	1									КАК КоличествоУпаковок,
	|	0									КАК Цена,
	|	ТоварыДокумента.СуммаВознаграждения - ТоварыДокумента.СуммаНДСВознаграждения	КАК СтоимостьБезНДС,
	|	ТоварыДокумента.СтавкаНДСВознаграждения	КАК СтавкаНДС,
	|	ТоварыДокумента.СуммаВознаграждения		КАК СтоимостьСНДС,
	|	0									КАК СуммаСкидки,
	|	0									КАК ЦенаБезСкидки,
	|	ТоварыДокумента.СуммаВознаграждения - ТоварыДокумента.СуммаНДСВознаграждения	КАК СтоимостьБезНДСБезСкидки,
	|	ТоварыДокумента.СуммаВознаграждения		КАК СтоимостьСНДСБезСкидки,
	|	ТоварыДокумента.СуммаНДСВознаграждения	КАК СуммаНДС,
	|	""""								КАК Содержание,
	|	ЛОЖЬ								КАК ЭтоТовар,
	|	ЛОЖЬ								КАК ЭтоВозвратнаяТара
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках КАК ТоварыДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОснований КАК ТаблицаОснований
	|		ПО ТоварыДокумента.Ссылка = ТаблицаОснований.ДокументОснование
	|
	|ГДЕ
	|	ТоварыДокумента.СпособРасчетаВознаграждения <> ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаКомиссионногоВознаграждения.НеРассчитывается)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
								"&ПустойВариантПредставленияНаборов",
								"ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ПустаяСсылка)");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
								"&ПустойВариантРасчетаЦенНаборов",
								"ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ПустаяСсылка)");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
								"&ПустаяУпаковка",
								"ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)");
	
	Если Не ЭтоТекстВТ Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТоварыОснования", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти
//-- Локализация

#КонецОбласти


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ Локализация

// Параметры:
// 	Объект - ДокументОбъект
Процедура ЗаполнитьСчетаФактурыПолученные(Объект) Экспорт
	
	ДокументыПриобретения = Объект.Товары.Выгрузить(, "ДокументПриобретения");
	ДокументыПриобретения.Свернуть("ДокументПриобретения", "");
	
	СчетаФактуры = УчетНДСРФ.СчетаФактурыПолученныеПоДокументамОснованиям(ДокументыПриобретения , Истина); // см. УчетНДСУП.СчетаФактурыПолученныеПоДокументамОснованиям
	СчетаФактуры.Индексы.Добавить("ДокументОснование");
	
	Для каждого СтрТовары из Объект.Товары Цикл
		НовоеЗначениеСчетФактураПолученный = Документы.СчетФактураПолученный.ПустаяСсылка();
		СчетФактураПолученный = СчетаФактуры.НайтиСтроки(Новый Структура("ДокументОснование", СтрТовары.ДокументПриобретения));
		Если СчетФактураПолученный.Количество()>0 Тогда
			НовоеЗначениеСчетФактураПолученный = СчетФактураПолученный[0].Ссылка;
		КонецЕсли;
		Если Не СтрТовары.СчетФактураПолученный = НовоеЗначениеСчетФактураПолученный Тогда
			СтрТовары.СчетФактураПолученный = НовоеЗначениеСчетФактураПолученный;
			СтрТовары.ДатаСчетаФактуры = ?(ЗначениеЗаполнено(СтрТовары.СчетФактураПолученный),
				СчетФактураПолученный[0].ДатаСоставления, Дата(1,1,1));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
// 	Объект - ДанныеФормыСтруктура - содержит:
// 		* Товары - ДанныеФормыКоллекция
// 		* Ссылка - ДокументСсылка.ОтчетКомитентуОЗакупках
Процедура ЗаполнитьСчетаФактурыПеревыставленные(Объект) Экспорт
	
	СчетаФактурыПолученные = Объект.Товары.Выгрузить(, "СчетФактураПолученный");
	СчетаФактурыПолученные.Свернуть("СчетФактураПолученный", "");
	
	Запрос = Новый Запрос(УчетНДСРФ.ТекстЗапросаСчетовФактурПеревыставленныхКомитентамДляОтчетаКомитентуОЗакупках());
	
	Запрос.УстановитьПараметр("ДокументОснование", Объект.Ссылка);
	Запрос.УстановитьПараметр("СчетаФактурыПолученные", СчетаФактурыПолученные);
	
	УстановитьПривилегированныйРежим(Истина);
	СчетаФактурыВыданные = Запрос.Выполнить().Выгрузить();
	СчетаФактурыВыданные.Индексы.Добавить("СчетФактураПолученный");
	
	Для каждого СтрТовары Из Объект.Товары Цикл
		СчетФактураВыданный = СчетаФактурыВыданные.НайтиСтроки(Новый Структура("СчетФактураПолученный", СтрТовары.СчетФактураПолученный));
		СтрТовары.СчетФактураВыданныйКомитенту = Документы.СчетФактураВыданный.ПустаяСсылка();
		Если СчетФактураВыданный.Количество()>0 Тогда
			СтрТовары.СчетФактураВыданныйКомитенту = СчетФактураВыданный[0].СчетФактураВыданныйКомитенту; 
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает данные для формирования печатной формы УПД
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.ОтчетКомитентуОЗакупках - ссылки на документы, по которым необходимо
//																		получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемая при печати УПД, содержащая следующие свойства:
//		* РезультатПоШапке - РезультатЗапроса - общие данные накладной.
//		* РезультатПоТабличнойЧасти - РезультатЗапроса - данные табличной части накладной.
//		* РезультатПоПоставщикам - РезультатЗапроса - сведения о поставщиках.
//		* РезультатПоКонтрагентам - РезультатЗапроса - сведения о контрагентах.
//		* Маркировка - см. ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое
//		* ПрослеживаемыеКомплектующие - см. УчетПрослеживаемыхТоваровЛокализация.ПрослеживаемыеКомплектующиеДляПечатиДанных
//
Функция ПолучитьДанныеДляПечатнойФормыУПД_Товары(ПараметрыПечати, МассивОбъектов)
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка                  КАК Ссылка,
	|	ДанныеДокументов.Валюта                  КАК Валюта,
	|	ДанныеДокументов.Организация             КАК Организация,
	|	ДанныеДокументов.Контрагент              КАК Контрагент,
	|	ДанныеДокументов.Подразделение           КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|	ДанныеДокументов.ДокументПриобретения    КАК ДокументПриобретения,
	|	ДанныеДокументов.Дата                    КАК Период
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	УчетНДСРФ.ДополнитьЗапросПолученияДанныхДляПечатиУПД(Запрос, "Контрагент,ДокументПриобретения");

	Запрос.Выполнить();
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	ПараметрыЗаполнения.ВключаяНомераГТД                 = Истина;
	ПараметрыЗаполнения.ОбработатьНастройкиПечатиНаборов = Ложь;
	ПоместитьВременнуюТаблицуТоваров_Товары(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	Обработки.ПечатьОбщихФорм.ПоместитьВременнуюТаблицуДанныхПоставщика(МенеджерВременныхТаблиц);
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА НЕ ТаблицаТоваров.ЭтоТовар
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьУслуги,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(ТаблицаТоваров.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьПрослеживаемыеТовары,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ТаблицаТоваров.ЭтоТовар
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьТовары
	|ПОМЕСТИТЬ НоменклатураДокументов
	|ИЗ
	|	ОтчетКомитентуОЗакупкахТаблицаТоваров КАК ТаблицаТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	&ПредставлениеСчетФактура КАК ПредставлениеДокумента,
	|	2 КАК СтатусУПД,
	|	ВЫБОР
	|		КОГДА СчетФактура.Ссылка ЕСТЬ NULL
	|			ТОГДА ДанныеДокумента.Номер
	|		ИНАЧЕ СчетФактура.Номер
	|	КОНЕЦ КАК Номер,
	|	ВЫБОР
	|		КОГДА СчетФактура.Ссылка ЕСТЬ NULL
	|			ТОГДА ДанныеДокумента.Дата
	|		ИНАЧЕ СчетФактура.Дата
	|	КОНЕЦ КАК Дата,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправления,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправления,
	|	ЛОЖЬ КАК Исправление,
	|	ВЫБОР
	|		КОГДА СчетФактура.Ссылка ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ СчетФактура.Номер
	|	КОНЕЦ КАК НомерСчетаФактуры,
	|	ВЫБОР
	|		КОГДА СчетФактура.Ссылка ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ СчетФактура.Дата
	|	КОНЕЦ КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправленияСчетаФактуры,
	|	ЛОЖЬ КАК КорректировочныйСчетФактура,
	|	"""" КАК СтрокаПоДокументу,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаСчетаФактуры,
	|	ДанныеДокумента.Партнер КАК Партнер,
	|
	|	ДанныеДокумента.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДокументПриобретениеТоваровУслуг.Контрагент КАК Поставщик,
	|	ДокументПриобретениеТоваровУслуг.Контрагент.КПП КАК КПППоставщика,
	|
	|
	|	ДокументПриобретениеТоваровУслуг.Контрагент КАК Контрагент,
	|	ДокументПриобретениеТоваровУслуг.Контрагент КАК Грузоотправитель,
	|	ДанныеДокумента.Организация КАК Грузополучатель,
	|
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетОрганизации,
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетКонтрагента,
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетГрузоотправителя,
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетГрузополучателя,
	|
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|
	|	ДанныеДокумента.Организация.КПП КАК КПППокупателя,
	|
	|	ДанныеДокумента.Организация.Префикс КАК Префикс,
	|	"""" КАК АдресДоставки,
	|
	|	ДанныеДокумента.Валюта КАК Валюта,
	|	ДанныеДокумента.Валюта.НаименованиеПолное КАК ВалютаНаименованиеПолное,
	|	ДанныеДокумента.Валюта.Код КАК ВалютаКод,
	|	ВЫБОР
	|		КОГДА НоменклатураДокументов.ЕстьУслуги
	|				И НЕ НоменклатураДокументов.ЕстьТовары
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТолькоУслуги,
	|	ЕСТЬNULL(НоменклатураДокументов.ЕстьПрослеживаемыеТовары, ЛОЖЬ) КАК ЕстьПрослеживаемыеТовары,
	|	ЛОЖЬ КАК ЭтоПередачаНаКомиссию,
	|	НЕОПРЕДЕЛЕНО КАК Основание,
	|	НЕОПРЕДЕЛЕНО КАК ОснованиеДата,
	|	НЕОПРЕДЕЛЕНО КАК ОснованиеНомер,
	|	"""" КАК ДоверенностьНомер,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьДата,
	|	"""" КАК ДоверенностьВыдана,
	|	"""" КАК ДоверенностьЛицо,
	|	"""" КАК Кладовщик,
	|	НЕОПРЕДЕЛЕНО КАК ДолжностьКладовщика,
	|	ДанныеДокументов.ТребуетсяНаличиеСФ КАК ТребуетсяНаличиеСФ,
	|	ИСТИНА КАК ПеревыставленныйСчетФактура,
	|	""ПредставлениеСтроки5а"" КАК ПредставлениеСтроки5а,
	|	ЕСТЬNULL(ДанныеПоставщика.ИндексПодразделения, 0) КАК ИндексПодразделения
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ДанныеДокумента.Ссылка = ДанныеДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоставщика КАК ДанныеПоставщика
	|		ПО ДанныеДокумента.Ссылка = ДанныеПоставщика.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураДокументов КАК НоменклатураДокументов
	|		ПО ДанныеДокумента.Ссылка = НоменклатураДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацииВНалоговомОргане КАК ДанныеПодразделений
	|		ПО ДанныеПодразделений.Организация = ДанныеДокумента.Организация
	|			И ДанныеПодразделений.Подразделение = ДанныеДокумента.Подразделение
	|			И ДанныеПодразделений.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ДокументПриобретениеТоваровУслуг
	|		ПО ДокументПриобретениеТоваровУслуг.Ссылка = ДанныеДокумента.ДокументПриобретения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК Договоры
	|		ПО Договоры.Ссылка = ДанныеДокумента.Договор
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактура
	|		ПО СчетФактура.ДокументОснование = ДанныеДокумента.Ссылка
	|		И СчетФактура.Перевыставленный
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Ссылка                     КАК ТекущийОтчет,
	|	Товары.Ссылка.Дата                КАК ДатаОтчета,
	|	Товары.Ссылка.МоментВремени       КАК МоментВремениОтчета,
	|	Товары.Ссылка.Организация         КАК Организация,
	|	ВЫБОР
	|		КОГДА Товары.ДокументПриобретения ССЫЛКА Документ.КорректировкаПриобретения
	|			ТОГДА Товары.ДокументПриобретения.ДокументОснование
	|		КОГДА Товары.ДокументПриобретения ССЫЛКА Документ.ВозвратТоваровПоставщику
	|			ТОГДА Товары.ДокументПриобретения.ДокументПоступления
	|		ИНАЧЕ Товары.ДокументПриобретения
	|	КОНЕЦ                             КАК ДокументОснование,
	|	Товары.ДокументПриобретения       КАК ДокументПриобретения,
	|	Товары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Товары.НомерГТД                   КАК НомерГТД
	|ПОМЕСТИТЬ ДанныеТекущегоОтчета
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|		ПО (ТаблицаДанныхДокументов.Ссылка = Товары.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеПриобретений.Ссылка                     КАК Ссылка,
	|	ДанныеПриобретений.ДокументОснование          КАК ДокументОснование,
	|	ДанныеПриобретений.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ДанныеПриобретений.НомерГТД                   КАК НомерГТД,
	|	ДанныеПриобретений.МоментВремениОтчета        КАК МоментВремениОтчета,
	|	ДанныеПриобретений.ТекущийОтчет               КАК ТекущийОтчет,
	|	ДанныеПриобретений.ДокументПриобретения       КАК ДокументПриобретения
	|ПОМЕСТИТЬ ТаблицаОснований
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПриобретениеТоваровУслуг.Ссылка                  КАК Ссылка,
	|		ПриобретениеТоваровУслуг.Ссылка                  КАК ДокументОснование,
	|		ДокументыПриобретения.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ДокументыПриобретения.НомерГТД                   КАК НомерГТД,
	|		ДокументыПриобретения.МоментВремениОтчета        КАК МоментВремениОтчета,
	|		ДокументыПриобретения.ТекущийОтчет               КАК ТекущийОтчет,
	|		ДокументыПриобретения.ДокументПриобретения       КАК ДокументПриобретения
	|	ИЗ
	|		Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслуг
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеТекущегоОтчета КАК ДокументыПриобретения
	|			ПО (ДокументыПриобретения.ДокументОснование = ПриобретениеТоваровУслуг.Ссылка)
	|				И (ПриобретениеТоваровУслуг.Ссылка.Проведен)
	|				И ПриобретениеТоваровУслуг.Номенклатура = ДокументыПриобретения.АналитикаУчетаНоменклатуры.Номенклатура
	|				И ПриобретениеТоваровУслуг.Характеристика = ДокументыПриобретения.АналитикаУчетаНоменклатуры.Характеристика
	|				И ПриобретениеТоваровУслуг.Назначение = ДокументыПриобретения.АналитикаУчетаНоменклатуры.Назначение
	|				И ПриобретениеТоваровУслуг.НомерГТД = ДокументыПриобретения.НомерГТД
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		КорректировкаПриобретения.Ссылка,
	|		КорректировкаПриобретения.Ссылка.ДокументОснование,
	|		ДокументыПриобретения.АналитикаУчетаНоменклатуры,
	|		ДокументыПриобретения.НомерГТД,
	|		ДокументыПриобретения.МоментВремениОтчета,
	|		ДокументыПриобретения.ТекущийОтчет,
	|		ДокументыПриобретения.ДокументПриобретения
	|	ИЗ
	|		Документ.КорректировкаПриобретения.Товары КАК КорректировкаПриобретения
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеТекущегоОтчета КАК ДокументыПриобретения
	|			ПО (ДокументыПриобретения.ДокументОснование = КорректировкаПриобретения.Ссылка.ДокументОснование)
	|				И (КорректировкаПриобретения.Ссылка.Проведен)
	|				И КорректировкаПриобретения.Номенклатура = ДокументыПриобретения.АналитикаУчетаНоменклатуры.Номенклатура
	|				И КорректировкаПриобретения.Характеристика = ДокументыПриобретения.АналитикаУчетаНоменклатуры.Характеристика
	|				И КорректировкаПриобретения.Назначение = ДокументыПриобретения.АналитикаУчетаНоменклатуры.Назначение
	|				И КорректировкаПриобретения.НомерГТД = ДокументыПриобретения.НомерГТД
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВозвратТоваровПоставщику.Ссылка,
	|		ВозвратТоваровПоставщику.ДокументПоступления,
	|		ДокументыПриобретения.АналитикаУчетаНоменклатуры,
	|		ДокументыПриобретения.НомерГТД,
	|		ДокументыПриобретения.МоментВремениОтчета,
	|		ДокументыПриобретения.ТекущийОтчет,
	|		ДокументыПриобретения.ДокументПриобретения
	|	ИЗ
	|		Документ.ВозвратТоваровПоставщику.ВидыЗапасов КАК ВозвратТоваровПоставщику
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеТекущегоОтчета КАК ДокументыПриобретения
	|			ПО (ДокументыПриобретения.ДокументОснование = ВозвратТоваровПоставщику.ДокументПоступления)
	|				И (ВозвратТоваровПоставщику.Ссылка.Проведен)
	|				И ВозвратТоваровПоставщику.АналитикаУчетаНоменклатуры = ДокументыПриобретения.АналитикаУчетаНоменклатуры
	|				И ВозвратТоваровПоставщику.НомерГТД = ДокументыПриобретения.НомерГТД) КАК ДанныеПриобретений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыПриобретения.ТекущийОтчет                                             КАК ТекущийОтчет,
	|	Товары.Номенклатура                                                            КАК Номенклатура,
	|	Товары.Характеристика                                                          КАК Характеристика,
	|	Товары.Назначение                                                              КАК Назначение,
	|	Товары.НомерГТД                                                                КАК НомерГТД,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА Товары.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ Товары.Упаковка
	|	КОНЕЦ                                                                          КАК Упаковка,
	|	Товары.СтавкаНДС                                                               КАК СтавкаНДС,
	|	СУММА(Товары.Количество)                                                       КАК Количество,
	|	СУММА(ВЫБОР
	|			КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|				ТОГДА Товары.Количество
	|			ИНАЧЕ Товары.КоличествоУпаковок
	|		КОНЕЦ)                                                                     КАК КоличествоУпаковок,
	|	СУММА(Товары.КоличествоПоРНПТ)                                                 КАК КоличествоПоРНПТ,
	|	СУММА(ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Товары.СуммаСНДС - Товары.СуммаНДС)) КАК СуммаБезНДС,
	|	СУММА(ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Товары.СуммаНДС))                       КАК СуммаНДС,
	|	Товары.АналитикаУчетаНоменклатуры.Серия                                        КАК Серия
	|ПОМЕСТИТЬ ИсходныеДанныеПоОтчетам
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОснований КАК ДокументыПриобретения
	|		ПО Товары.ДокументПриобретения = ДокументыПриобретения.Ссылка
	|			И Товары.Ссылка.Проведен
	|			И Товары.АналитикаУчетаНоменклатуры = ДокументыПриобретения.АналитикаУчетаНоменклатуры
	|			И Товары.НомерГТД = ДокументыПриобретения.НомерГТД
	|			И Товары.Ссылка.МоментВремени < ДокументыПриобретения.МоментВремениОтчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыРегл
	|		ПО Товары.Ссылка = СуммыРегл.Регистратор
	|			И Товары.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыПриобретения.ТекущийОтчет,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение,
	|	Товары.НомерГТД,
	|	Товары.Упаковка,
	|	Товары.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА Товары.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ Товары.Упаковка
	|	КОНЕЦ,
	|	Товары.АналитикаУчетаНоменклатуры.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаДокумента.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаДокумента.Номенклатура.Код
	|	КОНЕЦ КАК НоменклатураКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ &ТекстЗапросаКодЕдиницыИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения               КАК ЕдиницаИзмеренияТНВЭД,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияТНВЭДНаименование,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Код           КАК ЕдиницаИзмеренияТНВЭДКод,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТаблицаДокумента.Серия.Наименование КАК СерияНаименование,	
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.НомерГТД КАК НомерГТДСсылка,
	|	ЕСТЬNULL(ТаблицаДокумента.НомерГТД.ТипНомераГТД, НЕОПРЕДЕЛЕНО) КАК ТипНомераГТД,
	|	ТаблицаДокумента.НомерГТД КАК НомерГТД,
	|	ТаблицаДокумента.КодТНВЭД КАК КодТНВЭД,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0) + ТаблицаДокумента.Количество
	|		ИНАЧЕ ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоУпаковок, 0) + ТаблицаДокумента.КоличествоУпаковок
	|	КОНЕЦ КАК Количество,
	|	ТаблицаДокумента.Количество КАК КоличествоБазовыхЕдиниц,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0) + ТаблицаДокумента.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ВЫБОР КОГДА НЕ ТаблицаДокумента.КоличествоУпаковок = 0 ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.КоличествоУпаковок ИНАЧЕ 0 КОНЕЦ
	|		ИНАЧЕ ВЫБОР КОГДА НЕ ТаблицаДокумента.Количество = 0 ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.Количество ИНАЧЕ 0 КОНЕЦ
	|	КОНЕЦ КАК Цена,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ТаблицаДокумента.СуммаБезНДС КАК СуммаБезНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0) + ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ТаблицаДокумента.СуммаБезНДС + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0) + ТаблицаДокумента.СуммаНДС КАК СуммаСНДС,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Серия КАК Серия,
	|	ТаблицаДокумента.Упаковка КАК Упаковка,
	|	ЛОЖЬ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	ОтчетКомитентуОЗакупкахТаблицаТоваров КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИсходныеДанныеПоОтчетам КАК ТаблицаТоваровПредыдущие
	|		ПО ТаблицаДокумента.Ссылка = ТаблицаТоваровПредыдущие.ТекущийОтчет
	|			И ТаблицаДокумента.Номенклатура = ТаблицаТоваровПредыдущие.Номенклатура
	|			И ТаблицаДокумента.Характеристика = ТаблицаТоваровПредыдущие.Характеристика
	|			И ТаблицаДокумента.Упаковка = ТаблицаТоваровПредыдущие.Упаковка
	|			И ТаблицаДокумента.НомерГТД = ТаблицаТоваровПредыдущие.НомерГТД
	|			И ТаблицаДокумента.Серия = ТаблицаТоваровПредыдущие.Серия
	|ГДЕ
	|	(ТаблицаДокумента.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			ИЛИ ТаблицаДокумента.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДокумента.НомерСтроки
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.ДокументПриобретения
	|ПОМЕСТИТЬ ТаблицаДокументовПриобретения
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках.Товары КАК ТаблицаТоваров
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО ДанныеДокументов.Ссылка = ТаблицаТоваров.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|ДанныеПоПрослеживаемостиПосле.Ссылка   КАК Ссылка,
	|ДанныеПоПрослеживаемостиПосле.Штрихкод КАК Штрихкод
	|
	|ИЗ
	|(ВЫБРАТЬ
	|	ТаблицаДанныхДокументов.Ссылка КАК Ссылка,
	|	ТаблицаШтрихкодов.ШтрихкодУпаковки КАК Штрихкод
	|ИЗ
	|	ТаблицаДокументовПриобретения КАК ТаблицаДанныхДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг.ШтрихкодыУпаковок КАК ТаблицаШтрихкодов
	|		ПО ТаблицаДанныхДокументов.ДокументПриобретения = ТаблицаШтрихкодов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриемкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаДанныхДокументов.ДокументПриобретения = ДокументыПрямогоОбмена.ДокументОснование
	|			И (НЕ ДокументыПрямогоОбмена.ПометкаУдаления)
	|ГДЕ
	|	ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДанныхДокументов.Ссылка КАК Ссылка,
	|	ТаблицаШтрихкодов.ШтрихкодУпаковки КАК Штрихкод
	|ИЗ
	|	ТаблицаДокументовПриобретения КАК ТаблицаДанныхДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения.ШтрихкодыУпаковок КАК ТаблицаШтрихкодов
	|		ПО ТаблицаДанныхДокументов.ДокументПриобретения = ТаблицаШтрихкодов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриемкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаДанныхДокументов.ДокументПриобретения = ДокументыПрямогоОбмена.ДокументОснование
	|			И (НЕ ДокументыПрямогоОбмена.ПометкаУдаления)
	|ГДЕ
	|	ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДанныхДокументов.Ссылка,
	|	ТаблицаШтрихкодов.ШтрихкодУпаковки
	|ИЗ
	|	ТаблицаДокументовПриобретения КАК ТаблицаДанныхДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровПоставщику.ШтрихкодыУпаковок КАК ТаблицаШтрихкодов
	|		ПО ТаблицаДанныхДокументов.ДокументПриобретения = ТаблицаШтрихкодов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриемкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаДанныхДокументов.ДокументПриобретения = ДокументыПрямогоОбмена.ДокументОснование
	|			И (НЕ ДокументыПрямогоОбмена.ПометкаУдаления)
	|ГДЕ
	|	ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL) КАК ДанныеПоПрослеживаемостиПосле
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеПоставщика.Ссылка КАК Ссылка,
	|	ДанныеПоставщика.Поставщик КАК Поставщик,
	|	ДанныеПоставщика.ИННПоставщика КАК ИННПоставщика,
	|	ДанныеПоставщика.КПППоставщика КАК КПППоставщика
	|
	|ИЗ
	|(ВЫБРАТЬ
	|	ОтчетыКомитентуОЗакупках.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Контрагент КАК Поставщик,
	|	ТаблицаДокумента.Контрагент.ИНН КАК ИННПоставщика,
	|	ТаблицаДокумента.Контрагент.КПП КАК КПППоставщика
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокументовПриобретения КАК ОтчетыКомитентуОЗакупках
	|		ПО (ОтчетыКомитентуОЗакупках.ДокументПриобретения = ТаблицаДокумента.Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтчетыКомитентуОЗакупках.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Контрагент КАК Поставщик,
	|	ТаблицаДокумента.Контрагент.ИНН КАК ИННПоставщика,
	|	ТаблицаДокумента.Контрагент.КПП КАК КПППоставщика
	|ИЗ
	|	Документ.КорректировкаПриобретения КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокументовПриобретения КАК ОтчетыКомитентуОЗакупках
	|		ПО (ОтчетыКомитентуОЗакупках.ДокументПриобретения = ТаблицаДокумента.Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтчетыКомитентуОЗакупках.Ссылка,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.Контрагент.ИНН,
	|	ТаблицаДокумента.Контрагент.КПП
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокументовПриобретения КАК ОтчетыКомитентуОЗакупках
	|		ПО (ОтчетыКомитентуОЗакупках.ДокументПриобретения = ТаблицаДокумента.Ссылка)) КАК ДанныеПоставщика
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДанныхДокументов.Ссылка          КАК Ссылка,
	|	ТаблицаДанныхДокументов.Организация     КАК Грузополучатель,
	|	ТаблицаДанныхДокументов.Контрагент      КАК Контрагент,
	|	ТаблицаДанныхДокументов.Контрагент.КПП  КАК КПППокупателя,
	|	ТаблицаДанныхДокументов.Контрагент.ИНН  КАК ИННПокупателя
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка								КАК Ссылка,
	|	ТаблицаДокумента.НомерГТД							КАК НомерГТД,
	|	Комплектующие.НомерРНПТ								КАК НомерРНПТ,
	|	ВЫБОР
	|		КОГДА Комплектующие.НомерРНПТ.РегистрационныйНомер = """"
	|			ТОГДА Комплектующие.НомерРНПТ
	|		ИНАЧЕ Комплектующие.НомерРНПТ.РегистрационныйНомер
	|	КОНЕЦ												КАК НомерТовара,
	|	НомераГТД.СуммаПоРНПТ								КАК ОбщаяСуммаПоКомплекту,
	|	Комплектующие.СуммаПоРНПТ							КАК СуммаПоРНПТ,
	|	Комплектующие.КоличествоПоРНПТ						КАК Количество,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД					КАК ЕдиницаИзмерения,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД.Код				КАК ЕдиницаИзмеренияКод,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД.Представление	КАК ЕдиницаИзмеренияНаименование
	|ИЗ
	|	ОтчетКомитентуОЗакупкахТаблицаТоваров КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД.ПрослеживаемыеКомплектующие КАК Комплектующие
	|		ПО ТаблицаДокумента.НомерГТД = Комплектующие.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК НомераГТД
	|		ПО ТаблицаДокумента.НомерГТД = НомераГТД.Ссылка";
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
	
	Запрос.УстановитьПараметр("ПредставлениеСчетФактура", НСтр("ru = 'счет-фактура'"));
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения", Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
	Запрос.УстановитьПараметр("ПересчитыватьВВалютуРегл", ПараметрыЗаполнения.ПересчитыватьВВалютуРегл);
	
	МассивРезультатов			= Запрос.ВыполнитьПакет();
	РезультатПоШапке			= МассивРезультатов[1];
	РезультатПоТабличнойЧасти	= МассивРезультатов[5];
	ШтрихкодыУпаковок			= МассивРезультатов[7].Выгрузить();
	Маркировка					= ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое(ШтрихкодыУпаковок);
	РезультатПоПоставщикам		= МассивРезультатов[8];
	РезультатПоКонтрагентам		= МассивРезультатов[9];
	ПрослеживаемыеКомплектующие	= УчетПрослеживаемыхТоваровЛокализация.ПрослеживаемыеКомплектующиеДляПечатиДанных(
									МассивРезультатов[10]);
	
	ДанныеДляПечати = Новый Структура;
	ДанныеДляПечати.Вставить("РезультатПоШапке",			РезультатПоШапке);
	ДанныеДляПечати.Вставить("РезультатПоТабличнойЧасти",	РезультатПоТабличнойЧасти);
	ДанныеДляПечати.Вставить("Маркировка",					Маркировка);
	ДанныеДляПечати.Вставить("РезультатПоПоставщикам",		РезультатПоПоставщикам);
	ДанныеДляПечати.Вставить("РезультатПоКонтрагентам",		РезультатПоКонтрагентам);
	ДанныеДляПечати.Вставить("ПрослеживаемыеКомплектующие",	ПрослеживаемыеКомплектующие);
	
	Возврат ДанныеДляПечати;
	
КонецФункции

// Возвращает данные для формирования печатной формы УПД
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.ОтчетКомитентуОЗакупках - ссылки на документы, по которым необходимо
//																		получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемая при печати УПД, содержащая следующие свойства:
//		* РезультатПоШапке - РезультатЗапроса - общие данные накладной.
//		* РезультатПоТабличнойЧасти - РезультатЗапроса - данные табличной части накладной.
//
Функция ПолучитьДанныеДляПечатнойФормыУПД_Вознаграждение(ПараметрыПечати, МассивОбъектов)
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка                  КАК Ссылка,
	|	ДанныеДокументов.Валюта                  КАК Валюта,
	|	ДанныеДокументов.Организация             КАК Организация,
	|	ДанныеДокументов.Подразделение           КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|	ДанныеДокументов.Дата                    КАК Период
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|	И НЕ ДанныеДокументов.СпособРасчетаВознаграждения = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаКомиссионногоВознаграждения.НеРассчитывается)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	УчетНДСРФ.ДополнитьЗапросПолученияДанныхДляПечатиУПД(Запрос);

	Запрос.Выполнить();
	
	ПоместитьВременнуюТаблицуТоваров_Вознаграждение(МенеджерВременныхТаблиц);
	Обработки.ПечатьОбщихФорм.ПоместитьВременнуюТаблицуДанныхПоставщика(МенеджерВременныхТаблиц);
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	&ПредставлениеСчетФактура КАК ПредставлениеДокумента,
	|	2 КАК СтатусУПД,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Дата КАК Дата,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправления,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправления,
	|	ЛОЖЬ КАК Исправление,
	|	НЕОПРЕДЕЛЕНО КАК НомерСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправленияСчетаФактуры,
	|	ЛОЖЬ КАК КорректировочныйСчетФактура,
	|	"""" КАК СтрокаПоДокументу,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаСчетаФактуры,
	|	ДанныеДокумента.Партнер КАК Партнер,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Контрагент.ОбособленноеПодразделение
	|			ТОГДА ДанныеДокумента.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ ДанныеДокумента.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ДанныеДокумента.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ДанныеПоставщика.ГоловнаяОрганизация КАК Организация,
	|	ДанныеДокумента.Организация.Префикс КАК Префикс,
	|	0 КАК ИндексПодразделения,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	НЕОПРЕДЕЛЕНО КАК Грузополучатель,
	|	НЕОПРЕДЕЛЕНО КАК Грузоотправитель,
	|
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетОрганизации,
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетКонтрагента,
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетГрузоотправителя,
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетГрузополучателя,
	|
	|	ДанныеПоставщика.КПППоставщика КАК КПППоставщика,
	|	""""                           КАК КПППокупателя,
	|	ДанныеДокумента.Контрагент.ИНН КАК ИННПокупателя,
	|	НЕОПРЕДЕЛЕНО КАК АдресДоставки,
	|	ДанныеДокумента.Валюта КАК Валюта,
	|	ДанныеДокумента.Валюта.НаименованиеПолное КАК ВалютаНаименованиеПолное,
	|	ДанныеДокумента.Валюта.Код КАК ВалютаКод,
	|	ИСТИНА КАК ТолькоУслуги,
	|	ЛОЖЬ КАК ЕстьПрослеживаемыеТовары,
	|	ЛОЖЬ КАК ЭтоПередачаНаКомиссию,
	|	НЕОПРЕДЕЛЕНО КАК Основание,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьНомер,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьДата,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьВыдана,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьЛицо,
	|	НЕОПРЕДЕЛЕНО КАК Кладовщик,
	|	НЕОПРЕДЕЛЕНО КАК ДолжностьКладовщика,
	|	ДанныеДокументов.ТребуетсяНаличиеСФ КАК ТребуетсяНаличиеСФ
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ДанныеДокумента.Ссылка = ДанныеДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоставщика КАК ДанныеПоставщика
	|		ПО ДанныеДокумента.Ссылка = ДанныеПоставщика.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаДокумента.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаДокумента.Номенклатура.Код
	|	КОНЕЦ КАК НоменклатураКод,
	|	ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияНаименование,
	|	ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|	ТаблицаДокумента.Номенклатура.ЕдиницаИзмеренияТНВЭД          КАК ЕдиницаИзмеренияТНВЭД,
	|	ТаблицаДокумента.Номенклатура.ЕдиницаИзмеренияТНВЭД.Представление КАК ЕдиницаИзмеренияТНВЭДНаименование,
	|	ТаблицаДокумента.Номенклатура.ЕдиницаИзмеренияТНВЭД.Код      КАК ЕдиницаИзмеренияТНВЭДКод,
	|	ТаблицаДокумента.Номенклатура.ЕдиницаИзмеренияТНВЭД.Код      КАК КодТНВЭД,
	|	НЕОПРЕДЕЛЕНО КАК Характеристика,
	|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаНаименование,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	НЕОПРЕДЕЛЕНО КАК НомерГТДСсылка,
	|	НЕОПРЕДЕЛЕНО КАК ТипНомераГТД,
	|	НЕОПРЕДЕЛЕНО КАК НомерГТД,
	|	НЕОПРЕДЕЛЕНО КАК СтранаПроисхождения,
	|	НЕОПРЕДЕЛЕНО КАК СтранаПроисхожденияКод,
	|	1 КАК Количество,
	|	0 КАК КоличествоПоРНПТ,
	|	ТаблицаДокумента.СуммаБезНДС КАК Цена,
	|	ТаблицаДокумента.СуммаБезНДС КАК СуммаБезНДС,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС КАК СуммаСНДС,
	|	1 КАК НомерСтроки,
	|	ЛОЖЬ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	ОтчетКомитентуОЗакупкахТаблицаТоваров КАК ТаблицаДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|
	|ИТОГИ ПО
	|	Ссылка
	|";

	Запрос.УстановитьПараметр("ПредставлениеСчетФактура", НСтр("ru='счет-фактура'"));
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	
	МассивРезультатов			= Запрос.ВыполнитьПакет();
	РезультатПоШапке			= МассивРезультатов[0];
	РезультатПоТабличнойЧасти	= МассивРезультатов[1];
	
	ДанныеДляПечати = Новый Структура;
	ДанныеДляПечати.Вставить("РезультатПоШапке",			РезультатПоШапке);
	ДанныеДляПечати.Вставить("РезультатПоТабличнойЧасти",	РезультатПоТабличнойЧасти);
	
	Возврат ДанныеДляПечати;
	
КонецФункции

// Формирует временную таблицу, содержащую табличную часть по таблице данных документов.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц, содержащий таблицу ТаблицаДанныхДокументов с полями:
//		Ссылка,
//		Валюта.
//
//	ПараметрыЗаполнения - Структура - структура, возвращаемая функцией ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров.
//
Процедура ПоместитьВременнуюТаблицуТоваров_Товары(МенеджерВременныхТаблиц, ПараметрыЗаполнения = Неопределено)
	
	Если ПараметрыЗаполнения = Неопределено Тогда
		ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета",     Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ПересчитыватьВВалютуРегл",       ПараметрыЗаполнения.ПересчитыватьВВалютуРегл);
	Запрос.УстановитьПараметр("ВключаяНомераГТД",               ПараметрыЗаполнения.ВключаяНомераГТД);
	Запрос.УстановитьПараметр("ПустаяГТД",                      Справочники.НомераГТД.ПустаяСсылка());
	Запрос.УстановитьПараметр("ВыводитьСерии",                  ПараметрыЗаполнения.ВыводитьСерииНоменклатуры);
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения", Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
	
	Если ПараметрыЗаполнения.ПересчитыватьВВалютуРегл И ПараметрыЗаполнения.АктуализироватьРасчеты Тогда
		Если НЕ ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
			Запрос.Текст = "
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
			|ИЗ
			|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
			|
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|		ТаблицаДанныхДокументов КАК ДанныеДокументов
			|	ПО
			|		РасчетыСКлиентами.Регистратор = ДанныеДокументов.Ссылка
			|
			|ГДЕ
			|	ДанныеДокументов.Валюта <> ДанныеДокументов.Ссылка.Организация.ВалютаРегламентированногоУчета
			|	И РасчетыСКлиентами.Активность
			|";
			ТаблицаАналитик = Запрос.Выполнить().Выгрузить();
			МассивАналитикУчетаПоПартнерам = ТаблицаАналитик.ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
			
			Если МассивАналитикУчетаПоПартнерам.Количество() > 0 Тогда
				ОкончаниеПериодаРасчета = КонецМесяца(ВзаиморасчетыСервер.ПолучитьМаксимальнуюДатуВКоллекцииДокументов(МенеджерВременныхТаблиц)) + 1;
				АналитикиРасчета = РаспределениеВзаиморасчетовВызовСервера.АналитикиРасчета();
				АналитикиРасчета.АналитикиУчетаПоПартнерам = МассивАналитикУчетаПоПартнерам;
				Попытка
					РаспределениеВзаиморасчетовВызовСервера.РаспределитьВсеРасчетыСКлиентами(ОкончаниеПериодаРасчета, АналитикиРасчета);
				Исключение
					ТекстСообщения = НСтр("ru = 'Печатная форма сформирована по неактуальным данным.
					|Необходимо актуализировать взаиморасчеты вручную и переформировать печатную форму.'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				КонецПопытки;
			КонецЕсли;
		Иначе
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДанныеДокументов.Ссылка КАК Ссылка
			|ИЗ
			|	ТаблицаДанныхДокументов КАК ДанныеДокументов
			|ГДЕ 
			|	ДанныеДокументов.Валюта <> ДанныеДокументов.Ссылка.Организация.ВалютаРегламентированногоУчета
			|	ИЛИ ДанныеДокументов.Валюта <> &ВалютаУправленческогоУчета";
			МассивДокументов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
			РегистрыСведений.СуммыДокументовВВалютахУчета.РассчитатьСуммыДокументовВВалютахУчета(МассивДокументов);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                 КАК Ссылка,
	|	
	|	ЗНАЧЕНИЕ(Справочник.ВариантыКомплектацииНоменклатуры.ПустаяСсылка) КАК ВариантКомплектацииНоменклатуры,
	|	ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ПустаяСсылка) КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ПустаяСсылка) КАК ВариантРасчетаЦеныНабора,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)   КАК НоменклатураНабора,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК ХарактеристикаНабора,
	|	
	|	МАКСИМУМ(ТаблицаДокумента.НомерСтроки)                  КАК НомерСтроки,
	|	ТаблицаДокумента.Номенклатура                           КАК Номенклатура,
	|	ТаблицаДокумента.Характеристика                         КАК Характеристика,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Серия       КАК Серия,
	|	ТаблицаДокумента.НомерГТД                               КАК НомерГТД,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД    КАК КодТНВЭД,
	|	СУММА(ТаблицаДокумента.Количество)                      КАК Количество,
	|	СУММА(ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ЕСТЬNULL(ТаблицаДокумента.Количество, 0)
	|		ИНАЧЕ ЕСТЬNULL(ТаблицаДокумента.КоличествоУпаковок, 0)
	|	КОНЕЦ)                                                  КАК КоличествоУпаковок,
	|	СУММА(ТаблицаДокумента.КоличествоПоРНПТ)                КАК КоличествоПоРНПТ,
	|	
	|	ТаблицаДокумента.Цена                                   КАК Цена,
	|	СУММА(ЕСТЬNULL(
	|		СуммыДокументовВВалютахУчета.СуммаБезНДСРегл,
	|		ТаблицаДокумента.Сумма
	|	))                                                      КАК СуммаБезНДС,
	|	
	|	ТаблицаДокумента.СтавкаНДС                              КАК СтавкаНДС,
	|	
	|	СУММА(ЕСТЬNULL(
	|		СуммыДокументовВВалютахУчета.СуммаНДСРегл,
	|		ТаблицаДокумента.СуммаСНДС
	|	))                                                      КАК СуммаНДС,
	|	
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Номенклатура.ТипНоменклатуры В
	|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ                                                   КАК ЭтоТовар,
	|	ЛОЖЬ                                                    КАК ЭтоНеВозвратнаяТара,
	|	ЛОЖЬ                                                    КАК ВернутьМногооборотнуюТару,
	|		ВЫБОР
	|			КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|				ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения
	|			ИНАЧЕ ТаблицаДокумента.Упаковка
	|		КОНЕЦ                                               КАК Упаковка
	|
	|ПОМЕСТИТЬ ОтчетКомитентуОЗакупкахТаблицаТоваров
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках.Товары КАК ТаблицаДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО
	|		ТаблицаДокумента.Ссылка = ДанныеДокументов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|	ПО
	|		ТаблицаДокумента.Ссылка = СуммыДокументовВВалютахУчета.Регистратор
	|		И ТаблицаДокумента.ИдентификаторСтроки = СуммыДокументовВВалютахУчета.ИдентификаторСтроки
	|		И СуммыДокументовВВалютахУчета.Активность
	|		И &ПересчитыватьВВалютуРегл
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Ссылка,
	|	Номенклатура,
	|	Характеристика,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Серия,
	|	НомерГТД,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД,
	|	Цена,
	|	ТаблицаДокумента.СтавкаНДС,
	|	Упаковка
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Формирует временную таблицу, содержащую табличную часть по таблице данных документов.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц, содержащий таблицу ТаблицаДанныхДокументов с полями:
//		Ссылка.
//
//	ПараметрыЗаполнения - Структура - структура, возвращаемая функцией ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров.
//
Процедура ПоместитьВременнуюТаблицуТоваров_Вознаграждение(МенеджерВременныхТаблиц, ПараметрыЗаполнения = Неопределено)
	
	Если ПараметрыЗаполнения = Неопределено Тогда
		ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета",     Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ПересчитыватьВВалютуРегл",       ПараметрыЗаполнения.ПересчитыватьВВалютуРегл);
	
	Если ПараметрыЗаполнения.ПересчитыватьВВалютуРегл Тогда
		Если ПараметрыЗаполнения.АктуализироватьРасчеты И НЕ ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
			Запрос.Текст = "
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
			|ИЗ
			|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
			|
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|		ТаблицаДанныхДокументов КАК ДанныеДокументов
			|	ПО
			|		РасчетыСКлиентами.Регистратор = ДанныеДокументов.Ссылка
			|
			|ГДЕ
			|	ДанныеДокументов.Валюта <> ДанныеДокументов.Ссылка.Организация.ВалютаРегламентированногоУчета
			|	И РасчетыСКлиентами.Активность
			|";
			ТаблицаАналитик = Запрос.Выполнить().Выгрузить();
			МассивАналитикУчетаПоПартнерам = ТаблицаАналитик.ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
			
			Если МассивАналитикУчетаПоПартнерам.Количество() > 0 Тогда
				ОкончаниеПериодаРасчета = КонецМесяца(ВзаиморасчетыСервер.ПолучитьМаксимальнуюДатуВКоллекцииДокументов(МенеджерВременныхТаблиц)) + 1;
				АналитикиРасчета = РаспределениеВзаиморасчетовВызовСервера.АналитикиРасчета();
				АналитикиРасчета.АналитикиУчетаПоПартнерам = МассивАналитикУчетаПоПартнерам;
				Попытка
					РаспределениеВзаиморасчетовВызовСервера.РаспределитьВсеРасчетыСКлиентами(ОкончаниеПериодаРасчета, АналитикиРасчета);
				Исключение
					ТекстСообщения = НСтр("ru ='Печатная форма сформирована по неактуальным данным.
					|Необходимо актуализировать взаиморасчеты вручную и переформировать печатную форму.'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				КонецПопытки;
			КонецЕсли;
		Иначе
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДанныеДокументов.Ссылка КАК Ссылка
			|ИЗ
			|	ТаблицаДанныхДокументов КАК ДанныеДокументов
			|ГДЕ 
			|	ДанныеДокументов.Валюта <> ДанныеДокументов.Ссылка.Организация.ВалютаРегламентированногоУчета
			|	ИЛИ ДанныеДокументов.Валюта <> &ВалютаУправленческогоУчета";
			МассивДокументов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
			РегистрыСведений.СуммыДокументовВВалютахУчета.РассчитатьСуммыДокументовВВалютахУчета(МассивДокументов);
			
		КонецЕсли;
	КонецЕсли;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                      КАК Ссылка,
	|	1                                                            КАК НомерСтроки,
	|	ТаблицаДокумента.Ссылка.Услуга                               КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)          КАК Серия,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)                  КАК НомерГТД,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)   КАК Упаковка,
	|	1                                                            КАК Количество,
	|	1                                                            КАК КоличествоУпаковок,
	|	0                                                            КАК КоличествоПоРНПТ,
	|	ТаблицаДокумента.Ссылка.СтавкаНДСВознаграждения              КАК СтавкаНДС,
	|
	|	СУММА(ЕСТЬNULL(
	|		СуммыДокументовВВалютахУчета.СуммаБезНДСРегл,
	|		ТаблицаДокумента.СуммаВознаграждения - ТаблицаДокумента.СуммаНДСВознаграждения
	|	))                                                           КАК СуммаБезНДС,
	|	СУММА(ЕСТЬNULL(
	|		СуммыДокументовВВалютахУчета.СуммаНДСРегл,
	|		ТаблицаДокумента.СуммаНДСВознаграждения
	|	))                                                           КАК СуммаНДС,
	|	
	|	ЛОЖЬ                                                         КАК ЭтоТовар,
	|	ЛОЖЬ                                                         КАК ВернутьМногооборотнуюТару
	|
	|ПОМЕСТИТЬ ОтчетКомитентуОЗакупкахТаблицаТоваров
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках.Товары КАК ТаблицаДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО
	|		ТаблицаДокумента.Ссылка = ДанныеДокументов.Ссылка
	|	
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|	ПО
	|		ТаблицаДокумента.Ссылка = СуммыДокументовВВалютахУчета.Регистратор
	|		И СуммыДокументовВВалютахУчета.ИдентификаторСтроки = ТаблицаДокумента.ИдентификаторСтрокиВознаграждения
	|		И СуммыДокументовВВалютахУчета.Активность
	|		И &ПересчитыватьВВалютуРегл
	|
	|СГРУППИРОВАТЬ ПО
	|		ТаблицаДокумента.Ссылка
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ПолучитьДанныеДляПечатнойФормыУКД_Товары(ПараметрыПечати, МассивОбъектов)
		
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка                  КАК Ссылка,
	|	ДанныеДокументов.Валюта                  КАК Валюта,
	|	ДанныеДокументов.Организация             КАК Организация,
	|	ДанныеДокументов.Контрагент              КАК Контрагент,
	|	ДанныеДокументов.Подразделение           КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|	ДанныеДокументов.ДокументПриобретения    КАК ДокументПриобретения,
	|	ДанныеДокументов.Дата                    КАК Период
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	УчетНДСРФ.ДополнитьЗапросПолученияДанныхДляПечатиУПД(Запрос, "Контрагент,ДокументПриобретения");

	Запрос.Выполнить();
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	ПараметрыЗаполнения.ВключаяНомераГТД                 = Истина;
	ПараметрыЗаполнения.ОбработатьНастройкиПечатиНаборов = Ложь;
	ПоместитьВременнуюТаблицуТоваров_Товары(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	Обработки.ПечатьОбщихФорм.ПоместитьВременнуюТаблицуДанныхПоставщика(МенеджерВременныхТаблиц);
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА НЕ ТаблицаТоваров.ЭтоТовар
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьУслуги,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(ТаблицаТоваров.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьПрослеживаемыеТовары,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ТаблицаТоваров.ЭтоТовар
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьТовары
	|ПОМЕСТИТЬ НоменклатураДокументов
	|ИЗ
	|	ОтчетКомитентуОЗакупкахТаблицаТоваров КАК ТаблицаТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Ссылка                     КАК ТекущийОтчет,
	|	Товары.Ссылка.Дата                КАК ДатаОтчета,
	|	Товары.Ссылка.МоментВремени       КАК МоментВремениОтчета,
	|	Товары.Ссылка.Организация         КАК Организация,
	|	ВЫБОР
	|		КОГДА Товары.ДокументПриобретения ССЫЛКА Документ.КорректировкаПриобретения
	|			ТОГДА Товары.ДокументПриобретения.ДокументОснование
	|		КОГДА Товары.ДокументПриобретения ССЫЛКА Документ.ВозвратТоваровПоставщику
	|			ТОГДА Товары.ДокументПриобретения.ДокументПоступления
	|		ИНАЧЕ Товары.ДокументПриобретения
	|	КОНЕЦ                             КАК ДокументОснование,
	|	Товары.ДокументПриобретения       КАК ДокументПриобретения,
	|	Товары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Товары.НомерГТД                   КАК НомерГТД
	|ПОМЕСТИТЬ ДанныеТекущегоОтчета
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|		ПО (ТаблицаДанныхДокументов.Ссылка = Товары.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументОтчета.Ссылка             КАК ИсходныйОтчет,
	|	ДанныеТекущегоОтчета.ТекущийОтчет КАК ТекущийОтчет
	|ПОМЕСТИТЬ ДанныеИсходногоОтчета
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках.Товары КАК ДокументОтчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеТекущегоОтчета КАК ДанныеТекущегоОтчета
	|		ПО (ДанныеТекущегоОтчета.ДокументОснование = ДокументОтчета.ДокументПриобретения)
	|			И (ДокументОтчета.Ссылка.Проведен)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                          КАК Ссылка,
	|	&ПредставлениеСчетФактура                                       КАК ПредставлениеДокумента,
	|	2                                                               КАК СтатусУПД,
	|	ВЫБОР
	|		КОГДА СчетФактура.Ссылка ЕСТЬ NULL
	|			ТОГДА ДанныеДокумента.Номер
	|		ИНАЧЕ СчетФактура.Номер
	|	КОНЕЦ                                                           КАК Номер,
	|	ВЫБОР
	|		КОГДА СчетФактура.Ссылка ЕСТЬ NULL
	|			ТОГДА ДанныеДокумента.Дата
	|		ИНАЧЕ СчетФактура.Дата
	|	КОНЕЦ                                                           КАК Дата,
	|	НЕОПРЕДЕЛЕНО                                                    КАК НомерИсправления,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ДатаИсправления,
	|	ЛОЖЬ                                                            КАК Исправление,
	|	ВЫБОР
	|		КОГДА СчетФактура.Ссылка ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ СчетФактура.Номер
	|	КОНЕЦ                                                           КАК НомерСчетаФактуры,
	|	ВЫБОР
	|		КОГДА СчетФактура.Ссылка ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ СчетФактура.Дата
	|	КОНЕЦ                                                           КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                                                    КАК НомерИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ДатаИсправленияСчетаФактуры,
	|	ИСТИНА                                                          КАК КорректировочныйСчетФактура,
	|	""""                                                            КАК СтрокаПоДокументу,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ВалютаСчетаФактуры,
	|	ДанныеДокумента.Партнер                                         КАК Партнер,
	|	ДанныеДокумента.НалогообложениеНДС                              КАК НалогообложениеНДС,
	|	ДанныеДокумента.Организация                                     КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                                    КАК Поставщик,
	|	""""                                                            КАК КПППоставщика,
	|	ДанныеДокумента.Контрагент                                      КАК Контрагент,
	|	ДокументПриобретениеТоваровУслуг.Контрагент                     КАК Грузоотправитель,
	|	ДанныеДокумента.Организация                                     КАК Грузополучатель,
	|	НЕОПРЕДЕЛЕНО                                                    КАК БанковскийСчетОрганизации,
	|	НЕОПРЕДЕЛЕНО                                                    КАК БанковскийСчетКонтрагента,
	|	НЕОПРЕДЕЛЕНО                                                    КАК БанковскийСчетГрузоотправителя,
	|	НЕОПРЕДЕЛЕНО                                                    КАК БанковскийСчетГрузополучателя,
	|	ТаблицаОтветственныеЛица.РуководительНаименование               КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность                  КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование           КАК ГлавныйБухгалтер,
	|	ДанныеДокумента.Организация.КПП                                 КАК КПППокупателя,
	|	ДанныеДокумента.Организация.Префикс                             КАК Префикс,
	|	""""                                                            КАК АдресДоставки,
	|	ДанныеДокумента.Валюта                                          КАК Валюта,
	|	ДанныеДокумента.Валюта.НаименованиеПолное                       КАК ВалютаНаименованиеПолное,
	|	ДанныеДокумента.Валюта.Код                                      КАК ВалютаКод,
	|	ВЫБОР
	|		КОГДА НоменклатураДокументов.ЕстьУслуги
	|				И НЕ НоменклатураДокументов.ЕстьТовары
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                                           КАК ТолькоУслуги,
	|	ЕСТЬNULL(НоменклатураДокументов.ЕстьПрослеживаемыеТовары, ЛОЖЬ) КАК ЕстьПрослеживаемыеТовары,
	|	ЛОЖЬ                                                            КАК ЭтоПередачаНаКомиссию,
	|	НЕОПРЕДЕЛЕНО                                                    КАК Основание,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ОснованиеДата,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ОснованиеНомер,
	|	""""                                                            КАК ДоверенностьНомер,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ДоверенностьДата,
	|	""""                                                            КАК ДоверенностьВыдана,
	|	""""                                                            КАК ДоверенностьЛицо,
	|	""""                                                            КАК Кладовщик,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ДолжностьКладовщика,
	|	ДанныеДокументов.ТребуетсяНаличиеСФ                             КАК ТребуетсяНаличиеСФ,
	|	ИСТИНА                                                          КАК ПеревыставленныйСчетФактура,
	|	""ПредставлениеСтроки5а""                                       КАК ПредставлениеСтроки5а,
	|	ЕСТЬNULL(ДанныеПоставщика.ИндексПодразделения, 0)               КАК ИндексПодразделения,
	|	ЛОЖЬ                                                            КАК ПечатьНеТребуется
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ДанныеДокумента.Ссылка = ДанныеДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоставщика КАК ДанныеПоставщика
	|		ПО ДанныеДокумента.Ссылка = ДанныеПоставщика.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураДокументов КАК НоменклатураДокументов
	|		ПО ДанныеДокумента.Ссылка = НоменклатураДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацииВНалоговомОргане КАК ДанныеПодразделений
	|		ПО ДанныеПодразделений.Организация = ДанныеДокумента.Организация
	|			И ДанныеПодразделений.Подразделение = ДанныеДокумента.Подразделение
	|			И ДанныеПодразделений.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ДокументПриобретениеТоваровУслуг
	|		ПО ДокументПриобретениеТоваровУслуг.Ссылка = ДанныеДокумента.ДокументПриобретения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК Договоры
	|		ПО Договоры.Ссылка = ДанныеДокумента.Договор
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактура
	|		ПО СчетФактура.ДокументОснование = ДанныеДокумента.Ссылка
	|			И СчетФактура.Перевыставленный
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТоваров.Ссылка               КАК Ссылка,
	|	ТаблицаТоваров.ДокументПриобретения КАК ДокументПриобретения
	|ПОМЕСТИТЬ ТаблицаДокументовПриобретения
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках.Товары КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО (ДанныеДокументов.Ссылка = ТаблицаТоваров.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|ДанныеПоПрослеживаемостиПосле.Ссылка   КАК Ссылка,
	|ДанныеПоПрослеживаемостиПосле.Штрихкод КАК Штрихкод
	|
	|ИЗ
	|(ВЫБРАТЬ
	|	ТаблицаДанныхДокументов.Ссылка     КАК Ссылка,
	|	ТаблицаШтрихкодов.ШтрихкодУпаковки КАК Штрихкод
	|ИЗ
	|	ТаблицаДокументовПриобретения КАК ТаблицаДанныхДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг.ШтрихкодыУпаковок КАК ТаблицаШтрихкодов
	|		ПО ТаблицаДанныхДокументов.ДокументПриобретения = ТаблицаШтрихкодов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриемкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаДанныхДокументов.ДокументПриобретения = ДокументыПрямогоОбмена.ДокументОснование
	|			И (НЕ ДокументыПрямогоОбмена.ПометкаУдаления)
	|ГДЕ
	|	ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДанныхДокументов.Ссылка,
	|	ТаблицаШтрихкодов.ШтрихкодУпаковки
	|ИЗ
	|	ТаблицаДокументовПриобретения КАК ТаблицаДанныхДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения.ШтрихкодыУпаковок КАК ТаблицаШтрихкодов
	|		ПО ТаблицаДанныхДокументов.ДокументПриобретения = ТаблицаШтрихкодов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриемкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаДанныхДокументов.ДокументПриобретения = ДокументыПрямогоОбмена.ДокументОснование
	|			И (НЕ ДокументыПрямогоОбмена.ПометкаУдаления)
	|ГДЕ
	|	ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДанныхДокументов.Ссылка,
	|	ТаблицаШтрихкодов.ШтрихкодУпаковки
	|ИЗ
	|	ТаблицаДокументовПриобретения КАК ТаблицаДанныхДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровПоставщику.ШтрихкодыУпаковок КАК ТаблицаШтрихкодов
	|		ПО ТаблицаДанныхДокументов.ДокументПриобретения = ТаблицаШтрихкодов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриемкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаДанныхДокументов.ДокументПриобретения = ДокументыПрямогоОбмена.ДокументОснование
	|			И (НЕ ДокументыПрямогоОбмена.ПометкаУдаления)
	|ГДЕ
	|	ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL) КАК ДанныеПоПрослеживаемостиПосле
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеПоставщика.Ссылка        КАК Ссылка,
	|	ДанныеПоставщика.Поставщик     КАК Поставщик,
	|	ДанныеПоставщика.ИННПоставщика КАК ИННПоставщика,
	|	ДанныеПоставщика.КПППоставщика КАК КПППоставщика
	|
	|ИЗ
	|(ВЫБРАТЬ
	|	ОтчетыКомитентуОЗакупках.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Контрагент     КАК Поставщик,
	|	ТаблицаДокумента.Контрагент.ИНН КАК ИННПоставщика,
	|	ТаблицаДокумента.Контрагент.КПП КАК КПППоставщика
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокументовПриобретения КАК ОтчетыКомитентуОЗакупках
	|		ПО (ОтчетыКомитентуОЗакупках.ДокументПриобретения = ТаблицаДокумента.Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтчетыКомитентуОЗакупках.Ссылка,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.Контрагент.ИНН,
	|	ТаблицаДокумента.Контрагент.КПП
	|ИЗ
	|	Документ.КорректировкаПриобретения КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокументовПриобретения КАК ОтчетыКомитентуОЗакупках
	|		ПО (ОтчетыКомитентуОЗакупках.ДокументПриобретения = ТаблицаДокумента.Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтчетыКомитентуОЗакупках.Ссылка,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.Контрагент.ИНН,
	|	ТаблицаДокумента.Контрагент.КПП
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокументовПриобретения КАК ОтчетыКомитентуОЗакупках
	|		ПО (ОтчетыКомитентуОЗакупках.ДокументПриобретения = ТаблицаДокумента.Ссылка)) КАК ДанныеПоставщика
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДанныхДокументов.Ссылка         КАК Ссылка,
	|	ТаблицаДанныхДокументов.Организация    КАК Грузополучатель,
	|	ТаблицаДанныхДокументов.Контрагент     КАК Контрагент,
	|	ТаблицаДанныхДокументов.Контрагент.КПП КАК КПППокупателя,
	|	ТаблицаДанныхДокументов.Контрагент.ИНН КАК ИННПокупателя
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка                           КАК Ссылка,
	|	ТаблицаДокумента.НомерГТД                         КАК НомерГТД,
	|	Комплектующие.НомерРНПТ                           КАК НомерРНПТ,
	|	ВЫБОР
	|		КОГДА Комплектующие.НомерРНПТ.РегистрационныйНомер = """"
	|			ТОГДА Комплектующие.НомерРНПТ
	|		ИНАЧЕ Комплектующие.НомерРНПТ.РегистрационныйНомер
	|	КОНЕЦ                                             КАК НомерТовара,
	|	НомераГТД.СуммаПоРНПТ                             КАК ОбщаяСуммаПоКомплекту,
	|	Комплектующие.СуммаПоРНПТ                         КАК СуммаПоРНПТ,
	|	Комплектующие.КоличествоПоРНПТ                    КАК Количество,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД               КАК ЕдиницаИзмерения,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД.Код           КАК ЕдиницаИзмеренияКод,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД.Представление КАК ЕдиницаИзмеренияНаименование
	|ИЗ
	|	ОтчетКомитентуОЗакупкахТаблицаТоваров КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД.ПрослеживаемыеКомплектующие КАК Комплектующие
	|		ПО ТаблицаДокумента.НомерГТД = Комплектующие.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК НомераГТД
	|		ПО ТаблицаДокумента.НомерГТД = НомераГТД.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеИсходногоОтчета.ТекущийОтчет КАК Ссылка,
	|	ДанныеОснования.Ссылка.Номер       КАК НомерСчетаФактуры,
	|	ДанныеОснования.Ссылка.Дата        КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                       КАК НомерИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                       КАК ДатаИсправленияСчетаФактуры
	|ИЗ
	|	ДанныеИсходногоОтчета КАК ДанныеИсходногоОтчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомитентуОЗакупках КАК ДанныеОснования
	|		ПО ДанныеИсходногоОтчета.ИсходныйОтчет = ДанныеОснования.Ссылка
	|			И ДанныеОснования.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеПриобретений.Ссылка                     КАК Ссылка,
	|	ДанныеПриобретений.ДокументОснование          КАК ДокументОснование,
	|	ДанныеПриобретений.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ДанныеПриобретений.НомерГТД                   КАК НомерГТД,
	|	ДанныеПриобретений.МоментВремениОтчета        КАК МоментВремениОтчета,
	|	ДанныеПриобретений.ТекущийОтчет               КАК ТекущийОтчет,
	|	ДанныеПриобретений.ДокументПриобретения       КАК ДокументПриобретения
	|ПОМЕСТИТЬ ТаблицаОснований
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПриобретениеТоваровУслуг.Ссылка                  КАК Ссылка,
	|		ПриобретениеТоваровУслуг.Ссылка                  КАК ДокументОснование,
	|		ДокументыПриобретения.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ДокументыПриобретения.НомерГТД                   КАК НомерГТД,
	|		ДокументыПриобретения.МоментВремениОтчета        КАК МоментВремениОтчета,
	|		ДокументыПриобретения.ТекущийОтчет               КАК ТекущийОтчет,
	|		ДокументыПриобретения.ДокументПриобретения       КАК ДокументПриобретения
	|	ИЗ
	|		Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслуг
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеТекущегоОтчета КАК ДокументыПриобретения
	|			ПО ДокументыПриобретения.ДокументОснование = ПриобретениеТоваровУслуг.Ссылка
	|				И ПриобретениеТоваровУслуг.Ссылка.Проведен
	|				И ПриобретениеТоваровУслуг.Номенклатура = ДокументыПриобретения.АналитикаУчетаНоменклатуры.Номенклатура
	|				И ПриобретениеТоваровУслуг.Характеристика = ДокументыПриобретения.АналитикаУчетаНоменклатуры.Характеристика
	|				И ПриобретениеТоваровУслуг.Назначение = ДокументыПриобретения.АналитикаУчетаНоменклатуры.Назначение
	|				И ПриобретениеТоваровУслуг.НомерГТД = ДокументыПриобретения.НомерГТД
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		КорректировкаПриобретения.Ссылка,
	|		КорректировкаПриобретения.Ссылка.ДокументОснование,
	|		ДокументыПриобретения.АналитикаУчетаНоменклатуры,
	|		ДокументыПриобретения.НомерГТД,
	|		ДокументыПриобретения.МоментВремениОтчета,
	|		ДокументыПриобретения.ТекущийОтчет,
	|		ДокументыПриобретения.ДокументПриобретения
	|	ИЗ
	|		Документ.КорректировкаПриобретения.Товары КАК КорректировкаПриобретения
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеТекущегоОтчета КАК ДокументыПриобретения
	|			ПО ДокументыПриобретения.ДокументОснование = КорректировкаПриобретения.Ссылка.ДокументОснование
	|				И КорректировкаПриобретения.Ссылка.Проведен
	|				И КорректировкаПриобретения.Номенклатура = ДокументыПриобретения.АналитикаУчетаНоменклатуры.Номенклатура
	|				И КорректировкаПриобретения.Характеристика = ДокументыПриобретения.АналитикаУчетаНоменклатуры.Характеристика
	|				И КорректировкаПриобретения.Назначение = ДокументыПриобретения.АналитикаУчетаНоменклатуры.Назначение
	|				И КорректировкаПриобретения.НомерГТД = ДокументыПриобретения.НомерГТД
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВозвратТоваровПоставщику.Ссылка,
	|		ВозвратТоваровПоставщику.ДокументПоступления,
	|		ДокументыПриобретения.АналитикаУчетаНоменклатуры,
	|		ДокументыПриобретения.НомерГТД,
	|		ДокументыПриобретения.МоментВремениОтчета,
	|		ДокументыПриобретения.ТекущийОтчет,
	|		ДокументыПриобретения.ДокументПриобретения
	|	ИЗ
	|		Документ.ВозвратТоваровПоставщику.ВидыЗапасов КАК ВозвратТоваровПоставщику
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеТекущегоОтчета КАК ДокументыПриобретения
	|			ПО ДокументыПриобретения.ДокументОснование = ВозвратТоваровПоставщику.ДокументПоступления
	|				И ВозвратТоваровПоставщику.Ссылка.Проведен
	|				И ВозвратТоваровПоставщику.АналитикаУчетаНоменклатуры = ДокументыПриобретения.АналитикаУчетаНоменклатуры
	|				И ВозвратТоваровПоставщику.НомерГТД = ДокументыПриобретения.НомерГТД) КАК ДанныеПриобретений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|ДанныеПоПрослеживаемостиДо.Ссылка   КАК Ссылка,
	|ДанныеПоПрослеживаемостиДо.Штрихкод КАК Штрихкод
	|
	|ИЗ
	|(ВЫБРАТЬ
	|	ПредыдущиеДокументы.ТекущийОтчет КАК Ссылка,
	|	ТаблицаШтрихкодовДо.ШтрихкодУпаковки КАК Штрихкод
	|ИЗ
	|	ТаблицаОснований КАК ПредыдущиеДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг.ШтрихкодыУпаковок КАК ТаблицаШтрихкодовДо
	|		ПО ПредыдущиеДокументы.Ссылка = ТаблицаШтрихкодовДо.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриемкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаШтрихкодовДо.Ссылка = ДокументыПрямогоОбмена.ДокументОснование
	|			И НЕ ДокументыПрямогоОбмена.ПометкаУдаления
	|ГДЕ
	|	ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПредыдущиеДокументы.ТекущийОтчет,
	|	ТаблицаШтрихкодовДо.ШтрихкодУпаковки
	|ИЗ
	|	ТаблицаОснований КАК ПредыдущиеДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения.ШтрихкодыУпаковок КАК ТаблицаШтрихкодовДо
	|		ПО ПредыдущиеДокументы.Ссылка = ТаблицаШтрихкодовДо.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриемкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаШтрихкодовДо.Ссылка = ДокументыПрямогоОбмена.ДокументОснование
	|			И НЕ ДокументыПрямогоОбмена.ПометкаУдаления
	|ГДЕ
	|	ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПредыдущиеДокументы.ТекущийОтчет,
	|	ТаблицаШтрихкодовДо.ШтрихкодУпаковки
	|ИЗ
	|	ТаблицаОснований КАК ПредыдущиеДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровПоставщику.ШтрихкодыУпаковок КАК ТаблицаШтрихкодовДо
	|		ПО ПредыдущиеДокументы.Ссылка = ТаблицаШтрихкодовДо.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриемкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаШтрихкодовДо.Ссылка = ДокументыПрямогоОбмена.ДокументОснование
	|			И НЕ ДокументыПрямогоОбмена.ПометкаУдаления
	|ГДЕ
	|	ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL) КАК ДанныеПоПрослеживаемостиДо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыПриобретения.ТекущийОтчет                                             КАК ТекущийОтчет,
	|	Товары.Номенклатура                                                            КАК Номенклатура,
	|	Товары.Характеристика                                                          КАК Характеристика,
	|	Товары.Назначение                                                              КАК Назначение,
	|	Товары.НомерГТД                                                                КАК НомерГТД,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА Товары.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ Товары.Упаковка
	|	КОНЕЦ                                                                          КАК Упаковка,
	|	Товары.СтавкаНДС                                                               КАК СтавкаНДС,
	|	СУММА(Товары.Количество)                                                       КАК Количество,
	|	СУММА(ВЫБОР
	|			КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|				ТОГДА Товары.Количество
	|			ИНАЧЕ Товары.КоличествоУпаковок
	|		КОНЕЦ)                                                                     КАК КоличествоУпаковок,
	|	СУММА(Товары.КоличествоПоРНПТ)                                                 КАК КоличествоПоРНПТ,
	|	СУММА(ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Товары.СуммаСНДС - Товары.СуммаНДС)) КАК СуммаБезНДС,
	|	СУММА(ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Товары.СуммаНДС))                       КАК СуммаНДС,
	|	Товары.АналитикаУчетаНоменклатуры.Серия                                        КАК Серия
	|ПОМЕСТИТЬ ИсходныеДанныеПоОтчетам
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОснований КАК ДокументыПриобретения
	|		ПО Товары.ДокументПриобретения = ДокументыПриобретения.Ссылка
	|			И Товары.Ссылка.Проведен
	|			И Товары.АналитикаУчетаНоменклатуры = ДокументыПриобретения.АналитикаУчетаНоменклатуры
	|			И Товары.НомерГТД = ДокументыПриобретения.НомерГТД
	|			И Товары.Ссылка.МоментВремени < ДокументыПриобретения.МоментВремениОтчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыРегл
	|		ПО Товары.Ссылка = СуммыРегл.Регистратор
	|			И Товары.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыПриобретения.ТекущийОтчет,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение,
	|	Товары.НомерГТД,
	|	Товары.Упаковка,
	|	Товары.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА Товары.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ Товары.Упаковка
	|	КОНЕЦ,
	|	Товары.АналитикаУчетаНоменклатуры.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ТаблицаДокумента.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	ЛОЖЬ КАК ЭтоНабор,
	|	ЛОЖЬ КАК ЭтоКомплектующие,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаДокумента.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаДокумента.Номенклатура.Код
	|	КОНЕЦ КАК НоменклатураКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ &ТекстЗапросаКодЕдиницыИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ТаблицаДокумента.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.НомерГТД КАК НомерГТДСсылка,
	|	ЕСТЬNULL(ТаблицаДокумента.НомерГТД.ТипНомераГТД, НЕОПРЕДЕЛЕНО) КАК ТипНомераГТД,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.НомерГТД.РегистрационныйНомер = """"""""
	|			ТОГДА ТаблицаДокумента.НомерГТД
	|		ИНАЧЕ ТаблицаДокумента.НомерГТД.РегистрационныйНомер
	|	КОНЕЦ КАК НомерГТД,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0) + ТаблицаДокумента.Количество
	|		ИНАЧЕ ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоУпаковок, 0) + ТаблицаДокумента.КоличествоУпаковок
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0)
	|		ИНАЧЕ ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоУпаковок, 0)
	|	КОНЕЦ КАК КоличествоДо,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0) + ТаблицаДокумента.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0) КАК КоличествоПоРНПТДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.КоличествоПоРНПТ >= ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0)
	|			ТОГДА ТаблицаДокумента.КоличествоПоРНПТ + ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоПрослежУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.КоличествоПоРНПТ < ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0)
	|			ТОГДА ТаблицаДокумента.КоличествоПоРНПТ + ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоПрослежУменьшение,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаДокумента.Количество <> 0
	|						ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.Количество
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблицаДокумента.КоличествоУпаковок <> 0
	|					ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.КоличествоУпаковок
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК Цена,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0) <> 0
	|						ТОГДА ТаблицаТоваровПредыдущие.СуммаБезНДС / ТаблицаТоваровПредыдущие.Количество
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоУпаковок, 0) <> 0
	|					ТОГДА ТаблицаТоваровПредыдущие.СуммаБезНДС / ТаблицаТоваровПредыдущие.КоличествоУпаковок
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК ЦенаДо,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ТаблицаДокумента.СуммаБезНДС КАК СуммаБезНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) КАК СуммаБезНДСДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС >= 0
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаБезНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС < 0
	|			ТОГДА -ТаблицаДокумента.СуммаБезНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаБезНДСУменьшение,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0) + ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0) КАК СуммаНДСДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаНДС >= 0
	|			ТОГДА ТаблицаДокумента.СуммаНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаНДС < 0
	|			ТОГДА -ТаблицаДокумента.СуммаНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаНДСУменьшение,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0) + ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС КАК СуммаСНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0) КАК СуммаСНДСДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС >= 0
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаСНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС < 0
	|			ТОГДА -(ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаСНДСУменьшение,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ЛОЖЬ КАК ЭтоВозвратнаяТара,
	|	1 КАК Порядок,
	|	ТаблицаДокумента.Номенклатура.КодТНВЭД КАК КодТНВЭД,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения КАК ЕдиницаИзмеренияТНВЭД,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияТНВЭДНаименование,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияТНВЭДКод,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0) + ТаблицаДокумента.Количество КАК КоличествоБазовыхЕдиниц,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0) КАК КоличествоБазовыхЕдиницДо
	|ИЗ
	|	ОтчетКомитентуОЗакупкахТаблицаТоваров КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИсходныеДанныеПоОтчетам КАК ТаблицаТоваровПредыдущие
	|		ПО ТаблицаДокумента.Ссылка = ТаблицаТоваровПредыдущие.ТекущийОтчет
	|			И ТаблицаДокумента.Номенклатура = ТаблицаТоваровПредыдущие.Номенклатура
	|			И ТаблицаДокумента.Характеристика = ТаблицаТоваровПредыдущие.Характеристика
	|			И ТаблицаДокумента.Упаковка = ТаблицаТоваровПредыдущие.Упаковка
	|			И ТаблицаДокумента.НомерГТД = ТаблицаТоваровПредыдущие.НомерГТД
	|			И ТаблицаДокумента.Серия = ТаблицаТоваровПредыдущие.Серия
	|ГДЕ
	|	(ТаблицаДокумента.СуммаНДС <> ТаблицаДокумента.СуммаНДС + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ИЛИ ТаблицаДокумента.СуммаБезНДС <> ТаблицаДокумента.СуммаНДС + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|			ИЛИ &УсловиеМаркируемаяПродукция)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка";

	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));

	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения", Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
	Запрос.УстановитьПараметр("ПредставлениеСчетФактура", НСтр("ru = 'счет-фактура'"));
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	Запрос.УстановитьПараметр("ПустаяУпаковкаНоменклатуры", Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПересчитыватьВВалютуРегл", ПараметрыЗаполнения.ПересчитыватьВВалютуРегл);
	
	ВидыМаркируемойПродукции = ИнтеграцияИСМП.ИспользуемыеВидыМаркируемойПродукцииИСМП();
	ИнтеграцияИСМПУТ.УстановитьУсловиеПоВидуМаркируемойПродукции(Запрос.Текст, ВидыМаркируемойПродукции, "ТаблицаДокумента.Номенклатура");
	
	МассивРезультатов			= Запрос.ВыполнитьПакет();
	РезультатПоШапке			= МассивРезультатов[3];
	ШтрихкодыУпаковок			= МассивРезультатов[5].Выгрузить();
	Маркировка					= ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое(ШтрихкодыУпаковок);
	РезультатПоПоставщикам		= МассивРезультатов[6];
	РезультатПоКонтрагентам		= МассивРезультатов[7];
	ПрослеживаемыеКомплектующие	= УчетПрослеживаемыхТоваровЛокализация.ПрослеживаемыеКомплектующиеДляПечатиДанных(
									МассивРезультатов[8]);
	РезультатПоИсходнымДанным   = МассивРезультатов[9];
	ШтрихкодыУпаковокДо         = МассивРезультатов[11].Выгрузить();
	МаркировкаДо				= ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое(ШтрихкодыУпаковокДо);
	РезультатПоТабличнойЧасти	= МассивРезультатов[13];
	
	ДанныеДляПечати = Новый Структура;
	ДанныеДляПечати.Вставить("РезультатПоШапке",            РезультатПоШапке);
	ДанныеДляПечати.Вставить("РезультатПоТабличнойЧасти",   РезультатПоТабличнойЧасти);
	ДанныеДляПечати.Вставить("Маркировка",                  Маркировка);
	ДанныеДляПечати.Вставить("МаркировкаДо",                МаркировкаДо);
	ДанныеДляПечати.Вставить("РезультатПоПоставщикам",      РезультатПоПоставщикам);
	ДанныеДляПечати.Вставить("РезультатПоКонтрагентам",     РезультатПоКонтрагентам);
	ДанныеДляПечати.Вставить("ПрослеживаемыеКомплектующие", ПрослеживаемыеКомплектующие);
	ДанныеДляПечати.Вставить("РезультатПоИсходнымДанным",   РезультатПоИсходнымДанным);
		
	Возврат ДанныеДляПечати;
		
КонецФункции

//-- Локализация

#Область Проведение

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация


	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

//++ Локализация
#Область Печать

#Область СчетНаОплату_1_01

Функция ТекстЗапросаДанныхДокументовДляСчетаНаОплату()
	
	ТекстыЗапроса = Новый Массив;
	ТекстыЗапроса.Добавить(ТекстЗапросаВТОснованийСчетаНаОплату_1_01());
	ТекстыЗапроса.Добавить(ТекстЗапросаВТТоваровОснованияСчетаНаОплату_1_01());
	ТекстыЗапроса.Добавить(СчетНаОплатуКлиентуЛокализация.ТекстЗапросаВТНоменклатураДокументовСчетаНаОплату_1_01());
	ТекстыЗапроса.Добавить(ТекстЗапросаДанныеШапкиСчетаНаОплату_1_01());
	ТекстыЗапроса.Добавить(ТекстЗапросаДанныеЭтаповОплатыСчетаНаОплату_1_01());
	ТекстыЗапроса.Добавить(ТекстЗапросаДанныеТоваровСчетаНаОплату_1_01());
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТОснованийСчетаНаОплату_1_01()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОснований.Ссылка	КАК Ссылка,
	|	ТаблицаОснований.Ссылка	КАК ДокументОснование
	|ПОМЕСТИТЬ ТаблицаОснований
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках КАК ТаблицаОснований
	|ГДЕ
	|	ТаблицаОснований.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ДокументОснование";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДанныеШапкиСчетаНаОплату_1_01()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка	КАК Ссылка,
	|	ДанныеДокумента.Номер	КАК НомерДокумента,
	|	ДанныеДокумента.Дата	КАК ДатаДокумента,
	|	ДанныеДокумента.Ссылка										КАК ДокументОснование,
	|	ПРЕДСТАВЛЕНИЕ(ДанныеДокумента.Ссылка)						КАК НаименованиеОснования,
	|	ДанныеДокумента.Номер										КАК НомерОснования,
	|	ДанныеДокумента.Дата										КАК ДатаОснования,
	|	ЕСТЬNULL(ДанныеДоговора.ИдентификаторГосКонтракта, """")	КАК ИдентификаторГосКонтракта,
	|	""""														КАК ДополнительныеСведения,
	|	ЕСТЬNULL(ДанныеСчетаОрганизации.Владелец, ДанныеДокумента.Организация)	КАК ОрганизацияПолучатель,
	|	ДанныеДокумента.БанковскийСчет											КАК БанковскийСчет,
	|	ДанныеДокумента.Организация	КАК Организация,
	|	ДанныеДокумента.Контрагент	КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО				КАК БанковскийСчетКонтрагента,
	|	НЕОПРЕДЕЛЕНО	КАК Грузоотправитель,
	|	НЕОПРЕДЕЛЕНО	КАК БанковскийСчетГрузоотправителя,
	|	НЕОПРЕДЕЛЕНО	КАК Грузополучатель,
	|	НЕОПРЕДЕЛЕНО	КАК БанковскийСчетГрузополучателя,

	|	ДанныеДокумента.СуммаДокумента + ДанныеДокумента.СуммаВознаграждения	КАК СтоимостьСНДС,
	|	ДанныеДокумента.Дата						КАК ДатаНачалаПоставки,
	|	""""										КАК НазначениеПлатежа,
	|	ДанныеДокумента.ИдентификаторПлатежа		КАК ИнформацияДляОплаты,
	|	ПРЕДСТАВЛЕНИЕ(ДанныеДокумента.ФормаОплаты)	КАК СпособОплаты,
	|	ДанныеДокумента.Валюта								КАК Валюта,
	|	ДанныеВалюты.НаименованиеПолное						КАК НаименованиеВалюты,
	|	ДанныеВалюты.Код									КАК КодВалюты,
	|	ДанныеОрганизации.ВалютаРегламентированногоУчета	КАК ВалютаРегламентированногоУчета,
	|	ДанныеДокумента.НалогообложениеНДС	КАК НалогообложениеНДС,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.НалогообложениеНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|													ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ								КАК УчитыватьНДС,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ								КАК ОперацияОблагаетсяНДСУПокупателя,
	|	ЛОЖЬ								КАК ЧастичнаяОплата,
	|	100									КАК ПроцентОплаты,
	|	ВЫБОР
	|		КОГДА НоменклатураДокументов.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		КОГДА НоменклатураДокументов.ЕстьУслуги
	|				И НЕ НоменклатураДокументов.ЕстьТовары
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ								КАК ТолькоУслуги
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОснований КАК ТаблицаОснований
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОснований.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДанныеДоговора
	|		ПО ДанныеДокумента.Договор = ДанныеДоговора.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчетаОрганизаций КАК ДанныеСчетаОрганизации
	|		ПО ДанныеДокумента.БанковскийСчет = ДанныеСчетаОрганизации.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Валюты КАК ДанныеВалюты
	|		ПО ДанныеДокумента.Валюта = ДанныеВалюты.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ДанныеОрганизации
	|		ПО ДанныеДокумента.Организация = ДанныеОрганизации.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураДокументов КАК НоменклатураДокументов
	|		ПО ДанныеДокумента.Ссылка = НоменклатураДокументов.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеДокумента.Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДанныеЭтаповОплатыСчетаНаОплату_1_01()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	1									КАК Порядок,
	|	ЭтапыОплаты.Ссылка					КАК Ссылка,
	|	МИНИМУМ(ЭтапыОплаты.НомерСтроки)	КАК НомерСтроки,
	|	ЭтапыОплаты.ДатаПлатежа				КАК ДатаПлатежа,
	|	СУММА(ЭтапыОплаты.ПроцентПлатежа)	КАК ПроцентПлатежа,
	|	СУММА(ЭтапыОплаты.СуммаПлатежа)		КАК СуммаПлатежа,
	|	ЛОЖЬ								КАК ЭтоЗалогЗаТару
	|ИЗ
	|	Документ.ОтчетКомитентуОЗакупках.ЭтапыГрафикаОплаты КАК ЭтапыОплаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОснований КАК ТаблицаОснований
	|		ПО ЭтапыОплаты.Ссылка = ТаблицаОснований.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ЭтапыОплаты.Ссылка,
	|	ЭтапыОплаты.ДатаПлатежа
	|
	|ИМЕЮЩИЕ
	|	СУММА(ЭтапыОплаты.СуммаПлатежа) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЭтапыОплаты.Ссылка,
	|	МИНИМУМ(ЭтапыОплаты.НомерСтроки)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДанныеТоваровСчетаНаОплату_1_01()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка										КАК Ссылка,
	|	ТаблицаТоваров.НомерСтроки									КАК НомерСтроки,
	|	ТаблицаТоваров.ВариантПредставленияНабораВПечатныхФормах	КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ТаблицаТоваров.ВариантРасчетаЦеныНабора						КАК ВариантРасчетаЦеныНабора,
	|	ТаблицаТоваров.НоменклатураНабора							КАК НоменклатураНабора,
	|	ТаблицаТоваров.НоменклатураНабора.НаименованиеПолное		КАК НоменклатураНабораНаименованиеПолное,
	|	ТаблицаТоваров.ХарактеристикаНабора							КАК ХарактеристикаНабора,
	|	ЕСТЬNULL(ТаблицаТоваров.ХарактеристикаНабора.НаименованиеПолное, """") КАК ХарактеристикаНабораНаименованиеПолное,
	|	ТаблицаТоваров.Номенклатура									КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ЕСТЬNULL(ДанныеНоменклатуры.Артикул, """")
	|		ИНАЧЕ ЕСТЬNULL(ДанныеНоменклатуры.Код, """")
	|	КОНЕЦ														КАК КодТовара,
	|	ЕСТЬNULL(ДанныеНоменклатуры.КодТНВЭД, НЕОПРЕДЕЛЕНО)			КАК КодТНВЭД,
	|	ЕСТЬNULL(ДанныеНоменклатуры.КодТНВЭД.Код, """")				КАК КодВидаТовара,
	|	ДанныеНоменклатуры.НаименованиеПолное						КАК НоменклатураНаименование,
	|	ДанныеНоменклатуры.ТипНоменклатуры							КАК ТипНоменклатуры,
	|	ЕСТЬNULL(ДанныеНоменклатуры.СтранаПроисхождения, НЕОПРЕДЕЛЕНО) КАК СтранаПроисхождения,
	|	ЕСТЬNULL(ДанныеНоменклатуры.СтранаПроисхождения.Код, """")	КАК КодСтраныПроисхождения,
	|	ЕСТЬNULL(ДанныеНоменклатуры.СтранаПроисхождения.Наименование, """") КАК НаименованиеСтраныПроисхождения,
	|	ТаблицаТоваров.Характеристика								КАК Характеристика,
	|	ЕСТЬNULL(ТаблицаТоваров.Характеристика.НаименованиеПолное, """") КАК ХарактеристикаНаименование,
	|	ТаблицаТоваров.Упаковка										КАК Упаковка,
	|	&ТекстЗапросаЕдиницаИзмерения								КАК ЕдиницаИзмерения,
	|	&ТекстЗапросаКодЕдиницыИзмерения							КАК КодЕдиницыИзмерения,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения					КАК НаименованиеЕдиницыИзмерения,
	|	ТаблицаТоваров.КоличествоУпаковок							КАК Количество,
	|	ТаблицаТоваров.Цена											КАК Цена,
	|	ТаблицаТоваров.СтоимостьБезНДС								КАК СтоимостьБезНДС,
	|	ТаблицаТоваров.СтавкаНДС									КАК СтавкаНДС,
	|	ТаблицаТоваров.СтоимостьСНДС								КАК СтоимостьСНДС,
	|	ТаблицаТоваров.СуммаСкидки									КАК СуммаСкидки,
	|	ТаблицаТоваров.ЦенаБезСкидки								КАК ЦенаБезСкидки,
	|	ТаблицаТоваров.СтоимостьБезНДСБезСкидки						КАК СтоимостьБезНДСБезСкидки,
	|	ТаблицаТоваров.СтоимостьСНДСБезСкидки						КАК СтоимостьСНДСБезСкидки,
	|	ТаблицаТоваров.СуммаНДС										КАК СуммаНДС,
	|	ТаблицаТоваров.СуммаНДС										КАК СуммаНДСБезСкидки,
	|	ЛОЖЬ														КАК ЭтоНабор,
	|	ЛОЖЬ														КАК ЭтоКомплектующие,
	|	ТаблицаТоваров.ЭтоТовар										КАК ЭтоТовар,
	|	ТаблицаТоваров.ЭтоВозвратнаяТара							КАК ЭтоВозвратнаяТара
	|ИЗ
	|	ТоварыОснования КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ДанныеНоменклатуры
	|		ПО ТаблицаТоваров.Номенклатура = ДанныеНоменклатуры.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.НомерСтроки";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
								"&ТекстЗапросаЕдиницаИзмерения",
								Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
									"Ссылка",
									"ТаблицаТоваров.Упаковка",
									"ТаблицаТоваров.Номенклатура"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
								"&ТекстЗапросаНаименованиеЕдиницыИзмерения",
								Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
									"Наименование",
									"ТаблицаТоваров.Упаковка",
									"ТаблицаТоваров.Номенклатура"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
								"&ТекстЗапросаКодЕдиницыИзмерения",
								Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
									"Код",
									"ТаблицаТоваров.Упаковка",
									"ТаблицаТоваров.Номенклатура"));
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

//-- Локализация
#КонецОбласти
