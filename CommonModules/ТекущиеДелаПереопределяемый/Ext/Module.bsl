///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Определяет список обработчиков (модулей менеджеров или общих модулей) для формирования и обновления
// списка всех текущих дел, предусмотренных в конфигурации.
//
// В указанных модулях должна быть размещена процедура обработчика, в которую передается параметр
// ТекущиеДела - см. ТекущиеДелаСервер.ТекущиеДела.
// 
// Далее пример процедуры обработчика для копирования в указанные модули:
//
//// Параметры:
////   ТекущиеДела - см. ТекущиеДелаСервер.ТекущиеДела.
////
//Процедура ПриЗаполненииСпискаТекущихДел(ТекущиеДела) Экспорт
//
//КонецПроцедуры
//
// Параметры:
//  ТекущиеДела - Массив - модули менеджеров или общие модули,
//                         например: Документы.ЗаказПокупателя, ТекущиеДелаПоПродажам.
// Пример:
//  ТекущиеДела.Добавить(Документы.ЗаказПокупателя);
//
Процедура ПриОпределенииОбработчиковТекущихДел(ТекущиеДела) Экспорт
	
	
	ТекущиеДелаУТ.ПриОпределенииОбработчиковТекущихДел(ТекущиеДела);
	ТекущиеДелаЛокализация.ПриОпределенииОбработчиковТекущихДел(ТекущиеДела);
	
КонецПроцедуры

// Задает начальный порядок разделов в панели текущих дел.
//
// Параметры:
//  Разделы - Массив - массив разделов командного интерфейса.
//                     Разделы в панели текущих дел выводятся в
//                     том порядке, в котором они были добавлены в массив.
//
Процедура ПриОпределенииПорядкаРазделовКомандногоИнтерфейса(Разделы) Экспорт
	
	Разделы.Добавить(Метаданные.Подсистемы.CRMИМаркетинг);
	Разделы.Добавить(Метаданные.Подсистемы.Продажи);
	Разделы.Добавить(Метаданные.Подсистемы.Закупки);
	Разделы.Добавить(Метаданные.Подсистемы.Склад);
	Разделы.Добавить(Метаданные.Подсистемы.Казначейство);
	
	Подсистема = Планирование.ПодсистемаПланирование();
	Если Подсистема <> Неопределено Тогда
		Разделы.Добавить(Подсистема);
	КонецЕсли;

	Разделы.Добавить(Метаданные.Подсистемы.ФинансовыйРезультатИКонтроллинг);
	
	Разделы.Добавить(Метаданные.Подсистемы.Администрирование);
	
КонецПроцедуры

// Определяет текущие дела, которые не будут выводиться пользователю.
//
// Параметры:
//  ОтключаемыеДела - Массив - массив строк идентификаторов текущих дел, которые нужно отключать.
//
Процедура ПриОтключенииТекущихДел(ОтключаемыеДела) Экспорт
	
КонецПроцедуры

// Позволяет менять некоторые настройки подсистемы.
//
// Параметры:
//  Параметры - Структура:
//     * ЗаголовокПрочихДел - Строка - заголовок раздела, в который выводятся
//                            дела, не отнесенные к разделам командного интерфейса.
//                            Применимо для тех дел, размещение которых в панели
//                            определяется функцией ТекущиеДелаСервер.РазделыДляОбъекта.
//                            Если не указано - дела выводятся в группу с заголовком
//                            "Прочие дела".
//
Процедура ПриОпределенииНастроек(Параметры) Экспорт
	
	
	
КонецПроцедуры

// Позволяет установить параметры запросов, общие для нескольких текущих дел.
//
// Например, если в нескольких обработчиках получения текущих дел устанавливается
// параметр "ТекущаяДата", то установку параметра можно прописать в данной
// процедуре, а в обработчике формирования дела сделать вызов процедуры
// ТекущиеДела.УстановитьОбщиеПараметрыЗапросов(), которая установит данный параметр.
//
// Параметры:
//  Запрос - Запрос - выполняемый запрос.
//  ОбщиеПараметрыЗапросов - Структура - общие значения для расчета текущих дел.
//
Процедура УстановитьОбщиеПараметрыЗапросов(Запрос, ОбщиеПараметрыЗапросов) Экспорт
	
	Запрос.УстановитьПараметр("ДатаАктуальности", НачалоДня(ОбщиеПараметрыЗапросов.ТекущаяДата));
	
КонецПроцедуры

// Процедура-обработчик, которую можно вызывать в формах расшифровки текущих дел
// для переопределения параметров открытия формы и установки необходимых отборов списках формы.
//
// Параметры:
//  Форма  - ФормаКлиентскогоПриложения - форма, из которой был осуществлен вызов метода.
//  Список - ДинамическийСписок - список, параметры которого можно переопределить.
//
Процедура ПриСозданииНаСервере(Форма, Список) Экспорт
	Перем СтруктураБыстрогоОтбора;
	Перем ИмяТекущейСтраницы;
	
	// Заполним структуру реквизитов формы
	СтруктураРеквизитов = Новый Структура;
	Для Каждого Реквизит Из Форма.ПолучитьРеквизиты() Цикл
		СтруктураРеквизитов.Вставить(Реквизит.Имя);
	КонецЦикла;
	
	Если Форма.Параметры.Свойство("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора) Тогда
		
		Для Каждого КлючИЗначение Из СтруктураБыстрогоОтбора Цикл
			
			Если СтруктураРеквизитов.Свойство(КлючИЗначение.Ключ) Тогда
				
				// Элемент отбора динамического списка выведен на форму
				ОтборыСписковКлиентСервер.ОтборПоЗначениюСпискаПриСозданииНаСервере(
					Список,
					КлючИЗначение.Ключ,
					Форма[КлючИЗначение.Ключ],
					СтруктураБыстрогоОтбора);
				
			Иначе
				
				// Отбор выполняется непосредственно в свойствах динамического списка
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
					Список,
					КлючИЗначение.Ключ,
					КлючИЗначение.Значение,
					ВидСравненияКомпоновкиДанных.Равно,
					,
					Истина);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Установим текущий элемент формы
	Если Форма.Параметры.Свойство("ИмяТекущейСтраницы", ИмяТекущейСтраницы) Тогда
		Форма.ТекущийЭлемент = Форма.Элементы[ИмяТекущейСтраницы];
	КонецЕсли;
	
КонецПроцедуры

// Процедура-обработчик, которую можно вызывать в соответствующем обработчике форм
// расшифровки текущих дел для замены сохраненных значений реквизитов формы.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, из которой был осуществлен вызов метода.
//  Настройки - Соответствие - настройки формы, в которых находятся значения реквизитов.
//
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Форма, Настройки) Экспорт
	Перем СтруктураБыстрогоОтбора;
	
	Если Форма.Параметры.Свойство("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора) Тогда
		
		Для Каждого КлючИЗначение Из СтруктураБыстрогоОтбора Цикл
			
			ЗначениеНастройки = Настройки.Получить(КлючИЗначение.Ключ);
			
			// Если есть сохраненное значение реквизита формы и оно не совпадает со значением
			// из СтруктураБыстрогоОтбора, то заменим сохраненное значение на значение быстрого отбора.
			Если ЗначениеНастройки <> Неопределено И ЗначениеНастройки <> КлючИЗначение.Значение Тогда
				Настройки.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти