
#Область ПрограммныйИнтерфейс

#Область ПараметрыСканирования

Процедура ЗаполнитьПараметрыСканирования(ПараметрыСканирования, Контекст, ВидПродукции, ФормаВыбора) Экспорт
	
	АлкогольнаяПродукция = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная");
	
	Если ЗначениеЗаполнено(ВидПродукции)
			И ОбщегоНазначенияИСКлиентСервер.ВидыПродукцииЕГАИС().Найти(ВидПродукции) = Неопределено Тогда
		Возврат;
		
	ИначеЕсли Контекст = Неопределено И ВидПродукции = АлкогольнаяПродукция Тогда
		
		ВключитьПоддержкуАлкогольнойПродукции(ПараметрыСканирования);
		Возврат;
		
	КонецЕсли;
	
	ЗаполнитьРасширенныеПараметрыСканированияПоДокументам(Контекст, ПараметрыСканирования, ФормаВыбора);
	
КонецПроцедуры

//Дополняет свойства параметров сканирования необходимых для работы с алкогольной продукцией.
//
//Параметры:
//   ПараметрыСканирования - (См. ШтрихкодированиеОбщегоНазначенияИСКлиент.ПараметрыСканирования).
//   Контекст - Произвольный - источник данных,
//   ФормаВыбора - Неопределено, ФормаКлиентскогоПриложения - альтернативный источник данных.
//
Процедура ЗаполнитьБазовыеПараметрыСканирования(ПараметрыСканирования, Контекст, ФормаВыбора = Неопределено) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "ОрганизацияЕГАИС")
		И ЗначениеЗаполнено(Контекст.ОрганизацияЕГАИС) Тогда
		ПараметрыСканирования.ОрганизацияЕГАИС = Контекст.ОрганизацияЕГАИС;
	КонецЕсли;
	
	ИсточникПараметров = ?(ФормаВыбора = Неопределено, Контекст, ФормаВыбора);
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ИсточникПараметров, "Номенклатура") Тогда
		ПараметрыСканирования.Номенклатура = ИсточникПараметров.Номенклатура;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ИсточникПараметров, "Характеристика") Тогда
		ПараметрыСканирования.Характеристика = ИсточникПараметров.Характеристика;
	КонецЕсли;
	
КонецПроцедуры

//Дополняет свойства параметров сканирования необходимых для работы с алкогольной продукцией.
//
//Параметры:
//   ПараметрыСканирования - (См. ШтрихкодированиеОбщегоНазначенияИСКлиент.ПараметрыСканирования).
//   ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - добавляемый вид продукции,
//                - Неопределено - добавить все доступные для обработки виды продукции.
//
//Возвращаемое значение:
//   Булево - параметры сканирования успешно дополнены
//
Функция ВключитьПоддержкуАлкогольнойПродукции(ПараметрыСканирования, ВидПродукции = Неопределено) Экспорт
	
	Алкоголь = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная");
	
	Если ЗначениеЗаполнено(ВидПродукции) И ВидПродукции <> Алкоголь Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПараметрыСканирования.ДопустимыеВидыПродукции.Добавить(Алкоголь);
	ПараметрыСканирования.Вставить("ДокументЕГАИС",                                 Неопределено);
	ПараметрыСканирования.Вставить("КонтрольАкцизныхМарок",                         Ложь);
	ПараметрыСканирования.Вставить("ОрганизацияЕГАИС",                              Неопределено);
	ПараметрыСканирования.Вставить("АлкогольнаяПродукция",                          Неопределено);
	ПараметрыСканирования.Вставить("КонтрольПустогоСтатуса150СимвольныхМарок",      Истина);
	ПараметрыСканирования.Вставить("ИспользуетсяСоответствиеШтрихкодовСтрокДерева", Ложь);
	ПараметрыСканирования.Вставить("СоответствиеШтрихкодовСтрокДерева",             Неопределено);
	ПараметрыСканирования.Вставить("КонтрольЗаполненияМарокСНомеромИСерией",        Истина);
	ПараметрыСканирования.Вставить("ДоступныеСтатусы",                              Новый Массив);
	ПараметрыСканирования.Вставить("Номенклатура",                                  Неопределено);
	ПараметрыСканирования.Вставить("Характеристика",                                Неопределено);
	ПараметрыСканирования.Вставить("Серия",                                         Неопределено);
	ПараметрыСканирования.Вставить("КлючевыеРеквизиты",                             Новый Массив);
	ПараметрыСканирования.Вставить("Операция",                                      Неопределено);
	ПараметрыСканирования.Вставить("ЗапрашиватьНоменклатуру",                       Истина);
	ПараметрыСканирования.Вставить("ТолькоЕГАИС",                                   Ложь);
	
	Возврат Истина;
	
КонецФункции

// Дополняет свойства параметров сканирования необходимых для работы с кодами ИСМП средствами ЕГАИС.
//   Может использоваться для прикладных форм расширяющих функциональность ЕГАИС.
//   Подразумевается что поддержка алкогольной продукции включена.
//
//Параметры:
//   ПараметрыСканирования - (См. ШтрихкодированиеОбщегоНазначенияИСКлиент.ПараметрыСканирования).
//
Процедура ВключитьПоддержкуПива(ПараметрыСканирования) Экспорт
	
	Если ПараметрыСканирования.ПодсистемаИСМПСуществует Тогда
		
		ВидыПродукции = ОбщегоНазначенияИСКлиентСервер.ВидыПродукцииЕГАИС();
		Для Каждого ВидПродукции Из ВидыПродукции Цикл
			ПараметрыСканирования.ДопустимыеВидыПродукции.Добавить(ВидПродукции);
		КонецЦикла;

		ПараметрыСканирования.ТолькоЕГАИС = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьРасширенныеПараметрыСканированияПоДокументам(Контекст, ПараметрыСканирования, ФормаВыбора)
	
	Если ОбщегоНазначенияИСКлиентСервер.ЭтоРасширеннаяВерсияГосИС("ЕГАИС") Тогда
		
		#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
			//@skip-check wrong-string-literal-content
			Модуль = ОбщегоНазначения.ОбщийМодуль("ШтрихкодированиеЕГАИСКлиентСервер");
		#Иначе
			//@skip-check wrong-string-literal-content
			Модуль = ОбщегоНазначенияКлиент.ОбщийМодуль("ШтрихкодированиеЕГАИСКлиентСервер");
		#КонецЕсли
		
		Модуль.ЗаполнитьРасширенныеПараметрыСканированияПоДокументам(Контекст, ПараметрыСканирования, ФормаВыбора);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
