#Область ПрограммныйИнтерфейс

Функция АдресДанныхПроверкиАлкогольнойПродукции(ПараметрыСканирования, Знач Объект, УникальныйИдентификатор) Экспорт
	
	Возврат ПроверкаИПодборПродукцииЕГАИСУТ.АдресДанныхПроверкиАлкогольнойПродукции(ПараметрыСканирования, Объект, УникальныйИдентификатор);
	
КонецФункции

Процедура ПриЗакрытииФормыПроверкиИПодбораВФормеПроверкиЗаполненияДокумента(Форма, Результат) Экспорт
	
	РезультатПроверки = ПолучитьИзВременногоХранилища(Результат);
	ДеревоМаркируемойПродукции = РезультатПроверки.ДеревоМаркируемойПродукции;
	
	ИнтеграцияИСУТ.УдалитьСтрокиАкцизныхМарокПоВидуПродукции(Форма.АкцизныеМарки, Перечисления.ВидыПродукцииИС.Алкогольная);
	
	ПараметрыЗаполнения = АкцизныеМаркиЕГАИС.ПараметрыЗаполненияТоваровИАкцизныхМарок(Форма, Ложь);
	ПараметрыЗаполнения.ЕстьСправка2                   = Ложь;
	ПараметрыЗаполнения.ПараметрыУказанияСерий         = Форма.ПараметрыУказанияСерий;
	ПараметрыЗаполнения.ИмяКолонкиАлкогольнаяПродукция = "НоменклатураЕГАИС";
	ПараметрыЗаполнения.ИмяКолонкиКоличество           = "КоличествоФакт";
	ПараметрыЗаполнения.ИмяКолонкиКоличествоУпаковок   = "КоличествоУпаковокФакт";
	
	АкцизныеМаркиЕГАИС.ЗаполнитьТоварыИАкцизныеМарки(
		Форма, ДеревоМаркируемойПродукции, ПараметрыЗаполнения);
	
	Форма.Подключаемый_ОбработатьСтрокиТЧ(
		ПараметрыЗаполнения.ДобавленныеСтроки,
		ПараметрыЗаполнения.ИзмененныеСтроки);
	
КонецПроцедуры

Процедура ПриЗакрытииФормыПроверкиИПодбораВФормеРМК(Форма, Результат) Экспорт
	
	РезультатПроверки = ПолучитьИзВременногоХранилища(Результат);
	ДеревоМаркируемойПродукции = РезультатПроверки.ДеревоМаркируемойПродукции;
	
	ИнтеграцияИСУТ.УдалитьСтрокиАкцизныхМарокПоВидуПродукции(Форма.Объект.АкцизныеМарки, Перечисления.ВидыПродукцииИС.Алкогольная);
	
	Если Форма.ИмяФормы = "Документ.ЧекККМ.Форма.ФормаДокументаРМК" Тогда
		
		ПараметрыЗаполнения = АкцизныеМаркиЕГАИС.ПараметрыЗаполненияТоваровИАкцизныхМарок(Форма, Ложь);
		ПараметрыЗаполнения.ЕстьКоличествоАкцизныхМарок    = Ложь;
		ПараметрыЗаполнения.ЕстьСправка2                   = Ложь;
		ПараметрыЗаполнения.ПараметрыУказанияСерий         = Форма.ПараметрыУказанияСерий;
		ПараметрыЗаполнения.ИмяКолонкиАлкогольнаяПродукция = "НоменклатураЕГАИС";
		
		Менеджер = ОбщегоНазначенияИС.МенеджерОбъектаПоПолномуИмени(Форма.ИмяФормы);
		Менеджер.ЗаполнитьТоварыИАкцизныеМарки(Форма.Объект, ДеревоМаркируемойПродукции, ПараметрыЗаполнения);
		
		Форма.ОбработатьСтрокиТЧ(
			ПараметрыЗаполнения.ДобавленныеСтроки,
			ПараметрыЗаполнения.ИзмененныеСтроки);
		
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(Форма.Объект,НоменклатураСервер.ПараметрыУказанияСерий(Форма.Объект, Документы.ЧекККМ));
	
	Иначе
		
		ДобавитьМаркиИзДереваВЧекВозврата(Форма, ДеревоМаркируемойПродукции);
		
	КонецЕсли;
	
	ПроверкаИПодборПродукцииИС.ЗаполнитьКешШтрихкодовУпаковок(Форма, ПроверкаИПодборПродукцииИСМПУТ.НастройкиИсточникаКешаЧека());
	ПроверкаИПодборПродукцииИС.ПрименитьКешШтрихкодовУпаковок(Форма, ПроверкаИПодборПродукцииИСМПУТ.НастройкиИсточникаКешаЧека());
	ШтрихкодированиеИС.ОбновитьКэшМаркируемойПродукции(Форма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьМаркиИзДереваВЧекВозврата(Форма, ДеревоМаркируемойПродукции)
	
	Для Каждого ДанныеМарки Из ДеревоМаркируемойПродукции.Строки Цикл
		Если ДанныеМарки.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар Тогда
			НоваяСтрокаМарка = Форма.Объект.АкцизныеМарки.Добавить();
			НоваяСтрокаМарка.АкцизнаяМарка = ДанныеМарки.ШтрихкодУпаковки;
			НоваяСтрокаМарка.Справка2 = ДанныеМарки.Справка2;
		Иначе
			ДобавитьМаркиИзДереваВЧекВозврата(Форма, ДанныеМарки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти