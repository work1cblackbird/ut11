///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.ПереводыСБПc2b.
// ОбщийМодуль.ПереводыСБПc2bКлиентСервер.
//
// Клиент-серверные процедуры общих настроек и статических идентификаторов:
//  - преобразование функциональных ссылок.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Формирует URL функциональной ссылки СБП для записи на NFC.
//
// Параметры:
//  ФункциональнаяСсылка - Строка - URL кассовой ссылки или платежной ссылки СБП.
//
// Возвращаемое значение:
//  Строка - функциональная ссылка СБП для записи на NFC.
//
Функция ФункциональнаяСсылкаNFC(ФункциональнаяСсылка) Экспорт
	
	РезультатURL = "";
	СтруктураURIСсылки = ОбщегоНазначенияКлиентСервер.СтруктураURI(ФункциональнаяСсылка);
	
	Если ПустаяСтрока(СтруктураURIСсылки.ПутьНаСервере) Или ПустаяСтрока(СтруктураURIСсылки.Схема) Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Передан некорректный URL кассовой ссылки: %1'"),
			ФункциональнаяСсылка);
	Иначе
		
		Если Не ПустаяСтрока(СтруктураURIСсылки.Схема) Тогда
			РезультатURL = РезультатURL + СтруктураURIСсылки.Схема + "://";
		КонецЕсли;
		
		Если Не ПустаяСтрока(СтруктураURIСсылки.Логин) Тогда
			РезультатURL = РезультатURL + СтруктураURIСсылки.Логин + ":" + СтруктураURIСсылки.Пароль + "@";
		КонецЕсли;
		
		РезультатURL = РезультатURL + "web." + СтруктураURIСсылки.Хост;
		
		Если Не ПустаяСтрока(СтруктураURIСсылки.Порт) Тогда
			РезультатURL = РезультатURL + ":" 
				+ ?(ТипЗнч(СтруктураURIСсылки.Порт) = Тип("Число"), Формат(СтруктураURIСсылки.Порт, ""), СтруктураURIСсылки.Порт);
		КонецЕсли;
		
		РезультатURL = РезультатURL + "/" + СтруктураURIСсылки.ПутьНаСервере;
		
	КонецЕсли;
	
	Возврат РезультатURL;
	
КонецФункции

#Область УстаревшиеПроцедурыИФункции

// Устарела. Следует использовать ОбщегоНазначенияКлиентСервер.СтруктураURI.
// Определяет валидность URL переданной кассовой ссылки.
//
// Параметры:
//  КассоваяСсылка - Строка - URL кассовой ссылки.
//  ПроверкаПутиНаСервере - Булево - признак проверки заполненности пути на серверер в переданном URL.
//
// Возвращаемое значение:
//  Структура - Содержит результат проверки:
//    *URLВалиден - Булево - если Истина, URL валиден.
//    *СтруктураURI - Структура - составные части URL в виде структуры.
//
Функция ПроверкаURLКассовойСсылки(Знач КассоваяСсылка, Знач ПроверкаПутиНаСервере) Экспорт
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("URLВалиден", Истина);
	РезультатПроверки.Вставить("СтруктураURI", Неопределено);
	
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(
		КассоваяСсылка);
	
	Если СтруктураURI.Схема <> "https"
		Или (СтруктураURI.Порт <> Неопределено И СтруктураURI.Порт <> 443)
		Или (ПроверкаПутиНаСервере И ПустаяСтрока(СтруктураURI.ПутьНаСервере)) Тогда
		РезультатПроверки.URLВалиден = Ложь;
	КонецЕсли;
	
	РезультатПроверки.СтруктураURI = СтруктураURI;
	
	Возврат РезультатПроверки;
	
КонецФункции

#КонецОбласти

#КонецОбласти
