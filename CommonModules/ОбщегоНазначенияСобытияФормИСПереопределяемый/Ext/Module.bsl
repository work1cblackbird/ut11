#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиСобытийФормы

// Возникает на сервере при создании формы.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - создаваемая форма,
//  Отказ - Булево - признак отказа от создания формы,
//  СтандартнаяОбработка - Булево - признак выполнения стандартной обработки.
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	//++ НЕ ГОСИС
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ВозвратИзРегистра2ЕГАИС.Форма.ФормаСпискаДокументов" Тогда
		Форма.Элементы.СтраницаКОформлению.Видимость = Ложь;
		Форма.Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	ИначеЕсли ИмяФормы = "Обработка.ПанельАдминистрированияЕГАИС.Форма.НастройкиЕГАИС" Тогда
		
		НоваяСтрока           = Форма.ДополнительныеКонстанты.Добавить();
		НоваяСтрока.Имя       = "РазрешатьПродажуАлкогольнойПродукцииБезСопоставленияЕГАИС";
		НоваяСтрока.Заголовок = НСтр("ru = 'Продажа без сопоставления классификаторов с ЕГАИС'");
		НоваяСтрока.Описание  = НСтр("ru = 'Разрешать продажу алкогольной продукции без сопоставления классификаторов номенклатуры с ЕГАИС.'");
		
		НоваяСтрока           = Форма.ДополнительныеКонстанты.Добавить();
		НоваяСтрока.Имя       = "ДатаНачалаПримененияПриказа164";
		НоваяСтрока.Заголовок = НСтр("ru = 'Дата начала применения приказа ФСРАР №164'");
		НоваяСтрока.Описание  = НСтр("ru = 'Дата начала применения приказа ФСРАР №164 ""О форме журнала учета объема розничной продажи алкогольной и спиртосодержащей продукции и порядке его заполнения"".'");
		
		НоваяСтрока           = Форма.ДополнительныеКонстанты.Добавить();
		НоваяСтрока.Имя       = "ДатаНачалаРегистрацииЗакупокПоЕГАИС";
		НоваяСтрока.Заголовок = НСтр("ru = 'Дата начала регистрации закупок по ЕГАИС'");
		НоваяСтрока.Описание  = НСтр("ru = 'Дата начала оформления закупок по ЕГАИС: получение ТТН ЕГАИС, ввод накладных с отправкой ответа в ЕГАИС.'");
		
	ИначеЕсли ИмяФормы = "Обработка.ПанельОбменИСМП.Форма.Форма" Тогда
		ИнтеграцияИСКлиентСервер.НастроитьОтборПоОрганизации(Форма, Форма.Организации, Неопределено, "Отбор");
	
	ИначеЕсли ИмяФормы = "Документ.ПриемкаТоваровИСМП.Форма.ЗагрузкаВходящихДокументов" Тогда
		Если Форма.Организации.Количество() = 0 Тогда
			ОрганизацияПоУмолчанию = Справочники.Организации.ОрганизацияПоУмолчанию();
			Если ЗначениеЗаполнено(ОрганизацияПоУмолчанию) Тогда
				ЭлементСписка = Форма.Организации.Добавить();
				ЭлементСписка.Значение = ОрганизацияПоУмолчанию;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ИмяФормы = "Документ.ПогашениеСДИЗЗЕРНО.Форма.ФормаДокумента" Тогда
		
		Форма.Элементы.СгенерироватьСерии.Видимость = Ложь;
		
	ИначеЕсли ИмяФормы = "Справочник.РеестрПартийЗЕРНО.Форма.ФормаЭлемента" Тогда
		
		ДобавляемыеРеквизиты = Новый Массив;
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Ссылка", Новый ОписаниеТипов("СправочникСсылка.РеестрПартийЗЕРНО")));
		Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
		
	ИначеЕсли ИмяФормы = "Справочник.ПартииСАТУРН.Форма.ФормаЭлемента" Тогда
		
		ДобавляемыеРеквизиты = Новый Массив;
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Ссылка", Новый ОписаниеТипов("СправочникСсылка.ПартииСАТУРН")));
		Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
		
	ИначеЕсли ИмяФормы = "Обработка.ГенерацияШтрихкодовУпаковок.Форма.Форма" Тогда
		
		Если Не ПравоДоступа("Чтение", Метаданные.Справочники.ШаблоныЭтикетокИЦенников) Тогда
			Форма.Элементы.ФормаПечатьЭтикетокШтрихкода.Видимость = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		
		Если ИмяФормы = "Документ.ВозвратВОборотИСМП.Форма.ФормаСпискаДокументов"
			ИЛИ ИмяФормы = "Документ.ВыводИзОборотаИСМП.Форма.ФормаСпискаДокументов"
			ИЛИ ИмяФормы = "Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ.Форма.ФормаСпискаДокументов"
			ИЛИ ИмяФормы = "Документ.МаркировкаТоваровИСМП.Форма.ФормаСпискаДокументов"
			ИЛИ ИмяФормы = "Документ.ПеремаркировкаТоваровИСМП.Форма.ФормаСпискаДокументов"
			ИЛИ ИмяФормы = "Документ.ОтгрузкаТоваровИСМП.Форма.ФормаСпискаДокументов"
			ИЛИ ИмяФормы = "Документ.СписаниеКодовМаркировкиИСМП.Форма.ФормаСпискаДокументов"
			ИЛИ ИмяФормы = "Документ.УточнениеСведенийОКодахМаркировкиИСМП.Форма.ФормаСпискаДокументов"
			ИЛИ ИмяФормы = "Документ.ОтчетИСМП.Форма.ФормаСпискаДокументов" Тогда
			
			Форма.Элементы.СтраницыОформленоОтборОрганизация.Видимость = Ложь;
			Форма.Элементы.СтраницыКОформлениюОтборОрганизация.Видимость = Ложь;
			Форма.Элементы.СписокКОформлениюОрганизация.Видимость = Ложь;
			Форма.Элементы.Организация.Видимость = Ложь;
			Форма.Элементы.СписокКОформлениюОрганизация.Видимость = Ложь;
			
		ИначеЕсли ИмяФормы = "Документ.ПриемкаТоваровИСМП.Форма.ФормаСпискаДокументов" Тогда
			
			Форма.Элементы.СтраницыОформленоОтборОрганизация.Видимость = Ложь;
			Форма.Элементы.Организация.Видимость = Ложь;
			
		ИначеЕсли ИмяФормы = "Обработка.ПанельОбменИСМП.Форма.Форма" Тогда
			
			Форма.Элементы.СтраницыОтборОрганизация.Видимость = Ложь;
			
		ИначеЕсли ИмяФормы = "Документ.ВозвратВОборотИСМП.Форма.ФормаСписка"
			ИЛИ ИмяФормы = "Документ.ВыводИзОборотаИСМП.Форма.ФормаСписка"
			ИЛИ ИмяФормы = "Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ.Форма.ФормаСписка"
			ИЛИ ИмяФормы = "Документ.МаркировкаТоваровИСМП.Форма.ФормаСписка"
			ИЛИ ИмяФормы = "Документ.ПеремаркировкаТоваровИСМП.Форма.ФормаСписка"
			ИЛИ ИмяФормы = "Документ.ОтгрузкаТоваровИСМП.Форма.ФормаСписка"
			ИЛИ ИмяФормы = "Документ.ПриемкаТоваровИСМП.Форма.ФормаСписка"
			ИЛИ ИмяФормы = "Документ.СписаниеКодовМаркировкиИСМП.Форма.ФормаСписка"
			ИЛИ ИмяФормы = "Документ.УточнениеСведенийОКодахМаркировкиИСМП.Форма.ФормаСписка"
			ИЛИ ИмяФормы = "Документ.ОтчетИСМП.Форма.ФормаСписка"  Тогда
			
			Форма.Элементы.Организация.Видимость = Ложь;
			
		ИначеЕсли ИмяФормы = "Документ.ВозвратВОборотИСМП.Форма.ФормаДокумента"
			ИЛИ ИмяФормы = "Документ.ВыводИзОборотаИСМП.Форма.ФормаДокумента"
			ИЛИ ИмяФормы = "Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ.Форма.ФормаДокумента"
			ИЛИ ИмяФормы = "Документ.МаркировкаТоваровИСМП.Форма.ФормаДокумента"
			ИЛИ ИмяФормы = "Документ.ПеремаркировкаТоваровИСМП.Форма.ФормаДокумента"
			ИЛИ ИмяФормы = "Документ.ОтгрузкаТоваровИСМП.Форма.ФормаДокумента"
			ИЛИ ИмяФормы = "Документ.ПриемкаТоваровИСМП.Форма.ФормаДокумента"
			ИЛИ ИмяФормы = "Документ.СписаниеКодовМаркировкиИСМП.Форма.ФормаДокумента"
			ИЛИ ИмяФормы = "Документ.УточнениеСведенийОКодахМаркировкиИСМП.Форма.ФормаДокумента"
			ИЛИ ИмяФормы = "Документ.ОтчетИСМП.Форма.ФормаДокумента" Тогда
			
			Форма.Элементы.Организация.Видимость = Ложь;
		ИначеЕсли ИмяФормы = "Документ.ПриемкаТоваровИСМП.Форма.ЗагрузкаВходящихДокументов"
			И Форма.Организации.Количество() Тогда
			Форма.Элементы.Организация.Видимость = Ложь;
		ИначеЕсли ИмяФормы = "Справочник.ОтветственныеЗаАктуализациюТокеновАвторизацииИСМП.Форма.ФормаСписка" Тогда
			
			Форма.Элементы.Организация.Видимость = Ложь;
			
		ИначеЕсли ИмяФормы = "Справочник.ОтветственныеЗаАктуализациюТокеновАвторизацииИСМП.Форма.ФормаЭлемента" Тогда
			
			Форма.Элементы.Организация.Видимость = Ложь;
			
		ИначеЕсли ИмяФормы = "РегистрСведений.СогласиеОПредоставленииИнформацииГИСМТ.Форма.ФормаСписка" Тогда
			
			Форма.Элементы.СтраницыОтборОрганизация.Видимость = Ложь;
			Форма.Элементы.СписокОрганизация.Видимость = Ложь;
			
		ИначеЕсли ИмяФормы = "РегистрСведений.СогласиеОПредоставленииИнформацииГИСМТ.Форма.ФормаЗаписи" Тогда
			
			Форма.Элементы.Организация.Видимость = Ложь;
			
		ИначеЕсли ИмяФормы = "Документ.ФормированиеПартийЗЕРНО.Форма.ФормаСпискаДокументов"
			ИЛИ ИмяФормы = "Документ.ФормированиеПартийИзДругихПартийЗЕРНО.Форма.ФормаСпискаДокументов"
			Или ИмяФормы = "Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО.Форма.ФормаСпискаДокументов"
			Или ИмяФормы = "Документ.ОформлениеСДИЗЗЕРНО.Форма.ФормаСпискаДокументов"
			Или ИмяФормы = "Документ.СписаниеПартийЗЕРНО.Форма.ФормаСпискаДокументов"
			Или ИмяФормы = "Документ.ФормированиеПартийПриПроизводствеЗЕРНО.Форма.ФормаСпискаДокументов" Тогда
			
			Форма.Элементы.СтраницыОформленоОтборОрганизация.Видимость = Ложь;
			Форма.Элементы.СтраницыКОформлениюОтборОрганизация.Видимость = Ложь;
			Форма.Элементы.СписокКОформлениюОрганизация.Видимость = Ложь;
			Форма.Элементы.Организация.Видимость = Ложь;
			
		ИначеЕсли ИмяФормы = "Документ.ЗапросОстатковПартийЗЕРНО.Форма.ФормаСпискаДокументов" Тогда
			
			Форма.Элементы.СтраницыОформленоОтборОрганизация.Видимость = Ложь;
			Форма.Элементы.Организация.Видимость = Ложь;
			
		ИначеЕсли ИмяФормы = "Документ.ПогашениеСДИЗЗЕРНО.Форма.ФормаСпискаДокументов" Тогда
			
			Форма.Элементы.СтраницыОформленоОтборОрганизация.Видимость = Ложь;
			Форма.Элементы.СтраницыКОформлениюОтборОрганизация.Видимость = Ложь;
			Форма.Элементы.Организация.Видимость = Ложь;
			
		ИначеЕсли ИмяФормы = "Документ.ФормированиеПартийЗЕРНО.Форма.ФормаСписка"
			Или ИмяФормы = "Документ.ФормированиеПартийИзДругихПартийЗЕРНО.Форма.ФормаСписка"
			Или ИмяФормы = "Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО.Форма.ФормаСписка"
			Или ИмяФормы = "Документ.ЗапросОстатковПартийЗЕРНО.Форма.ФормаСписка"
			Или ИмяФормы = "Документ.ОформлениеСДИЗЗЕРНО.Форма.ФормаСписка"
			Или ИмяФормы = "Документ.ПогашениеСДИЗЗЕРНО.Форма.ФормаСписка"
			Или ИмяФормы = "Документ.СписаниеПартийЗЕРНО.Форма.ФормаСписка"
			Или ИмяФормы = "Документ.ФормированиеПартийПриПроизводствеЗЕРНО.Форма.ФормаСписка" Тогда
			
			Форма.Элементы.Организация.Видимость = Ложь;
			
		ИначеЕсли ИмяФормы = "Справочник.РеестрПартийЗЕРНО.Форма.ФормаСписка" Тогда
			
			Форма.Элементы.СтраницыОтборОрганизация.Видимость = Ложь;
			
		ИначеЕсли ИмяФормы = "Документ.ФормированиеПартийЗЕРНО.Форма.ФормаДокумента"
			Или ИмяФормы = "Документ.ФормированиеПартийИзДругихПартийЗЕРНО.Форма.ФормаДокумента"
			Или ИмяФормы = "Документ.ВнесениеСведенийОСобранномУрожаеЗЕРНО.Форма.ФормаДокумента"
			Или ИмяФормы = "Документ.ЗапросОстатковПартийЗЕРНО.Форма.ФормаДокумента"
			Или ИмяФормы = "Документ.ОформлениеСДИЗЗЕРНО.Форма.ФормаДокумента"
			Или ИмяФормы = "Документ.ПогашениеСДИЗЗЕРНО.Форма.ФормаДокумента"
			Или ИмяФормы = "Документ.СписаниеПартийЗЕРНО.Форма.ФормаДокумента"
			Или ИмяФормы = "Документ.ФормированиеПартийПриПроизводствеЗЕРНО.Форма.ФормаДокумента" Тогда
			
			Форма.Элементы.Организация.Видимость = Ложь;
			
		ИначеЕсли ИмяФормы = "Справочник.ОтветственныеЗаПодписаниеСообщенийЗЕРНО.Форма.ФормаСписка"
			Или ИмяФормы = "Справочник.ОтветственныеЗаПодписаниеСообщенийЗЕРНО.Форма.ФормаЭлемента" Тогда
			
			Форма.Элементы.Организация.Видимость = Ложь;
			
		ИначеЕсли ИмяФормы = "Обработка.ПанельОбменЗЕРНО.Форма.Форма" Тогда
			
			Форма.Элементы.СтраницыОтборОрганизация.Видимость = Ложь;
			
		ИначеЕсли ИмяФормы = "РегистрСведений.ОчередьСообщенийЗЕРНО.Форма.ФормаСписка" Тогда
			
			Форма.Элементы.Организация.Видимость = Ложь;
			
		ИначеЕсли ИмяФормы = "РегистрСведений.ОчередьСообщенийЗЕРНО.Форма.СообщенияКПодписаниюЗЕРНО" Тогда
			
			Форма.Элементы.СтраницыОтборОрганизация.Видимость = Ложь;
			Форма.Элементы.СообщенияКПодписаниюОрганизация.Видимость = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
#Область ОграничениеТипаОпределяемыйТипСтавкаНДС
	Если ИмяФормы = "Документ.МаркировкаТоваровИСМП.Форма.ФормаДокумента"
		Или ИмяФормы = "Документ.ОтгрузкаТоваровИСМП.Форма.ФормаДокумента"
		Или ИмяФормы = "Документ.ВыводИзОборотаИСМП.Форма.ФормаДокумента"
		Или ИмяФормы = "Документ.ПриемкаТоваровИСМП.Форма.ФормаДокумента" Тогда
			Форма.Элементы.ТоварыСтавкаНДС.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.СтавкиНДС");
	КонецЕсли;
#КонецОбласти

#Область ПроверкаИПодбор

#Область ЗаполнениеДанных
	Если Форма.ИмяФормы = "Обработка.ПроверкаИПодборПродукцииИСМП.Форма.ПроверкаИПодбор"
		ИЛИ Форма.ИмяФормы = "Обработка.ПроверкаИПодборТабачнойПродукцииМОТП.Форма.ПроверкаИПодбор"
		ИЛИ Форма.ИмяФормы = "Обработка.ПроверкаИПодборАлкогольнойПродукцииЕГАИС.Форма.ПроверкаИПодбор" Тогда
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Отправитель") Тогда
			Форма.Отправитель = Форма.Склад;
			Форма.Получатель = Форма.Склад;
		КонецЕсли;
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "СкладОтправитель") Тогда
			Форма.СкладОтправитель = Форма.Склад;
			Форма.СкладПолучатель = Форма.Склад;
		КонецЕсли;
	КонецЕсли;
#КонецОбласти
	
	// В форму проверки и подбора добавляются реквизиты и элементы свзанные с ордерными складами
	Если Форма.ИмяФормы = "Обработка.ПроверкаИПодборПродукцииИСМП.Форма.ПроверкаИПодбор"
		ИЛИ Форма.ИмяФормы = "Обработка.ПроверкаИПодборТабачнойПродукцииМОТП.Форма.ПроверкаИПодбор" Тогда
	
#Область РеквизитыФорммы
		ЦелевыеРеквизиты = Новый Структура;
		ЦелевыеРеквизиты.Вставить("РазрешитьЗагрузкуПоОрдерам", Новый ОписаниеТипов("Булево"));
		
		ВсеРеквизиты = Форма.ПолучитьРеквизиты();
		Для каждого ЭлемРеквизит Из ВсеРеквизиты Цикл	// РеквизитФормы
			Если ЦелевыеРеквизиты.Количество() = 0 Тогда
				Прервать;
			КонецЕсли;
			
			Если ЦелевыеРеквизиты.Свойство(ЭлемРеквизит.Имя) Тогда
				ЦелевыеРеквизиты.Удалить(ЭлемРеквизит.Имя);
			КонецЕсли;
		КонецЦикла;
		Если ЦелевыеРеквизиты.Количество() > 0 Тогда
			ДобавляемыеРеквизиты = Новый Массив;
			Для каждого Элем Из ЦелевыеРеквизиты Цикл
				ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(Элем.Ключ, Элем.Значение));
			КонецЦикла;
			Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
		КонецЕсли;
		
		ИмяРеквизита = "РазрешитьЗагрузкуПоОрдерам";
		Если Форма.Параметры.Свойство(ИмяРеквизита) Тогда
			Форма[ИмяРеквизита] = Форма.Параметры[ИмяРеквизита];
		КонецЕсли;
		
#КонецОбласти
	
#Область ЭлементыФорммы
		ИмяНовойГруппыФормы = "ГруппаНадписьИКартинкаНадписьИнформация";
		ИмяРодительскойГруппыФормы = "ГруппаПодвалМаркируемая";
		ИмяЗамыкающегоЭлемента = "ГруппаПредставлениеПроверкиПодчиненных";
		НоваяКартинкаИмя = "КартинкаИнформация";
		НоваяНадписьИмя = "НадписьИнформация";
		
		ЦелеваяГруппаФормы = Форма.Элементы.Найти(ИмяНовойГруппыФормы);
		ВидимостьЦелевойГруппы = ОбщегоНазначения.ОбъектЯвляетсяГруппой(Форма.Склад);
		Если ВидимостьЦелевойГруппы Тогда
			Форма.ТолькоПросмотр = Истина;
		КонецЕсли;
		Если ЦелеваяГруппаФормы  <> Неопределено Тогда
			ЦелеваяГруппаФормы.Видимость = ВидимостьЦелевойГруппы;
			Возврат;
		КонецЕсли;
		
		РодительскаяГруппа = Форма.Элементы.Найти(ИмяРодительскойГруппыФормы);
		ЗамыкающийЭлемент = Форма.Элементы.Найти(ИмяЗамыкающегоЭлемента);
		
		ЦелеваяГруппаФормы = Форма.Элементы.Вставить(ИмяНовойГруппыФормы, Тип("ГруппаФормы"), РодительскаяГруппа, ЗамыкающийЭлемент);
		ЦелеваяГруппаФормы.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ЦелеваяГруппаФормы.Отображение = ОтображениеОбычнойГруппы.Нет;
		ЦелеваяГруппаФормы.Видимость = ВидимостьЦелевойГруппы;
		
		НоваяКартинка = Форма.Элементы.Добавить(НоваяКартинкаИмя, Тип("ДекорацияФормы"), ЦелеваяГруппаФормы);
		НоваяКартинка.Вид = ВидДекорацииФормы.Картинка;
		НоваяКартинка.Картинка = БиблиотекаКартинок.Информация;
		
		НоваяНадпись = Форма.Элементы.Добавить(НоваяНадписьИмя, Тип("ДекорацияФормы"), ЦелеваяГруппаФормы);
		НоваяНадпись.Вид = ВидДекорацииФормы.Надпись;
		НоваяНадпись.Заголовок = НСтр("ru = 'Форма открыта без возможности изменения. Работа в режиме проверки и подбора марок возможна только если выбран один склад, а не группа складов.'");
		НоваяНадпись.РастягиватьПоГоризонтали = Истина;
		НоваяНадпись.ЦветТекста = ЦветаСтиля.ТекстИнформационнойНадписиИС;
		НоваяНадпись.АвтоМаксимальнаяШирина = Ложь;
		НоваяНадпись.МаксимальнаяШирина = 0;
#КонецОбласти
	КонецЕсли;
#КонецОбласти
	

	СобытияФорм.ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка);
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

// Вызывается при чтении объекта на сервере.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма читаемого объекта,
//  ТекущийОбъект - ДокументОбъект, СправочникОбъект - читаемый объект.
Процедура ПриЧтенииНаСервере(Форма, ТекущийОбъект) Экспорт
	
	СобытияФормИС.ПриЧтенииНаСервере(Форма, ТекущийОбъект);
	
	Возврат;
	
КонецПроцедуры

// Переопределяемая процедура, вызываемая из одноименного обработчика события формы.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, из обработчика события которой происходит вызов процедуры.
//          См. справочную информацию по событиям управляемой формы.
//  ТекущийОбъект - Произвольный - записанный объект.
//  ПараметрыЗаписи - Структура - использованные параметры записи объекта.
Процедура ПослеЗаписиНаСервере(Форма, ТекущийОбъект, ПараметрыЗаписи)Экспорт
	
	СобытияФорм.ПослеЗаписиНаСервере(Форма, ТекущийОбъект, ПараметрыЗаписи);
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
