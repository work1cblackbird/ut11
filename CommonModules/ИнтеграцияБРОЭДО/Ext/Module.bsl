#Область СлужебныйПрограммныйИнтерфейс

// Проверяет необходимость переиздания указанного сертификата для использования в ЭДО.
//
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация для проверки.
//  Сертификат - СертификатКриптографии - сертификат для проверки.
//  ТребуетсяПереиздание - Булево - признак необходимости переиздать сертификат для ЭДО.
//
Процедура ТребуетсяПереизданиеСертификатаЭДО(Знач Организация, Знач Сертификат, ТребуетсяПереиздание) Экспорт
	
	СсылкаНаСертификат = ЭлектроннаяПодпись.СсылкаНаСертификат(Сертификат);
	
	Если ЗначениеЗаполнено(СсылкаНаСертификат) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СертификатыУчетныхЗаписей.ИдентификаторЭДО КАК ИдентификаторОрганизации
		|ИЗ
		|	СертификатыУчетныхЗаписей КАК СертификатыУчетныхЗаписей";
		
		Отбор = СинхронизацияЭДО.НовыйОтборСертификатовУчетныхЗаписей();
		Отбор.Сертификат = "&Сертификат";
		ЗапросСертификатов = СинхронизацияЭДО.ЗапросСертификатовУчетныхЗаписей("СертификатыУчетныхЗаписей", Отбор);
		
		Запросы = Новый Массив;
		Запросы.Добавить(ЗапросСертификатов);
		
		ИтоговыйЗапрос = ОбщегоНазначенияБЭД.СоединитьЗапросы(Запрос, Запросы);
		ИтоговыйЗапрос.УстановитьПараметр("Сертификат", СсылкаНаСертификат);
		
		УстановитьПривилегированныйРежим(Истина);
		Результат = ИтоговыйЗапрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		
		ТребуетсяПереиздание = Не Результат.Пустой();
	Иначе
		ТребуетсяПереиздание = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Инициализирует настройки подключения ЭДО.
//
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация для подключения ЭДО.
//  КодФНС - Строка - код налогового органа организации.
//  Настройки - Строка - инициализированные настройки.
//
Процедура ИнициализироватьНастройкиПодключенияЭДО(Знач Организация, Знач КодФНС, Настройки) Экспорт
	
	Параметры = СинхронизацияЭДОКлиентСервер.НовыеПараметрыПодключенияЭДО();
	Параметры.Организация = Организация;
	Параметры.КодНалоговогоОргана = КодФНС;
	Параметры.ОператорЭДО = "2AE"; // Калуга Астрал.
	Параметры.СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО;
	Параметры.НаименованиеУчетнойЗаписи = СтрШаблон(НСтр("ru = '%1, %2'"), Параметры.Организация, Параметры.СпособОбменаЭД);
	Параметры.НазначениеУчетнойЗаписи = НСтр("ru = 'Основная'");
	Параметры.ПринятыУсловияИспользования = Истина;
	
	КонтактнаяИнформация = ИнтеграцияБСПБЭД.КонтактнаяИнформацияОбъекта(Организация, "ЮрАдресОрганизации");
	Параметры.АдресОрганизации         = КонтактнаяИнформация.Представление;
	Параметры.АдресОрганизацииЗначение = КонтактнаяИнформация.Значение;
		
	Операция = СинхронизацияЭДОКлиентСервер.НоваяОперацияПодключенияЭДО(Параметры);
	
	Настройки = ИнтеграцияБРОЭДОСлужебный.ОперацияЭДОВСтроку(Операция);
	
КонецПроцедуры

// Инициализирует настройки переиздания сертификата криптографии.
//
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация для переиздания сертификата.
//  КодФНС - Строка - код налогового органа организации.
//  Сертификат - СертификатКриптографии - сертификат для переиздания.
//  Настройки - Строка - инициализированные настройки.
//
Процедура ИнициализироватьНастройкиПереизданияСертификатаЭДО(Знач Организация, Знач КодФНС, Знач Сертификат, Настройки) Экспорт
	
	СсылкаНаСертификат = ЭлектроннаяПодпись.СсылкаНаСертификат(Сертификат);
	
	Параметры = СинхронизацияЭДОКлиентСервер.НовыеПараметрыОбновленияСертификата();
	Параметры.Организация = Организация;
	Параметры.Сертификат  = СсылкаНаСертификат;
	
	Операция = СинхронизацияЭДОКлиентСервер.НоваяОперацияОбновленияСертификата(Параметры);
	
	Настройки = ИнтеграцияБРОЭДОСлужебный.ОперацияЭДОВСтроку(Операция);
	
КонецПроцедуры

// Проверяет корректность настроек операции ЭДО (подключение ЭДО, переиздание сертификата).
//
// Параметры:
//  Настройки - Строка - настройки для проверки.
//                       См. ИнициализироватьНастройкиПодключенияЭДО.
//                       См. ИнициализироватьНастройкиПереизданияСертификатаЭДО.
//  НастройкиКорректны - Булево - результат проверки настроек.
//
Процедура ПроверитьНастройкиРегистрацииЭДО(Знач Настройки, НастройкиКорректны) Экспорт
	
	Операция = ИнтеграцияБРОЭДОСлужебный.ОперацияЭДОИзСтроки(Настройки);
	
	НастройкиКорректны = ИнтеграцияБРОЭДОСлужебный.ОперацияЭДОКорректна(Операция);
	
КонецПроцедуры

// Выгружает электронные документы для предоставления в ФНС.
// Предназначена для использования совместно с библиотекой "Регламентированная отчетность".
//
// Параметры:
//  УчетныеДокументы - Массив - массив ссылок на документы информационной базы.
//  УникальныйИдентификатор - УникальныйИдентификатор - будет использован для помещения файлов выгрузки во временное хранилище.
//
// Возвращаемое значение:
//  Соответствие - соответствие документов ИБ:
//    * Ключ     - ДокументСсылка - ссылка на документ-владелец электронного документа.
//    * Значение - Массив Из Структура - параметры электронных документов, с ключами:
//       * ТипФайла - Строка - возможные значения: "ФайлВыгрузки", "ЭЦП", "ФайлПодтверждения", "ЭЦППодтверждения".
//       * КНД      - Строка - КНД выгружаемого электронного документа, заполняется только для файла выгрузки и файла подтверждения.
//       * ИмяФайла - Строка - имя выгружаемого файла.
//       * АдресВременногоХранилища - Строка - адрес временного хранилища с данными файлов выгрузки.
//
Функция ВыгрузкаДокументовДляПередачиВФНС(УчетныеДокументы, УникальныйИдентификатор) Экспорт
	
	ДанныйФайлов = ЭлектронныеДокументыЭДО.ДанныеФайловЭлектронныхДокументовДляВыгрузкиВФНС(
		УчетныеДокументы, УникальныйИдентификатор);
	
	КонверторТипов = Новый Соответствие;
	КонверторТипов.Вставить("ОсновнойТитул", "ФайлВыгрузки");
	КонверторТипов.Вставить("ОтветныйТитул", "ФайлПодтверждения");
	КонверторТипов.Вставить("ОсновнаяПодпись", "ЭЦП");
	КонверторТипов.Вставить("ОтветнаяПодпись", "ЭЦППодтверждения");
		
	Результат = Новый Соответствие;
	
	Для каждого Данные Из ДанныйФайлов Цикл
		
		НаборОписанийФайлов = Новый Массив;
		
		Для каждого Файл Из Данные.Значение Цикл
		
			ОписаниеФайла = Новый Структура;
			ОписаниеФайла.Вставить("ТипФайла", КонверторТипов[Файл.Тип]);
			ОписаниеФайла.Вставить("ИмяФайла", Файл.Имя);
			ОписаниеФайла.Вставить("КНД", Файл.КНД);
			ОписаниеФайла.Вставить("АдресВременногоХранилища", Файл.Данные);
			
			НаборОписанийФайлов.Добавить(ОписаниеФайла);
		
		КонецЦикла;
		
		Результат.Вставить(Данные.Ключ, НаборОписанийФайлов);
	
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

// Выгружает неформализованные электронные документы, подписанные и переданные в формате PDF, для предоставления в ФНС
// в переданное соответствие, исключает выгрузку документов сформированных по стандарту PDF/A-3.
// Предназначена для использования совместно с библиотекой "Регламентированная отчетность".
//
// Параметры:
//  ДанныеФайловПоОбъектамУчета - Соответствие Из КлючИЗначение:
//    * Ключ - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО - ссылка на документ-владелец электронного документа
//    * Значение - Массив Из Структура:
//       * Имя - Строка - имя выгружаемого файла
//       * Размер - Число - размер выгружаемого файла в байтах
//       * АдресДанных - Строка - адрес временного хранилища с данными файла выгрузки
//       * ИмяФайлаПодписи - Строка - имя файла подписи выгружаемого файла
//       * АдресФайлаПодписи - Строка - адрес временного хранилища с данными файла подписи
//  УникальныйИдентификатор - УникальныйИдентификатор - будет использован для помещения файлов выгрузки 
//                                                      во временное хранилище.
//
Процедура ВыгрузкаНеформализованныхДокументовPDFДляПередачиВФНС(ДанныеФайловПоОбъектамУчета, 
	УникальныйИдентификатор) Экспорт
	
	ОбъектыУчета = ОбщегоНазначения.ВыгрузитьКолонку(ДанныеФайловПоОбъектамУчета, "Ключ", Истина);
	
	ДанныеФайловНеформализованныхДокументов = 
		ЭлектронныеДокументыЭДО.ДанныеФайловНеформализованныхДокументовPDFДляВыгрузкиВФНС(ОбъектыУчета, 
			УникальныйИдентификатор);
		
	Для Каждого ДанныеФайловНеформализованногоДокумента Из ДанныеФайловНеформализованныхДокументов Цикл
		ДанныеФайловПоОбъектуУчета = ДанныеФайловПоОбъектамУчета[ДанныеФайловНеформализованногоДокумента.Ключ];
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДанныеФайловПоОбъектуУчета, 
			ДанныеФайловНеформализованногоДокумента.Значение);
	КонецЦикла;
	
КонецПроцедуры

// Выгружает электронные документы для предоставления в ФНС.
//
// Параметры:
//  ЭлектронныеДокументы - Массив из ДокументСсылка.ЭлектронныйДокументВходящийЭДО, ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//
// Возвращаемое значение:
//  Структура - Структура результатов обработки и соответствия файлов:
//  * РезультатыОбработки - Соответствие Из КлючИЗначение - Результаты обработки документов по организациям:
//   ** Ключ - Строка - Наименование организации
//   ** Значение - См. НовыеРезультатыОбработки 
//  * СоответствиеФайлов - Соответствие Из КлючИЗначение:
//   ** Ключ - Строка - Имя файла
//   ** Значение - ДвоичныеДанные - Двоичные данные файла
//  * Ошибки - Массив Из См. НоваяОшибка
Функция СформироватьФайлыВыгрузкиЭДДляФНС(Знач ЭлектронныеДокументы) Экспорт
	
	Результат = Новый Структура("РезультатыОбработки, СоответствиеФайлов, Ошибки",
		Новый Соответствие, Новый Соответствие, Новый Массив);
	
	Если ЭлектронныеДокументы.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	ДанныеОбъектовУчета = ДанныеОбъектовУчетаДляВыгрузкиЭДДляФНС(ЭлектронныеДокументы);
	ЭлектронныеДокументыДляВыгрузки = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЭлектронныеДокументы", ЭлектронныеДокументы);
	Запрос.УстановитьПараметр("ТипыДокументовЭДОВыгрузкиДляФНС", 
		ЭлектронныеДокументыЭДО.ТипыДокументовЭДОВыгрузкиДляФНС());
	Запрос.Текст = ЭлектронныеДокументыЭДО.ТекстЗапросаДляВыгрузкиЭДДляФНС();
	ВыборкаПоОрганизации = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоОрганизации.Следующий() Цикл

		ПакетыДокументовДляВыгрузки = НовыеПакетыДокументовДляВыгрузки();
		РезультатОбработкиПоОрганизации = НовыеРезультатыОбработки();
		
		Организация = ВыборкаПоОрганизации.Организация;
		ОрганизацияПредставление = ВыборкаПоОрганизации.ОрганизацияПредставление;
		ВыборкаДокументов = ВыборкаПоОрганизации.Выбрать();
	
		Пока ВыборкаДокументов.Следующий() Цикл
		
			ЭлектронныеДокументыДляВыгрузки.Добавить(ВыборкаДокументов.ЭлектронныйДокумент);
			ДанныеДокумента = НовыеДанныеДокументаДляВыгрузки();
			ЗаполнитьЗначенияСвойств(ДанныеДокумента, ВыборкаДокументов);
				
			Если ДанныеДокумента.ТипДокументаНеПодходитДляВыгрузки Тогда
				ОбработатьНепредназначенныйДляВыгрузкиЭД(ДанныеДокумента, ПакетыДокументовДляВыгрузки);
				Продолжить;
			КонецЕсли;
				
			РаспределитьЭлектронныеДокументыПоПакетам(ДанныеДокумента, ПакетыДокументовДляВыгрузки);
			
		КонецЦикла;
		
		Если ПакетыДокументовДляВыгрузки.Формализованные.Количество() Тогда
			ВыгрузитьФормализованныеДокументы(Организация, ДанныеОбъектовУчета, ПакетыДокументовДляВыгрузки,
				Результат.СоответствиеФайлов, РезультатОбработкиПоОрганизации, Результат.Ошибки);
		КонецЕсли;
			
		Если ПакетыДокументовДляВыгрузки.НеформализованныеPDF.Количество()
			Или ПакетыДокументовДляВыгрузки.НеформализованныеОфис.Количество()
			Или ПакетыДокументовДляВыгрузки.НеформализованныеКОбработке.Количество() Тогда
			ВыгрузитьНеформализованныеДокументы(Организация, ДанныеОбъектовУчета, ПакетыДокументовДляВыгрузки, 
				Результат.СоответствиеФайлов, РезультатОбработкиПоОрганизации, Результат.Ошибки);
		КонецЕсли;
		
		Если ПакетыДокументовДляВыгрузки.Необработанные.Количество() Тогда
			ВыгрузитьСписокНеобработанныхДокументов(ОрганизацияПредставление, ПакетыДокументовДляВыгрузки, 
				Результат.СоответствиеФайлов, РезультатОбработкиПоОрганизации);
		КонецЕсли;
		
		РезультатОбработкиПоОрганизации.КоличествоДокументов = 
			РезультатОбработкиПоОрганизации.КоличествоВФормализованномВиде
			+ РезультатОбработкиПоОрганизации.КоличествоВФорматеPDF
			+ РезультатОбработкиПоОрганизации.КоличествоСНанесениемШтамповЭП
			+ РезультатОбработкиПоОрганизации.КоличествоСОтдельнымиШтампамиЭП
			+ РезультатОбработкиПоОрганизации.КоличествоНеВыгружено;
		
		Результат.РезультатыОбработки.Вставить(ОрганизацияПредставление, РезультатОбработкиПоОрганизации);
		
	КонецЦикла;
	
	НеобработанныеДокументы = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ЭлектронныеДокументы,
		ЭлектронныеДокументыДляВыгрузки);
	Для Каждого НеобработанныйДокумент Из НеобработанныеДокументы Цикл
		ТекстОшибки = СтрШаблон(НСтр("ru='Документ ""%1"" не подлежит выгрузке для предоставления в ФНС'"),
			НеобработанныйДокумент);
		ДобавитьОшибкуОбработки(Результат.Ошибки, НеобработанныйДокумент, ТекстОшибки);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Получает свойства объектов учета, которые будут отражаться в едином списке документов, представляемых по требованию ФНС.
// Для объекта учета должны существовать электронные документы по завершенным обменам, не помеченные на удаление и 
// имеющие один из следующих типов:
// УПД, СчетФактура, ТоварнаяНакладная, АктВыполненныхРабот, АктНаПередачуПрав,
// УКД, КорректировочныйСчетФактура, СоглашениеОбИзмененииСтоимости, АктОРасхождениях, ДоговорнойДокумент, 
// АктСверкиВзаиморасчетов, АктПриемкиСтроительныхРаботУслуг.
//
// Параметры:
//  ОбъектыУчета - Массив Из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО - массив ссылок на объекты учета электронных документов.
//                       Если параметр указан, требуется заполнить свойства только указанных объектов.
//                       Если параметр не указан или массив пустой, тогда требуется заполнить свойства
//                       для всех объектов учета, по которым ЭДО завершен.
//
// Возвращаемое значение:
//   Соответствие Из КлючИЗначение - Соответствие объектов учета и видов электронных документов:
//    * Ключ     - ДокументСсылка - ссылка на документ учета.
//    * Значение - Строка - тип электронного документа, который следует преобразовать
//                 к строковому представлению определенного формата.
//                 Возможные значения:
//                 ПередачаТоваров, ПередачаУслуг,
//                 УПД, СчетФактура, ТоварнаяНакладнаяТОРГ12, АктПриемкиСдачиРабот, АктНаПередачуПрав,
//                 УКД, КорректировочныйСчетФактура, ДокументОбИзмененииСтоимости, АктОРасхождениях
//                 ДоговорнойДокумент, АктСверкиВзаиморасчетов, АктПриемкиСтроительныхРаботУслуг.
Функция СвойстваОбъектовУчетаЭлектронныхДокументовДляВыгрузкиВФНС(ОбъектыУчета = Неопределено) Экспорт

	СвойстваДокументов = ЭлектронныеДокументыЭДО.СвойстваЭлектронныхДокументовДляВыгрузкиВФНС(ОбъектыУчета);
	
	КонверторКНД = Новый Соответствие;
	КонверторКНД.Вставить("1175010", "ПередачаТоваров");
	КонверторКНД.Вставить("1175012", "ПередачаУслуг");
	
	КонверторТипов = Новый Соответствие;
	КонверторТипов.Вставить(Перечисления.ТипыДокументовЭДО.УПД, "УПД");
	КонверторТипов.Вставить(Перечисления.ТипыДокументовЭДО.СчетФактура, "СчетФактура");
	КонверторТипов.Вставить(Перечисления.ТипыДокументовЭДО.ТоварнаяНакладная, "ТоварнаяНакладнаяТОРГ12");
	КонверторТипов.Вставить(Перечисления.ТипыДокументовЭДО.АктВыполненныхРабот, "АктПриемкиСдачиРабот");
	КонверторТипов.Вставить(Перечисления.ТипыДокументовЭДО.АктНаПередачуПрав, "АктНаПередачуПрав");
	КонверторТипов.Вставить(Перечисления.ТипыДокументовЭДО.УКД, "УКД");
	КонверторТипов.Вставить(Перечисления.ТипыДокументовЭДО.КорректировочныйСчетФактура, "КорректировочныйСчетФактура");
	КонверторТипов.Вставить(
		Перечисления.ТипыДокументовЭДО.СоглашениеОбИзмененииСтоимости, "ДокументОбИзмененииСтоимости");
	КонверторТипов.Вставить(Перечисления.ТипыДокументовЭДО.АктОРасхождениях, "АктОРасхождениях");
	КонверторТипов.Вставить(Перечисления.ТипыДокументовЭДО.ДоговорныйДокумент, "ДоговорнойДокумент");
	КонверторТипов.Вставить(Перечисления.ТипыДокументовЭДО.АктСверкиВзаиморасчетов, "АктСверкиВзаиморасчетов");
	КонверторТипов.Вставить(
		Перечисления.ТипыДокументовЭДО.АктПриемкиСтроительныхРаботУслуг, "АктПриемкиСтроительныхРаботУслуг");
	
	Результат = Новый Соответствие;
	
	Для каждого Свойство Из СвойстваДокументов Цикл
	
		Тип = КонверторКНД[Свойство.КНД];
		
		Если Не ЗначениеЗаполнено(Тип) Тогда
			
			Тип = КонверторТипов[Свойство.Тип];
		
		КонецЕсли;
		
		Если Тип = Неопределено Тогда
			Тип = "";
		КонецЕсли;
		
		Результат.Вставить(Свойство.ОбъектУчета, Тип);
		
	КонецЦикла;
	
	Возврат Результат
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВыгрузкаЭлектронныхДокументовДляФНС

#Область ОбработкаПакетов

// Обрабатывает электронные документы в зависимости от типа регламента и типа документа, распределяет
// в соответствующие массивы документов пакетов для выгрузки.
// 
// Параметры:
//  ДанныеДокумента - см. НовыеДанныеДокументаДляВыгрузки
//  ПакетыДокументовДляВыгрузки - см. НовыеПакетыДокументовДляВыгрузки
//
Процедура РаспределитьЭлектронныеДокументыПоПакетам(ДанныеДокумента, ПакетыДокументовДляВыгрузки)

	ФайлДокумента = ЭлектронныеДокументыЭДО.ОсновнойФайлИнформацииОтправителя(
		ДанныеДокумента.ЭлектронныйДокумент);
	
	Попытка
		ДанныеФайла = РаботаСФайлами.ДанныеФайла(ФайлДокумента);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ТекстОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
		ДобавитьДокументВПакетСОшибкой(ПакетыДокументовДляВыгрузки, ДанныеДокумента.ЭлектронныйДокумент, ТекстОшибки);
		Возврат;
	КонецПопытки;

	Если ДанныеДокумента.ТипДокумента = Перечисления.ТипыДокументовЭДО.ДоговорныйДокумент Тогда
		
		Если ДоговорнойДокументВозможноПредоставитьКакФормализованный(ДанныеДокумента, ДанныеФайла) Тогда
			ПакетыДокументовДляВыгрузки.Формализованные.Добавить(ДанныеДокумента);
		ИначеЕсли Не ФайлПодлежитПечати(ДанныеДокумента, ДанныеФайла, ПакетыДокументовДляВыгрузки) Тогда
			Возврат;
		ИначеЕсли РаботаСФайламиБЭДКлиентСервер.ЭтоРасширениеФайлаPDF(ДанныеФайла.Расширение) Тогда
			ПакетыДокументовДляВыгрузки.НеформализованныеPDF.Добавить(ДанныеДокумента);
		ИначеЕсли РаботаСФайламиБЭД.ВозможноДобавитьШтампЭП(ДанныеФайла.Расширение) Тогда
			ПакетыДокументовДляВыгрузки.НеформализованныеОфис.Добавить(ДанныеДокумента);
		Иначе
			ПакетыДокументовДляВыгрузки.НеформализованныеКОбработке.Добавить(ДанныеДокумента);
		КонецЕсли;
		
	ИначеЕсли ЭлектронныеДокументыЭДО.ДляТипаДокументаЕстьФорматНоНетРегламента(ДанныеДокумента.ТипДокумента) 
		И РаботаСФайламиБЭДКлиентСервер.ЭтоXML(ДанныеФайла.Расширение) Тогда
			
		ПакетыДокументовДляВыгрузки.Формализованные.Добавить(ДанныеДокумента);
		
	ИначеЕсли ДанныеДокумента.ТипРегламента = Перечисления.ТипыРегламентовЭДО.Неформализованный Тогда
		
		Если Не ФайлПодлежитПечати(ДанныеДокумента, ДанныеФайла, ПакетыДокументовДляВыгрузки) Тогда
			Возврат;
		КонецЕсли;
	
		СтандартPDFA3 = РаботаСФайламиБЭД.ЭтоФайлФорматаPDFА3(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла).Результат;
	
		Если РаботаСФайламиБЭДКлиентСервер.ЭтоРасширениеФайлаPDF(ДанныеФайла.Расширение) И Не СтандартPDFA3 Тогда
			ПакетыДокументовДляВыгрузки.НеформализованныеPDF.Добавить(ДанныеДокумента);
		ИначеЕсли РаботаСФайламиБЭД.ВозможноДобавитьШтампЭП(ДанныеФайла.Расширение) Тогда
			ПакетыДокументовДляВыгрузки.НеформализованныеОфис.Добавить(ДанныеДокумента);
		Иначе
			ПакетыДокументовДляВыгрузки.НеформализованныеКОбработке.Добавить(ДанныеДокумента);
		КонецЕсли;
		
	Иначе
		ПакетыДокументовДляВыгрузки.Формализованные.Добавить(ДанныеДокумента);
	КонецЕсли;
	
КонецПроцедуры

// Выгружает формализованные документы в архив по формату БРО, заполняет результаты обработки документов по организации.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация
//  ДанныеОбъектовУчета - см. ДанныеОбъектовУчетаДляВыгрузкиЭДДляФНС
//  ПакетыДокументовДляВыгрузки - см. НовыеПакетыДокументовДляВыгрузки
//  ДанныеФайловСДвоичнымиДанными - Соответствие Из КлючИЗначение:
//   * Ключ - Строка - Имя файла
//   * Значение - ДвоичныеДанные - Двоичные данные файла
//  РезультатОбработкиПоОрганизации - см. НовыеРезультатыОбработки
//  Ошибки - Массив Из См. НоваяОшибка
//
Процедура ВыгрузитьФормализованныеДокументы(Организация, ДанныеОбъектовУчета, ПакетыДокументовДляВыгрузки,
	ДанныеФайловСДвоичнымиДанными, РезультатОбработкиПоОрганизации, Ошибки)

	ОписьВыгрузки = НоваяОписьВыгрузкиФормализованныеДокументы();
	АдресКаталога = РаботаСФайламиБЭД.ВременныйКаталог();
	
	ЗаполнитьОписьИВыгрузитьВКаталог(
		ОписьВыгрузки,
		АдресКаталога,
		ПакетыДокументовДляВыгрузки,
		ДанныеОбъектовУчета);
	
	ИмяФайлаОписания = АдресКаталога + "описание.xml";
	Если ОписьВыгрузки.Количество()
		И Не СформироватьФайлОписанияВыгрузкиФормализованныеДокументы(Организация, ОписьВыгрузки, ИмяФайлаОписания) Тогда
		РаботаСФайламиБЭД.УдалитьВременныеФайлы(АдресКаталога);
		Возврат;
	КонецЕсли;

	Файлы = НайтиФайлы(АдресКаталога, ПолучитьМаскуВсеФайлы());
	Если Файлы.Количество() = 0 Тогда
		ШаблонСообщения = НСтр("ru = 'Не удалось выгрузить документы по Организации ""%1"".'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, Организация);
		ДобавитьОшибкуОбработки(Ошибки, Организация, ТекстСообщения);
		РаботаСФайламиБЭД.УдалитьВременныеФайлы(АдресКаталога);
		Возврат;
	КонецЕсли;
	
	НазваниеАрхива = ИмяФайлаКонтейнераВыгрузкиФормализованныхЭДДляФНС(Организация); 
	ПоместитьФайлыКаталогаВАрхив(Файлы, АдресКаталога, НазваниеАрхива, ДанныеФайловСДвоичнымиДанными);
	
	РезультатОбработкиПоОрганизации.КоличествоВФормализованномВиде = 
		ПакетыДокументовДляВыгрузки.Формализованные.Количество();
	РезультатОбработкиПоОрганизации.НаименованиеАрхиваФормализованных = НазваниеАрхива;
	
КонецПроцедуры

// Выгружает неформализованные документы в архивы в зависимости от необходимых действий
// заполняет результаты обработки документов по организации.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация
//  ДанныеОбъектовУчета - См. ДанныеОбъектовУчетаДляВыгрузкиЭДДляФНС
//  ПакетыДокументовДляВыгрузки - См. НовыеПакетыДокументовДляВыгрузки
//  ДанныеФайловСДвоичнымиДанными - Соответствие Из КлючИЗначение:
//   * Ключ - Строка - Имя файла
//   * Значение - ДвоичныеДанные - Двоичные данные файла
//  РезультатОбработкиПоОрганизации - См. НовыеРезультатыОбработки
//  Ошибки - Массив Из См. НоваяОшибка
//
Процедура ВыгрузитьНеформализованныеДокументы(Организация, ДанныеОбъектовУчета, ПакетыДокументовДляВыгрузки, 
	ДанныеФайловСДвоичнымиДанными, РезультатОбработкиПоОрганизации, Ошибки)
	
	ОбработаноДокументов = 0;
	КоличествоДокументов = ПакетыДокументовДляВыгрузки.НеформализованныеPDF.Количество()
		+ ПакетыДокументовДляВыгрузки.НеформализованныеОфис.Количество()
		+ ПакетыДокументовДляВыгрузки.НеформализованныеКОбработке.Количество();
	
	// Обработка документов в формате PDF
	НазваниеАрхиваPDF = ИмяФайлаКонтейнераВыгрузкиНеформализованныхЭДДляФНС(Организация,, Истина);
	АдресКаталогаPDF = РаботаСФайламиБЭД.ВременныйКаталог();
	ВыгрузитьПакетДокументовPDF(Организация, ДанныеОбъектовУчета, ПакетыДокументовДляВыгрузки, АдресКаталогаPDF, 
		КоличествоДокументов, ОбработаноДокументов, Ошибки);
	
	ФайлыPDF = НайтиФайлы(АдресКаталогаPDF, ПолучитьМаскуВсеФайлы());
	Если ФайлыPDF.Количество() Тогда
		ПоместитьФайлыКаталогаВАрхив(ФайлыPDF, АдресКаталогаPDF, НазваниеАрхиваPDF, ДанныеФайловСДвоичнымиДанными);
	Иначе
		РаботаСФайламиБЭД.УдалитьВременныеФайлы(АдресКаталогаPDF);
	КонецЕсли;
		
	// Обработка документов с вставкой штампов ЭП
	НомерКонтейнера = "1";
	НазваниеАрхиваСНанесеннымиШтампами = ИмяФайлаКонтейнераВыгрузкиНеформализованныхЭДДляФНС(Организация, 
		НомерКонтейнера);
	АдресКаталогаФайлыСоШтампами = РаботаСФайламиБЭД.ВременныйКаталог();
	
	ВыгрузитьПакетДокументовСВставкойШтамповЭП(ПакетыДокументовДляВыгрузки, АдресКаталогаФайлыСоШтампами, 
		КоличествоДокументов, ОбработаноДокументов);
	
	ФайлыСоШтампами = НайтиФайлы(АдресКаталогаФайлыСоШтампами, ПолучитьМаскуВсеФайлы());
	Если ФайлыСоШтампами.Количество() Тогда
		ПоместитьФайлыКаталогаВАрхив(ФайлыСоШтампами, АдресКаталогаФайлыСоШтампами, НазваниеАрхиваСНанесеннымиШтампами,
			ДанныеФайловСДвоичнымиДанными);
	Иначе
		РаботаСФайламиБЭД.УдалитьВременныеФайлы(АдресКаталогаФайлыСоШтампами);
	КонецЕсли;
	
	// Обработка документов без вставки штампов ЭП
	Если ПакетыДокументовДляВыгрузки.НеформализованныеОфис.Количество() Тогда
		НомерКонтейнера = "2";
	КонецЕсли;
	НазваниеАрхиваСДокументамиДляОбработки = ИмяФайлаКонтейнераВыгрузкиНеформализованныхЭДДляФНС(Организация, 
		НомерКонтейнера);
	АдресКаталогаФайлыБезШтампов = РаботаСФайламиБЭД.ВременныйКаталог();
	
	ВыгрузитьПакетДокументовБезВставкиШтамповЭП(ПакетыДокументовДляВыгрузки, АдресКаталогаФайлыБезШтампов,
		КоличествоДокументов, ОбработаноДокументов);
		
	ФайлыБезШтампов = НайтиФайлы(АдресКаталогаФайлыБезШтампов, ПолучитьМаскуВсеФайлы());
	Если ФайлыБезШтампов.Количество() Тогда
		ПоместитьФайлыКаталогаВАрхив(ФайлыБезШтампов, АдресКаталогаФайлыБезШтампов, 
			НазваниеАрхиваСДокументамиДляОбработки, ДанныеФайловСДвоичнымиДанными);
	Иначе
		РаботаСФайламиБЭД.УдалитьВременныеФайлы(АдресКаталогаФайлыБезШтампов);
	КонецЕсли;
	
	// Формирование результатов обработки
	РезультатОбработкиПоОрганизации.КоличествоВФорматеPDF = 
		ПакетыДокументовДляВыгрузки.НеформализованныеPDF.Количество();
	Если РезультатОбработкиПоОрганизации.КоличествоВФорматеPDF <> 0 Тогда
		РезультатОбработкиПоОрганизации.НаименованиеАрхиваНеформализованныхPDF = НазваниеАрхиваPDF;
	КонецЕсли;
	
	РезультатОбработкиПоОрганизации.КоличествоСНанесениемШтамповЭП = 
		ПакетыДокументовДляВыгрузки.НеформализованныеОфис.Количество();
	Если РезультатОбработкиПоОрганизации.КоличествоСНанесениемШтамповЭП <> 0 Тогда
		РезультатОбработкиПоОрганизации.НаименованиеАрхиваСНанесениемШтампов = НазваниеАрхиваСНанесеннымиШтампами;
	КонецЕсли;
	
	РезультатОбработкиПоОрганизации.КоличествоСОтдельнымиШтампамиЭП = 
		ПакетыДокументовДляВыгрузки.НеформализованныеКОбработке.Количество();
	Если РезультатОбработкиПоОрганизации.КоличествоСОтдельнымиШтампамиЭП <> 0 Тогда
		РезультатОбработкиПоОрганизации.НаименованиеАрхиваСОтдельнымиШтампами = НазваниеАрхиваСДокументамиДляОбработки;
	КонецЕсли;
	
	Если ФайлыPDF.Количество() = 0
		И ФайлыСоШтампами.Количество() = 0
		И ФайлыБезШтампов.Количество() = 0 
		Тогда
		ШаблонСообщения = НСтр("ru = 'Не удалось выгрузить документы по Организации ""%1"".'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, Организация);
		ДобавитьОшибкуОбработки(Ошибки, Организация, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

// Выгружает документы в формате PDF с подписями в каталог, формирует опись выгрузки.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация
//  ДанныеОбъектовУчета - См. ДанныеОбъектовУчетаДляВыгрузкиЭДДляФНС
//  ПакетыДокументовДляВыгрузки - См. НовыеПакетыДокументовДляВыгрузки
//  АдресКаталога - Строка
//  КоличествоДокументов - Число
//  ОбработаноДокументов - Число
//  Ошибки - Массив Из См. НоваяОшибка
//
Процедура ВыгрузитьПакетДокументовPDF(Организация, ДанныеОбъектовУчета, ПакетыДокументовДляВыгрузки, АдресКаталога,
	КоличествоДокументов, ОбработаноДокументов, Ошибки)
	
	ОписьВыгрузки = НоваяОписьВыгрузкиДокументыPDF();
	НеобработанныеСОшибкой = Новый Массив;
	
	Для Каждого ДанныеДокумента Из ПакетыДокументовДляВыгрузки.НеформализованныеPDF Цикл
		
		Документ = ДанныеДокумента.ЭлектронныйДокумент;
		ИнформацияОтправителя = ЭлектронныеДокументыЭДО.ДанныеФайлаИнформацииОтправителяДляВыгрузкиФНС(Документ);
		РеквизитыИмениФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ИнформацияОтправителя.ИмяФайла);
		РезультатПроверкиПодписей = ПроверкаПодписейДокументаВыполнена(Документ, ИнформацияОтправителя);
		
		Если РезультатПроверкиПодписей.Результат Тогда
			
			УстановленныеПодписи = ИнформацияОтправителя.УстановленныеПодписи;
			КоличествоПодписей = УстановленныеПодписи.Количество();
			
			Для Каждого ДанныеПодписи Из УстановленныеПодписи Цикл
				
				Если КоличествоПодписей > 1 Тогда
					ИмяФайлаИнформацииОтправителя = СтрШаблон("%1_%2%3", РеквизитыИмениФайла.ИмяБезРасширения, 
						ДанныеПодписи.ПорядковыйНомер, РеквизитыИмениФайла.Расширение);
				Иначе
					ИмяФайлаИнформацииОтправителя = ИнформацияОтправителя.ИмяФайла;
				КонецЕсли;
				
				ДанныеИнформацииОтправителя = ИнформацияОтправителя.ДвоичныеДанные;
				ДанныеИнформацииОтправителя.Записать(АдресКаталога + ИмяФайлаИнформацииОтправителя);
				
				ИмяФайлаПодписи = СтрШаблон("%1SGN.sgn", ИмяФайлаИнформацииОтправителя);
				ДанныеПодписи.Подпись.Записать(АдресКаталога + ИмяФайлаПодписи);
				
				ЭлементОписи = ОписьВыгрузки.Добавить();
				ЭлементОписи.ИмяФайлаДанных = ИмяФайлаИнформацииОтправителя;
				ЭлементОписи.РазмерФайлаДанных = ИнформацияОтправителя.Размер;
				ЭлементОписи.ИмяФайлаПодписи = ИмяФайлаПодписи;
				ЗаполнитьЗначенияСвойств(ЭлементОписи, ДанныеДокумента);
				ЗаполнитьНомерДатаОснованияВЭлементеОписи(ЭлементОписи, ДанныеОбъектовУчета, Документ);
				
			КонецЦикла;
			
		Иначе
			
			ДобавитьДокументВПакетСОшибкой(ПакетыДокументовДляВыгрузки, Документ, РезультатПроверкиПодписей.ТекстОшибки);
			НеобработанныеСОшибкой.Добавить(ДанныеДокумента);
			Продолжить;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ИмяФайлаОписиВыгрузки = АдресКаталога + "scandescription.xml";
	Если ОписьВыгрузки.Количество()
		И Не СформироватьФайлОписанияВыгрузкиДокументыPDF(Организация, ОписьВыгрузки, ИмяФайлаОписиВыгрузки) Тогда
		РаботаСФайламиБЭД.УдалитьВременныеФайлы(АдресКаталога);
		ПакетыДокументовДляВыгрузки.НеформализованныеPDF.Очистить();
		Возврат;
	КонецЕсли;
	
	ПакетыДокументовДляВыгрузки.НеформализованныеPDF = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
		ПакетыДокументовДляВыгрузки.НеформализованныеPDF, НеобработанныеСОшибкой);
	
КонецПроцедуры

// Наносит на документы, подписанные в форматах поддерживающих автоматическое нанесение штампа, 
// штамп электронной подписи, преобразовывает в PDF и сохраняет в каталог.
// 
// Параметры:
//  ПакетыДокументовДляВыгрузки - См. НовыеПакетыДокументовДляВыгрузки
//  АдресКаталога - Строка
//  КоличествоДокументов - Число
//  ОбработаноДокументов - Число
//
Процедура ВыгрузитьПакетДокументовСВставкойШтамповЭП(ПакетыДокументовДляВыгрузки, АдресКаталога, КоличествоДокументов, 
	ОбработаноДокументов)
		
	СообщениеПрогресс = НСтр("ru='""%1"" - попытка сохранить в PDF и добавить штамп электронной подписи.
		|Выполнена обработка %2 из %3 неформализованных документов'");

	НеобработанныеСОшибкой = Новый Массив;
	
	Для Каждого Элемент Из ПакетыДокументовДляВыгрузки.НеформализованныеОфис Цикл
		
		Документ = Элемент.ЭлектронныйДокумент;
		ИнформацияОтправителя = ЭлектронныеДокументыЭДО.ДанныеФайлаИнформацииОтправителяДляВыгрузкиФНС(Документ);
		РезультатПроверкиПодписей = ПроверкаПодписейДокументаВыполнена(Документ, ИнформацияОтправителя);
		
		Если РезультатПроверкиПодписей.Результат Тогда
			
			ДлительныеОперации.СообщитьПрогресс(, 
				СтрШаблон(СообщениеПрогресс, Документ, ОбработаноДокументов, КоличествоДокументов));
			
			ДанныеФайла = ДанныеФайлаНеформализованногоЭД(Документ);
			Попытка 
				ШтампЭП = ЭлектронныеДокументыЭДО.ДанныеШтампаЭППоДаннымСообщенияЭДО(ДанныеФайла.Владелец);
				СсылкаНаВизуализацию = РаботаСФайламиБЭД.ВизуализацияДокументаСШтампомЭП(
					ДанныеФайла.СсылкаНаДвоичныеДанныеФайла, ШтампЭП, ДанныеФайла.Расширение);
			Исключение
				ПакетыДокументовДляВыгрузки.НеформализованныеКОбработке.Добавить(Элемент);
				Продолжить;
			КонецПопытки;
			
			Если СсылкаНаВизуализацию = Неопределено Тогда
				ПакетыДокументовДляВыгрузки.НеформализованныеКОбработке.Добавить(Элемент);
				Продолжить;
			КонецЕсли;
			
			ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(СсылкаНаВизуализацию);
			ДвоичныеДанныеФайла.Записать(СтрШаблон("%1%2.pdf", АдресКаталога, ДанныеФайла.Наименование));
			ОбработаноДокументов = ОбработаноДокументов + 1;
			
		Иначе
			
			ДобавитьДокументВПакетСОшибкой(ПакетыДокументовДляВыгрузки, Документ, РезультатПроверкиПодписей.ТекстОшибки);
			НеобработанныеСОшибкой.Добавить(Элемент);
			Продолжить;
			
		КонецЕсли;
	КонецЦикла;
	
	ПакетыДокументовДляВыгрузки.НеформализованныеОфис = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
		ПакетыДокументовДляВыгрузки.НеформализованныеОфис, ПакетыДокументовДляВыгрузки.НеформализованныеКОбработке);
	ПакетыДокументовДляВыгрузки.НеформализованныеОфис = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
		ПакетыДокументовДляВыгрузки.НеформализованныеОфис, НеобработанныеСОшибкой);
		
КонецПроцедуры

// Выгружает документы с картинками штампов электронной подписи в виде отдельного файла в каталог.
// 
// Параметры:
//  ПакетыДокументовДляВыгрузки - См. НовыеПакетыДокументовДляВыгрузки
//  АдресКаталога - Строка
//  КоличествоДокументов - Число
//  ОбработаноДокументов - Число
//
Процедура ВыгрузитьПакетДокументовБезВставкиШтамповЭП(ПакетыДокументовДляВыгрузки, АдресКаталога, КоличествоДокументов, 
	ОбработаноДокументов)

	СообщениеПрогресс = НСтр("ru='""%1"" - сохранение штампа электронной подписи отдельным файлом.
		|Выполнена обработка %2 из %3 неформализованных документов'");
	
	НеобработанныеСОшибкой = Новый Массив;
	
	Для Каждого Элемент Из ПакетыДокументовДляВыгрузки.НеформализованныеКОбработке Цикл
		
		Документ = Элемент.ЭлектронныйДокумент;
		ИнформацияОтправителя = ЭлектронныеДокументыЭДО.ДанныеФайлаИнформацииОтправителяДляВыгрузкиФНС(Документ);
		РезультатПроверкиПодписей = ПроверкаПодписейДокументаВыполнена(Документ, ИнформацияОтправителя);
		
		Если РезультатПроверкиПодписей.Результат Тогда
			
			ДлительныеОперации.СообщитьПрогресс(, 
				СтрШаблон(СообщениеПрогресс, Документ, ОбработаноДокументов, КоличествоДокументов));
			
			ДанныеФайла = ДанныеФайлаНеформализованногоЭД(Документ);
			ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
			ДвоичныеДанныеФайла.Записать(СтрШаблон("%1%2", АдресКаталога, ДанныеФайла.ИмяФайла));
			
			// Сохраняем штамп ЭП отдельным файлом
			ДанныеШтампаЭП = ЭлектронныеДокументыЭДО.ДанныеШтампаЭППоДаннымСообщенияЭДО(ДанныеФайла.Владелец);
			ДвоичныеДанныеШтампаЭП = ДанныеШтампаЭП.ПолучитьДвоичныеДанные();
			ДвоичныеДанныеШтампаЭП.Записать(СтрШаблон("%1%2_ShtampPodpisi.png", АдресКаталога, ДанныеФайла.Наименование));
			ОбработаноДокументов = ОбработаноДокументов + 1;
			
		Иначе
			
			ДобавитьДокументВПакетСОшибкой(ПакетыДокументовДляВыгрузки, Документ, РезультатПроверкиПодписей.ТекстОшибки);
			НеобработанныеСОшибкой.Добавить(Элемент);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПакетыДокументовДляВыгрузки.НеформализованныеКОбработке = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
		ПакетыДокументовДляВыгрузки.НеформализованныеКОбработке, НеобработанныеСОшибкой);
	
КонецПроцедуры

// Выгружает файл списка необработанных документов с ошибками и навигационными ссылками в формате txt,
// заполняет результаты обработки документов по организации
// 
// Параметры:
//  Организация - Строка - Наименование организации
//  ПакетыДокументовДляВыгрузки - см. НовыеПакетыДокументовДляВыгрузки
//  ДанныеФайловСДвоичнымиДанными - Соответствие Из КлючИЗначение:
//   * Ключ - Строка - ИмяФайла
//   * Значение - ДвоичныеДанные - Двоичные данные файла
//  РезультатОбработкиПоОрганизации - см. НовыеРезультатыОбработки
//
Процедура ВыгрузитьСписокНеобработанныхДокументов(Организация, ПакетыДокументовДляВыгрузки, 
	ДанныеФайловСДвоичнымиДанными, РезультатОбработкиПоОрганизации)

	ТекстовыйДокумент = Новый ТекстовыйДокумент();
	
	Сч = 0;
	Для Каждого Элемент Из ПакетыДокументовДляВыгрузки.Необработанные Цикл
		Сч = Сч + 1;
		НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(Элемент.ЭлектронныйДокумент);
		СтрокаШаблон = НСтр("ru='%1. ""%2"" Навигационная ссылка: %3
			|Причина: %4'");
		Строка = СтрШаблон(СтрокаШаблон, Сч, Элемент.ЭлектронныйДокумент, НавигационнаяСсылка, Элемент.ТекстОшибки);
		ТекстовыйДокумент.ДобавитьСтроку(Строка);
	КонецЦикла;
	
	НаименованиеОрганизации = СтроковыеФункции.СтрокаЛатиницей(Организация);
	ОбработанноеНаименование = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(
		НаименованиеОрганизации, "");
	
	ИмяФайла = СтрШаблон("%1 %2", ОбработанноеНаименование, "neobrabotannye dokumenty.txt");
	Поток = Новый ПотокВПамяти();
	ТекстовыйДокумент.Записать(Поток, КодировкаТекста.UTF8);
	ДвоичныеДанные = Поток.ЗакрытьИПолучитьДвоичныеДанные();
	ДанныеФайловСДвоичнымиДанными.Вставить(ИмяФайла, ДвоичныеДанные);
	
	РезультатОбработкиПоОрганизации.КоличествоНеВыгружено = ПакетыДокументовДляВыгрузки.Необработанные.Количество();
	РезультатОбработкиПоОрганизации.НаименованиеФайлаСНевыгруженными = ИмяФайла; //АПК:1036 не проверять строку на орфографию.
	
КонецПроцедуры

#КонецОбласти

#Область Конструкторы

// Возвращает новую таблицу для описи формализованных электронных документов для выгрузки в ФНС.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Пустая таблица описи выгружаемых для ФНС формализованных документов:
//   * ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументВходящийЭДО,ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//   * Контрагент - ОпределяемыйТип.КонтрагентБЭД
//   * ТипДокументаЭДО - ПеречислениеСсылка.ТипыДокументовЭДО
//   * ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//   * ТипРегламента - ПеречислениеСсылка.ТипыРегламентовЭДО
//   * КНД - Строка - Номер по классификатору налоговых документов
//   * НаправлениеЭДО - ПеречислениеСсылка.НаправленияЭДО 
//   * НомерДокумента - Строка
//   * ДатаДокумента - Дата
//   * НомерДокументаОснования - Строка
//   * ДатаДокументаОснования - Дата
//   * ИмяФайлаДанных - Строка - Наименование файла информации отправителя
//   * ИмяФайлаПодписи - Строка - Наименование файла подписи информации отправителя
//   * РазмерФайлаДанных - Число - Размер файла информации отправителя
//   * РазмерФайлаПодписи - Число - Размер файла подписи информации отправителя
//   * КНДПодтверждения - Строка - Номер по классификатору налоговых документов
//   * ИмяФайлаДанныхПодтверждения - Строка - Наименование файла информации получателя
//   * ИмяФайлаПодписиПодтверждения - Строка - Наименование файла подписи информации получателя
//   * РазмерФайлаДанныхПодтверждения - Число - Размер файла информации получателя
//   * РазмерФайлаПодписиПодтверждения - Число - Размер файла подписи информации получателя
//
Функция НоваяОписьВыгрузкиФормализованныеДокументы()
	
	ОписьВыгрузки = Новый ТаблицаЗначений;
	
	МассивТиповЭД = Новый Массив;
	МассивТиповЭД.Добавить(Тип("ДокументСсылка.ЭлектронныйДокументВходящийЭДО"));
	МассивТиповЭД.Добавить(Тип("ДокументСсылка.ЭлектронныйДокументИсходящийЭДО"));
	ТипЭлектронныйДокумент = Новый ОписаниеТипов(МассивТиповЭД);
	
	ОписьВыгрузки.Колонки.Добавить("ЭлектронныйДокумент", ТипЭлектронныйДокумент);
	ОписьВыгрузки.Колонки.Добавить("Контрагент", Метаданные.ОпределяемыеТипы.КонтрагентБЭД.Тип);
	ОписьВыгрузки.Колонки.Добавить("ТипДокументаЭДО", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыДокументовЭДО"));
	ОписьВыгрузки.Колонки.Добавить("ТипЭлементаРегламента", 
		Новый ОписаниеТипов("ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО"));
	ОписьВыгрузки.Колонки.Добавить("ТипРегламента", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыРегламентовЭДО"));
	ОписьВыгрузки.Колонки.Добавить("КНД", Новый ОписаниеТипов("Строка"));
	ОписьВыгрузки.Колонки.Добавить("НаправлениеЭДО", Новый ОписаниеТипов("ПеречислениеСсылка.НаправленияЭДО"));
	ОписьВыгрузки.Колонки.Добавить("НомерДокумента", Новый ОписаниеТипов("Строка"));
	ОписьВыгрузки.Колонки.Добавить("ДатаДокумента", Новый ОписаниеТипов("Дата"));
	ОписьВыгрузки.Колонки.Добавить("НомерДокументаОснования", Новый ОписаниеТипов("Строка"));
	ОписьВыгрузки.Колонки.Добавить("ДатаДокументаОснования", Новый ОписаниеТипов("Дата"));
	ОписьВыгрузки.Колонки.Добавить("ИмяФайлаДанных", Новый ОписаниеТипов("Строка"));
	ОписьВыгрузки.Колонки.Добавить("ИмяФайлаПодписи", Новый ОписаниеТипов("Строка"));
	ОписьВыгрузки.Колонки.Добавить("РазмерФайлаДанных", Новый ОписаниеТипов("Число"));
	ОписьВыгрузки.Колонки.Добавить("РазмерФайлаПодписи", Новый ОписаниеТипов("Число"));
	ОписьВыгрузки.Колонки.Добавить("КНДПодтверждения", Новый ОписаниеТипов("Строка"));
	ОписьВыгрузки.Колонки.Добавить("ИмяФайлаДанныхПодтверждения", Новый ОписаниеТипов("Строка"));
	ОписьВыгрузки.Колонки.Добавить("ИмяФайлаПодписиПодтверждения", Новый ОписаниеТипов("Строка"));
	ОписьВыгрузки.Колонки.Добавить("РазмерФайлаДанныхПодтверждения", Новый ОписаниеТипов("Число"));
	ОписьВыгрузки.Колонки.Добавить("РазмерФайлаПодписиПодтверждения", Новый ОписаниеТипов("Число"));
	
	Возврат ОписьВыгрузки;
	
КонецФункции

// Возвращает новую таблицу для описи электронных документов в формате PDF для выгрузки в ФНС.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Пустая таблица описи выгружаемых для ФНС документов в формате PDF:
//   * ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументВходящийЭДО,ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//   * ТипДокументаЭДО - ПеречислениеСсылка.ТипыДокументовЭДО
//   * НомерДокумента - Строка
//   * ДатаДокумента - Дата
//   * НомерДокументаОснования - Строка
//   * ДатаДокументаОснования - Дата
//   * ИмяФайлаДанных - Строка - Наименование файла информации отправителя
//   * РазмерФайлаДанных - Число - Размер файла информации отправителя
//   * ИмяФайлаПодписи - Строка - Наименование файла подписи информации отправителя
//
Функция НоваяОписьВыгрузкиДокументыPDF()
	
	ТаблицаОписи = Новый ТаблицаЗначений;
	
	ТипыЭлектронныхДокументов = Новый Массив;
	ТипыЭлектронныхДокументов.Добавить(Тип("ДокументСсылка.ЭлектронныйДокументВходящийЭДО"));
	ТипыЭлектронныхДокументов.Добавить(Тип("ДокументСсылка.ЭлектронныйДокументИсходящийЭДО"));
	ТипЭлектронныйДокумент = Новый ОписаниеТипов(ТипыЭлектронныхДокументов);
	ТаблицаОписи.Колонки.Добавить("ЭлектронныйДокумент", ТипЭлектронныйДокумент);
	ТаблицаОписи.Колонки.Добавить("ТипДокументаЭДО", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыДокументовЭДО"));
	ТаблицаОписи.Колонки.Добавить("НомерДокумента", Новый ОписаниеТипов("Строка"));
	ТаблицаОписи.Колонки.Добавить("ДатаДокумента", Новый ОписаниеТипов("Дата"));
	ТаблицаОписи.Колонки.Добавить("НомерДокументаОснования", Новый ОписаниеТипов("Строка"));
	ТаблицаОписи.Колонки.Добавить("ДатаДокументаОснования", Новый ОписаниеТипов("Дата"));
	ТаблицаОписи.Колонки.Добавить("ИмяФайлаДанных", Новый ОписаниеТипов("Строка"));
	ТаблицаОписи.Колонки.Добавить("РазмерФайлаДанных", Новый ОписаниеТипов("Число"));
	ТаблицаОписи.Колонки.Добавить("ИмяФайлаПодписи", Новый ОписаниеТипов("Строка"));
	
	Возврат ТаблицаОписи;
	
КонецФункции

// Возвращает новую структуру для формирования результатов обработки документов для выгрузки в ФНС.
// 
// Возвращаемое значение:
//  Структура - новая структура результатов обработки:
//   * КоличествоДокументов - Число - общее число документов.
//   * КоличествоВФормализованномВиде - Число - количество документов в формализованном виде
//   * НаименованиеАрхиваФормализованных - Строка - наименование архива формализованных документов
//   * КоличествоВФорматеPDF - Число - количество документов в формате PDF
//   * НаименованиеАрхиваНеформализованныхPDF - Строка - наименование архива документов в формате PDF
//   * КоличествоСНанесениемШтамповЭП - Число - количество документов, на которых удалось нанести штамп ЭП
//   * НаименованиеАрхиваСНанесениемШтампов - Строка - наименование архива неформализованных документов
//                                                     с нанесенными штампами ЭП
//   * КоличествоСОтдельнымиШтампамиЭП - Число - количество документов, на которых НЕ удалось нанести штамп ЭП
//   * НаименованиеАрхиваСОтдельнымиШтампами - Строка - наименование архива неформализованных документов
//                                                      с отдельными штампами ЭП
//   * КоличествоНеВыгружено - Число - количество документов, которые не удалось выгрузить
//   * НаименованиеФайлаСНевыгруженными - Строка - наименование текстового файла, в котором указаны невыгруженные //АПК:1036 не проверять строку на орфографию.
//                                                 документы с указанием причины и навигационной ссылкой.
//
Функция НовыеРезультатыОбработки() 
	
	РезультатОбработки = Новый Структура;
	РезультатОбработки.Вставить("КоличествоДокументов", 0);
	РезультатОбработки.Вставить("КоличествоВФормализованномВиде", 0);
	РезультатОбработки.Вставить("НаименованиеАрхиваФормализованных", "");
	РезультатОбработки.Вставить("КоличествоВФорматеPDF", 0);
	РезультатОбработки.Вставить("НаименованиеАрхиваНеформализованныхPDF", "");
	РезультатОбработки.Вставить("КоличествоСНанесениемШтамповЭП", 0);
	РезультатОбработки.Вставить("НаименованиеАрхиваСНанесениемШтампов", "");
	РезультатОбработки.Вставить("КоличествоСОтдельнымиШтампамиЭП", 0);
	РезультатОбработки.Вставить("НаименованиеАрхиваСОтдельнымиШтампами", "");
	РезультатОбработки.Вставить("КоличествоНеВыгружено", 0);
	РезультатОбработки.Вставить("НаименованиеФайлаСНевыгруженными", ""); //АПК:1036 не проверять строку на орфографию.
	
	Возврат РезультатОбработки;

КонецФункции

// Возвращает новую структуру для обработки документов для выгрузки в ФНС.
// 
// Возвращаемое значение:
//  Структура - Новая структура пакетов для выгрузки:
//   * Формализованные - Массив Из см. НовыеДанныеДокументаДляВыгрузки
//   * НеформализованныеPDF - Массив Из см. НовыеДанныеДокументаДляВыгрузки
//   * НеформализованныеОфис - Массив Из см. НовыеДанныеДокументаДляВыгрузки
//   * НеформализованныеКОбработке - Массив Из см. НовыеДанныеДокументаДляВыгрузки
//   * Необработанные - Массив Из см. НовыеНеобработанныеДокументы
//
Функция НовыеПакетыДокументовДляВыгрузки()
		
	ПакетыДокументовДляВыгрузки = Новый Структура;
	ПакетыДокументовДляВыгрузки.Вставить("Формализованные", Новый Массив);
	ПакетыДокументовДляВыгрузки.Вставить("НеформализованныеPDF", Новый Массив);
	ПакетыДокументовДляВыгрузки.Вставить("НеформализованныеОфис", Новый Массив);
	ПакетыДокументовДляВыгрузки.Вставить("НеформализованныеКОбработке", Новый Массив);
	ПакетыДокументовДляВыгрузки.Вставить("Необработанные", Новый Массив);

	Возврат ПакетыДокументовДляВыгрузки;
	
КонецФункции

// Возвращает новую структуру для данных документа для выгрузки в ФНС.
// 
// Возвращаемое значение:
//  Структура - Новая структура данные документа для выгрузки:
//   * ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументВходящийЭДО,ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//   * Организация - ОпределяемыйТип.Организация - организация электронного документа
//   * Контрагент - ОпределяемыйТип.КонтрагентБЭД - контрагент электронного документа
//   * НомерДокумента - Строка - номер электронного документа
//   * ДатаДокумента - Дата - дата электронного документа
//   * ТипРегламента - ПеречислениеСсылка.ТипыРегламентовЭДО - тип регламента электронного документа
//   * ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО - тип электронного документа
//   * ТипДокументаНеПодходитДляВыгрузки - Булево - информация о возможности выгрузки документа для ФНС
//   * НаправлениеЭДО - ПеречислениеСсылка.НаправленияЭДО - направление сообщения электронного документа
//   * ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО - тип регламента электронного документа
//   * ТипДокументаЭДО - ПеречислениеСсылка.ТипыДокументовЭДО - тип документа сообщения электронного документа
//
Функция НовыеДанныеДокументаДляВыгрузки() 
	
	ДанныеДокументаДляВыгрузки = Новый Структура;
	
	ДанныеДокументаДляВыгрузки.Вставить("ЭлектронныйДокумент");
	ДанныеДокументаДляВыгрузки.Вставить("Организация", ИнтеграцияЭДО.ПустаяСсылкаОрганизации());
	ДанныеДокументаДляВыгрузки.Вставить("Контрагент", ИнтеграцияЭДО.ПустаяСсылкаКонтрагента());
	ДанныеДокументаДляВыгрузки.Вставить("НомерДокумента", "");
	ДанныеДокументаДляВыгрузки.Вставить("ДатаДокумента", Дата(1, 1, 1));
	ДанныеДокументаДляВыгрузки.Вставить("ТипРегламента", Перечисления.ТипыРегламентовЭДО.ПустаяСсылка());
	ДанныеДокументаДляВыгрузки.Вставить("ТипДокумента", Перечисления.ТипыДокументовЭДО.ПустаяСсылка());
	ДанныеДокументаДляВыгрузки.Вставить("ТипДокументаНеПодходитДляВыгрузки", Ложь);
	ДанныеДокументаДляВыгрузки.Вставить("НаправлениеЭДО", Перечисления.НаправленияЭДО.ПустаяСсылка());
	ДанныеДокументаДляВыгрузки.Вставить("ТипЭлементаРегламента", Перечисления.ТипыЭлементовРегламентаЭДО.ПустаяСсылка());
	ДанныеДокументаДляВыгрузки.Вставить("ТипДокументаЭДО", Перечисления.ТипыДокументовЭДО.ПустаяСсылка());
	
	Возврат ДанныеДокументаДляВыгрузки;
	
КонецФункции

// Возвращает новую структуру для необработанных документы.
// 
// Возвращаемое значение:
//  Структура - Новая структура необработанные документы:
//   * ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументВходящийЭДО,ДокументСсылка.ЭлектронныйДокументИсходящийЭДО 
//   * ТекстОшибки - Строка
//
Функция НовыеНеобработанныеДокументы()
	
	НеобработанныеДокументы = Новый Структура;

	НеобработанныеДокументы.Вставить("ЭлектронныйДокумент");
	НеобработанныеДокументы.Вставить("ТекстОшибки", "");
	
	Возврат НеобработанныеДокументы;
	
КонецФункции

// Возвращает новую структуру для записи ошибок.
//
// Возвращаемое значение:
//  Структура:
//   * КлючДанных - ЛюбаяСсылка
//   * ТекстОшибки - Строка
Функция НоваяОшибка()
	
	НоваяОшибка = Новый Структура;
	НоваяОшибка.Вставить("КлючДанных");
	НоваяОшибка.Вставить("ТекстОшибки", "");
	
	Возврат НоваяОшибка;
	
КонецФункции

#КонецОбласти

// Выгружает файлы сообщений.
// 
// Параметры:
//  ЭлементОписи - СтрокаТаблицыЗначений: См. НоваяТаблицаОписиВыгрузкиЭДДляФНС
//  АдресКаталога - Строка - Адрес каталога
//  Сообщения - Массив из Структура:
//   * ИмяФайла - Строка
//   * ДвоичныеДанные - ДвоичныеДанные
//   * Размер - Число
//   * КНД - Строка - номер по классификатору налоговых документов
//   * УстановленныеПодписи - см. ЭлектроннаяПодпись.УстановленныеПодписи
//
Процедура ВыгрузитьФайлыСообщений(ЭлементОписи, АдресКаталога, Сообщения)

	Для Каждого Сообщение Из Сообщения Цикл
		
		ДанныеЭД = Сообщение.ДвоичныеДанные;
		ДанныеЭД.Записать(АдресКаталога + Сообщение.ИмяФайла);
			
		МассивСтруктурПодписей = Сообщение.УстановленныеПодписи;
		СтруктураПодписи = МассивСтруктурПодписей[0];
		ИмяФайлаПодписи = Сообщение.ИмяФайла + "SGN.sgn";
		СтруктураПодписи.Подпись.Записать(АдресКаталога + ИмяФайлаПодписи);
		
		Если Сообщение.ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя Тогда
			ЭлементОписи.ИмяФайлаДанных = Сообщение.ИмяФайла;
			ЭлементОписи.РазмерФайлаДанных = Сообщение.Размер;
			ЭлементОписи.КНД = Сообщение.КНД;
			ЭлементОписи.ИмяФайлаПодписи = ИмяФайлаПодписи;
			ЭлементОписи.РазмерФайлаПодписи = СтруктураПодписи.Подпись.Размер();
		Иначе
			ЭлементОписи.ИмяФайлаДанныхПодтверждения = Сообщение.ИмяФайла;
			ЭлементОписи.РазмерФайлаДанныхПодтверждения = Сообщение.Размер;
			ЭлементОписи.КНДПодтверждения = Сообщение.КНД;
			ЭлементОписи.ИмяФайлаПодписиПодтверждения = ИмяФайлаПодписи;
			ЭлементОписи.РазмерФайлаПодписиПодтверждения = СтруктураПодписи.Подпись.Размер();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Формирует файл описания выгрузки формализованных электронных документов для ФНС по заполненной таблице описи, 
// сохраняет в каталог, возвращает результат успешности формирования файла.
//
// Параметры:
//  Организация - ОпределяемыйТип.Организация - Организация, по документам которой составлена опись
//  ДанныеДляОписи - См. НоваяОписьВыгрузкиФормализованныеДокументы
//  ИмяФайла - Строка - Адрес, куда нужно сохранить файл
//
// Возвращаемое значение:
//  Булево - Истина, если файл сформирован.
//
Функция СформироватьФайлОписанияВыгрузкиФормализованныеДокументы(Организация, ДанныеДляОписи, ИмяФайла)
	
	ПространствоИменСхемы = "Upload2Statements";
	Ошибки = Неопределено;
	
	ДанныеОрганизации = ИнтеграцияЭДО.ДанныеЮрФизЛица(Организация);
	
	Попытка
		
		Файл = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Файл", ПространствоИменСхемы);
		
		ДатаВыгрузки = ТекущаяДатаСеанса();
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Файл, "ВерсФорм", "1.03", Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Файл, "ДатаВыгрузки", Формат(ДатаВыгрузки, "ДФ=dd.MM.yyyy"), Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Файл, "ВремяВыгрузки", Формат(ДатаВыгрузки, "ДФ=HH:mm:ss"), Истина, Ошибки);
		
		СвОрганизация = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Файл.Организация", ПространствоИменСхемы);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
			СвОрганизация,
			"Наименование",
			НаименованиеОрганизацииДляФайлаОписания(Организация, ДанныеОрганизации),
			Истина,
			Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвОрганизация, "ИНН", ДанныеОрганизации.ИНН, Истина, Ошибки);
		Если Не ИнтеграцияЭДО.ЭтоФизЛицо(Организация)
			И КППЗаполненКорректно(ДанныеОрганизации.КПП) Тогда
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвОрганизация, "КПП", ДанныеОрганизации.КПП, Истина, Ошибки);
		КонецЕсли;
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Файл, "Организация", СвОрганизация, Истина, Ошибки);
		
		СвКонтрагенты = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Файл.Контрагенты", ПространствоИменСхемы);
		ИДКонтрагентов = Новый Соответствие;
		
		Для Каждого СтрокаОписи Из ДанныеДляОписи Цикл
			
			Если ИДКонтрагентов[СтрокаОписи.Контрагент] = Неопределено Тогда
				
				ДанныеКонтрагента = ДанныеУчастникаЭДО(СтрокаОписи.Контрагент);
				ИДКонтрагента = ДанныеКонтрагента.ИНН + ДанныеКонтрагента.КПП;
				ИДКонтрагентов.Вставить(СтрокаОписи.Контрагент, ИДКонтрагента);
				
				СвКонтрагент = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML(
					"Файл.Контрагенты.Контрагент", ПространствоИменСхемы);
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвКонтрагент, "Идентификатор", ИДКонтрагента, Истина, Ошибки);
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
					СвКонтрагент, "Наименование", ДанныеКонтрагента.Наименование, Истина, Ошибки);
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвКонтрагент, "ИНН", ДанныеКонтрагента.ИНН, Истина, Ошибки);
				Если ДанныеКонтрагента.ЭтоЮЛ
					И КППЗаполненКорректно(ДанныеКонтрагента.КПП) Тогда
					РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвКонтрагент, "КПП", ДанныеКонтрагента.КПП, Истина, Ошибки);
				КонецЕсли;
				СвКонтрагенты.Контрагент.Добавить(СвКонтрагент);
				
			КонецЕсли;
			
			СвДокумент = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Файл.Документ", ПространствоИменСхемы);
			
			КодВидаДокументаБРО = КодВидаДокументаБРОПоСвойствамЭДО(СтрокаОписи.ТипРегламента,
				СтрокаОписи.ТипДокументаЭДО, СтрокаОписи.ТипЭлементаРегламента);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДокумент, "Вид", КодВидаДокументаБРО, Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДокумент, "КНД", СтрокаОписи.КНД, Истина, Ошибки);
			Направление = ?(СтрокаОписи.НаправлениеЭДО = Перечисления.НаправленияЭДО.Входящий, "0", "1");
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДокумент, "Направление", Направление, Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДокумент, "Номер", СтрокаОписи.НомерДокумента, Истина, Ошибки);
			ДатаДок = Формат(СтрокаОписи.ДатаДокумента, "ДФ=dd.MM.yyyy");
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДокумент, "Дата", ДатаДок, Истина, Ошибки);
			Если ЗначениеЗаполнено(СтрокаОписи.ДатаДокументаОснования)
				И ЗначениеЗаполнено(СтрокаОписи.НомерДокументаОснования) Тогда
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДокумент, "НомерДокОсн", СтрокаОписи.НомерДокументаОснования, , Ошибки);
				ДатаДок = Формат(СтрокаОписи.ДатаДокументаОснования, "ДФ=dd.MM.yyyy");
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДокумент, "ДатаДокОсн", ДатаДок, , Ошибки);
			КонецЕсли;
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДокумент, "ИдКонтрагента", ИДКонтрагентов[СтрокаОписи.Контрагент], Истина, Ошибки);
			
			СвФайл = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Файл.Документ.ФайлДок", ПространствоИменСхемы);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвФайл, "Имя", СтрокаОписи.ИмяФайлаДанных, Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвФайл, "Размер", СтрокаОписи.РазмерФайлаДанных, Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвФайл, "КНД", СтрокаОписи.КНД, Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДокумент, "ФайлДок", СвФайл, Истина, Ошибки);
			
			СвФайл = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Файл.Документ.ФайлЭЦП", ПространствоИменСхемы);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвФайл, "Имя", СтрокаОписи.ИмяФайлаПодписи, Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвФайл, "Размер", СтрокаОписи.РазмерФайлаПодписи, Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДокумент, "ФайлЭЦП", СвФайл, Истина, Ошибки);
			
			Если ЗначениеЗаполнено(СтрокаОписи.ИмяФайлаДанныхПодтверждения) Тогда
				
				СвФайл = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Файл.Документ.ФайлДокПодтверждения", ПространствоИменСхемы);
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвФайл, "Имя", СтрокаОписи.ИмяФайлаДанныхПодтверждения, Истина, Ошибки);
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвФайл, "Размер", СтрокаОписи.РазмерФайлаДанныхПодтверждения, Истина, Ошибки);
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвФайл, "КНД", СтрокаОписи.КНДПодтверждения, Истина, Ошибки);
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДокумент, "ФайлДокПодтверждения", СвФайл, , Ошибки);
				
				СвФайл = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Файл.Документ.ФайлЭЦППодтверждения", ПространствоИменСхемы);
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвФайл, "Имя", СтрокаОписи.ИмяФайлаПодписиПодтверждения, Истина, Ошибки);
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвФайл, "Размер", СтрокаОписи.РазмерФайлаПодписиПодтверждения, Истина, Ошибки);
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДокумент, "ФайлЭЦППодтверждения", СвФайл, , Ошибки);
				
			КонецЕсли;
			
			Файл.Документ.Добавить(СвДокумент);
			
		КонецЦикла;
		
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Файл, "Контрагенты", СвКонтрагенты, Истина, Ошибки);
		
		Файл.Проверить();
		Если ЗначениеЗаполнено(Ошибки) Тогда
			ТекстОшибки = ОбщегоНазначенияБЭД.СоединитьОшибки(Ошибки);
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;
		
		РаботаСФайламиБЭД.СохранитьXDTO(Файл, ИмяФайла, Ложь);
		
		Возврат Истина;
		
	Исключение
		
		СформироватьОшибкуФормированияФайлаОписиВыгрузки(Ошибки);
		Возврат Ложь;
		
	КонецПопытки;
	
КонецФункции

// Формирует файл описания выгрузки электронных документов, подписанных в формате PDF, для ФНС по заполненной 
// таблице описи, сохраняет в каталог, возвращает результат успешности формирования файла.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - Организация, по документам которой составлена опись
//  ДанныеДляОписи - См. НоваяОписьВыгрузкиФормализованныеДокументы
//  ИмяФайла - Строка - Адрес, куда нужно сохранить файл
//
// Возвращаемое значение:
//  Булево - Истина, если файл сформирован.
//
Функция СформироватьФайлОписанияВыгрузкиДокументыPDF(Организация, ДанныеДляОписи, ИмяФайла)
	
	ПространствоИменСхемы = "Upload2StatementsDocPDF";
	Ошибки = Неопределено;
	
	ДатаВыгрузки = ТекущаяДатаСеанса();
	ОграничениеДлиныСтрокиИмениФайла = 150;
	
	ДанныеОрганизации = ИнтеграцияЭДО.ДанныеЮрФизЛица(Организация);
	
	Попытка
	
		ЭлементФайл = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Файл", ПространствоИменСхемы);
		
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ЭлементФайл, "ВерсФорм", "1.01", Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
			ЭлементФайл, "ДатаВыгрузки", Формат(ДатаВыгрузки, "ДФ=dd.MM.yyyy"), Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
			ЭлементФайл, "ВремяВыгрузки", Формат(ДатаВыгрузки, "ДФ=HH.mm.ss"), Истина, Ошибки);
	
		ЭлементОрганизация = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Файл.Организация", ПространствоИменСхемы);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
			ЭлементОрганизация,
			"Наименование",
			НаименованиеОрганизацииДляФайлаОписания(Организация, ДанныеОрганизации),
			Истина,
			Ошибки);
			
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ЭлементОрганизация, "ИНН", ДанныеОрганизации.ИНН, Истина, Ошибки);
		Если Не ИнтеграцияЭДО.ЭтоФизЛицо(Организация)
			И КППЗаполненКорректно(ДанныеОрганизации.КПП) Тогда
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ЭлементОрганизация, "КПП", ДанныеОрганизации.КПП, Истина, Ошибки);
		КонецЕсли;
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ЭлементФайл, "Организация", ЭлементОрганизация, Истина, Ошибки);

		Для Каждого СтрокаОписи Из ДанныеДляОписи Цикл

			ЭлементДокумент = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Файл.Документ", ПространствоИменСхемы);
			
			КодВидаДокумента = КодВидаДокументаБРОВФорматеPDFПоТипуДокументаЭДО(СтрокаОписи.ТипДокументаЭДО);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ЭлементДокумент, "Вид", КодВидаДокумента, Истина, Ошибки);
			
			НаименованиеДокумента = Строка(СтрокаОписи.ЭлектронныйДокумент);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ЭлементДокумент, "НаимДок", НаименованиеДокумента, Истина, Ошибки);
			
			Если ЗначениеЗаполнено(СтрокаОписи.ДатаДокументаОснования)
				И ЗначениеЗаполнено(СтрокаОписи.НомерДокументаОснования) Тогда
				НаименованиеОснования = СтрШаблон(НСтр("ru='Договорной документ №%1 от %2'"), 
					СтрокаОписи.НомерДокументаОснования,
					Формат(СтрокаОписи.ДатаДокументаОснования, "ДФ=dd.MM.yyyy"));
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
					ЭлементДокумент, "СвДокОсн", НаименованиеОснования, Истина, Ошибки);
			КонецЕсли;
			
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
				ЭлементДокумент, "ИмяФайлаПодписи", СтрокаОписи.ИмяФайлаПодписи, Истина, Ошибки);
			
			ЭлементДанныеФайла = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Файл.Документ.Файл", ПространствоИменСхемы);
			
			Если СтрДлина(СтрокаОписи.ИмяФайлаДанных) > ОграничениеДлиныСтрокиИмениФайла Тогда
				НаименованиеФайла = СтрШаблон("%1.pdf", НаименованиеДокумента);
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
					ЭлементДанныеФайла, "ИмяНаДиске", СтрокаОписи.ИмяФайлаДанных, Истина, Ошибки);
			Иначе
				НаименованиеФайла = СтрокаОписи.ИмяФайлаДанных;
			КонецЕсли;
			
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ЭлементДанныеФайла, "Имя", НаименованиеФайла, Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
				ЭлементДанныеФайла, "Размер", СтрокаОписи.РазмерФайлаДанных, Истина, Ошибки);
			
			ЭлементДокумент.Файл.Добавить(ЭлементДанныеФайла);
			ЭлементФайл.Документ.Добавить(ЭлементДокумент);
			
		КонецЦикла;
	
		ЭлементФайл.Проверить();
		Если ЗначениеЗаполнено(Ошибки) Тогда
			ТекстОшибки = ОбщегоНазначенияБЭД.СоединитьОшибки(Ошибки);
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;
		
		РаботаСФайламиБЭД.СохранитьXDTO(ЭлементФайл, ИмяФайла, Ложь);
		
		Возврат Истина;
		
	Исключение
		
		СформироватьОшибкуФормированияФайлаОписиВыгрузки(Ошибки, Истина);
		Возврат Ложь;
		
	КонецПопытки;
	
КонецФункции

// Параметры:
//  Ошибки - Неопределено,Массив Из Строка
//  ЭтоОписьPDF - Булево
Процедура СформироватьОшибкуФормированияФайлаОписиВыгрузки(Ошибки, ЭтоОписьPDF = Ложь)

	ТекстОшибки = ОбщегоНазначенияБЭД.СоединитьОшибки(Ошибки);
	ТекстСообщения = ?(ЗначениеЗаполнено(ТекстОшибки), ТекстОшибки, 
		ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
	
	Если ЭтоОписьPDF Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось создать файл описания выгрузки документов в формате PDF:
			|%1'"), ТекстСообщения);
	Иначе
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось создать файл описания выгрузки формализованных документов:
			|%1'"), ТекстСообщения);
	КонецЕсли;
	
	ЭлектронноеВзаимодействие.ОбработатьОшибку(НСтр("ru = 'Формирование выгрузки ЭД в 1С-Отчетность'"),
		ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), ТекстСообщения);
		
КонецПроцедуры

// Возвращает данные по наименованию, ИНН и КПП организации/контрагента, а также является ли юридическим лицом.
// 
// Параметры:
//  УчастникЭДО - ОпределяемыйТип.Организация,ОпределяемыйТип.КонтрагентБЭД
// 
// Возвращаемое значение:
//  Структура:
//   * Наименование - Строка,Неопределено
//   * ИНН - Строка,Неопределено
//   * КПП - Строка,Неопределено
//   * ЭтоЮЛ - Булево
//
Функция ДанныеУчастникаЭДО(УчастникЭДО)
	
	Результат = Новый Структура;
	
	ИмяРеквизитаНаименование = ЭлектронноеВзаимодействие.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении(
		"НаименованиеКонтрагентаДляСообщенияПользователю");
	ИмяРеквизитаИНН = ЭлектронноеВзаимодействие.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННКонтрагента");
	ИмяРеквизитаКПП = ЭлектронноеВзаимодействие.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("КППКонтрагента");
	ИменаПолучаемыхРеквизитов = СтрШаблон("%1, %2, %3", ИмяРеквизитаНаименование, ИмяРеквизитаИНН, ИмяРеквизитаКПП);
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(УчастникЭДО, ИменаПолучаемыхРеквизитов);
	
	Результат.Вставить("Наименование", РеквизитыОрганизации[ИмяРеквизитаНаименование]);
	Результат.Вставить("ИНН", РеквизитыОрганизации[ИмяРеквизитаИНН]);
	Результат.Вставить("КПП", РеквизитыОрганизации[ИмяРеквизитаКПП]);
	Результат.Вставить("ЭтоЮЛ", (СтрДлина(Результат.ИНН) = 10));
	
	Возврат Результат;
	
КонецФункции

// Определяет наименование организации для описи выгружаемых документов, используемой для формирования представления 
// загружаемых данных на стороне БРО
//
// Параметры:
//  Организация - ОпределяемыйТип.Организация - Организация
//  ДанныеОрганизации - См. ОбщегоНазначенияБЭД.ДанныеЮрФизЛица
// 
// Возвращаемое значение:
//  Строка - Наименование организации для файла описания
Функция НаименованиеОрганизацииДляФайлаОписания(Организация, ДанныеОрганизации)
	Результат = "";
	
	Если ДанныеОрганизации.Свойство("Наименование") И ЗначениеЗаполнено(ДанныеОрганизации.Наименование) Тогда
		Результат = ДанныеОрганизации.Наименование;
	Иначе
		ДанныеУчастникаЭДО = ДанныеУчастникаЭДО(Организация);
		Результат = ДанныеУчастникаЭДО.Наименование;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает данные объектов учета по электронным документам. Используется в выгрузке для ФНС.
//
// Параметры:
//  ЭлектронныеДокументы - Массив из ДокументСсылка.ЭлектронныйДокументВходящийЭДО, ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//
// Возвращаемое значение:
//  ТаблицаЗначений - Данные объектов учета электронных документов:
//   * ОбъектУчета - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//   * ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
//   * ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументВходящийЭДО,ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//   * НомерДоговора - Строка
//   * ДатаДоговора - Дата
//
Функция ДанныеОбъектовУчетаДляВыгрузкиЭДДляФНС(Знач ЭлектронныеДокументы)
	
	ДанныеОбъектовУчета = ИнтеграцияЭДО.ОбъектыУчетаАктуальныхЭлектронныхДокументов(ЭлектронныеДокументы);
	ДанныеОбъектовУчета.Колонки.Добавить("НомерДоговора", Новый ОписаниеТипов("Строка"));
	ДанныеОбъектовУчета.Колонки.Добавить("ДатаДоговора", Новый ОписаниеТипов("Дата"));
	
	ОбъектыУчета = ДанныеОбъектовУчета.ВыгрузитьКолонку("ОбъектУчета");
	ДанныеДокументовОснований = ИнтеграцияЭДО.ПолучитьНомерДатаДоговораДокументов(ОбъектыУчета);
	
	Если ТипЗнч(ДанныеДокументовОснований) = Тип("Соответствие") Тогда
		
		Для Каждого ДанныеОбъектаУчета Из ДанныеОбъектовУчета Цикл
			ДанныеДокументаОснования = ДанныеДокументовОснований.Получить(ДанныеОбъектаУчета.ОбъектУчета);
			Если ТипЗнч(ДанныеДокументаОснования) <> Тип("Структура") Тогда
				ДанныеДокументаОснования = Новый Структура;
			КонецЕсли;
			ДанныеДокументаОснования.Свойство("НомерДоговора", ДанныеОбъектаУчета.НомерДоговора);
			ДанныеДокументаОснования.Свойство("ДатаДоговора", ДанныеОбъектаУчета.ДатаДоговора);
		КонецЦикла;
		
	КонецЕсли;
	
	ДанныеОбъектовУчета.Индексы.Добавить("ЭлектронныйДокумент");
	
	Возврат ДанныеОбъектовУчета;
	
КонецФункции

// Заполняет опись выгрузки и выгружает файлы в каталог.
// 
// Параметры:
//  ОписьВыгрузки - см. НоваяТаблицаОписиВыгрузкиЭДДляФНС
//  АдресКаталога - Строка - Адрес временного каталога для записи файлов
//  ПакетыДокументовДляВыгрузки - см. НовыеПакетыДокументовДляВыгрузки
//  ДанныеОбъектовУчета - см. ДанныеОбъектовУчетаДляВыгрузкиЭДДляФНС
//
Процедура ЗаполнитьОписьИВыгрузитьВКаталог(ОписьВыгрузки, АдресКаталога, ПакетыДокументовДляВыгрузки, 
	ДанныеОбъектовУчета)
	
	ЭлектронныеДокументы = Новый Массив;
	Для Каждого ТекущийЭлемент Из ПакетыДокументовДляВыгрузки.Формализованные Цикл
		ЭлектронныеДокументы.Добавить(ТекущийЭлемент.ЭлектронныйДокумент);
	КонецЦикла;
	СообщенияЭлектронныхДокументов = ЭлектронныеДокументыЭДО.ДанныеФайловДляВыгрузкиФНС(ЭлектронныеДокументы);
	
	МассивНеобработанныхДанных = Новый Массив;
	Для Каждого Элемент Из ПакетыДокументовДляВыгрузки.Формализованные Цикл
		
		Документ = Элемент.ЭлектронныйДокумент;
		
		Если Не ЗначениеЗаполнено(Элемент.НомерДокумента) Тогда
			ТекстОшибки = НСтр("ru = 'Не заполнен номер документа.'");
			ДобавитьДокументВПакетСОшибкой(ПакетыДокументовДляВыгрузки, Документ, ТекстОшибки);
			МассивНеобработанныхДанных.Добавить(Элемент);
			Продолжить;
		КонецЕсли;
		
		СообщенияДокумента = СообщенияЭлектронныхДокументов.Получить(Документ);
		Если Не ЗначениеЗаполнено(СообщенияДокумента) Тогда
			Продолжить;
		КонецЕсли;
		
		РезультатПроверкиПодписей = ПроверитьНаличиеПодписейВСообщениях(СообщенияДокумента);
		Если Не РезультатПроверкиПодписей.Результат Тогда
			ДобавитьДокументВПакетСОшибкой(ПакетыДокументовДляВыгрузки, Документ, РезультатПроверкиПодписей.ТекстОшибки);
			МассивНеобработанныхДанных.Добавить(Элемент);
			Продолжить;
		КонецЕсли;
		
		ЭлементОписи = ОписьВыгрузки.Добавить();
		ЗаполнитьЗначенияСвойств(ЭлементОписи, Элемент);
		ЗаполнитьНомерДатаОснованияВЭлементеОписи(ЭлементОписи, ДанныеОбъектовУчета, Документ);
		
		ВыгрузитьФайлыСообщений(ЭлементОписи, АдресКаталога, СообщенияДокумента);
		
	КонецЦикла;
	ПакетыДокументовДляВыгрузки.Формализованные = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
		ПакетыДокументовДляВыгрузки.Формализованные, МассивНеобработанныхДанных);
	
КонецПроцедуры

// Заполнить номер дата основания в элементе описи по данным объекта учета.
// 
// Параметры:
//  ЭлементОписи - СтрокаТаблицыЗначений - элемент описи:
//   * НомерДокументаОснования - Строка, Неопределено - номер документа основания
//   * ДатаДокументаОснования - Дата, Неопределено - дата документа основания
//  ДанныеОбъектовУчета - ТаблицаЗначений - Данные объектов учета:
//   * НомерДоговора - Строка
//   * ДатаДоговора - Дата
//  ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументВходящийЭДО,ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//
Процедура ЗаполнитьНомерДатаОснованияВЭлементеОписи(ЭлементОписи, ДанныеОбъектовУчета, ЭлектронныйДокумент)

	ДанныеОбъектаУчета = ДанныеОбъектовУчета.Найти(ЭлектронныйДокумент, "ЭлектронныйДокумент");
	Если ДанныеОбъектаУчета = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ДанныеОбъектаУчета.ОбъектУчета) Тогда
		ЭлементОписи.НомерДокументаОснования = ДанныеОбъектаУчета.НомерДоговора;
		ЭлементОписи.ДатаДокументаОснования = ДанныеОбъектаУчета.ДатаДоговора;
	КонецЕсли;
			
КонецПроцедуры

// Возвращает данные файла неформализованного ЭД.
// 
// Параметры:
//  ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументВходящийЭДО,ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
// 
// Возвращаемое значение:
//  Неопределено, 
//  См. РаботаСФайлами.ДанныеФайла
// 
Функция ДанныеФайлаНеформализованногоЭД(ЭлектронныйДокумент)
	
	ФайлЭлектронногоДокумента = ЭлектронныеДокументыЭДО.ОсновнойФайлИнформацииОтправителя(ЭлектронныйДокумент);
	Если Не ЗначениеЗаполнено(ФайлЭлектронногоДокумента) Тогда
		Возврат Неопределено;
	КонецЕсли;
	ДанныеФайла = РаботаСФайлами.ДанныеФайла(ФайлЭлектронногоДокумента);
	
	Возврат ДанныеФайла; 
	
КонецФункции

// Поместить файлы каталога в архив во временном хранилище.
// 
// Параметры:
//  Файлы - Массив из Файл - Файлы для помещения во временное хранилище
//  АдресКаталога - Строка - Адрес каталога для удаления временных файлов
//  НазваниеАрхива - Строка - Наименование файла архива
//  ДанныеФайловСДвоичнымиДанными - Соответствие Из КлючИЗначение:
//   * Ключ - Строка - ИмяФайла
//   * Значение - ДвоичныеДанные - Двоичные данные файла
//
Процедура ПоместитьФайлыКаталогаВАрхив(Файлы, АдресКаталога, НазваниеАрхива, ДанныеФайловСДвоичнымиДанными)

	СодержимоеКонтейнера = Новый Массив;
	Для Каждого Файл Из Файлы Цикл
		СодержимоеКонтейнера.Добавить(Файл.ПолноеИмя);
	КонецЦикла;
	
	ДвоичныеДанныеКонтейнера = РаботаСФайламиБЭД.СформироватьАрхивФайлов(СодержимоеКонтейнера);
	ДанныеФайловСДвоичнымиДанными.Вставить(НазваниеАрхива, ДвоичныеДанныеКонтейнера);
	
	РаботаСФайламиБЭД.УдалитьВременныеФайлы(АдресКаталога);
	
КонецПроцедуры

// Проверяет подписи электронного документа на возможность выгрузки.
// 
// Параметры:
//  ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументВходящийЭДО,ДокументСсылка.ЭлектронныйДокументИсходящийЭДО 
//  ИнформацияОтправителя - см. ЭлектронныеДокументыЭДО.ДанныеФайлаИнформацииОтправителяДляВыгрузкиФНС
//  ИнформацияПолучателя - см. ЭлектронныеДокументыЭДО.ДанныеФайлаИнформацииПолучателяДляВыгрузкиФНС
// 
// Возвращаемое значение:
//  Структура - Структура результатов проверки подписей:
//  * Результат - Булево
//  * ТекстОшибки - Строка
//
Функция ПроверкаПодписейДокументаВыполнена(ЭлектронныйДокумент, ИнформацияОтправителя, 
	ИнформацияПолучателя = Неопределено)
	
	Результат = Новый Структура("Результат, ТекстОшибки", Истина, "");
	
	Если НЕ ЕстьПодписиВДанныхФайла(ИнформацияОтправителя) Тогда
		ТекстОшибки = НСтр("ru = 'Не удалось выгрузить подпись по документу.'");
		Результат.Результат = Ложь;
		Результат.ТекстОшибки = ТекстОшибки;
		Возврат Результат;
	КонецЕсли;

	Если ЗначениеЗаполнено(ИнформацияПолучателя)
		И ЗначениеЗаполнено(ИнформацияПолучателя.ИмяФайла)
		И НЕ ЕстьПодписиВДанныхФайла(ИнформацияПолучателя) Тогда
		ТекстОшибки = НСтр("ru = 'Не удалось выгрузить ответную подпись по документу.'");
		Результат.Результат = Ложь;
		Результат.ТекстОшибки = ТекстОшибки;
		Возврат Результат;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Проверяет подписи сообщений.
// 
// Параметры:
//  Сообщения - Массив из Структура:
//   * ИмяФайла - Строка
//   * ДвоичныеДанные - ДвоичныеДанные
//   * Размер - Число
//   * КНД - Строка - номер по классификатору налоговых документов
//   * УстановленныеПодписи - см. ЭлектроннаяПодпись.УстановленныеПодписи
// 
// Возвращаемое значение:
//  Структура - Структура результатов проверки подписей:
//  * Результат - Булево
//  * ТекстОшибки - Строка
//
Функция ПроверитьНаличиеПодписейВСообщениях(Сообщения)
	
	Результат = Новый Структура("Результат, ТекстОшибки", Истина, "");
	
	Для Каждого Сообщение Из Сообщения Цикл
		Если ЗначениеЗаполнено(Сообщение.ИмяФайла) И Не ЕстьПодписиВДанныхФайла(Сообщение) Тогда
			ТекстОшибки = НСтр("ru = 'Не удалось выгрузить подпись по документу.'");
			Результат.Результат = Ложь;
			Результат.ТекстОшибки = ТекстОшибки;
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Есть подписи в данных файла.
// 
// Параметры:
//  ДанныеФайла - см. РаботаСФайламиБЭД.ДанныеФайла
// 
// Возвращаемое значение:
//  Булево - Есть подписи в данных файла
//
Функция ЕстьПодписиВДанныхФайла(ДанныеФайла)
	
	Подписи = ДанныеФайла.УстановленныеПодписи;
	Возврат ТипЗнч(Подписи) = Тип("Массив") И Подписи.Количество() > 0;
	
КонецФункции

// Возвращает имя файла контейнера формализованных документов, в соответствии с утвержденным ФНС форматом.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация
// 
// Возвращаемое значение:
//  Строка - Имя файла контейнера выгрузки
//
Функция ИмяФайлаКонтейнераВыгрузкиФормализованныхЭДДляФНС(Организация)

	ИмяРеквизитаИНН = ЭлектронноеВзаимодействие.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННОрганизации");
	ИмяРеквизитаКПП = ЭлектронноеВзаимодействие.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("КППОрганизации");
	ИменаПолучаемыхРеквизитов = СтрШаблон("%1, %2", ИмяРеквизитаИНН, ИмяРеквизитаКПП);
	
	ЗнаковВИННЮЛ = 12;
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, ИменаПолучаемыхРеквизитов);
	ИНН = СокрЛП(РеквизитыОрганизации[ИмяРеквизитаИНН]);
	ИДОтправителя = ИНН + ?(СтрДлина(ИНН) = ЗнаковВИННЮЛ, "", СокрЛП(РеквизитыОрганизации[ИмяРеквизитаКПП]));
	ИДВыгрузки = Формат(ТекущаяДатаСеанса(), "ДФ=yyyyMMddЧЧммсс");
	
	ИмяФайла = СтрШаблон("EDI_%1_%2.zip", ИДОтправителя, ИДВыгрузки);
	ИмяФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяФайла);
	
	Возврат ИмяФайла;
	
КонецФункции

// Возвращает имя файла контейнера НЕ формализованных документов
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация
//  НомерКонтейнера - Строка - в случае, если необходимы уникальные имена контейнеров неформализованных документов
//   в рамках одной выгрузки
//  ЭтоPDF - Булево - значение по умолчанию Ложь 
// 
// Возвращаемое значение:
//  Строка - Имя файла контейнера выгрузки
//
Функция ИмяФайлаКонтейнераВыгрузкиНеформализованныхЭДДляФНС(Организация, НомерКонтейнера = "", ЭтоPDF = Ложь)

	ИмяРеквизитаИНН = ЭлектронноеВзаимодействие.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННОрганизации");
	ИмяРеквизитаКПП = ЭлектронноеВзаимодействие.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("КППОрганизации");
	ИменаПолучаемыхРеквизитов = СтрШаблон("%1, %2", ИмяРеквизитаИНН, ИмяРеквизитаКПП);
	
	ЗнаковВИННЮЛ = 12;
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, ИменаПолучаемыхРеквизитов);
	ИНН = СокрЛП(РеквизитыОрганизации[ИмяРеквизитаИНН]);
	ИДОтправителя = ИНН + ?(СтрДлина(ИНН) = ЗнаковВИННЮЛ, "", СокрЛП(РеквизитыОрганизации[ИмяРеквизитаКПП]));
	ИДВыгрузки = Формат(ТекущаяДатаСеанса(), "ДФ=yyyyMMddЧЧммсс");
	
	Если ЭтоPDF Тогда
		ИмяФайла = СтрШаблон("SCAN_%1_%2.zip", ИДОтправителя, ИДВыгрузки);
	ИначеЕсли ЗначениеЗаполнено(НомерКонтейнера) Тогда
		ИмяФайла = СтрШаблон("NEFORM_%1_%2_%3.zip", НомерКонтейнера, ИДОтправителя, ИДВыгрузки);
	Иначе
		ИмяФайла = СтрШаблон("NEFORM_%1_%2.zip", ИДОтправителя, ИДВыгрузки);
	КонецЕсли;
		
	ИмяФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяФайла);
	
	Возврат ИмяФайла;
	
КонецФункции

// Формирует текст ошибки для не предназначенного для выгрузки электронного документа, добавляет информацию 
// в соответствующий массив документов пакета для выгрузки.
// 
// Параметры:
//  ДанныеДокумента - см. НовыеДанныеДокументаДляВыгрузки
//  ПакетыДокументовДляВыгрузки - см. НовыеПакетыДокументовДляВыгрузки
//
Процедура ОбработатьНеПредназначенныйДляВыгрузкиЭД(ДанныеДокумента, ПакетыДокументовДляВыгрузки)

	ШаблонОшибки = НСтр("ru = 'Тип документа ""%1"" не предназначен для предоставления в ФНС.'");
	ТекстОшибки = СтрШаблон(ШаблонОшибки, ДанныеДокумента.ТипДокумента);
	ДобавитьДокументВПакетСОшибкой(ПакетыДокументовДляВыгрузки, ДанныеДокумента.ЭлектронныйДокумент, ТекстОшибки);
	
КонецПроцедуры

// Возвращает информацию о возможности печати файлов по его расширению, добавляет информацию в соответствующий 
// массив документов пакета для выгрузки.
// 
// Параметры:
//  ДанныеДокумента - см. НовыеДанныеДокументаДляВыгрузки
//  ДанныеФайла - см. РаботаСФайлами.ДанныеФайла
//  ПакетыДокументовДляВыгрузки - см. НовыеПакетыДокументовДляВыгрузки
//  
// Возвращаемое значение:
//  Булево - Файл подлежит печати
//
Функция ФайлПодлежитПечати(ДанныеДокумента, ДанныеФайла, ПакетыДокументовДляВыгрузки)
	
	Если Не РаботаСФайламиБЭД.ФайлПодлежитПечати(ДанныеФайла.Расширение) Тогда
		ТекстОшибки = НСтр("ru = 'Основной файл документа не является печатным.'");
		ДобавитьДокументВПакетСОшибкой(ПакетыДокументовДляВыгрузки, ДанныеДокумента.ЭлектронныйДокумент, ТекстОшибки);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Добавляет в пакет необработанных документов документ с ошибкой.
// 
// Параметры:
//  ПакетыДокументовДляВыгрузки - См. НовыеПакетыДокументовДляВыгрузки
//  ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//  					- ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  ТекстОшибки - Строка
Процедура ДобавитьДокументВПакетСОшибкой(ПакетыДокументовДляВыгрузки, ЭлектронныйДокумент, ТекстОшибки)
	
	ДокументСОшибкой = НовыеНеобработанныеДокументы();
	ДокументСОшибкой.ЭлектронныйДокумент = ЭлектронныйДокумент;
	ДокументСОшибкой.ТекстОшибки = ТекстОшибки;
	ПакетыДокументовДляВыгрузки.Необработанные.Добавить(ДокументСОшибкой);
	
КонецПроцедуры

// Добавляет ошибку с ключом данных в список ошибок обработки.
// 
// Параметры:
//  Ошибки - Массив Из См. НоваяОшибка
//  КлючДанных - ЛюбаяСсылка
//  ТекстОшибки - Строка
Процедура ДобавитьОшибкуОбработки(Ошибки, КлючДанных, ТекстОшибки)
	
	Ошибка = НоваяОшибка();
	Ошибка.КлючДанных = КлючДанных;
	Ошибка.ТекстОшибки = ТекстОшибки;
	Ошибки.Добавить(Ошибка);
	
КонецПроцедуры

// Параметры:
//  ДанныеДокумента - См. НовыеДанныеДокументаДляВыгрузки
//  ДанныеФайла - См. РаботаСФайлами.ДанныеФайла
// 
// Возвращаемое значение:
//  Булево
Функция ДоговорнойДокументВозможноПредоставитьКакФормализованный(ДанныеДокумента, ДанныеФайла)
	
	Если Не ЗначениеЗаполнено(ДанныеДокумента.НомерДокумента) 
		Или Не ЗначениеЗаполнено(ДанныеДокумента.ДатаДокумента) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СтандартPDFA3 = РаботаСФайламиБЭД.ЭтоФайлФорматаPDFА3(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла).Результат;
	ФорматXML = РаботаСФайламиБЭДКлиентСервер.ЭтоXML(ДанныеФайла.Расширение);
	
	Возврат СтандартPDFA3 Или ФорматXML;

КонецФункции

// Параметры:
//  КПП - Строка
// 
// Возвращаемое значение:
//  Булево
Функция КППЗаполненКорректно(КПП)
	
	КоличествоСимволовВКПП = 9;
	Возврат СтрДлина(КПП) = КоличествоСимволовВКПП;
	
КонецФункции

#КонецОбласти

// Возвращает код вида документа по свойствам ЭДО
// 
// Параметры:
// 	ТипРегламента - ПеречислениеСсылка.ТипыРегламентовЭДО
// 	ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО
// 	ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
// Возвращаемое значение:
// 	- Строка
// 	- Неопределено
//
Функция КодВидаДокументаБРОПоСвойствамЭДО(ТипРегламента, ТипДокумента, ТипЭлементаРегламента)
	
	КодВидДокумента = Неопределено;
	Если ТипДокумента = Перечисления.ТипыДокументовЭДО.СчетФактура
		Или ТипДокумента = Перечисления.ТипыДокументовЭДО.УПД Тогда
		Если ТипРегламента = Перечисления.ТипыРегламентовЭДО.Формализованный Тогда
			КодВидДокумента = "01";
		Иначе
			КодВидДокумента = "07";
		КонецЕсли;
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовЭДО.АктВыполненныхРабот Тогда
		Если ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя Тогда
			КодВидДокумента = "02";
		Иначе
			КодВидДокумента = "06";
		КонецЕсли;
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовЭДО.ТоварнаяНакладная Тогда
		Если ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя Тогда
			КодВидДокумента = "03";
		Иначе
			КодВидДокумента = "05";
		КонецЕсли;
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовЭДО.КорректировочныйСчетФактура
		Или ТипДокумента = Перечисления.ТипыДокументовЭДО.УКД Тогда
		Если ТипРегламента = Перечисления.ТипыРегламентовЭДО.Формализованный Тогда
			КодВидДокумента = "04";
		Иначе
			КодВидДокумента = "08";
		КонецЕсли;
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовЭДО.СоглашениеОбИзмененииСтоимости Тогда
		
		КодВидДокумента = "08";
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовЭДО.АктНаПередачуПрав Тогда
		
		КодВидДокумента = "07";
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовЭДО.АктОРасхождениях Тогда
		
		КодВидДокумента = "09";
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовЭДО.АктСверкиВзаиморасчетов Тогда
		Если ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя Тогда
			КодВидДокумента = "10";
		Иначе
			КодВидДокумента = "11";
		КонецЕсли;
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовЭДО.АктПриемкиСтроительныхРаботУслуг Тогда
		Если ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя Тогда
			КодВидДокумента = "12";
		Иначе
			КодВидДокумента = "13";
		КонецЕсли;
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовЭДО.ДоговорныйДокумент Тогда
		КодВидДокумента = "14";
	КонецЕсли;
	
	Возврат КодВидДокумента;
	
КонецФункции

// Возвращает код вида документа БРО, подписанного и переданного в формате PDF, по типу документа ЭДО.
// 
// Параметры:
//  ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО
// 
// Возвращаемое значение:
//  Строка
//
Функция КодВидаДокументаБРОВФорматеPDFПоТипуДокументаЭДО(ТипДокумента)
	
	Если ТипДокумента = Перечисления.ТипыДокументовЭДО.СчетФактура
		Или ТипДокумента = Перечисления.ТипыДокументовЭДО.УПД Тогда
			
		КодВидДокумента = "01";
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовЭДО.АктВыполненныхРабот Тогда
		
		КодВидДокумента = "03";
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовЭДО.ТоварнаяНакладная Тогда
			
		КодВидДокумента = "06";
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовЭДО.ДоговорныйДокумент
		Или ТипДокумента = Перечисления.ТипыДокументовЭДО.Договор Тогда
		
		КодВидДокумента = "09";
		
	ИначеЕсли ТипДокумента = Перечисления.ТипыДокументовЭДО.КорректировочныйСчетФактура
		Или ТипДокумента = Перечисления.ТипыДокументовЭДО.УКД Тогда

		КодВидДокумента = "10";
	
	Иначе
		
		КодВидДокумента = "12";
		
	КонецЕсли;
	
	Возврат КодВидДокумента;
	
КонецФункции

#КонецОбласти