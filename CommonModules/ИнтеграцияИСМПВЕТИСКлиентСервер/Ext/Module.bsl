/////////////////////////////////////////////////////////////////////////////
// Совместная работа подсистем ВетИС и ИСМП.
//   * Если подсистема ВетИС отсутствует, выставить в переопределяемом модуле Стандартная обработка = Ложь.
//   * Если подсистема ВетИС внешняя по отношению к библиотеке ГосИС, модуль следует рассматривать как переопределяемый
//       для связи с библиотечной подсистемой ИСМП.
//

#Область ПрограммныйИнтерфейс

// Дополняет параметры сканирования для документа Маркировка товаров при необходимости передавать специфику ВетИС:
//   * Взводит флаг "ЗаполнятьДанныеВЕТИС".
//   * Определяет тип идентификатора происхождения ВетИС.
// 
// Параметры:
//   Операция              - ПеречислениеСсылка.ВидыОперацийИСМП - операция маркировки.
//   ПараметрыСканирования - См. ШтрихкодированиеОбщегоНазначенияИСКлиент.ПараметрыСканирования.
//
Процедура ДополнитьПараметрыСканированияМаркировкаТоваров(Операция, ПараметрыСканирования) Экспорт
	
	СтандартнаяОбработка = Истина;
	ИнтеграцияИСМПВЕТИСКлиентСерверПереопределяемый.ДополнитьПараметрыСканированияМаркировкаТоваров(Операция, ПараметрыСканирования, СтандартнаяОбработка);
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	ОперацииМаркировки = ИнтеграцияИСМПСлужебныйКлиентСервер.ОперацииМаркировки(Операция);
	Если ОперацииМаркировки.ТребуетсяЗаполнениеИдентификаторовПримененияВЕТИС Тогда
		ПараметрыСканирования.ЗаполнятьДанныеВЕТИС = Истина; // заполнение обязательно
	КонецЕсли;
	
КонецПроцедуры

// Дополняет параметры сканирования для документа Уточнение сведений о кодах маркировки при необходимости передавать специфику ВетИС:
//   * Взводит флаг "ЗаполнятьДанныеВЕТИС".
//   * Определяет тип идентификатора происхождения ВетИС.
// 
// Параметры:
//   Операция              - ПеречислениеСсылка.ВидыОперацийИСМП - операция маркировки.
//   ПараметрыСканирования - См. ШтрихкодированиеОбщегоНазначенияИСКлиент.ПараметрыСканирования.
//
Процедура ДополнитьПараметрыСканированияУточнениеСведенийОКодах(Операция, ПараметрыСканирования) Экспорт
	
	СтандартнаяОбработка = Истина;
	ИнтеграцияИСМПВЕТИСКлиентСерверПереопределяемый.ДополнитьПараметрыСканированияУточнениеСведенийОКодах(Операция, ПараметрыСканирования, СтандартнаяОбработка);
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	Если Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.КорректировкаСведенийКМСрокГодностиВСД") Тогда
		ПараметрыСканирования.ЗаполнятьДанныеВЕТИС = Истина; // заполнение обязательно
	КонецЕсли;
	
КонецПроцедуры

// Возвращает имя полей/колонок "Идентификатор происхождения ВетИС" в интерфейсе
// 
// Возвращаемое значение:
//  Строка - Имя идентификатора
//
Функция ИмяИдентификатораПроисхожденияВЕТИС() Экспорт
	
	ИмяИдентификатора = НСтр("ru = 'ВСД'");
	ИнтеграцияИСМПВЕТИСКлиентСерверПереопределяемый.НастроитьИмяИдентификатораПроисхожденияВЕТИС(ИмяИдентификатора);
	Возврат ИмяИдентификатора;
	
КонецФункции

// Добавляет команды выбора основания-документа ВетИС в документ ИСМП
// 
// Параметры:
//  ИмяФормы - Строка - Имя формы
//  Команды - Массив Из См. ПодключаемыеКомандыИСКлиенСервер.ОписаниеКоманды - Добавляемые на форму команды
Процедура ПриОпределенииКоманд(ИмяФормы, Команды) Экспорт
	
	СтандартнаяОбработка = Истина;
	
	ИнтеграцияИСМПВЕТИСКлиентСерверПереопределяемый.ПриОпределенииКоманд(ИмяФормы, Команды, СтандартнаяОбработка);
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяФормы = "Документ.МаркировкаТоваровИСМП.Форма.ФормаДокумента" Тогда
		ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "ПроизводственнаяОперацияВЕТИС", НСтр("ru = 'Производственная операция ВетИС'"), "ВестиУчетПодконтрольныхТоваровВЕТИС");
		ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "ВходящаяТранспортнаяОперацияВЕТИС", НСтр("ru = 'Входящая транспортная операция ВетИС'"), "ВестиУчетПодконтрольныхТоваровВЕТИС");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти