#Область ПрограммныйИнтерфейс

// Этап подготовки к расчету
//
// Параметры:
//	ПараметрыРасчета - Структура - параметры расчета себестоимости
//
Процедура ПодготовкаИсходныхДанныхКРасчету(ПараметрыРасчета) Экспорт
	
	РасчетСебестоимостиПодготовкаДанныхЛокализация.ПодготовкаИсходныхДанныхКРасчету(ПараметрыРасчета);
	
	ПерепровестиДокументыПоРегистрам(ПараметрыРасчета);
	
	// Исправим проблемы в движениях и в самих документах в рассчитываемом периоде.
	ИсправитьПроблемыВДвиженияхИДокументах(ПараметрыРасчета);
	
	ОчиститьДанныеУпрРегистров(ПараметрыРасчета);
	
	// Проверим корректность исходных данных для расчета.
	РасчетСебестоимостиПрикладныеАлгоритмы.ПроверитьКорректностьИсходныхДанныхДоРасчета(ПараметрыРасчета);
	
	// Обновим кэши регистров, данные которых были изменены в этой процедуре.
	РасчетСебестоимостиПрикладныеАлгоритмы.ОбновитьРасчетныеКэшиРегистров(ПараметрыРасчета, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПерепровестиДокументыПоРегистрам(ПараметрыРасчета)
	
	// Перепроведение документов выполняется в автотестах на файловой базе.
	Если НЕ ПараметрыРасчета.АвтоматическоеТестирование
	 ИЛИ НЕ РасчетСебестоимостиПовтИсп.ЗначенияТехнологическихПараметров().ПерепроводитьДокументыПоРегистрам Тогда
		Возврат;
	КонецЕсли;
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "ПерепровестиДокументыПоРегистрам");
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	#Область СебестоимостьТоваров_ВыручкаИСебестоимостьПродаж
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	ДД.Регистратор           КАК Ссылка,
	|	ДД.Организация           КАК Организация,
	|	1						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДД
	|ГДЕ
	|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И НЕ ДД.РасчетПартий
	|	И НЕ ДД.РасчетСебестоимости
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ВыручкаИСебестоимостьПродаж""	         КАК ИмяРегистра,
	|	ДД.Регистратор                           КАК Ссылка,
	|	ДД.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	2							             КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ДД
	|ГДЕ
	|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.АналитикаУчетаПоПартнерам.Организация В (&МассивОрганизаций)
	|	И НЕ ДД.РасчетПартий
	|	И НЕ ДД.РасчетСебестоимости
	|";
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Перепроведение документов по регистру ""Себестоимость товаров"" при автотестировании'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru='Перепроведение документов по регистру ""Выручка и себестоимость продаж"" при автотестировании'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='перепроведение документов по регистрам при автотестировании'", ОбщегоНазначения.КодОсновногоЯзыка()),
		Неопределено, // Дополнительные поля
		Истина // Только перепроведение
		);
		
	#КонецОбласти
	
	#Область ПрочиеРасходы
	
	Запрос.УстановитьПараметр("ТипыДокументовОСиНМА", РасчетСебестоимостиПрикладныеАлгоритмы.ТипыДокументовОСиНМА());
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Регистратор КАК Ссылка,
	|	ДД.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК ДД
	|ГДЕ
	|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И НЕ ДД.РасчетПартий
	|	И НЕ ДД.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(ДД.Регистратор) В (&ТипыДокументовОСиНМА)
	|
	|";
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	ОписаниеРегистра = ПараметрыРасчета.Движения[Метаданные.РегистрыНакопления.ПрочиеРасходы.Имя];
	ОписаниеРегистра.НадоОбновитьРасчетныйКэш = Истина;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВосстановитьДвиженияДокументовПоРегиструНакопления(
		ПараметрыРасчета, 
		ВыборкаДокументов, 
		Метаданные.РегистрыНакопления.ПрочиеРасходы.Имя);
		
	#КонецОбласти
	
	#Область ПартииПрочихРасходов
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Регистратор КАК Ссылка,
	|	ДД.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК ДД
	|ГДЕ
	|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И НЕ ДД.РасчетПартий
	|	И НЕ ДД.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(ДД.Регистратор) В (&ТипыДокументовОСиНМА)
	|";
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	ОписаниеРегистра = ПараметрыРасчета.Движения[Метаданные.РегистрыНакопления.ПартииПрочихРасходов.Имя];
	ОписаниеРегистра.НадоОбновитьРасчетныйКэш = Истина;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВосстановитьДвиженияДокументовПоРегиструНакопления(
		ПараметрыРасчета, 
		ВыборкаДокументов, 
		Метаданные.РегистрыНакопления.ПартииПрочихРасходов.Имя);
		
	#КонецОбласти
	
КонецПроцедуры

// Механизм, аналогичный по своему назначению механизму обновления ИБ.
// Выполняется при расчете конкретного периода и исправляет документы только этого периода.
//
// Исправляет проблемы в движениях документов и в самих документах в рассчитываемом периоде.
//
// Есть ошибки, которые "портят" исходные данные для расчета,
// но для исправления их последствий не пишется обработчик обновления.
// Например, из-за какой-то старой ошибки в каких-то случаях движения документа были некорректны.
// - если для исправления этой ошибки написать обработчик обновления,
// то он изменит данные прошлых, "закрытых" периодов, из-за чего эти периоды перестанут быть "закрытыми".
// Придется повторно закрывать эти периоды, из-за чего в них может измениться себестоимость и, как следствие,
// регламентированная отчетность. Это плохо, т.к. по этим периодам отчетность уже может быть сдана.
// - с другой стороны, в тех периодах, которые пользователь будет пересчитывать, эту ошибку в движениях надо бы исправить,
// чтобы в этих пересчитанных периодах себестоимость основывалась на правильных исходных данных.
//
// Для исправления таких ошибок и предназначена данная процедура.
// По сути, она является аналогом обработчика обновления, исправляющего движения документов в периоде, указанном пользователем.
//
// Также эта процедура используется для корректировки движений периода при его перерасчете в партионном учете версии 2.2.
Процедура ИсправитьПроблемыВДвиженияхИДокументах(ПараметрыРасчета)
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "ИсправитьПроблемыВДвиженияхИДокументах");
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	СуществующиеВТДоЭтапа = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(Запрос);
	
	#Область ПоляПартионногоУчетаВерсии22
	
	// Проверим в первичных движениях документов корректность заполнения полей:
	// Партия, КорПартия, ТипЗаписи, аналитика учета партий, вид деятельности НДС.
	// Если поля заполнены некорректно, то, видимо, документ был проведен до перехода на партионный учет версии 2.2 -
	// надо перепровести документ по регистру, содержащему некорректные движения.
	// Если после перепроведения документа поля все равно заполнены некорректно, значит ошибка кроется
	// в процедурах формирования движений документа или в макете описания движений (для регистра себестоимости товаров).
	
	// Подготовим вспомогательные данные.
	ОписаниеЦепочек 		= РасчетСебестоимостиЗаполнениеПартий.ОписаниеЦепочекПартийТоваров(ПараметрыРасчета);
	ИспользуемыеТипыЗаписей = РасчетСебестоимостиПрикладныеАлгоритмы.ИспользуемыеТипыЗаписейВЦепочках(ОписаниеЦепочек);
	
	Запрос.УстановитьПараметр("ИспользуемыеТипыЗаписей", ИспользуемыеТипыЗаписей);
	
	#Область ТекстЗапроса
	
	// Подготовим запрос для формирования ВТРегистраторыСНекорректнымиДвижениями.
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.ВидДвижения			 КАК ВидДвижения,
	|	Т.Регистратор 			 КАК Регистратор,
	|	Т.Организация 			 КАК Организация,
	|	Т.АналитикаУчетаПартий	 КАК АналитикаУчетаПартий,
	|	Т.ВидДеятельностиНДС	 КАК ВидДеятельностиНДС
	|ПОМЕСТИТЬ ВТДвиженияПартииПрочихРасходов
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	//++ Локализация
	|	И НЕ Т.Регистратор ССЫЛКА Документ.РаспределениеНДС
	//-- Локализация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор КАК Регистратор,
	|	Т.Организация КАК Организация,
	|	Т.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ПОМЕСТИТЬ ВозвратыТоваровНаДругойСклад
	|ИЗ
	|	ВТДвиженияСебестоимостьТоваров КАК Т
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Т.Регистратор) В (ТИП(Документ.ВозвратТоваровОтКлиента), ТИП(Документ.ВозвратТоваровМеждуОрганизациями))
	|	И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ВозвратНаДругойСклад)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор КАК Регистратор,
	|	Т.Организация,
	|	Т.ХозяйственнаяОперация
	|ПОМЕСТИТЬ РеализацииВПутиРазныйРазделУчета
	|ИЗ
	|	ВТДвиженияСебестоимостьТоваров КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|		ПО СебестоимостьТоваров.Регистратор = Т.Регистратор
	|		И СебестоимостьТоваров.ОперацияУчетаСебестоимости = ЗНАЧЕНИЕ(Перечисление.ОперацииУчетаСебестоимости.Перемещение)
	|		И СебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И СебестоимостьТоваров.АналитикаУчетаНоменклатуры = Т.АналитикаУчетаНоменклатуры
	|		И СебестоимостьТоваров.ВидЗапасов = Т.ВидЗапасов
	|		И СебестоимостьТоваров.РазделУчета <> Т.РазделУчета
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.РеализацияТоваровУслуг)
	|	И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
	|	И Т.ОперацияУчетаСебестоимости = ЗНАЧЕНИЕ(Перечисление.ОперацииУчетаСебестоимости.Реализация)
	|ИНДЕКСИРОВАТЬ ПО
	|    Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.ИмяРегистра КАК ИмяРегистра,
	|	Т.Ссылка 	  КАК Ссылка,
	|	Т.Организация КАК Организация,
	|	Т.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Т.КодОшибки   КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ (
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"" КАК ИмяРегистра,
	|		Т.Регистратор КАК Ссылка,
	|		Т.Организация КАК Организация,
	|		Т.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|		ВЫБОР
	|			КОГДА Правила.ПустоеЗначениеРегистратора ЕСТЬ NULL 
	|				ТОГДА 1
	|			КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ВЫБОР
	|						КОГДА Т.ТипЗаписи В
	|							(ВЫБРАТЬ
	|								ТипыЗаписей.ТипЗаписиПриход
	|							 ИЗ
	|								ВТПравилаЗаполненияПоляТипЗаписи КАК ТипыЗаписей
	|							 ГДЕ
	|								ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИПЗНАЧЕНИЯ(ТипыЗаписей.ПустоеЗначениеРегистратора)
	|								И (ТипыЗаписей.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|									ИЛИ Т.ХозяйственнаяОперация = ТипыЗаписей.ХозяйственнаяОперация)
	|								И (ТипыЗаписей.ПоложительноеКоличество = НЕОПРЕДЕЛЕНО
	|									ИЛИ Т.ПоложительноеКоличество = ТипыЗаписей.ПоложительноеКоличество))
	|							ТОГДА 0
	|						ИНАЧЕ 2
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|						КОГДА Т.ТипЗаписи В
	|							(ВЫБРАТЬ
	|								ТипыЗаписей.ТипЗаписиРасход
	|							 ИЗ
	|								ВТПравилаЗаполненияПоляТипЗаписи КАК ТипыЗаписей
	|							 ГДЕ
	|								ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИПЗНАЧЕНИЯ(ТипыЗаписей.ПустоеЗначениеРегистратора)
	|								И (ТипыЗаписей.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|									ИЛИ Т.ХозяйственнаяОперация = ТипыЗаписей.ХозяйственнаяОперация)
	|								И (ТипыЗаписей.ПоложительноеКоличество = НЕОПРЕДЕЛЕНО
	|									ИЛИ Т.ПоложительноеКоличество = ТипыЗаписей.ПоложительноеКоличество))
	|						ТОГДА 0
	|					ИНАЧЕ 3
	|				КОНЕЦ
	|		КОНЕЦ КАК КодОшибки
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТПравилаЗаполненияПоляТипЗаписи КАК Правила
	|			ПО ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИПЗНАЧЕНИЯ(Правила.ПустоеЗначениеРегистратора)
	|				И (Правила.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|					ИЛИ Т.ХозяйственнаяОперация = Правила.ХозяйственнаяОперация)
	|				И (Правила.ПоложительноеКоличество = НЕОПРЕДЕЛЕНО
	|					ИЛИ Т.ПоложительноеКоличество = Правила.ПоложительноеКоличество)
	|	ГДЕ
	|		НЕ Т.Организация В(&ОрганизацииСреднескользящая)
	|			ИЛИ Т.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		ВЫБОР
	// Проверка кор. партии в расходных движениях при заполненной кор. организации (схема Интеркампани).
	|			КОГДА Т.Организация В (&ОрганизацииСФИФОСкользящая)
	|				И Т.КорОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				И НЕ Т.КорОрганизация В (&ОрганизацииСФИФОСкользящая)
	|				И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА
	|					ВЫБОР
	|						КОГДА Т.КорПартия = НЕОПРЕДЕЛЕНО
	|							ТОГДА 0
	|						ИНАЧЕ 5
	|					КОНЕЦ
	|			КОГДА НЕ Т.Организация В (&ОрганизацииСФИФОСкользящая)
	|				И Т.КорОрганизация В (&ОрганизацииСФИФОСкользящая)
	|				И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И Т.КорРазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
	|				ТОГДА
	|					ВЫБОР
	|						КОГДА Т.КорПартия = Т.Регистратор
	|							ТОГДА 0
	|						ИНАЧЕ 6
	|					КОНЕЦ
	// Проверка кор. партии в расходных движениях без кор. организации (изменение документа партии внутри одной организации).
	|			КОГДА Т.Организация В (&ОрганизацииСФИФОСкользящая)
	|				И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И НЕ Т.КорРазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку),
	|										ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение),
	|										ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка))
	|				И ЕСТЬNULL(Правила.ТипЗаписиРасход, НЕОПРЕДЕЛЕНО) = Т.ТипЗаписи
	|				И (
	// При списании на партии производства кор. партия должна быть заполнена.
	|					(
	|						(ЕСТЬNULL(Правила.КорПартияВРасходе, ЛОЖЬ)
	|							И Т.КорПартия = НЕОПРЕДЕЛЕНО
	|						ИЛИ НЕ ЕСТЬNULL(Правила.КорПартияВРасходе, ЛОЖЬ)
	|							И НЕ Т.КорПартия = НЕОПРЕДЕЛЕНО)
	|						)
	|				)
	|				ТОГДА 6
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТПравилаЗаполненияПоляТипЗаписи КАК Правила
	|			ПО ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИПЗНАЧЕНИЯ(Правила.ПустоеЗначениеРегистратора)
	|				И (Правила.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|					ИЛИ Т.ХозяйственнаяОперация = Правила.ХозяйственнаяОперация)
	|				И (Правила.ПоложительноеКоличество = НЕОПРЕДЕЛЕНО
	|					ИЛИ Т.ПоложительноеКоличество = Правила.ПоложительноеКоличество)
	|	ГДЕ
	|		НЕ Т.Сторно
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		ВЫБОР
	|			КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|					И Правила.ДокументИсточникВПриходе И Т.ДокументИсточник = НЕОПРЕДЕЛЕНО
	|			  ТОГДА 18
	|			КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И Правила.ДокументИсточникВРасходе И Т.ДокументИсточник = НЕОПРЕДЕЛЕНО
	|			  ТОГДА 19
	|			  ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТПравилаЗаполненияПоляТипЗаписи КАК Правила
	|			ПО ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИПЗНАЧЕНИЯ(Правила.ПустоеЗначениеРегистратора)
	|				И (Правила.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|					ИЛИ Т.ХозяйственнаяОперация = Правила.ХозяйственнаяОперация)
	|				И (Правила.ПоложительноеКоличество = НЕОПРЕДЕЛЕНО
	|					ИЛИ Т.ПоложительноеКоличество = Правила.ПоложительноеКоличество)
	|				И (Правила.ДокументИсточникВПриходе И Т.ТипЗаписи = Правила.ТипЗаписиПриход
	|					ИЛИ Правила.ДокументИсточникВРасходе И Т.ТипЗаписи = Правила.ТипЗаписиРасход)
	|	ГДЕ
	|		ВЫБОР
	|			КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|					И Правила.ДокументИсточникВПриходе И Т.ДокументИсточник = НЕОПРЕДЕЛЕНО
	|			  ТОГДА 18
	|			КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И Правила.ДокументИсточникВРасходе И Т.ДокументИсточник = НЕОПРЕДЕЛЕНО
	|			  ТОГДА 19
	|			  ИНАЧЕ 0
	|		КОНЕЦ <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"" КАК ИмяРегистра,
	|		Т.Регистратор КАК Ссылка,
	|		Т.Организация КАК Организация,
	|		Т.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|		ВЫБОР
	|			КОГДА Т.Организация В (&ОрганизацииСФИФОСкользящая) И Т.Партия = НЕОПРЕДЕЛЕНО
	|				ТОГДА 4
	|			КОГДА НЕ (Т.Организация В (&ОрганизацииСФИФОСкользящая)) И Т.Партия <> НЕОПРЕДЕЛЕНО
	|			  И Т.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
	|			  И Т.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|			  И Т.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|				ТОГДА 5
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК КодОшибки
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		(Т.ТипЗаписи В (&ТипыЗаписейПервичныхПартий)
	|		ИЛИ Т.ТипЗаписи В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретения),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретенияПрошлогоПериода))
	|			И ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.КорректировкаРеализации)
	|		ИЛИ Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыВПути)
	|			И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности))
	|		И НЕ Т.Сторно
	|		ИЛИ Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссиюВПути)
	|			И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности)
	|		ИЛИ Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссиюВПути)
	|			И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		7
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		НЕ (Т.ТипЗаписи В (&ИспользуемыеТипыЗаписей))
	|		И НЕ (Т.ТипЗаписи В (&НепересчитываемыеТипыЗаписей))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		8
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК Аналитики
	|			ПО Т.АналитикаУчетаПартий = Аналитики.Ссылка
	|	ГДЕ
	|		Аналитики.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		9
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК Аналитики
	|			ПО Т.КорАналитикаУчетаПартий = Аналитики.Ссылка
	|	ГДЕ
	|		Аналитики.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ПартииПрочихРасходов"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		8
	|	ИЗ
	|		ВТДвиженияПартииПрочихРасходов КАК Т
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК Аналитики
	|			ПО Т.АналитикаУчетаПартий = Аналитики.Ссылка
	|	ГДЕ
	|		Аналитики.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ПартииПрочихРасходов"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		10
	|	ИЗ
	|		ВТДвиженияПартииПрочихРасходов КАК Т
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК Аналитики
	|			ПО Т.АналитикаУчетаПартий = Аналитики.Ссылка
	|	ГДЕ
	|		Т.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		12
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.КорАналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Возвраты.Регистратор,
	|		Возвраты.Организация,
	|		Возвраты.ХозяйственнаяОперация,
	|		13
	|	ИЗ
	|		ВозвратыТоваровНаДругойСклад КАК Возвраты
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТДвиженияСебестоимостьТоваров КАК Т
	|			ПО Т.Регистратор = Возвраты.Регистратор
	|			И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СторноВозвратНаДругойСклад)
	|	ГДЕ
	|		Т.Регистратор ЕСТЬ NULL
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		14
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)
	|		И Т.МетодОценкиСтоимостиТоваров <> ЗНАЧЕНИЕ(Перечисление.МетодыОценкиСтоимостиТоваров.Среднескользящая)
	|		И НЕ Т.РазделУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработкуПереданныеПартнерам),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажиПереданныеПартнерам))
	|
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		17
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ДополнительныеРасходыСПартией)
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыСПартией)
	|		И Т.ДокументИсточник = НЕОПРЕДЕЛЕНО
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		20
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		НЕ Т.Организация В (&ОрганизацииСФИФОСкользящая)
	|		И Т.КорАналитикаУчетаПартий = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
	|		И Т.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И (Т.СтоимостьРегл <> 0 ИЛИ Т.НДСРегл <> 0)
	|		И Т.ТипЗаписи В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретения))
	|		И Т.РазделУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
	|		И НЕ Т.Сторно
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		21
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиентаПрошлыхПериодов)
	|		И Т.ПериодПродажи = ДАТАВРЕМЯ(1,1,1)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		22
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет)
	|		И Т.КорОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		24
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
	|			ПО ВыручкаИСебестоимостьПродаж.Регистратор = Т.Регистратор
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.Организация = ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|		И Т.АналитикаУчетаПоПартнерам = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|		И Т.ТипЗаписи <> ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ВозвратНаДругойСклад)
	|		И Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		25
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		(Т.КорАналитикаУчетаПартий = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
	|			ИЛИ Т.КорАналитикаУчетаПартий = Т.АналитикаУчетаПартий) // По ошибке перезаполнился реквизит КорАналитикаУчетаПартий
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.ПриобретениеТоваровУслуг)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.КорректировкаПриобретения)
	|		И Т.ТипЗаписи В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыБезПартии),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыСПартией))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ВыручкаИСебестоимостьПродаж"",
	|		Т.Регистратор,
	|		Аналитика.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		28
	|	ИЗ
	|		РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
	|			ПО Аналитика.Ссылка = Т.АналитикаУчетаПоПартнерам
	|	ГДЕ
	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Аналитика.Организация В (&МассивОрганизаций)
	|		И Т.Активность
	|		И НЕ Т.РасчетПартий
	|		И НЕ Т.РасчетСебестоимости
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ОтчетПоКомиссииМеждуОрганизациями)
	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		30
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияИмпорт КАК Декларация
	|			ПО Декларация.Ссылка = Т.Регистратор
	|	ГДЕ
	|		Т.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		31
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		&УчитыватьСебестоимостьТоваровПоНазначениям
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И (Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) В (
	|			ТИП(Документ.СборкаТоваров))
	|		ИЛИ Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ПередачаТоваровМеждуОрганизациями))
	|
	|		И ЕСТЬNULL(Т.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС, ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка))
	|			<> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|		И ЕСТЬNULL(Т.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС, ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка))
	|			<> Т.ВидДеятельностиНДС
	|	

	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		36
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения КАК КорректировкаПриобретения
	|			ПО КорректировкаПриобретения.Ссылка = Т.Регистратор
	|	ГДЕ
	|		Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретенияПрошлогоПериода)
	|		И Т.Стоимость <> 0
	|		ИЛИ
	|		Т.ТипЗаписи В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретения))
	|		И Т.Количество = 0
	|		И Т.НастройкаХозяйственнойОперации <> ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВключениеНДСВСтоимость)
	|		ИЛИ
	|		Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретенияПрошлогоПериода)
	|		И Т.Количество = 0
	|		И Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|		И Т.НастройкаХозяйственнойОперации <> ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ЗакупкаУПоставщика)
	|	
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ПартииПрочихРасходов"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		39
	|	ИЗ
	|		РегистрНакопления.ПартииПрочихРасходов КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрочиеДоходыРасходы КАК Отбор
	|			ПО Т.Регистратор = Отбор.Ссылка
	|			И Отбор.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеклассификацияРасходов)
	|	ГДЕ
	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Т.Организация В (&МассивОрганизаций)
	|		И Т.Активность
	|		И НЕ Т.РасчетПартий
	|		И НЕ Т.РасчетСебестоимости
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		40
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		И Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		41
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.КорректировкаНазначенияТоваров)
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.АналитикаУчетаНоменклатуры = Т.КорАналитикаУчетаНоменклатуры
	|		И Т.ВидЗапасов = Т.КорВидЗапасов
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		42
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.КорСтоимость <> 0
	|		И Т.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровПоставщику),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровМеждуОрганизациями),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваровСПереоценкой),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваровСПереоценкой),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПереданнойВозвратнойТары),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноПереданнойТары),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноПереданнойТарыВозвратНаДругойСклад),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаПартийТоваров))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		43
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваровСПереоценкой)
	|		И Т.РазделУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
	|		И Т.СтатьяДоходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)
	|	
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		45
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи)
	|		И Т.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		47
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.РазделУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ПриобретениеТоваровУслуг)
	|		И Т.ИдентификаторСтроки = """"
	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		48
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ИсправлениеРазвернутогоСальдоТоваровОрганизаций)
	|		И Т.ВидЗапасов = Т.КорВидЗапасов
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		49
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.РазделУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыВПути),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссиюВПути))
	|		И Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		52
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию)
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ЕСТЬNULL(Т.КорАналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры, НЕОПРЕДЕЛЕНО)
	|			= ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		И Т.КорРазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
	|		И Т.КорРазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|

	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		55
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|		И Т.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		56
	|	ИЗ
	|		(ВЫБРАТЬ
	|			Т.Регистратор,
	|			Т.Организация,
	|			Т.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|			Т.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|			СУММА(1) КАК КоличествоСтрок,
	|			МИНИМУМ(Ключи.КодСтроки) КАК КодСтроки
	|		ИЗ
	|			ВТДвиженияСебестоимостьТоваров КАК Т
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК Ключи
	|			ПО Т.АналитикаУчетаПартий = Ключи.Ссылка
	|		ГДЕ
	|			Т.Количество > 0
	|			И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)
	|			И Т.РазделУчета В (
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути),
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
	|			И НЕ Т.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость))
	|		СГРУППИРОВАТЬ ПО
	|			Т.Регистратор,
	|			Т.Организация,
	|			Т.АналитикаУчетаНоменклатуры.Номенклатура,
	|			Т.АналитикаУчетаНоменклатуры.Характеристика) КАК Т
	|	ГДЕ
	|		Т.КоличествоСтрок > 1
	|		И Т.КодСтроки = 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		57
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК Реализация
	|			ПО Реализация.Ссылка = Т.ДокументИсточник
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.Количество < 0
	|		И Т.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноРеализации),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионера))
	|		И (Т.ПериодПродажи < &НачалоПериода И Т.ПериодПродажи <> ДАТАВРЕМЯ(1,1,1)
	|			ИЛИ ЕСТЬNULL(Реализация.Дата, &НачалоПериода) < &НачалоПериода)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ВыручкаИСебестоимостьПродаж"",
	|		Т.Регистратор,
	|		Аналитика.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		57
	|	ИЗ
	|		РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
	|			ПО Аналитика.Ссылка = Т.АналитикаУчетаПоПартнерам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДвиженияСебестоимостьТоваров КАК Себестоимость
	|			ПО Себестоимость.Регистратор = Т.Регистратор
	|			И Себестоимость.ИдентификаторФинЗаписи = Т.ИдентификаторФинЗаписи
	|			И Себестоимость.ИдентификаторФинЗаписи <> """"
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК Реализация
	|			ПО Реализация.Ссылка = Себестоимость.ДокументИсточник
	|	ГДЕ
	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Аналитика.Организация В (&МассивОрганизаций)
	|		И Т.Активность
	|		И НЕ Т.РасчетПартий
	|		И НЕ Т.РасчетСебестоимости
	|		И Т.Количество < 0
	|		И Т.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноРеализации),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионера))
	|		И (Себестоимость.ПериодПродажи < &НачалоПериода И Себестоимость.ПериодПродажи <> ДАТАВРЕМЯ(1,1,1)
	|			ИЛИ ЕСТЬNULL(Реализация.Дата, &НачалоПериода) < &НачалоПериода)
	|
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		59
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДвиженияСебестоимостьТоваров КАК ДвиженияПриход
	|			ПО ДвиженияПриход.Регистратор = Т.Регистратор
	|			И ДвиженияПриход.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			И ДвиженияПриход.АналитикаУчетаНоменклатуры = Т.КорАналитикаУчетаНоменклатуры
	|			И ДвиженияПриход.РазделУчета = Т.РазделУчета
	|			И ДвиженияПриход.ВидДеятельностиНДС = Т.КорВидДеятельностиНДС
	|	ГДЕ
	//		Проверка корреспонденции движений для переноса балансовой стоимости товаров в управленческую организацию
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиентуРеглУчет)
	|		И НЕ Т.РазделУчета В (&ЗабалансовыеРазделыУчета)
	|		И ДвиженияПриход.Регистратор ЕСТЬ NULL
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		60
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтчетКомиссионера)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ОтчетКомиссионера)
	|		И Т.ИдентификаторСтроки = """"
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		61
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидЗапасов.ТипЗапасов В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца))
	|		И Т.РазделУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		62
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиентаПрошлыхПериодов)
	|		И Т.Количество < 0
	|		И НЕ Т.Сторно
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ПартииПрочихРасходов"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		63
	|	ИЗ
	|		ВТДвиженияПартииПрочихРасходов КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения КАК Корректировка
	|			ПО Корректировка.Ссылка = Т.Регистратор
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.АналитикаУчетаПартий = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// Товары, переданные на комиссию по комиссионной продаже версии 2.5, учитываются в разделе "Товары переданные на комиссию",
	// кроме товаров, принятых на комиссию. Принятые на комиссию товаров и остаются на разделе учета "Товары принятые на комиссию".
	// На комиссию межно передавать только собственные товары, и товары, принятые на комиссию.
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		64
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.ДоговорКонтрагента)
	|		И Т.АналитикаУчетаНоменклатуры.Договор.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СКомиссионером)
	|		И НЕ Т.РазделУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию))
	|	
	//++ Локализация
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// В себестоимости товаров не должно быть вида деятельности НДС "Экспорт". Такой вид деятельности преобразовывается в
	// "Экспорт сырьевых товаров, услуг (работ)" или "Экспорт несырьевых товаров".
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		65
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|		ИЛИ Т.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|		ИЛИ Т.ВидДеятельностиНДСДокумента = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	//-- Локализация
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	// В себестоимости товаров должна быть заполнена операция учета себестоимости для выбытий по фиксированной стоимости,
	// Это нужно для правильной работы процедуры СкорректироватьСтоимостьСписанияЗапасов().
	// В запросах этой процедуры есть условия на операцию учета себестоимости.
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		66
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.Количество <> 0
	|		И Т.ОперацияУчетаСебестоимости = ЗНАЧЕНИЕ(Перечисление.ОперацииУчетаСебестоимости.ПустаяСсылка)
	|		И Т.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровПоставщику),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваровСПереоценкой),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваровСПереоценкой))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	// Реализация без перехода права собственности для комиссионного товара должна иметь одинаковый раздел учета
	|	ВЫБРАТЬ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		67
	|	ИЗ
	|		РеализацииВПутиРазныйРазделУчета КАК Т
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		""ВыручкаИСебестоимостьПродаж"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		67
	|	ИЗ
	|		РеализацииВПутиРазныйРазделУчета КАК Т
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// Исправление движений для типов записей с неправильным знаком количеста.
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		68
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И НЕ Т.Сторно
	|		И Т.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|		И Т.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияВРозницу)
	|		И (Т.Количество < 0 И Т.ТипЗаписи  = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление)
	|			ИЛИ Т.Количество > 0 И Т.ТипЗаписи  = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Сторно))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ВыручкаИСебестоимостьПродаж"",
	|		Т.Регистратор,
	|		Аналитика.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		70
	|	ИЗ
	|		РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
	|			ПО Аналитика.Ссылка = Т.АналитикаУчетаПоПартнерам
	|	ГДЕ
	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Аналитика.Организация В (&МассивОрганизаций)
	|		И Т.Активность
	|		И НЕ Т.РасчетПартий
	|		И НЕ Т.РасчетСебестоимости
	|		И Т.Количество <> 0
	|		И Т.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОприходованиеПоВозврату)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		71
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУчетныеПолитикиДокументовИсточников КАК ПолитикиДокументовИсточников
	|			ПО Т.Регистратор = ПолитикиДокументовИсточников.Регистратор
	|			И Т.Организация = ПолитикиДокументовИсточников.Организация
	|			И Т.ДокументИсточник = ПолитикиДокументовИсточников.ДокументИсточник
	|	ГДЕ
	|		&УчитыватьСебестоимостьТоваровПоНазначениям
	|		И Т.МетодОценкиСтоимостиТоваров <> ЗНАЧЕНИЕ(Перечисление.МетодыОценкиСтоимостиТоваров.Среднескользящая)
	|		И ЕСТЬNULL(ПолитикиДокументовИсточников.МетодОценкиСтоимостиТоваров, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.МетодыОценкиСтоимостиТоваров.Среднескользящая)
	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|		И ЕСТЬNULL(Т.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС, ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка))
	|			<> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|		И ЕСТЬNULL(Т.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС, ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка))
	|			<> Т.ВидДеятельностиНДС
	|		И НЕ Т.Регистратор ССЫЛКА Документ.ЗаявлениеОВвозеТоваров
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ПрочиеРасходы"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		72
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК ПрочиеРасходы
	|			ПО ПрочиеРасходы.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|			И ПрочиеРасходы.Регистратор = Т.Регистратор
	|			И ПрочиеРасходы.ИдентификаторФинЗаписи = Т.ИдентификаторФинЗаписи
	|			И ПрочиеРасходы.НастройкаХозяйственнойОперации = Т.НастройкаХозяйственнойОперации
	|			И ПрочиеРасходы.ХозяйственнаяОперация <> Т.ХозяйственнаяОперация
	|	ГДЕ
	|		Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНаРасходыФиксированнаяСтоимость)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		73
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВключениеНДСВСтоимость)
	|		И Т.ТипЗаписи <> ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ВключениеНДСВСтоимость)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ТаможеннаяДекларацияИмпорт)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		75
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК Приобретение
	|			ПО Приобретение.Ссылка = Т.ДокументИсточник
	|			И Приобретение.Дата < &НачалоПериода
	|	ГДЕ
	|		Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.Сторно)
	|		И Т.ТипЗаписи <> ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение)
	|		И Т.Количество <> 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		76
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение)
	|		И Т.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// Документы "ПередачаТоваровХранителю" и "ПоступлениеТоваровОтХранителя" не имеют данных о виде деятельности НДС,
	// поэтому для данных документов должен сохраняться вид деятельности НДС подобранной партии.
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		77
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДеятельностиНДСДокумента <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|		И ЕСТЬNULL(Т.КорАналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС, ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка))
	|			= ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|		И (ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ПередачаТоваровХранителю)
	|			ИЛИ ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ПоступлениеТоваровОтХранителя))
	|	) КАК Т
	|
	|ГДЕ
	|	Т.КодОшибки <> 0
	|";
	
	Запрос.Текст = ТекстЗапросаВТДвиженияСебестоимостьТоваров() + ТекстЗапроса;
	
	#КонецОбласти
	
	// Сформируем описание возможных кодов ошибок в движениях.
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Для движения не найдено соответствующее ему правило заполнения типа записи'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru='Тип записи приходного движения не соответствует правилу'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(3, НСтр("ru='Тип записи расходного движения не соответствует правилу'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(4, НСтр("ru='Для организаций с методом оценки ""ФИФО (скользящая)"" не заполнена первичная партия'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(5, НСтр("ru='Для организаций с методом оценки, отличным от ""ФИФО (скользящая)"", заполнена первичная партия или кор. партия'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(6, НСтр("ru='Для организаций с методом оценки ""ФИФО (скользящая)"" некорректно заполнена кор. партия в расходном движении'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(7, НСтр("ru='Тип записи, указанный в движении, не используется при распределении партий'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(8, НСтр("ru='Аналитика учета партий не содержит вида ценности'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(9, НСтр("ru='Кор. аналитика учета партий не содержит вида ценности'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(10, НСтр("ru='Не заполнен вид деятельности НДС при заполненной аналитике учета партий'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(11, НСтр("ru='Для аналитики учета партий выпуска изделий не заполнен код строки (продукции)'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(12, НСтр("ru='Не заполнена кор. аналитика учета номенклатуры для операции ""Реализация (товары в пути)""'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(13, НСтр("ru='Для документов возврата товаров от клиента/организации на склад, отличный от склада реализации, используются неправильные хозяйственные операции и типы записей'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(14, НСтр("ru='Не заполнен вид деятельности НДС у приходных движений с типом записи ""Партия""'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(17, НСтр("ru='Не заполнен документ - источник для операции ""Дополнительные расходы с указанием документа партии"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(18, НСтр("ru='Заполнение документа-источника в приходном движении не соответствует правилу.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(19, НСтр("ru='Заполнение документа-источника в расходном движении не соответствует правилу.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(20, НСтр("ru='Не заполнена кор. аналитика учета партий приходных движений с типом записи ""Партия"" или ""Корректировка приобретения"" для организаций с методом оценки, отличным от ""ФИФО (скользящая)""'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(21, НСтр("ru='Не заполнен период продажи для операции ""Возврат товаров от клиента прошлых периодов""'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(22, НСтр("ru='Не заполнены кор. реквизиты для операции ""Закупка по регл. учету""'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(24, НСтр("ru='Не заполнена аналитика учета по партнерам в движениях документов продажи'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(25, НСтр("ru='Не заполнена кор. аналитика учета партий в приходных движениях с типом записей ""Доп. расходы (не указана партия)"" и ""Доп. расходы (указана партия)""'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(28, НСтр("ru='Не корректно заполнен раздел учета для операции ""Отчет по комиссии между организациями""'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(30, НСтр("ru='Не корректно заполнен кор. вид деятельности НДС для документа ""Таможенная декларация на импорт"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(31, НСтр("ru='Не корректно заполнен вид деятельности НДС в приходном движении документа.'"));
	РасшифровкаКодовОшибок.Вставить(32, НСтр("ru='Не корректно заполнен тип записи в приходном движении для документа ""Производство без заказа"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(33, НСтр("ru='Не корректные движения по изменению назначения работ для документа ""Этап производства"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(34, НСтр("ru='Некорректные движения по разделу учета ""Производственные затраты"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(36, НСтр("ru='Некорректные движения документа ""Корректировка приобретения"".'"));
	РасшифровкаКодовОшибок.Вставить(39, НСтр("ru='Неправильный вид движения документа ""Отражение прочих доходов и расходов"".'"));
	РасшифровкаКодовОшибок.Вставить(40, НСтр("ru='Неправильный раздел учета для товаров на хранении с правом продажи.'"));
	РасшифровкаКодовОшибок.Вставить(41, НСтр("ru='Некорректные движения документа ""Корректировка назначения товаров"" при отключенном учете себестоимости по назначениям.'"));
	РасшифровкаКодовОшибок.Вставить(42, НСтр("ru='В движениях заполнен реквизит ""Кор стоимость"".'"));
	РасшифровкаКодовОшибок.Вставить(43, НСтр("ru='Не заполнена статья доходов в движениях документа ""Порча товаров"".'"));
	РасшифровкаКодовОшибок.Вставить(45, НСтр("ru='Неправильный раздел учета для собственных товаров.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(46, НСтр("ru='Неправильный тип записи в движениях списания неизрасходованных материалов на расходы.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(47, НСтр("ru='Не заполнен идентификатор строки в движениях документа ""Приобретение товаров услуг"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(48, НСтр("ru='Лишние движения документа ""Исправление развернутого сальдо товаров организаций"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(49, НСтр("ru='Неправильный раздел учета ""Товары в пути (отгрузка без перехода права собственности)"" для комиссионных товаров.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(52, НСтр("ru='Неправильный кор. раздел учета при реализации работ между организациями.'", ОбщегоНазначения.КодОсновногоЯзыка()));

	РасшифровкаКодовОшибок.Вставить(55, НСтр("ru='Неправильный раздел учета для товаров в пути от поставщиков.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(56, НСтр("ru='Не заполнен код строки в аналитике учета партии приходного документа'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(57, НСтр("ru='Неправильная хозяйственная операция для возврата от клиента товаров, проданных в прошлом периоде.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(59, НСтр("ru='Не корректно заполнен кор. вид деятельности НДС в расходном движении ""Реализация (только регл. учет)"".'"));
	РасшифровкаКодовОшибок.Вставить(60, НСтр("ru='Не заполнен идентификатор строки в движениях документа ""Отчет комиссионера"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(61, НСтр("ru='Неправильный раздел учета для давальческих материалов, полуфабрикатов или продукции.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(62, НСтр("ru='Неправильный знак у количества для операции ""Возврат товаров от клиента прошлых периодов""'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(63, НСтр("ru='Не заполнена аналитика учета партий в движениях документа ""Корректировка приобретения"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(64, НСтр("ru='Неправильный раздел учета для товаров у комиссионера.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	//++ Локализация
	РасшифровкаКодовОшибок.Вставить(65, НСтр("ru='Вида деятельности НДС ""Экспорт"" не должно быть в движениях себестоимости товаров.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	//-- Локализация
	РасшифровкаКодовОшибок.Вставить(66, НСтр("ru='Не заполнена операция учета себестоимости в движениях себестоимости товаров.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(67, НСтр("ru='Некорректный раздел учета себестоимости в реализации комиссионного товара в пути.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(68, НСтр("ru='Не правильный знак у количества в расходном движении.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(70, НСтр("ru='Не заполнен вид деятельности НДС при возврате от клиента по указанной стоимости'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(71, НСтр("ru='Не корректно заполнен вид деятельности НДС в разделе ""Неотфактурованные поставки"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(72, НСтр("ru='Не правильная хозяйственная операция для выпуска по фикс. стоимости в регистре ""Прочие расходы"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(73, НСтр("ru='Не правильный тип записи для движения по включению НДС в стоимость в регистре ""Себестоимость товаров"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(75, НСтр("ru='Не правильный тип записи для сторно движения фактуровки поставки в регистре ""Себестоимость товаров"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(76, НСтр("ru='Не заполнен вид деятельности НДС в движениях с типом записи ""Дополнение"" в регистре ""Себестоимость товаров"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(77, НСтр("ru='Заполнен вид деятельности НДС документа в движениях по регистру ""Себестоимость товаров"" у документов ""Передача товаров хранителю"" и ""Поступление товаров от хранителя"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	ДополнительныеПоля = Новый Структура("ХозяйственнаяОперация", НСтр("ru = 'операция'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='исправление ошибок в движениях документов'", ОбщегоНазначения.КодОсновногоЯзыка()),
		ДополнительныеПоля);
	
	#КонецОбласти
	

	#Область ВключениеУчетаСебестоимости
	
	РасшифровкаКодовОшибок = Новый Соответствие;	
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Включение учета себестоимости'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.УстановитьПараметр("ТипДокументаИмпорта", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ТаможеннаяДекларацияИмпорт"));
	
	Запрос.Текст = ТекстЗапросаВключениеУчетаСебестоимости();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='включение учета себестоимости'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти

	#Область ВыручкаИСебестоимостьПродаж
	
	// Исправление некорректных движений в регистре "Выручка и себестоимость продаж".
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Дублирование расчетной себестоимости в первичных движениях'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	СуществующиеВТ = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(Запрос);
	
	Запрос.Текст = ТекстЗапросаДокументыСДублямиДвиженийПоВыручкеИСебестоимостиПродаж();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='ошибка 00-00071689'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	НовыеВТ = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(Запрос, СуществующиеВТ);
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета, НовыеВТ);
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Пропавшие первичные движения по корректировке выручки'"));
	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru='Движения по регистру ""Выручка и себестоимость продаж"" не соответствуют движениям по регистру ""Себестоимость товаров""'"));
	РасшифровкаКодовОшибок.Вставить(3, НСтр("ru='Для работы не заполнен раздел учета в движениях по регистру ""Выручка и себестоимости продаж""'"));
	
	СуществующиеВТ = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(Запрос);
	
	Запрос.Текст = ТекстЗапросаДокументыСПропавшимиДвижениямиПоВыручкеИСебестоимостиПродаж();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='исправление движений по регистру ""Выручка и себестоимость продаж""'"));
	
	НовыеВТ = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(Запрос, СуществующиеВТ);
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета, НовыеВТ);
	
	#КонецОбласти

	#Область НекорректнаяАналитикаФинансовогоУчета
	
	// Исправление некорректной пустой аналитики финансового учета в регистре "Себестоимость товаров".
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Пустая аналитика финансового учет в первичных движениях имеет значение отличное от НЕОПРЕДЕЛЕНО'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru='Пустая кор аналитика финансового учет в первичных движениях имеет значение отличное от НЕОПРЕДЕЛЕНО'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(3, НСтр("ru='Пустая аналитика финансового учет в первичных движениях отличается от кор аналитики финансового учета'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.АналитикаФинансовогоУчета В
	|		(ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка),
	|		 ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка),
	|		 ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка),
	|		 ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	2 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.КорАналитикаФинансовогоУчета В
	|		(ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка),
	|		 ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка),
	|		 ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка),
	|		 ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка))
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) В (
	|		ТИП(Документ.РеализацияТоваровУслуг),
	|		ТИП(Документ.ПеремещениеТоваров))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	3 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Движения
	|		ПО Движения.Период = Т.Период
	|		И Движения.Регистратор = Т.Регистратор
	|		И Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И НЕ Движения.РасчетПартий
	|		И НЕ Движения.РасчетСебестоимости
	|		И Движения.КорАналитикаУчетаНоменклатуры = Т.АналитикаУчетаНоменклатуры
	|		И Движения.КорВидЗапасов = Т.ВидЗапасов
	|		И Движения.КорАналитикаФинансовогоУчета = Т.АналитикаФинансовогоУчета
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И Т.КорАналитикаФинансовогоУчета  <> НЕОПРЕДЕЛЕНО
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ПередачаТоваровМеждуОрганизациями)
	|	И Т.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссиюВДругуюОрганизацию)
	|	И Движения.Регистратор ЕСТЬ NULL
	|";

	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='исправление аналитики финансового учета в движениях'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти

	#Область СуммыВДвижениях
	
	// Проверка соответствия заполненности суммы упр./регл. значениям функциональных опций.
	РасшифровкаКодовОшибок = Новый Соответствие;
	
	Шаблон1 = НСтр("ru='Заполнение упр. сумм в движениях не соответствует значению функциональной опции ""%Парам1%""'", ОбщегоНазначения.КодОсновногоЯзыка());
	Шаблон1 = СтрЗаменить(Шаблон1, "%Парам1%", "ВестиУправленческийУчетОрганизаций");
	РасшифровкаКодовОшибок.Вставить(1,Шаблон1 );
	
	Шаблон1 = НСтр("ru='Заполнение регл. сумм в движениях не соответствует значению функциональной опции ""%Парам1%""'", ОбщегоНазначения.КодОсновногоЯзыка());
	Шаблон1 = СтрЗаменить(Шаблон1, "%Парам1%", "ИспользоватьУчетПрочихДоходовРасходовРегл");
	РасшифровкаКодовОшибок.Вставить(2,Шаблон1 );

	РасшифровкаКодовОшибок.Вставить(3, НСтр("ru='Заполнение суммы без НДС в движениях не соответствует варианту распределения статьи расходов'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(4, НСтр("ru='Движения по регистру ""Прочие доходы"" сформированы без признака ""Расчет себестоимости"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#Область ИсключаемыеТипыДокументов
	
	Запрос.УстановитьПараметр("ТипыДокументовОСиНМА", РасчетСебестоимостиПрикладныеАлгоритмы.ТипыДокументовОСиНМА());
	Запрос.УстановитьПараметр("ТипыРегламентныхДокументов", РасчетСебестоимостиПрикладныеАлгоритмы.ТипыДокументовРегламентные());
	
	#КонецОбласти
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И (Т.ТипЗаписи В (&НепересчитываемыеТипыЗаписей)
	|		ИЛИ Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление)
	|		  И Т.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию))
	|	И (Т.Стоимость <> 0 ИЛИ Т.КорСтоимость <> 0 ИЛИ Т.ДопРасходы <> 0 ИЛИ Т.Трудозатраты <> 0
	|		ИЛИ Т.ПостатейныеПостоянныеСНДС <> 0 ИЛИ Т.ПостатейныеПеременныеСНДС <> 0
	|		ИЛИ Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию))
	|	И НЕ Т.РазделУчета В (
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку),
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи))
	|	И ((&УправленческийУчетОрганизаций И (Т.СтоимостьБезНДС <> 0 ИЛИ Т.КорСтоимость <> 0
	|			ИЛИ Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию)
	|			 И Т.СтоимостьРегл <> 0
	|			 И Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		И Т.СтоимостьУпр = 0 И Т.ДопРасходыУпр = 0 И Т.ТрудозатратыУпр = 0
	|		И Т.ПостатейныеПостоянныеУпр = 0 И Т.ПостатейныеПеременныеУпр = 0)
	|	ИЛИ (НЕ &УправленческийУчетОрганизаций
	|		И (Т.СтоимостьУпр <> 0 ИЛИ Т.ДопРасходыУпр <> 0 ИЛИ Т.ТрудозатратыУпр <> 0
	|		ИЛИ Т.ПостатейныеПостоянныеУпр <> 0 ИЛИ Т.ПостатейныеПеременныеУпр <> 0)))
	|	И НЕ (Т.СтоимостьБезНДС = 0	И Т.ДопРасходы <> 0 И Т.ДопРасходыУпр = 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПрочиеДоходы"" 		 КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	ВЫБОР
	|		КОГДА Т.Сумма <> 0
	|			И Т.СуммаРегл = 0
	|			И &ИспользоватьУчетПрочихДоходовРасходовРегл
	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)
	|			ТОГДА 2
	|		КОГДА Т.СуммаРегл <> 0
	|			И НЕ &ИспользоватьУчетПрочихДоходовРасходовРегл
	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ					 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ПрочиеДоходы КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютОрганизаций КАК Курсы
	|		ПО Т.Организация = Курсы.Организация
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)
	|	И ((&УправленческийУчетОрганизаций
	|			И Т.СуммаУпр = 0
	|			И Т.Сумма <> 0)
	|		ИЛИ (НЕ &УправленческийУчетОрганизаций
	|			И Т.СуммаУпр <> 0)
	|		ИЛИ (Т.Сумма <> 0
	|			И Т.СуммаРегл = 0
	|			И НЕ (ВЫРАЗИТЬ(Т.Сумма * Курсы.КоэффициентВалютыУпр КАК ЧИСЛО(31,2)) МЕЖДУ -&ЗначениеПогрешностиСебестоимостьРегл И &ЗначениеПогрешностиСебестоимостьРегл)
	|			И &ИспользоватьУчетПрочихДоходовРасходовРегл)
	|		ИЛИ (Т.СуммаРегл <> 0
	|			И НЕ &ИспользоватьУчетПрочихДоходовРасходовРегл)
	|	  )
	|
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПрочиеДоходы"" 		 КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	4						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ПрочиеДоходы КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетСебестоимости
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ВозвратТоваровОтКлиента)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПрочиеРасходы"" 		 КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И ((&УправленческийУчетОрганизаций И Т.СуммаУпр = 0 И Т.Сумма <> 0 И Т.СуммаРегл <> 0
	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА))
	|		ИЛИ (НЕ &УправленческийУчетОрганизаций И Т.СуммаУпр <> 0
	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПрочиеРасходы"" 		 КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	2 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.НачислениеРеверсивногоНДС)
	//++ Локализация
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.СчетФактураНалоговыйАгент)
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.СчетФактураПолученныйНалоговыйАгент)
	//-- Локализация
	
	// Корректировкой задолженности вся сумма задолженности может быть списана за счет резервов. При этом сумма регл в движениях будет равна 0.
	|	И Т.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеБезнадежнойЗадолженностиЗаСчетРезервовПоСомнительнымДолгам)
	// В реализации услуг и прочих активов сумма регл. может быть не заполнена.
	|	И НЕ Т.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СебестоимостьРеализацииНМА))
	// Если сумма без НДС равна 0, это значит что в документе указана только сумма НДС. При этом сумма регл не будет заполнена.
	|	И ((&ИспользоватьУчетПрочихДоходовРасходовРегл И Т.СуммаРегл = 0 И Т.Сумма <> 0 И Т.СуммаБезНДС <> 0
	|			И НЕ Т.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеРасходовУУ),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РегистрацияРасходовУУ))
	|			И НЕ Т.СтатьяРасходов.ВариантРаспределенияРасходовРегл В (
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы),
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)))
	|		ИЛИ (НЕ &ИспользоватьУчетПрочихДоходовРасходовРегл И Т.СуммаРегл <> 0))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПартииПрочихРасходов"" КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютОрганизаций КАК Курсы
	|		ПО Т.Организация = Курсы.Организация
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И ((&УправленческийУчетОрганизаций И Т.Стоимость <> 0 И Т.НДСРегл <> 0 И Т.НДСУпр = 0
	|			И ВЫРАЗИТЬ(Т.НДСРегл * Курсы.КоэффициентВалютыРегл КАК ЧИСЛО(31,2)) <> 0
	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА))
	|		ИЛИ (НЕ &УправленческийУчетОрганизаций И Т.НДСУпр <> 0)
	|		ИЛИ (Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РегистрацияРасходовУУ)
	|			И Т.НДСУпр <> 0))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПартииПрочихРасходов"" КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	2 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютОрганизаций КАК Курсы
	|		ПО Т.Организация = Курсы.Организация
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И ((&ИспользоватьУчетПрочихДоходовРасходовРегл И Т.Стоимость <> 0 И Т.СтоимостьРегл = 0 И Т.НДСРегл = 0 И Т.ПостояннаяРазница = 0 И Т.ВременнаяРазница = 0
	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)
	|			И НЕ Т.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеРасходовУУ),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РегистрацияРасходовУУ))
	|			И (Т.СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	//++ Локализация
	|				ИЛИ (Т.СтатьяРасходов.ВариантРаспределенияРасходовРегл В (
	|						ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
	|						ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства))
	|					И Т.Организация В (&ОрганизацииСРаздельнымУчетомПостатейныхЗатрат))
	//-- Локализация
	|		  ))
	|		ИЛИ (НЕ &ИспользоватьУчетПрочихДоходовРасходовРегл И (Т.СтоимостьРегл <> 0 ИЛИ Т.НДСРегл <> 0 ИЛИ Т.ПостояннаяРазница <> 0 ИЛИ Т.ВременнаяРазница <> 0)))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ВыручкаИСебестоимостьПродаж"" КАК ИмяРегистра,
	|	Т.Регистратор 			        КАК Ссылка,
	|	Аналитика.Организация 			КАК Организация,
	|	1 						        КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
	|		ПО Аналитика.Ссылка = Т.АналитикаУчетаПоПартнерам
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Аналитика.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И ((&УправленческийУчетОрганизаций И Т.Стоимость <> 0 И Т.СтоимостьУпр = 0)
	|		ИЛИ (НЕ &УправленческийУчетОрганизаций И Т.СтоимостьУпр <> 0))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Проверка заполнения суммы без НДС в движениях.
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПрочиеРасходы"" 		 КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	3 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.НачислениеРеверсивногоНДС)
	//++ Локализация
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.СчетФактураНалоговыйАгент)
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.СчетФактураПолученныйНалоговыйАгент)
	//-- Локализация
	// Сумма без НДС может быть не заполнена, если в в документе указана сумма НДС равная сумме документа.
	// В этом случае сумма без НДС и сумма регл будут равны 0. 
	|	И Т.Сумма <> 0 И Т.СуммаБезНДС = 0 И Т.СуммаРегл <> 0
	|	И НЕ Т.СтатьяРасходов.ВариантРаспределенияРасходовУпр В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы))
	|	И НЕ Т.Сторно
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='некорректное заполнение сумм упр. учета'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти


	#Область ДвиженияНоменклатураДоходыРасходы
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Сформированы некорректные движения по регистру ""Движения Номенклатура - Доходы/Расходы"".'"));
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор				КАК Ссылка,
	|	Т.Период					КАК Период,
	|	Т.Организация				КАК Организация
	|ПОМЕСТИТЬ ВТДвиженияСебестоимостиСписаниеВПроизводстве
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноСписанияНаРасходы)
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Период
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор 				КАК Ссылка,
	|	Т.Период	  				КАК Период
	|ПОМЕСТИТЬ ВТДвиженияНоменклатураСписаниеВПроизводстве
	|ИЗ
	|	РегистрНакопления.ДвиженияНоменклатураДоходыРасходы КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноСписанияНаРасходы)
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Период
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ДвиженияНоменклатураДоходыРасходы""	КАК ИмяРегистра,
	|	Себестоимость.Ссылка					КАК Ссылка,
	|	Себестоимость.Организация				КАК Организация,
	|	1 										КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	ВТДвиженияСебестоимостиСписаниеВПроизводстве КАК Себестоимость
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТДвиженияНоменклатураСписаниеВПроизводстве КАК Движения
	|		ПО Движения.Ссылка = Себестоимость.Ссылка
	|		 И Движения.Период = Себестоимость.Период
	|ГДЕ
	|	Движения.Ссылка ЕСТЬ NULL
	|";

	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='исправление движений документов по регистру ""Движения номенклатура доходы-расходы""'"));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(
		ПараметрыРасчета,
		"ВТДвиженияСебестоимостиСписаниеВПроизводстве, ВТДвиженияНоменклатураСписаниеВПроизводстве");
	
	#КонецОбласти

	#Область НаправлениеДеятельностиВПрочихРасходах
		
	// В регистрах "Прочие расходы" и "Партии прочих расходов" направление деятельности заполняется только при включенном
	// учете затрат по направлению деятельности. 
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Заполнено направление деятельности с выключенным учетом затрат'"));
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПрочиеРасходы""		 КАК ИмяРегистра,
	|	Т.Регистратор			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.НаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И НЕ Т.НаправлениеДеятельности.УчетЗатрат
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПартииПрочихРасходов"" КАК ИмяРегистра,
	|	Т.Регистратор			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.НаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И НЕ Т.НаправлениеДеятельности.УчетЗатрат
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)
	|";
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='некорректное заполнение направления деятельности в прочих расходах'"));
	
	#КонецОбласти

	#Область ПартнерВКлючеАналитикиУчетаНоменклатуры
	
	// Замена ключей аналитики учета номенклатуры с партнером на ключи с договором контрагента.
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='В ключе аналитики учета номенклатуры в регистре ""Себестоимость товаров"" указан партнер, а не договор контрагента.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru='В ключе аналитики учета номенклатуры в регистре ""Закупки"" указан партнер, а не договор контрагента.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(3, НСтр("ru='В ключе аналитики учета номенклатуры в регистре ""Движения Номенклатура - Номенклатура"" указан партнер, а не договор контрагента.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров""	КАК ИмяРегистра,
	|	Т.Регистратор				КАК Ссылка,
	|	Т.Организация				КАК Организация,
	|	1							КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И Т.РазделУчета В (
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути),
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки))
	|	И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""Закупки""	  КАК ИмяРегистра,
	|	Т.Регистратор КАК Ссылка,
	|	Т.Организация КАК Организация,
	|	2			  КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.Закупки КАК Т
	|
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И Т.ТипЗапасов В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке))
	|	И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|	И НЕ Т.РасчетСебестоимости
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ДвиженияНоменклатураНоменклатура"" КАК ИмяРегистра,
	|	Т.Регистратор						 КАК Ссылка,
	|	Т.Организация						 КАК Организация,
	|	3									 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ДвиженияНоменклатураНоменклатура КАК Т
	|
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И Т.ТипЗапасов В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке))
	|	И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|	И НЕ Т.РасчетСебестоимости
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='исправление ключей аналитики учета номенклатуры в регистрах (замена партнера на договор)'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти
	
	#Область НетТаможеннойПошлиныВДвиженияхДокументаТаможеннаяДекларацияИмпорт
	
	// Исправление движений по регистру "Себестоимость товаров" для документов "Таможенная декларация на импорт".
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='В движения таможенной декларации на импорт по регистру ""Себестоимость товаров"" отсутствует сумма пошлины. Требуется перепровести документ по регистру ""Себестоимость товаров""'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Декларация.Ссылка 		 КАК Ссылка,
	|	Декларация.Организация 	 КАК Организация,
	|	1 						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт КАК Декларация
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Движения
	|		ПО Декларация.Ссылка = Движения.Регистратор
	|		И Движения.ТипЗаписи В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыСПартией),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыБезПартии))
	|ГДЕ
	|	Декларация.Проведен
	|	И Декларация.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Декларация.Организация В (&МассивОрганизаций)
	|	И Декларация.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыТаможенныхДеклараций.ТаможенноеОформление)
	|	И (Декларация.СуммаДокумента - Декларация.ТаможенныйСбор - Декларация.ТаможенныйШтраф) <> 0
	|	И Движения.Регистратор ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ ПЕРВЫЕ 0
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Движения.Регистратор	 КАК Ссылка,
	|	Движения.Организация 	 КАК Организация,
	|	1 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Движения
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='не отражена таможенная пошлина в движениях по регистру ""Себестоимость товаров""'", ОбщегоНазначения.КодОсновногоЯзыка()));

	
	#КонецОбласти
	
	#Область АналитикаОтгрузкиВВозвратеТоваровМеждуОрганизациями
	
	// Заполнение нового реквизита табличной части ВидыЗапасов
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Не заполнена аналитика номенклатуры отгрузки в документе возврата товаров между организациями.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Ссылка				КАК Ссылка,
	|	Т.Ссылка.Организация	КАК Организация,
	|	1						КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыДляПерепроведения
	|ИЗ
	|	Документ.ВозвратТоваровМеждуОрганизациями.ВидыЗапасов КАК Т
	|
	|ГДЕ
	|	Т.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Ссылка.Организация В(&МассивОрганизаций)
	|	И Т.Ссылка.Проведен
	|	И Т.СпособОпределенияСебестоимости = ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияСебестоимостиВозврата.ИзДокументаПередачи)
	|	И Т.АналитикаУчетаНоменклатурыОтгрузки = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументы(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='исправление ключей аналитики учета номенклатуры отгрузки в документе возврата товаров между организациями'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти

	#Область ВключениеУчетаСебестоимостиПоНазначениям
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Включение учета себестоимости по назначениям'", ОбщегоНазначения.КодОсновногоЯзыка()));

	Запрос.УстановитьПараметр("ТипДокументаИмпорта", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ТаможеннаяДекларацияИмпорт"));
	
	Запрос.Текст = ТекстЗапросаВключениеУчетаСебестоимостиПоНазначениям();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='включение учета себестоимости по назначениям'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	Запрос.Текст = ТекстЗапросаВключениеУчетаВыручкиИСебестоимостиПродажПоНазначениям();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='включение учета выручки и себестоимости продаж по назначениям'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти
	
	#Область ВыключениеУчетаСебестоимостиПоНазначениям
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Выключение учета себестоимости по назначениям'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст = ТекстЗапросаВыключениеУчетаСебестоимостиПоНазначениям();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='выключение учета себестоимости по назначениям'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	Запрос.Текст = ТекстЗапросаВыключениеУчетаВыручкиИСебестоимостиПродажПоНазначениям();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='выключение учета выручки и себестоимости продаж по назначениям'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	#КонецОбласти
		
	#Область ВключениеУчетаСебестоимостиПоВидамЗапасов
	
	РасшифровкаКодовОшибок = Новый Соответствие;	
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Включение учета себестоимости по видам запасов'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст = ТекстЗапросаВключениеУчетаСебестоимостиПоВидамЗапасов();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='включение учета себестоимости по видам запасов'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	Запрос.Текст = ТекстЗапросаВключениеУчетаВыручкиИСебестоимостиПродажПоВидамЗапасов();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='включение учета выручки и себестоимости продаж по видам запасов'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти
	
	
	#Область КорректировкаПриобретения
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Неправильная операция учета себестоимости в движениях документа ""Корректировка приобретения"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru='Неправильные суммы НДС в движениях документа ""Корректировка приобретения"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Т.Ссылка 		 		 КАК Ссылка,
	|	Т.Организация 	 		 КАК Организация,
	|	1 						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	Документ.КорректировкаПриобретения КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Движения
	|		ПО Т.Ссылка = Движения.Регистратор
	|ГДЕ
	|	Т.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Движения.ОперацияУчетаСебестоимости = ЗНАЧЕНИЕ(Перечисление.ОперацииУчетаСебестоимости.ВыбытиеПоФиксированнойСтоимости)
	|	И Т.Проведен
	|	И НЕ Движения.РасчетПартий
	|	И НЕ Движения.РасчетСебестоимости
	|
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Т.Ссылка 		 		 КАК Ссылка,
	|	Т.Организация 	 		 КАК Организация,
	|	2 						 КАК КодОшибки
	|ИЗ
	|	Документ.КорректировкаПриобретения КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Движения
	|		ПО Т.Ссылка = Движения.Регистратор
	|ГДЕ
	|	Т.Проведен
	|	И Т.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Движения.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|	И (Движения.НДСРегл <> 0 ИЛИ Движения.НДСУпр <> 0)
	|	И НЕ Движения.РасчетПартий
	|	И НЕ Движения.РасчетСебестоимости
	|";

	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='исправление движений документа ""Корректировка приобретения""'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти
	
	#Область КорректировкаРеализации
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Неправильное движение в документе ""Корректировка реализации"".'", ОбщегоНазначения.КодОсновногоЯзыка()));

	Запрос.УстановитьПараметр("ДатаВключенияОбособленногоУчетаСебестоимостиПоНазначениям", Константы.ДатаВключенияОбособленногоУчетаСебестоимостиПоНазначениям.Получить());
	
	ТекстыЗапросов = Новый Массив;
	
	// При выключенном обособленном учете себестоимости по назначениеям, в документе "Корректировка реализации",
	// созданном на основании документа прошлого периода, с заполненным направлением деятельности,
	// формировалось приходное движение с неправильно подоборанными аналитиками учета номенклатуры,
	// у которых было заполнено назначение.
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	СебестоимостьТоваров.Регистратор КАК Ссылка,
	|	СебестоимостьТоваров.Организация КАК Организация,
	|	1 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитикиУчетаНоменклатуры
	|		ПО СебестоимостьТоваров.АналитикаУчетаНоменклатуры = КлючиАналитикиУчетаНоменклатуры.Ссылка
	|ГДЕ
	|	СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ТИПЗНАЧЕНИЯ(СебестоимостьТоваров.Регистратор) = ТИП(Документ.КорректировкаРеализации)
	|	И СебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И СебестоимостьТоваров.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиентаПрошлыхПериодов)
	|	И КлючиАналитикиУчетаНоменклатуры.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И КлючиАналитикиУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И (НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ИЛИ &УчитыватьСебестоимостьТоваровПоНазначениям
	|				И СебестоимостьТоваров.Период < &ДатаВключенияОбособленногоУчетаСебестоимостиПоНазначениям)";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
	// При корректировке в сторону уменьшения (оприходование) возвратной тары от реализации прошлого периода не формировались партии при закрытии месяца, так как не заполнялись кор аналитика и кор вид запасов.
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	СебестоимостьТоваров.Регистратор КАК Ссылка,
	|	СебестоимостьТоваров.Организация КАК Организация,
	|	1 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|ГДЕ
	|	СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ТИПЗНАЧЕНИЯ(СебестоимостьТоваров.Регистратор) = ТИП(Документ.КорректировкаРеализации)
	|	И СебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И СебестоимостьТоваров.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТарыОтКлиентаПрошлыхПериодов)
	|	И СебестоимостьТоваров.КорАналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|	И СебестоимостьТоваров.КорВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
	Запрос.Текст = СтрСоединить(ТекстыЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
	ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'исправление движений документа ""Корректировка реализации""'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	// Документы корректировки, которые по РН "ПрочиеРасходы" сфоримровали некорректные приходные движения по расхождениям в части НДС.                                       .
	// До исправления, в подобных движениях не формировалась сумма постоянной разницы, из-за чего в прочих расходах зависали остатки, а в проводках появлялись лишние суммы НУ
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Некорректные движения по документу ""Корректировка реализации"" в регистре ""Прочие расходы""'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПрочиеРасходы"" КАК ИмяРегистра,
	|	ПрочиеРасходы.Регистратор КАК Ссылка,
	|	ПрочиеРасходы.Организация КАК Организация,
	|	1 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК ПрочиеРасходы
	|ГДЕ 
	|	ПрочиеРасходы.Период МЕЖДУ &НачалоПериода И &КонецПериода 
	|	И ТИПЗНАЧЕНИЯ(ПрочиеРасходы.Регистратор) = ТИП(Документ.КорректировкаРеализации)
	|	И ПрочиеРасходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ПрочиеРасходы.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.РеализацияКлиенту)
	|	И НЕ ПрочиеРасходы.СтатьяРасходов.ПринятиеКНалоговомуУчету
	|	И ПрочиеРасходы.ПостояннаяРазница = 0
	|	И ПрочиеРасходы.Сумма = 0
	|	И ПрочиеРасходы.СуммаБезНДС = 0
	|	И ПрочиеРасходы.СуммаРегл <> 0
	|	И ПрочиеРасходы.СуммаУпр <> 0";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'некорректные движения по документу ""Корректировка реализации"" в регистре ""Прочие расходы""'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти
	
	#Область ПрочееОприходованиеТоваров_СторноСписанияНаРасходы
	
	// Для сторно списания на расходы не заполняется направление деятельности в прочих расходах, хотя при списании указывается,
	//	необходимо заполнять КорНаправлениеДеятельности в таких документах.
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Неправильное движение в документе ""Прочее оприходование товаров"".'", ОбщегоНазначения.КодОсновногоЯзыка()));

	Запрос.Текст = "ВЫБРАТЬ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	СебестоимостьТоваров.Регистратор КАК Ссылка,
	|	СебестоимостьТоваров.Организация КАК Организация,
	|	1 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрочееОприходованиеТоваров КАК ПрочееОприходованиеТоваров
	|		ПО СебестоимостьТоваров.Регистратор = ПрочееОприходованиеТоваров.Ссылка
	|ГДЕ
	|	СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И СебестоимостьТоваров.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноСписанияНаРасходы)
	|	И СебестоимостьТоваров.КорНаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И ПрочееОприходованиеТоваров.НаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
	ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'исправление движений документа ""Прочее оприходование товаров""'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	#КонецОбласти
	
	
	#Область ПрочаяВыручка
	
	// Документы, у которых ранее были движения по регистру "Прочие расходы", теперь должны формировать движения по регистру "Прочая выручка":
	// - "Реализация услуг и прочих активов"
	// - "Корректировка реализации", у которой документ - основание "Реализация услуг и прочих активов"
	// - "Начисления по кредитам и депозитам"
	// - "Отражение зарплаты в финансовом учете"  
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Восстановление движений по регистру ""Прочая выручка"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор     КАК Регистратор,
	|	Т.Организация     КАК Организация
	|ПОМЕСТИТЬ ВТРегистраторыСПрочимиДоходами
	|ИЗ
	|	РегистрНакопления.ПрочиеДоходы КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК Корректировка
	|		ПО Корректировка.Ссылка = Т.Регистратор
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов КАК РеализацияУслуг
	|		ПО РеализацияУслуг.Ссылка = Корректировка.ДокументОснование
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И НЕ Т.РасчетСебестоимости
	|	И (ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.РеализацияУслугПрочихАктивов)
	// Корректировка реализации услуг прошлого года отражается в регистре "Прочие доходы", а не в регистре "Прочая выручка".
	|		ИЛИ НЕ РеализацияУслуг.Ссылка ЕСТЬ NULL
	|			И НАЧАЛОПЕРИОДА(Корректировка.Дата, ГОД) = НАЧАЛОПЕРИОДА(РеализацияУслуг.Дата, ГОД)
	|		ИЛИ ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.НачисленияКредитовИДепозитов)
	//++ Локализация


	//-- Локализация
	|		)
	|;
	|ВЫБРАТЬ
	|	""ПрочаяВыручка"" КАК ИмяРегистра,
	|	Т.Регистратор     КАК Ссылка,
	|	Т.Организация     КАК Организация,
	|	1                 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	ВТРегистраторыСПрочимиДоходами КАК Т
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ПрочиеДоходы""  КАК ИмяРегистра,
	|	Т.Регистратор     КАК Ссылка,
	|	Т.Организация     КАК Организация,
	|	1                 КАК КодОшибки
	|ИЗ
	|	ВТРегистраторыСПрочимиДоходами КАК Т
	|;
	|УНИЧТОЖИТЬ ВТРегистраторыСПрочимиДоходами
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='восстановление движений по регистру ""Прочая выручка""'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти
	
	#Область ПартииПрочихРасходов
	
	// Отражение статей с вариантом распределения НаПроизводственныеЗатраты в регистре накопления ПартииПрочихРасходов.
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Отражение статей с вариантом распределения ""На производственные затраты"" в регистре ""Партии прочих расходов"" не соответствует учетной политике организации'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru='Отражение статей с вариантом распределения ""На себестоимость товаров"" в регистре ""Партии прочих расходов"" не соответствует учетной политике организации'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(3, НСтр("ru='Для статей с вариантом распределения ""На себестоимость товаров"" нет движений в регистре ""Партии прочих расходов"" для организации плательщика НДД'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	СуществующиеВТ = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(Запрос);
	
	Запрос.Текст = ТекстЗапросаСтатьиСРаздельнымУчетомНДСВРегистреПартииПрочихРасходов();
	ПараметрыОтбораДокументовЗапросаСтатьиСРаздельнымУчетомНДСВРегистреПартииПрочихРасходов(Запрос);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='некорректные движения по регистру ""Партии прочих расходов""'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Заполнена аналитика учета номенклатуры в регистре ""Партии прочих расходов"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru='Заполнена сумма НДС в движениях с признаком Сторно в регистре ""Партии прочих расходов"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	""ПартииПрочихРасходов"" КАК ИмяРегистра,
	|	Т.Регистратор     КАК Ссылка,
	|	Т.Организация     КАК Организация,
	|	1                 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И Т.АналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовБезДвижений)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ПартииПрочихРасходов"" КАК ИмяРегистра,
	|	Т.Регистратор     КАК Ссылка,
	|	Т.Организация     КАК Организация,
	|	2                 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И Т.Сторно
	|	И (Т.НДСРегл <> 0 ИЛИ Т.НДСУпр <> 0)
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='некорректные движения по регистру ""Партии прочих расходов""'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	НовыеВТ = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(Запрос, СуществующиеВТ);
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета, НовыеВТ);
	
	#КонецОбласти
	
	#Область ПрочиеРасходы
	
	// В документах "Движение прочих активов и пассивов по операции увеличения активов за счет уменьшения расходов формировались неправильные движения в регистре "Прочие расходы":
	// - было: расходное движение с положительными суммами
	// - должно быть: приходное движение с отрицательными суммами
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Сформированы некорректные движения по регистру  ""Прочие расходы"".'", ОбщегоНазначения.КодОсновногоЯзыка()));;
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПрочиеРасходы"" 		 КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И Т.Регистратор ССЫЛКА Документ.ДвижениеПрочихАктивовПассивов
	|	И ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтражениеПрочихАктивовПассивов)
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.Сумма > 0 И Т.СуммаБезНДС > 0
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'исправление движений документов по регистру ""Прочие расходы"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти
	
	#Область СебестоимостьТоваровСуммыВТаможенныхДекларацияхИмпорт
	
	// В документах ТаможеннаяДекларацияИмпорт, регламентом расчета себестоимости,
	// могли быть сформированы проводки с неполной суммой, ошибка исправлена в рамках задачи 00-00542007
	// для восстановления сумм проводок необходимо перепроведение документов
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1,
		НСтр("ru = 'Отличаются суммы в движения по регистру ""Себестоимость товаров"" от регистра ""Закупки""'",
			ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(2,
		НСтр("ru = 'Отличаются суммы в движения по регистру ""Закупки"" от регистра ""Себестоимость товаров""'",
			ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ВЗ.Регистратор КАК Регистратор,
	|	ВЗ.Организация КАК Организация,
	|	ВЗ.Номенклатура КАК Номенклатура,
	|	ВЗ.Характеристика КАК Характеристика,
	|	ВЗ.Серия КАК Серия,
	|	ВЗ.МестоХранения КАК МестоХранения,
	|	СУММА(ВЗ.ДопРасходы) КАК ДопРасходы,
	|	СУММА(ВЗ.ДопРасходыБезНДС) КАК ДопРасходыБезНДС,
	|	СУММА(ВЗ.ДопРасходыРегл) КАК ДопРасходыРегл
	|ПОМЕСТИТЬ ВтСуммыДвиженийТаможеннойДекларации
	|ИЗ
	|	(ВЫБРАТЬ
	|		СебестоимостьТоваров.Организация КАК Организация,
	|		СебестоимостьТоваров.Регистратор КАК Регистратор,
	|		СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|		СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|		СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Серия КАК Серия,
	|		СебестоимостьТоваров.АналитикаУчетаНоменклатуры.МестоХранения КАК МестоХранения,
	|		СебестоимостьТоваров.ДопРасходы КАК ДопРасходы,
	|		СебестоимостьТоваров.ДопРасходыБезНДС КАК ДопРасходыБезНДС,
	|		СебестоимостьТоваров.ДопРасходыРегл КАК ДопРасходыРегл
	|	ИЗ
	|		РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|	ГДЕ
	|		СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И СебестоимостьТоваров.Организация В (&МассивОрганизаций)
	|		И СебестоимостьТоваров.Активность
	|		И СебестоимостьТоваров.Регистратор ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
	|		И СебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И НЕ СебестоимостьТоваров.РасчетПартий
	|		И НЕ СебестоимостьТоваров.РасчетСебестоимости
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Закупки.Организация,
	|		Закупки.Регистратор,
	|		Закупки.АналитикаУчетаНоменклатуры.Номенклатура,
	|		Закупки.АналитикаУчетаНоменклатуры.Характеристика,
	|		Закупки.АналитикаУчетаНоменклатуры.Серия,
	|		Закупки.АналитикаУчетаНоменклатуры.МестоХранения,
	|		-Закупки.Сумма КАК ДопРасходы,
	|		-Закупки.СуммаБезНДС КАК ДопРасходыБезНДС,
	|		-Закупки.СтоимостьРегл КАК ДопРасходыРегл
	|	ИЗ
	|		РегистрНакопления.Закупки КАК Закупки
	|	ГДЕ
	|		Закупки.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Закупки.Организация В (&МассивОрганизаций)
	|		И Закупки.Активность
	|		И Закупки.Регистратор ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
	|) КАК ВЗ
	|СГРУППИРОВАТЬ ПО
	|	ВЗ.Регистратор,
	|	ВЗ.Номенклатура,
	|	ВЗ.Организация,
	|	ВЗ.Характеристика,
	|	ВЗ.Серия,
	|	ВЗ.МестоХранения
	|ИМЕЮЩИЕ
	|	(СУММА(ВЗ.ДопРасходы) <> 0
	|	ИЛИ СУММА(ВЗ.ДопРасходыБезНДС) <> 0
	|	ИЛИ СУММА(ВЗ.ДопРасходыРегл) <> 0)
	|;
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Т.Регистратор КАК Ссылка,
	|	Т.Организация КАК Организация,
	|	1 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	ВтСуммыДвиженийТаможеннойДекларации КАК Т
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""Закупки"" КАК ИмяРегистра,
	|	Т.Регистратор КАК Ссылка,
	|	Т.Организация КАК Организация,
	|	2 КАК КодОшибки
	|ИЗ
	|	ВтСуммыДвиженийТаможеннойДекларации КАК Т
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'исправление движений документа ""Таможенная декларация на испорт"" по регистрам ""Себестоимость товаров"" и ""Закупки"".'",
			ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти
	
	#Область РаспределениеПрочихЗатрат
	
	// В закрываемом периоде не должно быть 2х документов по НУ и Регл. учету со схожими аналитиками
	// При наличии таких документов, один помечаем на удаление, у второго ставим 2 галочки.
	Запрос.Текст = "ВЫБРАТЬ
	|	РаспределениеПрочихЗатрат.Организация КАК Организация,
	|	РаспределениеПрочихЗатрат.Подразделение КАК Подразделение,
	|	РаспределениеПрочихЗатрат.СтатьяРасходов КАК СтатьяРасходов,
	|	РаспределениеПрочихЗатрат.АналитикаРасходов КАК АналитикаРасходов,
	|	КОЛИЧЕСТВО(РаспределениеПрочихЗатрат.Ссылка) КАК КоличествоДокументов,
	|	МАКСИМУМ(РаспределениеПрочихЗатрат.УправленческийУчет) КАК УправленческийУчет,
	|	МАКСИМУМ(РаспределениеПрочихЗатрат.РегламентированныйУчет) КАК РегламентированныйУчет,
	|	МАКСИМУМ(РаспределениеПрочихЗатрат.НалоговыйУчет) КАК НалоговыйУчет,
	|	НАЧАЛОПЕРИОДА(РаспределениеПрочихЗатрат.Дата, МЕСЯЦ) КАК МесяцРасхода,
	|	РаспределениеПрочихЗатрат.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	МАКСИМУМ(ВЫБОР
	|		КОГДА РаспределениеПрочихЗатрат.РегламентированныйУчет
	|			ТОГДА РаспределениеПрочихЗатрат.Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ) КАК ДокументДляПроведения
	|ПОМЕСТИТЬ РеквизитыСЗадвоеннымиДокументами
	|ИЗ
	|	Документ.РаспределениеПрочихЗатрат КАК РаспределениеПрочихЗатрат
	|ГДЕ
	|	РаспределениеПрочихЗатрат.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И РаспределениеПрочихЗатрат.Организация В (&МассивОрганизаций)
	|	И РаспределениеПрочихЗатрат.Проведен
	|	И (РаспределениеПрочихЗатрат.РегламентированныйУчет ИЛИ РаспределениеПрочихЗатрат.НалоговыйУчет)
	|	И (РаспределениеПрочихЗатрат.СтатьяРасходов.ВариантРаспределенияРасходовРегл = РаспределениеПрочихЗатрат.СтатьяРасходов.ВариантРаспределенияРасходовНУ
	|		)
	|СГРУППИРОВАТЬ ПО
	|	РаспределениеПрочихЗатрат.Организация,
	|	РаспределениеПрочихЗатрат.Подразделение,
	|	РаспределениеПрочихЗатрат.СтатьяРасходов,
	|	РаспределениеПрочихЗатрат.АналитикаРасходов,
	|	НАЧАЛОПЕРИОДА(РаспределениеПрочихЗатрат.Дата, МЕСЯЦ),
	|	РаспределениеПрочихЗатрат.НаправлениеДеятельности
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РаспределениеПрочихЗатрат.Ссылка) > 1
	|	И МАКСИМУМ(РаспределениеПрочихЗатрат.РегламентированныйУчет)
	|	И МАКСИМУМ(РаспределениеПрочихЗатрат.НалоговыйУчет)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеквизитыСЗадвоеннымиДокументами.ДокументДляПроведения КАК ДокументДляПроведения,
	|	РеквизитыСЗадвоеннымиДокументами.УправленческийУчет КАК УправленческийУчет
	|ИЗ
	|	РеквизитыСЗадвоеннымиДокументами КАК РеквизитыСЗадвоеннымиДокументами
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаспределениеПрочихЗатрат.Ссылка КАК ДокументДляПометкиНаУдаление
	|ИЗ
	|	РеквизитыСЗадвоеннымиДокументами КАК РеквизитыСЗадвоеннымиДокументами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК РаспределениеПрочихЗатрат
	|		ПО РеквизитыСЗадвоеннымиДокументами.Организация = РаспределениеПрочихЗатрат.Организация
	|		И РеквизитыСЗадвоеннымиДокументами.Подразделение = РаспределениеПрочихЗатрат.Подразделение
	|		И РеквизитыСЗадвоеннымиДокументами.СтатьяРасходов = РаспределениеПрочихЗатрат.СтатьяРасходов
	|		И РеквизитыСЗадвоеннымиДокументами.АналитикаРасходов = РаспределениеПрочихЗатрат.АналитикаРасходов
	|		И (РаспределениеПрочихЗатрат.РегламентированныйУчет
	|		ИЛИ РаспределениеПрочихЗатрат.НалоговыйУчет)
	|		И РеквизитыСЗадвоеннымиДокументами.НаправлениеДеятельности = РаспределениеПрочихЗатрат.НаправлениеДеятельности
	|		И РеквизитыСЗадвоеннымиДокументами.ДокументДляПроведения <> РаспределениеПрочихЗатрат.Ссылка
	|		И РаспределениеПрочихЗатрат.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|		И РаспределениеПрочихЗатрат.Проведен";
	
	РезультатЗапросаПакет = Запрос.ВыполнитьПакет();
	
	НачатьТранзакцию();
	
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.РаспределениеПрочихЗатрат");
		ЭлементБлокировки.ИсточникДанных = РезультатЗапросаПакет[1];
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "ДокументДляПроведения");
		Блокировка.Заблокировать();
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.РаспределениеПрочихЗатрат");
		ЭлементБлокировки.ИсточникДанных = РезультатЗапросаПакет[2];
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "ДокументДляПометкиНаУдаление");
		Блокировка.Заблокировать();  
		
		ДокументыДляПроведения = РезультатЗапросаПакет[1].Выбрать();
		ДокументыДляПоменткиНаУдаление = РезультатЗапросаПакет[2].Выбрать();
		
		Пока ДокументыДляПоменткиНаУдаление.Следующий() Цикл
			ДокументОбъект = ДокументыДляПоменткиНаУдаление.ДокументДляПометкиНаУдаление.ПолучитьОбъект();
			ДокументОбъект.ДополнительныеСвойства.Вставить("ОтменитьЗаписьЗаданияКРасчетуСебестоимости", Истина);
			ДокументОбъект.ПометкаУдаления = Истина;
			ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		КонецЦикла;
		
		Пока ДокументыДляПроведения.Следующий() Цикл
			ДокументОбъект = ДокументыДляПроведения.ДокументДляПроведения.ПолучитьОбъект();
			ДокументОбъект.УправленческийУчет = ДокументыДляПроведения.УправленческийУчет;
			ДокументОбъект.НалоговыйУчет = Истина;
			ДокументОбъект.ОбменДанными.Загрузка = Истина;
			ДокументОбъект.Записать();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстДляПротокола = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не удалось исправить документы распределения прочих затрат по причине:
				|%1'", ОбщегоНазначения.КодОсновногоЯзыка()),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		РасчетСебестоимостиПротоколРасчета.ЗафиксироватьОшибкуРасчета(
			ПараметрыРасчета,
			Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаВИсходныхДанныхДляРасчета,
			ТекстДляПротокола);
		
		ВызватьИсключение РасчетСебестоимостиПрикладныеАлгоритмы.ТекстИсключениеДляРегистрацииПроблемы(
			ТекстДляПротокола);
		
	КонецПопытки;
	
	#КонецОбласти
	
	#Область ВыручкаИСебестоимостьПродажЗадвоениеКоличестваВДВижениях
	
	// В документах ВозвратТоваровОтКлиента, регламентом расчета себестоимости, Этап ЗаполнениеПартийВРегистреВыручкаИСебестоимостьПродаж
	// добавляет в регистр накопления ВыручкаИСебестоимостьПродаж движение без партии и с задвоеным, затроенным и т.д. количеством
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1,
		НСтр("ru = 'Сформированы некорректные движения по регистру ""Выручка и себестоимость продаж"", найдены расхождения по количеству с регистром ""Себестоимость товаров""'",
			ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ВЗ.Организация КАК Организация,
	|	ВЗ.Регистратор КАК Регистратор,
	|	ВЗ.Номенклатура КАК Номенклатура,
	|	ВЗ.РазделУчета КАК РазделУчета,
	|	ВЗ.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	СУММА(ВЗ.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТРегистраторыСОшибкамиВКоличестве
	|ИЗ
	|	(ВЫБРАТЬ
	|		Т.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|		Т.Регистратор КАК Регистратор,
	|		Т.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|		Т.РазделУчета КАК РазделУчета,
	|		Т.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	// Количество при данном виде возврата, в движениях, отрицательное
	|		Т.Количество КАК Количество
	|	ИЗ
	|		РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
	|		ПО Т.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
	|	ГДЕ
	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Аналитики.Организация В(&МассивОрганизаций)
	|		И Т.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента 
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиентаПрошлыхПериодов)
	// Услуга не отображается в себестоимости, поэтому исключаем ее из ВыручкаИСебестоимостьПродаж 
	|		И Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Т.Организация,
	|		Т.Регистратор,
	|		СпрНоменклатура.Ссылка КАК Номенклатура,
	|		Т.РазделУчета,
	|		Т.ХозяйственнаяОперация,
	|		Т.Количество
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК СпрКлючиАналитикиУчетаНоменклатуры
	|		ПО (СпрКлючиАналитикиУчетаНоменклатуры.Ссылка = Т.АналитикаУчетаНоменклатуры)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КорКлючиАналитикиУчетаНоменклатуры
	|		ПО (КорКлючиАналитикиУчетаНоменклатуры.Ссылка = Т.КорАналитикаУчетаНоменклатуры)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО (ЕСТЬNULL(КорКлючиАналитикиУчетаНоменклатуры.Номенклатура, СпрКлючиАналитикиУчетаНоменклатуры.Номенклатура) = СпрНоменклатура.Ссылка)
	|	ГДЕ
	|		Т.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента
	// Многооборотная тара не отражается в выручке если отражается возврат многооборотной тары, поэтому исключим ее из СебестоимостьТоваров
	// У возврата многооборотной тары хозяйственная операция "Сторно переданной тары" или "Возврат тары от клиента прошлых периодов", поэтому условие ниже её исключает
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиентаПрошлыхПериодов)
	|	) КАК ВЗ
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЗ.Организация,
	|	ВЗ.Регистратор,
	|	ВЗ.Номенклатура,
	|	ВЗ.РазделУчета,
	|	ВЗ.ХозяйственнаяОперация
	|	
	|ИМЕЮЩИЕ СУММА(ВЗ.Количество) <> 0
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ВыручкаИСебестоимостьПродаж"" КАК ИмяРегистра,
	|	Т.Регистратор КАК Ссылка,
	|	Т.Организация КАК Организация,
	|	1 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	ВТРегистраторыСОшибкамиВКоличестве КАК Т
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'исправление движений документов по регистру ""Выручка и себестоимость продаж"".'",
			ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти
	
	РасчетСебестоимостиПодготовкаДанныхЛокализация.ИсправитьПроблемыВДвиженияхИДокументах(ПараметрыРасчета);
	
	ВТЭтапа = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(Запрос, СуществующиеВТДоЭтапа);
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(Запрос, ВТЭтапа);
	
КонецПроцедуры

Функция ТекстЗапросаСтатьиСРаздельнымУчетомНДСВРегистреПартииПрочихРасходов() Экспорт
	Возврат
		"ВЫБРАТЬ
		|	Т.Ссылка 							 КАК Ссылка,
		|	Т.Организация 						 КАК Организация,
		|	Т.Подразделение 					 КАК Подразделение,
		|	Т.НаправлениеДеятельности 			 КАК НаправлениеДеятельности,
		|	Т.СтатьяРасходов 					 КАК СтатьяРасходов,
		|	Т.АналитикаРасходов					 КАК АналитикаРасходов,
		|	МАКСИМУМ(Т.ЕстьПрочиеРасходы) 		 КАК ЕстьПрочиеРасходы,
		|	МАКСИМУМ(Т.ЕстьПартииПрочихРасходов) КАК ЕстьПартииПрочихРасходов
		|ПОМЕСТИТЬ ВТДвижения
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		Т.Регистратор КАК Ссылка,
		|		Т.Организация КАК Организация,
		|		Т.Подразделение КАК Подразделение,
		|		Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		Т.СтатьяРасходов КАК СтатьяРасходов,
		|		Т.АналитикаРасходов	КАК АналитикаРасходов,
		|		ИСТИНА КАК ЕстьПрочиеРасходы,
		|		ЛОЖЬ КАК ЕстьПартииПрочихРасходов
		|	ИЗ
		|		РегистрНакопления.ПрочиеРасходы КАК Т
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статья
		|			ПО Статья.Ссылка = Т.СтатьяРасходов
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Сторно КАК ДокументСторно
		|			ПО ДокументСторно.Ссылка = Т.Регистратор
		|	ГДЕ
		|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И Т.Организация В(&МассивОрганизаций)
		// Условие выбоки записей должны соответствовать функции:
		// РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов()
		|		И (
		//++ Локализация
		|			(((Статья.ВариантРаспределенияРасходовУпр В (
		|						ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
		|						ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства)) 
		// Когда статья расходов распределяется на производственные затраты только в УУ, 
		// движения в РН ПартииПрочихРасходов формируются только в том случае, если есть сумма НДС. Определить это можно по разнице Суммы и СуммыБезНДС.
		|				И Т.Сумма <> Т.СуммаБезНДС
		|				)
		|				ИЛИ Статья.ВариантРаспределенияРасходовРегл В (
		|						ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
		|						ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства)))
		|			И Статья.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ИзДокумента)
		|			И Т.Организация В (&ОрганизацииСРаздельнымУчетомПостатейныхЗатрат)
		|			)
		|		  ИЛИ
		//-- Локализация
		|			(Статья.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|				ИЛИ Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|			И (Т.Организация В (&ОрганизацииСУчетомПартийНДСВерсии22)
		|				ИЛИ Т.Организация В (&ОрганизацииСУчетомПартийНДСВерсии24)
		|				ИЛИ Т.Организация В (&ОрганизацииСДетализациейМатериальныхИПостатейныхЗатрат))
		|			)
		|		  )
		// Исключаем проверку движений по переброске расходов между организациями в упр. учете.
		|		И НЕ Т.ХозяйственнаяОперация В (
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеРасходовУУ),
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РегистрацияРасходовУУ))
		// Исключаем списание продукции на расходы по плановой стоимости
		|		И Т.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
		|		И НЕ Т.РасчетПартий
		|		И НЕ Т.РасчетСебестоимости
		|		И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)
		|		И НЕ ТИПЗНАЧЕНИЯ(ДокументСторно.СторнируемыйДокумент) В (&ТипыДокументовОСиНМА)
		|		И (НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
		|			ИЛИ ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.КорректировкаПриобретения))
		|		И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовБезДвижений)
		//++ Локализация
		
		
		//-- Локализация
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		Т.Регистратор,
		|		Т.Организация,
		|		Т.Подразделение,
		|		Т.НаправлениеДеятельности,
		|		Т.СтатьяРасходов,
		|		Т.АналитикаРасходов,
		|		ЛОЖЬ,
		|		ИСТИНА
		|	ИЗ
		|		РегистрНакопления.ПартииПрочихРасходов КАК Т
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статья
		|			ПО Статья.Ссылка = Т.СтатьяРасходов
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Сторно КАК ДокументСторно
		|			ПО ДокументСторно.Ссылка = Т.Регистратор
		|	ГДЕ
		|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И Т.Организация В(&МассивОрганизаций)
		// Условие выборки записей должны соответствовать функции:
		// РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов()
		|		И (
		//++ Локализация
		|			((Статья.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
		|				ИЛИ Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты))
		|			И Статья.ВариантРаздельногоУчетаНДС <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.Распределение)
		|			И Т.Организация В (&ОрганизацииСРаздельнымУчетомПостатейныхЗатрат)
		|			)
		|		  ИЛИ
		//-- Локализация
		|			(Статья.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|				ИЛИ Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|			И (Т.Организация В (&ОрганизацииСУчетомПартийНДСВерсии22)
		|				ИЛИ Т.Организация В (&ОрганизацииСУчетомПартийНДСВерсии24)
		|				ИЛИ Т.Организация В (&ОрганизацииСДетализациейМатериальныхИПостатейныхЗатрат))
		|			)
		|		  )
		// Исключаем проверку движений по переброске расходов между организациями в упр. учете.
		|		И НЕ Т.ХозяйственнаяОперация В (
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеРасходовУУ),
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РегистрацияРасходовУУ))
		// Исключаем списание продукции на расходы по плановой стоимости
		|		И Т.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
		|		И НЕ Т.РасчетПартий
		|		И НЕ Т.РасчетСебестоимости
		|		И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)
		|		И НЕ ТИПЗНАЧЕНИЯ(ДокументСторно.СторнируемыйДокумент) В (&ТипыДокументовОСиНМА)
		|		И (НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
		|			ИЛИ ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.КорректировкаПриобретения))
		|		И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовБезДвижений)
		|	) КАК Т
		|
		|СГРУППИРОВАТЬ ПО
		|	Т.Ссылка,
		|	Т.Организация,
		|	Т.Подразделение,
		|	Т.НаправлениеДеятельности,
		|	Т.СтатьяРасходов,
		|	Т.АналитикаРасходов
		|;
		|/////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ПартииПрочихРасходов"" КАК ИмяРегистра,
		|	Т.Ссылка 				 КАК Ссылка,
		|	Т.Организация 			 КАК Организация,
		|	(ВЫБОР
		|		КОГДА Статья.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|		 ИЛИ Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|			ТОГДА (ВЫБОР
		|					КОГДА 
		//++ Локализация
		|					  (Т.Организация В (&ОрганизацииПлательщикиНДД)
		|			 		  И Т.ЕстьПрочиеРасходы И НЕ Т.ЕстьПартииПрочихРасходов) ИЛИ
		//-- Локализация
		|					  ЛОЖЬ
		|						ТОГДА 3
		|					ИНАЧЕ 2 КОНЕЦ)
		|		ИНАЧЕ 1 КОНЕЦ)		 КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	ВТДвижения КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО Т.Организация = Организации.Ссылка
		//++ Локализация
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНастройкиУчетаНДС КАК УчетнаяПолитика
		|		ПО Организации.Ссылка = УчетнаяПолитика.Организация
		//-- Локализация
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статья
		|		ПО Статья.Ссылка = Т.СтатьяРасходов
		|ГДЕ
		|	ВЫБОР
		//++ Локализация
		|		КОГДА Т.Организация В (&ОрганизацииСУчетомПартийНДСВерсии24)
		|		 И УчетнаяПолитика.РаздельныйУчетПостатейныхПроизводственныхЗатратПоНалогообложениюНДС
		|		 И (Статья.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
		|			ИЛИ Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты))
		|		 И Статья.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ИзДокумента)
		|			ТОГДА Т.ЕстьПрочиеРасходы И НЕ Т.ЕстьПартииПрочихРасходов
		//-- Локализация
		|		КОГДА (Т.Организация В (&ОрганизацииСУчетомПартийНДСВерсии22)
		|			ИЛИ Т.Организация В (&ОрганизацииСУчетомПартийНДСВерсии24)
		|			ИЛИ Т.Организация В (&ОрганизацииСДетализациейМатериальныхИПостатейныхЗатрат))
		|		 И (Статья.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|		 ИЛИ Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров))
		|			ТОГДА Т.ЕстьПрочиеРасходы И НЕ Т.ЕстьПартииПрочихРасходов
		|		ИНАЧЕ Т.ЕстьПартииПрочихРасходов
		|	КОНЕЦ
		|;
		|/////////////////////////////////////////////////////////////////////////////
		|
		|УНИЧТОЖИТЬ ВТДвижения";
КонецФункции

Процедура ПараметрыОтбораДокументовЗапросаСтатьиСРаздельнымУчетомНДСВРегистреПартииПрочихРасходов(Запрос) Экспорт
	
	// Типы документов, которые имеют движения только по одному из регистров ПартииПрочихРасходов или ПрочиеРасходы.
	ТипыРегистраторов = Новый Массив;
	
	Для Каждого МетаДокумент Из Метаданные.Документы Цикл
		Если МетаДокумент.Движения.Содержит(Метаданные.РегистрыНакопления.ПрочиеРасходы) <>
		  МетаДокумент.Движения.Содержит(Метаданные.РегистрыНакопления.ПартииПрочихРасходов) Тогда
			ТипыРегистраторов.Добавить(Тип("ДокументСсылка." + МетаДокумент.Имя));
		КонецЕсли;
	КонецЦикла;
	
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.РаспределениеПрочихЗатрат"));
	
	Запрос.УстановитьПараметр("ТипыДокументовБезДвижений", ТипыРегистраторов);
	
КонецПроцедуры

// Возвращает текст запроса для формирования ВТДвиженияСебестоимостьТоваров
//
// Возвращаемое значение:
//	Строка - текст запроса
//
Функция ТекстЗапросаВТДвиженияСебестоимостьТоваров() Экспорт
	Возврат
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.ВидДвижения				 КАК ВидДвижения,
		|	Т.Регистратор 				 КАК Регистратор,
		|	Т.Организация 				 КАК Организация,
		|	Т.КорОрганизация 			 КАК КорОрганизация,
		|	Т.Партия 	  				 КАК Партия,
		|	Т.КорПартия 	  			 КАК КорПартия,
		|	Т.ХозяйственнаяОперация 	 КАК ХозяйственнаяОперация,
		|	Т.ТипЗаписи					 КАК ТипЗаписи,
		|	Т.АналитикаУчетаПартий		 КАК АналитикаУчетаПартий,
		|	Т.КорАналитикаУчетаПартий	 КАК КорАналитикаУчетаПартий,
		|	Т.РазделУчета				 КАК РазделУчета,
		|	Т.ВидЗапасов				 КАК ВидЗапасов,
		|	Т.КорВидЗапасов				 КАК КорВидЗапасов,
		|	Т.КорРазделУчета			 КАК КорРазделУчета,
		|	Т.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
		|	Т.КорАналитикаУчетаНоменклатуры	КАК КорАналитикаУчетаНоменклатуры,
		|	Т.АналитикаФинансовогоУчета	КАК АналитикаФинансовогоУчета,
		|	Т.КорАналитикаФинансовогоУчета	КАК КорАналитикаФинансовогоУчета,
		|	Т.ВидДеятельностиНДС		 КАК ВидДеятельностиНДС,
		|	Т.КорВидДеятельностиНДС		 КАК КорВидДеятельностиНДС,
		|	Т.ВидДеятельностиНДСДокумента КАК ВидДеятельностиНДСДокумента,
		|	Т.АналитикаУчетаПоПартнерам	 КАК АналитикаУчетаПоПартнерам,
		|	Т.ГруппаПродукции			 КАК ГруппаПродукции,
		|	Т.ДокументИсточник			 КАК ДокументИсточник,
		|	Т.ПериодПродажи				 КАК ПериодПродажи,
		|	Т.Сторно					 КАК Сторно,
		|	Т.ИдентификаторСтроки		 КАК ИдентификаторСтроки,
		|	Т.ИдентификаторФинЗаписи	 КАК ИдентификаторФинЗаписи,
		|	Т.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
		|	Т.ОперацияУчетаСебестоимости КАК ОперацияУчетаСебестоимости,
		|	ВЫБОР КОГДА Т.Количество > 0
		|		ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ 						 КАК ПоложительноеКоличество,
		|	Т.ДокументДвижения			 КАК ДокументДвижения,
		|	ЕСТЬNULL(УчетныеПолитикиФинУчета.МетодОценкиСтоимостиТоваров, НЕОПРЕДЕЛЕНО) КАК МетодОценкиСтоимостиТоваров,
		|	Т.КорСтоимость				 КАК КорСтоимость,
		|	Т.СтатьяДоходов				 КАК СтатьяДоходов,
		|	Т.Количество				 КАК Количество,
		|	Т.Стоимость					 КАК Стоимость,
		|	Т.СтоимостьРегл				 КАК СтоимостьРегл,
		|	Т.НДСРегл					 КАК НДСРегл,
		|	Т.СтоимостьЗабалансоваяРегл	 КАК СтоимостьЗабалансоваяРегл
		|ПОМЕСТИТЬ ВТДвиженияСебестоимостьТоваров
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК Т
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУчетныеПолитикиФинУчета КАК УчетныеПолитикиФинУчета
		|			ПО Т.Организация = УчетныеПолитикиФинУчета.Организация
		|ГДЕ
		|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Т.Организация В(&МассивОрганизаций)
		|	И НЕ Т.РасчетПартий
		|	И НЕ Т.РасчетСебестоимости
		|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (ТИП(Документ.РасчетСебестоимостиТоваров),
		|									ТИП(Документ.КорректировкаРегистров))
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Регистратор,
		|	Т.ДокументИсточник,
		|	Т.Организация
		|ПОМЕСТИТЬ ВТДокументыИсточникиИзСебестоимости
		|ИЗ 
		|	ВТДвиженияСебестоимостьТоваров КАК Т
		|ГДЕ
		|	Т.ДокументИсточник <> НЕОПРЕДЕЛЕНО
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументИсточник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Регистратор,
		|	Т.ДокументИсточник,
		|	Т.Организация КАК Организация,
		|	Реестр.ДатаДокументаИБ КАК Дата
		|ПОМЕСТИТЬ ВТДокументыИсточникиИзСебестоимостиСДатами
		|ИЗ 
		|	ВТДокументыИсточникиИзСебестоимости КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК Реестр
		|		ПО Т.ДокументИсточник = Реестр.Ссылка
		|		И НЕ Реестр.ДополнительнаяЗапись
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Регистратор,
		|	Т.ДокументИсточник,
		|	Т.Организация КАК Организация,
		|	Т.Дата,
		|	МАКСИМУМ(УчетнаяПолитика.Период) КАК ПериодПолитики
		|ПОМЕСТИТЬ ВТДокументыИсточникиИзСебестоимостиСДатамиИПериодамиПолитик
		|ИЗ 
		|	ВТДокументыИсточникиИзСебестоимостиСДатами КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета КАК УчетнаяПолитика
		|		ПО Т.Организация = УчетнаяПолитика.Организация
		|		И Т.Дата >= УчетнаяПолитика.Период
		|СГРУППИРОВАТЬ ПО
		|	Т.Регистратор,
		|	Т.ДокументИсточник,
		|	Т.Организация,
		|	Т.Дата
		|ИНДЕКСИРОВАТЬ ПО
		|	ПериодПолитики,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Регистратор,
		|	Т.ДокументИсточник,
		|	Т.Организация,
		|	УчетнаяПолитика.МетодОценкиСтоимостиТоваров
		|ПОМЕСТИТЬ ВТУчетныеПолитикиДокументовИсточников
		|ИЗ 
		|	ВТДокументыИсточникиИзСебестоимостиСДатамиИПериодамиПолитик КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета КАК УчетнаяПолитика
		|		ПО Т.Организация = УчетнаяПолитика.Организация
		|		И Т.ПериодПолитики = УчетнаяПолитика.Период
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|";
КонецФункции

// Возвращает текст запроса, позволяющего получить список документов
// с некорректными (задублированными) движениями по выручке и себестоимости продаж,
// появившимися в результате ошибки 00-00071689.
//
// Возвращаемое значение:
//	Строка - текст запроса
//
Функция ТекстЗапросаДокументыСДублямиДвиженийПоВыручкеИСебестоимостиПродаж() Экспорт
	Возврат
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж"" КАК ИмяРегистра,
		|	Т.Регистратор 					КАК Ссылка,
		|	Аналитики.Организация			КАК Организация,
		|	1 								КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО Т.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|ГДЕ
		|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитики.Организация В(&МассивОрганизаций)
		|	И Т.Количество = 0
		|	И Т.Стоимость <> 0
		|	И НЕ Т.РасчетСебестоимости
		|	И НЕ Т.Регистратор ССЫЛКА Документ.КорректировкаРегистров
		|	И НЕ Т.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		|	И НЕ Т.Регистратор ССЫЛКА Документ.КорректировкаРеализации";
КонецФункции

// Возвращает текст запроса, позволяющего получить список документов
// с пропавшими движениями по корректировке выручки и себестоимости продаж,
// появившимися в результате ошибки 00-00302463.
//
Функция ТекстЗапросаДокументыСПропавшимиДвижениямиПоВыручкеИСебестоимостиПродаж()
	Возврат
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	""ВыручкаИСебестоимостьПродаж"" 		КАК ИмяРегистра,
		|	Т.Регистратор							КАК Ссылка,
		|	Т.АналитикаУчетаПоПартнерам.Организация	КАК Организация,
		|	1 										КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж"" КАК ИмяРегистра,
		|	Т.Ссылка 						КАК Ссылка,
		|	Т.Ссылка.Организация			КАК Организация,
		|	1 								КАК КодОшибки
		|ИЗ
		|	Документ.КорректировкаРеализации.ВидыЗапасовКорректировкаВыручки КАК Т
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Выручка
		|		ПО Т.Ссылка = Выручка.Регистратор
		|		И Т.АналитикаУчетаНоменклатуры.Номенклатура = Выручка.АналитикаУчетаНоменклатуры.Номенклатура
		|		И Т.АналитикаУчетаНоменклатуры.Характеристика = Выручка.АналитикаУчетаНоменклатуры.Характеристика
		|		И Т.АналитикаУчетаНоменклатуры.Серия = Выручка.АналитикаУчетаНоменклатуры.Серия
		|		И Выручка.Количество = 0
		|		И НЕ Выручка.РасчетСебестоимости
		|ГДЕ
		|	Т.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Т.Ссылка.Организация В(&МассивОрганизаций)
		|	И (Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|		ИЛИ (Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|			И ВЫБОР КОГДА Т.Ссылка.ДокументОснование ССЫЛКА Документ.РеализацияТоваровУслуг
		|				ТОГДА ВЫРАЗИТЬ(Т.Ссылка.ДокументОснование КАК Документ.РеализацияТоваровУслуг).ВернутьМногооборотнуюТару
		|				ИНАЧЕ ЛОЖЬ
		|			  КОНЕЦ = ЛОЖЬ)
		|		ИЛИ Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры ЕСТЬ NULL)
		|	И Выручка.Регистратор ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Движения по регистру "Выручка и себестоимость продаж" должны соответствовать движениям по регистру "Себестоимость товаров"
		// по полям: Период, Регистратор, АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, РазделУчета,
		// ЗаказКлиента, ИдентификаторФинЗаписи, Сторно.
		// Партионные измерения не учитываем, т.к. они еще не заполнены в начале закрытия месяца.
		// По полю АналитикаУчетаПоПартнерам соответствие не проверяем, т.к. это поле не всегда заполняется в движениях регистра "Себестоимость товаров".
		// У отчетов давальцу в движениях по регистру "Выручка и себестоимость продаж" вид запасов не заполняется.
		// Для таких движений соединение с себестоимостью товаров по полю "Вид запасов" не нужно делать. 
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж"" КАК ИмяРегистра,
		|	Т.Регистратор					КАК Ссылка,
		|	Аналитика.Организация			КАК Организация,
		|	2 								КАК КодОшибки
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
		|		ПО Аналитика.Ссылка = Т.АналитикаУчетаПоПартнерам
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
		|		ПО Себестоимость.Период = Т.Период
		|		И Себестоимость.Регистратор = Т.Регистратор
		|		И (Себестоимость.АналитикаУчетаНоменклатуры = Т.АналитикаУчетаНоменклатуры
		|			ИЛИ Т.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента), 
		|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиентаПрошлыхПериодов))
		|			И Себестоимость.КорАналитикаУчетаНоменклатуры = Т.АналитикаУчетаНоменклатуры
		|		)
		// Для отчетов давальцу вид запасов не заполняется.
		|		И (Себестоимость.ВидЗапасов = Т.ВидЗапасов
		|			ИЛИ Т.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга))
		|		И Себестоимость.Организация = Аналитика.Организация
		|		И Себестоимость.РазделУчета = Т.РазделУчета
		|		И (Себестоимость.ЗаказКлиента = Т.ЗаказКлиента
		|			ИЛИ Себестоимость.ОперацияУчетаСебестоимости = ЗНАЧЕНИЕ(Перечисление.ОперацииУчетаСебестоимости.ВнешнееПоступление))
		|		И Себестоимость.ИдентификаторФинЗаписи = Т.ИдентификаторФинЗаписи
		|		И Себестоимость.Сторно = Т.Сторно
		|ГДЕ
		|	&ИспользоватьУчетСебестоимости
		|	И Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитика.Организация В (&МассивОрганизаций)
		|	И Т.Количество <> 0
		|	И Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
		// Для хоз. операции "Оприходование по возврату" расчет себестоимости не выполняется (себестоимость определяется данными документа),
		// поэтому по таким операциям не нужно проверять соответствие движений регистров.
		|	И Т.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОприходованиеПоВозврату)
		|	И Т.НастройкаХозяйственнойОперации <> ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ОприходованиеПоВозврату)
		|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.ВводОстатковОПродажахЗаПрошлыеПериоды)
		|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.ВводОстатков)
		// В прошлых периодах могут быть движения с устаревшими видами запасов. Такие движения исключаем из проверки.
		|	И НЕ Т.ВидЗапасов.Устаревший 
		|	И Т.Активность
		|	И НЕ Т.РасчетПартий
		|	И НЕ Т.РасчетСебестоимости
		|	И Себестоимость.Регистратор ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж"" КАК ИмяРегистра,
		|	Т.Регистратор					КАК Ссылка,
		|	Аналитика.Организация			КАК Организация,
		|	3 								КАК КодОшибки
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
		|		ПО Аналитика.Ссылка = Т.АналитикаУчетаПоПартнерам
		|ГДЕ
		|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитика.Организация В (&МассивОрганизаций)
		|	И Т.Количество <> 0
		|	И Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|	И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяССылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Движения по регистру "Себестоимость товаров" должны соответствовать регистру "Выручка и себестоимость продаж"
		// в части заполнение поля "Аналитика учета по партнерам".
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""СебестоимостьТоваров""		КАК ИмяРегистра,
		|	Т.Регистратор					КАК Ссылка,
		|	Аналитика.Организация			КАК Организация,
		|	2 								КАК КодОшибки
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
		|		ПО Аналитика.Ссылка = Т.АналитикаУчетаПоПартнерам
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Выручка
		|		ПО Выручка.Период = Т.Период
		|		И Выручка.Регистратор = Т.Регистратор
		|		И Выручка.АналитикаУчетаНоменклатуры = Т.АналитикаУчетаНоменклатуры
		|		И Выручка.ВидЗапасов = Т.ВидЗапасов
		|		И Выручка.АналитикаУчетаПоПартнерам.Организация = Т.Организация
		|		И Выручка.РазделУчета = Т.РазделУчета
		|		И Выручка.ЗаказКлиента = Т.ЗаказКлиента
		|		И Выручка.АналитикаУчетаПоПартнерам = Т.АналитикаУчетаПоПартнерам
		|		И Выручка.ИдентификаторФинЗаписи = Т.ИдентификаторФинЗаписи
		|		И Выручка.Сторно = Т.Сторно
		|ГДЕ
		|	&ИспользоватьУчетСебестоимости
		|	И Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Т.Организация В (&МассивОрганизаций)
		|	И Т.Количество <> 0
		// Для хоз. операции "Оприходование по возврату" расчет себестоимости не выполняется (себестоимость определяется данными документа),
		// поэтому по таким операциям не нужно проверять соответствие движений регистров.
		|	И Т.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОприходованиеПоВозврату)
		|	И Т.НастройкаХозяйственнойОперации <> ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ОприходованиеПоВозврату)
		|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ОтчетОРозничныхВозвратах)
		|	И Т.Активность
		|	И НЕ Т.РасчетПартий
		|	И НЕ Т.РасчетСебестоимости
		|	И Выручка.Регистратор ЕСТЬ NULL
		|";
КонецФункции

Процедура ОчиститьДанныеУпрРегистров(ПараметрыРасчета)
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "ОчиститьДанныеУправленческихРегистров");
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Регистратор
		|ИЗ
		|	РегистрНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы КАК Т
		|ГДЕ
		|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Т.Организация В(&МассивОрганизаций)
		|	И Т.Регистратор ССЫЛКА Документ.РаспределениеПрочихЗатрат";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		
		ПараметрыЗаписи = РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьПараметрыЗаписи(
			ПараметрыРасчета.РасчетныйПериод.НачалоПериода,
			ПараметрыРасчета.НомерЗаданияДоРасчета);
	
		Попытка
			РасчетСебестоимостиПрикладныеАлгоритмы.ЗаписатьДвиженияПоРегистру(
				Выборка,
				РегистрыНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы,
				ПараметрыЗаписи);
		Исключение
			
			// Информацию об ошибке добавим в протокол расчета, в механизме закрытия месяца ошибки уже должны быть зарегистрированы.
			ТекстДляПротокола = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			РасчетСебестоимостиПротоколРасчета.ЗафиксироватьОшибкуРасчета(
				ПараметрыРасчета,
				Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаЗаписиДвиженийПоРегистрам,
				ТекстДляПротокола);
				
			ВызватьИсключение ТекстДляПротокола;
			
		КонецПопытки;
		
		РасчетСебестоимостиПротоколРасчета.ДополнительнаяИнформация(
			ПараметрыРасчета,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Очищено движений по регистру %1: %2'", ОбщегоНазначения.КодОсновногоЯзыка()),
				Метаданные.РегистрыНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы.ПолноеИмя(),
				РасчетСебестоимостиПротоколРасчета.ПредставлениеЗначения(Выборка.Количество())));
		
		
	КонецЕсли;
	
КонецПроцедуры

#Область ВключениеУчетаСебестоимостиПоВидамЗапасов

// Выбираем документы, в движениях которых не заполнен вид запасов, кроме исключений.
// Дополнительно выбираем данные из документов "Исправление развернутого сальдо товаров организаций",
// если у них не было движений по регистру "Себестоимость товаров" и отличаются виды запасов списания и оприходования.
//
// Движения, для которых не заполняется вид запасов в учете себестоимости товаров:
// - в аналитике учета номенклатуры указана номенклатура с типом "Услуга"
// - в аналитике учета номенклатуры указана непрослеживаемая номенклатура с типом номенклатуры "Работа"
// - товары, выкупленные у комитентов
//		раздел учета "Товары на складах", в аналитике учета номенклатуры тип места хранения "Партнер" или "Организация"
// - продукция, отраженная отчетами переработчика
//		раздел учета "Производственные затраты", в аналитике учета номенклатуры тип места хранения "Партнер"
// - работы к оформлению отчетов давальцу
//		движения "Приход" по разделу учета "Производственные затраты" с заполненным реквизитом "Кор. аналитика учета номенклатуры", 
//		хозяйственной операцией "Выпуск продукции", и типом записи "Партия"
// - продукция, реализованная документом "Отчет давальцу"
//		движения с хозяйственной операцией "Отчет давальцу"
//
// Дополнительные исключения для выручки и себестоимости продаж:
// - в типе запасов указано значение "Агентская услуга"
//	(в аналитике учета номенклатуры указана номенклатура с типом номенклатуры "Услуга. Выполняется партнером")
//
// Исключения для документов производства 2.1:	
// - вид запасов в движениях заполняется оффлайн: раздел учета "Производственные затраты" и тип записи "Потребление (производство)"
// - для документа "Перемещение материалов в производстве" вид запасов в движениях заполняется оффлайн
//
// Необходимо исправить движения в регистрах:
//	- Себестоимость товаров
//	- Выручка и себестоимость продаж
//
// Возвращаемое значение:
//	Строка - текст запроса
//
Функция ТекстЗапросаВключениеУчетаСебестоимостиПоВидамЗапасов() Экспорт
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""СебестоимостьТоваров""			КАК ИмяРегистра,
		|	СебестоимостьТоваров.Регистратор	КАК Ссылка,
		|	СебестоимостьТоваров.Организация	КАК Организация,
		|	1									КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|ГДЕ
		|	СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СебестоимостьТоваров.Организация В(&МассивОрганизаций)
		|	И СебестоимостьТоваров.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
		|	И СебестоимостьТоваров.Количество <> 0
		|	И НЕ СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
		// Непрослеживаемые работы.
		|	И НЕ (
		|		СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|		И НЕ СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура.ПрослеживаемыйТовар)
		// Работы в НЗП.
		|	И НЕ (
		|		СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|		И СебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство))
		// Товары, выкупленные у комитентов, или отраженные отчетами переработчика
		|	И НЕ (
		|		СебестоимостьТоваров.РазделУчета В (
		|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
		|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
		|		И СебестоимостьТоваров.АналитикаУчетаНоменклатуры.ТипМестаХранения В (
		|			ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Организация)))


		//++ Устарело_Производство21


		//-- Устарело_Производство21
		|	И НЕ СебестоимостьТоваров.РасчетСебестоимости
		|	И НЕ СебестоимостьТоваров.РасчетПартий
		|	И НЕ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		|	И НЕ СебестоимостьТоваров.Сторно
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Дополнительно выбираем данные из документов "Исправление развернутого сальдо товаров организаций",
		// если у них не было движений по регистру "Себестоимость товаров" и отличаются виды запасов списания и оприходования.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""СебестоимостьТоваров""		КАК ИмяРегистра,
		|	Исправления.Ссылка				КАК Ссылка,
		|	Исправления.Ссылка.Организация	КАК Организация,
		|	1								КАК КодОшибки
		|ИЗ
		|	Документ.ИсправлениеРазвернутогоСальдоТоваровОрганизаций.Исправления КАК Исправления
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|		ПО СебестоимостьТоваров.Период = Исправления.Ссылка.Дата
		|		И СебестоимостьТоваров.Регистратор = Исправления.Ссылка
		|ГДЕ
		|	Исправления.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Исправления.Ссылка.Организация В(&МассивОрганизаций)
		|	И Исправления.ВидЗапасовСписания <> Исправления.ВидЗапасовОприходования
		|	И СебестоимостьТоваров.Регистратор ЕСТЬ NULL
		|";
КонецФункции

// Выбираем документы, в движениях которых не заполнен вид запасов, кроме исключений.
//
// Возвращаемое значение:
//	Строка - текст запроса
//
Функция ТекстЗапросаВключениеУчетаВыручкиИСебестоимостиПродажПоВидамЗапасов() Экспорт
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж""			КАК ИмяРегистра,
		|	ВыручкаИСебестоимостьПродаж.Регистратор	КАК Ссылка,
		|	Аналитики.Организация					КАК Организация,
		|	1										КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|ГДЕ
		|	ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитики.Организация В(&МассивОрганизаций)
		|	И ВыручкаИСебестоимостьПродаж.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
		|	И ВыручкаИСебестоимостьПродаж.Количество <> 0
		|	И НЕ ВыручкаИСебестоимостьПродаж.ТипЗапасов В (
		|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга),
		|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.АгентскаяУслуга))
		// Непрослеживаемые работы.
		|	И НЕ (ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|		И НЕ ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.ПрослеживаемыйТовар)
		|	И НЕ ВыручкаИСебестоимостьПродаж.РасчетСебестоимости
		|	И НЕ ВыручкаИСебестоимостьПродаж.РасчетПартий
		|	И НЕ ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		|	И НЕ ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.ВводОстатков
		|	И НЕ ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.ВводОстатковОПродажахЗаПрошлыеПериоды
		|";
КонецФункции

#КонецОбласти

#Область ВключениеУчетаСебестоимости

// Выбираем документы, у которых есть движения по оперативным регистрам, но нет движений по учету себестоимости.
// Используются данные следующих оперативных регистров:
//	- Товары организаций
//		кроме таможенных деклараций с нулевыми суммами пошлины и НДС
//		кроме перемещений товаров в статусе "Отгружено"
//		кроме сборок товаров в статусе "В работе"
//++ Локализация
//		кроме уведомлений об остатках и ввозах прослеживаемых товаров (документы не делают движения по учету себестоимости)
//-- Локализация
//	- Товары организаций к передаче
//	- Товары переданные переработчику
//	- Товары полученные от переработчика
//	- Товары к оформлению таможенных деклараций (движения "Приход)
//	- Материалы и работы в производстве (учитываются работы при партионном учете версии 2.2,
//		при партионном учете 2.1 не используется одновременно с онлайн движениями по регистру "Себестоимость товаров")
//		исключаются движения у документов "Маршрутный лист производства", "Списание затрат на выпуск", "Распределение материалов и работ",
//		используемых для производства 2.1, т.к. эти документы не делают онлайн движений по регистру "Себестоимость товаров" 
//		одновременно с регистром "Материалы и работы в производстве".
// Дополнительно к оперативным регистрам выбираем данные непосредственно из документов (у этих документов нет оперативных регистров):
//	- Отчет давальцу
//	- Заявление о ввозе товаров из ЕАЭС с заполненной суммой НДС
//	- Ввод остатков с выключенным флажком "ОУ"
// Исключения:
//	- Для движений себестоимости по разделу учета "Незавершенное производство" нет соответствующих оперативных регистров.
//		Но этот раздел учета всегда корреспондирует с другими разделами, которые имеют соответствующие оперативные регистры.
//		Поэтому движения по разделу "Незавершенное производстве" не проверяем. Они будут переформированы одновременно с другими движениями.
//	- Движения документов "Заявление о ввозе товаров из ЕАЭС" по себестоимости не имеют соответствующих оперативных регистров. 
//		Проверяем данные в табличной части документов
//	- Движения документов "Распределение материалов и работ" при распределении материалов по правилу
//	- Движения документов "Исправление развернутого сальдо товаров организаций" если в движениях не меняется вид запасов.
// 		У таких документов не будет движений по регистру "Себестоимость товаров".
//	- Документы корректировки регистров. У таких документов могут быть движения только по оперативным регистрам.
//	- Документы ввода начальных остатков, у которых снят флажок "Себестоимость" (у таких документов нет движений по учету себестоимости)
//
// Возвращаемое значение:
//	Строка - текст запроса
//
Функция ТекстЗапросаВключениеУчетаСебестоимости() Экспорт
	Возврат "
		// Выбираем таможенные декларации на импорт, в которых указаны нулевые суммы таможенной пошлины и НДС.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Ссылка,
		|	Строки.АналитикаУчетаНоменклатуры
		|ПОМЕСТИТЬ ДекларацииСНулевымиСуммами
		|ИЗ
		|	Документ.ТаможеннаяДекларацияИмпорт КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Строки
		|		ПО Строки.Ссылка = ДД.Ссылка
		|ГДЕ
		|	ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Проведен
		|	И Строки.СуммаПошлины = 0
		|	И Строки.СуммаНДС = 0
		|	И ДД.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыТаможенныхДеклараций.ВыпущеноСТаможни)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Если в аналитике учета номенклатуры место хранения отличается от склада,
		// то подберем аналитику учета номенклатуры, соответствующую складу.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Ссылка,
		|	Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры
		|ИЗ
		|	Документ.ТаможеннаяДекларацияИмпорт КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Строки
		|		ПО Строки.Ссылка = ДД.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Номенклатура = Строки.Номенклатура
		|		И Аналитика.Характеристика = Строки.Характеристика
		|		И Аналитика.Серия = Строки.Серия
		|		И Аналитика.МестоХранения = Строки.Склад
		|		И (ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
		|			ТОГДА Аналитика.Назначение = Строки.Назначение
		|			ИНАЧЕ Аналитика.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КОНЕЦ)
		|ГДЕ
		|	ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Проведен
		|	И Строки.СуммаПошлины = 0
		|	И Строки.СуммаНДС = 0
		|	И ДД.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыТаможенныхДеклараций.ВыпущеноСТаможни)
		|;
		// Выбираем документы, у которых есть движения по оперативным регистрам.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.Регистратор КАК Регистратор,
		|	Товары.Организация КАК Организация
		|ПОМЕСТИТЬ ДокументыСДвижениями
		|ИЗ (
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыОрганизаций.Регистратор КАК Регистратор,
		|		ТоварыОрганизаций.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		// Исключаем таможенные декларации с нулевыми суммами пошлины и НДС
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДекларацииСНулевымиСуммами КАК Декларации
		|			ПО Декларации.Ссылка = ТоварыОрганизаций.Регистратор
		|			И Декларации.АналитикаУчетаНоменклатуры = ТоварыОрганизаций.АналитикаУчетаНоменклатуры
		|			И ТИПЗНАЧЕНИЯ(ТоварыОрганизаций.Регистратор) = ТИП(Документ.ТаможеннаяДекларацияИмпорт)
		// Исключаем перемещения товаров в статусе "Отгружено"
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров КАК Перемещение
		|			ПО Перемещение.Ссылка = ТоварыОрганизаций.Регистратор
		|			И Перемещение.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПеремещенийТоваров.Отгружено)
		// Исключаем сторно перемещения товаров в статусе "Отгружено"
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Сторно КАК СторноПеремещенияТоваров
		|			ПО СторноПеремещенияТоваров.Ссылка = ТоварыОрганизаций.Регистратор
		|			И СторноПеремещенияТоваров.СторнируемыйДокумент ССЫЛКА Документ.ПеремещениеТоваров
		|			И ВЫРАЗИТЬ(СторноПеремещенияТоваров.СторнируемыйДокумент КАК Документ.ПеремещениеТоваров).Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПеремещенийТоваров.Отгружено)
		// Исключаем сборки товаров в статусе "В работе"
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СборкаТоваров КАК Сборка
		|			ПО Сборка.Ссылка = ТоварыОрганизаций.Регистратор
		|			И Сборка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.ВРаботе)
		|	ГДЕ
		|		&ИспользоватьУчетСебестоимости
		|		И ТоварыОрганизаций.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыОрганизаций.Организация В(&МассивОрганизаций)
		|		И (ТоварыОрганизаций.ОрганизацияОтгрузки = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|			ИЛИ ТоварыОрганизаций.ОрганизацияОтгрузки = ТоварыОрганизаций.Организация)
		|		И ТИПЗНАЧЕНИЯ(ТоварыОрганизаций.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
		// Исключаем Отчет комитенту (принципалу) о закупках, т.к. он делет движения только по товарам организаций.
		|		И ТИПЗНАЧЕНИЯ(ТоварыОрганизаций.Регистратор) <> ТИП(Документ.ОтчетКомитентуОЗакупках)
		// Исключаем движения по типу назначения "ПоставкаПодПринципала", т.к. по таким назначениям, по идее, движения по себестоимости формироваться не должны.
		|		И ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Назначение.ТипНазначения <> ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
		// Исключаем движения документов производства 2.2, для которых нет онлайн движений по регистру "Себестоимость товаров"
		|		И ТоварыОрганизаций.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовНаПартииПроизводства)
		// Исключаем движения документов "Исправление развернутого сальдо товаров организаций" и "Корректировка назначения товаров",
		// если в движениях не меняется вид запасов. У таких документов не будет движений по регистру "Себестоимость товаров".
		|		И НЕ (
		|			ТоварыОрганизаций.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОбособленногоУчета)
		|			И ТоварыОрганизаций.ВидЗапасов = ТоварыОрганизаций.КорВидЗапасов)
		// Исключаем таможенные декларации с нулевыми суммами пошлины и НДС
		|		И Декларации.Ссылка ЕСТЬ NULL
		// Исключаем вводы остатков с выключенным учетом себестоимости
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ВводОстатков).ОтражатьСебестоимость, ИСТИНА)
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ВводОстатковТоваров).ОтражатьСебестоимость, ИСТИНА)
		// Исключаем перемещения товаров в статусе "Отгружено"
		|		И Перемещение.Ссылка ЕСТЬ NULL
		// Исключаем сторно перемещения товаров в статусе "Отгружено"
		|		И СторноПеремещенияТоваров.Ссылка ЕСТЬ NULL
		// Исключаем сборки товаров в статусе "В работе"
		|		И Сборка.Ссылка ЕСТЬ NULL
		//++ Локализация
		
		// Исключаем уведомления об остатках и ввозах прослеживаемых товаров (документы не делают движений по учету себестоимости)
		|		И ТИПЗНАЧЕНИЯ(ТоварыОрганизаций.Регистратор) <> ТИП(Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров)
		|		И ТИПЗНАЧЕНИЯ(ТоварыОрганизаций.Регистратор) <> ТИП(Документ.УведомлениеОВвозеПрослеживаемыхТоваров)
		//-- Локализация
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыОрганизацийКПередаче.Регистратор КАК Регистратор,
		|		ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизацийКПередаче КАК ТоварыОрганизацийКПередаче
		|	ГДЕ
		|		&ИспользоватьУчетСебестоимости
		|		И ТоварыОрганизацийКПередаче.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация В(&МассивОрганизаций)
		|		И ТИПЗНАЧЕНИЯ(ТоварыОрганизацийКПередаче.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
		|
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыКОформлениюДокументовИмпорта.Регистратор КАК Регистратор,
		|		ТоварыКОформлениюДокументовИмпорта.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыКОформлениюДокументовИмпорта КАК ТоварыКОформлениюДокументовИмпорта
		|	ГДЕ
		|		&ИспользоватьУчетСебестоимости
		|		И ТоварыКОформлениюДокументовИмпорта.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыКОформлениюДокументовИмпорта.Организация В(&МассивОрганизаций)
		|		И ТоварыКОформлениюДокументовИмпорта.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И ТИПЗНАЧЕНИЯ(ТоварыКОформлениюДокументовИмпорта.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
		|		И ТоварыКОформлениюДокументовИмпорта.ТипДокументаИмпорта = &ТипДокументаИмпорта
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		МатериалыИРаботыВПроизводстве.Регистратор КАК Регистратор,
		|		МатериалыИРаботыВПроизводстве.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.МатериалыИРаботыВПроизводстве КАК МатериалыИРаботыВПроизводстве
		|	ГДЕ
		|		&ИспользоватьУчетСебестоимости
		|		И МатериалыИРаботыВПроизводстве.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И МатериалыИРаботыВПроизводстве.Организация В(&МассивОрганизаций)
		|		И МатериалыИРаботыВПроизводстве.Количество <> 0
		|		И НЕ ТИПЗНАЧЕНИЯ(МатериалыИРаботыВПроизводстве.Регистратор) В (
		|			ТИП(Документ.КорректировкаРегистров),
		|			ТИП(Документ.РасчетСебестоимостиТоваров))
		// Исключаем вводы остатков с выключенным учетом себестоимости
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(МатериалыИРаботыВПроизводстве.Регистратор КАК Документ.ВводОстатков).ОтражатьСебестоимость, ИСТИНА)
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(МатериалыИРаботыВПроизводстве.Регистратор КАК Документ.ВводОстатковТоваров).ОтражатьСебестоимость, ИСТИНА)
		|
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ЗаявлениеОВвозеТовары.Ссылка КАК Регистратор,
		|		ЗаявлениеОВвозеТовары.Ссылка.Организация КАК Организация
		|	ИЗ
		|		Документ.ЗаявлениеОВвозеТоваров.Товары КАК ЗаявлениеОВвозеТовары
		|	ГДЕ
		|		&ИспользоватьУчетСебестоимости
		|		И ЗаявлениеОВвозеТовары.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ЗаявлениеОВвозеТовары.Ссылка.Организация В(&МассивОрганизаций)
		|		И ЗаявлениеОВвозеТовары.Ссылка.Проведен
		|		И ЗаявлениеОВвозеТовары.СуммаНДС <> 0
		|		И ЗаявлениеОВвозеТовары.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|		И ТИПЗНАЧЕНИЯ(ЗаявлениеОВвозеТовары.ДокументПоступления) <> ТИП(Документ.ПриобретениеУслугПрочихАктивов)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ВводОстатков.Ссылка КАК Регистратор,
		|		ВводОстатков.Ссылка.Организация КАК Организация
		|	ИЗ
		|		Документ.ВводОстатков.Товары КАК ВводОстатков
		|	ГДЕ
		|		&ИспользоватьУчетСебестоимости
		|		И ВводОстатков.Ссылка.ОтражатьСебестоимость
		|		И НЕ ВводОстатков.Ссылка.ОтражатьВОперативномУчете
		|		И ВводОстатков.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ВводОстатков.Ссылка.Организация В(&МассивОрганизаций)
		|		И ВводОстатков.Ссылка.Проведен
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ВводОстатков.Ссылка КАК Регистратор,
		|		ВводОстатков.Ссылка.Организация КАК Организация
		|	ИЗ
		|		Документ.ВводОстатковТоваров.Товары КАК ВводОстатков
		|	ГДЕ
		|		&ИспользоватьУчетСебестоимости
		|		И ВводОстатков.Ссылка.ОтражатьСебестоимость
		|		И НЕ ВводОстатков.Ссылка.ОтражатьВОперативномУчете
		|		И ВводОстатков.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ВводОстатков.Ссылка.Организация В(&МассивОрганизаций)
		|		И ВводОстатков.Ссылка.Проведен
		|	) КАК Товары
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		// Выбираем документы, у которых есть движения по себестоимости.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СебестоимостьТоваров.Регистратор КАК Регистратор,
		|	СебестоимостьТоваров.Организация КАК Организация
		|
		|ПОМЕСТИТЬ ЕстьДвиженияСебестоимости
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыСДвижениями КАК ДокументыСДвижениями
		|		ПО ДокументыСДвижениями.Регистратор = СебестоимостьТоваров.Регистратор
		|		И ДокументыСДвижениями.Организация = СебестоимостьТоваров.Организация
		|ГДЕ
		|	&ИспользоватьУчетСебестоимости
		|	И СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СебестоимостьТоваров.Организация В(&МассивОрганизаций)
		|	И НЕ СебестоимостьТоваров.РасчетСебестоимости
		|	И НЕ СебестоимостьТоваров.РасчетПартий
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		//Выбираем документы, у которых есть движения по оперативным регистрам, но нет движений по учету себестоимости.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""СебестоимостьТоваров""			КАК ИмяРегистра,
		|	ДокументыСДвижениями.Регистратор	КАК Ссылка,
		|	ДокументыСДвижениями.Организация	КАК Организация,
		|	1									КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	ДокументыСДвижениями КАК ДокументыСДвижениями
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ ЕстьДвиженияСебестоимости КАК ЕстьДвиженияСебестоимости
		|		ПО ЕстьДвиженияСебестоимости.Регистратор = ДокументыСДвижениями.Регистратор
		|		И ЕстьДвиженияСебестоимости.Организация = ДокументыСДвижениями.Организация
		|
		// Исключаем из ошибок возвраты и сторно возвратов, многооборотной тары, операция - "Возврат от комиссионера (2.0)"
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента КАК ВозвратТоваровОтКлиента
		|		ПО ЕстьДвиженияСебестоимости.Регистратор ЕСТЬ NULL
		|			И ДокументыСДвижениями.Регистратор = ВозвратТоваровОтКлиента.Ссылка
		|			И ВозвратТоваровОтКлиента.ВозвратПереданнойМногооборотнойТары
		|			И ВозвратТоваровОтКлиента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера)
		|			
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Сторно КАК Сторно
		|		ПО ЕстьДвиженияСебестоимости.Регистратор ЕСТЬ NULL
		|			И ДокументыСДвижениями.Регистратор = Сторно.Ссылка
		|			И  ТИПЗНАЧЕНИЯ(Сторно.СторнируемыйДокумент) = ТИП(Документ.ВозвратТоваровОтКлиента)
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента КАК СторнированныйВозвратТоваровОтКлиента
		|		ПО Сторно.СторнируемыйДокумент = СторнированныйВозвратТоваровОтКлиента.Ссылка
		|			И СторнированныйВозвратТоваровОтКлиента.ВозвратПереданнойМногооборотнойТары
		|			И СторнированныйВозвратТоваровОтКлиента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера)
		|
		|ГДЕ
		|	&ИспользоватьУчетСебестоимости
		|	И ЕстьДвиженияСебестоимости.Регистратор ЕСТЬ NULL
		|
		// Исключаем из ошибок возвраты и сторно возвратов, многооборотной тары, операция - "Возврат от комиссионера (2.0)"
		|	И ВозвратТоваровОтКлиента.Ссылка ЕСТЬ NULL
		|	И СторнированныйВозвратТоваровОтКлиента.Ссылка ЕСТЬ NULL
		|;
		|УНИЧТОЖИТЬ ДекларацииСНулевымиСуммами;
		|УНИЧТОЖИТЬ ДокументыСДвижениями;
		|УНИЧТОЖИТЬ ЕстьДвиженияСебестоимости
		|";
КонецФункции

#КонецОбласти

#Область ВыключениеУчетаСебестоимостиПоНазначениям

// Выбираем документы, у которых в движениях в аналитике учета номенклатуры указано назначение.
// Необходимо исправить движения в регистрах:
//	- Себестоимость товаров
//	- Выручка и себестоимость продаж
//
// Возвращаемое значение:
//	Строка - текст запроса
//
Функция ТекстЗапросаВыключениеУчетаСебестоимостиПоНазначениям() Экспорт
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""СебестоимостьТоваров""			КАК ИмяРегистра,
		|	СебестоимостьТоваров.Регистратор	КАК Ссылка,
		|	СебестоимостьТоваров.Организация	КАК Организация,
		|	1									КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|ГДЕ
		|	НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
		|	И СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СебестоимостьТоваров.Организация В(&МассивОрганизаций)
		|	И СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	И НЕ СебестоимостьТоваров.РасчетСебестоимости
		|	И НЕ СебестоимостьТоваров.РасчетПартий
		|	И НЕ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		|";
КонецФункции

// Выбираем документы, у которых в движениях в аналитике учета номенклатуры указано назначение.
//
// Возвращаемое значение:
//	Строка - текст запроса
//
Функция ТекстЗапросаВыключениеУчетаВыручкиИСебестоимостиПродажПоНазначениям() Экспорт
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж""			КАК ИмяРегистра,
		|	ВыручкаИСебестоимостьПродаж.Регистратор	КАК Ссылка,
		|	Аналитики.Организация					КАК Организация,
		|	1										КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|ГДЕ
		|	НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
		|	И ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитики.Организация В(&МассивОрганизаций)
		|	И ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	И НЕ ВыручкаИСебестоимостьПродаж.РасчетСебестоимости
		|	И НЕ ВыручкаИСебестоимостьПродаж.РасчетПартий
		|	И НЕ ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		|";
КонецФункции

#КонецОбласти

#Область ВключениеУчетаСебестоимостиПоНазначениям

// Выбираем документы, у которых в движениях по оперативным регистрам указано назначение,
// а в движении учета себестоимости не указано назначение.
// Используются данные следующих оперативных регистров:
//	- Товары организаций
//		кроме таможенных деклараций с нулевыми суммами пошлины и НДС
//		кроме перемещений товаров в статусе "Отгружено"
//		кроме сборок товаров в статусе "В работе"
//	- Товары организаций к передаче
//	- Товары переданные переработчику
//	- Товары полученные от переработчика
//	- Товары к оформлению таможенных деклараций (движения "Приход)
//	- Материалы и работы в производстве (учитываются работы при партионном учете версии 2.2,
//		при партионном учете 2.1 не используется одновременно с онлайн движениями по регистру "Себестоимость товаров")
//		исключаются движения у документов "Маршрутный лист производства", "Списание затрат на выпуск", "Распределение материалов и работ",
//		используемых для производства 2.1, т.к. эти документы не делают онлайн движений по регистру "Себестоимость товаров" 
//		одновременно с регистром "Материалы и работы в производстве".
// Дополнительно к оперативным регистрам выбираем данные непосредственно из документов (у этих документов нет оперативных регистров):
//	- Отчет давальцу
//	- Заявление о ввозе товаров из ЕАЭС с заполненной суммой НДС
//	- Ввод остатков с выключенным флажком "ОУ"
// Исключения:
//	- По товарам, переданным на комиссию, учет по назначениям не ведется
//	- По товаром к оформлению отчетов комитенту учет по назначениям не ведется
//	- По товарам, отгруженным без перехода права собственности, учет по назначениям не ведется
//	- Для движений себестоимости по разделу учета "Незавершенное производство" нет соответствующих оперативных регистров.
//		Но этот раздел учета всегда корреспондирует с другими разделами, которые имеют соответствующие оперативные регистры.
//		Поэтому движения по разделу "Незавершенное производстве" не проверяем. Они будут переформированы одновременно с другими движениями.
//	- Движения документов "Заявление о ввозе товаров из ЕАЭС" по себестоимости не имеют соответствующих оперативных регистров. 
//		Проверяем данные в табличной части документов
//	- Движения документов "Распределение материалов и работ" при распределении материалов по правилу
//	- Движения документов "Исправление развернутого сальдо товаров организаций" если в движениях не меняется вид запасов.
// 		У таких документов не будет движений по регистру "Себестоимость товаров".
//	- Документы корректировки регистров. У таких документов могут быть движения только по оперативным регистрам.
//	- Документы ввода начальных остатков, у которых снят флажок "Себестоимость" (у таких документов нет движений по учету себестоимости)
//
// Возвращаемое значение:
//	Строка - текст запроса
//
Функция ТекстЗапросаВключениеУчетаСебестоимостиПоНазначениям() Экспорт
	Возврат "
		|ВЫБРАТЬ
		|	Ключи.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ АналитикаСНазначениями
		|ИЗ
		|	Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И &ИспользоватьУчетСебестоимости
		|	И Ключи.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		// Выбираем таможенные декларации на импорт, в которых указаны нулевые суммы таможенной пошлины и НДС.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Ссылка КАК Ссылка,
		|	Строки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
		|ПОМЕСТИТЬ ДекларацииСНулевымиСуммами
		|ИЗ
		|	Документ.ТаможеннаяДекларацияИмпорт КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Строки
		|		ПО Строки.Ссылка = ДД.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|		ПО АналитикаСНазначениями.Ссылка = Строки.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Проведен
		|	И Строки.СуммаПошлины = 0
		|	И Строки.СуммаНДС = 0
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка,
		|	АналитикаУчетаНоменклатуры
		|;
		// Выбираем документы, у которых в движениях по оперативным регистрам указано назначение.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.Регистратор КАК Регистратор,
		|	Товары.Организация КАК Организация
		|ПОМЕСТИТЬ ДокументыСНазначениями
		|ИЗ (
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыОрганизаций.Регистратор КАК Регистратор,
		|		ТоварыОрганизаций.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыОрганизаций.АналитикаУчетаНоменклатуры
		// Исключаем таможенные декларации с нулевыми суммами пошлины и НДС
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДекларацииСНулевымиСуммами КАК Декларации
		|			ПО Декларации.Ссылка = ТоварыОрганизаций.Регистратор
		|			И Декларации.АналитикаУчетаНоменклатуры = ТоварыОрганизаций.АналитикаУчетаНоменклатуры
		|			И ТИПЗНАЧЕНИЯ(ТоварыОрганизаций.Регистратор) = ТИП(Документ.ТаможеннаяДекларацияИмпорт)
		// Исключаем перемещения товаров в статусе "Отгружено"
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров КАК Перемещение
		|			ПО Перемещение.Ссылка = ТоварыОрганизаций.Регистратор
		|			И Перемещение.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПеремещенийТоваров.Отгружено)
		// Исключаем сторно перемещения товаров в статусе "Отгружено"
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Сторно КАК СторноПеремещенияТоваров
		|			ПО СторноПеремещенияТоваров.Ссылка = ТоварыОрганизаций.Регистратор
		|			И СторноПеремещенияТоваров.СторнируемыйДокумент ССЫЛКА Документ.ПеремещениеТоваров
		|			И ВЫРАЗИТЬ(СторноПеремещенияТоваров.СторнируемыйДокумент КАК Документ.ПеремещениеТоваров).Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПеремещенийТоваров.Отгружено)
		// Исключаем сборки товаров в статусе "В работе"
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СборкаТоваров КАК Сборка
		|			ПО Сборка.Ссылка = ТоварыОрганизаций.Регистратор
		|			И Сборка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.ВРаботе)
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыОрганизаций.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыОрганизаций.Организация В(&МассивОрганизаций)
		|		И ТИПЗНАЧЕНИЯ(ТоварыОрганизаций.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
		// Исключаем движения документов производства 2.2, для которых нет онлайн движений по регистру "Себестоимость товаров"
		|		И ТоварыОрганизаций.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовНаПартииПроизводства)
		// Исключаем движения документов "Исправление развернутого сальдо товаров организаций" если в движениях не меняется вид запасов.
		// У таких документов не будет движений по регистру "Себестоимость товаров".
		|		И НЕ (
		|			ТоварыОрганизаций.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОбособленногоУчета)
		|			И ТоварыОрганизаций.ВидЗапасов = ТоварыОрганизаций.КорВидЗапасов
		|			И ТИПЗНАЧЕНИЯ(ТоварыОрганизаций.Регистратор) <> ТИП(Документ.КорректировкаНазначенияТоваров))
		// Исключаем таможенные декларации с нулевыми суммами пошлины и НДС
		|		И Декларации.Ссылка ЕСТЬ NULL
		// Исключаем вводы остатков с выключенным учетом себестоимости
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ВводОстатков).ОтражатьСебестоимость, ИСТИНА)
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ВводОстатковТоваров).ОтражатьСебестоимость, ИСТИНА)
		// Исключаем перемещения товаров в статусе "Отгружено"
		|		И Перемещение.Ссылка ЕСТЬ NULL
		// Исключаем сторно перемещения товаров в статусе "Отгружено"
		|		И СторноПеремещенияТоваров.Ссылка ЕСТЬ NULL
		// Исключаем сборки товаров в статусе "В работе"
		|		И Сборка.Ссылка ЕСТЬ NULL
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыОрганизацийКПередаче.Регистратор КАК Регистратор,
		|		ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизацийКПередаче КАК ТоварыОрганизацийКПередаче
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыОрганизацийКПередаче.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыОрганизацийКПередаче.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация В(&МассивОрганизаций)
		|		И ТИПЗНАЧЕНИЯ(ТоварыОрганизацийКПередаче.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
		|
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыКОформлениюДокументовИмпорта.Регистратор КАК Регистратор,
		|		ТоварыКОформлениюДокументовИмпорта.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыКОформлениюДокументовИмпорта КАК ТоварыКОформлениюДокументовИмпорта
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыКОформлениюДокументовИмпорта.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыКОформлениюДокументовИмпорта.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыКОформлениюДокументовИмпорта.Организация В(&МассивОрганизаций)
		|		И ТоварыКОформлениюДокументовИмпорта.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И ТИПЗНАЧЕНИЯ(ТоварыКОформлениюДокументовИмпорта.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
		|		И ТоварыКОформлениюДокументовИмпорта.ТипДокументаИмпорта = &ТипДокументаИмпорта
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		МатериалыИРаботыВПроизводстве.Регистратор КАК Регистратор,
		|		МатериалыИРаботыВПроизводстве.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.МатериалыИРаботыВПроизводстве КАК МатериалыИРаботыВПроизводстве
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И МатериалыИРаботыВПроизводстве.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И МатериалыИРаботыВПроизводстве.Организация В(&МассивОрганизаций)
		|		И МатериалыИРаботыВПроизводстве.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		И МатериалыИРаботыВПроизводстве.Количество <> 0
		|		И НЕ ТИПЗНАЧЕНИЯ(МатериалыИРаботыВПроизводстве.Регистратор) В (
		|			ТИП(Документ.КорректировкаРегистров),
		|			ТИП(Документ.РасчетСебестоимостиТоваров))
		// Исключаем вводы остатков с выключенным учетом себестоимости
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(МатериалыИРаботыВПроизводстве.Регистратор КАК Документ.ВводОстатков).ОтражатьСебестоимость, ИСТИНА)
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(МатериалыИРаботыВПроизводстве.Регистратор КАК Документ.ВводОстатковТоваров).ОтражатьСебестоимость, ИСТИНА)
		|
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ЗаявлениеОВвозеТовары.Ссылка КАК Регистратор,
		|		ЗаявлениеОВвозеТовары.Ссылка.Организация КАК Организация
		|	ИЗ
		|		Документ.ЗаявлениеОВвозеТоваров.Товары КАК ЗаявлениеОВвозеТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ЗаявлениеОВвозеТовары.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ЗаявлениеОВвозеТовары.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ЗаявлениеОВвозеТовары.Ссылка.Организация В(&МассивОрганизаций)
		|		И ЗаявлениеОВвозеТовары.Ссылка.Проведен
		|		И ЗаявлениеОВвозеТовары.СуммаНДС <> 0
		|		И ТИПЗНАЧЕНИЯ(ЗаявлениеОВвозеТовары.ДокументПоступления) <> ТИП(Документ.ПриобретениеУслугПрочихАктивов)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ВводОстатков.Ссылка КАК Регистратор,
		|		ВводОстатков.Ссылка.Организация КАК Организация
		|	ИЗ
		|		Документ.ВводОстатков.Товары КАК ВводОстатков
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ВводОстатков.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И ВводОстатков.Ссылка.ОтражатьСебестоимость
		|		И НЕ ВводОстатков.Ссылка.ОтражатьВОперативномУчете
		|		И ВводОстатков.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ВводОстатков.Ссылка.Организация В(&МассивОрганизаций)
		|		И ВводОстатков.Ссылка.Проведен
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ВводОстатков.Ссылка КАК Регистратор,
		|		ВводОстатков.Ссылка.Организация КАК Организация
		|	ИЗ
		|		Документ.ВводОстатковТоваров.Товары КАК ВводОстатков
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ВводОстатков.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И ВводОстатков.Ссылка.ОтражатьСебестоимость
		|		И НЕ ВводОстатков.Ссылка.ОтражатьВОперативномУчете
		|		И ВводОстатков.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ВводОстатков.Ссылка.Организация В(&МассивОрганизаций)
		|		И ВводОстатков.Ссылка.Проведен
		|	) КАК Товары
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		// Выбираем документы, у которых в движениях по себестоимости указано назначение.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СебестоимостьТоваров.Регистратор КАК Регистратор,
		|	СебестоимостьТоваров.Организация КАК Организация
		|
		|ПОМЕСТИТЬ ДвиженияСебестоимостиСНазначениями
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыСНазначениями КАК ДокументыСНазначениями
		|		ПО ДокументыСНазначениями.Регистратор = СебестоимостьТоваров.Регистратор
		|		И ДокументыСНазначениями.Организация = СебестоимостьТоваров.Организация
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|		ПО АналитикаСНазначениями.Ссылка = СебестоимостьТоваров.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СебестоимостьТоваров.Организация В(&МассивОрганизаций)
		|	И НЕ СебестоимостьТоваров.РасчетСебестоимости
		|	И НЕ СебестоимостьТоваров.РасчетПартий
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СебестоимостьТоваров.Регистратор КАК Регистратор,
		|	СебестоимостьТоваров.Организация КАК Организация
		|
		|ПОМЕСТИТЬ ЕстьДвиженияСебестоимости
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыСНазначениями КАК ДокументыСНазначениями
		|		ПО ДокументыСНазначениями.Регистратор = СебестоимостьТоваров.Регистратор
		|		И ДокументыСНазначениями.Организация = СебестоимостьТоваров.Организация
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СебестоимостьТоваров.Организация В(&МассивОрганизаций)
		|	И НЕ СебестоимостьТоваров.РасчетСебестоимости
		|	И НЕ СебестоимостьТоваров.РасчетПартий
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		// Выбираем документы, у которых в движениях по оперативным регистрам указано назначение,
		// а в движениях учета себестоимости не указано назначение (нет движений с указанным назначением).
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""СебестоимостьТоваров""			КАК ИмяРегистра,
		|	ДокументыСНазначениями.Регистратор	КАК Ссылка,
		|	ДокументыСНазначениями.Организация	КАК Организация,
		|	1									КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	ДокументыСНазначениями КАК ДокументыСНазначениями
		// Выполняем проверерку только при наличии движений по себестоимости товаров. 
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЕстьДвиженияСебестоимости КАК ЕстьДвиженияСебестоимости
		|		ПО ЕстьДвиженияСебестоимости.Регистратор = ДокументыСНазначениями.Регистратор
		|		И ЕстьДвиженияСебестоимости.Организация = ДокументыСНазначениями.Организация
		|	ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияСебестоимостиСНазначениями КАК СебестоимостьТоваров
		|		ПО СебестоимостьТоваров.Регистратор = ДокументыСНазначениями.Регистратор
		|		И СебестоимостьТоваров.Организация = ДокументыСНазначениями.Организация
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И СебестоимостьТоваров.Регистратор ЕСТЬ NULL
		|;
		|УНИЧТОЖИТЬ АналитикаСНазначениями;
		|УНИЧТОЖИТЬ ДекларацииСНулевымиСуммами;
		|УНИЧТОЖИТЬ ДокументыСНазначениями;
		|УНИЧТОЖИТЬ ЕстьДвиженияСебестоимости;
		|УНИЧТОЖИТЬ ДвиженияСебестоимостиСНазначениями
		|";
КонецФункции

// Выбираем документы, у которых в движениях в аналитике учета номенклатуры не указано назначение.
//
// Возвращаемое значение:
//	Строка - текст запроса
//
Функция ТекстЗапросаВключениеУчетаВыручкиИСебестоимостиПродажПоНазначениям() Экспорт
	Возврат "
		|ВЫБРАТЬ
		|	Ключи.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ АналитикаСНазначениями
		|ИЗ
		|	Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И &ИспользоватьУчетСебестоимости
		|	И Ключи.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВыручкаИСебестоимостьПродаж.Регистратор КАК Регистратор,
		|	Аналитики.Организация КАК Организация
		|ПОМЕСТИТЬ ДокументыПродаж
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК ДокументРеализации
		|		ПО ДокументРеализации.Ссылка = ВыручкаИСебестоимостьПродаж.Регистратор
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И &ИспользоватьУчетСебестоимости
		|	И ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитики.Организация В (&МассивОрганизаций)
		|	И ЕСТЬNULL(ДокументРеализации.ХозяйственнаяОперация, НЕОПРЕДЕЛЕНО)
		|		<> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
		|	И ЕСТЬNULL(ДокументРеализации.ХозяйственнаяОперация, НЕОПРЕДЕЛЕНО)
		|		<> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности)
		|;
		// Выбираем документы продаж, у которых в движениях по оперативным регистрам указано назначение.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.Регистратор КАК Регистратор,
		|	Товары.Организация КАК Организация
		|ПОМЕСТИТЬ ДокументыСНазначениями
		|ИЗ (
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыОрганизаций.Регистратор КАК Регистратор,
		|		ТоварыОрганизаций.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыПродаж КАК ДокументыПродаж
		|			ПО ДокументыПродаж.Регистратор = ТоварыОрганизаций.Регистратор
		|			И ДокументыПродаж.Организация = ТоварыОрганизаций.Организация
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыОрганизаций.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыОрганизаций.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыОрганизаций.Организация В(&МассивОрганизаций)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыОрганизацийКПередаче.Регистратор КАК Регистратор,
		|		ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизацийКПередаче КАК ТоварыОрганизацийКПередаче
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыПродаж КАК ДокументыПродаж
		|			ПО ДокументыПродаж.Регистратор = ТоварыОрганизацийКПередаче.Регистратор
		|			И ДокументыПродаж.Организация = ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыОрганизацийКПередаче.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыОрганизацийКПередаче.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация В(&МассивОрганизаций)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		МатериалыИРаботыВПроизводстве.Регистратор КАК Регистратор,
		|		МатериалыИРаботыВПроизводстве.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.МатериалыИРаботыВПроизводстве КАК МатериалыИРаботыВПроизводстве
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыПродаж КАК ДокументыПродаж
		|			ПО ДокументыПродаж.Регистратор = МатериалыИРаботыВПроизводстве.Регистратор
		|			И ДокументыПродаж.Организация = МатериалыИРаботыВПроизводстве.Организация
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И МатериалыИРаботыВПроизводстве.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И МатериалыИРаботыВПроизводстве.Организация В(&МассивОрганизаций)
		|		И МатериалыИРаботыВПроизводстве.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	) КАК Товары
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		// Выбираем документы, у которых в движениях по выручке и себестоимости продаж указано назначение.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВыручкаИСебестоимостьПродаж.Регистратор КАК Регистратор,
		|	Аналитики.Организация КАК Организация
		|
		|ПОМЕСТИТЬ ДвиженияСебестоимостиСНазначениями
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыСНазначениями КАК ДокументыСНазначениями
		|		ПО ДокументыСНазначениями.Регистратор = ВыручкаИСебестоимостьПродаж.Регистратор
		|		И ДокументыСНазначениями.Организация = Аналитики.Организация
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|		ПО АналитикаСНазначениями.Ссылка = ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитики.Организация В(&МассивОрганизаций)
		|	И ВыручкаИСебестоимостьПродаж.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
		|	И ВыручкаИСебестоимостьПродаж.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности)
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		// Выбираем документы, у которых в движениях по оперативным регистрам указано назначение,
		// а в движении выручки и себестоимости продаж не указано назначение (нет движений с указанным назначением).
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж""		КАК ИмяРегистра,
		|	ДокументыСНазначениями.Регистратор	КАК Ссылка,
		|	ДокументыСНазначениями.Организация	КАК Организация,
		|	1									КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	ДокументыСНазначениями КАК ДокументыСНазначениями
		|	ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияСебестоимостиСНазначениями КАК СебестоимостьТоваров
		|		ПО СебестоимостьТоваров.Регистратор = ДокументыСНазначениями.Регистратор
		|		И СебестоимостьТоваров.Организация = ДокументыСНазначениями.Организация
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И СебестоимостьТоваров.Регистратор ЕСТЬ NULL
		|;
		|УНИЧТОЖИТЬ АналитикаСНазначениями;
		|УНИЧТОЖИТЬ ДокументыПродаж;
		|УНИЧТОЖИТЬ ДокументыСНазначениями;
		|УНИЧТОЖИТЬ ДвиженияСебестоимостиСНазначениями
		|";
КонецФункции

#КонецОбласти

#КонецОбласти
