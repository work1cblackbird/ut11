#Область ПрограммныйИнтерфейс

// Возвращает фактический адрес торгового объекта для чека ЕГАИС.
//
// Параметры:
//  Адрес          - Строка                              - адрес торгового объекта.
//  ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС - ссылка на торговый объект для определения адреса.
Процедура АдресТорговогоОбъекта(Адрес, ТорговыйОбъект) Экспорт
	
	//++ НЕ ГОСИС
	Адрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ТорговыйОбъект, Справочники.ВидыКонтактнойИнформации.АдресСклада);
	//-- НЕ ГОСИС
	
	Возврат;
	
КонецПроцедуры

// В функции нужно определить значения по умолчанию, которые будут подставляться в
// реквизиты не сопоставленных элементов справочника.
//
// Параметры:
//  СобственнаяОрганизация - ОпределяемыйТип.ОрганизацияКонтрагентГосИС - значение по умолчанию для собственной организации,
//  СобственныйТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС - значение по умолчанию для собственного торгового объекта,
//  СторонняяОрганизация - ОпределяемыйТип.ОрганизацияКонтрагентГосИС - значение по умолчанию для сторонней организации,
//  СтороннийТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС - значение по умолчанию для стороннего торгового объекта.
Процедура ЗначенияПоУмолчаниюНеСопоставленныхОбъектов(
		СобственнаяОрганизация,
		СобственныйТорговыйОбъект,
		СторонняяОрганизация      = Неопределено,
		СтороннийТорговыйОбъект   = Неопределено) Экспорт
	
	//++ НЕ ГОСИС
	СобственнаяОрганизация    = Справочники.Организации.ПустаяСсылка();
	СобственныйТорговыйОбъект = Справочники.Склады.ПустаяСсылка();
	СторонняяОрганизация      = Справочники.Контрагенты.ПустаяСсылка();
	СтороннийТорговыйОбъект   = Справочники.Партнеры.ПустаяСсылка();
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

// Процедура вызывается перед записью организации в базу.
//
// Параметры:
//  Организация - СправочникОбъект.КлассификаторОрганизацийЕГАИС - загружаемая организация.
Процедура ПриЗагрузкеОрганизации(Организация) Экспорт
	
	//++ НЕ ГОСИС
	Если ЗначениеЗаполнено(Организация.ИНН) И Не Организация.Сопоставлено Тогда
		РезультатПоиска = ПартнерыИКонтрагентыЛокализация.ИННКППУжеИспользуетсяВИнформационнойБазе(Организация.ИНН, Организация.КПП);
		
		Если РезультатПоиска.НайденныеСовпадения.Количество() > 0  Тогда
			
			Организация.Контрагент     = РезультатПоиска.НайденныеСовпадения[0].Ссылка;
			Организация.ТорговыйОбъект = РезультатПоиска.НайденныеСовпадения[0].Партнер;
			Организация.Сопоставлено   = Истина;
		
		КонецЕсли
		
	КонецЕсли;
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

#КонецОбласти