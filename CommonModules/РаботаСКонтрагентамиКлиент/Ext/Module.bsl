///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Обрабатывает стандартным способом ошибку, возвращенную в поле ОписаниеОшибки
// одним из методов:
// - РаботаСКонтрагентами.ЮридическиеЛицаПоНаименованию;
// - РаботаСКонтрагентами.РеквизитыПредпринимателяПоИНН;
// - РаботаСКонтрагентами.СведенияОЮридическомЛицеПоИНН;
// - РаботаСКонтрагентами.ИнформацияОСвязяхЮридическогоЛицаПоИНН;
// - РаботаСКонтрагентами.ИнформацияОСвязяхПредпринимателяПоИНН;
// - РаботаСКонтрагентами.ИнформацияОПроверкахКонтролирующимиОрганамиПоСпискуИНН;
// - РаботаСКонтрагентами.РеквизитыНалоговогоОрганаПоКоду;
// - РаботаСКонтрагентами.РеквизитыОтделенияФССПоКоду;
// - РаботаСКонтрагентами.РеквизитыОтделенияПФРПоКоду;
//
// Параметры:
//	ОписаниеОшибки - Строка - полученное описание ошибки;
//	ОбработчикЗавершения - ОписаниеОповещения - обработчик, вызываемый
//		при завершении обработки ошибки. В обработчик передается
//		значение типа Структура с полями:
//		*ПовторитьДействие - Булево - если Истина, пользователь выполнил
//			действия, необходимые для исправления ошибки и можно повторно
//			вызвать выбранный метод;
//	ДополнительныеПараметры - Структура - дополнительные параметры
//		обработки ошибки. Поля:
//		*ПредставлениеДействия - Строка - представление выполняемого действия
//			в сообщениях пользователю;
//		*ИдентификаторМестаВызова - Строка - произвольный идентификатор
//			функциональности конфигурации;
//		*Форма - ФормаКлиентскогоПриложения - форма, в которой вызывается функциональность.
//			Используется как владелец форм, открываемых в режиме
//			"Блокировать окно владельца".
//
Процедура ОбработатьОшибку(
	ОписаниеОшибки,
	ОбработчикЗавершения = Неопределено,
	ДополнительныеПараметры = Неопределено) Экспорт
	
	ПредставлениеДействия    = Неопределено;
	ИдентификаторМестаВызова = Неопределено;
	Форма                    = Неопределено;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
		ПредставлениеДействия    = ДополнительныеПараметры.ПредставлениеДействия;
		ИдентификаторМестаВызова = ДополнительныеПараметры.ИдентификаторМестаВызова;
		Форма                    = ДополнительныеПараметры.Форма;
	КонецЕсли;
	
	ДополнительныеПараметрыОбработчиковЗавершения = Новый Структура;
	ДополнительныеПараметрыОбработчиковЗавершения.Вставить("ОбработчикЗавершения", ОбработчикЗавершения);
	ДополнительныеПараметрыОбработчиковЗавершения.Вставить("Форма"               , Форма);
	
	Если ОписаниеОшибки = "Сервис1СКонтрагентНеПодключен" Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ИдентификаторМестаВызова", ИдентификаторМестаВызова);
		
		Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПодключениеСервисовСопровождения") Тогда
			
			ОбработчикПриЗакрытииФормы = Новый ОписаниеОповещения(
				"ФормаСервисКонтрагентНеПодключенПриЗакрытии",
				ЭтотОбъект,
				ДополнительныеПараметрыОбработчиковЗавершения);
			
			ПодключитьТестовыйПериод(
				ПараметрыФормы,
				Форма,
				ДополнительныеПараметрыОбработчиковЗавершения,
				ОбработчикПриЗакрытииФормы);
		Иначе
			ОткрытьФорму(
				"ОбщаяФорма.Сервис1СКонтрагентНеПодключен",
				ПараметрыФормы,
				Форма,
				,
				,
				,
				ОбработчикПриЗакрытииФормы);
		КонецЕсли;
		
		Возврат;
		
	ИначеЕсли ОписаниеОшибки = "НеУказаныПараметрыАутентификации"
		Или ОписаниеОшибки = "НеУказанПароль" Тогда
		
		Если Не ЗначениеЗаполнено(ПредставлениеДействия) Тогда
			ТекстСообщения = НСтр("ru = 'Для продолжения необходимо подключить Интернет-поддержку пользователей.'");
		Иначе
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 станет возможным после подключения Интернет-поддержки пользователей.'"),
				ПредставлениеДействия);
		КонецЕсли;
		
		Если ИнтернетПоддержкаПользователейКлиент.ДоступноПодключениеИнтернетПоддержки() Тогда
			
			ОбработчикЗавершения = Новый ОписаниеОповещения(
				"ПриОтветеНаВопросПодключитьИнтернетПоддержкуОбработатьОшибку",
				ЭтотОбъект,
				ДополнительныеПараметрыОбработчиковЗавершения);
			ПоказатьВопрос(
				ОбработчикЗавершения,
				ТекстСообщения + Символы.ПС + НСтр("ru = 'Подключить Интернет-поддержку?'"),
				РежимДиалогаВопрос.ДаНет);
			
		Иначе
			
			ОбработчикЗавершения = Новый ОписаниеОповещения(
				"ПредупреждениеТекстОшибкиЗавершение",
				ЭтотОбъект,
				ДополнительныеПараметрыОбработчиковЗавершения);
			ПоказатьПредупреждение(ОбработчикЗавершения, ТекстСообщения + Символы.ПС + НСтр("ru = 'Обратитесь к администратору.'"));
			
		КонецЕсли;
		
	Иначе
		
		Если ОписаниеОшибки = "ОшибкаПроверкиИНН" Тогда
			ТекстПредупреждения  = НСтр("ru = 'Поле ИНН должно иметь длину 10 или 12 цифр'");
		ИначеЕсли ОписаниеОшибки = "ОшибкаПроверкиНаименования" Тогда
			ТекстПредупреждения  = НСтр("ru = 'Поле Наименование является обязательным.'");
		ИначеЕсли ОписаниеОшибки = "ОшибкаПроверкиКодаРегиона" Тогда
			ТекстПредупреждения  = НСтр("ru = 'Поле Код региона должно состоять из двух цифр.'");
		ИначеЕсли ОписаниеОшибки = "ОшибкаАутентификацииВСервисе" Тогда
			ТекстПредупреждения  = НСтр("ru = 'Ошибка аутентификации в сервисе. Обратитесь к администратору.'");
		ИначеЕсли ОписаниеОшибки = "ПревышеноКоличествоПопыток" Тогда
			ТекстПредупреждения  = НСтр("ru = 'Превышено количество попыток. Повторите попытку позже.'");
		ИначеЕсли ОписаниеОшибки = "ОшибкаПроверкиКПП" Тогда
			ТекстПредупреждения  = НСтр("ru = 'Поле КПП должно иметь длину 9 цифр.'");
		ИначеЕсли ОписаниеОшибки = "ОшибкаПроверкиУникальногоНомера" Тогда
			ТекстПредупреждения  = НСтр("ru = 'Уникальный номер передан некорректно.'");
		ИначеЕсли ОписаниеОшибки = "НеизвестнаяОшибка" Тогда
			ТекстПредупреждения  = НСтр("ru = 'Неизвестная ошибка при подключении к сервису.'");
		Иначе
			ТекстПредупреждения  = ОписаниеОшибки;
		КонецЕсли;
		
		ОбработчикЗавершения = Новый ОписаниеОповещения(
			"ПредупреждениеТекстОшибкиЗавершение",
			ЭтотОбъект,
			ДополнительныеПараметрыОбработчиковЗавершения);
		ПоказатьПредупреждение(ОбработчикЗавершения, ТекстПредупреждения);
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает структуру дополнительных параметров для метода ОбработатьОшибку().
//
// Возвращаемое значение:
//	Структура - дополнительные параметры. Поля:
//		*ПредставлениеДействия - Строка - представление выполняемого действия
//			в сообщениях пользователю;
//		*ИдентификаторМестаВызова - Строка - произвольный идентификатор
//			функциональности конфигурации;
//		*Форма - ФормаКлиентскогоПриложения - форма, в которой вызывается функциональность.
//			Используется как владелец форм, открываемых в режиме
//			"Блокировать окно владельца".
//
Функция НовыйДополнительныеПараметрыОбработкиОшибки() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ПредставлениеДействия"   , "");
	Результат.Вставить("ИдентификаторМестаВызова", "");
	Результат.Вставить("Форма"                   , Неопределено);
	
	Возврат Результат;
	
КонецФункции

// Возвращает структуру параметров для подбора контрагентов.
//
// Возвращаемое значение:
//  Структура - параметры.
//    *Заголовок - Строка - заголовок формы;
//    *СтрокаПоиска - Строка - ИНН или наименование контрагента;
//    *Регион - Строка - регион контрагента;
//    *Адрес - Строка - адрес контрагента;
//    *РасширенныйРезультатПодбора - Булево - признак расширенного подбора.
//         Когда Ложь, в результате подбора будет возвращаться строка ИНН,
//         Когда Истина - Структура с полями ИНН, УникальныйНомер, Идентификатор.
//
Функция НовыйПараметрыПоискаКонтрагента() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Заголовок");
	Результат.Вставить("СтрокаПоиска");
	Результат.Вставить("Регион");
	Результат.Вставить("Адрес");
	Результат.Вставить("РасширенныйРезультатПодбора", Истина);
	
	Возврат Результат;
	
КонецФункции

// Открывает форму заполнения контрагента из данных ЕГРЮЛ.
//
// Параметры:
//  ПараметрыПоиска - Структура - параметры поиска контрагента. См НовыйПараметрыПоискаКонтрагента
//    *Заголовок - Строка - заголовок формы;
//    *СтрокаПоиска - Строка - ИНН или наименование контрагента;
//    *Регион - Строка - регион контрагента;
//    *Адрес - Строка - адрес контрагента;
//  ФормаВладелец - ФормаКлиентскогоПриложения
//                - Неопределено - форма, из которой открывается подбор.
//  ОписаниеОповещения - ОписаниеОповещения - оповещение, которое необходимо выполнить после выбора контрагента.
//   В результате подбора может быть передана строка ИНН или структура с данными контрагента:
//    *ИНН - Строка - ИНН контрагента;
//    *ЭтоПодразделение - Булево - признак обособленного подразделения;
//    *Идентификатор  - Строка - идентификатор подразделения.
//
Процедура ОткрытьПодборКонтрагента(ПараметрыПоиска, ФормаВладелец, ОписаниеОповещения) Экспорт
	
	ОткрытьФорму(
		"ОбщаяФорма.ЗаполнениеРеквизитовКонтрагента",
		ПараметрыПоиска, ФормаВладелец, , , , ОписаниеОповещения);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПредупреждениеТекстОшибкиЗавершение(ДополнительныеПараметры) Экспорт
	
	ОповеститьОЗавершенииОбработкиОшибки(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПодключитьТестовыйПериод(
		ПараметрыФормы,
		Владелец,
		ДополнительныеПараметрыОбработчиков,
		ОбработчикПриЗакрытииФормы = Неопределено) Экспорт
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПодключениеСервисовСопровождения") Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПараметрыФормы",                      ПараметрыФормы);
		ДополнительныеПараметры.Вставить("Владелец",                            Владелец);
		ДополнительныеПараметры.Вставить("ОбработчикПриЗакрытииФормы",          ОбработчикПриЗакрытииФормы);
		ДополнительныеПараметры.Вставить("ДополнительныеПараметрыОбработчиков", ДополнительныеПараметрыОбработчиков);
		
		ОповещениеПриЗавершенииПодключения = Новый ОписаниеОповещения(
			"ЗавершениеПодключенияТестовогоПериода",
			ЭтотОбъект,
			ДополнительныеПараметры);
		
		МодульПодключениеСервисовСопровожденияКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
			"ПодключениеСервисовСопровожденияКлиент");
		
		МодульПодключениеСервисовСопровожденияКлиент.ПодключитьТестовыйПериод(
			"1C-Counteragent",
			Владелец,
			ОповещениеПриЗавершенииПодключения);
		
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму(
		"ОбщаяФорма.Сервис1СКонтрагентНеПодключен",
		ПараметрыФормы,
		Владелец);
	
КонецПроцедуры

Процедура ЗавершениеПодключенияТестовогоПериода(Результат, ДополнительныеПараметры) Экспорт
	
	МодульПодключениеСервисовСопровожденияКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
		"ПодключениеСервисовСопровожденияКлиент");
	
	Если Результат = МодульПодключениеСервисовСопровожденияКлиент.СостояниеПодключенияПодключениеНедоступно() Тогда
		ОткрытьФорму(
			"ОбщаяФорма.Сервис1СКонтрагентНеПодключен",
			ДополнительныеПараметры.ПараметрыФормы,
			ДополнительныеПараметры.Владелец,
			,
			,
			,
			ДополнительныеПараметры.ОбработчикПриЗакрытииФормы);
	ИначеЕсли Результат = МодульПодключениеСервисовСопровожденияКлиент.СостояниеПодключенияПодключен() Тогда
		ОповеститьОЗавершенииОбработкиОшибки(
			ДополнительныеПараметры.ДополнительныеПараметрыОбработчиков,
			Истина);
	ИначеЕсли Результат = МодульПодключениеСервисовСопровожденияКлиент.СостояниеПодключенияНеПодключен() Тогда
		ОповеститьОЗавершенииОбработкиОшибки(
			ДополнительныеПараметры.ДополнительныеПараметрыОбработчиков,
			Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ФормаСервисКонтрагентНеПодключенПриЗакрытии(Результат, ДополнительныеПараметры) Экспорт
	
	ОповеститьОЗавершенииОбработкиОшибки(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПриОтветеНаВопросПодключитьИнтернетПоддержкуОбработатьОшибку(КодВозврата, ДополнительныеПараметры) Экспорт
	
	Если КодВозврата <> КодВозвратаДиалога.Да Тогда
		
		ОповеститьОЗавершенииОбработкиОшибки(ДополнительныеПараметры);
		
	Иначе
		
		ОбработчикЗавершения = Новый ОписаниеОповещения(
			"ОбработатьОшибкуПодключитьИнтернетПоддержкуЗавершение",
			ЭтотОбъект,
			ДополнительныеПараметры);
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(
			ОбработчикЗавершения,
			ДополнительныеПараметры.Форма);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОшибкуПодключитьИнтернетПоддержкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ОповеститьОЗавершенииОбработкиОшибки(ДополнительныеПараметры, (Результат <> Неопределено));
	
КонецПроцедуры

Процедура ОповеститьОЗавершенииОбработкиОшибки(ДополнительныеПараметрыОбработчика, ПовторитьДействие = Ложь)
	
	Если ТипЗнч(ДополнительныеПараметрыОбработчика) = Тип("Структура")
		И ДополнительныеПараметрыОбработчика.ОбработчикЗавершения <> Неопределено Тогда
		Результат = Новый Структура;
		Результат.Вставить("ПовторитьДействие", ПовторитьДействие);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметрыОбработчика.ОбработчикЗавершения, Результат);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
