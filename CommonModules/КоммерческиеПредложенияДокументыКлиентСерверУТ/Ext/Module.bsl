
////////////////////////////////////////////////////////////////////////////////
// Подсистема "Объекты УТ11, КА2, УП2".
// ОбщийМодуль.КоммерческиеПредложенияДокументыКлиентСерверУТ.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// См. КоммерческиеПредложенияДокументыКлиентСерверПереопределяемый.СформироватьПредставлениеСостоянияПоСтатусу
//
// Параметры:
//  Форма        - ФормаКлиентскогоПриложения.
//  Статус       - ОпределяемыйТип.СтатусыКоммерческихПредложенийКлиентам.
//  ТекущаяДата  - Дата.
//
Процедура СформироватьПредставлениеСостоянияПоСтатусу(Форма, Статус, ТекущаяДата) Экспорт
	
	Состояние = "";
	СрокДействия = Форма.Объект.СрокДействия;
	
	Если Статус = ПредопределенноеЗначение("Перечисление.СтатусыКоммерческихПредложенийКлиентам.НеСогласовано") Тогда
		Состояние = НСтр("ru = 'На согласовании.'");
	ИначеЕсли Статус = ПредопределенноеЗначение("Перечисление.СтатусыКоммерческихПредложенийКлиентам.Черновик") Тогда
		Состояние = НСтр("ru = 'Подготавливается.'");
	ИначеЕсли Статус = ПредопределенноеЗначение("Перечисление.СтатусыКоммерческихПредложенийКлиентам.Отменено") Тогда
		Состояние = НСтр("ru = 'Отменено.'");
	ИначеЕсли Статус = ПредопределенноеЗначение("Перечисление.СтатусыКоммерческихПредложенийКлиентам.Действует") Тогда
		
		Если СрокДействия >= ТекущаяДата Тогда
			Состояние = НСтр("ru = 'Действует.'");
		Иначе
			Состояние = НСтр("ru = 'Срок действия окончен.'");
		КонецЕсли;
		
	КонецЕсли;
	
	ЭлементФормыСостояние = Форма.Элементы.Состояние; // ДекорацияФормы 
	ЭлементФормыСостояние.Заголовок = Состояние;
	
КонецПроцедуры

// См. КоммерческиеПредложенияДокументыКлиентСерверПереопределяемый.СформироватьПредставлениеНоменклатурыДляПечати
//
Процедура СформироватьПредставлениеНоменклатурыДляПечати(ПредставлениеДляПечати, Номенклатура, Характеристика, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ПредставлениеДляПечати = КоммерческиеПредложенияДокументыУТВызовСервера.ПредставлениеНоменклатурыДляПечати(Номенклатура, Характеристика);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Параметры:
//  Налогообложение - Булево -
//
// Возвращаемое значение:
//   ПеречислениеСсылка.ТипыНалогообложенияНДС - 
//
Функция НалогообложениеНДСПоНалогообложениюКоммерческихПредложений(Налогообложение) Экспорт

	Если Налогообложение Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС");
	Иначе
		Возврат ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС");
	КонецЕсли;

КонецФункции

Функция НалогообложениеКоммерческихПредложенийПоНалогообложениюНДС(НалогообложениеНДС) Экспорт

	Если НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС")
		Или НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД") Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;

КонецФункции

Функция ХозяйственнаяОперацияДокументаКоммерческоеПредложениеКлиенту(Операция) Экспорт
	
	Если Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийКоммерческихПредложений.ЗакупкаУПоставщика") Тогда
		
		ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.РеализацияКлиенту");
		
	Иначе
		
		ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию");
		
	КонецЕсли;
	
	Возврат ХозяйственнаяОперация;
	
КонецФункции

Функция МассивОтборовПоТипуНоменклатуры(ВидОперации) Экспорт
	
	Если ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийКоммерческихПредложений.ЗакупкаУПоставщика") Тогда
		МассивОтборов = НоменклатураКлиентСервер.ОтборПоТоваруМногооборотнойТареУслугеРаботе(Истина);
	Иначе
		МассивОтборов = НоменклатураКлиентСервер.ОтборПоТоваруМногооборотнойТареРаботе(Истина);
	КонецЕсли;
	
	РезультатПоиска = МассивОтборов.Найти(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.МногооборотнаяТара"));
	
	Если РезультатПоиска <> Неопределено Тогда
		МассивОтборов.Удалить(РезультатПоиска);
	КонецЕсли;
	
	Возврат МассивОтборов;
	
КонецФункции

#КонецОбласти
