
#Область СлужебныйПрограммныйИнтерфейс

// Только для внутреннего использования.
Функция НоваяСтруктураСписокВходящихДокументов(ДанныеОбработки, ТекстОшибки)
	
	Результат = Новый Структура;
	Результат.Вставить("ДанныеОбработки",             ДанныеОбработки);
	Результат.Вставить("ТекстОшибки",                 ТекстОшибки);
	
	Возврат Результат;
	
КонецФункции

// Только для внутреннего использования.
// Вызывается из: ИнтеграцияЕГАИСКлиент.ПроверитьВходящиеДокументы.
//
// Параметры:
//  Результат - См. НоваяСтруктураСписокВходящихДокументов
//  ДополнительныеПараметры - Структура - Дополнительные параметры.
//
Процедура ПроверитьВходящиеДокументы_ОбработатьСписокВходящихДокументов(Результат, ДополнительныеПараметры) Экспорт
	
	ТекстОшибки = Результат.ТекстОшибки;
	
	Для Каждого СтрокаТЧ Из Результат.ДанныеОбработки Цикл
		ДополнительныеПараметры.АдресаURLВходящихДокументов.Добавить(СтрокаТЧ);
	КонецЦикла;
	
	Результат = НоваяСтруктураСписокВходящихДокументов(
		ДополнительныеПараметры.АдресаURLВходящихДокументов,
		ТекстОшибки);
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПриЗавершении, Результат);
	
КонецПроцедуры

#Область ГлобальныеОбработчикиОбмена

// Только для внутреннего использования.
// Вызывается из: ПослеЗавершенияОбмена
Процедура ОткрытьРезультатВыполненияОбмена(ДополнительныеПараметры) Экспорт
	
	Если ДополнительныеПараметры.Изменения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.РезультатВыполненияОбменаЕГАИС", ДополнительныеПараметры);
	
КонецПроцедуры

// Только для внутреннего использования.
// Вызывается из: ОповещениеПослеЗавершенииОбмена
Процедура ПослеЗавершенияОбмена(Изменения, ДополнительныеПараметры) Экспорт
	
	СоответствиеДокументыОснования  = Новый Соответствие;
	СоответствиеДокументыСтатусы    = Новый Соответствие;
	СоответствиеИзмененныеДокументы = Новый Соответствие;
	
	Для Каждого ЭлементДанных Из Изменения Цикл
		
		Если ЗначениеЗаполнено(ЭлементДанных.ТекстОшибки) Тогда
			ОбщегоНазначенияИСКлиент.СообщитьПользователюВФорму(ДополнительныеПараметры.ИдентификаторВладельца, ЭлементДанных.ТекстОшибки);
		КонецЕсли;
		
		СоответствиеДокументыОснования.Вставить(ЭлементДанных.Объект, ЭлементДанных.ДокументОснование);
		СоответствиеДокументыСтатусы.Вставить(ЭлементДанных.Объект, ЭлементДанных.НовыйСтатус);
		Если ЭлементДанных.ОбъектИзменен Тогда
			СоответствиеИзмененныеДокументы.Вставить(ЭлементДанных.Объект, Истина);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого КлючИЗначение Из СоответствиеДокументыОснования Цикл
		
		ОбъектИзменен = СоответствиеИзмененныеДокументы.Получить(КлючИЗначение.Ключ);
		Если ОбъектИзменен = Неопределено Тогда
			ОбъектИзменен = Ложь;
		КонецЕсли;
		
		ПараметрОповещения = Новый Структура;
		ПараметрОповещения.Вставить("Ссылка",        КлючИЗначение.Ключ);
		ПараметрОповещения.Вставить("Основание",     КлючИЗначение.Значение);
		ПараметрОповещения.Вставить("ОбъектИзменен", ОбъектИзменен);
		
		Оповестить("ИзменениеСостоянияЕГАИС", ПараметрОповещения);
		
	КонецЦикла;
	
	Если ТипЗнч(ДополнительныеПараметры.Контекст) = Тип("ТаблицаФормы") Тогда
		
		// Выполнено действие из динамического списка
		ТекстСообщения = СтрШаблон(
			НСтр("ru='Для %1 из %2 выделенных в списке документов выполнено действие: %3'"),
			СоответствиеДокументыСтатусы.Количество(),
			ДополнительныеПараметры.Контекст.ВыделенныеСтроки.Количество(),
			ДополнительныеПараметры.ДальнейшееДействие);
			
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Выполнено действие'"),,
			ТекстСообщения,
			БиблиотекаКартинок.Информация32ГосИС);
		
	ИначеЕсли ЗначениеЗаполнено(ДополнительныеПараметры.Контекст) Тогда
		
		// Выполнено действие из формы документа
		Для Каждого КлючИЗначение Из СоответствиеДокументыСтатусы Цикл
			
			Если КлючИЗначение.Значение = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru='Для документа %1 изменен статус ЕГАИС: %2.'"),
				КлючИЗначение.Ключ,
				КлючИЗначение.Значение);
			
			ПоказатьОповещениеПользователя(
				НСтр("ru = 'Выполнено действие'"),
				ПолучитьНавигационнуюСсылку(КлючИЗначение.Ключ),
				ТекстСообщения,
				БиблиотекаКартинок.Информация32ГосИС);
			
		КонецЦикла;
		
	Иначе
		
		// Выполнен обмен с ЕГАИС
		ДополнительныеПараметрыОповещения = Новый Структура;
		ДополнительныеПараметрыОповещения.Вставить("СоответствиеДокументыОснования", СоответствиеДокументыОснования);
		ДополнительныеПараметрыОповещения.Вставить("СоответствиеДокументыСтатусы",   СоответствиеДокументыСтатусы);
		ДополнительныеПараметрыОповещения.Вставить("Изменения",                      Изменения);
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Изменено объектов: %1'"), СоответствиеДокументыСтатусы.Количество());
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Выполнен обмен с ЕГАИС'"),
			Новый ОписаниеОповещения("ОткрытьРезультатВыполненияОбмена", ОбменДаннымиЕГАИССлужебныйКлиент, ДополнительныеПараметрыОповещения),
			ТекстСообщения,
			БиблиотекаКартинок.Информация32ГосИС);
		
	КонецЕсли;
	
	ПараметрыПриложения.Удалить("ЕГАИС.ДанныеСеансаОбмена");
	
	Если ДополнительныеПараметры.ОповещениеПриЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПриЗавершении, Изменения);
	КонецЕсли;
	
КонецПроцедуры

//Только для внутреннего использования.
//
//Параметры:
//  ОповещениеПриЗавершении            - ОписаниеОповещения - процедура которую надо выполнить после передачи всех данных в УТМ
//  ДанныеДляВыполненияОбменаНаКлиенте - Структура          - данные для выполнения обмена.
//
Процедура ОбработатьОчередьПередачиДанных(ОповещениеПриЗавершении, ДанныеДляВыполненияОбменаНаКлиенте) Экспорт
	
	СообщенияXMLКПередаче = ДанныеДляВыполненияОбменаНаКлиенте.СообщенияXMLКПередаче;
	
	ИдетОбменСЕГАИС = ПараметрыПриложения["ЕГАИС.ДанныеСеансаОбмена"];
	Если ИдетОбменСЕГАИС <> Неопределено Тогда 
		Для Каждого ЭлементСообщение Из СообщенияXMLКПередаче Цикл 
			ДанныеДляВыполненияОбменаНаКлиенте.Изменения.Добавить(ЭлементСообщение);
		КонецЦикла;
		СообщенияXMLКПередаче.Очистить();
	КонецЕсли;
	
	Если СообщенияXMLКПередаче.Количество() > 0 Тогда
		
		ДополнительныеПараметры = ОбщиеПараметрыОбменаНаКлиенте(ДанныеДляВыполненияОбменаНаКлиенте);
		ДополнительныеПараметры.ОповещениеПриЗавершении = ОповещениеПриЗавершении;
		ДополнительныеПараметры.ВОсновнойФорме          = ДанныеДляВыполненияОбменаНаКлиенте.Свойство("ВОсновнойФорме");
		ДополнительныеПараметры.Вставить("РезультатыПередачиСообщенийПоОрганизациямЕГАИС", Новый Соответствие);
		
		ПередатьСообщения(СообщенияXMLКПередаче, ДополнительныеПараметры);
		
	Иначе
		
		Если ОповещениеПриЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, ДанныеДляВыполненияОбменаНаКлиенте.Изменения);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Только для внутреннего использования.
Процедура ОбработатьСписокВходящихДокументов(ДанныеОбработкиТекстОшибки, Контекст) Экспорт
	
	ДополнительныеПараметры = ОбщиеПараметрыОбменаНаКлиенте(Контекст);
	ДополнительныеПараметры.Вставить("ДокументыКЗагрузке", Новый Соответствие);
	
	ПолучитьСообщения(ДанныеОбработкиТекстОшибки.ДанныеОбработки, ДополнительныеПараметры);
	
КонецПроцедуры

// Только для внутреннего использования.
Процедура ПослеОбработкиОчередиПередачиДанных(Изменения, Контекст) Экспорт
	
	ПараметрыОбработкиСпискаВходящихДокументов = ОбщиеПараметрыОбменаНаКлиенте(Контекст);
	ПараметрыОбработкиСпискаВходящихДокументов.Изменения = Изменения;
	
	ОповещениеПриЗавершенииПолученияСпискаВходящихДокументов = Новый ОписаниеОповещения(
		"ОбработатьСписокВходящихДокументов",
		ОбменДаннымиЕГАИССлужебныйКлиент,
		ПараметрыОбработкиСпискаВходящихДокументов);
	
	ДополнительныеПараметры = ОбщиеПараметрыОбменаНаКлиенте(Контекст);
	ДополнительныеПараметры.Изменения = Изменения;
	ДополнительныеПараметры.ОповещениеПриЗавершении = ОповещениеПриЗавершенииПолученияСпискаВходящихДокументов;
	ДополнительныеПараметры.Вставить("Результат",   Новый Соответствие);
	ДополнительныеПараметры.Вставить("ТекстОшибки", "");
	
	ОрганизацииЕГАИС = ОбменДаннымиЕГАИСКлиентСервер.ОрганизацииЕГАИС(Контекст.НастройкиОбменаЕГАИС);
	
	ПолучитьСпискиВходящихСообщений(ОрганизацииЕГАИС, ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти

Процедура ОбработатьОчереднуюПорциюДанных() Экспорт
	ВыполнитьОбработкуОповещения(ПараметрыПриложения["ЕГАИС.ДанныеСеансаОбмена"].ОбработчикОповещения);
КонецПроцедуры

#Область ПередачаДанных

// Только для внутреннего использования.
//
// Запускает очередь обмена с УТМ ЕГАИС (через глобальную переменную ПараметрыПриложения["ЕГАИС.ДанныеСеансаОбмена"]), передача данных
Процедура ПередатьСообщения(СообщенияXML, Контекст)
	
	Если ОбменУжеЗапущен(Контекст) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПриложения.Вставить("ЕГАИС.ДанныеСеансаОбмена",
		СохраняемыеДанныеОбмена(
			СообщенияXML,
			Контекст,
			Новый ОписаниеОповещения("ПередатьСообщение", ОбменДаннымиЕГАИССлужебныйКлиент),
			Новый ОписаниеОповещения("ПослеПередачиСообщений", ОбменДаннымиЕГАИССлужебныйКлиент)));
	ПодключитьОбработчикОжидания("ОбработчикОжиданияОбменаОчереднойПорциейДанныхЕГАИС", 0.1, Истина);
	
КонецПроцедуры

// Только для внутреннего использования.
//
// Выгружает очередной элемент очереди в УТМ
Процедура ПередатьСообщение(Результат, ДополнительныеПараметры) Экспорт
	
	ДанныеОбменаЕГАИС = ПараметрыПриложения["ЕГАИС.ДанныеСеансаОбмена"];
	Если ДанныеОбменаЕГАИС.СообщенийXML > ДанныеОбменаЕГАИС.НомерКОбработке Тогда
		
		ПорцияДанных = ДанныеОбменаЕГАИС.СообщенияXML[ДанныеОбменаЕГАИС.НомерКОбработке];
		НастройкаОбменаНаКлиенте = ДанныеОбменаЕГАИС.Контекст.НастройкиОбменаЕГАИС.Получить(ПорцияДанных.ОрганизацияЕГАИС);
		
		ПолеФормы = Новый Структура;
		ПолеФормы.Вставить("ИмяПоля",  "xml_file");
		ПолеФормы.Вставить("ИмяФайла", "data.xml");
		ПолеФормы.Вставить("Тип",      "text/xml; charset=utf-8");
		ПолеФормы.Вставить("Тело",     ПорцияДанных.ТекстСообщенияXML);
		
		ПоляФормы = Новый Массив;
		ПоляФормы.Добавить(ПолеФормы);
		
		ДанныеПреобразования = ОбменДаннымиИСКлиентСервер.ДвоичныеДанныеPOSTЗапросаКакФорма(ПоляФормы);
		ТелоЗапроса = ДанныеПреобразования.ДвоичныеДанные;
		Размер      = ДанныеПреобразования.Размер;
		
		ЗаголовкиHTTP = Новый Соответствие;
		ЗаголовкиHTTP.Вставить("Content-Type",   "multipart/form-data; boundary=" + ДанныеПреобразования.Разделитель);
		ЗаголовкиHTTP.Вставить("Accept-Charset", "utf-8");
		ЗаголовкиHTTP.Вставить("Content-Lenght", Формат(Размер, "ЧН=0; ЧГ=0;"));
		
		ПараметрыHTTPЗапроса = ОбменДаннымиЕГАИСКлиентСервер.ПараметрыHTTPЗапроса(
			ТелоЗапроса, ЗаголовкиHTTP,
			ПорцияДанных.АдресЗапроса);
		
		ОтправитьHTTPЗапрос(
			Новый ОписаниеОповещения("ПослеПередачиСообщения", ОбменДаннымиЕГАИССлужебныйКлиент),
			НастройкаОбменаНаКлиенте,
			ПараметрыHTTPЗапроса);
		
	Иначе
		
		ВыполнитьОбработкуОповещения(ДанныеОбменаЕГАИС.ДействиеПриЗавершении);
		
	КонецЕсли;
	
КонецПроцедуры

// Только для внутреннего использования.
//
// Оповещение после передачи очередного документа
Процедура ПослеПередачиСообщения(РезультатОтправкиHTTPЗапроса, Контекст) Экспорт
	
	ДанныеОбменаЕГАИС = ПараметрыПриложения["ЕГАИС.ДанныеСеансаОбмена"];
	ПорцияДанных = ДанныеОбменаЕГАИС.СообщенияXML[ДанныеОбменаЕГАИС.НомерКОбработке];
	ОрганизацияЕГАИС = ПорцияДанных.ОрганизацияЕГАИС;
	
	ЭлементДанных = Новый Структура;
	ЭлементДанных.Вставить("ИсходящееСообщение",           ПорцияДанных.Ссылка);
	ЭлементДанных.Вставить("РезультатОтправкиHTTPЗапроса", ОбработатьРезультатОтправкиHTTPЗапроса(РезультатОтправкиHTTPЗапроса));
	
	Если ДанныеОбменаЕГАИС.Результат.Получить(ОрганизацияЕГАИС) = Неопределено Тогда
		ДанныеОбменаЕГАИС.Результат.Вставить(ОрганизацияЕГАИС, Новый Массив);
	КонецЕсли;
	
	ДанныеОбменаЕГАИС.Результат[ОрганизацияЕГАИС].Добавить(ЭлементДанных);
	
	ДанныеОбменаЕГАИС.НомерКОбработке = ДанныеОбменаЕГАИС.НомерКОбработке + 1;
	ПодключитьОбработчикОжидания("ОбработчикОжиданияОбменаОчереднойПорциейДанныхЕГАИС", 0.1, Истина);
	
КонецПроцедуры

// Только для внутреннего использования.
//
// Действия после выгрузки очереди в УТМ
Процедура ПослеПередачиСообщений(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	ДанныеОбменаЕГАИС = ПараметрыПриложения["ЕГАИС.ДанныеСеансаОбмена"];
	ЗакрытьФормуВыполненияОбмена(ДанныеОбменаЕГАИС.Контекст.Форма);
	Если ДанныеОбменаЕГАИС.Результат.Количество() = 0 Тогда
		ПараметрОповещения = ДанныеОбменаЕГАИС.Результат;
	Иначе
		ПараметрОповещения = ОбменДаннымиЕГАИСВызовСервера.ПриЗавершенииПередачиДанных(ДанныеОбменаЕГАИС.Результат);
	КонецЕсли;
	ОповещениеПриЗавершении = ДанныеОбменаЕГАИС.Контекст.ОповещениеПриЗавершении;
	ПараметрыПриложения.Удалить("ЕГАИС.ДанныеСеансаОбмена");
	
	Если ОповещениеПриЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, ПараметрОповещения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаСписковДокументов

// Только для внутреннего использования.
//
// Запускает очередь обмена с УТМ ЕГАИС (через глобальную переменную ПараметрыПриложения["ЕГАИС.ДанныеСеансаОбмена"]), получение списков входящих документов
Процедура ПолучитьСпискиВходящихСообщений(ОрганизацииЕГАИС, Контекст) Экспорт
	
	Если ОбменУжеЗапущен(Контекст) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПриложения.Вставить("ЕГАИС.ДанныеСеансаОбмена", 
		СохраняемыеДанныеОбмена(
			ОрганизацииЕГАИС,
			Контекст,
			Новый ОписаниеОповещения("ПолучитьДокументыПоОчереднойОрганизацииИзОчередиОбмена", ОбменДаннымиЕГАИССлужебныйКлиент),
			Новый ОписаниеОповещения("ПослеПолученияСписковСообщений", ОбменДаннымиЕГАИССлужебныйКлиент)));
	ПодключитьОбработчикОжидания("ОбработчикОжиданияОбменаОчереднойПорциейДанныхЕГАИС", 0.1, Истина);
	
КонецПроцедуры

// Только для внутреннего использования.
//
// Получает список документов по очередной организации из очереди обмена
Процедура ПолучитьДокументыПоОчереднойОрганизацииИзОчередиОбмена(Результат, ДополнительныеПараметры) Экспорт
	
	ДанныеОбменаЕГАИС = ПараметрыПриложения["ЕГАИС.ДанныеСеансаОбмена"];
	Если ДанныеОбменаЕГАИС.СообщенийXML > ДанныеОбменаЕГАИС.НомерКОбработке Тогда
		ПорцияДанных = ДанныеОбменаЕГАИС.СообщенияXML[ДанныеОбменаЕГАИС.НомерКОбработке];
		
		ОбменДаннымиЕГАИСКлиент.ОбновитьДатуПоследнегоЗапускаОбменаНаКлиентеПоРасписанию(ПорцияДанных);
		
		НастройкаОбменаНаКлиенте = ДанныеОбменаЕГАИС.Контекст.НастройкиОбменаЕГАИС.Получить(ПорцияДанных);
		Если НастройкаОбменаНаКлиенте.ЗагружатьВходящиеДокументы Тогда
			
			ПараметрыHTTPЗапроса = ОбменДаннымиЕГАИСКлиентСервер.СтруктураДанныхHTTPЗапроса("GET", "/opt/out");
			
			ОтправитьHTTPЗапрос(
				Новый ОписаниеОповещения("ПослеПолученияСписка", ОбменДаннымиЕГАИССлужебныйКлиент),
				НастройкаОбменаНаКлиенте,
				ПараметрыHTTPЗапроса);
			
		Иначе
			
			ДанныеОбменаЕГАИС.НомерКОбработке = ДанныеОбменаЕГАИС.НомерКОбработке + 1;
			ПодключитьОбработчикОжидания("ОбработчикОжиданияОбменаОчереднойПорциейДанныхЕГАИС", 0.1, Истина);
			
		КонецЕсли;
		
	Иначе
		
		ВыполнитьОбработкуОповещения(ДанныеОбменаЕГАИС.ДействиеПриЗавершении);
		
	КонецЕсли;
	
КонецПроцедуры

// Только для внутреннего использования.
//
// Оповещение после получения списка входящих документов очередной организации
Процедура ПослеПолученияСписка(РезультатОтправкиHTTPЗапроса, Контекст) Экспорт
	
	ДанныеОбменаЕГАИС = ПараметрыПриложения["ЕГАИС.ДанныеСеансаОбмена"];
	ОрганизацияЕГАИС = ДанныеОбменаЕГАИС.СообщенияXML[ДанныеОбменаЕГАИС.НомерКОбработке];
	
	РезультатОперации = ОбработатьРезультатОтправкиHTTPЗапроса(РезультатОтправкиHTTPЗапроса);
	
	Если РезультатОперации.ТекстСообщенияXMLПолучен Тогда
		
		Если ДанныеОбменаЕГАИС.Результат.Получить(ОрганизацияЕГАИС) = Неопределено Тогда
			ДанныеОбменаЕГАИС.Результат.Вставить(ОрганизацияЕГАИС, Новый Массив);
		КонецЕсли;
		
		ДанныеОбменаЕГАИС.Результат[ОрганизацияЕГАИС].Добавить(РезультатОперации.ТекстВходящегоСообщенияXML);
		
	Иначе
		
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Не удалось получить список входящих документов организации %1:
			           |По причине:
			           |%2'"),
			ОрганизацияЕГАИС,
			РезультатОперации.ТекстОшибки);
		
		ОбщегоНазначенияИСКлиент.СообщитьПользователюВФорму(ДанныеОбменаЕГАИС.Контекст.ИдентификаторВладельца, ТекстОшибки);
		
		Если ЗначениеЗаполнено(ДанныеОбменаЕГАИС.ТекстОшибки) Тогда
			ДанныеОбменаЕГАИС.ТекстОшибки = ДанныеОбменаЕГАИС.ТекстОшибки
			                              + Символы.ПС + ТекстОшибки;
		Иначе
			ДанныеОбменаЕГАИС.ТекстОшибки = ТекстОшибки;
		КонецЕсли;
		
	КонецЕсли;
	
	ДанныеОбменаЕГАИС.НомерКОбработке = ДанныеОбменаЕГАИС.НомерКОбработке + 1;
	ПодключитьОбработчикОжидания("ОбработчикОжиданияОбменаОчереднойПорциейДанныхЕГАИС", 0.1, Истина);
	
КонецПроцедуры

// Только для внутреннего использования.
//
// Действия после загрузки списков сообщений из УТМ
Процедура ПослеПолученияСписковСообщений(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	ДанныеОбменаЕГАИС = ПараметрыПриложения["ЕГАИС.ДанныеСеансаОбмена"];
	ЗакрытьФормуВыполненияОбмена(ДанныеОбменаЕГАИС.Контекст.Форма);
	
	#Если ВебКлиент Тогда
		ДанныеОбработки = ОбменДаннымиЕГАИСВызовСервера.ОбработатьОтветНаЗапросПолученияДокументов(ДанныеОбменаЕГАИС.Результат);
	#Иначе
		ДанныеОбработки = ОбменДаннымиЕГАИСКлиентСервер.ОбработатьОтветНаЗапросПолученияДокументов(ДанныеОбменаЕГАИС.Результат);
	#КонецЕсли
	
	Результат = НоваяСтруктураСписокВходящихДокументов(
		ДанныеОбработки,
		ДанныеОбменаЕГАИС.Контекст.ТекстОшибки);
	
	Если ЗначениеЗаполнено(ДанныеОбменаЕГАИС.ТекстОшибки) Тогда
		ОбщегоНазначенияИСКлиент.СообщитьПользователюВФорму(
			ДанныеОбменаЕГАИС.Контекст.ИдентификаторВладельца,
			ДанныеОбменаЕГАИС.ТекстОшибки);
	КонецЕсли;
	
	ОповещениеПриЗавершении = ДанныеОбменаЕГАИС.Контекст.ОповещениеПриЗавершении;
	ПараметрыПриложения.Удалить("ЕГАИС.ДанныеСеансаОбмена");
	
	ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, Результат);
	
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаДокументов

// Только для внутреннего использования.
//
// Запускает очередь обмена с УТМ ЕГАИС (через глобальную переменную ПараметрыПриложения["ЕГАИС.ДанныеСеансаОбмена"]), получение входящих документов
Процедура ПолучитьСообщения(ДанныеДокументовКПолучению, Контекст) Экспорт
	
	Если ОбменУжеЗапущен(Контекст) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПриложения.Вставить("ЕГАИС.ДанныеСеансаОбмена",
		СохраняемыеДанныеОбмена(
			ДанныеДокументовКПолучению,
			Контекст,
			Новый ОписаниеОповещения("ПолучитьСообщение", ОбменДаннымиЕГАИССлужебныйКлиент),
			Новый ОписаниеОповещения("ПослеПолученияСообщений", ОбменДаннымиЕГАИССлужебныйКлиент)));
	ПодключитьОбработчикОжидания("ОбработчикОжиданияОбменаОчереднойПорциейДанныхЕГАИС", 0.1, Истина);
	
КонецПроцедуры

// Только для внутреннего использования.
//
// Получает очередной документ из очереди обмена
Процедура ПолучитьСообщение(Результат, ДополнительныеПараметры) Экспорт
	
	ДанныеОбменаЕГАИС = ПараметрыПриложения["ЕГАИС.ДанныеСеансаОбмена"];
	Если ДанныеОбменаЕГАИС.СообщенийXML > ДанныеОбменаЕГАИС.НомерКОбработке Тогда
		ПорцияДанных = ДанныеОбменаЕГАИС.СообщенияXML[ДанныеОбменаЕГАИС.НомерКОбработке];
		НастройкаОбменаНаКлиенте = ДанныеОбменаЕГАИС.Контекст.НастройкиОбменаЕГАИС.Получить(ПорцияДанных.ОрганизацияЕГАИС);
		
		СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(ПорцияДанных.АдресURL);
		
		ПараметрыHTTPЗапроса = ОбменДаннымиЕГАИСКлиентСервер.СтруктураДанныхHTTPЗапроса("GET", СтруктураURI.ПутьНаСервере);
		
		ОтправитьHTTPЗапрос(
			Новый ОписаниеОповещения("ПослеПолученияСообщения", ОбменДаннымиЕГАИССлужебныйКлиент),
			НастройкаОбменаНаКлиенте,
			ПараметрыHTTPЗапроса);
	Иначе
		
		ВыполнитьОбработкуОповещения(ДанныеОбменаЕГАИС.ДействиеПриЗавершении);
		
	КонецЕсли;
	
КонецПроцедуры

// Только для внутреннего использования.
//
// Оповещение после получения очередного документа
Процедура ПослеПолученияСообщения(РезультатОтправкиHTTPЗапроса, Контекст) Экспорт
	
	ДанныеОбменаЕГАИС = ПараметрыПриложения["ЕГАИС.ДанныеСеансаОбмена"];
	ПорцияДанных = ДанныеОбменаЕГАИС.СообщенияXML[ДанныеОбменаЕГАИС.НомерКОбработке];
	ОрганизацияЕГАИС = ПорцияДанных.ОрганизацияЕГАИС;
	РезультатОперации = ОбработатьРезультатОтправкиHTTPЗапроса(РезультатОтправкиHTTPЗапроса);
	
	Если РезультатОперации.ТекстСообщенияXMLПолучен Тогда
		
		Если ДанныеОбменаЕГАИС.Результат.Получить(ОрганизацияЕГАИС) = Неопределено Тогда
			ДанныеОбменаЕГАИС.Результат.Вставить(ОрганизацияЕГАИС, Новый Массив);
		КонецЕсли;
		
		ДанныеОбменаЕГАИС.Результат[ОрганизацияЕГАИС].Добавить(
			ОбменДаннымиЕГАИСКлиентСервер.СтруктураЗагрузкиВходящегоДокумента(
				ОрганизацияЕГАИС,
				ПорцияДанных.ИдентификаторЗапроса,
				ПорцияДанных.АдресURL,
				РезультатОперации.ТекстВходящегоСообщенияXML));
		
	КонецЕсли;
	
	ДанныеОбменаЕГАИС.НомерКОбработке = ДанныеОбменаЕГАИС.НомерКОбработке + 1;
	ПодключитьОбработчикОжидания("ОбработчикОжиданияОбменаОчереднойПорциейДанныхЕГАИС", 0.1, Истина);
	
КонецПроцедуры

// Только для внутреннего использования.
//
// Действия после загрузки документов из УТМ
Процедура ПослеПолученияСообщений(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	ДанныеОбменаЕГАИС = ПараметрыПриложения["ЕГАИС.ДанныеСеансаОбмена"];
	ЗакрытьФормуВыполненияОбмена(ДанныеОбменаЕГАИС.Контекст.Форма);
	Контекст = ДанныеОбменаЕГАИС.Контекст;
	Результат = ДанныеОбменаЕГАИС.Результат;
	
	ПараметрыПриложения.Удалить("ЕГАИС.ДанныеСеансаОбмена");
	
	Если Результат.Количество() = 0 Тогда
		
		Если Контекст.ОповещениеПриЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(Контекст.ОповещениеПриЗавершении, Контекст.Изменения);
		КонецЕсли;
		
	Иначе
		
		Результат = ОбменДаннымиЕГАИСВызовСервера.ОбработатьВходящиеДокументы(Результат, Контекст.ИдентификаторВладельца);
		
		ДокументыКУдалению = Новый Массив;
		Для Каждого ЭлементДанных Из Результат.Изменения Цикл
			
			Контекст.Изменения.Добавить(ЭлементДанных);
			
			Для Каждого Данные Из ЭлементДанных.СлужебныеДанные Цикл
				ДокументыКУдалению.Добавить(Данные);
			КонецЦикла;
			
		КонецЦикла;
		
		ДополнительныеПараметры = ОбщиеПараметрыОбменаНаКлиенте(Контекст);
		
		ПодтвердитьЗагруженныеСообщения(ДокументыКУдалению, ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область УдалениеЗагруженныхДокументов

// Только для внутреннего использования.
//
// Запускает очередь обмена с УТМ ЕГАИС (через глобальную переменную ПараметрыПриложения["ЕГАИС.ДанныеСеансаОбмена"]), удаление полученных документов
Процедура ПодтвердитьЗагруженныеСообщения(ДанныеДокументовКУдалению, Контекст)
	
	Если ОбменУжеЗапущен(Контекст) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПриложения.Вставить("ЕГАИС.ДанныеСеансаОбмена",
		СохраняемыеДанныеОбмена(
			ДанныеДокументовКУдалению,
			Контекст,
			Новый ОписаниеОповещения("ПодтвердитьСообщение", ОбменДаннымиЕГАИССлужебныйКлиент),
			Новый ОписаниеОповещения("ПослеПодтвержденияСообщений", ОбменДаннымиЕГАИССлужебныйКлиент)));
	ПодключитьОбработчикОжидания("ОбработчикОжиданияОбменаОчереднойПорциейДанныхЕГАИС", 0.1, Истина);
	
КонецПроцедуры

// Только для внутреннего использования.
//
// Удаляет очередной загруженный документ из очереди обмена
Процедура ПодтвердитьСообщение(Результат, ДополнительныеПараметры) Экспорт
	
	ДанныеОбменаЕГАИС = ПараметрыПриложения["ЕГАИС.ДанныеСеансаОбмена"];
	Если ДанныеОбменаЕГАИС.СообщенийXML > ДанныеОбменаЕГАИС.НомерКОбработке Тогда
		
		ПорцияДанных = ДанныеОбменаЕГАИС.СообщенияXML[ДанныеОбменаЕГАИС.НомерКОбработке];
		НастройкаОбменаНаКлиенте = ДанныеОбменаЕГАИС.Контекст.НастройкиОбменаЕГАИС.Получить(ПорцияДанных.ОрганизацияЕГАИС);
		СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(ПорцияДанных.АдресЗапроса);
		ПараметрыHTTPЗапроса = ОбменДаннымиЕГАИСКлиентСервер.СтруктураДанныхHTTPЗапроса("DELETE", СтруктураURI.ПутьНаСервере);
		
		ОтправитьHTTPЗапрос(
			Новый ОписаниеОповещения("ПослеПодтвержденияСообщения", ОбменДаннымиЕГАИССлужебныйКлиент),
			НастройкаОбменаНаКлиенте,
			ПараметрыHTTPЗапроса);
		
	Иначе
		
		ВыполнитьОбработкуОповещения(ДанныеОбменаЕГАИС.ДействиеПриЗавершении);
		
	КонецЕсли;
	
КонецПроцедуры

// Только для внутреннего использования.
//
// Оповещение после удаления очередного документа
Процедура ПослеПодтвержденияСообщения(РезультатОтправкиHTTPЗапроса, Контекст) Экспорт
	
	ДанныеОбменаЕГАИС = ПараметрыПриложения["ЕГАИС.ДанныеСеансаОбмена"];
	
	//результат удаления не контролируется все равно
	ОбработатьРезультатОтправкиHTTPЗапроса(РезультатОтправкиHTTPЗапроса);
	
	ДанныеОбменаЕГАИС.НомерКОбработке = ДанныеОбменаЕГАИС.НомерКОбработке + 1;
	ПодключитьОбработчикОжидания("ОбработчикОжиданияОбменаОчереднойПорциейДанныхЕГАИС", 0.1, Истина);
	
КонецПроцедуры

// Только для внутреннего использования.
//
// Действия после подтверждения получения сообщений
Процедура ПослеПодтвержденияСообщений(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	ДанныеОбменаЕГАИС = ПараметрыПриложения["ЕГАИС.ДанныеСеансаОбмена"];
	ЗакрытьФормуВыполненияОбмена(ДанныеОбменаЕГАИС.Контекст.Форма);
	ОповещениеПриЗавершении = ДанныеОбменаЕГАИС.Контекст.ОповещениеПриЗавершении;
	Изменения = ДанныеОбменаЕГАИС.Контекст.Изменения;
	ПараметрыПриложения.Удалить("ЕГАИС.ДанныеСеансаОбмена");
	
	Если ОповещениеПриЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, Изменения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Только для внутреннего использования.
//
// Параметры:
//  ПакетыОбмена - Массив Из Структура - массив данных для обмена
//  Контекст     - Структура - контекст вызова (прочие сохраняемые значения)
// 
// Возвращаемое значение:
//  Структура - значения которые будут сохраняться в глобальной переменной на клиенте до окончания обработки всего пакета
//
Функция СохраняемыеДанныеОбмена(ПакетыОбмена, Контекст, ОбработчикОповещения, ДействиеПриЗавершении) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("СообщенияXML",          ПакетыОбмена);
	Результат.Вставить("Контекст",              Контекст);
	Результат.Вставить("СообщенийXML",          ПакетыОбмена.Количество());
	Результат.Вставить("НомерКОбработке",       0);
	Результат.Вставить("Результат",             Новый Соответствие);
	Результат.Вставить("ТекстОшибки",           "");
	Результат.Вставить("ОбработчикОповещения",  ОбработчикОповещения);
	Результат.Вставить("ДействиеПриЗавершении", ДействиеПриЗавершении);
	
	Возврат Результат;
	
КонецФункции

// Только для внутреннего использования.
Асинх Процедура ОтправитьHTTPЗапрос(ОповещениеПриЗавершении, НастройкаОбменаНаКлиенте, ПараметрыHTTPЗапроса) Экспорт
	
#Если ВебКлиент Тогда
	
	СистемнаяИнфо = Новый СистемнаяИнформация;
	Если ОбщегоНазначенияКлиентСервер.СравнитьВерсии(СистемнаяИнфо.ВерсияПриложения, "8.3.21.0") < 0 Тогда
		РезультатОтправкиHTTPЗапроса = Новый Структура;
		РезультатОтправкиHTTPЗапроса.Вставить("КодСостояния", 0);
		РезультатОтправкиHTTPЗапроса.Вставить("Заголовки");
		РезультатОтправкиHTTPЗапроса.Вставить("ТекстОтвета", "");
		РезультатОтправкиHTTPЗапроса.Вставить("ТекстОшибки",
			НСтр("ru = 'Обмен с ЕГАИС из вебклиента недоступен для текущей версии платформы. Используйте тонкий клиент.'"));
	Иначе
		РезультатОтправкиHTTPЗапроса = Ждать ОбменДаннымиЕГАИСВебКлиент.ОтправитьHTTPЗапрос(
			НастройкаОбменаНаКлиенте,
			ПараметрыHTTPЗапроса);
	КонецЕсли;
	
#Иначе
	
		РезультатОтправкиHTTPЗапроса = ОбменДаннымиЕГАИСКлиентСервер.ОтправитьHTTPЗапрос(
			НастройкаОбменаНаКлиенте,
			ПараметрыHTTPЗапроса);
	
#КонецЕсли
	
	ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, РезультатОтправкиHTTPЗапроса);
	
КонецПроцедуры

// Только для внутреннего использования.
// 
// Параметры:
//  РезультатОтправкиHTTPЗапроса - см. ОбменДаннымиЕГАИСКлиентСервер.ОтправитьHTTPЗапрос
// 
// Возвращаемое значение:
//  см. ОбменДаннымиЕГАИСВызовСервера.ОбработатьРезультатОтправкиHTTPЗапроса
Функция ОбработатьРезультатОтправкиHTTPЗапроса(РезультатОтправкиHTTPЗапроса) Экспорт
	
	#Если ВебКлиент Тогда
		Возврат ОбменДаннымиЕГАИСВызовСервера.ОбработатьРезультатОтправкиHTTPЗапроса(РезультатОтправкиHTTPЗапроса);
	#Иначе
		Возврат ОбменДаннымиЕГАИСКлиентСервер.ОбработатьРезультатОтправкиHTTPЗапроса(РезультатОтправкиHTTPЗапроса);
	#КонецЕсли
	
КонецФункции

// Только для внутреннего использования.
// 
// Параметры:
//  Контекст - Структура - Контекст:
// * НастройкиОбменаЕГАИС - Неопределено - 
// * Изменения - Неопределено - 
// * ОповещениеПриЗавершении - Неопределено - 
// * ИдентификаторВладельца - Неопределено - 
// * ВОсновнойФорме - Булево - 
// * Результат - Соответствие из КлючИЗначение- 
// * ТекстОшибки - Строка - 
//  ВыводитьСообщения - Булево - Выводить сообщения
// 
// Возвращаемое значение:
//  Булево - Обмен уже запущен
Функция ОбменУжеЗапущен(Контекст, ВыводитьСообщения = Истина) Экспорт
	
	ИдетОбменСЕГАИС = ПараметрыПриложения["ЕГАИС.ДанныеСеансаОбмена"];
	Если ИдетОбменСЕГАИС <> Неопределено Тогда
		Если ВыводитьСообщения Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Обмен с УТМ ЕГАИС уже запущен, дождитесь завершения предыдущего сеанса'"));
		КонецЕсли;
		Возврат Истина;
	КонецЕсли;
	
	Контекст.Вставить("Форма", Неопределено);
	Если Контекст.ВОсновнойФорме = Ложь Тогда
		Контекст.Форма = ОткрытьФорму(
			"ОбщаяФорма.ДлительнаяОперация",
			Новый Структура("ТекстСообщения", НСтр("ru = 'Выполняется обмен с  ЕГАИС'")),,
			"EGAIS3",,,,
			РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	КонецЕсли;
		
	Возврат Ложь;
	
КонецФункции

// Только для внутреннего использования.
Процедура ЗакрытьФормуВыполненияОбмена(Форма) Экспорт
	
	Если Форма <> Неопределено Тогда
		Если Форма.Открыта() Тогда
			Форма.Закрыть();
		КонецЕсли;
		Форма = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

// Только для внутреннего использования.
// 
// Параметры:
//  Источник - Структура - Источник
// 
// Возвращаемое значение:
//  Структура - Общие параметры обмена на клиенте:
// * НастройкиОбменаЕГАИС - Неопределено - 
// * Изменения - Неопределено - 
// * ОповещениеПриЗавершении - Неопределено - 
// * ИдентификаторВладельца - Неопределено - 
// * ВОсновнойФорме - Булево - 
Функция ОбщиеПараметрыОбменаНаКлиенте(Источник) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("НастройкиОбменаЕГАИС",    Неопределено);
	Результат.Вставить("Изменения",               Неопределено);
	Результат.Вставить("ОповещениеПриЗавершении", Неопределено);
	Результат.Вставить("ИдентификаторВладельца",  Неопределено);
	Результат.Вставить("ВОсновнойФорме",          Истина);
	ЗаполнитьЗначенияСвойств(Результат, Источник);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
