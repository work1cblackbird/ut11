////////////////////////////////////////////////////////////////////////////////
// Клиентские процедуры работы с номенклатурой и связанными справочниками
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Формирует наименование элемента справочника по заданному для вида номенклатуры шаблону.
//
// Параметры:
//		ДанныеДляФормированияНаименования - Структура - структура, возвращаемая функцией НоменклатураСервер.ФормулаНаименования
//		ВидНоменклатуры - СправочникСсылка.ВидыНоменклатуры	- вид номенклатуры, в котором задан шаблон наименования
//		ЗначениеНаименования - Строка - в формулу полного наименования может входить наименование. Наименование может быть не
//								заполнено на момент формирования массива ДанныеДляФормированияНаименования.МассивЗначенийРеквизитов,
//								поэтому перед расчетом полного наименования значение наименования нужно обновить.
// Возвращаемое значение:
// Строка - Наименование полученное по алгоритму расчета
// Пустая строка - если не удалось сформировать наименование или не заполнены операнды алгоритма.
//
Функция НаименованиеПоФормуле(ДанныеДляФормированияНаименования, ВидНоменклатуры, ЗначениеНаименования = "") Экспорт
	
	Наименование = "";
	Если ЗначениеЗаполнено(ДанныеДляФормированияНаименования.ФормулаНаименования) Тогда
		
		МассивЗначенийРеквизитов = ДанныеДляФормированияНаименования.МассивЗначенийРеквизитов;
		
		Для Каждого Индекс Из ДанныеДляФормированияНаименования.ИндексыНаименованияВМассивеЗначенийРеквизитов Цикл
			МассивЗначенийРеквизитов[Индекс] = ЗначениеНаименования;
		КонецЦикла;
		
		Попытка
			
			Выполнить("Наименование = """" + " + ДанныеДляФормированияНаименования.ФормулаНаименования);
			
		Исключение
			
			ШаблонСообщенияОбОшибке = НСтр("ru = 'Невозможно рассчитать наименование по заданному для вида номенклатуры ""%ВидНоменклатуры%"" шаблону. Проверьте правильность шаблона.'");
			
			СообщениеОбОшибке = СтрЗаменить(ШаблонСообщенияОбОшибке, "%ВидНоменклатуры%", ВидНоменклатуры);
			ВызватьИсключение СообщениеОбОшибке;
			
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат Наименование;
	
КонецФункции

// Возвращает фильтр, используемый для выбора файлов-изображений.
// Возвращаемое значение:
// Строка - строка, содержащая фильтр для файлов-изображений.
//
Функция ФильтрФайловИзображений() Экспорт
	
	ВсеКартинки		= НСтр("ru = 'Все картинки (*.bmp;*.gif;*.png;*.jpeg;*.dib;*.rle;*.tif;*.jpg;*.ico;*.wmf;*.emf)|*.bmp;*.gif;*.png;*.jpeg;*.dib;*.rle;*.tif;*.jpg;*.ico;*.wmf;*.emf'");
	ВсеФайлы		= НСтр("ru = 'Все файлы(*.*)|*.*'");
	ФорматBMP		= НСтр("ru = 'Формат bmp(*.bmp*;*.dib;*.rle)|*.bmp;*.dib;*.rle'");
	ФорматGIF		= НСтр("ru = 'Формат GIF(*.gif*)|*.gif'");
	ФорматJPEG		= НСтр("ru = 'Формат JPEG(*.jpeg;*.jpg)|*.jpeg;*.jpg'");
	ФорматPNG		= НСтр("ru = 'Формат PNG(*.png*)|*.png'");
	ФорматTIFF		= НСтр("ru = 'Формат TIFF(*.tif)|*.tif'");
	ФорматICON		= НСтр("ru = 'Формат icon(*.ico)|*.ico'");
	ФорматМетафайл	= НСтр("ru = 'Формат метафайл(*.wmf;*.emf)|*.wmf;*.emf'");
	
	ФильтрФайлов = ВсеКартинки + "|" + ВсеФайлы + "|" + ФорматBMP + "|" + ФорматGIF + "|" + ФорматJPEG + "|" + ФорматPNG
					+ "|" + ФорматTIFF + "|" + ФорматICON + "|" + ФорматМетафайл;
	
	Возврат ФильтрФайлов;
	
КонецФункции

// Выполняет оповещение пользователя о выполненном подборе единиц измерений из классификатора.
//
// Параметры:
//	КоличествоНовыхЭлементов - Число - количество подобранных единиц измерений из классификатора,
//	МассивЭлементов			 - Массив - массив ссылок на элементы справочника 'УпаковкиЕдиницыИзмерения',
//		подобранных из классификатора единиц измерений.
//
Процедура ОповеститьОбИзмененииСпискаВыбораЕдиницИзмерения(КоличествоНовыхЭлементов, МассивЭлементов) Экспорт
	
	ТекстЗаголовка = НСтр("ru='Добавление элементов'");
	
	Если КоличествоНовыхЭлементов = 1 Тогда
		ДобавляемыйЭлемент = МассивЭлементов[0];
		
		СсылкаЕдиницыИзмерения = ПолучитьНавигационнуюСсылку(ДобавляемыйЭлемент);
		
		ТекстСообщения = НСтр("ru='В список добавлена новая единица измерения: %ЕдиницаИзмерения%.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ЕдиницаИзмерения%", Строка(ДобавляемыйЭлемент));
	Иначе
		СсылкаЕдиницыИзмерения = "";
		
		СклоняемоеВыражение = ОбщегоНазначенияУТКлиентСервер.ФормаМножественногоЧисла(НСтр("ru='новая единица измерения'"),
																					НСтр("ru='новые единицы измерения'"),
																					НСтр("ru='новых единиц измерений'"),
																					КоличествоНовыхЭлементов);
		
		ТекстСообщения = НСтр("ru='В список добавлено %КоличествоНовыхЭлементов% %СклоняемоеВыражение%.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%КоличествоНовыхЭлементов%", КоличествоНовыхЭлементов);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%СклоняемоеВыражение%", СклоняемоеВыражение);
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(ТекстЗаголовка, СсылкаЕдиницыИзмерения, ТекстСообщения,
		БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

#Область ИнтерактивныеДействия

// Функция является обработчиком события "НачалоВыбора" для тех элементов форм, в которых может быть
//  выбрана как группа справочника, так и элемент.
//
// Параметры:
//  Форма				 - ФормаКлиентскогоПриложения	 - контекст действия.
//  Элемент				 - ПолеФормы		 - элемент, в котором осуществляется выбор.
//  СтандартнаяОбработка - Булево			 - признак стандартной обработки выбора в поле.
//
Процедура ВыбратьЭлементГруппуНоменклатуры(Форма, Элемент, СтандартнаяОбработка) Экспорт
	Если Элемент.ВыборГруппИЭлементов <> ГруппыИЭлементы.Группы
		И Элемент.ВыборГруппИЭлементов <> ГруппыИЭлементы.Элементы Тогда 
		
		СтандартнаяОбработка = Ложь;
		
		СписокГруппаЭлемент = Новый СписокЗначений;
		СписокГруппаЭлемент.Добавить(ИспользованиеГруппИЭлементов.Группы, НСтр("ru = 'Выбрать группу номенклатуры'"));
		СписокГруппаЭлемент.Добавить(ИспользованиеГруппИЭлементов.Элементы, НСтр("ru = 'Выбрать позицию номенклатуры'"));
		
		ЭлементСписка = Форма.ВыбратьИзСписка(СписокГруппаЭлемент,Элемент);
		
		Если ЭлементСписка = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыФормыВыбораНоменклатуры = Новый Структура;
		ПараметрыФормыВыбораНоменклатуры.Вставить("ВыборГруппИЭлементов", ЭлементСписка.Значение);
	
		Если ПараметрыФормыВыбораНоменклатуры.ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Элементы Тогда
			ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора",ПараметрыФормыВыбораНоменклатуры, Элемент);
		Иначе
			ОткрытьФорму("Справочник.Номенклатура.ФормаВыбораГруппы",ПараметрыФормыВыбораНоменклатуры, Элемент);
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыРаботыССериями

// Функция проверяет необходимость указания серий в строке, если возможно, открывает форму указания,
//  если форма указания не требует контекстного вызова сервера.
//
// Параметры:
//  Форма					 - ФормаКлиентскогоПриложения	 - форма документа, в которой инициировано указание серий, содержит в том числе:
//  	* Объект - ДокументОбъект, ДанныеФормыСтруктура - 
//  ПараметрыУказанияСерий	 - Структура		 - параметры указания серий, возвращаемые соответствующей процедурой модуля менеджера документа.
//  Текст					 - Строка			 - текст, введенный в поле ввода (параметр событий ОкончаниеВводаТекста и АвтоПодборВводаТекста);
//  ТекущиеДанные			 - Структура, ДанныеФормыЭлементКоллекции - данные строки, в которой указывается серия, если значение не передано, то используются текущие данные табличного поля с именем ПараметрыУказанияСерий.ИмяТЧТовары, должна содержать:
//  	* Ссылка - ДокументСсылка
//  ОпределятьРаспоряжение	 - Булево			 - признак необходимости определения распоряжения по значениям полей;
//  ВыдаватьСообщения		 - Булево			 - признак необходимости вывода пользователю информационного сообщения.
// 
// Возвращаемое значение:
//  Булево - нужен контекстный вызов.
//
Функция ДляУказанияСерийНуженСерверныйВызов(Форма, ПараметрыУказанияСерий, Текст, ТекущиеДанные = Неопределено, ОпределятьРаспоряжение = Истина, ВыдаватьСообщения = Истина) Экспорт
	
	Если Не ПроверитьЗаполнениеПолейПередУказаниемСерий(Форма, ПараметрыУказанияСерий, ТекущиеДанные, ВыдаватьСообщения) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекстПоляДляПодбораСерий = "";
	Для Каждого СтрМас Из ПараметрыУказанияСерий.ПоляСвязи Цикл
		ТекстПоляДляПодбораСерий = ТекстПоляДляПодбораСерий + "," + СтрМас  ;
	КонецЦикла;
	Для Каждого СтрМас Из ПараметрыУказанияСерий.ИменаПолейДополнительные Цикл
		ТекстПоляДляПодбораСерий = ТекстПоляДляПодбораСерий + "," + СтрМас  ;
	КонецЦикла;
	ПараметрыПодбораСерий = Новый Структура("Номенклатура,Характеристика" + ТекстПоляДляПодбораСерий);
	Если ПараметрыПодбораСерий.Свойство("Назначение") Тогда
		ПараметрыПодбораСерий.Удалить("Назначение");
	КонецЕсли;
	
	Если ПараметрыУказанияСерий.ТоварВШапке И ТекущиеДанные = Неопределено Тогда
		
		Если Форма.Объект.СтатусУказанияСерий = 13
			Или Форма.Объект.СтатусУказанияСерий = 14
			Или Форма.Объект.СтатусУказанияСерий = 15
			Или Форма.Объект.СтатусУказанияСерий = 19 Тогда
			
			Если ПараметрыУказанияСерий.ТолькоПросмотр
				Или Форма.ТолькоПросмотр Тогда
				ПараметрыПодбораСерий.Вставить("ТолькоПросмотр", Истина);
			Иначе
				Форма.ЗаблокироватьДанныеФормыДляРедактирования();
				Форма.Модифицированность = Истина;
				
				ПараметрыПодбораСерий.Вставить("ТолькоПросмотр", Ложь);
			КонецЕсли;
			
			ПараметрыПодбораСерий.Вставить("Номенклатура", Форма.Объект.Номенклатура);
			ПараметрыПодбораСерий.Вставить("Характеристика", Форма.Объект.Характеристика);
			
			Если ПараметрыУказанияСерий.ИмяПоляКоличество <> Неопределено Тогда
				ПараметрыПодбораСерий.Вставить("Количество", Форма.Объект[ПараметрыУказанияСерий.ИмяПоляКоличество]);
			КонецЕсли;
			
			// Заполнение назначения в заказе на сборку
			Если Форма.Объект.Свойство("НазначениеТовары") 
				И ПараметрыУказанияСерий.СкладскиеОперации.Найти(ПредопределенноеЗначение("Перечисление.СкладскиеОперации.ОтгрузкаКомплектовДляРазборки")) <> Неопределено Тогда
				Если Форма.Объект.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить")
							И Форма.Объект.Обособленно
						Или Форма.Объект.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.СоСклада")
							И Форма.Объект.Обособленно Тогда
					Если ЗначениеЗаполнено(Форма.Объект.Назначение) Тогда
						ПараметрыПодбораСерий.Вставить("Назначение", Форма.Объект.Назначение);
					Иначе
						ПараметрыПодбораСерий.Вставить("Назначение", Форма.Объект.НазначениеТовары);
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли Форма.Объект.Свойство("Назначение") Тогда
				ПараметрыПодбораСерий.Вставить("Назначение", Форма.Объект.Назначение);
			КонецЕсли;
			
			ПараметрыПодбораСерий.Вставить("Серия", Форма.Объект.Серия);
			
			Если Форма.Объект.Свойство("Ссылка") Тогда
				ПараметрыПодбораСерий.Вставить("Регистратор", Форма.Объект.Ссылка);
			КонецЕсли;
				
			Если ЗначениеЗаполнено(ПараметрыУказанияСерий.ИмяПоляСклад) Тогда
				ПараметрыПодбораСерий.Вставить("Склад", Форма.Объект[ПараметрыУказанияСерий.ИмяПоляСклад]);
			КонецЕсли;
			
			ПараметрыПодбораСерий.Вставить("ПараметрыУказанияСерий", ПараметрыУказанияСерий);
			
			ЗначенияПолейДляОпределенияРаспоряжения = НоменклатураКлиентСервер.ЗначенияПолейДляОпределенияРаспоряжения(Форма.Объект,
																							  Форма.Объект,
																							  ПараметрыУказанияСерий);

			ПараметрыПодбораСерий.Вставить("ЗначенияПолейДляОпределенияРаспоряжения", ЗначенияПолейДляОпределенияРаспоряжения);
			ПараметрыПодбораСерий.Вставить("Текст", Текст);
			
			ОткрытьФорму("Обработка.ПодборСерийВДокументы.Форма.УказаниеСерииВСтрокеТоваров",ПараметрыПодбораСерий, Форма);
			
			Возврат Ложь;
		КонецЕсли;
		
	Иначе
		
		
		ИдентификаторТекущейСтроки = Неопределено;
		
		Если Не ТекущиеДанные.Свойство("ИдентификаторТекущейСтроки", ИдентификаторТекущейСтроки) Тогда
			ИдентификаторТекущейСтроки = ТекущиеДанные.ПолучитьИдентификатор();
		КонецЕсли;
			
		Если НоменклатураКлиентСервер.ВЭтомСтатусеСерииУказываютсяВТЧТовары(ТекущиеДанные.СтатусУказанияСерий, ПараметрыУказанияСерий)
			И ТекущиеДанные.Свойство("Серия")
			И (НЕ ПараметрыУказанияСерий.ЭтоОрдер
				Или (ПараметрыУказанияСерий.СкладскиеОперации.Найти(ПредопределенноеЗначение("Перечисление.СкладскиеОперации.ОтражениеИзлишков")) <> Неопределено
					И ПараметрыУказанияСерий.СкладскиеОперации.Найти(ПредопределенноеЗначение("Перечисление.СкладскиеОперации.ОтражениеНедостач")) <> Неопределено)) Тогда
			
			Если ПараметрыУказанияСерий.ИмяИсточникаЗначенийВФормеОбъекта = "ТекущиеДанные" Тогда
				ИсточникДанныхФормы = ТекущиеДанные;
			Иначе 
				ИсточникДанныхФормы = Форма[ПараметрыУказанияСерий.ИмяИсточникаЗначенийВФормеОбъекта];
			КонецЕсли; 
			
			Если ПараметрыУказанияСерий.ТолькоПросмотр
				Или Форма.ТолькоПросмотр Тогда
				ПараметрыПодбораСерий.Вставить("ТолькоПросмотр", Истина);
			Иначе
				Форма.ЗаблокироватьДанныеФормыДляРедактирования();
				Форма.Модифицированность = Истина;
				
				ПараметрыПодбораСерий.Вставить("ТолькоПросмотр", Ложь);
			КонецЕсли;
				
			ЗаполнитьЗначенияСвойств(ПараметрыПодбораСерий,ТекущиеДанные);
			
			Если ПараметрыУказанияСерий.ИмяПоляКоличество <> Неопределено Тогда
				ПараметрыПодбораСерий.Вставить("Количество", ТекущиеДанные[ПараметрыУказанияСерий.ИмяПоляКоличество]);
			КонецЕсли;
			
			Если ТекущиеДанные.Свойство("РежимПомощника") Тогда
				ПараметрыПодбораСерий.Вставить("РежимПомощника", ТекущиеДанные.РежимПомощника);
			КонецЕсли;
			
			Если ТекущиеДанные.Свойство("НазначениеОтправителя") Тогда
				ПараметрыПодбораСерий.Вставить("НазначениеОтправителя", ТекущиеДанные.НазначениеОтправителя);	
			ИначеЕсли ПараметрыУказанияСерий.СкладскиеОперации.Найти(ПредопределенноеЗначение("Перечисление.СкладскиеОперации.ОтгрузкаПоПеремещению")) <> Неопределено 
				И ТекущиеДанные.Свойство("ВариантОбеспечения") Тогда
				Если ТекущиеДанные.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить")
							И ТекущиеДанные.Обособленно
						Или ТекущиеДанные.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.СоСклада")
							И ТекущиеДанные.Обособленно Тогда
					Если ЗначениеЗаполнено(ТекущиеДанные.Назначение) Тогда
						ПараметрыПодбораСерий.Вставить("НазначениеОтправителя", ТекущиеДанные.Назначение);
					Иначе
						ПараметрыПодбораСерий.Вставить("НазначениеОтправителя", ИсточникДанныхФормы.Назначение);
					КонецЕсли;
				Иначе
					ПараметрыПодбораСерий.Вставить("НазначениеОтправителя", ПредопределенноеЗначение("Справочник.Назначения.ПустаяСсылка"));	
				КонецЕсли;
			КонецЕсли;
			
			Если ТекущиеДанные.Свойство("Назначение") Тогда
				ПараметрыПодбораСерий.Вставить("Назначение", ТекущиеДанные.Назначение);
			ИначеЕсли ТипЗнч(ИсточникДанныхФормы) <> Тип("ФормаКлиентскогоПриложения") 
				И ИсточникДанныхФормы.Свойство("Назначение") Тогда
				
				Если ТекущиеДанные.Свойство("ВариантОбеспечения") Тогда 
				Если ТекущиеДанные.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить")
							И ТекущиеДанные.Обособленно
						Или ТекущиеДанные.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.СоСклада")
							И ТекущиеДанные.Обособленно Тогда
						ПараметрыПодбораСерий.Вставить("Назначение", Форма.Объект.Назначение);
					КонецЕсли; 
				КонецЕсли;
				
			КонецЕсли;
			
			// Заполнение назначения в заказе на сборку
			Если ТипЗнч(ИсточникДанныхФормы) <> Тип("ФормаКлиентскогоПриложения") 
				И ИсточникДанныхФормы.Свойство("НазначениеТовары")
				И ПараметрыУказанияСерий.СкладскиеОперации.Найти(ПредопределенноеЗначение("Перечисление.СкладскиеОперации.ОтгрузкаКомплектующихДляСборки")) <> Неопределено Тогда
				Если ТекущиеДанные.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить")
							И ТекущиеДанные.Обособленно
						Или ТекущиеДанные.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.СоСклада")
							И ТекущиеДанные.Обособленно Тогда
					Если ЗначениеЗаполнено(ИсточникДанныхФормы.Назначение) Тогда
						ПараметрыПодбораСерий.Вставить("Назначение", Форма.Объект.Назначение);
					Иначе
						ПараметрыПодбораСерий.Вставить("Назначение", Форма.Объект.НазначениеТовары);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			

			Если ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.КорректировкаНазначенияТоваров" Тогда
				ПараметрыПодбораСерий.Вставить("Назначение", ПараметрыПодбораСерий.ИсходноеНазначение);
			КонецЕсли;
			
			ПараметрыПодбораСерий.Вставить("Серия", ТекущиеДанные.Серия);
			ПараметрыПодбораСерий.Вставить("Регистратор", ИсточникДанныхФормы.Ссылка);
			
			Если ПараметрыУказанияСерий.ИмяПоляПомещение <> Неопределено Тогда
				ПараметрыПодбораСерий.Вставить("Помещение", ИсточникДанныхФормы[ПараметрыУказанияСерий.ИмяПоляПомещение]);
			ИначеЕсли ПараметрыУказанияСерий.ПоляСвязи.Найти("Помещение") <> Неопределено Тогда
				ПараметрыПодбораСерий.Вставить("Помещение", ТекущиеДанные.Помещение);
			КонецЕсли;
			
			Если ПараметрыУказанияСерий.ПоляСвязи.Найти("Склад") <> Неопределено
				Или ПараметрыУказанияСерий.ИменаПолейДополнительные.Найти("Склад") <> Неопределено Тогда
				ПараметрыПодбораСерий.Вставить("Склад", ТекущиеДанные.Склад);
			ИначеЕсли ПараметрыУказанияСерий.ПоляСвязи.Найти(ПараметрыУказанияСерий.ИмяПоляСклад) <> Неопределено Тогда
				ПараметрыПодбораСерий.Вставить("Склад", ТекущиеДанные[ПараметрыУказанияСерий.ИмяПоляСклад]);
			ИначеЕсли ПараметрыУказанияСерий.ПоляСвязи.Найти("Подразделение") <> Неопределено Тогда
				ПараметрыПодбораСерий.Вставить("Склад", ТекущиеДанные.Подразделение);
			ИначеЕсли ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ПоступлениеТоваровОтХранителя"
				Или ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ВыкупТоваровХранителем"
				Или ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ОтчетОСписанииТоваровУХранителя"
				Или ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ОприходованиеИзлишковТоваровУХранителя"
				Или ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ОтчетКомиссионера"
				Или ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ОтчетОСписанииТоваровУХранителя"
				Тогда
				ПараметрыПодбораСерий.Вставить("Склад", ИсточникДанныхФормы.Договор);
			ИначеЕсли ЗначениеЗаполнено(ПараметрыУказанияСерий.ИмяПоляСклад) Тогда
				ПараметрыПодбораСерий.Вставить("Склад", ИсточникДанныхФормы[ПараметрыУказанияСерий.ИмяПоляСклад]);
			КонецЕсли;
			
			ПараметрыПодбораСерий.Вставить("ПараметрыУказанияСерий", ПараметрыУказанияСерий);
			
			Если ОпределятьРаспоряжение Тогда
				ЗначенияПолейДляОпределенияРаспоряжения = НоменклатураКлиентСервер.ЗначенияПолейДляОпределенияРаспоряжения(ИсточникДанныхФормы,
																									ТекущиеДанные,
																									ПараметрыУказанияСерий);
			Иначе
				ЗначенияПолейДляОпределенияРаспоряжения = Новый Структура;
			КонецЕсли;
			ПараметрыПодбораСерий.Вставить("ЗначенияПолейДляОпределенияРаспоряжения", ЗначенияПолейДляОпределенияРаспоряжения);
			ПараметрыПодбораСерий.Вставить("ИдентификаторТекущейСтроки", ИдентификаторТекущейСтроки);
			ПараметрыПодбораСерий.Вставить("Текст", Текст);
			
			ОткрытьФорму("Обработка.ПодборСерийВДокументы.Форма.УказаниеСерииВСтрокеТоваров",ПараметрыПодбораСерий, Форма);
			
			Возврат Ложь;
			
		КонецЕсли;
				
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Функция проверяет возможность отмены указания серий в строке.
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - форма документа, в которой инициировано указание серий;
//	ПараметрыУказанияСерий - Структура - параметры указания серий, возвращаемая соответствующей процедурой модуля менеджера документа;
//	ИмяТабличногоПоля - Строка - имя табличной части документа.
//
// Возвращаемое значение:
//	Булево - Истина, если серверный вызов нужен.
//
Функция ДляОтменыУказанияСерийНуженСерверныйВызов(Форма,ПараметрыУказанияСерий, ИмяТабличногоПоля = "") Экспорт
	
	Если ИмяТабличногоПоля = "" Тогда
		ИмяТабличногоПоля = ПараметрыУказанияСерий.ИмяТЧТовары;
	КонецЕсли;
	
	Если ПараметрыУказанияСерий.ТоварВШапке Тогда
		
		Если Не Форма.Объект.УказыватьСерии Тогда
			
			ТекстСообщения = НСтр("ru='Для комплекта серии не указаны.'");
			ПоказатьПредупреждение(,ТекстСообщения);
			Возврат Ложь;
			
		КонецЕсли;
		
	Иначе
		
		ТекущиеДанные = Форма.Элементы[ИмяТабличногоПоля].ТекущиеДанные;
		
		Если ТекущиеДанные = Неопределено Тогда
			
			ТекстСообщения = НСтр("ru='Необходимо выбрать строку товаров, для которой необходимо отменить указание серий.'");
			ПоказатьПредупреждение(,ТекстСообщения);
			Возврат Ложь;
			
		ИначеЕсли Не ТекущиеДанные.УказыватьСерии Тогда
			
			ТекстСообщения = НСтр("ru='Для выбранной строки серии не указаны.'");
			ПоказатьПредупреждение(,ТекстСообщения);
			Возврат Ложь;
			
		ИначеЕсли ТекущиеДанные.СтатусУказанияСерий = 14 Тогда
			
			Если ТекущиеДанные.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.СоСклада") Тогда
				ТекущиеДанные.СтатусУказанияСерий = 15;
			Иначе
				ТекущиеДанные.СтатусУказанияСерий = 13;
			КонецЕсли;
			
			ТекущиеДанные.Серия               = ПредопределенноеЗначение("Справочник.СерииНоменклатуры.ПустаяСсылка");
			ТекущиеДанные.УказыватьСерии      = Ложь;
			
			Возврат Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Форма.ЗаблокироватьДанныеФормыДляРедактирования();
	Форма.Модифицированность = Истина;
	
	Возврат Истина;
	
КонецФункции

// Процедура обновляет кеш ключевых реквизитов текущей строки товаров. По ключевым реквизитам осуществляется связь
//  между ТЧ серий и ТЧ товаров.
//
// Параметры:
//  ТаблицаФормы			 - ТаблицаФормы	 - таблица, в которой отображается ТЧ с товарами.
//  КэшированныеЗначения	 - Структура	 - переменная модуля формы, в которой хранятся кешируемые значения.
//  ПараметрыУказанияСерий	 - Структура	 - структура параметров указания серий, возвращаемая соответствующей процедурой модуля менеджера документа.
//  Копирование				 - Булево		 - признак, что кешированная строка скопирована (параметр события ПриНачалеРедактирования).
//
Процедура ОбновитьКешированныеЗначенияДляУчетаСерий(ТаблицаФормы,КэшированныеЗначения,ПараметрыУказанияСерий,Копирование = Ложь) Экспорт
	
	ТекущиеДанные = ТаблицаФормы.ТекущиеДанные;
	
	НоменклатураКлиентСервер.ОбновитьКешированныеЗначенияДляУчетаСерий(ТекущиеДанные,КэшированныеЗначения,ПараметрыУказанияСерий,Копирование);
	
КонецПроцедуры

// Обновляет кеш ключевых реквизитов товара в шапке документа. По ключевым реквизитам осуществляется связь
//  между ТЧ серий товаром.
//
// Параметры:
//  Объект					 - ДанныеФормыСтруктура	 - основной реквизит формы
//  КэшированныеЗначения	 - Структура			 - переменная модуля формы, в которой хранятся кешируемые значения
//  ПараметрыУказанияСерий	 - Структура			 - параметры указания серий, возвращаемая соответствующей процедурой модуля менеджера документа.
//
Процедура ОбновитьКешированныеЗначенияШапкиДляУчетаСерий(Объект, КэшированныеЗначения, ПараметрыУказанияСерий) Экспорт
	
	Если Не ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры Тогда
		Возврат;
	КонецЕсли;
	
	Если КэшированныеЗначения = Неопределено Тогда
		КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	КонецЕсли;
	
	КэшированныеЗначения.Вставить("НоменклатураШапка", Объект.Номенклатура);
	КэшированныеЗначения.Вставить("ХарактеристикаШапка", Объект.Характеристика);
	КэшированныеЗначения.Вставить("НазначениеШапка", Объект.Назначение);
	КэшированныеЗначения.Вставить("КоличествоШапка", Объект.Количество);
	
	Для Каждого СтрМас Из ПараметрыУказанияСерий.ПоляСвязи Цикл
		ИмяПоляШапкиТовара = НоменклатураКлиентСервер.ИмяПоляШапкиТовара(СтрМас);
		
		КэшированныеЗначения.Вставить(ИмяПоляШапкиТовара, Объект[СтрМас]);
	КонецЦикла;
		
КонецПроцедуры

// Функция проверяет необходимость обновления статусов указания серий при окончании редактирования строки товаров.
//
// Параметры:
//  ТаблицаФормы			 - ТаблицаФормы	 - таблица формы, отображающая ТЧ товаров;
//  КэшированныеЗначения	 - Структура	 - переменная модуля формы, в которой хранятся кешируемые значения;
//  ПараметрыУказанияСерий	 - Структура	 - параметры указания серий, возвращаемые соответствующей процедурой модуля менеджера документа.
//  Удаление				 - Булево		 - Истина, признак, что проверка вызывается при удалении строки ТЧ.
// 
// Возвращаемое значение:
//  Булево - нужно ли обновить статусы указания серий.
//
Функция НеобходимоОбновитьСтатусыСерий(ТаблицаФормы,КэшированныеЗначения,ПараметрыУказанияСерий, Удаление = Ложь) Экспорт
	
	ТекущиеДанные = ТаблицаФормы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Удаление Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если КэшированныеЗначения = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ПараметрыУказанияСерий.ИмяТЧТовары = ПараметрыУказанияСерий.ИмяТЧСерии Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекстПоляСвязи = "";
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для Каждого СтрМас Из ПараметрыУказанияСерий.ПоляСвязи Цикл
		ТекстПоляСвязи = ТекстПоляСвязи + "," + СтрМас;
	КонецЦикла;
	
	Если ТекущиеДанные.Свойство("Отменено") Тогда
		ТекстПоляСвязи = ТекстПоляСвязи + ",Отменено";
	КонецЕсли;
	
	Если Не ОбщегоНазначенияУТКлиентСервер.СтруктурыРавны(КэшированныеЗначения,ТекущиеДанные,
		ПараметрыУказанияСерий.ИмяПоляКоличество + ",Номенклатура,Характеристика"+ТекстПоляСвязи) Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Функция проверяет необходимость обновления статуса указания серий для товара в шапке документа.
//
// Параметры:
//  Объект					 - ДанныеФормыСтруктура	 - основной реквизит формы;
//  КэшированныеЗначения	 - Структура			 - переменная модуля формы, в которой хранятся кешируемые значения;
//  ПараметрыУказанияСерий	 - Структура			 - параметры указания серий, возвращаемые соответствующей процедурой
//  			модуля менеджера документа.
// 
// Возвращаемое значение:
//  Булево - нужно ли обновить статус указания серий.
//
Функция НеобходимоОбновитьСтатусСерийВШапке(Объект,КэшированныеЗначения,Знач ПараметрыУказанияСерий) Экспорт
	
	Если Не ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если КэшированныеЗначения.НоменклатураШапка <> Объект.Номенклатура
		Или КэшированныеЗначения.ХарактеристикаШапка <> Объект.Характеристика
		Или КэшированныеЗначения.КоличествоШапка <> Объект.Количество
		Или КэшированныеЗначения.НазначениеШапка <> Объект.Назначение Тогда
		Возврат Истина;
	Иначе	
		Для Каждого СтрМас Из ПараметрыУказанияСерий.ПоляСвязи Цикл
			Если КэшированныеЗначения[СтрМас+"Шапка"] <> Объект[СтрМас] Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Запрашивает у пользователя подтверждение перезаполнения серий по FEFO.
//
// Параметры:
//  ОповещениеПослеОтвета	 - ОписаниеОповещения - действие после ответа.
//
Процедура ЗадатьВопросОПерезаполненииСерийПоFEFO(ОповещениеПослеОтвета) Экспорт 
	
	ТекстВопроса = НСтр("ru = 'Есть строки, в которых серии по FEFO уже заполнены. При выполнении операции
		|серии в этих строках будут перезаполнены. Продолжить?'");
	
	ПоказатьВопрос(
		Новый ОписаниеОповещения("ЗадатьВопросОПерезаполненииСерийПоFEFOЗавершение", 
			ЭтотОбъект, 
			Новый Структура("ОповещениеПослеОтвета", ОповещениеПослеОтвета)), 
		ТекстВопроса, 
		РежимДиалогаВопрос.ОКОтмена,
		,
		КодВозвратаДиалога.ОК);
		
КонецПроцедуры

// Процедура выполняет обработку оповещения после вопроса о перезаполнении серий по FEFO.
//
// Параметры:
//  РезультатВопроса		 - КодВозвратаДиалога - ответ на вопрос.
//  ДополнительныеПараметры	 - Структура - передается ОповещениеПослеОтвета.
//
Процедура ЗадатьВопросОПерезаполненииСерийПоFEFOЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	ОповещениеПослеОтвета = ДополнительныеПараметры.ОповещениеПослеОтвета;
	
	Если РезультатВопроса <> КодВозвратаДиалога.ОК Тогда
		ВыполнитьОбработкуОповещения(ОповещениеПослеОтвета, Ложь);
	Иначе
		ВыполнитьОбработкуОповещения(ОповещениеПослеОтвета, Истина);
	КонецЕсли;

КонецПроцедуры

// Функция выводит предупреждение, что в ТЧ нет строк, серии по которым можно заполнить по FEFO.
Процедура ПредупредитьОбОтсутствииСтрокЗаполняемыхПоFEFO() Экспорт
	ТекстПредупреждения = НСтр("ru = 'В табличной части нет товаров, по которым серии можно заполнить по FEFO.'");	
	
	ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
КонецПроцедуры

// Обрабатывает указание серии в ТЧ "Товары".
//	Параметры:
//		Форма - ФормаКлиентскогоПриложения - форма документа, в которой инициировано указание серии
//		ПараметрыУказанияСерий - Структура - параметры указания серий, возвращаемые соответствующей процедурой модуля менеджера документа
//		ВыбраннаяСерия - Структура - формат которой описан в функции НоменклатураКлиентСервер.ВыбраннаяСерия
//		ТекущиеДанные - Структура, ДанныеФормыЭлементКоллекции  - данные строки, в которой указывается серия, если значение не передано,
//									то используются текущие данные табличного поля с именем ПараметрыУказанияСерий.ИмяТЧТовары.
//
Процедура ОбработатьУказаниеСерии(Форма, ПараметрыУказанияСерий, ВыбраннаяСерия, ТекущиеДанные = Неопределено) Экспорт
	
	ЕстьПризнакУказанияСерий = ПараметрыУказанияСерий.ПоляСвязи.Найти("УказыватьСерии") <> Неопределено;
	
	Если ТекущиеДанные = Неопределено Тогда 
		Если Не ПараметрыУказанияСерий.ТоварВШапке Тогда		
			ТекущиеДанные = Форма.Элементы[ПараметрыУказанияСерий.ИмяТЧТовары].ДанныеСтроки(ВыбраннаяСерия.ИдентификаторТекущейСтроки);
			
			Если ТекущиеДанные = Неопределено Тогда
				Возврат;
			КонецЕсли;
		Иначе
			ТекущиеДанные = Форма.Объект;
		КонецЕсли;
	КонецЕсли;
	
	// Можно пересчитать на клиенте статус указания серий после указания серии,
	// если не нужно для этого анализировать ТЧ Серии, т.е. когда ее нет, или когда серии указываются в ТЧ Товары.
	Если НоменклатураКлиентСервер.ВЭтомСтатусеСерииУказываютсяВТЧТовары(ТекущиеДанные.СтатусУказанияСерий, ПараметрыУказанияСерий) Тогда
		
		Если ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Количество() = 0 Тогда
			НоменклатураКлиентСервер.ПересчитатьСтатусУказанияСерийПриОбработке(ПараметрыУказанияСерий,
																				ТекущиеДанные.СтатусУказанияСерий,
																				ЗначениеЗаполнено(ВыбраннаяСерия.Значение),
																				Неопределено,
																				?(ПараметрыУказанияСерий.ЭтоЗаказ, ТекущиеДанные.ВариантОбеспечения, Неопределено),
																				ТекущиеДанные);
		Иначе
			
			Для Каждого ИмяПоляСтатус Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
				Если ТекущиеДанные.Свойство(ИмяПоляСтатус) Тогда
					НоменклатураКлиентСервер.ПересчитатьСтатусУказанияСерийПриОбработке(ПараметрыУказанияСерий,
																						ТекущиеДанные[ИмяПоляСтатус],
																						ЗначениеЗаполнено(ВыбраннаяСерия.Значение),
																						Неопределено,
																						?(ПараметрыУказанияСерий.ЭтоЗаказ, ТекущиеДанные.ВариантОбеспечения, Неопределено),
																						ТекущиеДанные);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		ТекущиеДанные.Серия = ВыбраннаяСерия.Значение;
		
		Если ЕстьПризнакУказанияСерий Тогда
			ТекущиеДанные.УказыватьСерии = Не НоменклатураКлиентСервер.ВЭтомСтатусеСерииНеУказываются(ТекущиеДанные.СтатусУказанияСерий,
																										ПараметрыУказанияСерий);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает параметры необходимые для расшифровки отчета ВедомостьПоСериямНоменклатуры.
//
// Возвращаемое значение:
//   Структура   - параметры необходимые для расшифровки отчета.
//
Функция ПараметрыРасшифровкиОтчетВедомостьПоСериямНоменклатуры() Экспорт

	МенюОтчетов  = Новый Массив;
	МенюДействий = Новый Массив;
	
	// Расшифровать отчетом -> Движение серии номенклатуры
	#Область ДвижениеСерииНоменклатурыКонтекст
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Имя", "ДвижениеСерииНоменклатурыКонтекст");
	ПараметрыОтчета.Вставить("Заголовок", НСтр("ru = 'Движение серии номенклатуры'"));
	ПараметрыОтчета.Вставить("ИмяОтчета", "Отчет.ДвижениеСерииНоменклатуры");
	
	ПоляРасшифровки  = Новый Массив;
	ПоляРасшифровки.Добавить("Серия");
	ПоляРасшифровки.Добавить("Характеристика");
	ПоляРасшифровки.Добавить("Получатель");
	ПоляРасшифровки.Добавить("Помещение");
	ПараметрыОтчета.Вставить("ПоляРасшифровки", ПоляРасшифровки);
	
	НеобходимыеПараметры = Новый Структура;
	НеобходимыеПараметры.Вставить("Серия");
	НеобходимыеПараметры.Вставить("Характеристика");
	ПараметрыОтчета.Вставить("НеобходимыеПараметры", НеобходимыеПараметры);
	
	СписокПараметров = Новый Массив;
	СписокПараметров.Добавить("Номенклатура");
	ПараметрыОтчета.Вставить("СписокПараметров", СписокПараметров);
	
	ЗаменаПараметров = Новый Структура;
	ЗаменаПараметров.Вставить("Получатель", "ОтправительИлиПолучатель");
	ЗаменаПараметров.Вставить("Помещение", "ОтправительИлиПолучательПомещение");
	ПараметрыОтчета.Вставить("ЗаменаПараметров", ЗаменаПараметров);
	
	ФиксированныеПараметры = Новый Структура;
	ФиксированныеПараметры.Вставить("ИспользоватьОтправительИлиПолучательПомещение", Истина);
	ПараметрыОтчета.Вставить("ФиксированныеПараметры", ФиксированныеПараметры);
	
	МенюОтчетов.Добавить(ПараметрыОтчета);
	#КонецОбласти
		
	ПараметрыРасшифровки = Новый Структура;
	ПараметрыРасшифровки.Вставить("МенюОтчетов",  МенюОтчетов);
	ПараметрыРасшифровки.Вставить("МенюДействий", МенюДействий);

	Возврат ПараметрыРасшифровки;
	
КонецФункции

// Функция вызывается в обработчике ОбработкаВыбора в форме, в которой указываются серии
// и проверяет, что источник выбора - это форма указания серий.
//
// Параметры:
//  ИсточникВыбора	 - Произвольный - форма, где осуществлен выбор. Параметр события ОбработкаВыбора формы.
// 
// Возвращаемое значение:
//   Булево - истина, если это указание серий.
//
Функция ЭтоУказаниеСерий(ИсточникВыбора) Экспорт
	
	Возврат ИсточникВыбора.ИмяФормы = "Обработка.ПодборСерийВДокументы.Форма.УказаниеСерииВСтрокеТоваров";
	
КонецФункции

// Выполняет переопределение платформенного обработчика события начала выбора реквизита "ДатаПроизводства" справочника "СерииНоменклатуры".
//
// Параметры:
//	Объект					 - ДанныеФормыСтруктура, ДанныеФормыЭлементКоллекции	 - структура данных, содержащая обрабатываемые сведения
//	ОповещениеОЗавершении	 - ОписаниеОповещения, Неопределено						 - описание оповещения, выполняемое после закрытия формы указания даты.
//	СтандартнаяОбработка	 - Булево												 - признак выполнения стандартной обработки события.
//
Процедура ДатаПроизводстваНачалоВыбора(Объект, ОповещениеОЗавершении, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы      = Новый Структура("ДатаПроизводства, УказаниеДатыПроизводства", Объект.ДатаПроизводства, Истина);
	ПараметрыОповещения = Новый Структура("Объект, ОповещениеОЗавершении", Объект, ОповещениеОЗавершении);
	ОписаниеОповещения  = Новый ОписаниеОповещения("ДатаПроизводстваНачалоВыбораЗавершение", ЭтотОбъект,
									ПараметрыОповещения);
	
	ОткрытьФорму("Обработка.ПодборСерийВДокументы.Форма.УказаниеДаты", ПараметрыФормы, ЭтотОбъект, , , ,
		ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Выполняет переопределение платформенного обработчика события начала выбора реквизита "ГоденДо" справочника "СерииНоменклатуры".
//
// Параметры:
//	Объект					 - ДанныеФормыСтруктура, ДанныеФормыЭлементКоллекции	 - структура данных, содержащая обрабатываемые сведения
//	ПараметрыФормы			 - Структура											 - параметры, используемые при открытии формы указания даты
//	ОповещениеОЗавершении	 - ОписаниеОповещения, Неопределено						 - описание оповещения, выполняемое после закрытия формы 
//																						указания даты.
//	СтандартнаяОбработка	 - Булево												 - признак выполнения стандартной обработки события.
//
Процедура ДатаИстеченияСрокаГодностиНачалоВыбора(Объект, ПараметрыФормы, ОповещениеОЗавершении, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Объект",                       Объект);
	ПараметрыОповещения.Вставить("ИспользоватьДатуПроизводства", ПараметрыФормы.ИспользоватьДатуПроизводства);
	ПараметрыОповещения.Вставить("ОповещениеОЗавершении",        ОповещениеОЗавершении);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДатаИстеченияСрокаГодностиНачалоВыбораЗавершение", ЭтотОбъект,
									ПараметрыОповещения);
	
	ОткрытьФорму("Обработка.ПодборСерийВДокументы.Форма.УказаниеДаты", ПараметрыФормы, ЭтотОбъект, , , ,
		ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Выполняет оповещение пользователя о выполненном перезаполнении серий
//
// Параметры:
//  ЕстьИзменения				 - Булево	 - было ли выполнение перезаполнение
//  КоличествоИзначальноВерное	 - Булево	 - признак обозначающий что изначальные данные соответствуют перезаполненным.
//
Процедура ОповеститьОПерезаполненииСерий(ЕстьИзменения, КоличествоИзначальноВерное) Экспорт
	
	Если ЕстьИзменения Тогда
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Серии перезаполнены'"),
				,
				НСтр("ru='В строках перезаполнены данные по сериям.'"),
				БиблиотекаКартинок.Информация32);
	
	ИначеЕсли КоличествоИзначальноВерное Тогда
			
			ПоказатьОповещениеПользователя(
				НСтр("ru = 'Перезаполнение не требуется'"),
				,
				НСтр("ru='Перезаполнение не требуется, т.к. во всех строках количество серий указано верно, либо поступление серий еще не выполнено.'"),
				БиблиотекаКартинок.Информация32);
	
	Иначе
			
			ПоказатьОповещениеПользователя(
				НСтр("ru = 'Перезаполнение не требуется'"),
				,
				НСтр("ru='Перезаполнение не требуется, т.к. поступление серий еще не выполнено.'"),
				БиблиотекаКартинок.Информация32);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СертификатыНоменклатуры

// Выполняет установку значения статуса сертификатов номенклатуры для выделенных строк в таблице формы.
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - форма-источник инициализации команды.
//	Список - ТаблицаФормы - таблица формы, для элементов которой выполняется установка статусов.
//	Статус - ПеречислениеСсылка.СтатусыСертификатовНоменклатуры - значение устанавливаемого статуса.
//
Процедура УстановитьСтатусСертификатовНоменклатуры(Форма, Список, Статус) Экспорт
	
	ОчиститьСообщения();
	
	ДанныеКОбработке = ОбщегоНазначенияУТКлиент.ПроверитьПолучитьВыделенныеВСпискеСсылки(Список);
	
	Если ДанныеКОбработке.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаполнения = НоменклатураКлиентСервер.ПараметрыУстановкиСтатусаСертификатовНоменклатурыПоУмолчанию();
	ПараметрыЗаполнения.Статус = Статус;
	ПараметрыЗаполнения.ДанныеКОбработке = ДанныеКОбработке;
	
	ДлительнаяОперация = НоменклатураВызовСервера.УстановитьСтатусСертификатовНоменклатуры(
							Форма.УникальныйИдентификатор,
							ПараметрыЗаполнения);
	
	ПараметрыОжидания	= ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОповещения	= Новый Структура("Список", Список);
	ОписаниеОповещения	= Новый ОписаниеОповещения("УстановитьСтатусСертификатовНоменклатурыЗавершение",
													ЭтотОбъект,
													ПараметрыОповещения);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

// Возвращает неинициализированные параметры результата работы с формой контроля уникальности данных информационной базы.
//
// Возвращаемое значение:
//	Структура - коллекция, содержащая следующие свойства:
//		* Действие - Строка - действие обработки данных.
//		* Ссылка - СправочникСсылка.Номенклатура, СправочникСсылка.ХарактеристикиНоменклатуры, СправочникСсылка.НомераГТД, Неопределено - ссылка на элемент информационной базы.
//
Функция НовыйРезультатОбработкиЗавершения() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Действие",	"");
	Результат.Вставить("Ссылка",	Неопределено);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроцедурыРаботыССериями

Процедура ДатаПроизводстваНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ОповещениеОЗавершении = ДополнительныеПараметры.ОповещениеОЗавершении;
	
	Если Результат <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ДополнительныеПараметры.Объект, Результат);
	КонецЕсли;
	
	Если ОповещениеОЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДатаИстеченияСрокаГодностиНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ОповещениеОЗавершении = ДополнительныеПараметры.ОповещениеОЗавершении;
	
	Если Результат <> Неопределено Тогда
		
		Объект = ДополнительныеПараметры.Объект;
		
		Объект.ГоденДо = Результат.ГоденДо;
		
		Если ЗначениеЗаполнено(Результат.ДатаПроизводства)
			И ДополнительныеПараметры.ИспользоватьДатуПроизводства Тогда
			
			Объект.ДатаПроизводства = Результат.ДатаПроизводства;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОповещениеОЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Результат);
	КонецЕсли;
	
КонецПроцедуры

Функция	ПроверитьЗаполнениеПолейПередУказаниемСерий(Форма, ПараметрыУказанияСерий, ТекущиеДанные, ВыдаватьСообщения)
	
	ВсеЗаполнено = Истина;
	
	Если ПараметрыУказанияСерий.ТоварВШапке И ТекущиеДанные = Неопределено Тогда
		
		Если Форма.Объект.СтатусУказанияСерий = 0 Тогда
			Если ВыдаватьСообщения Тогда
				ТекстСообщения = НСтр("ru='Для этого комплекта серии указывать не нужно.'");
				ПоказатьПредупреждение(,ТекстСообщения);
			КонецЕсли;
			ВсеЗаполнено = Ложь;
		КонецЕсли;
		
	Иначе		
		
		Если ТекущиеДанные = Неопределено Тогда
			ТекущиеДанные = Форма.Элементы[ПараметрыУказанияСерий.ИмяТЧТовары].ТекущиеДанные;
			Если ТекущиеДанные = Неопределено Тогда
				Если ВыдаватьСообщения Тогда
					ТекстСообщения = НСтр("ru='Выберите строку товаров, для которой необходимо указать серии.'");
					ПоказатьПредупреждение(,ТекстСообщения);
				КонецЕсли;
				ВсеЗаполнено = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если ВсеЗаполнено
				И ТекущиеДанные.СтатусУказанияСерий = 0 Тогда
			Если ВыдаватьСообщения Тогда
					ТекстСообщения = НСтр("ru='Для этого товара серии указывать не нужно. На возможность указания серий могут влиять: вид номенклатуры, склад, политика указания серий, статус документа.'");
				ПоказатьПредупреждение(,ТекстСообщения);
			КонецЕсли;
			ВсеЗаполнено = Ложь;
		КонецЕсли;
		
		Если ВсеЗаполнено
			И ТекущиеДанные.ХарактеристикиИспользуются
			И Не ЗначениеЗаполнено(ТекущиеДанные.Характеристика) Тогда
			Если ВыдаватьСообщения Тогда
				
				ТекстСообщения = НСтр("ru='Перед указанием серий нужно заполнить характеристику.'");
				ПоказатьПредупреждение(,ТекстСообщения);
			КонецЕсли;
			ВсеЗаполнено = Ложь;
		КонецЕсли;
		
		Если ВсеЗаполнено Тогда
			НоменклатураКлиентЛокализация.ПроверитьЗаполнениеПолейПередУказаниемСерий(ВсеЗаполнено, Форма, ПараметрыУказанияСерий, ТекущиеДанные, ВыдаватьСообщения);
		КонецЕсли;
						
	КонецЕсли;
	
	Возврат ВсеЗаполнено;
	
КонецФункции

#КонецОбласти

#Область СертификатыНоменклатуры

Процедура УстановитьСтатусСертификатовНоменклатурыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьРезультатУстановкиСтатусаСертификатовНоменклатуры(Результат, ДополнительныеПараметры.Список);
	
КонецПроцедуры

// Параметры:
//	Результат - см. Справочники.СертификатыНоменклатуры.УстановитьСтатусСертификатовНоменклатуры.
//	Список - ТаблицаФормы - таблица формы, в которой выполняется обработка данных.
//
Процедура ОбработатьРезультатУстановкиСтатусаСертификатовНоменклатуры(Результат, Список)
	
	Если Результат.Статус = "Ошибка" Тогда
		ТекстСообщения = НСтр("ru = 'Возникла ошибка в процессе изменения статуса сертификатов номенклатуры: %1'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Результат.КраткоеПредставлениеОшибки);
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
		Возврат;
	КонецЕсли;
	
	ВывестиСообщенияФоновогоЗадания(Результат.Сообщения);
	
	РезультатЗаполнения = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Если РезультатЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Список.Обновить();
	ОповеститьПользователяОбУстановкеСтатуса(РезультатЗаполнения.КоличествоИзмененныхДанных,
											РезультатЗаполнения.КоличествоДанныхКОбработке,
											РезультатЗаполнения.Статус);
	
КонецПроцедуры

Процедура ОповеститьПользователяОбУстановкеСтатуса(КоличествоОбработанных, КоличествоВсего, Статус)
	
	Если КоличествоОбработанных > 0 Тогда
		ТекстЗаголовка = НСтр("ru='Статус ""%1"" установлен'");
		ТекстЗаголовка = СтрШаблон(ТекстЗаголовка, Статус);
		
		ТекстСообщения = НСтр("ru='Для %1 из %2 выделенных в списке сертификатов номенклатуры установлен статус ""%3""'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, КоличествоОбработанных, КоличествоВсего, Статус);
	Иначе
		ТекстЗаголовка = НСтр("ru='Статус ""%1"" не установлен'");
		ТекстЗаголовка = СтрШаблон(ТекстЗаголовка, Статус);
		
		ТекстСообщения = НСтр("ru='Статус ""%1"" не установлен ни для одного сертификата номенклатуры'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Статус);
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(ТекстЗаголовка, , ТекстСообщения, БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

Процедура ВывестиСообщенияФоновогоЗадания(Сообщения)
	
	СообщенияЗадания = Новый Массив(Сообщения);
	
	Для Каждого СообщениеПредупреждения Из СообщенияЗадания Цикл
		ОбщегоНазначенияКлиент.СообщитьПользователю(СообщениеПредупреждения.Текст, СообщениеПредупреждения.КлючДанных);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
