// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

// Заполнение реквизитов формы.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма:
//   * Объект - ДанныеФормыСтруктураСКоллекцией - документ-основание на форме:
//   ** Ссылка - ОпределяемыйТип.ДокументыСПредварительнойПередачейМеждуОрганизациями - ссылка на объект.
//   * ТекстПерейтиКОбработкеПередачиТоваровМеждуОрганизациями - ФорматированнаяСтрока - текст гиперссылки на форме.
Процедура ЗаполнениеРеквизитовФормыДокументаОснования(Форма) Экспорт
	
	Состояние = ОформлениеПередачиТоваровМеждуОрганизациямиВызовСервера.СостояниеПередачиМеждуОрганизациями(Форма.Объект.Ссылка);
	Форма.ТекстПерейтиКОбработкеПередачиТоваровМеждуОрганизациями = Состояние.Состояние;
	Форма.Элементы.ТекстПерейтиКОбработкеПередачиТоваровМеждуОрганизациями.Видимость = ЗначениеЗаполнено(Состояние.Состояние);
	
	ВозможныеЭлементыФормы = Новый Массив; //Массив Из Строка
	// команды ввода на основании
	ВозможныеЭлементыФормы.Добавить("ТекстДокументаЕГАИС");
	ВозможныеЭлементыФормы.Добавить("ТекстДокументаВЕТИС");
	ВозможныеЭлементыФормы.Добавить("ТекстДокументаИСМП");
	ВозможныеЭлементыФормы.Добавить("ТекстДокументаЗЕРНО");
	ВозможныеЭлементыФормы.Добавить("ТекстДокументаСАТУРН");
	
	Для Каждого ИмяЭлемента Из ВозможныеЭлементыФормы Цикл
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Элементы, ИмяЭлемента) Тогда
			Форма.Элементы[ИмяЭлемента].Видимость = Не ЗначениеЗаполнено(Состояние.Состояние)
				И ЗначениеЗаполнено(Форма[ИмяЭлемента]);
		КонецЕсли;
	КонецЦикла;
	
	// Статус проверки ГИСМТ
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Элементы, "ГруппаСоответствиеТребованиямГИСМТГосИС") Тогда
		Форма.Элементы["ГруппаСоответствиеТребованиямГИСМТГосИС"].Видимость = Не Состояние.ПередачаДоступна Или Не ЗначениеЗаполнено(Состояние.Состояние);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
