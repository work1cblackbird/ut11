
#Область ПрограммныйИнтерфейс

// Открывает кассовую смену для кассы ККМ.
//
// Параметры:
//  КассаККМ - СправочникСсылка.КассыККМ - Касса ККМ.
//  ДополнительныеПараметры - Структура - 
//  ОписаниеОшибки - Строка - Описание ошибки.
//
// Возвращаемое значение:
//  Булево - Истина, Если операция выполнена успешно.
//
Функция ОткрытьКассовуюСмену(КассаККМ, ДополнительныеПараметры, ОписаниеОшибки = "") Экспорт
	
	Возврат РозничныеПродажи.ОткрытьКассовуюСмену(КассаККМ, ДополнительныеПараметры, ОписаниеОшибки);
	
КонецФункции

// Закрывает кассовую смену для кассы ККМ.
//
// Параметры:
//  Объект - СправочникСсылка.КассыККМ, СправочникСсылка.ПодключаемоеОборудование - Касса ККМ.
//  ОшибкаПриСнятииZОтчета - Булево - Признак успешного снятия z-отчета.
//  ОписаниеОшибки - Строка - Выходной параметр - описание ошибки.
//
// Возвращаемое значение:
//  Массив - Массив созданных отчетов о розничных продажах.
//
Функция ЗакрытьКассовуюСмену(Объект, ОшибкаПриСнятииZОтчета, ОписаниеОшибки = "") Экспорт
	
	Возврат РозничныеПродажи.ВыполнитьОперациюЗакрытияКассовойСмены(Объект, ОшибкаПриСнятииZОтчета, ОписаниеОшибки);
	
КонецФункции

// Создать документ внесение денежных средств в кассу ККМ.
//
// Параметры:
//  ВходныеДанные - Структура - Реквизиты документа.
//  СсылкаНаДокументВнесение - Неопределено, ДокументСсылка.ВнесениеДенежныхСредствВКассуККМ - Необязательный, ссылка на созданный документ, в случае неудачи - Неопределено.
//  ОписаниеОшибки - Строка - Необязательный, Описание ошибки.
// 
// Возвращаемое значение:
//   Булево - Истина, если создание документа выполнено успешно.
//
Функция СоздатьДокументВнесениеДенежныхСредствВКассуККМ(ВходныеДанные, СсылкаНаДокументВнесение = Неопределено, ОписаниеОшибки = Неопределено) Экспорт
	
	Результат = Истина;
	
	Попытка
		
		СсылкаНаДокументВнесение = РозничныеПродажи.СоздатьДокументВнесениеДенежныхСредствВКассуККМ(ВходныеДанные);
		
	Исключение
		
		Результат = Ложь;
		СсылкаНаДокументВнесение = Неопределено;
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Розничные продажи'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		                         УровеньЖурналаРегистрации.Ошибка, , ,
		                         НСтр("ru = 'Во время создания документа внесения денежных средств в кассу ККМ произошла ошибка.'")
		                         + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Создать документ выемка денежных средств из кассы ККМ.
//
// Параметры:
//  ВходныеДанные - Структура - Структура с реквизитами документа.
//  СсылкаНаДокументВыемка - Неопределено, ДокументСсылка.ВыемкаДенежныхСредствИзКассыККМ - Необязательный, ссылка на созданный документ, в случае неудачи - Неопределено.
//  ОписаниеОшибки - Строка - Необязательный, Описание ошибки.
// 
// Возвращаемое значение:
//   Булево - Истина, если создание документа выполнено успешно.
//
Функция СоздатьДокументВыемкаДенежныхСредствИзКассыККМ(ВходныеДанные, СсылкаНаДокументВыемка = Неопределено, ОписаниеОшибки = Неопределено) Экспорт
	
	Результат = Истина;
	
	Попытка
		
		СсылкаНаДокументВыемка = РозничныеПродажи.СоздатьДокументВыемкаДенежныхСредствИзКассыККМ(ВходныеДанные);
		
	Исключение
		
		Результат = Ложь;
		СсылкаНаДокументВыемка = Неопределено;
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Розничные продажи'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		                         УровеньЖурналаРегистрации.Ошибка, , ,
		                         НСтр("ru = 'Во время создания документа выемки денежных средств из кассы ККМ произошла ошибка.'")
		                         + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Удаляет документ при отмене фискальной операции.
//
// Параметры:
//  Ссылка - ДокументСсылка - Ссылка на удаляемый документ.
// 
// Возвращаемое значение:
//   Булево - Истина, если удаление документа выполнено успешно.
//
Функция УдалитьДокументПриОтменеФискальнойОперации(Ссылка) Экспорт
	
	Результат = Истина;
	
	Если Ссылка = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ДокументОбъект = Ссылка.ПолучитьОбъект(); // ДокументОбъект

	Если Не ДокументОбъект = Неопределено Тогда
		Если (ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЧекККМ") Или
			ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЧекККМВозврат") Или
			ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЧекККМКоррекции") Или
			ТипЗнч(Ссылка) = Тип("ДокументСсылка.РеализацияПодарочныхСертификатов") Или
			ТипЗнч(Ссылка) = Тип("ДокументСсылка.ВозвратПодарочныхСертификатов"))
			И ДокументОбъект.Статус = Перечисления.СтатусыЧековККМ.Пробит Тогда

			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Пробитый чек не может быть удален'"));
			Результат = Ложь;

		Иначе
			
			ДокументОбъектТип = ТипЗнч(ДокументОбъект.Ссылка);
			ДокументОбъектНомер = ДокументОбъект.Номер;
			ДокументОбъектДата = ДокументОбъект.Дата;
			
			Попытка			
				Если Не ДокументОбъект.ПометкаУдаления Тогда
					ДокументОбъект.УстановитьПометкуУдаления(Истина);
				КонецЕсли;
				СсылкиНаУдаление = Новый Массив;
				СсылкиНаУдаление.Добавить(ДокументОбъект.Ссылка);
				УдалитьОбъекты(СсылкиНаУдаление, Ложь);
			Исключение
				Результат = Ложь;
				ШаблонТекстаОшибки = НСтр("ru = 'При отмене фискальной операции не удалось удалить документ %1 %2 от %3 по причине: %4'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()); 
				ТекстОшибки = СтрШаблон(ШаблонТекстаОшибки, ДокументОбъектТип, ДокументОбъектНомер, ДокументОбъектДата, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ИмяСобытияЖурнала = НСтр("ru = 'Розничные продажи'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
				ЗаписьЖурналаРегистрации(ИмяСобытияЖурнала, УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			КонецПопытки;
			
		КонецЕсли;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции


// Получает структуру описания кассовой смены для кассы ККМ.
//
// Параметры:
//  КассаККМ - СправочникСсылка.КассыККМ - Касса ККМ.
//
// Возвращаемое значение:
//  см. РозничныеПродажи.ОписаниеКассовойСмены
//
Функция СостояниеКассовойСмены(КассаККМ) Экспорт
	
	Возврат РозничныеПродажи.ПолучитьСостояниеКассовойСмены(КассаККМ);
	
КонецФункции

// Получает штрихкоды номенклатуры
//
// Параметры:
//  Структура - Структура - Структура переданных параметров.
// 
// Возвращаемое значение:
//  Массив - Массив штрихкодов номенклатуры.
//
Функция ПолучитьШтрихкодыНоменклатуры(Структура) Экспорт
	
	МассивШтрихкодов = Новый Массив();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК КоличествоЕдиничныхУпаковок,
	|	УпаковкиНоменклатуры.Владелец КАК Владелец,
	|	УпаковкиНоменклатуры.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ УпаковкиНоменклатуры
	|ИЗ
	|	Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиНоменклатуры
	|ГДЕ
	|	УпаковкиНоменклатуры.ПометкаУдаления = ЛОЖЬ
	|	И (УпаковкиНоменклатуры.Владелец = &Владелец
	|		ИЛИ УпаковкиНоменклатуры.Владелец = &НаборУпаковок)
	|	И УпаковкиНоменклатуры.Владелец.ЕдиницаИзмерения = УпаковкиНоменклатуры.ЕдиницаИзмерения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УпаковкиНоменклатуры.КоличествоЕдиничныхУпаковок КАК КоличествоЕдиничныхУпаковок,
	|	УпаковкиНоменклатуры.Владелец КАК Владелец,
	|	УпаковкиНоменклатуры.Ссылка КАК Ссылка
	|ИЗ
	|	УпаковкиНоменклатуры КАК УпаковкиНоменклатуры
	|ГДЕ
	|	УпаковкиНоменклатуры.КоличествоЕдиничныхУпаковок = 1";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковки",
				Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки("УпаковкиНоменклатуры", Неопределено));
	НаборУпаковок = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Структура.Номенклатура,"НаборУпаковок");
	Запрос.УстановитьПараметр("НаборУпаковок", НаборУпаковок);
	Запрос.УстановитьПараметр("Владелец", Структура.Номенклатура);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЕдиничнаяУпаковка = Выборка.Ссылка;
		// Поиск штрихкодов по единичной упаковке и по единице измерения имеет смысл только тогда,
		// когда у номенклатуры 1 упаковка с коэффициентом 1 и ед. изм. 1.
		Если Не Выборка.Следующий()  Тогда
			Если ЕдиничнаяУпаковка = Структура.Упаковка Тогда
				МассивШтрихкодов = РегистрыСведений.ШтрихкодыНоменклатуры.ШтрихкодыНоменклатуры(Структура.Номенклатура,
																								Структура.Характеристика,
																								Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
			Иначе
				МассивШтрихкодов = РегистрыСведений.ШтрихкодыНоменклатуры.ШтрихкодыНоменклатуры(Структура.Номенклатура,
																								Структура.Характеристика,
																								ЕдиничнаяУпаковка);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если МассивШтрихкодов.Количество() = 0 Тогда
		
		МассивШтрихкодов = РегистрыСведений.ШтрихкодыНоменклатуры.ШтрихкодыНоменклатуры(Структура.Номенклатура,
																						Структура.Характеристика,
																						Структура.Упаковка);
					
	Иначе
		
		МассивШтрихкодовУпаковкиСтруктуры = РегистрыСведений.ШтрихкодыНоменклатуры.ШтрихкодыНоменклатуры(Структура.Номенклатура,
																										 Структура.Характеристика,
																										 Структура.Упаковка);
					
		Для Каждого Штрихкод Из МассивШтрихкодовУпаковкиСтруктуры Цикл 
			МассивШтрихкодов.Добавить(Штрихкод);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат МассивШтрихкодов;
	
КонецФункции

// Заполнить реквизит формы "РеквизитыКассира".
//
// Параметры:
//  Кассир - СправочникСсылка.Пользователи - Кассир.
//  Организация - СправочникСсылка.Организации - Организация для отбора в справочнике ОтветственныеЛицаОрганизаций.
//  ДатаВремя - Дата - Для отбора по периоду ДатаНачала - ДатаОкончания в справочнике ОтветственныеЛицаОрганизаций.
//
// Возвращаемое значение:
//  см. РозничныеПродажи.РеквизитыКассира
// 
Функция РеквизитыКассира(Знач Кассир = Неопределено, Знач Организация = Неопределено, Знач ДатаВремя = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Кассир) Тогда
		Кассир = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Возврат РозничныеПродажи.РеквизитыКассира(Кассир, Организация, ДатаВремя);
	
КонецФункции

// Возвращает значение организации по подключаемому оборудованию
// 
// Параметры:
//  ФискальноеУстройство - СправочникСсылка.ПодключаемоеОборудование
// 
// Возвращаемое значение:
//  Неопределено, Произвольный - Организация фискального устройства
Функция ОрганизацияФискальногоУстройства(Знач ФискальноеУстройство) Экспорт
	
	Если Не ТипЗнч(ФискальноеУстройство) = Тип("СправочникСсылка.ПодключаемоеОборудование") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ФискальноеУстройство) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФискальноеУстройство, "Организация");
	
КонецФункции

Функция РеквизитыАдресаМестаРасчетов(Знач ФискальноеУстройство) Экспорт
	
	РеквизитыАдресаМестаРасчетов = Новый Структура;
	РеквизитыАдресаМестаРасчетов.Вставить("АдресРасчетов", "");
	РеквизитыАдресаМестаРасчетов.Вставить("МестоРасчетов", "");
	
	КассаККМ = Справочники.КассыККМ.КассаККМПоПодключаемомуОборудованияДляРМК(ФискальноеУстройство);
	
	Если КассаККМ <> Неопределено Тогда
		РеквизитыКассыККМ = Справочники.КассыККМ.РеквизитыКассыККМ(КассаККМ);
		
		Если ТипЗнч(РеквизитыКассыККМ) = Тип("Структура")
			И РеквизитыКассыККМ.Свойство("Склад") Тогда
			
			Склад = РеквизитыКассыККМ.Склад;
			Если ЗначениеЗаполнено(Склад) И ТипЗнч(Склад) = Тип("СправочникСсылка.Склады") Тогда
				РеквизитыАдресаМестаРасчетов.АдресРасчетов = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Склад, Справочники.ВидыКонтактнойИнформации.АдресСклада);
				РеквизитыАдресаМестаРасчетов.МестоРасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад, "Наименование");
			КонецЕсли;
			
		КонецЕсли;
			
	КонецЕсли;
	
	Возврат РеквизитыАдресаМестаРасчетов;
	
КонецФункции

Функция ИспользуетсяККТФЗ54(КассоваяСмена) Экспорт
	
	ИспользуетсяККТФЗ54 = Ложь;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КассоваяСмена.ФискальноеУстройство.ТипОборудования КАК ТипОборудования
	|ИЗ
	|	Документ.КассоваяСмена КАК КассоваяСмена
	|ГДЕ
	|	КассоваяСмена.Ссылка = &КассоваяСмена
	|");
	Запрос.УстановитьПараметр("КассоваяСмена", КассоваяСмена);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ИспользуетсяККТФЗ54 = (Выборка.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ККТ);
		
	КонецЕсли;
	
	Возврат ИспользуетсяККТФЗ54;
	
КонецФункции

Функция НайтиПартнераПоКонтактнымДаннымЭлектронногоЧека(ВариантОтправкиЭлектронногоЧека, КонтактныеДанные) Экспорт
	
	ВозвращаемоеЗначение = Неопределено;
	
	Если ВариантОтправкиЭлектронногоЧека = Перечисления.ВариантыОтправкиЭлектронногоЧекаПокупателю.ОтправитьSMS Тогда
		
		ПараметрПоиска = "%" + Прав(КонтактныеДанные, 10);
		ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПартнерыКонтактнаяИнформация.Ссылка,
		|	ПартнерыКонтактнаяИнформация.Ссылка.ВариантОтправкиЭлектронногоЧека КАК ВариантОтправкиЭлектронногоЧека,
		|	ПартнерыКонтактнаяИнформация.АдресЭП КАК АдресЭП,
		|	ПартнерыКонтактнаяИнформация.НомерТелефона КАК НомерТелефона
		|ИЗ
		|	Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
		|ГДЕ
		|	ПартнерыКонтактнаяИнформация.НомерТелефона ПОДОБНО &КонтактныеДанные";
		
	ИначеЕсли ВариантОтправкиЭлектронногоЧека = Перечисления.ВариантыОтправкиЭлектронногоЧекаПокупателю.ОтправитьEmail Тогда
		
		ПараметрПоиска = КонтактныеДанные;
		ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПартнерыКонтактнаяИнформация.Ссылка,
		|	ПартнерыКонтактнаяИнформация.Ссылка.ВариантОтправкиЭлектронногоЧека КАК ВариантОтправкиЭлектронногоЧека,
		|	ПартнерыКонтактнаяИнформация.АдресЭП КАК АдресЭП,
		|	ПартнерыКонтактнаяИнформация.НомерТелефона КАК НомерТелефона
		|ИЗ
		|	Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
		|ГДЕ
		|	ПартнерыКонтактнаяИнформация.АдресЭП = &КонтактныеДанные";
		
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("КонтактныеДанные", ПараметрПоиска);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ВозвращаемоеЗначение = Новый Структура;
		ВозвращаемоеЗначение.Вставить("Партнер",                         Выборка.Ссылка);
		ВозвращаемоеЗначение.Вставить("ВариантОтправкиЭлектронногоЧека", Выборка.ВариантОтправкиЭлектронногоЧека);
		ВозвращаемоеЗначение.Вставить("Email",                           Выборка.АдресЭП);
		ВозвращаемоеЗначение.Вставить("Телефон",                         РозничныеПродажиКлиентСервер.НомерТелефонаВФормате10Знаков(Выборка.НомерТелефона));
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Возвращает статус партнера, является ли он розничным клиентом.
// 
// Параметры:
//  Партнер - СправочникСсылка.Партнеры - Партнер
// 
// Возвращаемое значение:
//  Булево - Партнер это розничный клиент
Функция ПартнерЭтоРозничныйКлиент(Партнер) Экспорт
	
	ЭтоРозничныйКлиент = Ложь;
	
	ЮрФизЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Партнер, "ЮрФизЛицо");
	Если ЮрФизЛицо = Перечисления.КомпанияЧастноеЛицо.ЧастноеЛицо Тогда
		ЭтоРозничныйКлиент = Истина;
	КонецЕсли;
	
	Возврат ЭтоРозничныйКлиент;
	
КонецФункции

// Возвращает статус контрагента, является ли он физическим лицом.
// 
// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты - Контрагент
// 
// Возвращаемое значение:
//  Булево - Контрагент - это физическое лицо
Функция КонтрагентЭтоФизическоеЛицо(Контрагент) Экспорт
	
	ЭтоРозничныйКлиент = Ложь;
	
	ЮридическоеФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "ЮрФизЛицо");
	Если ЮридическоеФизическоеЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		ЭтоРозничныйКлиент = Истина;
	КонецЕсли;
	
	Возврат ЭтоРозничныйКлиент;
	
КонецФункции

// Функция выполняет получение параметров кассы ККМ.
//
// Параметры:
//  КассаККМ - СправочникСсылка.КассыККМ - Касса ККМ.
// 
// Возвращаемое значение:
//  Структура - со свойствами:
//   * ИдентификаторУстройства - Строка - Идентификатор устройства.
//   * ИспользоватьБезПодключенияОборудования - Булево - Использовать без подключения оборудования.
//   * ЭтоФискальныйРегистратор - Булево - Признак фискального регистратора.
//   * ИспользоватьСкладскиеПомещения - Булево - Признак использования складских помещений.
//   * КассаККМ - СправочникСсылка.КассыККМ - Касса ККМ.
//   * НастройкиРМК - СправочникСсылка.НастройкиРМК - Ссылка на настройки РМК.
Функция ПараметрыКассыККМ(КассаККМ) Экспорт
	
	Возврат Новый ФиксированнаяСтруктура(Справочники.КассыККМ.ПараметрыКассыККМ(КассаККМ));
	
КонецФункции

// Функция определяет ККМ по ПодключаемомуОборудованию для текущей РМК.
//
// Возвращаемое значение:
//	 СправочникСсылка.КассыККМ - Найденная касса ККМ.
//
Функция КассаККМПоПодключаемомуОборудованияДляРМК(ФискальноеУстройство) Экспорт

	Возврат Справочники.КассыККМ.КассаККМПоПодключаемомуОборудованияДляРМК(ФискальноеУстройство);
	
КонецФункции

// Возвращает возможность фискализации
// 
// Возвращаемое значение:
// 	Булево
Функция ФискализацияДоступна() Экспорт

	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьРозничныеПродажи")
		И ПолучитьФункциональнуюОпцию("ИспользоватьПодключаемоеОборудование");

КонецФункции

// Возвращает возможность удаленной фискализации
// 
// Возвращаемое значение:
// 	Булево
Функция УдаленнаяФискализацияДоступна() Экспорт

	Возврат ФискализацияДоступна() И СистемаВзаимодействия.ИспользованиеДоступно();

КонецФункции

// Возвращает возможность печатать отдельный слип чек эквайринговой операции.
// Если Истина, то предполагается, что слип-чек эквайрингового терминала выводится отдельно и
// фискальный чек не содержит данные эквайринговой операции.
// 
// Возвращаемое значение:
// 	Булево
Функция ПечататьОтдельныйСлипЧекЭквайринговойОперации() Экспорт

	Возврат РозничныеПродажиЛокализация.ПечататьОтдельныйСлипЧекЭквайринговойОперации();

КонецФункции

// Возвращает возможность печатать отдельный слип чек ЕГАИС.
// Если Истина, то предполагается, что слип-чек ЕГАИС выводится отдельно.
// 
// Возвращаемое значение:
// 	Булево
Функция ПечататьОтдельныйСлипЧекЕГАИС() Экспорт

	Возврат РозничныеПродажиЛокализация.ПечататьОтдельныйСлипЧекЕГАИС();

КонецФункции

// Записывает конфиденциальные данные в безопасное хранилище.
// Вызывающий код должен самостоятельно устанавливать привилегированный режим.
//
// Безопасное хранилище недоступно для чтения пользователям (кроме администраторов),
// а доступно только коду, который делает обращения только к своей части данных и
// в том контексте, который предполагает чтение или запись конфиденциальных данных.
//
// Параметры:
//  Владелец - ПланОбменаСсылка
//           - СправочникСсылка
//           - Строка - ссылка на объект информационной базы,
//             представляющий объект-владелец сохраняемого пароля или строка до 128 символов.
//             Для объектов других типов в качестве владельца рекомендуется использовать ссылку на
//             элемент метаданных этого типа в справочнике ИдентификаторыОбъектовМетаданных
//             или ключ в виде строки с учетом имен подсистем.
//             Например, для БСП:
//               Владелец = ОбщегоНазначения.ИдентификаторОбъектаМетаданных("РегистрСведений.АдресныеОбъекты");
//             если нужно 1 хранилище на подсистему БСП:
//               Владелец = "СтандартныеПодсистемы.УправлениеДоступом";
//             если нужно более 1 хранилища на подсистему БСП:
//               Владелец = "СтандартныеПодсистемы.УправлениеДоступом.<Уточнение>";
//
//  Данные  - Произвольный - данные помещаемые в безопасное хранилище. Неопределенно - удаляет все данные.
//             Для удаления данных по ключу следует использовать процедуру УдалитьДанныеИзБезопасногоХранилища.
//  Ключ    - Строка       - ключ сохраняемых настроек, по умолчанию "Пароль".
//                           Ключ должен соответствовать правилам имен идентификаторов:
//                           1. Первым символом ключа должна быть буква или символ подчеркивания (_).
//                           2. Каждый из последующих символов может быть буквой, цифрой или символом подчеркивания (_).
//
// Пример:
//  Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
//      Если ТекущийПользовательМожетИзменятьПароль Тогда
//          УстановитьПривилегированныйРежим(Истина);
//          ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(ТекущийОбъект.Ссылка, Логин, "Логин");
//          ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(ТекущийОбъект.Ссылка, Пароль);
//          УстановитьПривилегированныйРежим(Ложь);
//      КонецЕсли;
//  КонецПроцедуры
//
Процедура ЗаписатьДанныеВБезопасноеХранилище(Владелец, Данные, Ключ = "Пароль") Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Владелец, Данные, Ключ);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Удаляет конфиденциальные данные в безопасное хранилище.
// Вызывающий код должен самостоятельно устанавливать привилегированный режим.
//
// Безопасное хранилище недоступно для чтения пользователям (кроме администраторов),
// а доступно только коду, который делает обращения только к своей части данных и
// в том контексте, который предполагает чтение или запись конфиденциальных данных.
//
// Параметры:
//  Владелец - ПланОбменаСсылка
//           - СправочникСсылка
//           - Строка - ссылка на объект информационной базы,
//               представляющий объект-владелец сохраняемого пароля или строка до 128 символов.
//  Ключи    - Строка - содержит список имен удаляемых данных, указанных через запятую. 
//               Неопределено - удаляет все данные.
//
// Пример:
//	Процедура ПередУдалением(Отказ)
//		
//		// Проверка значения свойства ОбменДанными.Загрузка отсутствует, так как удалять данные
//		// из безопасного хранилища нужно даже при удалении объекта при обмене данными.
//		
//		УстановитьПривилегированныйРежим(Истина);
//		ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(Ссылка);
//		УстановитьПривилегированныйРежим(Ложь);
//		
//	КонецПроцедуры
//
Процедура УдалитьДанныеИзБезопасногоХранилища(Владелец, Ключи = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(Владелец, Ключи);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Определяет, является ли номенклатура алкогольной продукцией, имеющая уникальный идентификатор (маркируовку)
// 
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура - Проверяемая номенклатура
// 
// Возвращаемое значение:
//  Булево - Истина, если номенклатура является алкогольной продукцией, имеющая уникальный идентификатор (маркируовку)
Функция АлкогольнаяПродукцияЕГАИСМаркируемая(Номенклатура) Экспорт
	
	Возврат РозничныеПродажиЛокализация.АлкогольнаяПродукцияЕГАИСМаркируемая(Номенклатура);
	
КонецФункции

// Для регистрации ошибок, возникающих на стороне клиента при использования слепых асинхронных вызовов,
//  например, ПродажиКлиент.ПечатьЧека_Завершение.
//  В записи используется независимый режим транзакции см. Синтакс-помощник ЗаписьЖурналаРегистрации.
// Параметры:
//  ИмяСобытия - Строка
//  ОбъектМетаданных - ОбъектМетаданных -
//  Данные - ЛюбаяСсылка, Число, Строка, Дата, Булево, Неопределено, Null, Тип - 
//  Комментарий - Строка
//
//@skip-check method-too-many-params
Процедура ЗаписьЖурналаУровеньОшибка(ИмяСобытия = "", ОбъектМетаданных = Неопределено, Данные = Неопределено, Комментарий = "") Экспорт
	
	ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, ОбъектМетаданных, Данные, Комментарий, РежимТранзакцииЗаписиЖурналаРегистрации.Независимая);
	
КонецПроцедуры

// Вычисляет есть ли проведенные документы отгрузки введенные на основании заказа.
// 
// Параметры:
//  Ссылка - ДокументСсылка.ЗаказКлиента, ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента - 
//
// Возвращаемое значение:
//  Булево -
//
Функция НаОснованииЗаказаОформленаОтгрузка(Ссылка) Экспорт
	Результат = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.ЗаказКлиента = &Ссылка
	|	И РеализацияТоваровУслуг.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	Документ.АктВыполненныхРабот КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.ЗаказКлиента = &Ссылка
	|	И РеализацияТоваровУслуг.Проведен
	|";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = Не Запрос.Выполнить().Пустой();
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура заполняет переданными значениями реквизиты "КассаККМ" и "Организация" у кассовой смены.
// 
// Параметры:
// 	КассоваяСмена - ДокументСсылка.КассоваяСмена - кассовая смена
// 	КассаККМ - СправочникСсылка.КассыККМ - устанавливаемое значение
// 	Организация - СправочникСсылка.Организации - устанавливаемое значение
// 
Процедура ЗаполнитьОрганизациюКассуККМ(КассоваяСмена, КассаККМ, Организация) Экспорт
	
	КассоваяСменаОбъект = КассоваяСмена.ПолучитьОбъект();
	КассоваяСменаОбъект.КассаККМ    = КассаККМ;
	КассоваяСменаОбъект.Организация = Организация;
	КассоваяСменаОбъект.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры

// Параметры:
//	Оборудование - см. РозничныеПродажи.ПолучитьПараметрыОбработкиСостоянияСмены.Оборудование
//
// Возвращаемое значение:
//	см. РозничныеПродажи.ПолучитьПараметрыОбработкиСостоянияСмены
//
Функция ПолучитьПараметрыОбработкиСостоянияСмены(Оборудование) Экспорт
	
	Возврат РозничныеПродажи.ПолучитьПараметрыОбработкиСостоянияСмены(Оборудование);
	
КонецФункции

// Получает оборудование ЭТ с признаком СверятьИтогиНаЭТПриЗакрытииКассовойСмены
// 
// Параметры:
//  Оборудование - СправочникСсылка.ПодключаемоеОборудование - Идентификатор устройства ККТ
// 
// Возвращаемое значение:
//  СправочникСсылка.ПодключаемоеОборудование - оборудование ЭТ.
//
Функция ОборрудованиеЭквайринговогоТерминалаДляСверкиИтогов(Оборудование) Экспорт
	Возврат	Справочники.ЭквайринговыеТерминалы.ПодключаемоеОборудованиеЭквайринговогоТерминалаДляСверкиИтогов(Оборудование);
КонецФункции

// Параметры:
//	ТекстОшибки - см. РозничныеПродажи.ЗаписьВЖурналРегистрации.ТекстОшибки
//	Событие - см. РозничныеПродажи.ЗаписьВЖурналРегистрации.Событие
//	Данные - см. РозничныеПродажи.ЗаписьВЖурналРегистрации.Данные
//	Уровень - см. РозничныеПродажи.ЗаписьВЖурналРегистрации.Уровень
Процедура ЗаписьВЖурналРегистрации(Знач ТекстОшибки, Знач Событие, Данные = Неопределено, Знач Уровень = "Ошибка") Экспорт
	РозничныеПродажи.ЗаписьВЖурналРегистрации(ТекстОшибки, Событие, Данные, Уровень);
КонецПроцедуры

//++ Локализация

#Область Локализация

// По возврату оплаты СБП определяет основание платежа
// 
// Параметры:
//  ДокументОплаты - ОпределяемыйТип.ДокументОперацииСБП - Документ операции СБП
// 
// Возвращаемое значение:
//  - Неопределено
//  - см. РозничныеПродажиКлиентСерверЛокализация.ДанныеДляВозвратаПоДокументуОплатыСБП
//
Функция ДанныеДляВозвратаПоДокументуОплатыСБП(ДокументОплаты) Экспорт

	ДанныеДляВозврата = Неопределено;
	
	Если ТипЗнч(ДокументОплаты) = Тип("ДокументСсылка.ОперацияПоПлатежнойКарте") Тогда

		РеквизитыДокументаОплаты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОплаты, "ДоговорЭквайринга.СпособПроведенияПлатежа,ОплатаВыполнена,ХозяйственнаяОперация,РасшифровкаПлатежа");

		Если РеквизитыДокументаОплаты.ДоговорЭквайрингаСпособПроведенияПлатежа = Перечисления.СпособыПроведенияПлатежей.СистемаБыстрыхПлатежей
			И РеквизитыДокументаОплаты.ОплатаВыполнена = Истина
			И РеквизитыДокументаОплаты.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента			
			И Не РеквизитыДокументаОплаты.РасшифровкаПлатежа.Пустой() Тогда
			
			ВыборкаРасшифровкиПлатежа = РеквизитыДокументаОплаты.РасшифровкаПлатежа.Выбрать();
			Если ВыборкаРасшифровкиПлатежа.Количество() = 1 Тогда
				ВыборкаРасшифровкиПлатежа.Следующий();

				ТипОснованияПлатежа = ТипЗнч(ВыборкаРасшифровкиПлатежа.ОснованиеПлатежа);
				Если ТипОснованияПлатежа = Тип("ДокументСсылка.ЗаказКлиента")
					Или ТипОснованияПлатежа = Тип("ДокументСсылка.СчетНаОплатуКлиенту")
					Или ТипОснованияПлатежа = Тип("ДокументСсылка.АктВыполненныхРабот")
					Или ТипОснованияПлатежа = Тип("ДокументСсылка.РеализацияТоваровУслуг")
					Или ТипОснованияПлатежа = Тип("ДокументСсылка.РеализацияУслугПрочихАктивов") Тогда
					
					ДанныеДляВозврата = РозничныеПродажиКлиентСерверЛокализация.ДанныеДляВозвратаПоДокументуОплатыСБП();
					ДанныеДляВозврата.ОснованиеПлатежа = ВыборкаРасшифровкиПлатежа.ОснованиеПлатежа;
					ДанныеДляВозврата.Партнер = ВыборкаРасшифровкиПлатежа.Партнер;
					ДанныеДляВозврата.Сумма = ВыборкаРасшифровкиПлатежа.Сумма;
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
				
	КонецЕсли;
	
	Возврат ДанныеДляВозврата;
	
КонецФункции

// Возвращает заводской номер ФН
// 
// Параметры:
//  ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - Идентификатор устройства
// 
// Возвращаемое значение:
//  Строка - номер ФН
Функция ЗаводскойНомерФискальногоНакопителя(ИдентификаторУстройства) Экспорт
	
	Возврат РозничныеПродажиЛокализация.ЗаводскойНомерФискальногоНакопителя(ИдентификаторУстройства);
	
КонецФункции

// Проверяет можно ли продавать номенклатуру документа по патентной СНО.
// 
// Параметры:
//  Объект - см. ПродажиСерверЛокализация.РазрешенаРозничнаяПродажаНоменклатурыПоПатенту.Объект
//  СообщитьОбОшибке - см. ПродажиСерверЛокализация.РазрешенаРозничнаяПродажаНоменклатурыПоПатенту.СообщитьОбОшибке
//
// Возвращаемое значение:
//  Булево -
Функция РазрешенаРозничнаяПродажаНоменклатурыПоПатенту(Знач Объект, СообщитьОбОшибке) Экспорт
	Результат = ПродажиСерверЛокализация.РазрешенаПродажаНоменклатурыПоПатенту(Объект, СообщитьОбОшибке);
	Возврат Результат;
КонецФункции

#КонецОбласти

//-- Локализация

#КонецОбласти
