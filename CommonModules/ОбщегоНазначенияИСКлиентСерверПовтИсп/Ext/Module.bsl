
#Область СлужебныйПрограммныйИнтерфейс

Функция ЭтоРасширеннаяВерсияГосИС(Подсистема = Неопределено) Экспорт
	
	Возврат ОбщегоНазначенияИСВызовСервера.ЭтоРасширеннаяВерсияГосИС(Подсистема);
	
КонецФункции

Функция ИменаПараметровРаботыКлиентаВыборТипаПодписиБСП() Экспорт
	
	ВозвращаемоеЗначение = Новый Структура();
	
	ВозвращаемоеЗначение.Вставить("ИмяПараметраПриложения", "ГОСИС_ВыборТипаПодписиБСП");
	ВозвращаемоеЗначение.Вставить(
		"ЗначениеПараметраВыборТипаПодписиНеПоддерживается",
		"ИнтеграцияГОСИС.ВыборТипаПодписиБСП.ВыборТипаПодписиНеПоддерживается");
	ВозвращаемоеЗначение.Вставить(
		"ЗначениеПараметраВыборТипаПодписиИспользуетКонструктор",
		"ИнтеграцияГОСИС.ВыборТипаПодписиБСП.ВыборТипаПодписиИспользуетКонструктор");
	ВозвращаемоеЗначение.Вставить(
		"ЗначениеПараметраВыборТипаПодписиЗадаетсяПеречислением",
		"ИнтеграцияГОСИС.ВыборТипаПодписиБСП.ВыборТипаПодписиЗадаетсяПеречислением");
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Возвращает признак принадлежности переданного в параметре вида продукции к наборам или групповым упаковкам.
// 
// Параметры:
// 	ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции для анализа.
// 	ВидУпаковки - ПеречислениеСсылка.ВидыУпаковокИС
// Возвращаемое значение:
// 	Булево - Принадлежность к виду продукции ИСМП
//
Функция ВидПродукцииИспользуетНаборыИлиГрупповыеУпаковки(ВидПродукции, ВидУпаковки = Неопределено) Экспорт
	
	Если ВидУпаковки = Неопределено Тогда
		Если ОбщегоНазначенияИСКлиентСервер.ВидПродукцииИспользуетНаборы(ВидПродукции) Тогда
			Возврат Истина;
		ИначеЕсли ОбщегоНазначенияИСКлиентСервер.ВидПродукцииИспользуетГрупповыеУпаковки(ВидПродукции) Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	Иначе
		Если ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.Групповая")
			И ОбщегоНазначенияИСКлиентСервер.ВидПродукцииИспользуетГрупповыеУпаковки(ВидПродукции) Тогда
			Возврат Истина;
		ИначеЕсли ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.Набор")
			И ОбщегоНазначенияИСКлиентСервер.ВидПродукцииИспользуетНаборы(ВидПродукции) Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция ВидПродукцииПоддерживаетОбъемноСортовойУчет(ВидПродукции) Экспорт
	Возврат (ОбщегоНазначенияИСКлиентСервер.ВидыПродукцииОбъемноСортовогоУчета().Найти(ВидПродукции) <> Неопределено);
КонецФункции

#Область ЧастичноеВыбытие

// Возвращает признак возможности для вида продукции участвовать в частичном выбытии.
// 
// Параметры:
//  ВидПродукции    - ПеречислениеСсылка.ВидыПродукцииИС                - вид маркируемой продукции.
//  ВидОперацииИСМП - ПеречислениеСсылка.ВидыОперацийИСМП, Неопределено - вид операции ИСМП.
// Возвращаемое значение:
//  Булево - Вид продукци может выбывать частично.
Функция ПоддерживаетсяЧастичноеВыбытие(ВидПродукции, ВидОперацииИСМП = Неопределено) Экспорт
	
	Возврат ОбщегоНазначенияИСВызовСервера.ПоддерживаетсяЧастичноеВыбытие(ВидПродукции, ВидОперацииИСМП);
	
КонецФункции

Функция ЗапрашиватьДанныеЧастичногоВыбытияИзСервиса(ВидПродукции, РасширеннаяВерсияГосИС) Экспорт
	
	Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ОбщегоНазначенияИСМПКлиентСерверПовтИсп.ЗапрашиватьДанныеСервиса();
	
КонецФункции

Функция ЗаголовокРежимаВыбытияПоВидуОперации(ОперацияИС) Экспорт
	
	Если ТипЗнч(ОперацияИС) = Тип("ПеречислениеСсылка.ВидыОперацийИСМП") Тогда
		Если ОперацияИС = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаРозничнаяПродажа")
			Или ОперацияИС = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ОтгрузкаПродажа") Тогда
			Возврат НСтр("ru = 'Продается'");
		Иначе
			Возврат НСтр("ru = 'Возвращается'");
		КонецЕсли;
	Иначе
		Возврат НСтр("ru = 'Продается'");
	КонецЕсли;
	
КонецФункции

Функция ПредставлениеРежимаВыбытияПоВидуПродукции(РежимВыбытия, ВидПродукции) Экспорт
	
	ВозвращаемоеЗначение = Неопределено;
	
	Если РежимВыбытия = "Полностью" Тогда
		
		Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Духи") Тогда
			ВозвращаемоеЗначение = НСтр("ru = 'Флакон'"); 
		ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АльтернативныйТабак")
			Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.НикотиносодержащаяПродукция") Тогда
			ВозвращаемоеЗначение = НСтр("ru = 'Пачка'");
		ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво") Тогда
			ВозвращаемоеЗначение = НСтр("ru = 'Кег'");
		ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная")
			Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АлкогольнаяПродукцияДо9Процентов")
			Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПивоВПотребительскихУпаковках") Тогда
			ВозвращаемоеЗначение = НСтр("ru = 'Бутылка'");
		КонецЕсли;
		
	Иначе
		
		Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Духи")
			Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво")
			Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПивоВПотребительскихУпаковках")
			Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная") Тогда
			ВозвращаемоеЗначение = НСтр("ru = 'На разлив'"); 
		ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АльтернативныйТабак")
			Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.НикотиносодержащаяПродукция") Тогда
			ВозвращаемоеЗначение = НСтр("ru = 'Поштучно'");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

#КонецОбласти