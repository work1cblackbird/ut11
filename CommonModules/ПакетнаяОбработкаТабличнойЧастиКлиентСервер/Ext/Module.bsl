
#Область ПрограммныйИнтерфейс

#Область ПолученияСтруктурКэшируемыхЗначений

// Возвращает структуру, содержащую поля кэшируемых значений.
//
// Возвращаемое значение:
//	Структура - структура кэшируемых значений со следующими полями:
//		* КоэффициентыУпаковок - Соответствие - используется для хранения значений коэффициентов упаковок товара.
//		* ОбъемУпаковок - Соответствие - используется для хранения значений объема упаковок товара.
//		* ВесУпаковок - Соответствие - используется для хранения значений веса упаковок товара.
//		* Штрихкоды - Соответствие - используется для хранения значений штрихкодов упаковок товара.
//		* КонтролироватьЗаполнениеАналитикиРасходов - Соответствие - используется для хранения значений коэффициентов упаковок товара.
//		* РаспределятьНДСпоСтатьеРасходов - Соответствие - используется для хранения признаков распределения НДС по 
//			статьям расходов.
//		* КонтролироватьЗаполнениеАналитикиДоходов - Соответствие - используется для хранения признаков контроля заполнения 
//			по аналитике доходов.
//		* ИспользоватьРучныеСкидкиВПродажах - Неопределено, Булево - Истина - в продажах используется расчет скидок вручную.
//		* ИспользоватьАвтоматическиеСкидкиВПродажах - Неопределено, Булево - Истина - в продажах используется 
//			автоматический расчет скидок.
//		* ПравоРегистрацииШтрихкодовНоменклатурыДоступно - Неопределено, Булево - Истина - пользователю разрешено 
//			регистрировать штрихкоды номенклатуры.
//		* ПринимаетсяКНУ - Соответствие - используется для хранения признаков контроля принятия статей расходов к
//			налоговому учету.
//		* ПризнакиСоглашений - Соответствие из СправочникСсылка.СоглашенияСКлиентами - используется для хранения типа
//			соглашений в строках, где значение - булево (истина - типовое, ложь - индивидуальное).
//
Функция ПолучитьСтруктуруКэшируемыеЗначения() Экспорт
	
	КэшированныеЗначения = Новый Структура;
	КэшированныеЗначения.Вставить("КоллекцияСКоллекцияССоставнымТипом",	Истина);
	КэшированныеЗначения.Вставить("КоэффициентыУпаковок",	Новый Соответствие);
	КэшированныеЗначения.Вставить("ОбъемУпаковок",			Новый Соответствие);
	КэшированныеЗначения.Вставить("ВесУпаковок",			Новый Соответствие);
	КэшированныеЗначения.Вставить("Штрихкоды",				Новый Соответствие);
	КэшированныеЗначения.Вставить("СвойстваНазначений",		Новый Соответствие);
	КэшированныеЗначения.Вставить("СвойстваСкладов",		Новый Соответствие);
	КэшированныеЗначения.Вставить("РеквизитыНоменклатуры",	Новый Соответствие); // Справочники.Номенклатура.ЗначенияРеквизитовНоменклатуры()
	КэшированныеЗначения.Вставить("ТипыНоменклатуры",		Новый Соответствие);
	КэшированныеЗначения.Вставить("КонтролироватьЗаполнениеАналитикиРасходов",	Новый Соответствие);
	КэшированныеЗначения.Вставить("РаспределятьНДСпоСтатьеРасходов",			Новый Соответствие);
	КэшированныеЗначения.Вставить("КонтролироватьЗаполнениеАналитикиДоходов",	Новый Соответствие);
	КэшированныеЗначения.Вставить("ИспользоватьАналитикуАктивовПассивов",		Новый Соответствие);
	КэшированныеЗначения.Вставить("ИспользоватьРучныеСкидкиВПродажах",			Неопределено);
	КэшированныеЗначения.Вставить("ИспользоватьАвтоматическиеСкидкиВПродажах",	Неопределено);
	КэшированныеЗначения.Вставить("ИспользоватьРучныеСкидкиВЗакупках",			Неопределено);
	КэшированныеЗначения.Вставить("ПравоРегистрацииШтрихкодовНоменклатурыДоступно",	Неопределено);
	КэшированныеЗначения.Вставить("ПринимаетсяКНУ", Новый Соответствие);
	КэшированныеЗначения.Вставить("КоэффициентыРНПТ", Новый Соответствие);
	КэшированныеЗначения.Вставить("АктуальныеСтавкиНДС", Неопределено);

	
	КэшированныеЗначения.Вставить("ОбработанныеСтроки", Новый Массив);
	
	КэшированныеЗначения.Вставить("КоллекцияБезСоставныхТипов", Истина);
	КэшированныеЗначения.Вставить("ПризнакиКатегорииЭксплуатации", Новый Соответствие);
	СтруктураПустойКатегории = Новый Структура;
	СтруктураПустойКатегории.Вставить("СрокЭксплуатации", 0);
	СтруктураПустойКатегории.Вставить("СтатьяРасходов", ПредопределенноеЗначение("ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка"));
	КэшированныеЗначения.ПризнакиКатегорииЭксплуатации.Вставить(ПредопределенноеЗначение("Справочник.КатегорииЭксплуатации.ПустаяСсылка"), СтруктураПустойКатегории);
	
	КэшированныеЗначения.Вставить("ПризнакиСоглашений", Новый Соответствие);
	
	Возврат КэшированныеЗначения;
	
КонецФункции // ПолучитьСтруктуруКэшируемыеЗначения()

// Возвращает структуру, содержащую поля кэшируемой информации, получаемой по штрихкоду товара.
//
// Возвращаемое значение:
//	Структура - структура кэшируемых данных товара, получаемая по штрихкоду товара, со следующими полями:
//		* Номенклатура - СправочникСсылка.Номенклатура - информация о товаре.
//		* ТипНоменклатуры - ПеречислениеСсылка.ТипыНоменклатуры - тип товара.
//		* Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика товара.
//		* Упаковка - СправочникСсылка.УпаковкиЕдиницыИзмерения - упаковка товара.
//		* ПредставлениеНоменклатуры - Строка - представление номенклатуры.
//		* ПодакцизныйТовар - Булево - Истина - товар является подакцизным.
//		* МаркируемаяПродукция - Булево - Истина - товар является маркируемой продукцией.
//		* ХарактеристикиИспользуются - Булево - Истина - признак использования характеристик.
//		* Коэффициент - Число - коэффициент упаковки.
//		* ЕдиницаИзмерения - СправочникСсылка.УпаковкиЕдиницыИзмерения - единица измерения товара.
//		* ЕдиничнаяУпаковка - СправочникСсылка.УпаковкиЕдиницыИзмерения - единичная упаковка товара.
//		* НесколькоЕдиничныхУпаковок - Булево - Истина - товар имеет несколько видов единичных упаковок.
//		* УпаковочныйЛист - ДокументСсылка.УпаковочныйЛист - упаковочный лист.
//		* НеизвестныйШтрихкод - Булево - Истина - в информационной базе отсутствуют данные по штрихкоду.
//		* Штрихкод - Строка - штрихкод товара.
//		* КоличествоПоШтрихкоду - Число - количество товара (в единицах хранения) по штрихкоду.
//		* КоличествоУпаковок - Число - количество упаковок товара.
//		* ШтрихкодНеИзКэша - Неопределено, Булево - Истина - штрихкод не из кэша памяти.
//		* НоменклатураНабора - СправочникСсылка.Номенклатура - информация по набору.
//		* ХарактеристикаНабора - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика набора.
//		* ВариантРасчетаЦеныНабора - ПеречислениеСсылка.ВариантыРасчетаЦенНаборов - вариант расчета цены набора.
//		* ВыполненныеДействия - Структура - структура действий, произведенных со строкой товара в табличной части документа:
//			** Добавлено - Булево - Истина - строка по товару добавлена.
//			** Изменено - Булево - Истина - строка по товару изменена.
//		* Назначения - Неопределено, СправочникСсылка.Назначения - назначение товара.
//
Функция СтруктураКешируемойИнформацииПоШтрихкоду() Экспорт
	
	ИнформацияПоШтрихкоду = Новый Структура;
	
	ИнформацияПоШтрихкоду.Вставить("Номенклатура");
	ИнформацияПоШтрихкоду.Вставить("ТипНоменклатуры");
	ИнформацияПоШтрихкоду.Вставить("Характеристика");
	ИнформацияПоШтрихкоду.Вставить("Упаковка");
	ИнформацияПоШтрихкоду.Вставить("ПредставлениеНоменклатуры", "");
	ИнформацияПоШтрихкоду.Вставить("ПодакцизныйТовар");
	ИнформацияПоШтрихкоду.Вставить("МаркируемаяПродукция", Ложь);
	ИнформацияПоШтрихкоду.Вставить("ХарактеристикиИспользуются");
	ИнформацияПоШтрихкоду.Вставить("Коэффициент",              1);
	ИнформацияПоШтрихкоду.Вставить("ЕдиницаИзмерения");
	ИнформацияПоШтрихкоду.Вставить("ЕдиничнаяУпаковка");
	ИнформацияПоШтрихкоду.Вставить("НесколькоЕдиничныхУпаковок");
	ИнформацияПоШтрихкоду.Вставить("УпаковочныйЛист",          ПредопределенноеЗначение("Документ.УпаковочныйЛист.ПустаяСсылка"));
	ИнформацияПоШтрихкоду.Вставить("НеизвестныйШтрихкод",      Истина);
	ИнформацияПоШтрихкоду.Вставить("Штрихкод",                 "");
	ИнформацияПоШтрихкоду.Вставить("КоличествоПоШтрихкоду",    0);
	ИнформацияПоШтрихкоду.Вставить("КоличествоУпаковок",       1);
	ИнформацияПоШтрихкоду.Вставить("ШтрихкодНеИзКэша",         Неопределено);
	ИнформацияПоШтрихкоду.Вставить("НоменклатураНабора",       ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"));
	ИнформацияПоШтрихкоду.Вставить("ХарактеристикаНабора",     ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка"));
	ИнформацияПоШтрихкоду.Вставить("ВариантРасчетаЦеныНабора", ПредопределенноеЗначение("Перечисление.ВариантыРасчетаЦенНаборов.ПустаяСсылка"));
	ИнформацияПоШтрихкоду.Вставить("ВыполненныеДействия",      Новый Структура("Добавлено, Изменено", Ложь, Ложь));
	ИнформацияПоШтрихкоду.Вставить("Назначения",               Неопределено);
	
	Возврат ИнформацияПоШтрихкоду;
	
КонецФункции

// Возвращает структуру, содержащую поля кэшируемых значений услуги.
//
// Возвращаемое значение:
//	Структура - структура кэшируемых данных услуг, со следующими полями:
//		* ПроцентыСтавокНДС - Соответствие - используется для хранения процентов ставок НДС.
//		* Штрихкоды - Соответствие - используется для хранения значений штрихкодов услуг.
//		* ИспользоватьРучныеСкидкиВПродажах - Неопределено, Булево - Истина - в продажах используется расчет скидок вручную.
//		* ИспользоватьАвтоматическиеСкидкиВПродажах - Неопределено, Булево - Истина - в продажах используется 
//			автоматический расчет скидок.
//		* ИспользоватьРучныеСкидкиВЗакупках - Неопределено, Булево - Истина - в закупках используется расчет скидок вручную.
//
Функция ПолучитьСтруктуруКэшируемыеЗначенияУслуг() Экспорт
	
	КэшированныеЗначения = Новый Структура;
	КэшированныеЗначения.Вставить("ПроцентыСтавокНДС",    Новый Соответствие);
	КэшированныеЗначения.Вставить("Штрихкоды",            Новый Соответствие);
	КэшированныеЗначения.Вставить("ИспользоватьРучныеСкидкиВПродажах",         Неопределено);
	КэшированныеЗначения.Вставить("ИспользоватьАвтоматическиеСкидкиВПродажах", Неопределено);
	КэшированныеЗначения.Вставить("ИспользоватьРучныеСкидкиВЗакупках",         Неопределено);
	
	Возврат КэшированныеЗначения;
	
КонецФункции // ПолучитьСтруктуруКэшируемыеЗначенияУслуг()

#КонецОбласти

#Область ПолучениеСтруктурПараметровДляОбработкиТабличнойЧастиТовары

// Возвращает структуру, содержащую поля значений, используемых для заполнения цен в строках табличной части документа.
//
// Параметры:
//	Объект - ДанныеФормыСтруктура - данные документа.
//	ВидЦеныВШапке - Булево - Истина - признак указания вида цены в шапке документа.
//
// Возвращаемое значение:
//	Структура - структура, используемая для заполнения цен в табличной части документа:
//		* Дата - Дата - дата документа.
//		* Валюта - СправочникСсылка.Валюты - валюта документа.
//		* ВидЦены - СправочникСсылка.ВидыЦен - Вид цены (может не быть, зависит от того есть ли вид цены в шапке документа или нет).
//
Функция ПараметрыЗаполненияЦеныВСтрокеТЧ(Объект, ВидЦеныВШапке = Ложь) Экспорт
	
	СтруктураЗаполненияЦены = Новый Структура;
	СтруктураЗаполненияЦены.Вставить("Дата",   Объект.Дата);
	СтруктураЗаполненияЦены.Вставить("Валюта", Объект.Валюта);
	
	Если ВидЦеныВШапке Тогда
		СтруктураЗаполненияЦены.Вставить("ВидЦены", Объект.ВидЦены);
	КонецЕсли;
	
	Возврат СтруктураЗаполненияЦены;
	
КонецФункции

// Возвращает структуру, содержащую поля значений, используемых для пересчета сумм НДС в строках табличной части 
// документа.
//
// Параметры:
//	Объект - ДанныеФормыСтруктура - данные документа.
//
// Возвращаемое значение:
//	Структура - структура, используемая для пересчета сумм НДС в табличной части документа:
//		* ЦенаВключаетНДС - Булево - Истина - НДС включен в цену товара.
//		* НалогообложениеНДС - ПеречислениеСсылка.ТипыНалогообложенияНДС - налогообложение документа.
//
Функция ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Объект) Экспорт
	
	СтруктураЗаполненияЦены = Новый Структура;
	СтруктураЗаполненияЦены.Вставить("ЦенаВключаетНДС", Объект.ЦенаВключаетНДС);
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Объект,"НалогообложениеНДС") Тогда
		СтруктураЗаполненияЦены.Вставить("НалогообложениеНДС", Объект.НалогообложениеНДС);
	КонецЕсли;
	
	Возврат СтруктураЗаполненияЦены;
	
КонецФункции

// Возвращает структуру, содержащую параметры для заполнения ставки НДС
// документа.
//
// Параметры:
//	Объект - ДанныеФормыСтруктура, ДокументОбъект - данные документа.
//	ПоДатеОтгрузки - Булево - Признак того, что ставки необходимо получать на дату отгрузки
//	ВернутьМногооборотнуюТару - Булево - признак необходимости возврата многооборотной тары.
//
// Возвращаемое значение:
//	Структура - структура, используемая для пересчета сумм НДС в табличной части документа:
//		* НалогообложениеНДС - ПеречислениеСсылка.ТипыНалогообложенияНДС - Налогообложение документа.
//		* Организация - СправочникСсылка.Организации - Организация документа
//		* Дата - Дата - Дата документа
//		* ПоДатеОтгрузки - Булево - Признак того, что ставки необходимо получать на дату отгрузки
//		* ВернутьМногооборотнуюТару - Булево - Признак необходимости возврата многооборотной тары.
//		* ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - Хозяйственная операция документа.
//
Функция ПараметрыЗаполненияСтавкиНДС(Объект = Неопределено, ПоДатеОтгрузки = Ложь, ВернутьМногооборотнуюТару = Ложь) Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("Дата");
	Параметры.Вставить("Организация");
	Параметры.Вставить("НалогообложениеНДС", ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС"));
	Параметры.Вставить("ПоДатеОтгрузки", ПоДатеОтгрузки);
	Параметры.Вставить("ВернутьМногооборотнуюТару", ВернутьМногооборотнуюТару);
	Параметры.Вставить("Суффикс", "");
	Параметры.Вставить("ИнициализацияВходящегоДокумента", Ложь);
	Параметры.Вставить("ЗаполнениеНаОснованииКопирование", Ложь);
	Параметры.Вставить("ХозяйственнаяОперация", ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПустаяСсылка"));
	
	Если Объект <> Неопределено Тогда
		
		Параметры.Дата = Объект.Дата;
		Параметры.Организация = Объект.Организация;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Объект, "НалогообложениеНДС") Тогда
			Параметры.Вставить("НалогообложениеНДС", Объект.НалогообложениеНДС);
		КонецЕсли;
		
		Если Не ВернутьМногооборотнуюТару И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Объект, "ВернутьМногооборотнуюТару") Тогда
			Параметры.Вставить("ВернутьМногооборотнуюТару", Объект.ВернутьМногооборотнуюТару);
		КонецЕсли;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Объект, "ХозяйственнаяОперация") Тогда
			Параметры.Вставить("ХозяйственнаяОперация", Объект.ХозяйственнаяОперация);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

// Возвращает структуру, содержащую поля значений, используемых для заполнения количества по РНПТ в строках табличной
// части документа.
//
// Параметры:
//	Объект - ДанныеФормыСтруктура - данные формы объекта.
//	ИмяПоляМестоХранения - Строка - имя поля Склад, находящегося в шапке объекта или табличной части.
//	МестоХраненияВТабличнойЧасти - Булево - признак наличия поля Склад в табличной части объекта.
//
// Возвращаемое значение:
//	см. УчетПрослеживаемыхТоваровКлиентСерверЛокализация.ПараметрыПолученияКоэффициентаРНПТ.
//
Функция ПараметрыПолученияКоэффициентаРНПТ(Объект, ИмяПоляМестоХранения = "Склад", МестоХраненияВТабличнойЧасти = Ложь) Экспорт
	
	Возврат УчетПрослеживаемыхТоваровКлиентСерверЛокализация.ПараметрыПолученияКоэффициентаРНПТ(Объект,
																								ИмяПоляМестоХранения,
																								МестоХраненияВТабличнойЧасти);
	
КонецФункции

// Возвращает структуру, содержащую поля значений, используемых для заполнения цен закупки в строках табличной части 
// документа.
//
// Параметры:
//	Объект - ДанныеФормыСтруктура - данные документа.
//	ИмяПоляВидЦены - Строка, Неопределено - Имя реквизита шапки вида цены, если имя не "ВидЦеныПоставщика".
//
// Возвращаемое значение:
//	Структура - структура, используемая для заполнения цен закупки в строках табличной части документа:
//		* Соглашение - СправочникСсылка.СоглашенияСКлиентами - соглашение по документу.
//		* Партнер - СправочникСсылка.Партнеры - партнер по документу.
//		* Дата - Дата - дата документа.
//		* Валюта - СправочникСсылка.Валюты - валюта документа.
//		* ВидЦеныПоставщика - СправочникСсылка.ВидыЦенПоставщиков - валюта документа.
//		* ИмяПоля - Строка - имя поля, в которое необходимо поместить полученную цену
//
Функция ПолучитьСтруктуруЗаполненияЦеныЗакупкиВСтрокеТЧ(Объект, ИмяПоляВидЦены = Неопределено) Экспорт
	
	СтруктураЗаполненияЦены = Новый Структура("Соглашение,Партнер,Дата,Организация,Валюта,Соглашение,НалогообложениеНДС,ВернутьМногооборотнуюТару,Партнер,Дата,Валюта,ВидЦеныПоставщика");
	ЗаполнитьЗначенияСвойств(СтруктураЗаполненияЦены,Объект);
	
	Если ИмяПоляВидЦены <> Неопределено Тогда
		СтруктураЗаполненияЦены.ВидЦеныПоставщика = Объект[ИмяПоляВидЦены];
	КонецЕсли;

	Если Не ЗначениеЗаполнено(СтруктураЗаполненияЦены.ВидЦеныПоставщика) Тогда
		СтруктураЗаполненияЦены.Удалить("ВидЦеныПоставщика");
		// Оставляем только для документов, у которых ВидЦеныПоставщика в шапке и заполнен
	КонецЕсли;
	Возврат СтруктураЗаполненияЦены;
	
КонецФункции

// Возвращает структуру, содержащую поля значений, используемых для заполнения условий продаж в строках табличной части 
// документа.
// 
// Параметры:
//  Объект - ДанныеФормыСтруктура - данные документа:
// * Дата - Дата - дата документа
// * Валюта - СправочникСсылка.Валюты - валюта документа
// * Соглашение - СправочникСсылка.СоглашенияСКлиентами - соглашение по документу
// * Партнер - СправочникСсылка.Партнеры - партнер из документа
// * Ссылка - ДокументСсылка - ссылка документа
// 
// Возвращаемое значение:
//  Структура - Получить структуру заполнения условий продаж в строке ТЧ:
// * Дата - Дата - дата документа
// * Валюта - СправочникСсылка.Валюты - валюта документа
// * Соглашение - СправочникСсылка.СоглашенияСКлиентами - соглашение по документу
// * Партнер - СправочникСсылка.Партнеры - партнер из документа
// * Ссылка - ДокументСсылка - ссылка документа
//
Функция ПолучитьСтруктуруЗаполненияУсловийПродажВСтрокеТЧ(Объект) Экспорт
	
	СтруктураЗаполненияЦены = Новый Структура;
	СтруктураЗаполненияЦены.Вставить("Дата",       Объект.Дата);
	СтруктураЗаполненияЦены.Вставить("Валюта",     Объект.Валюта);
	СтруктураЗаполненияЦены.Вставить("Соглашение", Объект.Соглашение);
	СтруктураЗаполненияЦены.Вставить("Партнер",    Объект.Партнер);
	СтруктураЗаполненияЦены.Вставить("Ссылка",     Объект.Ссылка);
	
	Возврат СтруктураЗаполненияЦены;
	
КонецФункции

// Возвращает структуру, содержащую поля значений, используемых для заполнения цен розницы в строках табличной части 
// документа.
//
// Параметры:
//	Объект - ДанныеФормыСтруктура - данные документа:
// * Дата - Дата - дата документа.
// * Валюта - СправочникСсылка.Валюты - валюта документа.
// * ВидЦены - СправочникСсылка.ВидыЦен - вид цен по документу.
//
// Возвращаемое значение:
//	Структура - структура, используемая для заполнения цен розницы в строках табличной части документа:
//		* Дата - Дата - дата документа.
//		* Валюта - СправочникСсылка.Валюты - валюта документа.
//		* ВидЦены - СправочникСсылка.ВидыЦен - вид цен по документу.
//
Функция ПолучитьСтруктуруЗаполненияЦеныРозницаВСтрокеТЧ(Объект) Экспорт
	
	СтруктураЗаполненияЦены = Новый Структура;
	СтруктураЗаполненияЦены.Вставить("Дата",    Объект.Дата);
	СтруктураЗаполненияЦены.Вставить("Валюта",  Объект.Валюта);
	СтруктураЗаполненияЦены.Вставить("ВидЦены", Объект.ВидЦены);
	
	Возврат СтруктураЗаполненияЦены;
	
КонецФункции

// Возвращает структуру, содержащую поля значений, используемых для заполнения цен по ассортименту в строках табличной 
// части документа.
//
// Параметры:
//	Объект - ДанныеФормыСтруктура - данные документа:
// * Дата - Дата - дата документа.
// * Валюта - СправочникСсылка.Валюты - валюта документа.
// * Склад - СправочникСсылка.Склады - склад по документу.
//
// Возвращаемое значение:
//	Структура - структура, используемая для заполнения цен по ассортименту в строках табличной части документа:
//		* Дата - Дата - дата документа.
//		* Валюта - СправочникСсылка.Валюты - валюта документа.
//		* Склад - СправочникСсылка.Склады - склад по документу.
//
Функция ПолучитьСтруктуруЗаполненияЦеныПоАссортиментуВСтрокеТЧ(Объект) Экспорт
	
	СтруктураЗаполненияЦены = Новый Структура;
	СтруктураЗаполненияЦены.Вставить("Дата",    Объект.Дата);
	СтруктураЗаполненияЦены.Вставить("Валюта",  Объект.Валюта);
	СтруктураЗаполненияЦены.Вставить("Склад", 	Объект.Склад);
	
	Возврат СтруктураЗаполненияЦены;
	
КонецФункции

// Возвращает структуру, содержащую поля значений, используемых для заполнения цен по ассортименту в строках табличной 
// части документа.
//
// Параметры:
//	Объект - ДанныеФормыСтруктура - данные документа:
// * Дата - Дата - дата документа.
// * Валюта - СправочникСсылка.Валюты - валюта документа.
//	ОбъектХраненияУсловийПродаж - Строка, СправочникСсылка.ФорматыМагазинов, СправочникСсылка.Склады - 
//
// Возвращаемое значение:
//	Структура - структура, используемая для заполнения цен по ассортименту в строках табличной части документа:
//		* Дата - Дата - дата документа.
//		* Валюта - СправочникСсылка.Валюты - валюта документа.
//		* ОбъектХраненияУсловийПродаж - Строка, СправочникСсылка.ФорматыМагазинов, СправочникСсылка.Склады -
//
Функция ПолучитьСтруктуруЗаполненияУсловийРозничныхПродажВСтрокеТЧ(Объект, ОбъектХраненияУсловийПродаж) Экспорт
	
	СтруктураЗаполненияЦены = Новый Структура;
	СтруктураЗаполненияЦены.Вставить("Дата", Объект.Дата);
	СтруктураЗаполненияЦены.Вставить("Валюта", Объект.Валюта);
	СтруктураЗаполненияЦены.Вставить("ОбъектХраненияУсловийПродаж", ОбъектХраненияУсловийПродаж);
	
	Возврат СтруктураЗаполненияЦены;
	
КонецФункции

// Возвращает структуру, содержащую поля значений, используемых для пересчета цены скидки продажи в строках
//  табличной части документа.
//
// Параметры:
//  Объект				 - ДанныеФормыСтруктура	 - данные документа.
//  ПередачаНаКомиссию	 - Булево				 - Истина если осуществляется передача на комиссию.
//  ИмяКоличества		 - Строка				 - имя поля количества в табличной части документа.
// 
// Возвращаемое значение:
//  Структура - структура, используемая для пересчета цены скидки продажи в строках табличной части документа:
//  * ИспользоватьРучныеСкидки - Булево - Истина - в продажах расчет скидок осуществляется вручную.
//  * ИспользоватьАвтоматическиеСкидки - Булево - Истина если в продажах используется автоматический расчет скидок.
//  * ИспользоватьБонусныеБаллы - Булево - Истина если в продажах используются бонусные баллы.
//  * ИмяКоличества - Строка - имя поля количества в табличной части документа.
//
Функция ПолучитьСтруктуруПересчетаЦеныСкидкиВПродажахВТЧ(Объект, ПередачаНаКомиссию = Ложь, ИмяКоличества = "КоличествоУпаковок") Экспорт
	
	СтруктураЗаполненияЦены = Новый Структура;
	
	Если Не ПередачаНаКомиссию Тогда
		СтруктураЗаполненияЦены.Вставить("ИспользоватьРучныеСкидки");
		СтруктураЗаполненияЦены.Вставить("ИспользоватьАвтоматическиеСкидки");
		СтруктураЗаполненияЦены.Вставить("ИспользоватьБонусныеБаллы");
	КонецЕсли;
	
	СтруктураЗаполненияЦены.Вставить("ИмяКоличества", ИмяКоличества);
	
	Возврат СтруктураЗаполненияЦены;
	
КонецФункции

// Возвращает структуру, содержащую поля значений, используемых для пересчета цены скидки закупки в строках
//  табличной части документа.
//
// Параметры:
//  Объект			 - ДанныеФормыСтруктура	 - данные документа.
//  ПриемНаКомиссию	 - Булево				 - Истина если осуществляется прием на комиссию.
// 
// Возвращаемое значение:
//  Структура - структура, используемая для пересчета цены скидки закупки в строках табличной части документа:
//  * ИспользоватьРучныеСкидки - Булево - Истина если расчет скидок осуществляется вручную.
//
Функция ПолучитьСтруктуруПересчетаЦеныСкидкиВЗакупкахВТЧ(Объект, ПриемНаКомиссию) Экспорт
	
	СтруктураЗаполненияЦены = Новый Структура;
	
	Если Не ПриемНаКомиссию Тогда
		СтруктураЗаполненияЦены.Вставить("ИспользоватьРучныеСкидки");
	КонецЕсли;
	
	Возврат СтруктураЗаполненияЦены;
	
КонецФункции

// Возвращает структуру, содержащую поля значений, используемых для проверки сопоставленной номенклатуры поставщика с
//  номенклатурой склада в строках табличной части документа.
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - данные документа:
//  	* Ссылка - ДокументСсылка
//  	* Партнер - СправочникСсылка.Партнеры - партнер по документу.
//  НеВыполнятьПроверкуДляПользователя - Булево - Истина если не нужно проверку для текущего пользователя.
// 
// Возвращаемое значение:
//  Структура - структура, используемая для проверки сопоставленной номенклатуры поставщика с номенклатурой склада в строках табличной части документа:
//  * Ссылка - ДокументСсылка - ссылка на документ.
//  * Партнер - СправочникСсылка.Партнеры - партнер по документу.
//  * НеВыполнятьПроверкуДляПользователя - Булево - Истина если не осуществлять проверку для текущего пользователя.
//
Функция ПолучитьСтруктуруПроверкиСопоставленнойНоменклатурыПоставщикаВСтрокеТЧ(Объект, НеВыполнятьПроверкуДляПользователя) Экспорт
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Ссылка",                             Объект.Ссылка);
	СтруктураПараметров.Вставить("Партнер",                            Объект.Партнер);
	СтруктураПараметров.Вставить("НеВыполнятьПроверкуДляПользователя", НеВыполнятьПроверкуДляПользователя);
	
	Возврат СтруктураПараметров;
	
КонецФункции

// Возвращает структуру, содержащую поля значений, используемых для заполнения сведений о складе в строках
//  табличной части документа.
// 
// Параметры:
// 	Объект - ДанныеФормыСтруктура	 - данные документа.
// 	СкладГруппа - Булево - Истина, если склад является иерархическим.
// 	ИмяПоляСкладОбъекта - Строка
// 	ИмяПоляСклад - Строка
// 	
// Возвращаемое значение:
// 	Структура - Описание:
// * ИмяПоляСклад - Строка
// * СкладПоУмолчанию - СправочникСсылка.Склады
// * СкладГруппа - Булево
// * СкладГруппаСсылка - СправочникСсылка.Склады
//
Функция ПолучитьСтруктуруЗаполненияСкладаВСтрокеТЧ(Объект, СкладГруппа, ИмяПоляСкладОбъекта = "Склад", ИмяПоляСклад = "Склад") Экспорт
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ИмяПоляСклад",      ИмяПоляСклад);
	СтруктураПараметров.Вставить("СкладПоУмолчанию",  Объект[ИмяПоляСкладОбъекта]);
	СтруктураПараметров.Вставить("СкладГруппа",       СкладГруппа);
	
	СтруктураПараметров.Вставить(
		"СкладГруппаСсылка",
		?(СкладГруппа, Объект[ИмяПоляСкладОбъекта], ПредопределенноеЗначение("Справочник.Склады.ПустаяСсылка")));
	
	Возврат СтруктураПараметров;
	
КонецФункции

// Возвращает структуру, содержащую поля значений, используемых для заполнения сведений об услугах в строках
//  табличной части документа.
//
// Параметры:
//  Объект					 - ДанныеФормыСтруктура	 - данные документа.
//  ЗаполнятьДляВсехУслуг	 - Булево				 - Если Истина, то заполнять сведения об услугах во всей табличной части документа.
// 
// Возвращаемое значение:
//  Структура - структура, используемая для заполнения сведений об услугах в строках табличной части документа:
//  * ЗаполнятьДляВсехУслуг - Булево - Истина - заполнять сведения об услугах во всей табличной части документа.
//  * УслугиПоПереработкеДавальческогоСырья - Булево - Истина - осуществляется предоставление услуг по переработке
//  давальческого сырья.
//
Функция ПолучитьСтруктуруЗаполненияСодержанияУслугиВСтрокеТЧ(Объект, ЗаполнятьДляВсехУслуг) Экспорт
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("ЗаполнятьДляВсехУслуг", ЗаполнятьДляВсехУслуг);
	
	
	Возврат СтруктураПараметров;
	
КонецФункции

// Возвращает структуру, содержащую поля значений, используемых для пересчета суммы НДС по товару в строках
//  табличной части документа.
//
// Параметры:
//  Объект	 - ДанныеФормыСтруктура	 - данные документа.
// 
// Возвращаемое значение:
//  Структура - структура, используемая для пересчета суммы НДС по товару в строках табличной части документа:
//		* ЦенаВключаетНДС - Булево - Истина - сумма по НДС включена в цену товара.
//		* НалогообложениеНДС - ПеречислениеСсылка.ТипыНалогообложенияНДС - налогообложение документа.
//
Функция ПараметрыПересчетаСуммыНДСВТЧ(Объект) Экспорт
	
	СтруктураЗаполненияЦены = Новый Структура;
	СтруктураЗаполненияЦены.Вставить("ЦенаВключаетНДС", Объект.ЦенаВключаетНДС);
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Объект,"НалогообложениеНДС") Тогда
		СтруктураЗаполненияЦены.Вставить("НалогообложениеНДС", Объект.НалогообложениеНДС);
	КонецЕсли;
	
	Возврат СтруктураЗаполненияЦены;
	
КонецФункции

// Возвращает структуру, содержащую поля значений, используемых для заполнения ставки НДС в строках 
// табличной части документа.
//
// Параметры:
//	Объект - ДанныеФормыСтруктура - данные документа:
//	 * Дата - Дата - дата документа.
//	 * Организация - СправочникСсылка.Организации - организация по документу, осуществляющая хозяйственную операцию.
//
// Возвращаемое значение:
//	Структура - структура, используемая для заполнения ставки НДС в строках табличной части документа:
//		* Дата - Дата - дата документа.
//		* Организация - СправочникСсылка.Организации - организация по документу, осуществляющая хозяйственную операцию.
//
Функция ПолучитьСтруктуруЗаполненияСтавкиНДС(Объект) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Дата"       , Объект.Дата);
	Результат.Вставить("Организация", Объект.Организация);
	
	Возврат Результат;
	
КонецФункции

// Возвращает структуру, содержащую поля значений, используемых для заполнения признаков идентификации безвозвратной
//  многооборотной тары в строках табличной части документа.
//
// Параметры:
//  Объект	 - ДанныеФормыСтруктура	 - данные документа:
//	 * ВернутьМногооборотнуюТару - Булево - Истина - осуществлять возврат многооборотной тары.
// 
// Возвращаемое значение:
//  Структура - структура, используемая для заполнения признаков идентификации безвозвратной многооборотной тары в
//  строках табличной части документа:
//  * ВернутьМногооборотнуюТару - Булево - Истина - осуществлять возврат многооборотной тары.
//
Функция ПолучитьСтруктуруЗаполненияПризнакаБезВозвратнойТары(Объект) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ВернутьМногооборотнуюТару" , Объект.ВернутьМногооборотнуюТару);
	
	Возврат Результат;
	
КонецФункции

// Возвращает параметры, используемые при заполнении страны происхождения номенклатуры в табличной части объекта.
//
// Возвращаемое значение:
//	Структура - коллекция, содержащая следующие свойства:
//		* Номенклатура - Строка - имя поля номенклатура табличной части объекта.
//		* НомерГТД - Строка - имя поля номера ГТД товара табличной части объекта.
//
Функция ПараметрыЗаполненияСтраныПроисхождения() Экспорт
	
	ПараметрыЗаполненияСтраныПроисхождения = Новый Структура;
	ПараметрыЗаполненияСтраныПроисхождения.Вставить("Номенклатура",	"Номенклатура");
	ПараметрыЗаполненияСтраныПроисхождения.Вставить("НомерГТД",		"НомерГТД");
	
	Возврат ПараметрыЗаполненияСтраныПроисхождения;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыПересчетаИЗаполненияКлиентСервер

// Заполняет поля, используемые для отражения расхождений, в текущей строке табличной 
// части документа "АктОРасхожденияхПослеПеремещения".
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьРасхождения(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Если НЕ СтруктураДействий.Свойство("ПересчитатьРасхождения") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ТекущаяСтрока.КоличествоУпаковокРасхождения = ТекущаяСтрока.КоличествоУпаковок - ТекущаяСтрока.КоличествоУпаковокПоДокументу;
	ТекущаяСтрока.ЕстьРасхождения               = (ТекущаяСтрока.КоличествоУпаковокРасхождения <> 0);
	
	Если ТекущаяСтрока.КоличествоУпаковокРасхождения = 0 Тогда
		ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПустаяСсылка");
	ИначеЕсли ТекущаяСтрока.КоличествоУпаковокРасхождения < 0 
		И (ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ВозвратПерепоставленного")
		ИЛИ ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПокупкаПерепоставленного")
		ИЛИ ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПерепоставленноеДарится")
		ИЛИ ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОприходоватьСейчас")) Тогда
		
		ТекущаяСтрока.Действие    = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПустаяСсылка");
		
	ИначеЕсли ТекущаяСтрока.КоличествоУпаковокРасхождения > 0 
		И (ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ДопоставкаНеТребуется")
		ИЛИ ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.НедостачаНеПризнана")
		ИЛИ ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка")
		ИЛИ ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОтгрузитьСейчас")) Тогда
		
		ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПустаяСсылка");
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет поля, используемые для отражения расхождений, в текущей строке табличной 
// части документа "АктОРасхожденияхПослеОтгрузки".
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьРасхожденияПослеОтгрузки(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Если НЕ СтруктураДействий.Свойство("ПересчитатьРасхожденияПослеОтгрузки") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ЗаполнитьРасхожденияВСтрокеАкта(ТекущаяСтрока);
	
	Если ТекущаяСтрока.КоличествоУпаковокРасхождения = 0 Тогда
		ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПустаяСсылка");
	ИначеЕсли ТекущаяСтрока.КоличествоУпаковокРасхождения < 0 
		И (ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ВозвратПерепоставленного")
		ИЛИ ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПокупкаПерепоставленного")
		ИЛИ ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПерепоставленноеДарится")
		ИЛИ ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОприходоватьСейчас")) Тогда
		
		ТекущаяСтрока.Действие    = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПустаяСсылка");
		
	ИначеЕсли ТекущаяСтрока.КоличествоУпаковокРасхождения > 0 
		И (ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ДопоставкаНеТребуется")
		ИЛИ ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.НедостачаНеПризнана")
		ИЛИ ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка")
		ИЛИ ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОтгрузитьСейчас")) Тогда
		
		ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПустаяСсылка");
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет поля, используемые для отражения расхождений, в текущей строке табличной 
// части документа "АктОРасхожденияхПослеПриемки".
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьРасхожденияПослеПриемки(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Если НЕ СтруктураДействий.Свойство("ПересчитатьРасхожденияПослеПриемки") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ЗаполнитьРасхожденияВСтрокеАкта(ТекущаяСтрока);
	
	Если ТекущаяСтрока.КоличествоУпаковокРасхождения = 0 Тогда
		ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ПустаяСсылка");
	ИначеЕсли ТекущаяСтрока.КоличествоУпаковокРасхождения > 0 
		И (ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОтнестиНедостачуНаПрочиеРасходы")
		ИЛИ ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьНедостачу")
		ИЛИ ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьНедостачуИОжидатьДопоставку")
		ИЛИ ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОтнестиНедостачуНаПрочиеРасходы")
		ИЛИ ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОжидатьДопоставкуБезОформления")) Тогда
		
		ТекущаяСтрока.Действие    = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ПустаяСсылка");
		ТекущаяСтрока.ПоВинеСтороннейКомпании = Ложь;
		
	ИначеЕсли ТекущаяСтрока.КоличествоУпаковокРасхождения < 0 
		И (ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ВернутьПерепоставленноеБезОформления")
		ИЛИ ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОтнестиПерепоставленноеНаПрочиеДоходы")
		ИЛИ ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьПерепоставленное")
		ИЛИ ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьПерепоставленноеИВернуть")) Тогда
		
		ТекущаяСтрока.Действие = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ПустаяСсылка");
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет поля признаков использования комментариев в текущей строке табличной 
// части документа "АктОРасхожденияхПослеПриемки".
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьПризнакНаличияКомментарияПриемка(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт

	Если НЕ СтруктураДействий.Свойство("ПризнакНаличиеКомментарияПриемка") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ТекущаяСтрока.ЕстьКомментарийПоставщика = НЕ ПустаяСтрока(ТекущаяСтрока.КомментарийПоставщика);
	ТекущаяСтрока.ЕстьКомментарийМенеджера = НЕ ПустаяСтрока(ТекущаяСтрока.КомментарийМенеджера);

КонецПроцедуры

// Заполняет поля признаков использования комментариев в текущей строке табличной 
// части документа "АктОРасхожденияхПослеОтгрузки".
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьПризнакНаличияКомментарияОтгрузка(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт

	Если НЕ СтруктураДействий.Свойство("ПризнакНаличиеКомментарияОтгрузка") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ТекущаяСтрока.ЕстьКомментарийКлиента = НЕ ПустаяСтрока(ТекущаяСтрока.КомментарийКлиента);
	ТекущаяСтрока.ЕстьКомментарийМенеджера = НЕ ПустаяСтрока(ТекущаяСтрока.КомментарийМенеджера);

КонецПроцедуры

// Заполняет поля признаков использования комментариев в текущей строке табличной 
// части документа "АктОРасхожденияхПослеПеремещения".
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьПризнакНаличияКомментария(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт

	Если НЕ СтруктураДействий.Свойство("ПризнакНаличиеКомментария") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ТекущаяСтрока.ЕстьКомментарийПолучателя = НЕ ПустаяСтрока(ТекущаяСтрока.КомментарийПолучателя);
	ТекущаяСтрока.ЕстьКомментарийМенеджера = НЕ ПустаяСтрока(ТекущаяСтрока.КомментарийМенеджера);

КонецПроцедуры

// Заполняет признак необходимости обработки строки табличной части документа "ПриходныйОрдерНаТовары".
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьПризнакТоварногоМестаТребуетсяОбработка(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Если НЕ СтруктураДействий.Свойство("ЗаполнитьПризнакТовМестаТребуетсяОбработка") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ТекущаяСтрока.Штрихкод)
			И ТекущаяСтрока.ШтрихкодНеОпределен
			И (ТекущаяСтрока.ЭтоУпаковочныйЛист И Не ЗначениеЗаполнено(ТекущаяСтрока.УпаковочныйЛист)
				Или Не ТекущаяСтрока.ЭтоУпаковочныйЛист И Не ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура))
		Или ТекущаяСтрока.ЭтоУпаковочныйЛист
			И ТекущаяСтрока.КоличествоЗаполненныхПодчиненныхПозиций = 0
		Или ТекущаяСтрока.ЭтоСлужебнаяСтрокаПустогоУпаковочногоЛиста Тогда
		ТекущаяСтрока.ТребуетсяОбработка = 1;
	Иначе
		ТекущаяСтрока.ТребуетсяОбработка = 0;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает структуру, содержащую поля для пересчета суммы в табличной части документа.
//
// Параметры:
//  Реквизиты - Строка - Содержит имена полей, заданных через запятую,
//  ИмяПоляКоличество     - Строка - Имя поля, по которому считается коэффициент пропорциональности.
//  РазрядностиОкругления - Структура - структура, в формате ИмяПоля => Количество знаков дробной части, которая будет
//                                      использоваться при пересчете реквизитов.
//
// Возвращаемое значение:
//  Структура:
//            * Поля - Структура - содержит поля для пересчета суммы в табличной части документа
//            * Строки - Массив из ДанныеФормыЭлементКоллекции - содержит элементы типа, ссылки нас строки для пересчета сумм.
//            * ИтогКоличество - Число - сумма значений в поле "Количество" в строках переданных в параметре "Строки"
//            * ИмяПоляКоличество - Строка
//            * РазрядностиОкругления - Структура
//
Функция СтруктураПересчетаСуммы(Реквизиты, ИмяПоляКоличество = "Количество", РазрядностиОкругления = Неопределено) Экспорт

	Поля = Новый Структура(Реквизиты);
	
	Результат = Новый Структура;
	Результат.Вставить("Поля", Поля);
	Результат.Вставить("Строки", Новый Массив());
	Результат.Вставить("ИтогКоличество", 0);
	Результат.Вставить("ИмяПоляКоличество", ИмяПоляКоличество);
	Результат.Вставить("РазрядностиОкругления", РазрядностиОкругления);

	Возврат Результат;

КонецФункции

// Инициализирует структуру для пересчета суммы в табличной части документа
//
// Параметры:
//  СтруктураПересчетаСуммы - см. СтруктураПересчетаСуммы.
//  ДанныеЗаполнения - ДанныеФормыЭлементКоллекции - строка, содержащая значения суммовых показателей,
//                     которые необходимо будет распределить между строками при пересчете сумм.
//
// Возвращаемое значение
//  Структура - Структура со следующими полями:
//              Поля - Структура - содержит поля для пересчета суммы в табличной части документа,
//              Строки - Массив - содержит элементы типа ДанныеФормыЭлементКоллекции, ссылки нас строки для пересчета сумм,
//              ИтогКоличество - Число - сумма значений в поле "Количество" в строках переданных в параметре "Строки".
//
Процедура ЗаполнитьСтруктуруПересчетаСуммы(СтруктураПересчетаСуммы, ДанныеЗаполнения) Экспорт

	ЗаполнитьЗначенияСвойств(СтруктураПересчетаСуммы.Поля, ДанныеЗаполнения);

	СтруктураПересчетаСуммы.ИтогКоличество = 0;
	СтруктураПересчетаСуммы.Строки.Очистить();

КонецПроцедуры

// Добавляет строку для пересчета суммы в структуру пересчета суммы, описание см. функция
// СтруктураПересчетаСуммы.
//
// Параметры:
//  СтруктураПересчетаСуммы - см. СтруктураПересчетаСуммы
//  Строка - ДанныеФормыЭлементКоллекции - строка, для которой необходимо рассчитать значения сумм.
//
Процедура ДобавитьСтрокуДляПересчетаСуммы(СтруктураПересчетаСуммы, Строка) Экспорт

	СтруктураПересчетаСуммы.Строки.Добавить(Строка);
	СтруктураПересчетаСуммы.ИтогКоличество = СтруктураПересчетаСуммы.ИтогКоличество + Строка[СтруктураПересчетаСуммы.ИмяПоляКоличество];

КонецПроцедуры

// Пересчитывает суммы в строках, добавленных в структуру пересчета суммы, описание см. функция
// СтруктураПересчетаСуммы.
//
// Параметры:
//  СтруктураПересчетаСуммы - см. СтруктураПересчетаСуммы
//
Процедура ПересчитатьСуммы(СтруктураПересчетаСуммы) Экспорт
	
	РазрядностиОкругления = Неопределено;
	Если СтруктураПересчетаСуммы.Свойство("РазрядностиОкругления") Тогда
		РазрядностиОкругления = СтруктураПересчетаСуммы.РазрядностиОкругления;
	КонецЕсли;
	
	Для Каждого Строка Из СтруктураПересчетаСуммы.Строки Цикл
		
		Если СтруктураПересчетаСуммы.ИтогКоличество = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Коэффициент = Строка[СтруктураПересчетаСуммы.ИмяПоляКоличество] / СтруктураПересчетаСуммы.ИтогКоличество;
		СтруктураПересчетаСуммы.ИтогКоличество = СтруктураПересчетаСуммы.ИтогКоличество - Строка[СтруктураПересчетаСуммы.ИмяПоляКоличество];

		Для Каждого Поле Из СтруктураПересчетаСуммы.Поля Цикл
			
			НовоеЗначение = Поле.Значение * Коэффициент;
			
			Если РазрядностиОкругления <> Неопределено Тогда
				Строка[Поле.Ключ] = Окр(НовоеЗначение, РазрядностиОкругления[Поле.Ключ]);
			Иначе
				Строка[Поле.Ключ] = НовоеЗначение;
			КонецЕсли;
			
			СтруктураПересчетаСуммы.Поля[Поле.Ключ] = Поле.Значение - Строка[Поле.Ключ];

		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

// Пересчитывает количество неотгружаемых упаковок товара в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьКоличествоНеОтгружатьСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоНеОтгружать") Тогда
		Если ТекущаяСтрока.КоличествоУпаковок - ТекущаяСтрока.КоличествоУпаковокНеОтгружать > ТекущаяСтрока.КоличествоУпаковокВДокументе - ТекущаяСтрока.КоличествоУпаковокВДокументеНеотгружаемые Тогда
			ТекущаяСтрока.КоличествоУпаковокНеОтгружать = ТекущаяСтрока.КоличествоУпаковок - (ТекущаяСтрока.КоличествоУпаковокВДокументе - ТекущаяСтрока.КоличествоУпаковокВДокументеНеотгружаемые);
		ИначеЕсли ТекущаяСтрока.КоличествоУпаковокНеОтгружать > ТекущаяСтрока.КоличествоУпаковок Тогда
			ТекущаяСтрока.КоличествоУпаковокНеОтгружать = ТекущаяСтрока.КоличествоУпаковок;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает реквизиты в измененных строках. 
// 
// Параметры:
//  ИзмененныеСтроки - Массив из Структура:
//  * Идентификатор - Число - идентификатор группы измененных строк,
//  * Строка - ДанныеФормыЭлементКоллекции,СтрокаТабличнойЧасти - строка, в которой нужно выполнить пересчет.
//  СтруктураПересчета - см. СтруктураПересчетаСуммы.
//
Процедура ПересчитатьРеквизитыВИзмененныхСтроках(ИзмененныеСтроки, СтруктураПересчета) Экспорт
	
	Идентификатор = Неопределено;
	Для Каждого Изменение Из ИзмененныеСтроки Цикл
		Если Идентификатор <> Изменение.Идентификатор Тогда
			Идентификатор = Изменение.Идентификатор;
			ПересчитатьСуммы(СтруктураПересчета);
			ЗаполнитьСтруктуруПересчетаСуммы(СтруктураПересчета, Изменение.Строка);
		КонецЕсли;
		ДобавитьСтрокуДляПересчетаСуммы(СтруктураПересчета, Изменение.Строка);
	КонецЦикла;
	ПересчитатьСуммы(СтруктураПересчета);
	
КонецПроцедуры

// Пересчитывает фактическое количество упаковок товара в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьКоличествоУпаковокПоФактуВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт 

	Если СтруктураДействий.Свойство("ПересчитатьКоличествоУпаковокПоФакту") Тогда
		ТекущаяСтрока.КоличествоУпаковок = ТекущаяСтрока.КоличествоУпаковокУчет - ТекущаяСтрока.КоличествоУпаковокФакт;
	КонецЕсли;

КонецПроцедуры

// Пересчитывает количество товара (в единицах хранения) в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьКоличествоЕдиницВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт 

	Перем УпаковкаНоменклатура;
	
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоЕдиниц", УпаковкаНоменклатура) Тогда
		ПараметрыПересчета = НормализоватьПараметрыПересчетаЕдиниц(ТекущаяСтрока, УпаковкаНоменклатура);
		Если ЗначениеЗаполнено(ПараметрыПересчета.Номенклатура) Тогда
			ДанныеУпаковки = ПолучитьКоэффициентУпаковки(ПараметрыПересчета.Упаковка, КэшированныеЗначения, ПараметрыПересчета.Номенклатура);
			Количество = ТекущаяСтрока.КоличествоУпаковок * ДанныеУпаковки.Коэффициент;
		
			Если ДанныеУпаковки.НужноОкруглятьКоличество
				И ПараметрыПересчета.НужноОкруглять Тогда
				ТекущаяСтрока.Количество = Окр(Количество, 0 ,РежимОкругления.Окр15как20);
			Иначе
				ТекущаяСтрока.Количество = Количество;
			КонецЕсли;
		ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "КоличествоУпаковок") Тогда
			ТекущаяСтрока.Количество = ТекущаяСтрока.КоличествоУпаковок;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает количество товара (в единицах хранения) в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьКоличествоПоРНПТВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт 

	Перем ПараметрыПересчета;
	
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоПоРНПТ", ПараметрыПересчета) Тогда
		
		ИмяПоляКоличество = "Количество";
		ИмяПоляКоличествоПоРНПТ = "КоличествоПоРНПТ";
		Если ПараметрыПересчета.Свойство("ИменаПолейССуффиксом")
			И ЗначениеЗаполнено(ПараметрыПересчета.ИменаПолейССуффиксом) Тогда
			
			Если ПараметрыПересчета.ИменаПолейССуффиксом.Свойство("Количество") Тогда
				ИмяПоляКоличество = ПараметрыПересчета.ИменаПолейССуффиксом.Количество;
			КонецЕсли;
			Если ПараметрыПересчета.ИменаПолейССуффиксом.Свойство("КоличествоПоРНПТ") Тогда
				ИмяПоляКоличествоПоРНПТ = ПараметрыПересчета.ИменаПолейССуффиксом.КоличествоПоРНПТ;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ТекущаяСтрока.ВедетсяУчетПоРНПТ Тогда
			Если СтруктураДействий.Свойство("ЗаполнитьТипНомераГТД") Тогда
				Если ТекущаяСтрока.ТипНомераГТД = ПредопределенноеЗначение("Перечисление.ТипыНомеровГТД.НомерРНПТКомплекта") Тогда
					ТекущаяСтрока[ИмяПоляКоличество] = ТекущаяСтрока[ИмяПоляКоличествоПоРНПТ];
				КонецЕсли;
			Иначе
				Коэффициент = ПолучитьКоэффициентРНПТ(ПараметрыПересчета, ТекущаяСтрока, КэшированныеЗначения);
				
				ТекущаяСтрока[ИмяПоляКоличествоПоРНПТ] = ТекущаяСтрока[ИмяПоляКоличество] * Коэффициент;
			КонецЕсли;
		Иначе
			ТекущаяСтрока[ИмяПоляКоличествоПоРНПТ] = 0;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает количество товара (в единицах хранения) в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьКоличествоПоРНПТСуффиксВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт 

	Перем ПараметрыПересчета;
	
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоПоРНПТСуффикс", ПараметрыПересчета) Тогда
		
		ИмяПоляКоличество = "Количество";
		ИмяПоляКоличествоПоРНПТ = "КоличествоПоРНПТ";
		Если ПараметрыПересчета.Свойство("ИменаПолейССуффиксом")
			И ЗначениеЗаполнено(ПараметрыПересчета.ИменаПолейССуффиксом) Тогда
			
			Если ПараметрыПересчета.ИменаПолейССуффиксом.Свойство("Количество") Тогда
				ИмяПоляКоличество = ПараметрыПересчета.ИменаПолейССуффиксом.Количество;
			КонецЕсли;
			Если ПараметрыПересчета.ИменаПолейССуффиксом.Свойство("КоличествоПоРНПТ") Тогда
				ИмяПоляКоличествоПоРНПТ = ПараметрыПересчета.ИменаПолейССуффиксом.КоличествоПоРНПТ;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ТекущаяСтрока.ВедетсяУчетПоРНПТ Тогда
			Если СтруктураДействий.Свойство("ЗаполнитьТипНомераГТД") Тогда
				Если ТекущаяСтрока.ТипНомераГТД = ПредопределенноеЗначение("Перечисление.ТипыНомеровГТД.НомерРНПТКомплекта") Тогда
					ТекущаяСтрока[ИмяПоляКоличество] = ТекущаяСтрока[ИмяПоляКоличествоПоРНПТ];
				КонецЕсли;
			Иначе
				Коэффициент = ПолучитьКоэффициентРНПТ(ПараметрыПересчета, ТекущаяСтрока, КэшированныеЗначения);
				
				ТекущаяСтрока[ИмяПоляКоличествоПоРНПТ] = ТекущаяСтрока[ИмяПоляКоличество] * Коэффициент;
			КонецЕсли;
		Иначе
			ТекущаяСтрока[ИмяПоляКоличествоПоРНПТ] = 0;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает количество товара (в единицах хранения), имеющего заданный признак, в текущей строке табличной 
// части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьКоличествоЕдиницСуффиксВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт

	Перем ПараметрыПересчетаСуффикс;
	
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоЕдиницСуффикс", ПараметрыПересчетаСуффикс) Тогда
		
		ПараметрыПересчета = НормализоватьПараметрыПересчетаЕдиницСуффикс(ТекущаяСтрока, ПараметрыПересчетаСуффикс);
		КоличествоССуффиксом = КоличествоССуффиксом(ПараметрыПересчета.Суффикс);
		Если ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
			ДанныеУпаковки = ПолучитьКоэффициентУпаковки(ТекущаяСтрока.Упаковка, КэшированныеЗначения, ТекущаяСтрока.Номенклатура);
			Количество = ТекущаяСтрока[КоличествоССуффиксом.КоличествоУпаковок] * ДанныеУпаковки.Коэффициент;
			
			Если ДанныеУпаковки.НужноОкруглятьКоличество
				И ПараметрыПересчета.НужноОкруглять Тогда
				ТекущаяСтрока[КоличествоССуффиксом.Количество] = Окр(Количество, 0 ,РежимОкругления.Окр15как20);
			Иначе
				ТекущаяСтрока[КоличествоССуффиксом.Количество] = Количество;
			КонецЕсли;
		ИначеЕсли ТекущаяСтрока.Свойство(КоличествоССуффиксом.КоличествоУпаковок) Тогда
			ТекущаяСтрока[КоличествоССуффиксом.Количество] = ТекущаяСтрока[КоличествоССуффиксом.КоличествоУпаковок];
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

// Пересчитывает количество упаковок товара в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьКоличествоУпаковокВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоУпаковок") Тогда
		Если ТекущаяСтрока.Количество = 0 Тогда
			ТекущаяСтрока.КоличествоУпаковок = 0;
		Иначе
			Коэффициент = ПолучитьКоэффициентУпаковки(ТекущаяСтрока.Упаковка, КэшированныеЗначения, ТекущаяСтрока.Номенклатура).Коэффициент;
			Если Коэффициент <> 0 Тогда
				ТекущаяСтрока.КоличествоУпаковок = ТекущаяСтрока.Количество / Коэффициент;
			Иначе
				ТекстИсключения = НСтр("ru = 'При попытке пересчета количества в %ЕдИзмерения% превышена допустимая разрядность.'");
				ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ЕдИзмерения%", ТекущаяСтрока.Упаковка);
				
				ТекущаяСтрока.Количество = 0;
				ТекущаяСтрока.КоличествоУпаковок = 0;
				ТекущаяСтрока.Упаковка = ПредопределенноеЗначение("Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка");
				
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Пересчитывает количество упаковок товара, имеющего заданный признак, в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьКоличествоУпаковокСуффиксВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт 
	
	Перем ПараметрыПересчетаСуффикс;
	
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоУпаковокСуффикс",ПараметрыПересчетаСуффикс) Тогда
		ПараметрыПересчета = НормализоватьПараметрыПересчетаЕдиницСуффикс(ТекущаяСтрока, ПараметрыПересчетаСуффикс);
		КоличествоССуффиксом = КоличествоССуффиксом(ПараметрыПересчета.Суффикс);
		Если ТекущаяСтрока[КоличествоССуффиксом.Количество] = 0 Тогда
			ТекущаяСтрока[КоличествоССуффиксом.КоличествоУпаковок] = 0;
		Иначе
			Коэффициент = ПолучитьКоэффициентУпаковки(ТекущаяСтрока.Упаковка, КэшированныеЗначения, ТекущаяСтрока.Номенклатура).Коэффициент;
			Если Коэффициент <> 0 Тогда
				ТекущаяСтрока[КоличествоССуффиксом.КоличествоУпаковок] = ТекущаяСтрока[КоличествоССуффиксом.Количество] / Коэффициент;
			Иначе
				ТекстИсключения = НСтр("ru = 'При попытке пересчета количества в %ЕдИзмерения% превышена допустимая разрядность.'");
				ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ЕдИзмерения%", ТекущаяСтрока.Упаковка);
				
				ТекущаяСтрока[КоличествоССуффиксом.Количество] = 0;
				ТекущаяСтрока[КоличествоССуффиксом.КоличествоУпаковок] = 0;
				ТекущаяСтрока.Упаковка = ПредопределенноеЗначение("Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка");
				
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает отклонения по количеству упаковок товара в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьКоличествоОтклонение(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт 

	Перем ЕстьУпаковки;
	
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоОтклонение", ЕстьУпаковки) Тогда
		ТекущаяСтрока.КоличествоОтклонение = ТекущаяСтрока.КоличествоФакт - ТекущаяСтрока.Количество;
		Если ЕстьУпаковки Тогда
			ТекущаяСтрока.КоличествоУпаковокОтклонение = ТекущаяСтрока.КоличествоУпаковокФакт - ТекущаяСтрока.КоличествоУпаковок;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Пересчитывает отклонения по количеству упаковок товара, имеющего заданный признак, в текущей строке 
// табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьКоличествоУпаковокСуффиксИзОтклоненияВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт 
	
	Суффикс = Неопределено;

	Если СтруктураДействий.Свойство("ПересчитатьКоличествоУпаковокСуффиксИзОтклонения",Суффикс) Тогда
		Если Суффикс = Неопределено Тогда
			Суффикс = "";
		КонецЕсли;
		ТекущаяСтрока[КоличествоССуффиксом(Суффикс).КоличествоУпаковок] = ТекущаяСтрока["КоличествоУпаковок"]
																		+ ТекущаяСтрока["КоличествоУпаковокОтклонение"];
	КонецЕсли;

КонецПроцедуры

// Заполняет вид цен в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьВидЦеныВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Перем СтруктураПараметровДействия;
	
	Если СтруктураДействий.Свойство("ЗаполнитьВидЦены", СтруктураПараметровДействия) Тогда
		
		ИмяПоляВидЦены = "ВидЦены";
		Если СтруктураПараметровДействия.Свойство("ИмяПоляВидЦены") Тогда
			ИмяПоляВидЦены = СтруктураПараметровДействия.ИмяПоляВидЦены
		КонецЕсли;
		
		ТекущаяСтрока[ИмяПоляВидЦены] = СтруктураПараметровДействия.ВидЦены;
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьВидЦеныВСтрокеТЧ()

// Пересчитывает цену за упаковку товара в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьЦенуЗаУпаковкуВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт

	Перем КоличествоЕдиницДоПересчета;

	Если СтруктураДействий.Свойство("ПересчитатьЦенуЗаУпаковку", КоличествоЕдиницДоПересчета)
	   И КоличествоЕдиницДоПересчета <> 0 Тогда
		ТекущаяСтрока.Цена = Окр(ТекущаяСтрока.Цена
		   / КоличествоЕдиницДоПересчета
		   * ТекущаяСтрока.Количество, 2, РежимОкругления.Окр15как20);
	КонецЕсли;

КонецПроцедуры

// Пересчитывает цену за упаковку товара в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьЦенуЗаУпаковкуСуффиксВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт

	Перем ПараметрыПересчета;

	Если СтруктураДействий.Свойство("ПересчитатьЦенуЗаУпаковкуСуффикс", ПараметрыПересчета) Тогда
		Суффикс = ПараметрыПересчета.Суффикс;
		КоличествоЕдиницДоПересчета = ПараметрыПересчета.Количество;
		Если КоличествоЕдиницДоПересчета <> 0 Тогда
			ТекущаяСтрока[ЦенаССуффиксом(Суффикс)] = Окр(ТекущаяСтрока[ЦенаССуффиксом(Суффикс)]
			   / КоличествоЕдиницДоПересчета
			   * ТекущаяСтрока[КоличествоССуффиксом(Суффикс).Количество], 2, РежимОкругления.Окр15как20);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Пересчитывает цену заказа за упаковку товара в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьЦенуЗаказаЗаУпаковкуВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт

	Перем КоличествоЕдиницДоПересчета;

	Если СтруктураДействий.Свойство("ПересчитатьЦенуЗаказаЗаУпаковку", КоличествоЕдиницДоПересчета)
	   И КоличествоЕдиницДоПересчета <> 0 Тогда
		ТекущаяСтрока.ЦенаЗаказа = Окр(ТекущаяСтрока.ЦенаЗаказа
		   / КоличествоЕдиницДоПересчета
		   * ТекущаяСтрока.Количество, 2, РежимОкругления.Окр15как20);
	КонецЕсли;

КонецПроцедуры

// Заполняет значение ставки НДС возвратной тары в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьСтавкуНДСВозвратнойТарыВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Перем ЭтоВозвратнаяТара;
	
	Если СтруктураДействий.Свойство("ЗаполнитьСтавкуНДСВозвратнойТары", ЭтоВозвратнаяТара)
		И ЭтоВозвратнаяТара И ТекущаяСтрока.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.МногооборотнаяТара") Тогда
		ТекущаяСтрока.СтавкаНДС = ПредопределенноеЗначение("Справочник.СтавкиНДС.БезНДС");
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает процент ручной скидки от стоимости товара в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьПроцентРучнойСкидкиВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	ИмяКоличества = Неопределено;
	
	Если СтруктураДействий.Свойство("ПересчитатьПроцентРучнойСкидки", ИмяКоличества) Тогда
		Если НЕ ЗначениеЗаполнено(ИмяКоличества) Тогда
			ИмяКоличества = "КоличествоУпаковок";
		КонецЕсли;
		Если ТекущаяСтрока[ИмяКоличества] * ТекущаяСтрока.Цена <> 0 Тогда
			ТекущаяСтрока.ПроцентРучнойСкидки = Окр(100*ТекущаяСтрока.СуммаРучнойСкидки / (ТекущаяСтрока[ИмяКоличества] *ТекущаяСтрока.Цена), 2);
		Иначе
			ТекущаяСтрока.ПроцентРучнойСкидки = 0;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

// Пересчитывает сумму ручной скидки на товар в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьСуммуРучнойСкидкиВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт

	ИмяКоличества = Неопределено;
		
	Если СтруктураДействий.Свойство("ПересчитатьСуммуРучнойСкидки", ИмяКоличества) Тогда
		
		Если НЕ ЗначениеЗаполнено(ИмяКоличества) Тогда
			ИмяКоличества = "КоличествоУпаковок"; 
		КонецЕсли;
			
		ТекущаяСтрока.СуммаРучнойСкидки = Окр(ТекущаяСтрока[ИмяКоличества] *ТекущаяСтрока.Цена * ТекущаяСтрока.ПроцентРучнойСкидки / 100, 2);
		
	КонецЕсли;

КонецПроцедуры

// Пересчитывает сумму товара в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьСуммуВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт

	ИмяКоличества = Неопределено;
	
	Если СтруктураДействий.Свойство("ПересчитатьСумму", ИмяКоличества) Тогда
		Если НЕ ЗначениеЗаполнено(ИмяКоличества) Тогда
			ИмяКоличества = "КоличествоУпаковок";
		КонецЕсли;
		Если ТекущаяСтрока <> Неопределено Тогда
			ТекущаяСтрока.Сумма = ТекущаяСтрока.Цена * ТекущаяСтрока[ИмяКоличества];
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Пересчитывает сумму товара, имеющего заданный признак, в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьСуммуСуффиксВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	ПараметрыПересчета = Неопределено;
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуСуффикс", ПараметрыПересчета) Тогда
		Если ТипЗнч(ПараметрыПересчета) = Тип("Структура") Тогда
			ИмяКоличества = ПараметрыПересчета.ИмяКоличества;
			Суффикс = ПараметрыПересчета.Суффикс;
		Иначе
			ИмяКоличества = "КоличествоУпаковок";
			Суффикс = ПараметрыПересчета;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Суффикс) Тогда
			Суффикс = ""
		КонецЕсли;
		
		Если ТекущаяСтрока <> Неопределено Тогда
			ТекущаяСтрока[СуммаССуффиксом(Суффикс).Сумма] = ТекущаяСтрока[ЦенаССуффиксом(Суффикс)] * ТекущаяСтрока[КоличествоССуффиксом(Суффикс)[ИмяКоличества]];
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает сумму товара, имеющего заданный признак, в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьСуммуНДССуффиксВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт

	Перем СтруктураПараметровДействия;
	Перем СтавкаНДС;
		
	Если СтруктураДействий.Свойство("ПересчитатьСуммуНДССуффикс", СтруктураПараметровДействия) Тогда
	
		НалогообложениеНДС = Неопределено;
		СтруктураПараметровДействия.Свойство("НалогообложениеНДС", НалогообложениеНДС);
		
		Суффикс = "";
		
		Если СтруктураПараметровДействия.Свойство("Суффикс", Суффикс)
			И Не ЗначениеЗаполнено(Суффикс) Тогда
			Суффикс = "";
		КонецЕсли;
		
		СуммаССуффиксом = СуммаССуффиксом(Суффикс);
		СтавкаССуффиксом = СтавкаССуффиксом(Суффикс);
		
		Если Не (СтруктураПараметровДействия <> Неопределено
				И СтруктураПараметровДействия.Свойство("СтавкаНДС", СтавкаНДС)) Тогда
			
			СтавкаНДС = ТекущаяСтрока[СтавкаССуффиксом.СтавкаНДС];
			
		КонецЕсли;
		
		Если КэшированныеЗначения.Свойство("ПроцентыСтавокНДС") Тогда
			ТекПроцентНДС = КэшированныеЗначения.ПроцентыСтавокНДС[СтавкаНДС];
		Иначе
			ТекПроцентНДС = Неопределено;
			
			КэшированныеЗначения.Вставить("ПроцентыСтавокНДС", Новый Соответствие);
		КонецЕсли;
		
		Если ТекПроцентНДС = Неопределено Тогда
			
			ТекПроцентНДС = УчетНДСУПКлиентСервер.ЗначениеСтавкиНДС(СтавкаНДС);
			КэшированныеЗначения.ПроцентыСтавокНДС.Вставить(СтавкаНДС, ТекПроцентНДС);
			
		КонецЕсли;
		
		ТекущаяСтрока[СуммаССуффиксом.СуммаНДС] = УчетНДСУПКлиентСервер.РассчитатьСуммуНДС(
			ТекущаяСтрока[СуммаССуффиксом.Сумма],
			ТекПроцентНДС,
			СтруктураПараметровДействия.ЦенаВключаетНДС,
			НалогообложениеНДС);
		
	КонецЕсли;

КонецПроцедуры

// Пересчитывает сумму товара в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьСуммуВСтрокеТЧРасхождения(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт

	Если СтруктураДействий.Свойство("ПересчитатьСуммуРасхождения") Тогда
		Если ТекущаяСтрока <> Неопределено Тогда
			Если ТекущаяСтрока.КоличествоУпаковокПоДокументу = 0 Тогда
				ТекущаяСтрока.Сумма = ТекущаяСтрока.Цена * ТекущаяСтрока.КоличествоУпаковок;
			Иначе
				ДоляРасхождения = ТекущаяСтрока.КоличествоУпаковок / ТекущаяСтрока.КоличествоУпаковокПоДокументу;
				ТекущаяСтрока.Сумма = ТекущаяСтрока.СуммаПоДокументу * ДоляРасхождения;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Формирует названия идентификаторов цены с суффиксом
//
// Параметры:
//  Суффикс - Строка
//
// Возвращаемое значение:
//  Строка - имя поля цена.
//
Функция ЦенаССуффиксом(Суффикс) Экспорт
	
	Если Суффикс = "Забаланс" Тогда
		ИмяПоляЦена = "ЦенаЗабаланс";
	ИначеЕсли Суффикс = "После" Тогда
		ИмяПоляЦена = "ЦенаПосле";
	Иначе
		ИмяПоляЦена = "Цена";
	КонецЕсли;
	
	Возврат ИмяПоляЦена;
	
КонецФункции

// Формирует названия идентификаторов суммы с суффиксом
// 
// Параметры:
// 	Суффикс - Строка - суффикс
// 
// Возвращаемое значение:
// 	Структура - набор названий идентификаторов. Описание:
// * Сумма - Строка - "Сумма" + суффикс
// * СуммаНДС - Строка - "СуммаНДС" + суффикс
// * СуммаСНДС - Строка - "СуммаСНДС" + суффикс
//
Функция СуммаССуффиксом(Суффикс) Экспорт
	
	СтруктураВозврата = Новый Структура("Сумма, СуммаНДС, СуммаСНДС, СуммаВзаиморасчетов");
	
	Если Не ЗначениеЗаполнено(Суффикс) Тогда
		СтруктураВозврата.Сумма = "Сумма";
		СтруктураВозврата.СуммаНДС = "СуммаНДС";
		СтруктураВозврата.СуммаСНДС = "СуммаСНДС";
		СтруктураВозврата.СуммаВзаиморасчетов = "СуммаВзаиморасчетов";
	ИначеЕсли Суффикс = "ПоДокументу" Тогда
		СтруктураВозврата.Сумма = "СуммаПоДокументу";
		СтруктураВозврата.СуммаНДС = "СуммаНДСПоДокументу";
		СтруктураВозврата.СуммаСНДС = "СуммаСНДСПоДокументу";
		СтруктураВозврата.СуммаВзаиморасчетов = "";
	ИначеЕсли Суффикс = "После" Тогда
		СтруктураВозврата.Сумма = "СуммаПосле";
		СтруктураВозврата.СуммаНДС = "СуммаНДСПосле";
		СтруктураВозврата.СуммаСНДС = "СуммаСНДСПосле";
		СтруктураВозврата.СуммаВзаиморасчетов = "СуммаВзаиморасчетовПосле";
	ИначеЕсли Суффикс = "Забаланс" Тогда
		СтруктураВозврата.Сумма     = "СуммаЗабаланс";
		СтруктураВозврата.СуммаНДС  = "";
		СтруктураВозврата.СуммаСНДС = "";
		СтруктураВозврата.СуммаВзаиморасчетов = "";
	Иначе
		ВызватьИсключение НСтр("ru = 'Неизвестный суффикс для суммы:'") + " " + Суффикс;
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Формирует названия идентификаторов ставки НДС с суффиксом
// 
// Параметры:
// 	Суффикс - Строка - суффикс
// 	
// Возвращаемое значение:
// 	Структура - набор названий идентификаторов. Описание:
// * СтавкаНДС - Строка - "СтавкаНДС" + суффикс
//
Функция СтавкаССуффиксом(Суффикс) Экспорт
	
	СтруктураВозврата = Новый Структура("СтавкаНДС");
	
	Если Не ЗначениеЗаполнено(Суффикс) Тогда
		СтруктураВозврата.СтавкаНДС = "СтавкаНДС";
	ИначеЕсли Суффикс = "ПоДокументу" Тогда
		СтруктураВозврата.СтавкаНДС = "СтавкаНДС";
	ИначеЕсли Суффикс = "После" Тогда
		СтруктураВозврата.СтавкаНДС = "СтавкаНДСПосле";
	Иначе
		ВызватьИсключение НСтр("ru = 'Неизвестный суффикс для ставки:'") + " " + Суффикс;
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции


// Пересчитывает цену скидки в текущей строке.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьЦенуСкидкуПоСуммеВПродажахВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	СтруктураПараметровДействия = Неопределено;
	
	Если СтруктураДействий.Свойство("ПересчитатьЦенуСкидкуПоСуммеВПродажах", СтруктураПараметровДействия) Тогда
		
		ИспользоватьРучныеСкидки = Ложь;
		Если СтруктураПараметровДействия <> Неопределено Тогда
			Если СтруктураПараметровДействия.Свойство("ИспользоватьРучныеСкидки") Тогда
				ИспользоватьРучныеСкидки = Истина;
			КонецЕсли;
		КонецЕсли;
		
		ИспользоватьАвтоматическиеСкидки = Ложь;
		Если СтруктураПараметровДействия <> Неопределено Тогда
			Если СтруктураПараметровДействия.Свойство("ИспользоватьАвтоматическиеСкидки") Тогда
				ИспользоватьАвтоматическиеСкидки = Истина;
			КонецЕсли;
		КонецЕсли;
		
		ИспользоватьБонусныеБаллы = Ложь;
		Если СтруктураПараметровДействия <> Неопределено Тогда
			Если СтруктураПараметровДействия.Свойство("ИспользоватьБонусныеБаллы") Тогда
				ИспользоватьБонусныеБаллы = Истина;
			КонецЕсли;
		КонецЕсли;
		
		ИмяКоличества = "КоличествоУпаковок";
		Если СтруктураПараметровДействия <> Неопределено Тогда
			Если НЕ СтруктураПараметровДействия.Свойство("ИмяКоличества",ИмяКоличества) Тогда
				ИмяКоличества = "КоличествоУпаковок";
			КонецЕсли;
		КонецЕсли;
		
		Если ИспользоватьРучныеСкидки И КэшированныеЗначения.ИспользоватьРучныеСкидкиВПродажах = Неопределено Тогда
			КэшированныеЗначения.ИспользоватьРучныеСкидкиВПродажах = 
				ОбщегоНазначенияУТВызовСервера.ЗначениеФункциональнойОпции("ИспользоватьРучныеСкидкиВПродажах");
		КонецЕсли;
		
		Если ИспользоватьАвтоматическиеСкидки И КэшированныеЗначения.ИспользоватьАвтоматическиеСкидкиВПродажах = Неопределено Тогда
			КэшированныеЗначения.ИспользоватьАвтоматическиеСкидкиВПродажах = 
				ОбщегоНазначенияУТВызовСервера.ЗначениеФункциональнойОпции("ИспользоватьАвтоматическиеСкидкиВПродажах");
		КонецЕсли;
		// Если используются ручные скидки - перерасчитаем процент и сумму ручной скидки, иначе перерасчитываем цену.
		Если ИспользоватьРучныеСкидки И КэшированныеЗначения.ИспользоватьРучныеСкидкиВПродажах Тогда
			
			Если ТекущаяСтрока.Сумма = 0 Или ТекущаяСтрока[ИмяКоличества] = 0 Тогда
				
				ТекущаяСтрока.Цена                = 0;
				ТекущаяСтрока.СуммаРучнойСкидки   = 0;
				
			Иначе
				Если ЗначениеЗаполнено(ТекущаяСтрока.ВидЦены) И ТекущаяСтрока.Цена = 0 Тогда
					ТекущаяСтрока.СуммаРучнойСкидки = 0;
					ТекущаяСтрока.ПроцентРучнойСкидки = 0;
				ИначеЕсли ТекущаяСтрока.Цена = 0 Тогда
					Если ИспользоватьБонусныеБаллы Тогда
						Если ЗначениеЗаполнено(ТекущаяСтрока.ПроцентРучнойСкидки) Тогда
							ТекущаяСтрока.СуммаРучнойСкидки = ((ТекущаяСтрока.Сумма - ТекущаяСтрока.СуммаБонусныхБалловКСписаниюВВалюте) * ТекущаяСтрока.ПроцентРучнойСкидки)  / (100 - ТекущаяСтрока.ПроцентРучнойСкидки);
						КонецЕсли;
						ТекущаяСтрока.Цена = Окр((ТекущаяСтрока.Сумма + ТекущаяСтрока.СуммаРучнойСкидки + ТекущаяСтрока.СуммаБонусныхБалловКСписаниюВВалюте) / ТекущаяСтрока[ИмяКоличества], 2);
					Иначе
						Если ЗначениеЗаполнено(ТекущаяСтрока.ПроцентРучнойСкидки) Тогда
							ТекущаяСтрока.СуммаРучнойСкидки = (ТекущаяСтрока.Сумма * ТекущаяСтрока.ПроцентРучнойСкидки)  / (100 - ТекущаяСтрока.ПроцентРучнойСкидки);
						КонецЕсли;
						ТекущаяСтрока.Цена = Окр((ТекущаяСтрока.Сумма + ТекущаяСтрока.СуммаРучнойСкидки) / ТекущаяСтрока[ИмяКоличества], 2);
					КонецЕсли;
				Иначе
					СуммаБезСкидки = ТекущаяСтрока.Цена * ТекущаяСтрока[ИмяКоличества];
					Если ИспользоватьБонусныеБаллы Тогда
						ТекущаяСтрока.СуммаРучнойСкидки   = СуммаБезСкидки - ТекущаяСтрока.Сумма - ТекущаяСтрока.СуммаБонусныхБалловКСписаниюВВалюте;
					Иначе
						ТекущаяСтрока.СуммаРучнойСкидки   = СуммаБезСкидки - ТекущаяСтрока.Сумма;
					КонецЕсли;
					ТекущаяСтрока.ПроцентРучнойСкидки = Окр(100*ТекущаяСтрока.СуммаРучнойСкидки / СуммаБезСкидки, 2);
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			Если ТекущаяСтрока.Сумма = 0 Или ТекущаяСтрока[ИмяКоличества] = 0 Тогда
				ТекущаяСтрока.Цена = 0;
			Иначе
				Если ИспользоватьБонусныеБаллы Тогда
					ТекущаяСтрока.Цена = Окр((ТекущаяСтрока.Сумма - ТекущаяСтрока.СуммаБонусныхБалловКСписаниюВВалюте) / ТекущаяСтрока[ИмяКоличества], 2);
				Иначе
					ТекущаяСтрока.Цена = Окр(ТекущаяСтрока.Сумма / ТекущаяСтрока[ИмяКоличества], 2);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ИспользоватьАвтоматическиеСкидки Тогда
			Если КэшированныеЗначения.ИспользоватьАвтоматическиеСкидкиВПродажах Тогда
				ТекущаяСтрока.СуммаАвтоматическойСкидки   = 0;
				ТекущаяСтрока.ПроцентАвтоматическойСкидки = 0;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает цену и сумму ручной скидки от стоимости товара в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьЦенуСкидкуПоСуммеВЗакупкахВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	СтруктураПараметровДействия = Неопределено;
	
	Если СтруктураДействий.Свойство("ПересчитатьЦенуСкидкуПоСуммеВЗакупках", СтруктураПараметровДействия) Тогда
		
		// Если используются ручные скидки - перерасчитаем процент и сумму ручной скидки, иначе перерасчитываем цену.
		ИспользоватьРучныеСкидки = Ложь;
		Если СтруктураПараметровДействия <> Неопределено Тогда
			Если СтруктураПараметровДействия.Свойство("ИспользоватьРучныеСкидки") Тогда
				ИспользоватьРучныеСкидки = Истина;
			КонецЕсли;
		КонецЕсли;
		
		ИмяКоличества = "КоличествоУпаковок";
		Если СтруктураПараметровДействия <> Неопределено Тогда
			Если НЕ СтруктураПараметровДействия.Свойство("ИмяКоличества",ИмяКоличества) Тогда
				ИмяКоличества = "КоличествоУпаковок";
			КонецЕсли;
		КонецЕсли;
		
		Если ИспользоватьРучныеСкидки И КэшированныеЗначения.ИспользоватьРучныеСкидкиВЗакупках = Неопределено Тогда
			КэшированныеЗначения.ИспользоватьРучныеСкидкиВЗакупках =
				ОбщегоНазначенияУТВызовСервера.ЗначениеФункциональнойОпции("ИспользоватьРучныеСкидкиВЗакупках");
		КонецЕсли;
		
		// Если используются ручные скидки - перерасчитаем процент и сумму ручной скидки, иначе перерасчитываем цену.
		Если ИспользоватьРучныеСкидки И КэшированныеЗначения.ИспользоватьРучныеСкидкиВЗакупках Тогда
			
			Если ТекущаяСтрока.Сумма = 0 Или ТекущаяСтрока[ИмяКоличества] = 0 Тогда
				
				ТекущаяСтрока.Цена                = 0;
				ТекущаяСтрока.СуммаРучнойСкидки   = 0;
				
			Иначе
				
				Если ТекущаяСтрока.Цена = 0 Тогда
					Если ЗначениеЗаполнено(ТекущаяСтрока.ПроцентРучнойСкидки) Тогда
						ТекущаяСтрока.СуммаРучнойСкидки = (ТекущаяСтрока.Сумма * ТекущаяСтрока.ПроцентРучнойСкидки)  / (100 - ТекущаяСтрока.ПроцентРучнойСкидки);
					КонецЕсли;
					ТекущаяСтрока.Цена = Окр((ТекущаяСтрока.Сумма + ТекущаяСтрока.СуммаРучнойСкидки) / ТекущаяСтрока[ИмяКоличества], 2);
				Иначе
					СуммаБезСкидки = ТекущаяСтрока.Цена * ТекущаяСтрока[ИмяКоличества];
					ТекущаяСтрока.СуммаРучнойСкидки   = СуммаБезСкидки - ТекущаяСтрока.Сумма;
					ТекущаяСтрока.ПроцентРучнойСкидки = Окр(100*ТекущаяСтрока.СуммаРучнойСкидки / СуммаБезСкидки, 2);
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			Если ТекущаяСтрока.Сумма = 0 Или ТекущаяСтрока[ИмяКоличества] = 0 Тогда
				ТекущаяСтрока.Цена = 0;
			Иначе
				ТекущаяСтрока.Цена = Окр(ТекущаяСтрока.Сумма / ТекущаяСтрока[ИмяКоличества], 2);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает цену от стоимости товара в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьЦенуПоСуммеВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	ИмяКоличества = Неопределено;
	
	Если СтруктураДействий.Свойство("ПересчитатьЦенуПоСумме",ИмяКоличества) Тогда
		Если Не ЗначениеЗаполнено(ИмяКоличества) Тогда
			ИмяКоличества = "КоличествоУпаковок";
		КонецЕсли;
		Если ТекущаяСтрока[ИмяКоличества] = 0 Тогда
			ТекущаяСтрока.Цена = ТекущаяСтрока.Сумма;
		Иначе
			ТекущаяСтрока.Цена = Окр(ТекущаяСтрока.Сумма / ТекущаяСтрока[ИмяКоличества], 2);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


// Пересчитывает цену продажи товара от суммы продажи в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьЦенуПродажиПоСуммеПродажиВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Если СтруктураДействий.Свойство("ПересчитатьЦенуПродажиПоСуммеПродажи") Тогда
		
		ТекущаяСтрока.ЦенаПродажи = ?(ТекущаяСтрока.КоличествоУпаковок <> 0, Окр(ТекущаяСтрока.СуммаПродажи / ТекущаяСтрока.КоличествоУпаковок, 2, 1), 0);
		
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает стоимость товара с учетом скидки в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьСуммуСУчетомАвтоматическойСкидкиВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт

	СтруктураПараметровДействия = Неопределено;
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуСУчетомАвтоматическойСкидки", СтруктураПараметровДействия) Тогда
		
		Очищать = Неопределено;
		СтруктураПараметровДействия.Свойство("Очищать", Очищать);
		
		Если Очищать Тогда
			
			ТекущаяСтрока.СуммаАвтоматическойСкидки = 0;
			ТекущаяСтрока.ПроцентАвтоматическойСкидки = 0;
			
		КонецЕсли;
		
		Скидка = ТекущаяСтрока.СуммаАвтоматическойСкидки;
		
		ТекущаяСтрока.Сумма = ТекущаяСтрока.Сумма - Скидка;
		
	КонецЕсли;

КонецПроцедуры

// Пересчитывает стоимость товара с учетом ручной скидки в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьСуммуСУчетомРучнойСкидкиВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт

	СтруктураПараметровДействия = Неопределено;
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуСУчетомРучнойСкидки", СтруктураПараметровДействия) Тогда
		
		Очищать = Неопределено;
		ПересчитыватьСуммуРучнойСкидки = Неопределено;
		ИмяКоличества = Неопределено;
		
		Если СтруктураПараметровДействия <> Неопределено Тогда
			
			СтруктураПараметровДействия.Свойство("Очищать", Очищать);
			СтруктураПараметровДействия.Свойство("ПересчитыватьСуммуРучнойСкидки", ПересчитыватьСуммуРучнойСкидки);
			СтруктураПараметровДействия.Свойство("ИмяКоличества", ИмяКоличества);
			
			Если НЕ ЗначениеЗаполнено(ИмяКоличества) Тогда
				ИмяКоличества = "КоличествоУпаковок";
			КонецЕсли;
			
			Если Очищать = Истина Тогда
				
				ТекущаяСтрока.СуммаРучнойСкидки = 0;
				ТекущаяСтрока.ПроцентРучнойСкидки = 0;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПересчитыватьСуммуРучнойСкидки = Ложь Тогда
			
		Иначе
			ТекущаяСтрока.СуммаРучнойСкидки = Окр(ТекущаяСтрока[ИмяКоличества] *ТекущаяСтрока.Цена * ТекущаяСтрока.ПроцентРучнойСкидки / 100, 2);
		КонецЕсли;
			
		ТекущаяСтрока.Сумма = ТекущаяСтрока.Сумма - ТекущаяСтрока.СуммаРучнойСкидки;
		
	КонецЕсли;

КонецПроцедуры

// Пересчитывает стоимость товара с учетом скидки по бонусным баллам в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьСуммуСУчетомСкидкиБонуснымиБалламиВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт

	СтруктураПараметровДействия = Неопределено;
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуСУчетомСкидкиБонуснымиБаллами", СтруктураПараметровДействия) Тогда
		
		ПересчитыватьСуммуБонусныхБалловПоСтроке = Неопределено;
		
		Если СтруктураПараметровДействия <> Неопределено Тогда
			
			СтруктураПараметровДействия.Свойство("ПересчитыватьСуммуБонусныхБалловПоСтроке", ПересчитыватьСуммуБонусныхБалловПоСтроке);
			
		КонецЕсли;
		
		Если ПересчитыватьСуммуБонусныхБалловПоСтроке = Истина Тогда
			
			ИмяКоличества = "КоличествоУпаковок";
			ИмяКоличестваИсходное = "КоличествоУпаковокВЗаказе";
			
			ПарыРеквизитовДляПересчета = Новый Соответствие;
			ПарыРеквизитовДляПересчета.Вставить("СуммаБонусныхБалловКСписанию",
												"СуммаБонусныхБалловКСписаниюПоЗаказу");
			ПарыРеквизитовДляПересчета.Вставить("СуммаБонусныхБалловКСписаниюВВалюте",
												"СуммаБонусныхБалловКСписаниюВВалютеПоЗаказу");
			
			КолвоУпаковок = ТекущаяСтрока[ИмяКоличества];
			КолвоУпаковокИсходное = ТекущаяСтрока[ИмяКоличестваИсходное];
			Если КолвоУпаковок < КолвоУпаковокИсходное Тогда
				Для Каждого Элем Из ПарыРеквизитовДляПересчета Цикл
					ТекущаяСтрока[Элем.Ключ] = ТекущаяСтрока[Элем.Значение]*КолвоУпаковок/КолвоУпаковокИсходное;
				КонецЦикла;
			Иначе
				Для Каждого Элем Из ПарыРеквизитовДляПересчета Цикл
					ТекущаяСтрока[Элем.Ключ] = ТекущаяСтрока[Элем.Значение];
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
		ДанныеДляПересчета = Неопределено;
		ИмяКоличества = Неопределено;

		Если СтруктураПараметровДействия <> Неопределено Тогда
			
			СтруктураПараметровДействия.Свойство("ПересчитыватьСумму", ДанныеДляПересчета);
			СтруктураПараметровДействия.Свойство("ИмяКоличества", ИмяКоличества);
			
			Если НЕ ЗначениеЗаполнено(ИмяКоличества) Тогда
				ИмяКоличества = "КоличествоУпаковок";
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеДляПересчета) Тогда
				ТекущаяСтрока.СуммаБонусныхБалловКСписаниюВВалюте =  Мин(Окр(
													ДанныеДляПересчета.СуммаБонусныхБалловКСписаниюВВалюте 
													* ТекущаяСтрока[ИмяКоличества] 
													/ ДанныеДляПересчета[ИмяКоличества], 2, РежимОкругления.Окр15как20),
													ДанныеДляПересчета.СуммаБонусныхБалловКСписаниюВВалюте);
			КонецЕсли;
			
		КонецЕсли;
		
		ТекущаяСтрока.Сумма = ТекущаяСтрока.Сумма - ТекущаяСтрока.СуммаБонусныхБалловКСписаниюВВалюте;
		
	КонецЕсли;

КонецПроцедуры

// Пересчитывает стоимость товара с НДС в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьСуммуСНДСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Перем СтруктураПараметровДействия;
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуСНДС", СтруктураПараметровДействия) Тогда
		
		ТекущаяСтрока.СуммаСНДС = ТекущаяСтрока.Сумма + ?(СтруктураПараметровДействия.ЦенаВключаетНДС, 0, ТекущаяСтрока.СуммаНДС);
		
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает стоимость товара с НДС, имеющего заданный признак, в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьСуммуСНДССуффиксВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Перем СтруктураПараметровДействия;
	
	Суффикс = "";
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуСНДССуффикс", СтруктураПараметровДействия) Тогда
		
		Если СтруктураПараметровДействия.Свойство("Суффикс", Суффикс)
			И Не ЗначениеЗаполнено(Суффикс) Тогда
			
			Суффикс = "";
			
		КонецЕсли;
		
		СуммаССуффиксом = СуммаССуффиксом(Суффикс);
		
		ТекущаяСтрока[СуммаССуффиксом.СуммаСНДС] = Окр(ТекущаяСтрока[СуммаССуффиксом.Сумма]
			+ ?(СтруктураПараметровДействия.ЦенаВключаетНДС, 0, ТекущаяСтрока[СуммаССуффиксом.СуммаНДС]), 2, РежимОкругления.Окр15как20);
		
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает стоимость товара без НДС в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьСуммуБезНДСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуБезНДС") Тогда
		
	    ТекущаяСтрока.СуммаБезНДС = Окр(ТекущаяСтрока.Сумма - ТекущаяСтрока.СуммаНДС, 2, РежимОкругления.Окр15как20);
		
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает стоимость товара в валюте регламентированного учета в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьСуммуРеглВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Перем КоэффициентПересчетаИзВалютыУпрВРегл;
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуРегл", КоэффициентПересчетаИзВалютыУпрВРегл) Тогда
		ТекущаяСтрока.СуммаРегл = Окр(ТекущаяСтрока.СуммаБезНДС * КоэффициентПересчетаИзВалютыУпрВРегл, 2, 1);
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает сумму постоянной разницы в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьСуммуПРВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуПР") И НЕ ТекущаяСтрока.ПринимаетсяКНУ Тогда
		ТекущаяСтрока.СуммаПР = ТекущаяСтрока.СуммаРегл;
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает сумму НДС в валюте регламентированного учета в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьНДСРеглВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Перем СтруктураПараметровДействия;
	
	Если СтруктураДействий.Свойство("ПересчитатьНДСРегл", СтруктураПараметровДействия) Тогда
		
		НалогообложениеНДС = Неопределено;
		СтруктураПараметровДействия.Свойство("НалогообложениеНДС", НалогообложениеНДС);
		
		СтавкаНДС = Неопределено;
		Если Не СтруктураПараметровДействия.Свойство("СтавкаНДС", СтавкаНДС) Тогда
			СтавкаНДС = ТекущаяСтрока.СтавкаНДС;
		КонецЕсли;
		
		Если КэшированныеЗначения.Свойство("ПроцентыСтавокНДС") Тогда
			ТекПроцентНДС = КэшированныеЗначения.ПроцентыСтавокНДС[СтавкаНДС];
		Иначе
			КэшированныеЗначения.Вставить("ПроцентыСтавокНДС", Новый Соответствие);
			ТекПроцентНДС = Неопределено;
		КонецЕсли;
		
		Если ТекПроцентНДС = Неопределено Тогда
			
			ТекПроцентНДС = УчетНДСУПКлиентСервер.ЗначениеСтавкиНДС(СтавкаНДС);
			КэшированныеЗначения.ПроцентыСтавокНДС.Вставить(СтавкаНДС, ТекПроцентНДС);
			
		КонецЕсли;
		
		ТекущаяСтрока.НДСРегл = УчетНДСУПКлиентСервер.РассчитатьСуммуНДС(
			ТекущаяСтрока.СуммаРегл,
			ТекПроцентНДС,
			Ложь,
			НалогообложениеНДС);
			
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает сумму продажи товара в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьСуммуПродажиВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Перем СтруктураПараметровДействия;
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуПродажи", СтруктураПараметровДействия) Тогда
	
		ТекущаяСтрока.СуммаПродажи = ТекущаяСтрока.ЦенаПродажи * ТекущаяСтрока.КоличествоУпаковок;
		
	КонецЕсли;

КонецПроцедуры

// Пересчитывает сумму продажи НДС товара в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьСуммуПродажиНДСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Перем СтруктураПараметровДействия;
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуПродажиНДС", СтруктураПараметровДействия) Тогда
		
		НалогообложениеНДС = Неопределено;
		Если СтруктураПараметровДействия <> Неопределено 
			И СтруктураПараметровДействия.Свойство("НалогообложениеНДС", НалогообложениеНДС) Тогда
			Если НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя") 
				Или НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДССМежценовойРазницы") Тогда
				// Пересчет не требуется 
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		СтавкаНДС = Неопределено;
		Если СтруктураПараметровДействия = Неопределено Или Не СтруктураПараметровДействия.Свойство("СтавкаНДС", СтавкаНДС) Тогда
			СтавкаНДС = ТекущаяСтрока.СтавкаНДС;
		КонецЕсли;
		
		Если КэшированныеЗначения.Свойство("ПроцентыСтавокНДС") Тогда
			ТекПроцентНДС = КэшированныеЗначения.ПроцентыСтавокНДС[СтавкаНДС];
		Иначе
			КэшированныеЗначения.Вставить("ПроцентыСтавокНДС", Новый Соответствие);
			ТекПроцентНДС = Неопределено;
		КонецЕсли;
		
		Если ТекПроцентНДС = Неопределено Тогда
			
			ТекПроцентНДС = УчетНДСУПКлиентСервер.ЗначениеСтавкиНДС(СтавкаНДС);
			КэшированныеЗначения.ПроцентыСтавокНДС.Вставить(СтавкаНДС, ТекПроцентНДС);
			
		КонецЕсли;
		
		ТекущаяСтрока.СуммаПродажиНДС = Окр(ТекущаяСтрока.СуммаПродажи * ТекПроцентНДС / (100 + ТекПроцентНДС), 2, РежимОкругления.Окр15как20);
	КонецЕсли;

КонецПроцедуры

// Устанавливает признак активности строки в табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура УстановитьАктивностьСтроки(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Если СтруктураДействий.Свойство("УстановитьАктивность") Тогда
		ТекущаяСтрока.Активность = Истина;
	КонецЕсли;
	
КонецПроцедуры


// Заполняет поле назначения товара (услуги) в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПроверитьЗаполнитьНазначениеВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Назначение = Неопределено;
	Если СтруктураДействий.Свойство("ПроверитьЗаполнитьНазначение", Назначение) Тогда
		
		ТекущаяСтрока.Назначение = ПредопределенноеЗначение("Справочник.Назначения.ПустаяСсылка");
		
		Если Назначение <> Неопределено Тогда
			
			Если ТекущаяСтрока.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар")
				Или ТекущаяСтрока.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Работа") Тогда
				
				ТекущаяСтрока.Назначение = Назначение;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет поле назначения товара (услуги) в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьФлагОбособленноПоНазначению(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	ИмяРеквизитаОтгружатьОбособленно = Неопределено;
	Если СтруктураДействий.Свойство("ЗаполнитьФлагОбособленноПоНазначению", ИмяРеквизитаОтгружатьОбособленно) Тогда
		
		ТекущаяСтрока[ИмяРеквизитаОтгружатьОбособленно] = ЗначениеЗаполнено(ТекущаяСтрока.Назначение);
		
	КонецЕсли;

КонецПроцедуры

// Заполняет поля, используемые для отражения расхождений и излишков по товару в текущей строке табличной 
// части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьФлагиРасхождениеИИзлишекПорча(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Если СтруктураДействий.Свойство("ЗаполнитьФлагРасхождение") Тогда
		ТекущаяСтрока.Расхождение = (ТекущаяСтрока.КоличествоУпаковок <> ТекущаяСтрока.КоличествоУпаковокФакт);		
	КонецЕсли;
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоУпаковокОтклонение") Тогда
		ТекущаяСтрока.КоличествоУпаковокОтклонение = ТекущаяСтрока.КоличествоУпаковокФакт - ТекущаяСтрока.КоличествоУпаковок;
	КонецЕсли;
	Если СтруктураДействий.Свойство("ЗаполнитьФлагИзлишекПорча") Тогда
		ТекущаяСтрока.ИзлишекПорча = Истина;		
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает нулевое значение в поле суммы взаиморасчетов по товару в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ОчиститьСуммуВзаиморасчетовВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Если СтруктураДействий.Свойство("ОчиститьСуммуВзаиморасчетов") Тогда
		
		ТекущаяСтрока.СуммаВзаиморасчетов = 0;
		
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает нулевое значение в поле суммы взаиморасчетов (с суффиксом) по товару в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ОчиститьСуммуВзаиморасчетовСуффиксВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Суффикс = Неопределено;
	Если СтруктураДействий.Свойство("ОчиститьСуммуВзаиморасчетовСуффикс", Суффикс) Тогда
	
		Если ЗначениеЗаполнено(Суффикс) Тогда
			ТекущаяСтрока[СуммаССуффиксом(Суффикс).СуммаВзаиморасчетов] = 0;
		Иначе
			ТекущаяСтрока.СуммаВзаиморасчетов = 0;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает нулевое значение для процента и суммы скидки по товару в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ОчиститьАвтоматическуюСкидкуВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Если СтруктураДействий.Свойство("ОчиститьАвтоматическуюСкидку") Тогда
		
		ТекущаяСтрока.ПроцентАвтоматическойСкидки = 0;
		ТекущаяСтрока.СуммаАвтоматическойСкидки = 0;
		
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает нулевое значение для суммы вознаграждения по товару в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ОчиститьСуммуВознагражденияВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Перем СтруктураПараметровДействия;
	
	Если СтруктураДействий.Свойство("ОчиститьСуммуВознаграждения", СтруктураПараметровДействия) Тогда
	
		ТекущаяСтрока.СуммаВознаграждения = 0;
		
	КонецЕсли;

КонецПроцедуры

// Заполняет поле партнера предопределенным значением в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьПартнера(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Если СтруктураДействий.Свойство("ЗаполнитьПартнера") Тогда
		Если Не ЗначениеЗаполнено(ТекущаяСтрока.Партнер) Тогда
			ТекущаяСтрока.Партнер = ПредопределенноеЗначение("Справочник.Партнеры.РозничныйПокупатель");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет признак безвозвратной тары в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьПризнакБезВозвратнойТары(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Перем ВернутьМногооборотнуюТару;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакБезВозвратнойТары", ВернутьМногооборотнуюТару) Тогда
	
		ТекущаяСтрока.БезВозвратнойТары = Не (ВернутьМногооборотнуюТару
			И ТекущаяСтрока.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.МногооборотнаяТара"));
	
	КонецЕсли;
	
КонецПроцедуры

// Заполняет признак отмены заказа по безвозвратной таре в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьПризнакОтмененоБезВозвратнойТары(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Перем ВернутьМногооборотнуюТару;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакОтмененоБезВозвратнойТары", ВернутьМногооборотнуюТару) Тогда
	
		ТекущаяСтрока.ОтмененоБезВозвратнойТары = Не (ВернутьМногооборотнуюТару
			И ТекущаяСтрока.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.МногооборотнаяТара")) И ТекущаяСтрока.Отменено;
	
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает признак активности строки безвозвратной тары в табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьПризнакАктивностьБезВозвратнойТары(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Перем ВернутьМногооборотнуюТару;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакАктивностьБезВозвратнойТары", ВернутьМногооборотнуюТару) Тогда
	
		ТекущаяСтрока.АктивностьБезВозвратнойТары = Не (ВернутьМногооборотнуюТару
			И ТекущаяСтрока.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.МногооборотнаяТара")) И ТекущаяСтрока.Активность;
	
	КонецЕсли;
	
КонецПроцедуры

// Заполняет поля дубликатов зависимых реквизитов в табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьДубликатыЗависимыхРеквизитов(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Перем ПараметрСтруктурыДействий;
	Если Не СтруктураДействий.Свойство("ЗаполнитьДубликатыЗависимыхРеквизитов", ПараметрСтруктурыДействий)
		Или Не ЗначениеЗаполнено(ПараметрСтруктурыДействий) Тогда
		Возврат;
	КонецЕсли;
	
	// ПараметрСтруктурыДействий - Структура
	// 		Ключ: Имя реквизита флага активности
	// 		Значение: Строка с перечислением через запятую имен зависимых реквизитов.
	
	Для Каждого ПолеСтруктуры Из ПараметрСтруктурыДействий Цикл
		
		ИмяФлагаАктивности = ПолеСтруктуры.Ключ;
		ЗначениеФлагаАктивности = ТекущаяСтрока[ИмяФлагаАктивности];
		
		ЗависимыеРеквизиты = Новый Структура(ПолеСтруктуры.Значение);
		Для Каждого Реквизит Из ЗависимыеРеквизиты Цикл
			ТекущаяСтрока[Реквизит.Ключ+ИмяФлагаАктивности] = ?(
				ЗначениеФлагаАктивности,
				ТекущаяСтрока[Реквизит.Ключ],
				0);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Пересчитывает сумму сверх заказа по товару в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьСуммуСверхЗаказаВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Перем СтруктураПараметровДействия;
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуСверхЗаказа", СтруктураПараметровДействия) Тогда
		Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(ТекущаяСтрока, "СуммаСверхЗаказа") Тогда
			Если СтруктураПараметровДействия.РеализацияПоступлениеПоЗаказу И ТекущаяСтрока.КодСтроки = 0 Тогда
				Если СтруктураПараметровДействия.Свойство("ТребуетсяЗалогЗаТару")
					И СтруктураПараметровДействия.ТребуетсяЗалогЗаТару Тогда
					ТекущаяСтрока.СуммаСверхЗаказа = ТекущаяСтрока.СуммаСНДС;
				Иначе
					ТекущаяСтрока.СуммаСверхЗаказа = ТекущаяСтрока.СуммаСНДСБезВозвратнойТары;
				КонецЕсли;
			Иначе
				ТекущаяСтрока.СуммаСверхЗаказа = 0;
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

// Заполняет поле варианта отражения корректировки реализации в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьВариантОтраженияКорректировкиРеализации(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Перем Операция;
	
	Если СтруктураДействий.Свойство("ЗаполнитьВариантОтраженияКорректировкиРеализации", Операция) Тогда
		
		Если (ТекущаяСтрока.ТипНоменклатуры <> ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар")
			И ТекущаяСтрока.ТипНоменклатуры <> ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.МногооборотнаяТара"))
			ИЛИ ТекущаяСтрока.КоличествоУпаковок = 0 Тогда
			
			Если Операция = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.РеализацияПерепоставленногоТовара") Тогда
				ТекущаяСтрока.ВариантОтражения = ПредопределенноеЗначение("Перечисление.ВариантыОтраженияКорректировокРеализаций.УвеличитьРеализацию");
			ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара") Тогда
				ТекущаяСтрока.ВариантОтражения = ПредопределенноеЗначение("Перечисление.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюСписатьНаРасходы");
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Формирует названия идентификаторов количества с суффиксом
// 
// Параметры:
// 	Суффикс - Строка - суффикс
// 	
// Возвращаемое значение:
// 	Структура - набор названий идентификаторов. Описание:
// * Количество - Строка - "Количество" + суффикс
// * КоличествоУпаковок - Строка - "КоличествоУпаковок" + суффикс
// * КоличествоПоРНПТ - Строка - "КоличествоПоРНПТ" + суффикс
//
Функция КоличествоССуффиксом(Суффикс) Экспорт
	
	СтруктураВозврата = Новый Структура();
	
	СтруктураВозврата.Вставить("Количество", "");
	СтруктураВозврата.Вставить("КоличествоУпаковок", "");
	СтруктураВозврата.Вставить("КоличествоПоРНПТ", "");
	
	Если Не ЗначениеЗаполнено(Суффикс) Тогда
		СтруктураВозврата.Количество = "Количество";
		СтруктураВозврата.КоличествоУпаковок = "КоличествоУпаковок";
		СтруктураВозврата.КоличествоПоРНПТ = "КоличествоПоРНПТ";
	ИначеЕсли Суффикс = "Подобрано" Тогда
		СтруктураВозврата.Количество = "КоличествоПодобрано";
		СтруктураВозврата.КоличествоУпаковок = "КоличествоУпаковокПодобрано";
		СтруктураВозврата.КоличествоПоРНПТ = "";
	ИначеЕсли Суффикс = "План" Тогда
		СтруктураВозврата.Количество = "КоличествоПлан";
		СтруктураВозврата.КоличествоУпаковок = "КоличествоУпаковокПлан";
		СтруктураВозврата.КоличествоПоРНПТ = "";
	ИначеЕсли Суффикс = "ПоДокументу" Тогда
		СтруктураВозврата.Количество = "КоличествоПоДокументу";
		СтруктураВозврата.КоличествоУпаковок = "КоличествоУпаковокПоДокументу";
		СтруктураВозврата.КоличествоПоРНПТ = "";
	ИначеЕсли Суффикс = "Факт" Тогда
		СтруктураВозврата.Количество = "КоличествоФакт";
		СтруктураВозврата.КоличествоУпаковок = "КоличествоУпаковокФакт";
		СтруктураВозврата.КоличествоПоРНПТ = "";
	ИначеЕсли Суффикс = "Отклонение" Тогда
		СтруктураВозврата.Количество = "КоличествоОтклонение";
		СтруктураВозврата.КоличествоУпаковок = "КоличествоУпаковокОтклонение";
		СтруктураВозврата.КоличествоПоРНПТ = "";
	ИначеЕсли Суффикс = "Отобрано" Тогда
		СтруктураВозврата.Количество = "КоличествоОтобрано";
		СтруктураВозврата.КоличествоУпаковок = "КоличествоУпаковокОтобрано";
		СтруктураВозврата.КоличествоПоРНПТ = "";
	ИначеЕсли Суффикс = "Размещено" Тогда
		СтруктураВозврата.Количество = "КоличествоРазмещено";
		СтруктураВозврата.КоличествоУпаковок = "КоличествоУпаковокРазмещено";
		СтруктураВозврата.КоличествоПоРНПТ = "";
	ИначеЕсли Суффикс = "Заказать" Тогда
		СтруктураВозврата.Количество = "КоличествоЗаказать";
		СтруктураВозврата.КоличествоУпаковок = "КоличествоУпаковокЗаказать";
		СтруктураВозврата.КоличествоПоРНПТ = "";
	ИначеЕсли Суффикс = "КВозврату" Тогда
		СтруктураВозврата.Количество = "КоличествоКВозврату";
		СтруктураВозврата.КоличествоУпаковок = "КоличествоУпаковокКВозврату";
		СтруктураВозврата.КоличествоПоРНПТ = "";
	ИначеЕсли Суффикс = "ВЗаказе" Тогда
		СтруктураВозврата.Количество = "КоличествоВЗаказе";
		СтруктураВозврата.КоличествоУпаковок = "КоличествоУпаковокВЗаказе";
		СтруктураВозврата.КоличествоПоРНПТ = "";
	ИначеЕсли Суффикс = "Собирается" Тогда
		СтруктураВозврата.Количество = "КоличествоСобирается";
		СтруктураВозврата.КоличествоУпаковок = "КоличествоУпаковокСобирается";
		СтруктураВозврата.КоличествоПоРНПТ = "";
	ИначеЕсли Суффикс = "ВОрдере" Тогда
		СтруктураВозврата.Количество = "КоличествоВОрдере";
		СтруктураВозврата.КоличествоУпаковок = "КоличествоУпаковокВОрдере";
		СтруктураВозврата.КоличествоПоРНПТ = "";
	ИначеЕсли Суффикс = "Норматив" Тогда
		СтруктураВозврата.Количество = "КоличествоНорматив";
		СтруктураВозврата.КоличествоУпаковок = "КоличествоУпаковокНорматив";
		СтруктураВозврата.КоличествоПоРНПТ = "";
	ИначеЕсли Суффикс = "Прогноз" Тогда
		СтруктураВозврата.Количество = "КоличествоПрогноз";
		СтруктураВозврата.КоличествоУпаковок = "КоличествоУпаковокПрогноз";
		СтруктураВозврата.КоличествоПоРНПТ = "";
	ИначеЕсли Суффикс = "После" Тогда
		СтруктураВозврата.Количество = "КоличествоПосле";
		СтруктураВозврата.КоличествоУпаковок = "КоличествоУпаковокПосле";
		СтруктураВозврата.КоличествоПоРНПТ = "КоличествоПоРНПТПосле";
	ИначеЕсли Суффикс = "Забаланс" Тогда
		СтруктураВозврата.Количество         = "";
		СтруктураВозврата.КоличествоУпаковок = "КоличествоУпаковок";
		СтруктураВозврата.КоличествоПоРНПТ   = "";
	Иначе
		ВызватьИсключение НСтр("ru = 'Неизвестный суффикс для количества:'") + " " + Суффикс;
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции


// Заполняет способ определения себестоимости строкой
//
// Параметры:
//  ТекущаяСтрока		 - Структура - данные обрабатываемой строки.
//  СтруктураДействий	 - Структура - описывает действия, где Ключ - наименование действия, Значение - Структура - параметры действия.
//  КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьСпособОпределенияСебестоимостиСтрокой(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Перем СтруктураПараметров;
	
	Если СтруктураДействий.Свойство("ЗаполнитьСпособОпределенияСебестоимостиСтрокой", СтруктураПараметров)
		И ТекущаяСтрока.Свойство("СпособОпределенияСебестоимостиСтрокой") Тогда
		
		Если ТекущаяСтрока.СпособОпределенияСебестоимости = ПредопределенноеЗначение("Перечисление.СпособыОпределенияСебестоимостиВозврата.ИзТекущегоДокумента") Тогда
			Представление = НСтр("ru = 'Равна сумме в данной строке'");
		ИначеЕсли ТекущаяСтрока.СпособОпределенияСебестоимости = ПредопределенноеЗначение("Перечисление.СпособыОпределенияСебестоимостиВозврата.ИзДокументаПродажи") Тогда
			Представление = НСтр("ru = 'Определяется из документа продажи'");
		ИначеЕсли ТекущаяСтрока.СпособОпределенияСебестоимости = ПредопределенноеЗначение("Перечисление.СпособыОпределенияСебестоимостиВозврата.ИзДокументаПередачи") Тогда
			Представление = НСтр("ru = 'Определяется из документа передачи'");
		ИначеЕсли ТекущаяСтрока.СпособОпределенияСебестоимости = ПредопределенноеЗначение("Перечисление.СпособыОпределенияСебестоимостиВозврата.Вручную") Тогда
			Если СтруктураПараметров.ВалютаУправленческогоУчета = СтруктураПараметров.ВалютаРегламентированногоУчета Тогда
				Если СтруктураПараметров.ИспользоватьРеглУчет Тогда
					Если СтруктураПараметров.НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС") Тогда
						Представление = НСтр("ru = 'Себестоимость с НДС: %1% (%2%), Себестоимость без НДС: %3% (%4%)'");
					Иначе
						Представление = НСтр("ru = 'Себестоимость: %1% (%2%)'");
					КонецЕсли;
				Иначе
					Если СтруктураПараметров.НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС") Тогда
						Представление = НСтр("ru = 'Себестоимость с НДС: %1% (%2%)'");
					Иначе
						Представление = НСтр("ru = 'Себестоимость: %1% (%2%)'");
					КонецЕсли;
				КонецЕсли;
			Иначе
				Если СтруктураПараметров.ИспользоватьРеглУчет Тогда
					Если СтруктураПараметров.НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС") Тогда
						Представление = НСтр("ru = 'Упр. с НДС: %1% (%2%), Регл. без НДС: %3% (%4%)'");
					Иначе
						Представление = НСтр("ru = 'Упр.: %1% (%2%), Регл.: %3% (%4%)'");
					КонецЕсли;
				Иначе
					Если СтруктураПараметров.НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС") Тогда
						Представление = НСтр("ru = 'Упр. с НДС: %1% (%2%)'");
					Иначе
						Представление = НСтр("ru = 'Упр.: %1% (%2%)'");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			Представление = СтрЗаменить(Представление, "%1%", Формат(ТекущаяСтрока.Себестоимость, "ЧДЦ=2; ЧН=0"));
			Представление = СтрЗаменить(Представление, "%2%", СтруктураПараметров.ВалютаУправленческогоУчета);
			Представление = СтрЗаменить(Представление, "%3%", Формат(ТекущаяСтрока.СебестоимостьРегл, "ЧДЦ=2; ЧН=0"));
			Представление = СтрЗаменить(Представление, "%4%", СтруктураПараметров.ВалютаРегламентированногоУчета);
		КонецЕсли;
		ТекущаяСтрока.СпособОпределенияСебестоимостиСтрокой = Представление;
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет способ определения себестоимости строкой
//
// Параметры:
//  ТекущаяСтрока		 - Структура - данные обрабатываемой строки.
//  СтруктураДействий	 - Структура - описывает действия, где Ключ - наименование действия, Значение - Структура - параметры действия.
//  КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьСпособОпределенияСебестоимости(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	Перем СпособОпределенияСебестоимости;
	
	Если СтруктураДействий.Свойство("ЗаполнитьСпособОпределенияСебестоимости", СпособОпределенияСебестоимости)
		И ТекущаяСтрока.Свойство("СпособОпределенияСебестоимости") Тогда
		
		Если ТекущаяСтрока.Свойство("ДокументРеализации") И ЗначениеЗаполнено(ТекущаяСтрока.ДокументРеализации) Тогда
			ТекущаяСтрока.СпособОпределенияСебестоимости = ПредопределенноеЗначение("Перечисление.СпособыОпределенияСебестоимостиВозврата.ИзДокументаПродажи");
		ИначеЕсли ТекущаяСтрока.Свойство("ДокументПередачи") И ЗначениеЗаполнено(ТекущаяСтрока.ДокументПередачи) Тогда
			ТекущаяСтрока.СпособОпределенияСебестоимости = ПредопределенноеЗначение("Перечисление.СпособыОпределенияСебестоимостиВозврата.ИзДокументаПередачи");
		Иначе
			ТекущаяСтрока.СпособОпределенияСебестоимости = СпособОпределенияСебестоимости;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает сумму налогового учета в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьСуммуНУВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуНУ") Тогда
		ТекущаяСтрока.СуммаНУ = ТекущаяСтрока.СуммаРегл - ТекущаяСтрока.СуммаПР
			- ?(СтруктураДействий.Свойство("ПересчитатьСуммуВР"), 0, ТекущаяСтрока.СуммаВР);
		
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает сумму временной разницы в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьСуммуВРВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуВР") Тогда
		ТекущаяСтрока.СуммаВР = ТекущаяСтрока.СуммаРегл - ТекущаяСтрока.СуммаНУ - ТекущаяСтрока.СуммаПР;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает структуру, содержащую поля для пересчета количества упаковок в табличной части документа.
//
// Параметры:
//  ИмяПоляКоличество         - Строка - Имя поля, по которому считается количество упаковок.
//  ИмяПоляКоличествоУпаковок - Строка - Имя поля, в которое подставляется рассчитанное количество упаковок.
//
// Возвращаемое значение:
//  Структура - содержит поля для пересчета количества упаковок в табличной части документа,
//              Строки - Массив - содержит элементы типа ДанныеФормыЭлементКоллекции, ссылки нас строки для пересчета сумм,
//              ИтогКоличество - Число - сумма значений в поле "Количество" в строках переданных в параметре "Строки".
//
Функция СтруктураПересчетаКоличестваУпаковок(ИмяПоляКоличество = "Количество", ИмяПоляКоличествоУпаковок = "КоличествоУпаковок") Экспорт

	Результат = Новый Структура;
	Результат.Вставить("Номенклатура");
	Результат.Вставить("Упаковка");
	Результат.Вставить(ИмяПоляКоличество, ИмяПоляКоличествоУпаковок);
	
	Возврат Результат;

КонецФункции

// Заполняет место хранения в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока        - Структура - Структура со свойствами строки документа.
//	СтруктураДействий    - Структура - Структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьТипМестаХраненияПоУмолчанию(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Перем ПараметрыЗаполнения;
	
	Если Не СтруктураДействий.Свойство("ЗаполнитьТипМестаХраненияПоУмолчанию", ПараметрыЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока.ТипМестаХранения = ПредопределенноеЗначение(ПараметрыЗаполнения.ТипМестаХранения);
	
КонецПроцедуры



// Выполняет пересчет количества упаковок в строках табличной части при подборе номера ГТД комплекта.
//
// Параметры:
//	ТекущаяСтрока - ДанныеФормыСтруктура, Структура - текущая строка, для которой выполнен подбор номера ГТД комплекта.
//	НоваяСтрока - ДанныеФормыСтруктура, Структура - новая строка, для которой подобрался остаточный Номер ГТД комплекта.
//	ИмяПоляКоличествоУпаковок - Строка - имя поля количества упаковок, для которого осуществляется пересчет.
//
Процедура ПересчитатьКоличествоУпаковокПриПодбореНомераГТД(ТекущаяСтрока,
															НоваяСтрока,
															ИмяПоляКоличествоУпаковок = "КоличествоУпаковок") Экспорт
	
	КоличествоУпаковокВСтроке	= ТекущаяСтрока[ИмяПоляКоличествоУпаковок];
	ЦелоеКоличествоУпаковок		= ТекущаяСтрока[ИмяПоляКоличествоУпаковок]
									- Цел(ТекущаяСтрока[ИмяПоляКоличествоУпаковок]) = 0;
	
	Если ЦелоеКоличествоУпаковок Тогда
		Если ТекущаяСтрока[ИмяПоляКоличествоУпаковок] > 1 Тогда
			ТекущаяСтрока[ИмяПоляКоличествоУпаковок]	= ТекущаяСтрока[ИмяПоляКоличествоУпаковок] - 1;
			НоваяСтрока[ИмяПоляКоличествоУпаковок]		= 1;
		Иначе
			ТекущаяСтрока[ИмяПоляКоличествоУпаковок]	= ТекущаяСтрока[ИмяПоляКоличествоУпаковок] / 2;
			НоваяСтрока[ИмяПоляКоличествоУпаковок]		= ТекущаяСтрока[ИмяПоляКоличествоУпаковок];
		КонецЕсли;
	Иначе
		Если ТекущаяСтрока[ИмяПоляКоличествоУпаковок]> 1 Тогда
			ТекущаяСтрока[ИмяПоляКоличествоУпаковок] = Цел(ТекущаяСтрока[ИмяПоляКоличествоУпаковок]);
		Иначе
			ТекущаяСтрока[ИмяПоляКоличествоУпаковок] = ТекущаяСтрока[ИмяПоляКоличествоУпаковок] / 2;
		КонецЕсли;
		
		НоваяСтрока[ИмяПоляКоличествоУпаковок] = КоличествоУпаковокВСтроке - ТекущаяСтрока[ИмяПоляКоличествоУпаковок];
	КонецЕсли;
	
КонецПроцедуры

// Выполняет заполнение поля 'ТипНомераГТД' значение по умолчанию.
//
// Параметры:
//	ТекущаяСтрока - ДанныеФормыСтруктура, Структура - строка, для которой требуется заполнить поле 'ТипНомераГТД'.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//
Процедура ЗаполнитьТипНомераГТДПоУмолчанию(ТекущаяСтрока, СтруктураДействий) Экспорт
	
	Перем ПараметрыДействия;
	
	Если СтруктураДействий.Свойство("ЗаполнитьТипНомераГТД") Тогда
		ТекущаяСтрока.ТипНомераГТД = ПредопределенноеЗначение("Перечисление.ТипыНомеровГТД.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПрочиеПроцедуры

Процедура ЗаполнитьРасхожденияВСтрокеАкта(ТекущаяСтрока)
	
	ТекущаяСтрока.КоличествоУпаковокРасхождения = ТекущаяСтрока.КоличествоУпаковок - ТекущаяСтрока.КоличествоУпаковокПоДокументу;
	ТекущаяСтрока.СуммаРасхождения              = ТекущаяСтрока.Сумма - ТекущаяСтрока.СуммаПоДокументу;
	ТекущаяСтрока.СуммаНДСРасхождения           = ТекущаяСтрока.СуммаНДС - ТекущаяСтрока.СуммаНДСПоДокументу;
	ТекущаяСтрока.СуммаСНДСРасхождения          = ТекущаяСтрока.СуммаСНДС - ТекущаяСтрока.СуммаСНДСПоДокументу;
	ТекущаяСтрока.ЕстьРасхождения               = (ТекущаяСтрока.КоличествоУпаковокРасхождения <> 0);
	
КонецПроцедуры

Функция НормализоватьПараметрыПересчетаЕдиниц(ТекущаяСтрока, УпаковкаНоменклатура) Экспорт
	
	Если УпаковкаНоменклатура = Неопределено Тогда
		
		Номенклатура   = ТекущаяСтрока.Номенклатура;
		Упаковка       = ТекущаяСтрока.Упаковка;
		НужноОкруглять = Истина;
		
	ИначеЕсли ТипЗнч(УпаковкаНоменклатура) = Тип("Структура") Тогда
		
		Если УпаковкаНоменклатура.Свойство("Упаковка") Тогда
			Упаковка = УпаковкаНоменклатура.Упаковка;
		ИначеЕсли УпаковкаНоменклатура.Свойство("ЕдиницаИзмеренияВЕТИС") Тогда
			Если ЗначениеЗаполнено(УпаковкаНоменклатура.ЕдиницаИзмеренияВЕТИС) Тогда
				Упаковка = УпаковкаНоменклатура.ЕдиницаИзмеренияВЕТИС;
			Иначе
				Упаковка = ТекущаяСтрока.ЕдиницаИзмеренияВЕТИС;
			КонецЕсли;
		Иначе
			Упаковка = ТекущаяСтрока.Упаковка;
		КонецЕсли;
		
		Если УпаковкаНоменклатура.Свойство("Номенклатура") Тогда
			Номенклатура = УпаковкаНоменклатура.Номенклатура;
		Иначе
			Номенклатура = ТекущаяСтрока.Номенклатура;
		КонецЕсли;
		
		Если УпаковкаНоменклатура.Свойство("НужноОкруглять") Тогда
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "НужноОкруглять") Тогда
				НужноОкруглять = ТекущаяСтрока.НужноОкруглять;
			Иначе
				НужноОкруглять = УпаковкаНоменклатура.НужноОкруглять;
			КонецЕсли;
		Иначе
			НужноОкруглять = Истина;
		КонецЕсли;
		
	Иначе
		Упаковка       = УпаковкаНоменклатура;
		Номенклатура   = ТекущаяСтрока.Номенклатура;
		НужноОкруглять = Истина;
	КонецЕсли;
	
	Возврат Новый Структура("Номенклатура,Упаковка,НужноОкруглять", Номенклатура, Упаковка, НужноОкруглять);
	
КонецФункции

Функция НормализоватьПараметрыПересчетаЕдиницСуффикс(ТекущаяСтрока, ПараметрыПересчета) Экспорт
	
	Суффикс = "";
	НужноОкруглять = Истина;
	
	Если ТипЗнч(ПараметрыПересчета) = Тип("Структура") Тогда
		
		Если ПараметрыПересчета.Свойство("Суффикс") Тогда
			Суффикс = ПараметрыПересчета.Суффикс;
		КонецЕсли;
		
		Если ПараметрыПересчета.Свойство("НужноОкруглять") Тогда
			НужноОкруглять = ПараметрыПересчета.НужноОкруглять;
		КонецЕсли;
	Иначе
		Суффикс = ПараметрыПересчета;
	КонецЕсли;

	Если Суффикс = Неопределено Тогда
		Суффикс = "";
	КонецЕсли;
	
	Возврат Новый Структура("Суффикс,НужноОкруглять", Суффикс, НужноОкруглять); 
	
КонецФункции

Функция ПолучитьКоэффициентУпаковки(ТекУпаковка, КэшированныеЗначения, ТекНоменклатура = Неопределено) Экспорт
	
	Результат = Новый Структура("Коэффициент,НужноОкруглятьКоличество");
	
	Если ЗначениеЗаполнено(ТекУпаковка) Тогда
		КлючКоэффициента = КлючКэшаУпаковки(ТекНоменклатура, ТекУпаковка); 
		Кэш = КэшированныеЗначения.КоэффициентыУпаковок[КлючКоэффициента];
		Если Кэш = Неопределено Тогда
			ЗначенияРеквизитов = ПакетнаяОбработкаТабличнойЧастиВызовСервера.ДанныеОбУпаковке(
				ТекНоменклатура, ТекУпаковка, КэшированныеЗначения);
			Результат.Коэффициент    		   =  ЗначенияРеквизитов.Коэффициент;
			Результат.НужноОкруглятьКоличество =  ЗначенияРеквизитов.НужноОкруглятьКоличество;
		Иначе
			Результат = Кэш;
		КонецЕсли;
	Иначе
		Результат.Коэффициент = 1;
		Результат.НужноОкруглятьКоличество = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьКоэффициентРНПТ(ПараметрыПересчета, ТекущаяСтрока, КэшированныеЗначения) Экспорт
	
	Возврат УчетПрослеживаемыхТоваровКлиентСерверЛокализация.ПолучитьКоэффициентРНПТ(ПараметрыПересчета,
																					ТекущаяСтрока,
																					КэшированныеЗначения);
	
КонецФункции

Процедура ПересчитатьКоличествоУпаковокПоВесу(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Суффикс = Неопределено;
	
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоУпаковокПоВесу", Суффикс) Тогда
		КоличествоССуффиксом = КоличествоССуффиксом(Суффикс);
		ТекущаяСтрока[КоличествоССуффиксом.КоличествоУпаковок] = ТекущаяСтрока.Вес
			* Окр(ПолучитьКоэффициентУпаковки(ТекущаяСтрока.ЕдиницаИзмеренияВеса, КэшированныеЗначения).Коэффициент
			/ ТекущаяСтрока.ВесУпаковки, 7, РежимОкругления.Окр15как20);
		
		ТекущаяСтрока[КоличествоССуффиксом.Количество] = ТекущаяСтрока[КоличествоССуффиксом.КоличествоУпаковок]
			* ПолучитьКоэффициентУпаковки(ТекущаяСтрока.Упаковка, КэшированныеЗначения, ТекущаяСтрока.Номенклатура).Коэффициент;
		
		ОбъемЕдиницыИзмерения = ПолучитьКоэффициентУпаковки(ТекущаяСтрока.ЕдиницаИзмеренияОбъема, КэшированныеЗначения).Коэффициент;
		Если ТекущаяСтрока.ОбъемУпаковки * ОбъемЕдиницыИзмерения <> 0 Тогда
			ТекущаяСтрока.Объем = ТекущаяСтрока[КоличествоССуффиксом.КоличествоУпаковок] 
				* 1/Окр(ОбъемЕдиницыИзмерения/ТекущаяСтрока.ОбъемУпаковки, 7, РежимОкругления.Окр15как20);
		Иначе
			ТекущаяСтрока.Объем = 0;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПересчитатьКоличествоУпаковокПоОбъему(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт

	Суффикс = Неопределено;
	
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоУпаковокПоОбъему", Суффикс) Тогда
		КоличествоССуффиксом = КоличествоССуффиксом(Суффикс);
		
		ТекущаяСтрока[КоличествоССуффиксом.КоличествоУпаковок] = ТекущаяСтрока.Объем 
			* Окр(ПолучитьКоэффициентУпаковки(ТекущаяСтрока.ЕдиницаИзмеренияОбъема, КэшированныеЗначения).Коэффициент
			/ ТекущаяСтрока.ОбъемУпаковки, 7, РежимОкругления.Окр15как20);
		
		ТекущаяСтрока[КоличествоССуффиксом.Количество] = ТекущаяСтрока[КоличествоССуффиксом.КоличествоУпаковок]
			* ПолучитьКоэффициентУпаковки(ТекущаяСтрока.Упаковка, КэшированныеЗначения, ТекущаяСтрока.Номенклатура).Коэффициент;
		
		ВесЕдиницыИзмерения = ПолучитьКоэффициентУпаковки(ТекущаяСтрока.ЕдиницаИзмеренияВеса, КэшированныеЗначения).Коэффициент;
		Если ТекущаяСтрока.ВесУпаковки * ВесЕдиницыИзмерения <> 0 Тогда
			ТекущаяСтрока.Вес = ТекущаяСтрока[КоличествоССуффиксом.КоличествоУпаковок]
			* 1/Окр(ВесЕдиницыИзмерения/ТекущаяСтрока.ВесУпаковки, 7, РежимОкругления.Окр15как20);
		Иначе
			ТекущаяСтрока.Вес = 0;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПересчитатьВесОбъемВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт 

	Суффикс = Неопределено;

	Если СтруктураДействий.Свойство("ПересчитатьВесОбъем", Суффикс) Тогда
		
		КоличествоССуффиксом = КоличествоССуффиксом(Суффикс);
		
		ВесЕдиницыИзмерения = ПолучитьКоэффициентУпаковки(ТекущаяСтрока.ЕдиницаИзмеренияВеса, КэшированныеЗначения).Коэффициент;
		Если ВесЕдиницыИзмерения * ТекущаяСтрока.ВесУпаковки <> 0 Тогда
			ТекущаяСтрока.Вес =	ТекущаяСтрока[КоличествоССуффиксом.КоличествоУпаковок]
				* 1/Окр(ВесЕдиницыИзмерения/ТекущаяСтрока.ВесУпаковки, 7, РежимОкругления.Окр15как20);
		КонецЕсли;
		
		ОбъемЕдиницыИзмерения = ПолучитьКоэффициентУпаковки(ТекущаяСтрока.ЕдиницаИзмеренияОбъема, КэшированныеЗначения).Коэффициент;
		Если ОбъемЕдиницыИзмерения * ТекущаяСтрока.ОбъемУпаковки<>0 Тогда
			ТекущаяСтрока.Объем =ТекущаяСтрока[КоличествоССуффиксом.КоличествоУпаковок]
				* 1/Окр(ОбъемЕдиницыИзмерения/ТекущаяСтрока.ОбъемУпаковки, 7, РежимОкругления.Окр15как20);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Функция КлючКэшаУпаковки(Номенклатура, Упаковка) Экспорт
	
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		КлючНоменклатура = Строка(Номенклатура.УникальныйИдентификатор());
	Иначе
		КлючНоменклатура = "ПустоеЗначение";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Упаковка) Тогда
		КлючУпаковка = Строка(Упаковка.УникальныйИдентификатор());
	Иначе
		КлючУпаковка = "ПустоеЗначение";
	КонецЕсли;
	
	Возврат КлючНоменклатура + КлючУпаковка;
	
КонецФункции

Функция КлючКэшаКоэффициентРНПТ(ТекущаяСтрока, ПараметрыПересчета) Экспорт
	
	Возврат УчетПрослеживаемыхТоваровКлиентСерверЛокализация.КлючКэшаКоэффициентРНПТ(ТекущаяСтрока, ПараметрыПересчета);
	
КонецФункции

Процедура ПриИзмененииНазначенияВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Если СтруктураДействий.Свойство("ПриИзмененииНазначения") Тогда
		
		ТекущаяСтрока.НазначениеОтправителя = ТекущаяСтрока.Назначение;
		
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает сумму автоматической скидки на товар в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - см. ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ПересчитатьСуммуАвтоматическойСкидкиВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт

	ИмяКоличества = Неопределено;
		
	Если СтруктураДействий.Свойство("ПересчитатьСуммуАвтоматическойСкидки", ИмяКоличества) Тогда
		
		Если НЕ ЗначениеЗаполнено(ИмяКоличества) Тогда
			ИмяКоличества = "КоличествоУпаковок"; 
		КонецЕсли;
			
		ТекущаяСтрока.СуммаАвтоматическойСкидки = Окр(ТекущаяСтрока[ИмяКоличества] *ТекущаяСтрока.Цена
		                                              * ТекущаяСтрока.ПроцентАвтоматическойСкидки / 100, 2);
		
	КонецЕсли;

КонецПроцедуры


#КонецОбласти

#КонецОбласти
