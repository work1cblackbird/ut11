#Область ПрограммныйИнтерфейс

#Область МетодыИВыраженияСистемыКомпоновкиДанных

// Возвращает уникальный идентификатор по ссылке на объект
// Предназначена для использования в выражениях СКД.
//
// Параметры:
//	СсылкаНаОбъект - ЛюбаяСсылка - любая ссылка
//
// Возвращаемое значение:
//	УникальныйИдентификатор
//
Функция УникальныйИдентификатор(СсылкаНаОбъект) Экспорт
	УникальныйИдентификатор = Неопределено;
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(СсылкаНаОбъект)) Тогда
		УникальныйИдентификатор = СсылкаНаОбъект.УникальныйИдентификатор();
	КонецЕсли;
	
	Возврат УникальныйИдентификатор;
КонецФункции

// Возвращает строковое представление документа для использования в выражениях СКД
//
// Параметры:
//	Документ - ДокументСсылка - ссылка на документ
//	Номер -Строка - номер документа
//	Дата - Дата - дата документа
//
// Возвращаемое значение:
//	Строка - представление документа.
//
Функция ПредставлениеДокумента(Документ, Номер = Неопределено, Дата = Неопределено) Экспорт
	ПредставлениеДокумента = "";
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Документ)) Тогда
		ПредставлениеДокумента = ОбщегоНазначенияУТ.ПолучитьПредставлениеДокумента(Документ, Номер, Дата);
	КонецЕсли;
	
	Возврат ПредставлениеДокумента;
КонецФункции

Функция ГраницаПериода(Дата, ВидГраницыИмяЗначения) Экспорт
	Возврат Новый Граница(Дата, ВидГраницы[ВидГраницыИмяЗначения]);
КонецФункции

// Возвращает непечатный символ по имени
//
// Параметры:
//	ИмяСимвола - Строка - имя непечатного символа как оно задано в системном перечислении Символы.
//
// Возвращаемое значение:
//	Строка - значение символа
//
Функция ЗначениеСимвола(ИмяСимвола) Экспорт
	ВозвращаемыйСимвол = "";
	
	Если ИмяСимвола = "ВК" Тогда
		ВозвращаемыйСимвол = Символы.ВК;
	ИначеЕсли ИмяСимвола = "Втаб" Тогда
		ВозвращаемыйСимвол = Символы.ВТаб;
	ИначеЕсли ИмяСимвола = "НПП" Тогда
		ВозвращаемыйСимвол = Символы.НПП;
	ИначеЕсли ИмяСимвола = "ПС" Тогда
		ВозвращаемыйСимвол = Символы.ПС;
	ИначеЕсли ИмяСимвола = "ПФ" Тогда
		ВозвращаемыйСимвол = Символы.ПФ;
	ИначеЕсли ИмяСимвола = "Таб" Тогда
		ВозвращаемыйСимвол = Символы.Таб;
	КонецЕсли;
	
	Возврат ВозвращаемыйСимвол
КонецФункции

// Полный аналог СтрЗаменить для использования в выражениях СКД
//
// Параметры:
//  Строка          - Строка - Исходная строка. 
//  ПодстрокаПоиска - Строка - Искомая подстрока. 
//  ПодстрокаЗамены - Строка - Подстрока, на которую будет заменена подстрока поиска. 
//
// Возвращаемое значение:
//  Строка - полученная в результате замены.
//
Функция СтрЗаменитьКомпоновка(Строка, ПодстрокаПоиска, ПодстрокаЗамены) Экспорт
	Возврат СтрЗаменить(Строка, ПодстрокаПоиска, ПодстрокаЗамены);
КонецФункции

#КонецОбласти 
	
#КонецОбласти 