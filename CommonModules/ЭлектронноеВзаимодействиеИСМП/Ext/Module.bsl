#Область ПрограммныйИнтерфейс

#Область ЗаполнениеДанныхЭДОПоПрикладнымДокументам

#Область ПроверкаЗаполнения

//Проверяет совпадение табличной части "Штрихкоды упаковок" и табличной части ("Товары")
//   в отношении маркируемой продукции перед формированием документа ЭДО.
//   Проверка не выполняется если по документу уже оформлена отгрузка товаров ИСМП (прямой обмен).
//   Допущение: в документе по одной номенклатуре нет отновременно строк с указанием серий и строк без серий.
//
// Параметры:
//   ПроверяемыйДокумент - ДокументСсылка - проверяемый документ
//   АвтоматическиФормироватьОСУ - Булево - коды ОСУ по документу могут формироваться автоматически
// Возвращаемое значение:
//   Булево - номенклатура и количество по маркируемой продукции совпадают
Функция ДанныеДокументаСоответствуютДаннымУпаковок(ПроверяемыйДокумент, АвтоматическиФормироватьОСУ = Ложь) Экспорт
	
	// Проверка прямого обмена: при его использовании проверка штрихкодов не производится
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ПроверяемыйДокумент);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтгрузкаТоваровИСМП.Ссылка
	|ИЗ
	|	Документ.ОтгрузкаТоваровИСМП КАК ОтгрузкаТоваровИСМП
	|ГДЕ
	|	НЕ ОтгрузкаТоваровИСМП.ПометкаУдаления
	|	И ОтгрузкаТоваровИСМП.ДокументОснование = &ДокументОснование";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Возврат Истина;
	КонецЕсли;
	
	ИмяДокумента = ПроверяемыйДокумент.Метаданные().Имя;
	
	ИмяТаблицы = СтрШаблон("Документ.%1.ШтрихкодыУпаковок", ИмяДокумента);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаШтрихКодыУпаковок.ШтрихкодУпаковки КАК ШтрихкодУпаковки
	|ИЗ
	|	&ИмяТаблицы КАК ТаблицаШтрихКодыУпаковок
	|ГДЕ
	|	ТаблицаШтрихКодыУпаковок.Ссылка = &ПроверяемыйДокумент";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяТаблицы", ИмяТаблицы);
	Запрос.УстановитьПараметр("ПроверяемыйДокумент", ПроверяемыйДокумент);
	
	ШтрихкодыУпаковок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ШтрихкодУпаковки");
	
	ВидыПродукции = ОбщегоНазначенияИСМПВызовСервера.УчитываемыеВидыМаркируемойПродукции(НачалоДня(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПроверяемыйДокумент,"Дата")), Ложь);
	
	ДанныеДерева    = Содержимое(ШтрихкодыУпаковок, ВидыПродукции);
	ДанныеДокумента = ТаблицаМаркируемойПродукцииДокумента(ПроверяемыйДокумент, ВидыПродукции);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДерева.Номенклатура,
	|	ДанныеДерева.Характеристика,
	|	ДанныеДерева.Серия,
	|	ДанныеДерева.Количество,
	|	ДанныеДерева.КоличествоНеУказано
	|ПОМЕСТИТЬ ДанныеДерева
	|ИЗ
	|	&ДанныеДерева КАК ДанныеДерева
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Номенклатура,
	|	ДанныеДокумента.Характеристика,
	|	ДанныеДокумента.Серия,
	|	ДанныеДокумента.Количество
	|ПОМЕСТИТЬ ДанныеДокумента
	|ИЗ
	|	&ДанныеДокумента КАК ДанныеДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Номенклатура      КАК Номенклатура,
	|	ДанныеДокумента.Характеристика    КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Серия В (&НезаполненнаяСерия)
	|			ТОГДА &ПустаяСерия
	|		ИНАЧЕ ДанныеДокумента.Серия
	|	КОНЕЦ                             КАК Серия,
	|	&ТребуетВзвешивания КАК ТребуетВзвешивания,
	|	СУММА(ДанныеДокумента.Количество) КАК Количество
	|ПОМЕСТИТЬ ОчищенныеДанныеДокумента
	|ИЗ
	|	ДанныеДокумента КАК ДанныеДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеИС
	|		ПО &ТоварыОписаниеНоменклатурыИС
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Номенклатура,
	|	ДанныеДокумента.Характеристика,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Серия В (&НезаполненнаяСерия)
	|			ТОГДА &ПустаяСерия
	|		ИНАЧЕ ДанныеДокумента.Серия
	|	КОНЕЦ,
	|	&ТребуетВзвешивания
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДерева.Номенклатура      КАК Номенклатура,
	|	ДанныеДерева.Характеристика    КАК Характеристика,
	|	ВЫБОР
	|		КОГДА Не ДанныеДокумента.Номенклатура ЕСТЬ NULL
	|			ТОГДА &ПустаяСерия
	|		КОГДА ДанныеДерева.Серия В (&НезаполненнаяСерия)
	|			ТОГДА &ПустаяСерия
	|		ИНАЧЕ ДанныеДерева.Серия
	|	КОНЕЦ                          КАК Серия,
	|	СУММА(ДанныеДерева.Количество) КАК Количество,
	|	СУММА(ДанныеДерева.КоличествоНеУказано) КАК КоличествоНеУказано
	|ПОМЕСТИТЬ ОчищенныеДанныеДерева
	|ИЗ
	|	ДанныеДерева КАК ДанныеДерева
	|	ЛЕВОЕ СОЕДИНЕНИЕ ОчищенныеДанныеДокумента КАК ДанныеДокумента
	|		ПО ДанныеДерева.Номенклатура = ДанныеДокумента.Номенклатура
	|		И ДанныеДерева.Характеристика = ДанныеДокумента.Характеристика
	|		И ДанныеДокумента.Серия = &ПустаяСерия
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДерева.Номенклатура,
	|	ДанныеДерева.Характеристика,
	|	ВЫБОР
	|		КОГДА Не ДанныеДокумента.Номенклатура ЕСТЬ NULL
	|			ТОГДА &ПустаяСерия
	|		КОГДА ДанныеДерева.Серия В (&НезаполненнаяСерия)
	|			ТОГДА &ПустаяСерия
	|		ИНАЧЕ ДанныеДерева.Серия
	|	КОНЕЦ
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ДанныеДокумента.Номенклатура, ДанныеДерева.Номенклатура)     КАК Номенклатура,
	|	ЕСТЬNULL(ДанныеДокумента.Характеристика, ДанныеДерева.Характеристика) КАК Характеристика,
	|	ЕСТЬNULL(ДанныеДокумента.Серия, ДанныеДерева.Серия)                   КАК Серия,
	|	ЕСТЬNULL(ДанныеДерева.Количество , 0) - ЕСТЬNULL(ДанныеДокумента.Количество,0) КАК Различие,
	|	ЕСТЬNULL(ДанныеДерева.Количество , 0) КАК ДанныеДерева
	|ПОМЕСТИТЬ ДанныеШтрихкодовУпаковок
	|ИЗ
	|	ОчищенныеДанныеДокумента КАК ДанныеДокумента
	|		ПОЛНОЕ СОЕДИНЕНИЕ ОчищенныеДанныеДерева КАК ДанныеДерева
	|		ПО ДанныеДерева.Номенклатура = ДанныеДокумента.Номенклатура
	|		И ДанныеДерева.Характеристика = ДанныеДокумента.Характеристика
	|		И ДанныеДерева.Серия = ДанныеДокумента.Серия
	|ГДЕ
	|	ЕСТЬNULL(ДанныеДерева.Количество, 0) - ЕСТЬNULL(ДанныеДокумента.Количество, 0) <> 0
	|	И (ЕСТЬNULL(ДанныеДокумента.ТребуетВзвешивания, ЛОЖЬ) = ЛОЖЬ
	|		ИЛИ ЕСТЬNULL(ДанныеДерева.КоличествоНеУказано, 0) = 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////";
	Если АвтоматическиФормироватьОСУ Тогда
		ТекстЗапроса = ТекстЗапроса + ШтрихкодированиеОбщегоНазначенияИС.ТекстЗапросаСвойстваМаркируемойПродукции()	+ "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДанныеШтрихкодовУпаковок.Номенклатура
	|ИЗ
	|	ДанныеШтрихкодовУпаковок КАК ДанныеШтрихкодовУпаковок
	|	ЛЕВОЕ СОЕДИНЕНИЕ СвойстваМаркируемойПродукции КАК СвойстваМаркируемойПродукции
	|		ПО СвойстваМаркируемойПродукции.Номенклатура = ДанныеШтрихкодовУпаковок.Номенклатура
	|ГДЕ
	|	ДанныеШтрихкодовУпаковок.ДанныеДерева <> 0
	|	ИЛИ НЕ СвойстваМаркируемойПродукции.ВидПродукции В (&ВидыПродукцииПоддерживаютОСУ)
	|";
	Иначе
		ТекстЗапроса = ТекстЗапроса + "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДанныеШтрихкодовУпаковок.Номенклатура
	|ИЗ
	|	ДанныеШтрихкодовУпаковок КАК ДанныеШтрихкодовУпаковок
	|";
	КонецЕсли;
	ОбщегоНазначенияИС.ОбновитьТекстЗапросаСРегистромОписаниеНоменклатурыИС(ТекстЗапроса, "ОписаниеИС", "ДанныеДокумента.Номенклатура");
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДанныеДерева",    ДанныеДерева);
	Запрос.УстановитьПараметр("ДанныеДокумента", ДанныеДокумента);
	Запрос.УстановитьПараметр("ПустаяСерия",        ОбщегоНазначенияИС.ПустоеЗначениеОпределяемогоТипа("СерияНоменклатуры"));
	Запрос.УстановитьПараметр("НезаполненнаяСерия", ИнтеграцияИС.НезаполненныеЗначенияОпределяемогоТипа("СерияНоменклатуры"));
	Запрос.УстановитьПараметр("ВидыПродукцииПоддерживаютОСУ", ОбщегоНазначенияИСКлиентСервер.ВидыПродукцииОбъемноСортовогоУчета());
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Пустой();
	
КонецФункции

//Проверяет что по текущему прикладному документу есть электронный документ (входящий или исходящий).
//   Вариант: по текущему прикладному документу есть электронный документ с данными о маркируемых товарах.
//
// Параметры:
//   Ссылка - ДокументСсылка - проверяемый документ
//   СодержащимДанныеОМаркируемыхТоварах - Булево - отбор документа ЭДО с данными о маркируемых товарах
//
// Возвращаемое значение:
//   Булево - с текущим документом связан электронный документ.
//
Функция ДокументСвязанСЭлектронным(Ссылка, СодержащимДанныеОМаркируемыхТоварах = Ложь) Экспорт
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	МодульОбменСКонтрагентами = ОбщегоНазначения.ОбщийМодуль("ОбменСКонтрагентами");
	УстановитьПривилегированныйРежим(Истина);
	Статус = МодульОбменСКонтрагентами.СтатусДокументооборота(Ссылка);
	
	Если Статус.ЭлектронныйДокумент = Неопределено Тогда
		Возврат Ложь;
	ИначеЕсли Не СодержащимДанныеОМаркируемыхТоварах Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ВерсияАПИ() = 1 Тогда
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Статус.ЭлектронныйДокумент, "СодержитДанныеОМаркируемыхТоварах");
	Иначе 
		Возврат МодульОбменСКонтрагентами.СведенияОбЭлектронномДокументе(Статус.ЭлектронныйДокумент).СодержитМаркируемыеТовары;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СоставШтрихкодовУпаковок

// Код маркировки для передачи УПД.
// 
// Параметры:
//  ЗначениеШтрихкода - Строка - Значение кода маркировки
//  ВидПродукции      - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции
//  РезультатРазбора  - см. ШтрихкодированиеОбщегоНазначенияИС.НоваяСтруктураОбработкиШтрихкода - Результат разбора,
//                    - Неопределено - разбор не производился.
// 
// Возвращаемое значение:
//  Строка - Код маркировки для передачи УПД.
Функция КодМаркировкиДляПередачиУПД(ЗначениеШтрихкода, ВидПродукции, РезультатРазбора = Неопределено) Экспорт
	
	Если РезультатРазбора = Неопределено Тогда
		РезультатРазбора = ШтрихкодированиеОбщегоНазначенияИС.НоваяСтруктураОбработкиШтрихкода(
			ЗначениеШтрихкода, ВидПродукции, ЭлектронноеВзаимодействиеИСМППовтИсп.ПараметрыРазбора());
	КонецЕсли;
	
	ПараметрыНормализации = ЭлектронноеВзаимодействиеИСМППовтИсп.ПараметрыНормализации(
		ВидПродукции, РезультатРазбора.ВидУпаковки);
	
	Если ОбщегоНазначенияИСПовтИсп.ЭтоПродукцияМОТП(ВидПродукции) Тогда
		
		Если РезультатРазбора.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
			НормализованныйКодМаркировки = РазборКодаМаркировкиИССлужебныйКлиентСервер.НормализоватьКодМаркировки(
				РезультатРазбора, ВидПродукции, ПараметрыНормализации);
		Иначе
			НормализованныйКодМаркировки = РазборКодаМаркировкиИССлужебныйКлиентСервер.НормализоватьКодМаркировки(
				РезультатРазбора, ВидПродукции, ПараметрыНормализации);
		КонецЕсли;
		
	ИначеЕсли ОбщегоНазначенияИСПовтИсп.ЭтоПродукцияИСМП(ВидПродукции) Тогда
		
		НормализованныйКодМаркировки = ШтрихкодированиеИСМП.КодМаркировкиДляПередачиИСМП(
			РезультатРазбора, ПараметрыНормализации);
		
	КонецЕсли;
	
	Возврат НормализованныйКодМаркировки;
	
КонецФункции

// Разворачивает таблицу транспортных и индивидуальных штрихкодов до номенклатуры, характеристики, серии
//   для дальнейшего сопоставления информации об упаковках с товарной частью.
// Для транспортных мультитоварных упаковок номенклатура (характеристика, серия)- это различная номенклатура 
//   (характеристика, серия) каждого вложенного (на любом уровне) индивидуального штрихкода, для монотоварных упаковок и
//   кодов маркировки - содержимое.
// 
// Параметры:
// 	ТаблицаУпаковок - ТаблицаЗначений - исходная таблица упаковок:
// 	 * Ссылка   - Произвольный - объект-владелец штрихкода
// 	 * Штрихкод - СправочникСсылка.ШтрихкодыУпаковокТоваров - Штрихкод
// Возвращаемое значение:
// ТаблицаЗначений - таблица упаковок с товарными данными:
// 	 * Ссылка             - Произвольный - объект-владелец штрихкода - исходная колонка
// 	 * Штрихкод           - СправочникСсылка.ШтрихкодыУпаковокТоваров - ссылка на индивидуальный штрихкод содержимого или мультитоварную упаковку
// 	 * Номенклатура       - ОпределяемыйТип.Номенклатура - содержимое
// 	 * Характеристика     - ОпределяемыйТип.ХарактеристикаНоменклатуры - содержимое
// 	 * Серия              - ОпределяемыйТип.СерияНоменклатуры - содержимое
// 	 * ТребуетВзвешивания - Булево - признак продукции требующей взвешивания потребительских кодов
// 	 * ЗначениеШтрихкода  - Строка - текстовое представление исходного штрихкода для передачи в ЭДО
// 	 * Мультитоварная     - Булево - признак мультитоварной упаковки
// 	 * ВидПродукции       - Перечислениессылка.ВидыПродукцииИС - вид продукции номенклатуры для кодировки значения штрихкода
// 	 * ВидУпаковки        - ПеречислениеСсылка.ВидыУпаковокИС - вид упаковки номенклатуры для кодировки значения штрихкода
// 	 * Обработан          - Булево - признак сопоставления строки товарной части документа
// 	 * Количество         - Число  - количество маркируемой продукции по коду маркировки
// 	 * КоличествоПотребительскихУпаковок - Число  - количество потребительских упаковок маркируемой продукции по коду маркировки
//   * ТекстОшибкиИС      - Строка - Текст ошибки по строке при наличии
//   * КоличествоВПотребительскойУпаковке - Число - количество в 1 потребительской упаковке (в т.ч. средний вес) для автоматического кода ОСУ
Функция ЧастичноеСодержимое(ТаблицаУпаковок) Экспорт
	
	СписокЗапросов = Новый СписокЗначений;
	
	СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	ШтрихкодыУпаковок.Ссылка   КАК Ссылка,
		|	ШтрихкодыУпаковок.Штрихкод КАК Штрихкод
		|ПОМЕСТИТЬ ШтрихкодыВходящие
		|ИЗ
		|	&ШтрихкодУпаковки КАК ШтрихкодыУпаковок
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Штрихкод
		|");
	
	СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	ШтрихкодыВходящие.Ссылка            КАК Ссылка,
		|	ШтрихкодыУпаковок.ЗначениеШтрихкода КАК ЗначениеШтрихкода,
		|	ШтрихкодыВходящие.Штрихкод          КАК Штрихкод,
		|	ШтрихкодыУпаковок.Номенклатура      КАК Номенклатура,
		|	ШтрихкодыУпаковок.Характеристика    КАК Характеристика,
		|	ШтрихкодыУпаковок.Серия             КАК Серия,
		|	ШтрихкодыУпаковок.ТипУпаковки       КАК ТипУпаковки,
		|	ШтрихкодыУпаковок.ТипУпаковки = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковок.МультитоварнаяУпаковка) КАК Мультитоварная,
		|	ЛОЖЬ                                КАК Обработан,
		|	ВЫБОР
		|		КОГДА ШтрихкодыУпаковок.Количество <> 0
		|		ТОГДА ШтрихкодыУпаковок.Количество
		|	ИНАЧЕ 1 КОНЕЦ                       КАК Количество,
		|	ВЫБОР
		|		КОГДА ШтрихкодыУпаковок.КоличествоПотребительскихУпаковок <> 0
		|		ТОГДА ШтрихкодыУпаковок.КоличествоПотребительскихУпаковок
		|		КОГДА ШтрихкодыУпаковок.Количество <> 0
		|		ТОГДА ШтрихкодыУпаковок.Количество
		|	ИНАЧЕ 1 КОНЕЦ                       КАК КоличествоПотребительскихУпаковок
		|ПОМЕСТИТЬ ДанныеШтрихкодовУпаковок
		|ИЗ
		|	Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковок
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ШтрихкодыВходящие КАК ШтрихкодыВходящие
		|		ПО ШтрихкодыВходящие.Штрихкод = ШтрихкодыУпаковок.Ссылка
		|");
	
	СписокЗапросов.Добавить(
		ШтрихкодированиеОбщегоНазначенияИС.ТекстЗапросаСвойстваМаркируемойПродукции());
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДанныеШтрихкодовУпаковок.Ссылка                    КАК Ссылка,
		|	ДанныеШтрихкодовУпаковок.ЗначениеШтрихкода         КАК ЗначениеШтрихкода,
		|	ДанныеШтрихкодовУпаковок.Штрихкод                  КАК Штрихкод,
		|	ДанныеШтрихкодовУпаковок.Номенклатура              КАК Номенклатура,
		|	ДанныеШтрихкодовУпаковок.Характеристика            КАК Характеристика,
		|	ДанныеШтрихкодовУпаковок.Серия                     КАК Серия,
		|	ДанныеШтрихкодовУпаковок.Мультитоварная            КАК Мультитоварная,
		|	ДанныеШтрихкодовУпаковок.Обработан                 КАК Обработан,
		|	ДанныеШтрихкодовУпаковок.ТипУпаковки               КАК ТипУпаковки,
		|	СвойстваМаркируемойПродукции.ВидПродукции          КАК ВидПродукции,
		|	&ТребуетВзвешивания КАК ТребуетВзвешивания,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыУпаковокИС.ПустаяСсылка) КАК ВидУпаковки,
		|	ДанныеШтрихкодовУпаковок.Количество                КАК Количество,
		|	ДанныеШтрихкодовУпаковок.КоличествоПотребительскихУпаковок КАК КоличествоПотребительскихУпаковок
		|ИЗ
		|	ДанныеШтрихкодовУпаковок КАК ДанныеШтрихкодовУпаковок
		|		ЛЕВОЕ СОЕДИНЕНИЕ СвойстваМаркируемойПродукции КАК СвойстваМаркируемойПродукции
		|		ПО ДанныеШтрихкодовУпаковок.Номенклатура = СвойстваМаркируемойПродукции.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеНоменклатурыИС
		|		ПО &ТоварыОписаниеНоменклатурыИС
		|";
	ОбщегоНазначенияИС.ОбновитьТекстЗапросаСРегистромОписаниеНоменклатурыИС(ТекстЗапроса, "ОписаниеНоменклатурыИС", "ДанныеШтрихкодовУпаковок.Номенклатура");
	СписокЗапросов.Добавить(ТекстЗапроса, "Результат");
	
	СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	ШтрихкодыВходящие.Ссылка   КАК Ссылка,
		|	ШтрихкодыВходящие.Штрихкод КАК Родитель,
		|	ШтрихкодыУпаковок.Штрихкод КАК Штрихкод
		|ИЗ
		|	Справочник.ШтрихкодыУпаковокТоваров.ВложенныеШтрихкоды КАК ШтрихкодыУпаковок
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ШтрихкодыВходящие КАК ШтрихкодыВходящие
		|		ПО ШтрихкодыВходящие.Штрихкод = ШтрихкодыУпаковок.Ссылка
		|		И ШтрихкодыУпаковок.НомерСтроки = 1
		|		И ШтрихкодыУпаковок.Ссылка.ТипУпаковки <> ЗНАЧЕНИЕ(Перечисление.ТипыУпаковок.МультитоварнаяУпаковка)
		|		И (ШтрихкодыУпаковок.Ссылка.Номенклатура В(&ПустаяНоменклатура)
		|			ИЛИ (ШтрихкодыУпаковок.Штрихкод.Номенклатура = ШтрихкодыУпаковок.Ссылка.Номенклатура
		|				И ШтрихкодыУпаковок.Штрихкод.Характеристика = ШтрихкодыУпаковок.Ссылка.Характеристика))
		|ГДЕ
		|	ШтрихкодыУпаковок.Ссылка В
		|			(ВЫБРАТЬ
		|				т.Штрихкод
		|			ИЗ
		|				ШтрихкодыВходящие КАК т)",
		"ВложенныеЗаписи");
	
	СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	ШтрихкодыВходящие.Ссылка   КАК Ссылка,
		|	ШтрихкодыВходящие.Штрихкод КАК Родитель,
		|	ШтрихкодыУпаковок.Штрихкод КАК Штрихкод
		|ИЗ
		|	Справочник.ШтрихкодыУпаковокТоваров.ВложенныеШтрихкоды КАК ШтрихкодыУпаковок
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ШтрихкодыВходящие КАК ШтрихкодыВходящие
		|		ПО ШтрихкодыВходящие.Штрихкод = ШтрихкодыУпаковок.Ссылка
		|		И ШтрихкодыВходящие.Штрихкод.ТипУпаковки = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковок.МультитоварнаяУпаковка)
		|		И (ШтрихкодыУпаковок.Ссылка.Номенклатура В(&ПустаяНоменклатура)
		|			ИЛИ (ШтрихкодыУпаковок.Штрихкод.Номенклатура = ШтрихкодыУпаковок.Ссылка.Номенклатура
		|				И ШтрихкодыУпаковок.Штрихкод.Характеристика = ШтрихкодыУпаковок.Ссылка.Характеристика))
		|ГДЕ
		|	ШтрихкодыУпаковок.Ссылка В
		|			(ВЫБРАТЬ
		|				т.Штрихкод
		|			ИЗ
		|				ШтрихкодыВходящие КАК т)",
		"МультитоварныеУпаковки");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ШтрихкодУпаковки", ТаблицаУпаковок);
	Запрос.УстановитьПараметр("ПустаяНоменклатура", ИнтеграцияИС.НезаполненныеЗначенияОпределяемогоТипа("Номенклатура"));
	РезультатыЗапроса = ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов, Ложь);
	
	Результат              = РезультатыЗапроса["Результат"].Выгрузить(); // Коды маркировки и шаблоны для упаковок
	Результат.Колонки.Добавить("КоличествоВПотребительскойУпаковке", ОбщегоНазначения.ОписаниеТипаЧисло(15, 6));
	Результат.Колонки.Добавить("ТекстОшибкиИС",                      ОбщегоНазначения.ОписаниеТипаСтрока(1024));
	ВложенныеЗаписи        = РезультатыЗапроса["ВложенныеЗаписи"].Выгрузить(); // Монотоварные упаковки
	МультитоварныеУпаковки = РезультатыЗапроса["МультитоварныеУпаковки"].Выгрузить(); // Мультитоварные упаковки
	
	Результат.Индексы.Добавить("Ссылка, Штрихкод");
	СтруктураПоиска = Новый Структура("Ссылка, Штрихкод");
	
	Пока ВложенныеЗаписи.Количество() Цикл
		Запрос.УстановитьПараметр("ШтрихкодУпаковки", ВложенныеЗаписи);
		ДанныеВложенныхШтрихкодов = ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов, Ложь);
		ДочерниеЗаписи = ДанныеВложенныхШтрихкодов["Результат"].Выгрузить();
		ВложенныеЗаписи.Индексы.Добавить("Ссылка, Штрихкод");
		Для Каждого ДанныеШтрихКода Из ДочерниеЗаписи Цикл
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, ДанныеШтрихКода);
			ДанныеРодителя = ВложенныеЗаписи.НайтиСтроки(СтруктураПоиска)[0];
			СтруктураПоиска.Штрихкод = ДанныеРодителя.Родитель;
			СтрокаЗаполнения = Результат.НайтиСтроки(СтруктураПоиска)[0];
			ЗаполнитьЗначенияСвойств(СтрокаЗаполнения, ДанныеШтрихКода, "Штрихкод, Номенклатура, Характеристика, Серия, ВидПродукции");
		КонецЦикла;
		ВложенныеЗаписи = ДанныеВложенныхШтрихкодов["ВложенныеЗаписи"].Выгрузить();
	КонецЦикла;
	
	//После первичного получения мультитоварной упаковки нужна вся вложенность
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"И ШтрихкодыВходящие.Штрихкод.ТипУпаковки = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковок.МультитоварнаяУпаковка)", "");
	СоответствиеПоОбъектам = Новый Соответствие;
	
	Результат.Индексы.Добавить("Ссылка, Штрихкод, Номенклатура, Характеристика, Серия, ВидПродукции");
	ПоляПоискаДублей = Новый Структура("Ссылка, Штрихкод, Номенклатура, Характеристика, Серия, ВидПродукции");
		
	Пока МультитоварныеУпаковки.Количество() Цикл
		
		Для Каждого СтрокаВложение Из МультитоварныеУпаковки Цикл
			
			СоответствиеУпаковок = СоответствиеПоОбъектам.Получить(СтрокаВложение.Ссылка);
			Если СоответствиеУпаковок = Неопределено Тогда
				СоответствиеУпаковок = Новый Соответствие;
				СоответствиеУпаковок.Вставить(СтрокаВложение.Штрихкод, СтрокаВложение.Родитель);
				СоответствиеПоОбъектам.Вставить(СтрокаВложение.Ссылка, СоответствиеУпаковок);
			ИначеЕсли СоответствиеУпаковок.Получить(СтрокаВложение.Родитель) = Неопределено Тогда
				СоответствиеУпаковок.Вставить(СтрокаВложение.Штрихкод, СтрокаВложение.Родитель);
			Иначе
				СоответствиеУпаковок.Вставить(СтрокаВложение.Штрихкод, СоответствиеУпаковок.Получить(СтрокаВложение.Родитель));
			КонецЕсли;
			
		КонецЦикла;
		
		Запрос.УстановитьПараметр("ШтрихкодУпаковки", МультитоварныеУпаковки);
		ДанныеВложенныхШтрихкодов = ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов, Ложь);
		ДочерниеЗаписи = ДанныеВложенныхШтрихкодов["Результат"].Выгрузить();
		
		МультитоварныеУпаковки.Индексы.Добавить("Ссылка, Штрихкод");
		
		Для Каждого ДанныеШтрихКода Из ДочерниеЗаписи Цикл
			Если ЗначениеЗаполнено(ДанныеШтрихКода.Номенклатура) Тогда
				ЗаполнитьЗначенияСвойств(СтруктураПоиска, ДанныеШтрихКода);
				ДанныеРодителя = МультитоварныеУпаковки.НайтиСтроки(СтруктураПоиска)[0];
				СтруктураПоиска.Штрихкод = СоответствиеУпаковок.Получить(ДанныеРодителя.Штрихкод);
				
				//Мультитоварные коды ОСУ сразу нужно разрезать на составляющие
				//ОСУ+КМ не поддерживаем
				СтрокаЗаполнения = Результат.НайтиСтроки(СтруктураПоиска);
				Если СтрокаЗаполнения.Количество() = 0 Тогда
					СтрокаЗаполнения = Результат.НайтиСтроки(
						Новый Структура("Номенклатура,Характеристика", ДанныеШтрихКода.Номенклатура, ДанныеШтрихКода.Характеристика));
					Если СтрокаЗаполнения.Количество() = 0 Тогда
						СтрокаЗаполнения = Результат.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаЗаполнения, ДанныеШтрихКода,,"Серия");
					Иначе
						СтрокаЗаполнения = СтрокаЗаполнения[0];
						КоличествоПотребительскихУпаковок = Формат(СтрокаЗаполнения.КоличествоПотребительскихУпаковок, "ЧГ=0");
						СтрокаЗаполнения.ЗначениеШтрихкода = Лев(СтрокаЗаполнения.ЗначениеШтрихкода, СтрДлина(СтрокаЗаполнения.ЗначениеШтрихкода) - СтрДлина(КоличествоПотребительскихУпаковок)); 
						СтрокаЗаполнения.Количество = СтрокаЗаполнения.Количество + ДанныеШтрихКода.Количество;
						СтрокаЗаполнения.КоличествоПотребительскихУпаковок = СтрокаЗаполнения.КоличествоПотребительскихУпаковок + ДанныеШтрихКода.КоличествоПотребительскихУпаковок;
						КоличествоПотребительскихУпаковок = Формат(СтрокаЗаполнения.КоличествоПотребительскихУпаковок, "ЧГ=0");
						СтрокаЗаполнения.ЗначениеШтрихкода = СтрокаЗаполнения.ЗначениеШтрихкода + КоличествоПотребительскихУпаковок; 
					КонецЕсли;
					Продолжить;
				КонецЕсли;
				СтрокаЗаполнения = СтрокаЗаполнения[0];
				РезультатРазбора = Неопределено;
				НормализованныйКодМаркировки = КодМаркировкиДляПередачиУПД(
					СтрокаЗаполнения.ЗначениеШтрихкода, ДанныеШтрихКода.ВидПродукции, РезультатРазбора);
				Если РезультатРазбора <> Неопределено
					И РезультатРазбора.ВидУпаковки = Перечисления.ВидыУпаковокИС.ОбъемноСортовойУчет Тогда
					ЗаполнитьЗначенияСвойств(СтрокаЗаполнения, ДанныеШтрихКода);
					СтрокаЗаполнения.Серия = ОбщегоНазначенияИС.ПустоеЗначениеОпределяемогоТипа("СерияНоменклатуры");
					Продолжить;
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(СтрокаЗаполнения.Номенклатура) Тогда
					ЗаполнитьЗначенияСвойств(СтрокаЗаполнения, ДанныеШтрихКода,
						"Номенклатура, Характеристика, Серия, ВидПродукции, ТребуетВзвешивания");
				Иначе
					ЗаполнитьЗначенияСвойств(ПоляПоискаДублей, ДанныеШтрихКода);
					ПоляПоискаДублей.Штрихкод = СтруктураПоиска.Штрихкод;
					Если Результат.НайтиСтроки(ПоляПоискаДублей).Количество() = 0 Тогда
						НоваяСтрокаМультитоварнойУпаковки = Результат.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрокаМультитоварнойУпаковки, СтрокаЗаполнения);
						ЗаполнитьЗначенияСвойств(НоваяСтрокаМультитоварнойУпаковки, ПоляПоискаДублей);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		МультитоварныеУпаковки = ДанныеВложенныхШтрихкодов["МультитоварныеУпаковки"].Выгрузить();
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из Результат Цикл
		
		РезультатРазбора = Неопределено; // см. ШтрихкодированиеОбщегоНазначенияИС.НоваяСтруктураОбработкиШтрихкода
		
		НормализованныйКодМаркировки = КодМаркировкиДляПередачиУПД(
			СтрокаТаблицы.ЗначениеШтрихкода, СтрокаТаблицы.ВидПродукции, РезультатРазбора);
		
		Если ЗначениеЗаполнено(РезультатРазбора.ВидУпаковки) Тогда
			СтрокаТаблицы.ВидУпаковки = РезультатРазбора.ВидУпаковки;
		ИначеЕсли СтрокаТаблицы.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар Тогда
			СтрокаТаблицы.ВидУпаковки = Перечисления.ВидыУпаковокИС.Потребительская;
		ИначеЕсли ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.ВозможнаГрупповаяУпаковкаИлиНабор(
			РезультатРазбора.ВидУпаковки, РезультатРазбора.ДанныеРазбора) Тогда
			СтрокаТаблицы.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая;
		КонецЕсли;
		
		СтрокаТаблицы.ЗначениеШтрихкода = НормализованныйКодМаркировки;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Разворачивает таблицу транспортных и индивидуальных штрихкодов до номенклатуры, характеристики, серии
//   для дальнейшего сопоставления информации об упаковках с товарной частью.
// Добавляет в таблицу коды ОСУ для строк, по которым коды маркировки не указывались. Считаем что таблица товаров
//   уже соответствует таблице штрихкодов упаковок (с учетом невнесенных ОСУ).
//
// Параметры:
// 	ТаблицаУпаковок - ТаблицаЗначений - исходная таблица упаковок:
// 	 * Ссылка   - Произвольный - объект-владелец штрихкода
// 	 * Штрихкод - СправочникСсылка.ШтрихкодыУпаковокТоваров - Штрихкод
//  ТаблицаТоваров	 - ТаблицаЗначений - исходная таблица товаров: 
// 	 * Ссылка         - Произвольный - объект-владелец штрихкода
// 	 * Номенклатура   - ОпределяемыйТип.Номенклатура - номенклатура
// 	 * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика
// 	 * Количество     - Число - количество
//  ПараметрыСканирования - См. ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования
// Возвращаемое значение:
//  ТаблицаЗначений - См. ЧастичноеСодержимое
//
Функция ЧастичноеСодержимоеИКодыОСУ(ТаблицаУпаковок, ТаблицаТоваров, ПараметрыСканирования) Экспорт
	
	ЧастичноеСодержимоеРезультат = ЧастичноеСодержимое(ТаблицаУпаковок);
	
	Если ЧастичноеСодержимоеРезультат.Колонки.Найти("ТекстОшибкиИС") = Неопределено Тогда
		ЧастичноеСодержимоеРезультат.Колонки.Добавить("ТекстОшибкиИС", ОбщегоНазначения.ОписаниеТипаСтрока(1024));
	КонецЕсли;
	
	ДополнитьРезультатКодамиОСУ(ТаблицаТоваров, ПараметрыСканирования, ЧастичноеСодержимоеРезультат);
	
	Возврат ЧастичноеСодержимоеРезультат;
	
КонецФункции

// Разворачивает таблицу транспортных и индивидуальных штрихкодов до номенклатуры, характеристики, серии
// для дальнейшего сравнения информации об упаковках с товарной частью.
// 
// Параметры:
//   ШтрихкодыУпаковок - Массив Из СправочникСсылка.ШтрихкодыУпаковокТоваров                                       - Исходные штрихкоды упаковок без частичного выбытия.
//                     - Массив Из см. ШтрихкодированиеИС.НовыйЭлементКоллекцииУпаковокДляРаспределенияПоТоварам - Исходные штрихкоды упаковок, используя частичное выбытие.
//   ВидыПродукцииИС   - ПеречислениеСсылка.ВидыПродукцииИС, Массив Из ПеречислениеСсылка.ВидыПродукцииИС, Неопределено -
//      ограничение содержимого штрихкодов упаковок по виду/видам маркируемой продукции.
//   ДоКомплектующих - Булево - Разворачивать наборы до комплектующих
//   ВключатьШтрихкодУпаковки - Булево - Сворачивает полученную таблицу содержимого до штрихкода упаковки
// Возвращаемое значение:
// ТаблицаЗначений - таблица состава упаковок:
//   * ШтрихкодУпаковки   - СправочникСсылка.ШтрихкодыУпаковокТоваров  - Штрихкод упаковки.
//   * Номенклатура       - ОпределяемыйТип.Номенклатура               - содержимое
//   * Характеристика     - ОпределяемыйТип.ХарактеристикаНоменклатуры - содержимое
//   * Серия              - ОпределяемыйТип.СерияНоменклатуры          - содержимое
//   * Количество         - Число                                      - количество единиц содержимого
//   * ЧастичноеВыбытиеВариантУчета - ПеречислениеСсылка.ВариантыУчетаЧастичногоВыбытияИС - Вариант частичного выбытия.
Функция Содержимое(Знач ШтрихкодыУпаковок, Знач ВидыПродукцииИС = Неопределено, ДоКомплектующих = Ложь, ВключатьШтрихкодУпаковки = Ложь) Экспорт
	
	Запрос           = Новый Запрос;
	КэшВходящихСтрок = Новый Соответствие;
	ПроверятьЧастичноеВыбытие = Неопределено;
	
	Если ШтрихкодыУпаковок.Количество()
		И ТипЗнч(ШтрихкодыУпаковок[0]) = Тип("СправочникСсылка.ШтрихкодыУпаковокТоваров") Тогда
		ПроверенныеШтрихкоды = ШтрихкодыУпаковок;
	Иначе
		ПроверенныеШтрихкоды = Новый Массив;
		Для Каждого ИсходнаяСтрока Из ШтрихкодыУпаковок Цикл
			ПроверенныеШтрихкоды.Добавить(ИсходнаяСтрока.ШтрихкодУпаковки);
			Если ПроверятьЧастичноеВыбытие = Неопределено Тогда
				ПроверятьЧастичноеВыбытие = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ИсходнаяСтрока, "ЧастичноеВыбытиеВариантУчета");
			КонецЕсли;
			Если ПроверятьЧастичноеВыбытие Тогда
				КэшВходящихСтрок.Вставить(ИсходнаяСтрока.ШтрихкодУпаковки, ИсходнаяСтрока);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ВыделитьШтрихкодыСодержащиеВидыПродукции(ПроверенныеШтрихкоды, ВидыПродукцииИС);
	
	Запрос.УстановитьПараметр("ШтрихкодУпаковки", ПроверенныеШтрихкоды);
	Запрос.УстановитьПараметр("ЭтоШтрихкодУпаковкиНабора", Ложь);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ШтрихкодыУпаковок.Ссылка         КАК Штрихкод,
	|	ШтрихкодыУпаковок.Номенклатура   КАК Номенклатура,
	|	ШтрихкодыУпаковок.Характеристика КАК Характеристика,
	|	ШтрихкодыУпаковок.Серия          КАК Серия,
	|	ВЫБОР
	|		КОГДА ШтрихкодыУпаковок.Количество = 0
	|			ТОГДА 1
	|		ИНАЧЕ ШтрихкодыУпаковок.Количество
	|	КОНЕЦ                            КАК Количество,
	|	ВЫБОР
	|		КОГДА ШтрихкодыУпаковок.Количество = 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ                            КАК КоличествоНеУказано,
	|	ВЫБОР
	|		КОГДА ШтрихкодыУпаковок.КоличествоПотребительскихУпаковок <> 0
	|			ТОГДА ШтрихкодыУпаковок.КоличествоПотребительскихУпаковок
	|		КОГДА ШтрихкодыУпаковок.Количество = 0
	|			ТОГДА 1
	|		ИНАЧЕ ШтрихкодыУпаковок.Количество
	|	КОНЕЦ                            КАК КоличествоПотребительскихУпаковок,
	|	ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаЧастичногоВыбытияИС.ПустаяСсылка) КАК ЧастичноеВыбытиеВариантУчета
	|ИЗ
	|	Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковок
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров.ВложенныеШтрихкоды КАК Вложенные
	|		ПО ШтрихкодыУпаковок.Ссылка = Вложенные.Ссылка
	|ГДЕ
	|	ШтрихкодыУпаковок.Ссылка В (&ШтрихкодУпаковки)
	|	И (Вложенные.Ссылка ЕСТЬ NULL
	|		ИЛИ &ЭтоШтрихкодУпаковкиНабора)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Вложенные.Штрихкод КАК Штрихкод
	|ИЗ
	|	Справочник.ШтрихкодыУпаковокТоваров.ВложенныеШтрихкоды КАК Вложенные
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковок
	|	ПО ШтрихкодыУпаковок.Ссылка = Вложенные.Ссылка
	|ГДЕ
	|	ШтрихкодыУпаковок.Ссылка В (&ШтрихкодУпаковки)
	|	И Вложенные.Ссылка В (&ШтрихкодУпаковки)
	|	И Не &ЭтоШтрихкодУпаковкиНабора";
	
	Если Не ДоКомплектующих Тогда
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст, "&ЭтоШтрихкодУпаковкиНабора",
			"(ШтрихкодыУпаковок.КоличествоПотребительскихУпаковок = 1
			|	ИЛИ (ШтрихкодыУпаковок.КоличествоПотребительскихУпаковок = 0 И ШтрихкодыУпаковок.Количество = 1))");
	КонецЕсли;
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	Результат = МассивРезультатов[0].Выгрузить();
	ВложенныеЗаписи = МассивРезультатов[1].Выгрузить().ВыгрузитьКолонку("Штрихкод");
	Для Каждого СтрокаТаблицы Из Результат Цикл
		ОбработкаСтрокиЧастичногоВыбытия(СтрокаТаблицы, КэшВходящихСтрок);
	КонецЦикла;
	Пока ВложенныеЗаписи.Количество() Цикл
		
		Запрос.УстановитьПараметр("ШтрихкодУпаковки", ВложенныеЗаписи);
		ДанныеВложенныхШтрихкодов = Запрос.ВыполнитьПакет();
		ДочерниеЗаписи = ДанныеВложенныхШтрихкодов[0].Выбрать();
		
		Пока ДочерниеЗаписи.Следующий() Цикл
			
			НоваяСтрока = Результат.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДочерниеЗаписи);
			ОбработкаСтрокиЧастичногоВыбытия(НоваяСтрока, КэшВходящихСтрок);
			
		КонецЦикла;
		
		ВложенныеЗаписи = ДанныеВложенныхШтрихкодов[1].Выгрузить().ВыгрузитьКолонку("Штрихкод");
		
	КонецЦикла;
	
	Если ВключатьШтрихкодУпаковки Тогда
		ПоляСвертки = "Номенклатура, Характеристика, Серия, Штрихкод";
	Иначе
		ПоляСвертки = "Номенклатура, Характеристика, Серия"
	КонецЕсли;

	Результат.Свернуть(ПоляСвертки, "КоличествоНеУказано, КоличествоПотребительскихУпаковок, ЧастичноеВыбытиеВариантУчета, Количество");
	
	Возврат Результат;
	
КонецФункции

// Разворачивает таблицу транспортных и индивидуальных штрихкодов до номенклатуры, характеристики, серии
// для дальнейшего сравнения информации об упаковках с товарной частью.
//
// Параметры:
//  Упаковки - Массив Из см. ШтрихкодированиеИС.НовыйЭлементКоллекцииУпаковокДляРаспределенияПоТоварам - штрихкоды упаковок, используя частичное выбытие.
//                  - ТаблицаЗначений - исходная таблица упаковок:
// 	 * Штрихкод                       - СправочникСсылка.ШтрихкодыУпаковокТоваров - Штрихкод
//  ТаблицаТоваров	 - ТаблицаЗначений - исходная таблица товаров:
// 	 * Ссылка         - Произвольный - объект-владелец штрихкода
// 	 * Номенклатура   - ОпределяемыйТип.Номенклатура - номенклатура
// 	 * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика
// 	 * Количество     - Число - количество
//  ПараметрыСканирования - См. ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования
// Возвращаемое значение:
//  ТаблицаЗначений - см. Содержимое
Функция СодержимоеИКодыОСУ(Упаковки, ТаблицаТоваров, ПараметрыСканирования) Экспорт
	
	Если ТипЗнч(Упаковки) = Тип("ТаблицаЗначений") Тогда
		ИсточникСодержимого = Упаковки.ВыгрузитьКолонку("Штрихкод");
	Иначе
		ИсточникСодержимого = Упаковки;
	КонецЕсли;
	
	СодержимоеРезультат = Содержимое(ИсточникСодержимого,,, Истина);

	СодержимоеРезультат.Колонки.Добавить("ТекстОшибкиИС",      ОбщегоНазначения.ОписаниеТипаСтрока(1024));
	СодержимоеРезультат.Колонки.Добавить("ВидУпаковки",        Новый ОписаниеТипов("ПеречислениеСсылка.ВидыУпаковокИС"));
	СодержимоеРезультат.Колонки.Добавить("Обработан",          Новый ОписаниеТипов("Булево"));
	СодержимоеРезультат.Колонки.Добавить("ВидПродукции",       Новый ОписаниеТипов("ПеречислениеСсылка.ВидыПродукцииИС"));
	СодержимоеРезультат.Колонки.Добавить("Мультитоварная",     Новый ОписаниеТипов("Булево"));
	СодержимоеРезультат.Колонки.Добавить("ТребуетВзвешивания", Новый ОписаниеТипов("Булево"));
	СодержимоеРезультат.Колонки.Добавить("ЗначениеШтрихкода",  ОбщегоНазначения.ОписаниеТипаСтрока(255));
	СодержимоеРезультат.Колонки.Добавить("КоличествоВПотребительскойУпаковке", ОбщегоНазначения.ОписаниеТипаЧисло(15, 6));

	ДополнитьРезультатКодамиОСУ(ТаблицаТоваров, ПараметрыСканирования, СодержимоеРезультат);

	Возврат СодержимоеРезультат;

КонецФункции

// Дополняет таблицу транспортных и индивидуальных штрихкодов видом продукции, видом упаковок
// для дальнейшего заполнения данных о кодах маркировки при формировании ТОРГ-2.
// 
// Параметры:
// 	ТаблицаУпаковок - ТаблицаЗначений - исходная таблица упаковок:
// 	 * Ссылка   - Произвольный - объект-владелец штрихкода
// 	 * Штрихкод - СправочникСсылка.ШтрихкодыУпаковокТоваров - Штрихкод
// 	 * Номенклатура       - ОпределяемыйТип.Номенклатура - содержимое
// 	 * Характеристика     - ОпределяемыйТип.ХарактеристикаНоменклатуры - содержимое
//   ПолеТипРасхождения - Строка - путь к данным тип расхождения, необязательный параметр.
// 
// Возвращаемое значение:
// ТаблицаЗначений - таблица упаковок с товарными данными:
// 	 * Ссылка             - Произвольный - объект-владелец штрихкода - исходная колонка
// 	 * Номенклатура       - ОпределяемыйТип.Номенклатура - содержимое
// 	 * Характеристика     - ОпределяемыйТип.ХарактеристикаНоменклатуры - содержимое
// 	 * ЗначениеШтрихкода  - Строка - текстовое представление исходного штрихкода для передачи в ЭДО
// 	 * ВидПродукции       - Перечислениессылка.ВидыПродукцииИС - вид продукции номенклатуры для кодировки значения штрихкода
// 	 * ВидУпаковки        - ПеречислениеСсылка.ВидыУпаковокИС - вид упаковки номенклатуры для кодировки значения штрихкода
// 	 * Обработан          - Булево - признак сопоставления строки товарной части документа
//   * ТипРасхождения     - ОпределяемыйТип.ТипРасхожденияИСМП - тип расхождения.
Функция ЧастичноеСодержимоеТОРГ2(ТаблицаУпаковок, ПолеТипРасхождения = Неопределено) Экспорт
	
	НормализоватьТаблицуЗначений(ТаблицаУпаковок);
	
	СписокЗапросов = Новый СписокЗначений;
	
	ТекстЗапросаДанныеШтрихкодовУпаковок = 
		"ВЫБРАТЬ
		|	ШтрихкодыУпаковок.Ссылка            КАК Ссылка,
		|	ШтрихкодыУпаковок.ЗначениеШтрихкода КАК ЗначениеШтрихкода,
		|	ШтрихкодыУпаковок.Номенклатура      КАК Номенклатура,
		|	ШтрихкодыУпаковок.Характеристика    КАК Характеристика,
		|	ШтрихкодыУпаковок.Количество        КАК Количество,
		|	ШтрихкодыУпаковок.ВидУпаковки       КАК ВидУпаковки,
		|	&ПолеТипРасхождения КАК ТипРасхождения 
		|ПОМЕСТИТЬ ДанныеШтрихкодовУпаковок
		|ИЗ
		|	&ШтрихкодУпаковки КАК ШтрихкодыУпаковок
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗначениеШтрихкода
		|";
	
	Если ПолеТипРасхождения = Неопределено Тогда
		ПолеТипРасхождения = "&ПустойТипРасхождения";
	Иначе
		ПолеТипРасхождения = "ШтрихкодыУпаковок." + ПолеТипРасхождения;
	КонецЕсли;
	ТекстЗапросаДанныеШтрихкодовУпаковок = СтрЗаменить(ТекстЗапросаДанныеШтрихкодовУпаковок, "&ПолеТипРасхождения", ПолеТипРасхождения);

	СписокЗапросов.Добавить(ТекстЗапросаДанныеШтрихкодовУпаковок);
	СписокЗапросов.Добавить(
		ШтрихкодированиеОбщегоНазначенияИС.ТекстЗапросаСвойстваМаркируемойПродукции());
	
	СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	ДанныеШтрихкодовУпаковок.Ссылка                    КАК Ссылка,
		|	ДанныеШтрихкодовУпаковок.ЗначениеШтрихкода         КАК ЗначениеШтрихкода,
		|	ДанныеШтрихкодовУпаковок.Номенклатура              КАК Номенклатура,
		|	ДанныеШтрихкодовУпаковок.Характеристика            КАК Характеристика,
		|	ДанныеШтрихкодовУпаковок.Количество                КАК Количество,
		|	ДанныеШтрихкодовУпаковок.ТипРасхождения            КАК ТипРасхождения,
		|	Ложь                                               КАК Обработан,
		|	СвойстваМаркируемойПродукции.ВидПродукции          КАК ВидПродукции,
		|	ДанныеШтрихкодовУпаковок.ВидУпаковки               КАК ВидУпаковки
		|ИЗ
		|	ДанныеШтрихкодовУпаковок КАК ДанныеШтрихкодовУпаковок
		|		ЛЕВОЕ СОЕДИНЕНИЕ СвойстваМаркируемойПродукции КАК СвойстваМаркируемойПродукции
		|		ПО ДанныеШтрихкодовУпаковок.Номенклатура = СвойстваМаркируемойПродукции.Номенклатура
		|",
		"Результат");
		
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ШтрихкодУпаковки", ТаблицаУпаковок);
	Запрос.УстановитьПараметр("ПустойТипРасхождения", ОбщегоНазначенияИС.ПустоеЗначениеОпределяемогоТипа("ТипРасхожденияИСМП"));
	
	РезультатыЗапроса = ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов, Ложь);
	
	Результат = РезультатыЗапроса["Результат"].Выгрузить();
	
	Для Каждого СтрокаТаблицы Из Результат Цикл
		
		РезультатРазбора = Неопределено; // см. ШтрихкодированиеОбщегоНазначенияИС.НоваяСтруктураОбработкиШтрихкода
		
		НормализованныйКодМаркировки = КодМаркировкиДляПередачиУПД(
			СтрокаТаблицы.ЗначениеШтрихкода, СтрокаТаблицы.ВидПродукции, РезультатРазбора);
		
		Если ЗначениеЗаполнено(РезультатРазбора.ВидУпаковки)
			И Не ЗначениеЗаполнено(СтрокаТаблицы.ВидУпаковки) Тогда
			СтрокаТаблицы.ВидУпаковки = РезультатРазбора.ВидУпаковки;
		КонецЕсли;
		
		СтрокаТаблицы.ЗначениеШтрихкода = НормализованныйКодМаркировки;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура НормализоватьШтрихкодыУпаковок(Товары, ШтрихкодыУпаковок) Экспорт
	
	РазделительЗапросов = "
						  |;
				          |///////////////////////////////////////////////////////////////
						  |";
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика
		|ПОМЕСТИТЬ ДанныеШтрихкодовУпаковок
		|ИЗ
		|	&Товары КАК Товары
		|";
	
	ТекстЗапроса = ТекстЗапроса + РазделительЗапросов;
	ТекстЗапроса = ТекстЗапроса + ШтрихкодированиеОбщегоНазначенияИС.ТекстЗапросаСвойстваМаркируемойПродукции();
	ТекстЗапроса = ТекстЗапроса + РазделительЗапросов;
	
	ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СвойстваМаркируемойПродукции.ВидПродукции КАК ВидПродукции
		|ИЗ
		|	СвойстваМаркируемойПродукции КАК СвойстваМаркируемойПродукции
		|ГДЕ
		|	СвойстваМаркируемойПродукции.ВидПродукции <> НЕОПРЕДЕЛЕНО";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("Товары", Товары);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ВидыПродукции = Неопределено;
	Иначе
		ВидыПродукции = Результат.Выгрузить().ВыгрузитьКолонку("ВидПродукции");
	КонецЕсли;
	
	НастройкиРазбораКодаМаркировки = РазборКодаМаркировкиИССлужебный.НастройкиРазбораКодаМаркировки(ВидыПродукции);
	
	Для Каждого СтрокаДанных из ШтрихкодыУпаковок Цикл
		ДанныеРазбора = РазборКодаМаркировкиИССлужебный.РазобратьКодМаркировки(
			СтрокаДанных.ЗначениеШтрихкода, ВидыПродукции,, НастройкиРазбораКодаМаркировки);
		Если ДанныеРазбора <> Неопределено Тогда
			СтрокаДанных.ЗначениеШтрихкода = ДанныеРазбора.НормализованныйКодМаркировки;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Нормализация кодов, полученных в переданной таблице, заполненной по данным ЭДО.
// Входящая таблица штрихкоды должна содержать данные о номенклатуре, используемой для определения вида продукции при разборе кодов.
// 
// Параметры:
//  ШтрихкодыУпаковок - ТаблицаЗначений - таблица, содержащая колонки:
//  * ЗначениеШтрихкода - Строка - Значение штрихкода.
//  * Номенклатура - ОпределяемыйТип.Номенклатура - Номенклатура.
//  * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - Характеристика.
Процедура НормализоватьШтрихкодыУпаковокУКД(ШтрихкодыУпаковок) Экспорт
	
	РазделительЗапросов = "
		|;
		|///////////////////////////////////////////////////////////////
		|";
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура
		
		|ПОМЕСТИТЬ ДанныеШтрихкодовУпаковок
		|ИЗ
		|	&ШтрихкодыУпаковок КАК Товары
		|";
	
	ТекстЗапроса = ТекстЗапроса + РазделительЗапросов;
	ТекстЗапроса = ТекстЗапроса + ШтрихкодированиеОбщегоНазначенияИС.ТекстЗапросаСвойстваМаркируемойПродукции();
	ТекстЗапроса = ТекстЗапроса + РазделительЗапросов;
	
	ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СвойстваМаркируемойПродукции.Номенклатура КАК Номенклатура,
		|	СвойстваМаркируемойПродукции.ВидПродукции КАК ВидПродукции
		|ИЗ
		|	СвойстваМаркируемойПродукции КАК СвойстваМаркируемойПродукции
		|ГДЕ
		|	СвойстваМаркируемойПродукции.ВидПродукции <> НЕОПРЕДЕЛЕНО
		|ИТОГИ ПО
		|	ВидПродукции";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("ШтрихкодыУпаковок", ШтрихкодыУпаковок);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		СвернутьТаблицуШтрихкодовУпаковок(ШтрихкодыУпаковок);
		Возврат;
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура();
	ПараметрыОтбора.Вставить("Номенклатура");

	ВыборкаПоВидуПродукции = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоВидуПродукции.Следующий() Цикл
		
		ВидПродукции = ВыборкаПоВидуПродукции.ВидПродукции;
		
		НастройкиРазбораКодаМаркировки = РазборКодаМаркировкиИССлужебный.НастройкиРазбораКодаМаркировки(ВидПродукции);
		ПользовательскиеПараметрыРазбораКодаМаркировки = РазборКодаМаркировкиИССлужебныйКлиентСервер.ПользовательскиеПараметрыРазбораКодаМаркировки();
		
		Выборка = ВыборкаПоВидуПродукции.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ПараметрыОтбора.Номенклатура = Выборка.Номенклатура;
			
			НайденныеСтроки = ШтрихкодыУпаковок.НайтиСтроки(ПараметрыОтбора);
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				
				ДанныеРазбора = РазборКодаМаркировкиИССлужебный.РазобратьКодМаркировки(
					НайденнаяСтрока.ЗначениеШтрихкода, ВидПродукции,, НастройкиРазбораКодаМаркировки, ПользовательскиеПараметрыРазбораКодаМаркировки);
				Если ДанныеРазбора <> Неопределено Тогда
					НайденнаяСтрока.ЗначениеШтрихкода = ДанныеРазбора.НормализованныйКодМаркировки;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

// Обрабатывает таблицы кодов маркировки для заполнения УКД только расхождениями (сокращенный формат).
//
// Параметры:
//  ШтрихкодыУпаковокДо - ТаблицаЗначений - См. ЧастичноеСодержимое. Таблица кодов документа-основания. Будет перезаполнена кодами, которые отсутствуют в корректируемом документе.
//  ШтрихкодыУпаковок   - ТаблицаЗначений - См. ЧастичноеСодержимое. Таблица кодов документа корректировки. Будет перезаполнена кодами, которые отсутствуют в документе основании.
Процедура ЗаполнитьРасхожденияКодовМаркировкиУКД(ШтрихкодыУпаковокДо, ШтрихкодыУпаковок) Экспорт
	
	СписокЗапросов = Новый СписокЗначений;
	
	СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	ШтрихкодыУпаковокДо.Ссылка                    КАК Ссылка,
		|	ШтрихкодыУпаковокДо.ЗначениеШтрихкода         КАК ЗначениеШтрихкода,
		|	ШтрихкодыУпаковокДо.Штрихкод                  КАК Штрихкод,
		|	ШтрихкодыУпаковокДо.Номенклатура              КАК Номенклатура,
		|	ШтрихкодыУпаковокДо.Характеристика            КАК Характеристика,
		|	ШтрихкодыУпаковокДо.Серия                     КАК Серия,
		|	ШтрихкодыУпаковокДо.Мультитоварная            КАК Мультитоварная,
		|	ШтрихкодыУпаковокДо.Обработан                 КАК Обработан,
		|	ШтрихкодыУпаковокДо.ТипУпаковки               КАК ТипУпаковки,
		|	ШтрихкодыУпаковокДо.ВидПродукции              КАК ВидПродукции,
		|	ШтрихкодыУпаковокДо.ВидУпаковки               КАК ВидУпаковки,
		|	ШтрихкодыУпаковокДо.Количество                КАК Количество,
		|	ШтрихкодыУпаковокДо.КоличествоВПотребительскойУпаковке КАК КоличествоВПотребительскойУпаковке
		|ПОМЕСТИТЬ втШтрихкодыУпаковокДо
		|ИЗ
		|	&ШтрихкодыУпаковокДо КАК ШтрихкодыУпаковокДо
		|ИНДЕКСИРОВАТЬ ПО Ссылка, Номенклатура, Характеристика, Серия
		|");
	
	СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	ШтрихкодыУпаковок.Ссылка                    КАК Ссылка,
		|	ШтрихкодыУпаковок.ЗначениеШтрихкода         КАК ЗначениеШтрихкода,
		|	ШтрихкодыУпаковок.Штрихкод                  КАК Штрихкод,
		|	ШтрихкодыУпаковок.Номенклатура              КАК Номенклатура,
		|	ШтрихкодыУпаковок.Характеристика            КАК Характеристика,
		|	ШтрихкодыУпаковок.Серия                     КАК Серия,
		|	ШтрихкодыУпаковок.Мультитоварная            КАК Мультитоварная,
		|	ШтрихкодыУпаковок.Обработан                 КАК Обработан,
		|	ШтрихкодыУпаковок.ТипУпаковки               КАК ТипУпаковки,
		|	ШтрихкодыУпаковок.ВидПродукции              КАК ВидПродукции,
		|	ШтрихкодыУпаковок.ВидУпаковки               КАК ВидУпаковки,
		|	ШтрихкодыУпаковок.Количество                КАК Количество,
		|	ШтрихкодыУпаковок.КоличествоВПотребительскойУпаковке КАК КоличествоВПотребительскойУпаковке
		|ПОМЕСТИТЬ втШтрихкодыУпаковок
		|ИЗ
		|	&ШтрихкодыУпаковок КАК ШтрихкодыУпаковок
		|ИНДЕКСИРОВАТЬ ПО Ссылка, Номенклатура, Характеристика, Серия
		|");
	
	СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	втШтрихкодыУпаковок.Ссылка КАК Ссылка,
		|	втШтрихкодыУпаковок.ЗначениеШтрихкода КАК ЗначениеШтрихкода,
		|	втШтрихкодыУпаковок.Штрихкод КАК Штрихкод,
		|	втШтрихкодыУпаковок.Номенклатура КАК Номенклатура,
		|	втШтрихкодыУпаковок.Характеристика КАК Характеристика,
		|	втШтрихкодыУпаковок.Серия КАК Серия,
		|	втШтрихкодыУпаковок.Мультитоварная КАК Мультитоварная,
		|	втШтрихкодыУпаковок.Обработан КАК Обработан,
		|	втШтрихкодыУпаковок.ТипУпаковки КАК ТипУпаковки,
		|	втШтрихкодыУпаковок.ВидПродукции КАК ВидПродукции,
		|	втШтрихкодыУпаковок.ВидУпаковки КАК ВидУпаковки,
		|	втШтрихкодыУпаковок.Количество КАК Количество,
		|	втШтрихкодыУпаковок.КоличествоВПотребительскойУпаковке КАК КоличествоВПотребительскойУпаковке
		|ИЗ
		|	втШтрихкодыУпаковок КАК втШтрихкодыУпаковок
		|		ЛЕВОЕ СОЕДИНЕНИЕ втШтрихкодыУпаковокДо КАК втШтрихкодыУпаковокДо
		|		ПО втШтрихкодыУпаковок.Ссылка = втШтрихкодыУпаковокДо.Ссылка
		|			И втШтрихкодыУпаковок.Номенклатура = втШтрихкодыУпаковокДо.Номенклатура
		|			И втШтрихкодыУпаковок.Характеристика = втШтрихкодыУпаковокДо.Характеристика
		|			И втШтрихкодыУпаковок.Серия = втШтрихкодыУпаковокДо.Серия
		|			И (ВЫБОР
		|				КОГДА втШтрихкодыУпаковок.ВидУпаковки = ЗНАЧЕНИЕ(Перечисление.ВидыУпаковокИС.ОбъемноСортовойУчет)
		|					ТОГДА втШтрихкодыУпаковок.ЗначениеШтрихкода = втШтрихкодыУпаковокДо.ЗначениеШтрихкода
		|						И втШтрихкодыУпаковок.Количество = втШтрихкодыУпаковокДо.Количество
		|				ИНАЧЕ втШтрихкодыУпаковок.Штрихкод = втШтрихкодыУпаковокДо.Штрихкод
		|			КОНЕЦ)
		|ГДЕ
		|	втШтрихкодыУпаковокДо.Ссылка ЕСТЬ NULL",
		"ШтрихкодыУпаковок");
	
	СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	втШтрихкодыУпаковокДо.Ссылка КАК Ссылка,
		|	втШтрихкодыУпаковокДо.ЗначениеШтрихкода КАК ЗначениеШтрихкода,
		|	втШтрихкодыУпаковокДо.Штрихкод КАК Штрихкод,
		|	втШтрихкодыУпаковокДо.Номенклатура КАК Номенклатура,
		|	втШтрихкодыУпаковокДо.Характеристика КАК Характеристика,
		|	втШтрихкодыУпаковокДо.Серия КАК Серия,
		|	втШтрихкодыУпаковокДо.Мультитоварная КАК Мультитоварная,
		|	втШтрихкодыУпаковокДо.Обработан КАК Обработан,
		|	втШтрихкодыУпаковокДо.ТипУпаковки КАК ТипУпаковки,
		|	втШтрихкодыУпаковокДо.ВидПродукции КАК ВидПродукции,
		|	втШтрихкодыУпаковокДо.ВидУпаковки КАК ВидУпаковки,
		|	втШтрихкодыУпаковокДо.Количество КАК Количество,
		|	втШтрихкодыУпаковокДо.КоличествоВПотребительскойУпаковке КАК КоличествоВПотребительскойУпаковке
		|ИЗ
		|	втШтрихкодыУпаковокДо КАК втШтрихкодыУпаковокДо
		|		ЛЕВОЕ СОЕДИНЕНИЕ втШтрихкодыУпаковок КАК втШтрихкодыУпаковок
		|		ПО втШтрихкодыУпаковокДо.Ссылка = втШтрихкодыУпаковок.Ссылка
		|			И втШтрихкодыУпаковокДо.Номенклатура = втШтрихкодыУпаковок.Номенклатура
		|			И втШтрихкодыУпаковокДо.Характеристика = втШтрихкодыУпаковок.Характеристика
		|			И втШтрихкодыУпаковокДо.Серия = втШтрихкодыУпаковок.Серия
		|			И (ВЫБОР
		|				КОГДА втШтрихкодыУпаковокДо.ВидУпаковки = ЗНАЧЕНИЕ(Перечисление.ВидыУпаковокИС.ОбъемноСортовойУчет)
		|					ТОГДА втШтрихкодыУпаковокДо.ЗначениеШтрихкода = втШтрихкодыУпаковок.ЗначениеШтрихкода
		|						И втШтрихкодыУпаковокДо.Количество = втШтрихкодыУпаковок.Количество
		|				ИНАЧЕ втШтрихкодыУпаковокДо.Штрихкод = втШтрихкодыУпаковок.Штрихкод
		|			КОНЕЦ)
		|ГДЕ
		|	втШтрихкодыУпаковок.Ссылка ЕСТЬ NULL",
		"ШтрихкодыУпаковокДо");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ШтрихкодыУпаковок", ШтрихкодыУпаковок);
	Запрос.УстановитьПараметр("ШтрихкодыУпаковокДо", ШтрихкодыУпаковокДо);

	РезультатыЗапроса = ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов, Ложь);
	
	ШтрихкодыУпаковок   = РезультатыЗапроса["ШтрихкодыУпаковок"].Выгрузить();
	ШтрихкодыУпаковокДо = РезультатыЗапроса["ШтрихкодыУпаковокДо"].Выгрузить();
	
КонецПроцедуры

// Заполняет в приемнике сведения по маркировке если они есть во входящих данных.
//
// Параметры:
//  Приемник - СтрокаТаблицыЗначений - для заполнения колонки СведенияОМаркировке.
//  Источник - СтрокаТаблицыЗначений - строка документа, содержащая колонки:
//   * Ссылка         - ДокументСсылка - ссылка на документ, для которого заполняются данные,
//   * Номенклатура   - ОпределяемыйТип.Номенклатура - номенклатура в строке документа,
//   * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика в строке,
//   * Серия          - ОпределяемыйТип.СерияНоменклатуры - серия в строке,
//   * Количество     - Число - количество в строке (ограничивает привязанные строки маркировки).
//  ДанныеШтрихкодовУпаковок - См. ЧастичноеСодержимое
//  ОбъектыСОграничениемДлины - Массив Из ДокументСсылка - документы в которых сработало ограничение на длину строки
//
Процедура ЗаполнитьСведенияОМаркировке(Приемник, Источник, ДанныеШтрихкодовУпаковок, ОбъектыСОграничениемДлины) Экспорт

	СтандартнаяОбработка = Истина;
	ЭлектронноеВзаимодействиеИСМППереопределяемый.ЗаполнитьСведенияОМаркировке(
		Приемник,
		Источник,
		ДанныеШтрихкодовУпаковок,
		СтандартнаяОбработка);
	
	Если СтандартнаяОбработка = Ложь Тогда
		Возврат;
	ИначеЕсли ДанныеШтрихкодовУпаковок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКДаннымДляОшибки = "Объект";
	
	ТаблицаКодов = Новый ТаблицаЗначений;
	ТаблицаКодов.Колонки.Добавить("КодУпаковки", Новый ОписаниеТипов("Строка"));
	
	СведенияОМаркировке = Новый Структура;
	СведенияОМаркировке.Вставить("КодыИндивидуальныхУпаковок", ТаблицаКодов);
	Приемник.СведенияОМаркировке = СведенияОМаркировке;
	
	ПараметрыПоиска = Новый Структура("Ссылка, Номенклатура");
	ЗаполнитьЗначенияСвойств(ПараметрыПоиска, Источник);
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Источник, "Характеристика") Тогда
		ПараметрыПоиска.Вставить("Характеристика", Источник.Характеристика);
	КонецЕсли;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Источник, "Серия")
			И ЗначениеЗаполнено(Источник.Серия) Тогда
		ПараметрыПоиска.Вставить("Серия", Источник.Серия);
	КонецЕсли;
	ПараметрыПоиска.Вставить("Обработан", Ложь);
	
	СтрокиУпаковок = ДанныеШтрихкодовУпаковок.НайтиСтроки(ПараметрыПоиска);
	ОбщаяДлинаКодов = 0;
	Для Каждого СтрокаУпаковки Из СтрокиУпаковок Цикл
		ОбщаяДлинаКодов = ОбщаяДлинаКодов + СтрДлина(СтрокаУпаковки.ЗначениеШтрихКода) + 2;
		Если ОбщаяДлинаКодов > 2000 Тогда
			ОбъектыСОграничениемДлины.Добавить(Источник.Ссылка);
			Прервать;
		ИначеЕсли СтрокаУпаковки.Обработан Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ТаблицаКодов.Добавить();
		НоваяСтрока.КодУпаковки = СтрокаУпаковки.ЗначениеШтрихКода;
		СтрокаУпаковки.Обработан = Истина;
		ОтметитьОбработанныеСтрокиДляМультитоварныхУпаковок(ДанныеШтрихкодовУпаковок, ПараметрыПоиска, СтрокаУпаковки);
	КонецЦикла;
	
	ВывестиОшибкуЗаполненияПользователю(ТаблицаКодов, "КодУпаковки", Источник.Ссылка, ПутьКДаннымДляОшибки);
	
КонецПроцедуры

// Проверяет распределение штрихкодов по строкам товаров. Требуется использование в формате ЭДО с ограничением
// количества символов кодов маркировки для строки маркируемой продукции (после применения ограничения могут быть
// распределены не все коды).
//
// Параметры:
//   ДанныеШтрихкодовУпаковок  - См. ЧастичноеСодержимое
//   ОбъектыСОграничениемДлины - Массив Из ДокументСсылка - ссылки на документы, где требуется проверка
//   Отказ                     - Булево - признак что не все коды распределены
//
Процедура ПроверитьСведенияОМаркировке(ДанныеШтрихкодовУпаковок, ОбъектыСОграничениемДлины, Отказ) Экспорт

	Если ДанныеШтрихкодовУпаковок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПоиска = Новый Структура("Обработан, Ссылка", Ложь, Неопределено);
	Для Каждого Ссылка Из ОбъектыСОграничениемДлины Цикл
		ПараметрыПоиска.Ссылка = Ссылка;
		СтрокиУпаковок = ДанныеШтрихкодовУпаковок.НайтиСтроки(ПараметрыПоиска);
		Если СтрокиУпаковок.Количество() Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Проверяет распределение штрихкодов по строкам товаров. Требуется использование в формате ЭДО с объемно-сортовыми
// кодами маркировки: для каждого такого кода нужна своя строка документа (после применения ограничения могут быть
// распределены не все коды).
//
// Параметры:
//   ДанныеШтрихкодовУпаковок  - См. ЧастичноеСодержимое
//   Отказ                     - Булево - признак что не все коды распределены
//
Процедура ПроверитьСведенияОМаркировке_2019(ДанныеШтрихкодовУпаковок, Отказ) Экспорт

	Если ДанныеШтрихкодовУпаковок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПоиска = Новый Структура("Обработан", Ложь);
	СтрокиУпаковок = ДанныеШтрихкодовУпаковок.НайтиСтроки(ПараметрыПоиска);
	ВыведенныеСообщения = Новый Соответствие;
	Если СтрокиУпаковок.Количество() Тогда
		Для Каждого НеРаспределенныйКод Из СтрокиУпаковок Цикл
			Если Не ЗначениеЗаполнено(НеРаспределенныйКод.Штрихкод) Тогда
				Продолжить;
			КонецЕсли;
			Если ВыведенныеСообщения.Получить(НеРаспределенныйКод.Номенклатура) = Неопределено Тогда
				ВыведенныеСообщения.Вставить(НеРаспределенныйКод.Номенклатура, Новый Соответствие);
			КонецЕсли;
			Если ВыведенныеСообщения.Получить(НеРаспределенныйКод.Номенклатура).Получить(НеРаспределенныйКод.Характеристика) = Неопределено Тогда
				ВыведенныеСообщения.Получить(НеРаспределенныйКод.Номенклатура).Вставить(НеРаспределенныйКод.Характеристика, Истина);
				ОбщегоНазначения.СообщитьПользователю(
					СтрШаблон(НСтр("ru = 'Не удалось распределить коды маркировки для номенклатуры %1 %2 по строкам электронного документа по причине:
					|	В случае отгрузки кодов маркировки в смешанном режиме (объемно-сортовой учет и индивидуальные коды маркировки),
					|	требуется разбить строку в табличной части исходящего документа'"),
					НеРаспределенныйКод.Номенклатура,
					НеРаспределенныйКод.Характеристика));
			КонецЕсли;
			Отказ = Истина;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Проверяет распределение штрихкодов по строкам товаров. Требуется использование в формате ЭДО с объемно-сортовыми
// кодами маркировки: для каждого такого кода нужна своя строка документа (после применения ограничения могут быть
// распределены не все коды).
//
// Параметры:
//   ДанныеШтрихкодовУпаковок  - См. ЧастичноеСодержимое
//   Отказ                     - Булево - признак что не все коды распределены
//
Процедура ПроверитьСведенияОМаркировке_5_02(ДанныеШтрихкодовУпаковок, Отказ) Экспорт

	Если ДанныеШтрихкодовУпаковок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПоиска = Новый Структура("Обработан", Ложь);
	СтрокиУпаковок = ДанныеШтрихкодовУпаковок.НайтиСтроки(ПараметрыПоиска);
	ВыведенныеСообщения = Новый Соответствие;
	Если СтрокиУпаковок.Количество() Тогда
		Для Каждого НеРаспределенныйКод Из СтрокиУпаковок Цикл
			Если Не ЗначениеЗаполнено(НеРаспределенныйКод.Штрихкод) Тогда
				Продолжить;
			КонецЕсли;
			Если ВыведенныеСообщения.Получить(НеРаспределенныйКод.Номенклатура) = Неопределено Тогда
				ВыведенныеСообщения.Вставить(НеРаспределенныйКод.Номенклатура, Новый Соответствие);
			КонецЕсли;
			Если ВыведенныеСообщения.Получить(НеРаспределенныйКод.Номенклатура).Получить(НеРаспределенныйКод.Характеристика) = Неопределено Тогда
				ВыведенныеСообщения.Получить(НеРаспределенныйКод.Номенклатура).Вставить(НеРаспределенныйКод.Характеристика, Истина);
				ОбщегоНазначения.СообщитьПользователю(
					СтрШаблон(НСтр("ru = 'Не удалось распределить коды маркировки для номенклатуры %1 %2 по строкам электронного документа по причине:
					|	В случае отгрузки кодов маркировки в смешанном режиме (объемно-сортовой учет и индивидуальные коды маркировки),
					|	требуется разбить строку в табличной части исходящего документа'"),
					НеРаспределенныйКод.Номенклатура,
					НеРаспределенныйКод.Характеристика));
			КонецЕсли;
			Отказ = Истина;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет в приемнике сведения по маркировке если они есть во входящих данных.
//
// Параметры:
//  Приемник - СтрокаТаблицыЗначений - для заполнения колонки СведенияОМаркировке.
//  Источник - СтрокаТаблицыЗначений - строка документа, содержащая колонки:
//   * Ссылка         - ДокументСсылка - ссылка на документ, для которого заполняются данные,
//   * Номенклатура   - ОпределяемыйТип.Номенклатура - номенклатура в строке документа,
//   * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика в строке,
//   * Серия          - ОпределяемыйТип.СерияНоменклатуры - серия в строке,
//   * Количество     - Число - количество в строке (ограничивает привязанные строки маркировки).
//  ДанныеШтрихкодовУпаковок - Неопределено, ТаблицаЗначений - содержит сведения об упаковках (См. ЧастичноеСодержимое)
Процедура ЗаполнитьСведенияОМаркировке_2019(Приемник, Источник, ДанныеШтрихкодовУпаковок) Экспорт

	СтандартнаяОбработка = Истина;
	ЭлектронноеВзаимодействиеИСМППереопределяемый.ЗаполнитьСведенияОМаркировке_2019(
		Приемник,
		Источник,
		ДанныеШтрихкодовУпаковок,
		СтандартнаяОбработка);
	
	Если СтандартнаяОбработка = Ложь Тогда
		Возврат;
	ИначеЕсли ДанныеШтрихкодовУпаковок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТипСтрока = Новый ОписаниеТипов("Строка");
	
	ТаблицаКодов = Новый ТаблицаЗначений;
	ТаблицаКодов.Колонки.Добавить("Код", ТипСтрока);
	
	КонтрольныеИдентификационныеЗнаки = Новый ТаблицаЗначений;
	КонтрольныеИдентификационныеЗнаки.Колонки.Добавить("Код", ТипСтрока);
	
	ИндивидуальныеУпаковки = Новый ТаблицаЗначений;
	ИндивидуальныеУпаковки.Колонки.Добавить("Код", ТипСтрока);
	
	ТранспортныеУпаковки = Новый ТаблицаЗначений;
	ТранспортныеУпаковки.Колонки.Добавить("Код", ТипСтрока);
	
	ПараметрыПоиска = Новый Структура("Ссылка, Номенклатура");
	ЗаполнитьЗначенияСвойств(ПараметрыПоиска, Источник);
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Источник, "Характеристика") Тогда
		ПараметрыПоиска.Вставить("Характеристика", Источник.Характеристика);
	КонецЕсли;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Источник, "Серия")
			И ЗначениеЗаполнено(Источник.Серия) Тогда
		ПараметрыПоиска.Вставить("Серия", Источник.Серия);
	КонецЕсли;
	ПараметрыПоиска.Вставить("Обработан", Ложь);
	
	ПараметрыПоиска.Вставить("ВидУпаковки", Перечисления.ВидыУпаковокИС.ОбъемноСортовойУчет);
	СтрокиУпаковок = ДанныеШтрихкодовУпаковок.НайтиСтроки(ПараметрыПоиска);
	Если СтрокиУпаковок.Количество() = 0 Тогда
		ПараметрыПоиска.Удалить("ВидУпаковки");
		СтрокиУпаковок = ДанныеШтрихкодовУпаковок.НайтиСтроки(ПараметрыПоиска);
	КонецЕсли;
	
	Для Каждого СтрокаУпаковки Из СтрокиУпаковок Цикл
		
		Если СтрокаУпаковки.ВидПродукции = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха
			Или СтрокаУпаковки.ВидПродукции = Перечисления.ВидыПродукцииИС.Алкогольная Тогда
			Прервать;
		КонецЕсли;
		
		Если СтрокаУпаковки.Обработан Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
			НоваяСтрока = ТранспортныеУпаковки.Добавить();
		ИначеЕсли СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая
			Или СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.ОбъемноСортовойУчет Тогда
			НоваяСтрока = ИндивидуальныеУпаковки.Добавить();
		Иначе
			НоваяСтрока = КонтрольныеИдентификационныеЗнаки.Добавить();
		КонецЕсли;
		
		Если СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.ОбъемноСортовойУчет
				И Не ЗначениеЗаполнено(СтрокаУпаковки.Штрихкод) Тогда
			НоваяСтрока.Код = СформироватьКодОСУИзGTIN(Источник, СтрокаУпаковки);
		Иначе
			НоваяСтрока.Код = СтрокаУпаковки.ЗначениеШтрихкода;
			СтрокаУпаковки.Обработан = Истина;
		КонецЕсли;
		
		Если СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.ОбъемноСортовойУчет Тогда
			Прервать;
		КонецЕсли;
		
		ОтметитьОбработанныеСтрокиДляМультитоварныхУпаковок(ДанныеШтрихкодовУпаковок, ПараметрыПоиска, СтрокаУпаковки);
		
	КонецЦикла;
	
	СведенияОМаркировке = Новый Структура;
	Если КонтрольныеИдентификационныеЗнаки.Количество() Тогда
		СведенияОМаркировке.Вставить("КонтрольныеИдентификационныеЗнаки", КонтрольныеИдентификационныеЗнаки);
	КонецЕсли;
	Если ИндивидуальныеУпаковки.Количество() Тогда
		СведенияОМаркировке.Вставить("ИндивидуальныеУпаковки",            ИндивидуальныеУпаковки);
	КонецЕсли;
	Если ТранспортныеУпаковки.Количество() Тогда
		СведенияОМаркировке.Вставить("ТранспортныеУпаковки",              ТранспортныеУпаковки);
	КонецЕсли;
	
	Приемник.СведенияОМаркировке = СведенияОМаркировке;
	
КонецПроцедуры

// Заполняет в приемнике сведения по маркировке если они есть во входящих данных.
//
// Параметры:
//  Приемник - СтрокаТаблицыЗначений - для заполнения колонки СведенияОМаркировке.
//  Источник - СтрокаТаблицыЗначений - строка документа, содержащая колонки:
//   * Ссылка         - ДокументСсылка - ссылка на документ, для которого заполняются данные,
//   * Номенклатура   - ОпределяемыйТип.Номенклатура - номенклатура в строке документа,
//   * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика в строке,
//   * Серия          - ОпределяемыйТип.СерияНоменклатуры - серия в строке,
//   * Количество     - Число - количество в строке (ограничивает привязанные строки маркировки).
//  ДанныеШтрихкодовУпаковок - Неопределено, ТаблицаЗначений - содержит сведения об упаковках (См. ЧастичноеСодержимое)
Процедура ЗаполнитьСведенияОМаркировке_5_02(Приемник, Источник, ДанныеШтрихкодовУпаковок) Экспорт

	СтандартнаяОбработка = Истина;
	ЭлектронноеВзаимодействиеИСМППереопределяемый.ЗаполнитьСведенияОМаркировке_5_02(
		Приемник,
		Источник,
		ДанныеШтрихкодовУпаковок,
		СтандартнаяОбработка);
	
	Если СтандартнаяОбработка = Ложь Тогда
		Возврат;
	ИначеЕсли ДанныеШтрихкодовУпаковок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТипСтрока = Новый ОписаниеТипов("Строка");
	ТипЧисло  = Новый ОписаниеТипов("Число");
	
	СведенияОМаркировке = Новый ТаблицаЗначений;
	СведенияОМаркировке.Колонки.Добавить("ИдентификаторУпаковки",  ТипСтрока);
	СведенияОМаркировке.Колонки.Добавить("Количество",             ТипЧисло);
	СведенияОМаркировке.Колонки.Добавить("Партия",                 ТипСтрока);
	СведенияОМаркировке.Колонки.Добавить("Идентификатор",          Неопределено);
	
	ПараметрыПоиска = Новый Структура("Ссылка, Номенклатура");
	ЗаполнитьЗначенияСвойств(ПараметрыПоиска, Источник);
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Источник, "Характеристика") Тогда
		ПараметрыПоиска.Вставить("Характеристика", Источник.Характеристика);
	КонецЕсли;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Источник, "Серия")
			И ЗначениеЗаполнено(Источник.Серия) Тогда
		ПараметрыПоиска.Вставить("Серия", Источник.Серия);
	КонецЕсли;
	ПараметрыПоиска.Вставить("Обработан", Ложь);
	
	ПараметрыПоиска.Вставить("ВидУпаковки", Перечисления.ВидыУпаковокИС.ОбъемноСортовойУчет);
	СтрокиУпаковок = ДанныеШтрихкодовУпаковок.НайтиСтроки(ПараметрыПоиска);
	Если СтрокиУпаковок.Количество() = 0 Тогда
		ПараметрыПоиска.Удалить("ВидУпаковки");
		СтрокиУпаковок = ДанныеШтрихкодовУпаковок.НайтиСтроки(ПараметрыПоиска);
	КонецЕсли;
	
	КонтрольныеИдентификационныеЗнаки = Новый ТаблицаЗначений;
	КонтрольныеИдентификационныеЗнаки.Колонки.Добавить("КонтрольныйЗнак", ТипСтрока);
	
	ИндивидуальныеУпаковки = Новый ТаблицаЗначений;
	ИндивидуальныеУпаковки.Колонки.Добавить("ИдентификаторУпаковки", ТипСтрока);
	
	Для Каждого СтрокаУпаковки Из СтрокиУпаковок Цикл
		
		Если СтрокаУпаковки.ВидПродукции = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха
			Или СтрокаУпаковки.ВидПродукции = Перечисления.ВидыПродукцииИС.Алкогольная Тогда
			Прервать;
		КонецЕсли;
		
		Если СтрокаУпаковки.Обработан Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
			
			НоваяСтрока = СведенияОМаркировке.Добавить();
			
			НоваяСтрока.ИдентификаторУпаковки = СтрокаУпаковки.ЗначениеШтрихкода;
			СтрокаУпаковки.Обработан = Истина;
			
		ИначеЕсли СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая Тогда
			
			НоваяСтрокаИндивидуальныхУпаковок = ИндивидуальныеУпаковки.Добавить();
			НоваяСтрокаИндивидуальныхУпаковок.ИдентификаторУпаковки = СтрокаУпаковки.ЗначениеШтрихкода;
			
			СтрокаУпаковки.Обработан = Истина;
			
		ИначеЕсли СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.ОбъемноСортовойУчет Тогда
			
			НоваяСтрока = СведенияОМаркировке.Добавить();
			
			НоваяСтрока.Количество = СформироватьКоличествоПоОСУ(Источник, СтрокаУпаковки);
			Приемник.ГТИН = СтрокаУпаковки.ЗначениеШтрихкода;
			
			НоваяСтрока.Идентификатор = Новый Структура("ИдентификаторыУпаковок", ИндивидуальныеУпаковки);
			
		Иначе
			
			НоваяСтрока = СведенияОМаркировке.Добавить();
			
			НоваяСтрокаКИЗ = КонтрольныеИдентификационныеЗнаки.Добавить();
			НоваяСтрокаКИЗ.КонтрольныйЗнак = СтрокаУпаковки.ЗначениеШтрихкода;
			
			СтрокаУпаковки.Обработан = Истина;
			
		КонецЕсли;
		
		Если СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.ОбъемноСортовойУчет Тогда
			Прервать;
		КонецЕсли;
		
		ОтметитьОбработанныеСтрокиДляМультитоварныхУпаковок(ДанныеШтрихкодовУпаковок, ПараметрыПоиска, СтрокаУпаковки);
		
	КонецЦикла; 
	
	Если КонтрольныеИдентификационныеЗнаки.Количество() Тогда
		
		НоваяСтрока = СведенияОМаркировке.Добавить();
		НоваяСтрока.Идентификатор = Новый Структура("КонтрольныеЗнаки", КонтрольныеИдентификационныеЗнаки);
		
	КонецЕсли;
	
	Если ИндивидуальныеУпаковки.Количество() Тогда
		
		НоваяСтрока = СведенияОМаркировке.Добавить();
		НоваяСтрока.Идентификатор = Новый Структура("ИдентификаторыУпаковок", ИндивидуальныеУпаковки);
		
	КонецЕсли;
	
	Приемник.СведенияОМаркировке = СведенияОМаркировке;
	
КонецПроцедуры

// Заполнение признаков наличия определенных маркируемых товаров в дереве: ЕстьАлкогольнаяПродукция, ЕстьТабачнаяПродукция.
// 
// Параметры:
//  ДеревоДанных - ДеревоЗначений - дерево электронного документа.
//  ДанныеШтрихкодовУпаковок - Неопределено, ТаблицаЗначений - содержит сведения об упаковках (См. ЧастичноеСодержимое)
Процедура ЗаполнениеПоказателейМаркировкиВДереве_5_02(ДеревоДанных, ДанныеШтрихкодовУпаковок) Экспорт
	
	Если ДанныеШтрихкодовУпаковок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьТабачнаяПродукция    = Ложь;
	ЕстьАлкогольнаяПродукция = Ложь;
	
	Для Каждого СтрокаДанныхШтрихкодов Из ДанныеШтрихкодовУпаковок Цикл
		
		Если ОбщегоНазначенияИСКлиентСервер.ЭтоПродукцияМОТП(СтрокаДанныхШтрихкодов.ВидПродукции) Тогда
			ЕстьТабачнаяПродукция = Истина;
		ИначеЕсли СтрокаДанныхШтрихкодов.ВидПродукции = Перечисления.ВидыПродукцииИС.Пиво
			Или СтрокаДанныхШтрихкодов.ВидПродукции = Перечисления.ВидыПродукцииИС.ПивоВПотребительскихУпаковках Тогда
			ЕстьАлкогольнаяПродукция = Истина;
		Иначе
			Продолжить;
		КонецЕсли;
		
		Если ЕстьТабачнаяПродукция И ЕстьАлкогольнаяПродукция Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЕстьТабачнаяПродукция Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ЕстьТабачнаяПродукция", ЕстьТабачнаяПродукция);
	КонецЕсли;
	
	Если ЕстьАлкогольнаяПродукция Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ЕстьАлкогольнаяПродукция", ЕстьАлкогольнаяПродукция);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет в приемнике сведения по маркировке если они есть во входящих данных.
//
// Параметры:
//  Приемник - СтрокаТаблицыЗначений - для заполнения колонки Маркировка.
//  Источник - СтрокаТаблицыЗначений - строка документа, содержащая колонки:
//   * Ссылка         - ДокументСсылка - ссылка на документ, для которого заполняются данные,
//   * Номенклатура   - ОпределяемыйТип.Номенклатура - номенклатура в строке документа,
//   * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика в строке,
//   * Серия          - ОпределяемыйТип.СерияНоменклатуры - серия в строке,
//   * Количество     - Число - количество в строке (ограничивает привязанные строки маркировки). Имя колонки определяется в 4м параметре.
//  ДанныеШтрихкодовУпаковок - Неопределено, ТаблицаЗначений - содержит сведения об упаковках (См. ЧастичноеСодержимоеТОРГ2)
//  ПолеКоличество - Строка - имя поля количество строки источника
//  ОтборПоТипуРасхождений - Булево - истина, если требуется отбирать коды маркировки с учетом типа расхождения
//  ТипРасхождения - ОпределяемыйТип.ТипРасхожденияИСМП - тип расхождения.
Процедура ЗаполнитьСведенияОМаркировкеАктОРасхождениях_2019(Приемник, Источник, ДанныеШтрихкодовУпаковок,
	ПолеКоличество, ОтборПоТипуРасхождений = Ложь, ТипРасхождения = Неопределено) Экспорт

	СтандартнаяОбработка = Истина;
	ЭлектронноеВзаимодействиеИСМППереопределяемый.ЗаполнитьСведенияОМаркировкеАктОРасхождениях_2019(
		Приемник,
		Источник,
		ДанныеШтрихкодовУпаковок,
		ПолеКоличество,
		ОтборПоТипуРасхождений,
		ТипРасхождения,
		СтандартнаяОбработка);
	
	Если СтандартнаяОбработка = Ложь Тогда
		Возврат;
	ИначеЕсли ДанныеШтрихкодовУпаковок = Неопределено 
		Или ДанныеШтрихкодовУпаковок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаКодов = Новый ТаблицаЗначений;
	ТаблицаКодов.Колонки.Добавить("Код");
	
	КонтрольныеИдентификационныеЗнаки = Новый ТаблицаЗначений;
	КонтрольныеИдентификационныеЗнаки.Колонки.Добавить("Код");
	
	ИндивидуальныеУпаковки = Новый ТаблицаЗначений;
	ИндивидуальныеУпаковки.Колонки.Добавить("Код");
	
	ТранспортныеУпаковки = Новый ТаблицаЗначений;
	ТранспортныеУпаковки.Колонки.Добавить("Код");
	
	ПараметрыПоиска = Новый Структура("Ссылка, Номенклатура");
	ЗаполнитьЗначенияСвойств(ПараметрыПоиска, Источник);
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Источник, "Характеристика") Тогда
		ПараметрыПоиска.Вставить("Характеристика", Источник.Характеристика);
	КонецЕсли;
	Если ОтборПоТипуРасхождений Тогда
		ПараметрыПоиска.Вставить("ТипРасхождения", ТипРасхождения);
	КонецЕсли;

	ПараметрыПоиска.Вставить("Обработан", Ложь);
	
	СтрокиУпаковок = ДанныеШтрихкодовУпаковок.НайтиСтроки(ПараметрыПоиска);
	Количество = Источник[ПолеКоличество];
	УчитыватьКоличество = Истина;
	Если ОтборПоТипуРасхождений И Количество = 0 Тогда
		УчитыватьКоличество = Ложь; 
	КонецЕсли;
	Для Каждого СтрокаУпаковки Из СтрокиУпаковок Цикл
		Если СтрокаУпаковки.ВидПродукции = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха
			Или СтрокаУпаковки.ВидПродукции = Перечисления.ВидыПродукцииИС.Алкогольная Тогда
			Прервать;
		ИначеЕсли УчитыватьКоличество И (Количество <= 0) Тогда
			Прервать;
		ИначеЕсли СтрокаУпаковки.Обработан Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
			НоваяСтрока = ТранспортныеУпаковки.Добавить();
		ИначеЕсли СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая
			Или СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.ОбъемноСортовойУчет Тогда
			НоваяСтрока = ИндивидуальныеУпаковки.Добавить();
		Иначе
			НоваяСтрока = КонтрольныеИдентификационныеЗнаки.Добавить();
		КонецЕсли;
		
		НоваяСтрока.Код = СтрокаУпаковки.ЗначениеШтрихКода;
		
		СтрокаУпаковки.Обработан = Истина;
		
		Количество = Количество - 1;
	КонецЦикла;
	
	СведенияОМаркировке = Новый Структура;
	Если КонтрольныеИдентификационныеЗнаки.Количество() Тогда
		СведенияОМаркировке.Вставить("КонтрольныеИдентификационныеЗнаки", КонтрольныеИдентификационныеЗнаки);
	КонецЕсли;
	Если ИндивидуальныеУпаковки.Количество() Тогда
		СведенияОМаркировке.Вставить("ИндивидуальныеУпаковки",            ИндивидуальныеУпаковки);
	КонецЕсли;
	Если ТранспортныеУпаковки.Количество() Тогда
		СведенияОМаркировке.Вставить("ТранспортныеУпаковки",              ТранспортныеУпаковки);
	КонецЕсли;
	
	Приемник.Вставить("Маркировка", СведенияОМаркировке);

КонецПроцедуры

// Заполняет в приемнике сведения по маркировке если они есть во входящих данных.
//
// Параметры:
//  Приемник - СтрокаТаблицыЗначений - для заполнения колонки СведенияОМаркировке.
//  Источник - СтрокаТаблицыЗначений - строка документа, содержащая колонки:
//   * Ссылка         - ДокументСсылка - ссылка на документ, для которого заполняются данные,
//   * Номенклатура   - ОпределяемыйТип.Номенклатура - номенклатура в строке документа,
//   * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика в строке,
//   * Серия          - ОпределяемыйТип.СерияНоменклатуры - серия в строке,
//   * КоличествоДо   - Число - количество в строке до корректировки (ограничивает привязанные строки маркировки),
//   * Количество     - Число - количество в строке после корректировки (ограничивает привязанные строки маркировки).
//  ДанныеШтрихкодовУпаковокДо - ТаблицаЗначений - содержит сведения об упаковках (См. ЧастичноеСодержимое)
//  ДанныеШтрихкодовУпаковокПосле  - ТаблицаЗначений - содержит сведения об упаковках (См. ЧастичноеСодержимое)
Процедура ЗаполнитьСведенияОМаркировкеУКД(Приемник, Источник, ДанныеШтрихкодовУпаковокДо, ДанныеШтрихкодовУпаковокПосле) Экспорт

	СтандартнаяОбработка = Истина;
	ЭлектронноеВзаимодействиеИСМППереопределяемый.ЗаполнитьСведенияОМаркировкеУКД(
		Приемник,
		Источник,
		ДанныеШтрихкодовУпаковокДо,
		ДанныеШтрихкодовУпаковокПосле,
		СтандартнаяОбработка);
	
	Если СтандартнаяОбработка = Ложь Тогда
		Возврат;
	ИначеЕсли ДанныеШтрихкодовУпаковокДо = Неопределено 
		И ДанныеШтрихкодовУпаковокПосле = Неопределено  Тогда 
		Возврат;
	КонецЕсли;
	
	КонтрольныеИдентификационныеЗнакиДо = Новый ТаблицаЗначений;
	КонтрольныеИдентификационныеЗнакиДо.Колонки.Добавить("Код");
	
	ТранспортныеУпаковкиДо = Новый ТаблицаЗначений;
	ТранспортныеУпаковкиДо.Колонки.Добавить("Код");
	
	КонтрольныеИдентификационныеЗнакиПосле = Новый ТаблицаЗначений;
	КонтрольныеИдентификационныеЗнакиПосле.Колонки.Добавить("Код");
	
	ТранспортныеУпаковкиПосле = Новый ТаблицаЗначений;
	ТранспортныеУпаковкиПосле.Колонки.Добавить("Код");
	
	ПараметрыПоиска = Новый Структура("Ссылка, Номенклатура");
	ЗаполнитьЗначенияСвойств(ПараметрыПоиска, Источник);
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Источник, "Характеристика") Тогда
		ПараметрыПоиска.Вставить("Характеристика", Источник.Характеристика);
	КонецЕсли;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Источник, "Серия") Тогда
		ПараметрыПоиска.Вставить("Серия", Источник.Серия);
	КонецЕсли;
	ПараметрыПоиска.Вставить("Обработан", Ложь);
	
	СтрокиУпаковокДо = ДанныеШтрихкодовУпаковокДо.НайтиСтроки(ПараметрыПоиска);
	Для Каждого СтрокаУпаковки Из СтрокиУпаковокДо Цикл
		Если СтрокаУпаковки.Обработан Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
			НоваяСтрока = ТранспортныеУпаковкиДо.Добавить();
		Иначе //групповая и индивидульная
			НоваяСтрока = КонтрольныеИдентификационныеЗнакиДо.Добавить();
		КонецЕсли;
		
		НоваяСтрока.Код = СтрокаУпаковки.ЗначениеШтрихКода;
		
		СтрокаУпаковки.Обработан = Истина;
		ОтметитьОбработанныеСтрокиДляМультитоварныхУпаковок(ДанныеШтрихкодовУпаковокДо, ПараметрыПоиска, СтрокаУпаковки);
	КонецЦикла;
	
	СтрокиУпаковокПосле = ДанныеШтрихкодовУпаковокПосле.НайтиСтроки(ПараметрыПоиска);
	Для Каждого СтрокаУпаковки Из СтрокиУпаковокПосле Цикл
		Если СтрокаУпаковки.Обработан Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
			НоваяСтрока = ТранспортныеУпаковкиПосле.Добавить();
		Иначе //групповая и индивидульная
			НоваяСтрока = КонтрольныеИдентификационныеЗнакиПосле.Добавить();
		КонецЕсли;
		
		НоваяСтрока.Код = СтрокаУпаковки.ЗначениеШтрихКода;
		
		СтрокаУпаковки.Обработан = Истина;
		ОтметитьОбработанныеСтрокиДляМультитоварныхУпаковок(ДанныеШтрихкодовУпаковокПосле, ПараметрыПоиска, СтрокаУпаковки);
	КонецЦикла;

	Если КонтрольныеИдентификационныеЗнакиДо.Количество() = 0 и КонтрольныеИдентификационныеЗнакиПосле.Количество() > 0 Тогда
		НоваяСтрока = КонтрольныеИдентификационныеЗнакиДо.Добавить();
		НоваяСтрока.Код = "-";
	ИначеЕсли КонтрольныеИдентификационныеЗнакиДо.Количество() > 0 и КонтрольныеИдентификационныеЗнакиПосле.Количество() = 0 Тогда
		НоваяСтрока = КонтрольныеИдентификационныеЗнакиПосле.Добавить();
		НоваяСтрока.Код = "-";
	КонецЕсли;
	
	Если ТранспортныеУпаковкиДо.Количество() = 0 и ТранспортныеУпаковкиПосле.Количество() > 0 Тогда
		НоваяСтрока = ТранспортныеУпаковкиДо.Добавить();
		НоваяСтрока.Код = "-";
	ИначеЕсли ТранспортныеУпаковкиДо.Количество() > 0 и ТранспортныеУпаковкиПосле.Количество() = 0 Тогда
		НоваяСтрока = ТранспортныеУпаковкиПосле.Добавить();
		НоваяСтрока.Код = "-";
	КонецЕсли;

	СведенияОМаркировке = Новый Структура;
	Если КонтрольныеИдентификационныеЗнакиДо.Количество() Тогда
		СведенияОМаркировке.Вставить("КонтрольныеИдентификационныеЗнаки", КонтрольныеИдентификационныеЗнакиДо);
	КонецЕсли;
	Если ТранспортныеУпаковкиДо.Количество() Тогда
		СведенияОМаркировке.Вставить("ТранспортныеУпаковки",              ТранспортныеУпаковкиДо);
	КонецЕсли;
	Приемник.СведенияОМаркировкеДо = СведенияОМаркировке;
	
	СведенияОМаркировке = Новый Структура;
	Если КонтрольныеИдентификационныеЗнакиПосле.Количество() Тогда
		СведенияОМаркировке.Вставить("КонтрольныеИдентификационныеЗнаки", КонтрольныеИдентификационныеЗнакиПосле);
	КонецЕсли;
	Если ТранспортныеУпаковкиПосле.Количество() Тогда
		СведенияОМаркировке.Вставить("ТранспортныеУпаковки",              ТранспортныеУпаковкиПосле);
	КонецЕсли;
	Приемник.СведенияОМаркировкеПосле = СведенияОМаркировке;
	
КонецПроцедуры

// Заполняет в приемнике сведения по маркировке если они есть во входящих данных.
//
// Параметры:
//  Приемник - СтрокаТаблицыЗначений - для заполнения колонки СведенияОМаркировке.
//  Источник - СтрокаТаблицыЗначений - строка документа, содержащая колонки:
//   * Ссылка         - ДокументСсылка - ссылка на документ, для которого заполняются данные,
//   * Номенклатура   - ОпределяемыйТип.Номенклатура - номенклатура в строке документа,
//   * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика в строке,
//   * Серия          - ОпределяемыйТип.СерияНоменклатуры - серия в строке,
//   * КоличествоДо   - Число - количество в строке до корректировки (ограничивает привязанные строки маркировки),
//   * Количество     - Число - количество в строке после корректировки (ограничивает привязанные строки маркировки).
//  ДанныеШтрихкодовУпаковокДо - ТаблицаЗначений - содержит сведения об упаковках (См. ЧастичноеСодержимое)
//  ДанныеШтрихкодовУпаковокПосле  - ТаблицаЗначений - содержит сведения об упаковках (См. ЧастичноеСодержимое)
Процедура ЗаполнитьСведенияОМаркировкеУКД2020(Приемник, Источник, ДанныеШтрихкодовУпаковокДо, ДанныеШтрихкодовУпаковокПосле) Экспорт

	СтандартнаяОбработка = Истина;
	ЭлектронноеВзаимодействиеИСМППереопределяемый.ЗаполнитьСведенияОМаркировкеУКД2020(
		Приемник,
		Источник,
		ДанныеШтрихкодовУпаковокДо,
		ДанныеШтрихкодовУпаковокПосле,
		СтандартнаяОбработка);
	
	Если СтандартнаяОбработка = Ложь Тогда
		Возврат;
	ИначеЕсли Не ЗначениеЗаполнено(ДанныеШтрихкодовУпаковокДо)
		И Не ЗначениеЗаполнено(ДанныеШтрихкодовУпаковокПосле) Тогда 
		Возврат;
	КонецЕсли;
	
	КонтрольныеИдентификационныеЗнакиДо = Новый ТаблицаЗначений;
	КонтрольныеИдентификационныеЗнакиДо.Колонки.Добавить("Код");
	
	ТранспортныеУпаковкиДо = Новый ТаблицаЗначений;
	ТранспортныеУпаковкиДо.Колонки.Добавить("Код");
	
	ИндивидуальныеУпаковкиДо = Новый ТаблицаЗначений;
	ИндивидуальныеУпаковкиДо.Колонки.Добавить("Код");

	КонтрольныеИдентификационныеЗнакиПосле = Новый ТаблицаЗначений;
	КонтрольныеИдентификационныеЗнакиПосле.Колонки.Добавить("Код");
	
	ТранспортныеУпаковкиПосле = Новый ТаблицаЗначений;
	ТранспортныеУпаковкиПосле.Колонки.Добавить("Код");
	
	ИндивидуальныеУпаковкиПосле = Новый ТаблицаЗначений;
	ИндивидуальныеУпаковкиПосле.Колонки.Добавить("Код");
	
	ПараметрыПоиска = Новый Структура("Ссылка, Номенклатура");
	ЗаполнитьЗначенияСвойств(ПараметрыПоиска, Источник);
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Источник, "Характеристика") Тогда
		ПараметрыПоиска.Вставить("Характеристика", Источник.Характеристика);
	КонецЕсли;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Источник, "Серия") Тогда
		ПараметрыПоиска.Вставить("Серия", Источник.Серия);
	КонецЕсли;
	ПараметрыПоиска.Вставить("Обработан", Ложь);
	
	КодыОСУДо = Новый Массив;

	СтрокиУпаковокДо = ДанныеШтрихкодовУпаковокДо.НайтиСтроки(ПараметрыПоиска);
	Для Каждого СтрокаУпаковки Из СтрокиУпаковокДо Цикл
		Если СтрокаУпаковки.Обработан Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
			НоваяСтрока = ТранспортныеУпаковкиДо.Добавить();
		ИначеЕсли СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Потребительская Тогда
			НоваяСтрока = КонтрольныеИдентификационныеЗнакиДо.Добавить();
		ИначеЕсли СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.ОбъемноСортовойУчет Тогда
			КодыОСУДо.Добавить(СтрокаУпаковки);
			НоваяСтрока = ИндивидуальныеУпаковкиДо.Добавить();
		Иначе //групповая
			НоваяСтрока = ИндивидуальныеУпаковкиДо.Добавить();
		КонецЕсли;
		
		Если СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.ОбъемноСортовойУчет
				И Не ЗначениеЗаполнено(СтрокаУпаковки.Штрихкод) Тогда
			НоваяСтрока.Код = СформироватьКодОСУИзGTIN(Источник, СтрокаУпаковки, "КоличествоДо");
		Иначе
			НоваяСтрока.Код = СтрокаУпаковки.ЗначениеШтрихкода;
			СтрокаУпаковки.Обработан = Истина;
		КонецЕсли;
		
		ОтметитьОбработанныеСтрокиДляМультитоварныхУпаковок(ДанныеШтрихкодовУпаковокДо, ПараметрыПоиска, СтрокаУпаковки);
	КонецЦикла;
	
	КодыОСУПосле = Новый Массив;

	СтрокиУпаковокПосле = ДанныеШтрихкодовУпаковокПосле.НайтиСтроки(ПараметрыПоиска);
	Для Каждого СтрокаУпаковки Из СтрокиУпаковокПосле Цикл
		Если СтрокаУпаковки.Обработан Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
			НоваяСтрока = ТранспортныеУпаковкиПосле.Добавить();
		ИначеЕсли СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Потребительская Тогда
			НоваяСтрока = КонтрольныеИдентификационныеЗнакиПосле.Добавить();
		ИначеЕсли СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.ОбъемноСортовойУчет Тогда
			КодыОСУПосле.Добавить(СтрокаУпаковки);
			НоваяСтрока = ИндивидуальныеУпаковкиПосле.Добавить();
		Иначе //групповая
			НоваяСтрока = ИндивидуальныеУпаковкиПосле.Добавить();
		КонецЕсли;
		
		Если СтрокаУпаковки.ВидУпаковки = Перечисления.ВидыУпаковокИС.ОбъемноСортовойУчет
				И Не ЗначениеЗаполнено(СтрокаУпаковки.Штрихкод) Тогда
			НоваяСтрока.Код = СформироватьКодОСУИзGTIN(Источник, СтрокаУпаковки);
		Иначе
			НоваяСтрока.Код = СтрокаУпаковки.ЗначениеШтрихКода;
			СтрокаУпаковки.Обработан = Истина;
		КонецЕсли;
		
		ОтметитьОбработанныеСтрокиДляМультитоварныхУпаковок(ДанныеШтрихкодовУпаковокПосле, ПараметрыПоиска, СтрокаУпаковки);
	КонецЦикла;

	Если КодыОСУДо.Количество() И Не КодыОСУПосле.Количество() И Приемник.Количество = 0 Тогда

		// Полный возврат по ОСУ: кол-во товара переданного получателю будет равно нулю (02+gtin+37+0).
		Для Каждого СтрокаУпаковки Из КодыОСУДо Цикл

			РезультатРазбора = ШтрихкодированиеОбщегоНазначенияИС.НоваяСтруктураОбработкиШтрихкода(
				СформироватьКодОСУИзGTIN(Источник, СтрокаУпаковки, "КоличествоДо"),
				СтрокаУпаковки.ВидПродукции, ЭлектронноеВзаимодействиеИСМППовтИсп.ПараметрыРазбора());

			Если РезультатРазбора <> Неопределено
				И РезультатРазбора.Свойство("ДанныеРазбора") И РезультатРазбора.ДанныеРазбора <> Неопределено
				И РезультатРазбора.ДанныеРазбора.СоставКодаМаркировки.Свойство("GTIN") Тогда

				СтруктураДанных = Новый Структура;
				СтруктураДанных.Вставить("ЗначениеШтрихКода", РезультатРазбора.ДанныеРазбора.СоставКодаМаркировки.GTIN);
				СтруктураДанных.Вставить("КоличествоУпаковок", 0);
				
				ШтрихкодированиеИСМПСлужебный.ЗаменитьЗначенияШтрихкодаНаОбъемноСортовойУчет(
					ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтруктураДанных),
					"ЗначениеШтрихкода", "КоличествоУпаковок");

				НормализованныйКодМаркировки = КодМаркировкиДляПередачиУПД(
					СтруктураДанных.ЗначениеШтрихкода, СтрокаУпаковки.ВидПродукции);

				НоваяСтрока = ИндивидуальныеУпаковкиПосле.Добавить();
				НоваяСтрока.Код = НормализованныйКодМаркировки;

			КонецЕсли;

		КонецЦикла;

	КонецЕсли;

	СведенияОМаркировке = Новый Структура;
	Если КонтрольныеИдентификационныеЗнакиДо.Количество() Тогда
		СведенияОМаркировке.Вставить("КонтрольныеИдентификационныеЗнаки", КонтрольныеИдентификационныеЗнакиДо);
	КонецЕсли;
	Если ТранспортныеУпаковкиДо.Количество() Тогда
		СведенияОМаркировке.Вставить("ТранспортныеУпаковки", ТранспортныеУпаковкиДо);
	КонецЕсли;
	Если ИндивидуальныеУпаковкиДо.Количество() Тогда
		СведенияОМаркировке.Вставить("ИндивидуальныеУпаковки", ИндивидуальныеУпаковкиДо);
	КонецЕсли;
	Приемник.СведенияОМаркировкеДо = СведенияОМаркировке;
	
	СведенияОМаркировке = Новый Структура;
	Если КонтрольныеИдентификационныеЗнакиПосле.Количество() Тогда
		СведенияОМаркировке.Вставить("КонтрольныеИдентификационныеЗнаки", КонтрольныеИдентификационныеЗнакиПосле);
	КонецЕсли;
	Если ТранспортныеУпаковкиПосле.Количество() Тогда
		СведенияОМаркировке.Вставить("ТранспортныеУпаковки", ТранспортныеУпаковкиПосле);
	КонецЕсли;
	Если ИндивидуальныеУпаковкиПосле.Количество() Тогда
		СведенияОМаркировке.Вставить("ИндивидуальныеУпаковки", ИндивидуальныеУпаковкиПосле);
	КонецЕсли;
	Приемник.СведенияОМаркировкеПосле = СведенияОМаркировке;
	
КонецПроцедуры

// Рассчитывает по таблице расхождений количество бракованных кодов
// 
// Параметры:
//  ВыборкаТовары - ВыборкаИзРезультатаЗапроса - данные выборки по строке товара.
//  ТаблицаУпаковкиРасхождения - См. ЧастичноеСодержимоеТОРГ2.
//
// Возвращаемое значение:
//  Число - количество, доступное к оформлению как брак по расхождениям.
//
Функция ПолучитьКоличествоОформитьРасхожденияКакБрак(ВыборкаТовары, ТаблицаУпаковкиРасхождения) Экспорт
	
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Номенклатура", ВыборкаТовары.Номенклатура);
	ПараметрыОтбора.Вставить("Характеристика", ВыборкаТовары.Характеристика);
	
	Количество = 0;

	ПараметрыОтбора.Вставить("Обработан", Ложь);
	ПараметрыОтбора.Вставить("ТипРасхождения", ИнтеграцияИСМП.ТипРасхожденияИСМПБрак());
	
	СтрокиРасхожденийБрак = ТаблицаУпаковкиРасхождения.НайтиСтроки(ПараметрыОтбора);
	
	Для Каждого СтрокаРасхождения Из СтрокиРасхожденийБрак Цикл
		Количество = Количество + СтрокаРасхождения.Количество;
	КонецЦикла;
	Возврат Количество;
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеПрикладныхДокументовНаОснованииДокументовЭДО

// Возвращает коды маркировки для строки товара из дерева электронного документа ЭДО.
//
// Параметры:
//	СведенияОТоваре - СтрокаДереваЗначений - строка товаров дерева электронного документа ЭДО.
//
// Пример использования:
//	СведенияОТоварах = ДеревоДанных.Строки.Найти("СведенияОТоварах", "ПолныйПуть");
//	Для Каждого СведенияОТоваре Из СведенияОТоварах.Строки Цикл
//		ШтрихкодыУпаковок = ЭлектронноеВзаимодействиеИСМП.ШтрихкодыУпаковокИзСтрокиДереваЭДО(СведенияОТоваре);
//	КонецЦикла;.
//
Функция ШтрихкодыУпаковокИзСтрокиДереваЭДО(СведенияОТоваре)
	
	ШтрихкодыУпаковок = Новый Массив;
	
	КодыУпаковок = СведенияОТоваре.Строки.Найти(
		"СведенияОТоварах.НомерСтроки.СведенияОМаркировке.КодыИндивидуальныхУпаковок", "ПолныйПуть", Истина);
	
	Если КодыУпаковок <> Неопределено И ЗначениеЗаполнено(КодыУпаковок.Значение) Тогда
		Для Каждого СтрокаКодаУпаковки Из КодыУпаковок.Строки Цикл
			
			ШтрихкодУпаковки = ЗначениеРеквизитаВДереве(СтрокаКодаУпаковки,
				"СведенияОТоварах.НомерСтроки.СведенияОМаркировке.КодыИндивидуальныхУпаковок.НомерСтроки.КодУпаковки");
			ШтрихкодыУпаковок.Добавить(ШтрихкодУпаковки);
			
		КонецЦикла;
	КонецЕсли;
	
	Возврат ШтрихкодыУпаковок;
	
КонецФункции

// Возвращает коды маркировки для строки товара из дерева электронного документа ЭДО.
//
// Параметры:
//	СведенияОТоваре - СтрокаДереваЗначений - строка товаров дерева электронного документа ЭДО.
//
// Пример использования:
//	СведенияОТоварах = ДеревоДанных.Строки.Найти("СведенияОТоварах", "ПолныйПуть");
//	Для Каждого СведенияОТоваре Из СведенияОТоварах.Строки Цикл
//		ШтрихкодыУпаковок = ЭлектронноеВзаимодействиеИСМП.ШтрихкодыУпаковокИзСтрокиДереваЭДО(СведенияОТоваре);
//	КонецЦикла;.
//
// Возвращаемое значение:
//  Массив из Строка - массив штрихкодов упаковок
Функция ШтрихкодыУпаковокИзСтрокиДереваЭДО_2019(СведенияОТоваре)
	
	ШтрихкодыУпаковок = Новый Массив;
	
	КодыУпаковок = СведенияОТоваре.Строки.Найти(
		"СведенияОТоварах.НомерСтроки.СведенияОМаркировке", "ПолныйПуть", Истина);
	
	Если КодыУпаковок <> Неопределено И ЗначениеЗаполнено(КодыУпаковок.Значение) Тогда
		
		КонтрольныеИдентификационныеЗнаки = КодыУпаковок.Строки.Найти(
			"СведенияОТоварах.НомерСтроки.СведенияОМаркировке.КонтрольныеИдентификационныеЗнаки", "ПолныйПуть", Истина);
		Для Каждого СтрокаКодаУпаковки Из КонтрольныеИдентификационныеЗнаки.Строки Цикл
			ШтрихкодУпаковки = ЗначениеРеквизитаВДереве(СтрокаКодаУпаковки,
				"СведенияОТоварах.НомерСтроки.СведенияОМаркировке.КонтрольныеИдентификационныеЗнаки.НомерСтроки.Код");
			Если ЗначениеЗаполнено(ШтрихкодУпаковки) Тогда 
				ШтрихкодыУпаковок.Добавить(ШтрихкодУпаковки);
			КонецЕсли;
		КонецЦикла;
		
		ИндивидуальныеУпаковки = КодыУпаковок.Строки.Найти(
			"СведенияОТоварах.НомерСтроки.СведенияОМаркировке.ИндивидуальныеУпаковки", "ПолныйПуть", Истина);
		Для Каждого СтрокаКодаУпаковки Из ИндивидуальныеУпаковки.Строки Цикл
			ШтрихкодУпаковки = ЗначениеРеквизитаВДереве(СтрокаКодаУпаковки,
				"СведенияОТоварах.НомерСтроки.СведенияОМаркировке.ИндивидуальныеУпаковки.НомерСтроки.Код");
			Если ЗначениеЗаполнено(ШтрихкодУпаковки) Тогда 
				ШтрихкодыУпаковок.Добавить(ШтрихкодУпаковки);
			КонецЕсли;
		КонецЦикла;
		
		ТранспортныеУпаковки = КодыУпаковок.Строки.Найти(
			"СведенияОТоварах.НомерСтроки.СведенияОМаркировке.ТранспортныеУпаковки", "ПолныйПуть", Истина);
		Для Каждого СтрокаКодаУпаковки Из ТранспортныеУпаковки.Строки Цикл
			ШтрихкодУпаковки = ЗначениеРеквизитаВДереве(СтрокаКодаУпаковки,
				"СведенияОТоварах.НомерСтроки.СведенияОМаркировке.ТранспортныеУпаковки.НомерСтроки.Код");
			Если ЗначениеЗаполнено(ШтрихкодУпаковки) Тогда 
				ШтрихкодыУпаковок.Добавить(ШтрихкодУпаковки);
			КонецЕсли;
		КонецЦикла;
			
	КонецЕсли;
	
	Возврат ШтрихкодыУпаковок;
	
КонецФункции

// Возвращает коды маркировки для строки товара из дерева электронного документа ЭДО.
//
// Параметры:
//	СведенияОТоваре - СтрокаДереваЗначений - строка товаров дерева электронного документа ЭДО.
//
// Пример использования:
//	СведенияОТоварах = ДеревоДанных.Строки.Найти("СведенияОТоварах", "ПолныйПуть");
//	Для Каждого СведенияОТоваре Из СведенияОТоварах.Строки Цикл
//		ШтрихкодыУпаковок = ЭлектронноеВзаимодействиеИСМП.ШтрихкодыУпаковокИзСтрокиДереваЭДО(СведенияОТоваре);
//	КонецЦикла;.
//
// Возвращаемое значение:
//  Массив из Строка - массив штрихкодов упаковок
Функция ШтрихкодыУпаковокИзСтрокиДереваЭДО_5_02(СведенияОТоваре) Экспорт
	
	ШтрихкодыУпаковок = Новый Массив;
	
	Путь = "ТаблицаСчетаФактуры.СведенияОбОтгруженныхПозициях.НомерСтроки.СведенияОМаркировке";
	ПутьКИЗ = Путь + ".НомерСтроки.Идентификатор.КонтрольныеЗнаки";
	ПутьУпаковки = Путь + ".НомерСтроки.Идентификатор.ИдентификаторыУпаковок";   
	ПутьГТИН = "ТаблицаСчетаФактуры.СведенияОбОтгруженныхПозициях.НомерСтроки.ГТИН";
	
	СведенияОМаркировке = СведенияОТоваре.Строки.Найти(Путь, "ПолныйПуть", Истина);
	ДанныеГТИН          = СведенияОТоваре.Строки.Найти(ПутьГТИН, "ПолныйПуть", Истина);
	
	Если СведенияОМаркировке <> Неопределено И ЗначениеЗаполнено(СведенияОМаркировке.Значение) Тогда
		
		Для Каждого Маркировка Из СведенияОМаркировке.Строки Цикл
			
			ЛогистическаяУпаковка = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
				Маркировка, Путь + ".НомерСтроки.ИдентификаторУпаковки");
				
			Если ЗначениеЗаполнено(ЛогистическаяУпаковка) Тогда
				ШтрихкодыУпаковок.Добавить(ЛогистическаяУпаковка);      
			КонецЕсли;                 
			
			КоличествоПоОСУ = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
				Маркировка, Путь + ".НомерСтроки.Количество");      
				
			Если ЗначениеЗаполнено(КоличествоПоОСУ) И ЗначениеЗаполнено(ДанныеГТИН.Значение) Тогда
				
				КодОСУ = СформироватьКодОСУПоКоличествуИGTIN(ДанныеГТИН.Значение, КоличествоПоОСУ);
				ШтрихкодыУпаковок.Добавить(КодОСУ);
				
			КонецЕсли;
			
			КонтрольныеЗнаки = Маркировка.Строки.Найти(ПутьКИЗ, "ПолныйПуть", Истина);
			Если КонтрольныеЗнаки <> Неопределено И ЗначениеЗаполнено(КонтрольныеЗнаки.Значение) Тогда
				
				Для Каждого КонтрольныйЗнак Из КонтрольныеЗнаки.Строки Цикл
					
					ШтрихкодыУпаковок.Добавить(ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
						КонтрольныйЗнак, ПутьКИЗ + ".НомерСтроки.КонтрольныйЗнак"));
					
				КонецЦикла;
				
			КонецЕсли;
				
			ИдентификаторыУпаковок = Маркировка.Строки.Найти(ПутьУпаковки, "ПолныйПуть", Истина);
			Если ИдентификаторыУпаковок <> Неопределено И ЗначениеЗаполнено(ИдентификаторыУпаковок.Значение) Тогда
				
				Для Каждого ИдентификаторУпаковки Из ИдентификаторыУпаковок.Строки Цикл
					
					ШтрихкодыУпаковок.Добавить(ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
						ИдентификаторУпаковки, ПутьУпаковки + ".НомерСтроки.ИдентификаторУпаковки"));
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ШтрихкодыУпаковок;
	
КонецФункции

// Возвращает коды маркировки для строки товара из дерева электронного документа ЭДО.
//
// Параметры:
//	СведенияОТоваре - СтрокаДереваЗначений - строка товаров дерева электронного документа ЭДО.
//	Постфикс - Строка - потсфикс имени реквизита строки ЭДО, хранящего сведения о маркировке ("До", "После")
// Пример использования:
//	СведенияОТоварах = ДеревоДанных.Строки.Найти("СведенияОТоварах", "ПолныйПуть");
//	Для Каждого СведенияОТоваре Из СведенияОТоварах.Строки Цикл
//		ШтрихкодыУпаковок = ЭлектронноеВзаимодействиеИСМП.ШтрихкодыУпаковокИзСтрокиДереваЭДО(СведенияОТоваре);
//	КонецЦикла;.
//
Функция ШтрихкодыУпаковокИзСтрокиДереваЭДО_УКД(СведенияОТоваре, Постфикс = "После")
	
	ШтрихкодыУпаковок = Новый Массив;
	
	ПутьСведенияОМаркировке = "СведенияОТоварах.НомерСтроки.СведенияОМаркировке" + Постфикс;
	
	КодыУпаковок = СведенияОТоваре.Строки.Найти(ПутьСведенияОМаркировке, "ПолныйПуть", Истина);
	
	Если КодыУпаковок <> Неопределено И ЗначениеЗаполнено(КодыУпаковок.Значение) Тогда
		
		КонтрольныеИдентификационныеЗнаки = КодыУпаковок.Строки.Найти(
			ПутьСведенияОМаркировке + ".КонтрольныеИдентификационныеЗнаки", "ПолныйПуть", Истина);
		
		Для Каждого СтрокаКодаУпаковки Из КонтрольныеИдентификационныеЗнаки.Строки Цикл
			ШтрихкодУпаковки = ЗначениеРеквизитаВДереве(СтрокаКодаУпаковки,
				ПутьСведенияОМаркировке + ".КонтрольныеИдентификационныеЗнаки.НомерСтроки.Код");
			Если ЗначениеЗаполнено(ШтрихкодУпаковки) 
				И Не ШтрихкодУпаковки = "-" Тогда 
				ШтрихкодыУпаковок.Добавить(ШтрихкодУпаковки);
			КонецЕсли;
		КонецЦикла;
		
		ТранспортныеУпаковки = КодыУпаковок.Строки.Найти(
			ПутьСведенияОМаркировке + ".ТранспортныеУпаковки", "ПолныйПуть", Истина);
		Для Каждого СтрокаКодаУпаковки Из ТранспортныеУпаковки.Строки Цикл
			ШтрихкодУпаковки = ЗначениеРеквизитаВДереве(СтрокаКодаУпаковки,
				ПутьСведенияОМаркировке + ".ТранспортныеУпаковки.НомерСтроки.Код");
			Если ЗначениеЗаполнено(ШтрихкодУпаковки) 
				И Не ШтрихкодУпаковки = "-" Тогда 
				ШтрихкодыУпаковок.Добавить(ШтрихкодУпаковки);
			КонецЕсли;
		КонецЦикла;
		
		ИндивидуальныеУпаковки = СведенияОТоваре.Строки.Найти(
			ПутьСведенияОМаркировке + ".ИндивидуальныеУпаковки", "ПолныйПуть", Истина);
		Если ИндивидуальныеУпаковки <> Неопределено Тогда
			Для Каждого СтрокаКодаУпаковки Из ИндивидуальныеУпаковки.Строки Цикл
				ШтрихкодУпаковки = ЗначениеРеквизитаВДереве(СтрокаКодаУпаковки,
					ПутьСведенияОМаркировке + ".ИндивидуальныеУпаковки.НомерСтроки.Код");
				Если ЗначениеЗаполнено(ШтрихкодУпаковки) 
					И Не ШтрихкодУпаковки = "-" Тогда 
					ШтрихкодыУпаковок.Добавить(ШтрихкодУпаковки);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
			
	КонецЕсли;
	
	Возврат ШтрихкодыУпаковок;
	
КонецФункции

// Создает новую таблицу значений для штрихкодов упаковок.
// В прикладных документах должна существовать табличная часть с аналогичным набором колонок.
// 
// Параметры:
// 	ДобавитьНоменклатуру - Булево - признак необходимости дополнить возвращаемую таблицу колонками номенклатуры.
//
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблица значений штрихкодов упаковок:
// * ШтрихкодУпаковки - СправочникСсылка.ШтрихкодыУпаковокТоваров - Штрихкод упаковки
// * ЗначениеШтрихкода - Строка - Значение штрихкода
// * Номенклатура - ОпределяемыйТип.Номенклатура - Номенклатура
// * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - Характеристика
Функция НоваяТаблицаШтрихкодыУпаковок(ДобавитьНоменклатуру = Ложь) Экспорт
	
	ШтрихкодыУпаковок = Новый ТаблицаЗначений;
	ШтрихкодыУпаковок.Колонки.Добавить("ШтрихкодУпаковки", Новый ОписаниеТипов("СправочникСсылка.ШтрихкодыУпаковокТоваров"));
	ШтрихкодыУпаковок.Колонки.Добавить("ЗначениеШтрихкода",Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(200)));
	
	Если ДобавитьНоменклатуру Тогда
		ШтрихкодыУпаковок.Колонки.Добавить("Номенклатура",   Метаданные.ОпределяемыеТипы.Номенклатура.Тип);
		ШтрихкодыУпаковок.Колонки.Добавить("Характеристика", Метаданные.ОпределяемыеТипы.ХарактеристикаНоменклатуры.Тип);
	КонецЕсли;
	
	Возврат ШтрихкодыУпаковок;
	
КонецФункции

// Создает таблицу значений для штрихкодов упаковок, полученных в ТОРГ-2
// В прикладных документах должна существовать табличная часть с аналогичным набором колонок.
// 
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблица значений штрихкодов упаковок:
// * ШтрихкодУпаковки - СправочникСсылка.ШтрихкодыУпаковокТоваров - Штрихкод упаковки
// * ЗначениеШтрихкода - Строка - Значение штрихкода
// * Номенклатура - ОпределяемыйТип.Номенклатура - Номенклатура
// * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - Характеристика
// * ТипРасхождения - ОпределяемыйТип.ТипРасхожденияИСМП - тип расхождения.
// * Количество - Число - количество единиц.
//
Функция ТаблицаШтрихкодыУпаковокНоменклатура() Экспорт

	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("ЗначениеШтрихкода",Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(200)));
	ТаблицаДанных.Колонки.Добавить("Номенклатура",   Метаданные.ОпределяемыеТипы.Номенклатура.Тип);
	ТаблицаДанных.Колонки.Добавить("Характеристика", Метаданные.ОпределяемыеТипы.ХарактеристикаНоменклатуры.Тип);
	ТаблицаДанных.Колонки.Добавить("Количество",     ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	ТаблицаДанных.Колонки.Добавить("ТипРасхождения", Метаданные.ОпределяемыеТипы.ТипРасхожденияИСМП.Тип);
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Добавляет коды маркировки из сведений о товаре документа ЭДО в общую таблицу штрихкодов упаковок.
// 
// Параметры:
// 	ШтрихкодыУпаковок - ТаблицаЗначений - Таблица штрихкодов (См. НоваяТаблицаШтрихкодыУпаковок).
// 	СведенияОТоваре - СтрокаДереваЗначений - Строка сведений о товаре из документа ЭДО.
Процедура ДобавитьШтрихкодыТаблицуШтрихкодовУпаковок(ШтрихкодыУпаковок, СведенияОТоваре) Экспорт
	
	ШтрихкодыУпаковокСтрокиТЧ = ШтрихкодыУпаковокИзСтрокиДереваЭДО(СведенияОТоваре);
	Для Каждого ЗначениеШтрихкода Из ШтрихкодыУпаковокСтрокиТЧ Цикл
		
		НоваяСтрока = ШтрихкодыУпаковок.Добавить();
		НоваяСтрока.ЗначениеШтрихкода = ЗначениеШтрихкода;
		
	КонецЦикла;
	
КонецПроцедуры

// Добавляет коды маркировки из сведений о товаре документа ЭДО в общую таблицу штрихкодов упаковок.
// 
// Параметры:
// 	ШтрихкодыУпаковок - ТаблицаЗначений - Таблица штрихкодов (См. НоваяТаблицаШтрихкодыУпаковок).
// 	СведенияОТоваре - СтрокаДереваЗначений - Строка сведений о товаре из документа ЭДО.
Процедура ДобавитьШтрихкодыТаблицуШтрихкодовУпаковок_2019(ШтрихкодыУпаковок, СведенияОТоваре) Экспорт
	
	ШтрихкодыУпаковокСтрокиТЧ = ШтрихкодыУпаковокИзСтрокиДереваЭДО_2019(СведенияОТоваре);
	Для Каждого ЗначениеШтрихкода Из ШтрихкодыУпаковокСтрокиТЧ Цикл
		
		НоваяСтрока = ШтрихкодыУпаковок.Добавить();
		НоваяСтрока.ЗначениеШтрихкода = ЗначениеШтрихкода;
		
	КонецЦикла;
	
КонецПроцедуры

// Добавляет коды маркировки из сведений о товаре документа ЭДО в общую таблицу штрихкодов упаковок.
// 
// Параметры:
// 	ШтрихкодыУпаковок - ТаблицаЗначений - Таблица штрихкодов (См. НоваяТаблицаШтрихкодыУпаковок).
// 	СведенияОТоваре - СтрокаДереваЗначений - Строка сведений о товаре из документа ЭДО.
Процедура ДобавитьШтрихкодыТаблицуШтрихкодовУпаковок_5_02(ШтрихкодыУпаковок, СведенияОТоваре) Экспорт
	
	ШтрихкодыУпаковокСтрокиТЧ = ШтрихкодыУпаковокИзСтрокиДереваЭДО_5_02(СведенияОТоваре);
	Для Каждого ЗначениеШтрихкода Из ШтрихкодыУпаковокСтрокиТЧ Цикл
		
		НоваяСтрока = ШтрихкодыУпаковок.Добавить();
		НоваяСтрока.ЗначениеШтрихкода = ЗначениеШтрихкода;
		
	КонецЦикла;
	
КонецПроцедуры

// Добавляет коды маркировки из сведений о товаре документа ЭДО в общую таблицу штрихкодов упаковок.
// Используется для УКД, заполненного кодами маркировки полным форматом (все коды До/После).
// 
// Параметры:
// 	ШтрихкодыУпаковок - ТаблицаЗначений - Таблица штрихкодов (См. НоваяТаблицаШтрихкодыУпаковок).
// 	СведенияОТоваре - СтрокаДереваЗначений - Строка сведений о товаре из документа ЭДО.
Процедура ДобавитьШтрихкодыТаблицуШтрихкодовУпаковок_УКД(ШтрихкодыУпаковок, СведенияОТоваре) Экспорт
	
	ШтрихкодыУпаковокСтрокиТЧ = ШтрихкодыУпаковокИзСтрокиДереваЭДО_УКД(СведенияОТоваре);
	Для Каждого ЗначениеШтрихкода Из ШтрихкодыУпаковокСтрокиТЧ Цикл
		
		НоваяСтрока = ШтрихкодыУпаковок.Добавить();
		НоваяСтрока.ЗначениеШтрихкода = ЗначениеШтрихкода;
		
	КонецЦикла;
	
КонецПроцедуры

// Добавляет коды маркировки из сведений о товаре документа ЭДО в общую таблицу штрихкодов упаковок.
// Используется для УКД, заполненного кодами маркировки по упрощенному формату (только расхождения До/После).
// 
// Параметры:
// 	ШтрихкодыУпаковок - ТаблицаЗначений - Таблица штрихкодов для заполнения данными из ЭДО (См. НоваяТаблицаШтрихкодыУпаковок).
// 	СведенияОТоваре - СтрокаДереваЗначений - Строка сведений о товаре из документа ЭДО.
//  ШтрихкодыУпаковокОснования - ТаблицаЗначений - таблица, содержащая колонки:
//  * ЗначениеШтрихкода - Строка - Значение штрихкода.
//  * Номенклатура - ОпределяемыйТип.Номенклатура - Номенклатура.
//  * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - Характеристика.
// 	СтрокаТовары - СтрокаТаблицыЗначений - строка документа, содержащая колонки:
//   * Номенклатура   - ОпределяемыйТип.Номенклатура - номенклатура в строке документа,
//   * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика в строке.
Процедура ДобавитьШтрихкодыТаблицуШтрихкодовУпаковок_УКД2020(ШтрихкодыУпаковок, СведенияОТоваре, ШтрихкодыУпаковокОснования, СтрокаТовары) Экспорт
	
	ШтрихкодыУпаковокПосле = ШтрихкодыУпаковокИзСтрокиДереваЭДО_УКД(СведенияОТоваре);
	ШтрихкодыУпаковокДо = ШтрихкодыУпаковокИзСтрокиДереваЭДО_УКД(СведенияОТоваре, "До");
	
	Для Каждого ЗначениеШтрихкода Из ШтрихкодыУпаковокПосле Цикл
		
		НоваяСтрока = ШтрихкодыУпаковок.Добавить();
		НоваяСтрока.ЗначениеШтрихкода = ЗначениеШтрихкода;
		НоваяСтрока.Номенклатура      = СтрокаТовары.Номенклатура;
		
	КонецЦикла;
	
	// При использовании сокращенного формата заполнения кодов, согласно доментации "Методические_рекомендации_по_оформлению_документов_ЭДО":
	// "может быть указан перечень КИ до/после, только по измененным КИ"
	// В таком случае, если в исходном документе были разные виды упаковок, но изменения по кодам пришли только в одном теге,
	// нужно дополнить штрихкоды данными исходного ЭД, исключая коды, указанные в теге "СведенияОМаркировкеДо".
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Номенклатура",   СтрокаТовары.Номенклатура);
	ПараметрыОтбора.Вставить("Характеристика", СтрокаТовары.Характеристика);

	ШтрихкодыОснованияСОтбором = ШтрихкодыУпаковокОснования.НайтиСтроки(ПараметрыОтбора);
	Для Каждого СтрокаТаблицы Из ШтрихкодыОснованияСОтбором Цикл
		Если ШтрихкодыУпаковокДо.Найти(СтрокаТаблицы.ЗначениеШтрихкода) = Неопределено Тогда
			НоваяСтрока = ШтрихкодыУпаковок.Добавить();
			НоваяСтрока.ЗначениеШтрихкода = СтрокаТаблицы.ЗначениеШтрихкода;
			НоваяСтрока.Номенклатура      = СтрокаТаблицы.Номенклатура;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

//Дополняет таблицу штрихкоды упаковок при заполнении УКД данными кодов документа-основания с отобором по номенклатуре строки.
// 
// Параметры:
// 	ШтрихкодыУпаковок - ТаблицаЗначений - Таблица штрихкодов (См. НоваяТаблицаШтрихкодыУпаковок).
//  ШтрихкодыУпаковокОснования - ТаблицаЗначений - таблица, содержащая колонки:
//  * ЗначениеШтрихкода - Строка - Значение штрихкода
//  * Номенклатура - ОпределяемыйТип.Номенклатура - Номенклатура
//  * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - Характеристика
//  СтрокаТовары - СтрокаТаблицыЗначений - строка документа, содержащая колонки:
//   * Номенклатура   - ОпределяемыйТип.Номенклатура - номенклатура в строке документа,
//   * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика в строке.
Процедура ДобавитьШтрихкодыПоДокументуОснованию_УКД(ШтрихкодыУпаковок, ШтрихкодыУпаковокОснования, СтрокаТовары) Экспорт
	
	Если ШтрихкодыУпаковокОснования = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Номенклатура", СтрокаТовары.Номенклатура);
	ПараметрыОтбора.Вставить("Характеристика", СтрокаТовары.Характеристика);
	
	ШтрихкодыНоменклатуры = ШтрихкодыУпаковокОснования.НайтиСтроки(ПараметрыОтбора);
	Для Каждого СтрокаШтрихкода Из ШтрихкодыНоменклатуры Цикл
		НоваяСтрока = ШтрихкодыУпаковок.Добавить();
		НоваяСтрока.ЗначениеШтрихкода = СтрокаШтрихкода.ЗначениеШтрихкода; 
		НоваяСтрока.Номенклатура      = СтрокаШтрихкода.Номенклатура;
	КонецЦикла;
	
КонецПроцедуры

// Добавляет коды маркировки из сведений о товаре документа ЭДО в таблицы штрихкодов упаковок (факт и расхождения).
// 
// Параметры:
//  ШтрихкодыУпаковок - ТаблицаЗначений - Таблица штрихкодов для заполнения данными раздела Факт (См. ТаблицаШтрихкодыУпаковокНоменклатура).
//  ШтрихкодыУпаковокРасхождения - ТаблицаЗначений - Таблица штрихкодов для заполнения данными по расхождениям (См. ТаблицаШтрихкодыУпаковокНоменклатура).
//  СведенияОТоваре - СтрокаДереваЗначений - Строка сведений о товаре из документа ЭДО.
Процедура ДобавитьШтрихкодыТаблицыШтрихкодовАктОРасхождениях(ШтрихкодыУпаковок, ШтрихкодыУпаковокРасхождения,
		СведенияОТоваре) Экспорт
	
	СтандартнаяОбработка = Истина;
	
	ЭлектронноеВзаимодействиеИСМППереопределяемый.ДобавитьШтрихкодыТаблицыШтрихкодовАктОРасхождениях(
		ШтрихкодыУпаковок,
		ШтрихкодыУпаковокРасхождения,
		СведенияОТоваре);
	
	Если СтандартнаяОбработка = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	ПарамерыЗаполнения = ПараметрыЗаполненияТаблицыШтрихкодовАктОРасхождениях();
	
	Если ЗначениеЗаполнено(СведенияОТоваре.Сопоставление.НоменклатураИБ) Тогда
		ПарамерыЗаполнения.Номенклатура = СведенияОТоваре.Сопоставление.НоменклатураИБ;
	КонецЕсли;
	Если ЗначениеЗаполнено(СведенияОТоваре.Сопоставление.ХарактеристикаИБ) Тогда
		ПарамерыЗаполнения.Характеристика = СведенияОТоваре.Сопоставление.ХарактеристикаИБ;
	КонецЕсли;
	
	ДобавитьШтрихкодыТаблицуШтрихкодовАктОРасхождениях(ШтрихкодыУпаковок, СведенияОТоваре.ПоФакту, ПарамерыЗаполнения);
	
	ПарамерыЗаполнения.ТипРасхождения = ИнтеграцияИСМП.ТипРасхожденияИСМПИзлишек();
	ДобавитьШтрихкодыТаблицуШтрихкодовАктОРасхождениях(ШтрихкодыУпаковокРасхождения, СведенияОТоваре.Излишки, ПарамерыЗаполнения);
	
	ПарамерыЗаполнения.ТипРасхождения = ИнтеграцияИСМП.ТипРасхожденияИСМПНедостача();
	ДобавитьШтрихкодыТаблицуШтрихкодовАктОРасхождениях(ШтрихкодыУпаковокРасхождения, СведенияОТоваре.Недостача, ПарамерыЗаполнения);
	
	ПарамерыЗаполнения.ТипРасхождения = ИнтеграцияИСМП.ТипРасхожденияИСМПБрак();
	ДобавитьШтрихкодыТаблицуШтрихкодовАктОРасхождениях(ШтрихкодыУпаковокРасхождения, СведенияОТоваре.Брак, ПарамерыЗаполнения);
	
КонецПроцедуры

Функция ПараметрыЗаполненияТаблицыШтрихкодовАктОРасхождениях()
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Номенклатура", ОбщегоНазначенияИС.ПустоеЗначениеОпределяемогоТипа("Номенклатура"));
	ПараметрыЗаполнения.Вставить("Характеристика", ОбщегоНазначенияИС.ПустоеЗначениеОпределяемогоТипа("ХарактеристикаНоменклатуры"));
	ПараметрыЗаполнения.Вставить("ЗаполнятьТипРасходения", Ложь);
	ПараметрыЗаполнения.Вставить("ТипРасхождения", ОбщегоНазначенияИС.ПустоеЗначениеОпределяемогоТипа("ТипРасхожденияИСМП"));
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

// Сворачивает таблицу штрихкодов упаковок.
// 
// Параметры:
// 	ШтрихкодыУпаковок - ТаблицаЗначений - Таблица штрихкодов упаковок.
Процедура СвернутьТаблицуШтрихкодовУпаковок(ШтрихкодыУпаковок) Экспорт
	
	ШтрихкодыУпаковок.Свернуть("ЗначениеШтрихкода, ШтрихкодУпаковки");
	
КонецПроцедуры

Процедура СвернутьТаблицуШтрихкодовУпаковокАкт(ШтрихкодыУпаковок) Экспорт
	
	ШтрихкодыУпаковок.Свернуть("ЗначениеШтрихкода, Номенклатура, Характеристика, ТипРасхождения", "Количество");
	
КонецПроцедуры

Процедура ОчиститьРезультатыПроверкиДокументаПриЗагрузкеДокумента(Документ) Экспорт
	
	РегистрыСведений.СтатусыПроверкиИПодбораДокументовИС.ОчиститьРезультатыПроверкиДокумента(Документ);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ВерсияАПИ() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВерсияБиблиотеки = ОбновлениеИнформационнойБазыБЭД.ВерсияБиблиотеки();
	Если ОбщегоНазначенияКлиентСервер.СравнитьВерсии(ВерсияБиблиотеки, "1.9.1.1") >= 0 Тогда
		Возврат 2;
	Иначе
		Возврат 1;
	КонецЕсли;
	
КонецФункции

// Возвращает пространство информации продавца УПД версии 5.02 (приказ ЕД-7-26/970@).
//
// Возвращаемое значение:
//  Строка
//
Функция ПространствоИмен_УПД_5_02_ИнформацияПродавца() Экспорт
	
	Возврат "ON_NSCHFDOPPR_1_997_01_05_02_01";
	
КонецФункции

Функция ЗавершенОбменПоЭДО(ПроверяемыйДокумент) Экспорт
	
	Результат = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(ПроверяемыйДокумент) 
	 И Не Метаданные.ОпределяемыеТипы.ДокументыИСМП.Тип.СодержитТип(ТипЗнч(ПроверяемыйДокумент))
	 И (ВерсияАПИ() = 1
	  Или Метаданные.ОпределяемыеТипы["ОснованияЭлектронныхДокументовЭДО"].Тип.СодержитТип(ТипЗнч(ПроверяемыйДокумент))) Тогда
		СтатусДокументаПоЭДО = ОбменСКонтрагентами.СтатусДокументооборота(ПроверяемыйДокумент).Статус;
		Если СтатусДокументаПоЭДО = "Утвержден" Тогда
			Результат = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция генерирует технический код объемно-сортового учета
//  по регулярному выражению [02][GTIN][37][количество].
// 
// Параметры:
//  GTIN - Строка - GTIN кода ОСУ
//  Количество - Число - Количество по ОСУ
// 
// Возвращаемое значение:
//  Строка
Функция СформироватьКодОСУПоКоличествуИGTIN(GTIN, Количество) Экспорт
	
	Возврат СтрШаблон(
		"%1%2%3%4",
		"02", GTIN, "37", Формат(Количество, "ЧДЦ=0; ЧН=0; ЧГ=;"));
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеПрикладныхДокументовНаОснованииДокументовЭДО

// Предназначена для получения значения из дерева значений по полному пути.
// (См. ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве)
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - объект поиска.
//  ПолныйПуть   - Строка         - значение поиска.
// 
Функция ЗначениеРеквизитаВДереве(ДеревоДанных, ПолныйПуть)
	
	СтандартнаяОбработка = Истина;
	ЗначениеРеквизита = "";
	ЭлектронноеВзаимодействиеИСМППереопределяемый.ПриОпределенииЗначенияРеквизитаВДереве(ЗначениеРеквизита, ДеревоДанных, ПолныйПуть, СтандартнаяОбработка);
	
	Если СтандартнаяОбработка Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ЭлектронноеВзаимодействие");
		ЗначениеРеквизита = Модуль.ЗначениеРеквизитаВДереве(ДеревоДанных, ПолныйПуть);
	КонецЕсли;
	
	Возврат ЗначениеРеквизита;
	
КонецФункции

// Предназначена для получения значения из дерева значений по полному пути.
// (См. ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве)
//
// Параметры:
//Параметры:
//   СтрокиТаблицы - Массив,ТаблицаЗначений - см. ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю.СтрокиТаблицы.
//   ПолеТаблицы   - Строка                 - см. ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю.ПолеТаблицы.
//   КлючДанных    - Строка, ЛюбаяСсылка    - см. ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю.КлючДанных.
//   ПутьКДанным   - Строка                 - см. ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю.ПутьКДанным.
// 
Процедура ВывестиОшибкуЗаполненияПользователю(СтрокиТаблицы, ПолеТаблицы, КлючДанных, ПутьКДанным)
	
	СтандартнаяОбработка = Истина;
	ЭлектронноеВзаимодействиеИСМППереопределяемый.ПриВыводеОшибкиЗаполненияПользователю(СтрокиТаблицы, ПолеТаблицы, КлючДанных, ПутьКДанным, СтандартнаяОбработка);
	
	Если СтандартнаяОбработка Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ЭлектронноеВзаимодействие");
		Модуль.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(СтрокиТаблицы, ПолеТаблицы, КлючДанных, ПутьКДанным);
	КонецЕсли;
	
КонецПроцедуры

// Добавляет коды маркировки из сведений о товаре документа ЭДО в общую таблицу штрихкодов упаковок.
// 
// Параметры:
// 	ШтрихкодыУпаковок - ТаблицаЗначений - Таблица штрихкодов (См. ТаблицаШтрихкодыУпаковокНоменклатура).
// 	СведенияОТоваре - СтрокаДереваЗначений - Строка сведений о товаре из документа ЭДО.
// 	ПараметрыЗаполнения - Структура - Структура (См. ПараметрыЗаполненияТаблицыШтрихкодовАктОРасхождениях).
Процедура ДобавитьШтрихкодыТаблицуШтрихкодовАктОРасхождениях(ШтрихкодыУпаковок, СведенияОТоваре,
		ПараметрыЗаполнения)
	
	Маркировка = Неопределено;
	
	Если Не СведенияОТоваре.Свойство("Маркировка", Маркировка) Тогда
		Возврат;
	КонецЕсли;
	
	Если Маркировка.Свойство("КонтрольныеИдентификационныеЗнаки") Тогда
		Для Каждого ЗначениеШтрихкода Из Маркировка.КонтрольныеИдентификационныеЗнаки Цикл
			НоваяСтрока = ШтрихкодыУпаковок.Добавить();
			НоваяСтрока.ЗначениеШтрихкода = ЗначениеШтрихкода.Код;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ПараметрыЗаполнения);
		КонецЦикла;
	КонецЕсли;
	
	Если Маркировка.Свойство("ИндивидуальныеУпаковки") Тогда
		Для Каждого ЗначениеШтрихкода Из Маркировка.ИндивидуальныеУпаковки Цикл
			НоваяСтрока = ШтрихкодыУпаковок.Добавить();
			НоваяСтрока.ЗначениеШтрихкода = ЗначениеШтрихкода.Код;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ПараметрыЗаполнения);
		КонецЦикла;
	КонецЕсли;
	
	Если Маркировка.Свойство("ТранспортныеУпаковки") Тогда
		Для Каждого ЗначениеШтрихкода Из Маркировка.ТранспортныеУпаковки Цикл
			НоваяСтрока = ШтрихкодыУпаковок.Добавить();
			НоваяСтрока.ЗначениеШтрихкода = ЗначениеШтрихкода.Код;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ПараметрыЗаполнения);
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаЗаполнения

Функция ТаблицаМаркируемойПродукцииДокумента(Документ, ВидыМаркируемойПродукции) Экспорт
	
	ТаблицаПродукции = Новый ТаблицаЗначений;
	ТаблицаПродукции.Колонки.Добавить("Номенклатура",     Метаданные.ОпределяемыеТипы.Номенклатура.Тип);
	ТаблицаПродукции.Колонки.Добавить("Характеристика",   Метаданные.ОпределяемыеТипы.ХарактеристикаНоменклатуры.Тип);
	ТаблицаПродукции.Колонки.Добавить("Серия",            Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Тип);
	ТаблицаПродукции.Колонки.Добавить("Количество",       ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	
	ИнтеграцияИСМППереопределяемый.СформироватьТаблицуМаркируемойПродукцииДокумента(Документ,
		ТаблицаПродукции, ВидыМаркируемойПродукции);
	
	Возврат ТаблицаПродукции;
	
КонецФункции

Процедура ОбработкаСтрокиЧастичногоВыбытия(СтрокаТаблицы, КэшВходящихСтрок)
	
	СтрокаКэша = КэшВходящихСтрок.Получить(СтрокаТаблицы.Штрихкод);
	
	Если СтрокаКэша <> Неопределено
		И ЗначениеЗаполнено(СтрокаКэша.ЧастичноеВыбытиеКоличество)
		И ЗначениеЗаполнено(СтрокаКэша.ЧастичноеВыбытиеВариантУчета) Тогда
		
		СтрокаТаблицы.Количество                   = СтрокаКэша.ЧастичноеВыбытиеКоличество;
		СтрокаТаблицы.ЧастичноеВыбытиеВариантУчета = СтрокаКэша.ЧастичноеВыбытиеВариантУчета;
		
		Если СтрокаКэша.ЧастичноеВыбытиеВариантУчета = Перечисления.ВариантыУчетаЧастичногоВыбытияИС.НастроеннаяНоменклатура Тогда
			СтрокаТаблицы.Номенклатура   = СтрокаКэша.ЧастичноеВыбытиеНоменклатура;
			СтрокаТаблицы.Характеристика = СтрокаКэша.ЧастичноеВыбытиеХарактеристика;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура ВыделитьШтрихкодыСодержащиеВидыПродукции(ШтрихкодыУпаковок, Знач ВидыПродукцииИС)
	
	Если НЕ ШтрихкодыУпаковок.Количество() Тогда
		Возврат;
	ИначеЕсли ВидыПродукцииИС = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ШтрихкодированиеИСПереопределяемый.ВыделитьШтрихкодыСодержащиеВидыПродукции(ШтрихкодыУпаковок, ВидыПродукцииИС);
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеДанныхЭДОПоПрикладнымДокументам

//Для случаев когда в электронный документ попадает не детализированная до серий информация
Процедура ОтметитьОбработанныеСтрокиДляМультитоварныхУпаковок(ДанныеШтрихкодовУпаковок, ПараметрыПоиска, СтрокаУпаковки)
	
	Если Не СтрокаУпаковки.Мультитоварная Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПоиска.Вставить("Штрихкод", СтрокаУпаковки.Штрихкод);
	СтрокиАналоги = ДанныеШтрихкодовУпаковок.НайтиСтроки(ПараметрыПоиска);
	Для Каждого СтрокаАналог Из СтрокиАналоги Цикл
		СтрокаАналог.Обработан = Истина;
	КонецЦикла;
	
	ПараметрыПоиска.Удалить("Штрихкод");
КонецПроцедуры

//Удаляет тип значения NULL для всех колонок в исходной таблице
Процедура НормализоватьТаблицуЗначений(Таблица) Экспорт
	
	ИсходныеКолонки = Новый Массив;
	Для Каждого Колонка Из Таблица.Колонки Цикл
		ИсходныеКолонки.Добавить(Колонка);
	КонецЦикла; 
	
	Для Каждого Колонка Из ИсходныеКолонки Цикл
		
		ИмяКолонки = Колонка.Имя;
		ТипКолонки = Колонка.ТипЗначения;
		
		Таблица.Колонки[ИмяКолонки].Имя = "Удалить" + ИмяКолонки;
		
		Таблица.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов(ТипКолонки, , "NULL"));
		Таблица.ЗагрузитьКолонку(Таблица.ВыгрузитьКолонку("Удалить"+ИмяКолонки), ИмяКолонки);
		
		Таблица.Колонки.Удалить("Удалить" + ИмяКолонки);
		
	КонецЦикла;
	
КонецПроцедуры

Функция СформироватьКодОСУИзGTIN(СтрокаТовары, СтрокаУпаковки, Поле = "Количество")
	
	КоличествоПоТегу37 = СформироватьКоличествоПоОСУ(СтрокаТовары, СтрокаУпаковки, Поле);
	
	Возврат СформироватьКодОСУПоКоличествуИGTIN(СтрокаУпаковки.ЗначениеШтрихкода, КоличествоПоТегу37);
	
КонецФункции

Функция СформироватьКоличествоПоОСУ(СтрокаТовары, СтрокаУпаковки, Поле = "Количество")
	
	КоличествоПоОСУ = 0;
	
	Если СтрокаУпаковки.КоличествоВПотребительскойУпаковке = 0 Тогда
		КоличествоПоОСУ = 0;
	Иначе
		КоличествоПоОСУ = Окр(СтрокаТовары[Поле] / СтрокаУпаковки.КоличествоВПотребительскойУпаковке);
	КонецЕсли;
	
	Возврат КоличествоПоОСУ;
	
КонецФункции 

#КонецОбласти

#Область ПрочиеПроцедурыИФункции

// Добавляет в таблицу коды ОСУ для строк, по которым коды маркировки не указывались. Считаем что таблица товаров
//   уже соответствует таблице штрихкодов упаковок (с учетом невнесенных ОСУ).
//
// Параметры:
//  ТаблицаТоваров	 - ТаблицаЗначений - исходная таблица товаров: 
// 	 * Ссылка         - Произвольный - объект-владелец штрихкода
// 	 * Номенклатура   - ОпределяемыйТип.Номенклатура - номенклатура
// 	 * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика
// 	 * Количество     - Число - количество
//  ПараметрыСканирования - См. ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования
//  ЧастичноеСодержимоеРезультат - см. ЧастичноеСодержимое
Процедура ДополнитьРезультатКодамиОСУ(ТаблицаТоваров, ПараметрыСканирования, ЧастичноеСодержимоеРезультат) Экспорт
	
	ЕстьОшибки = Ложь;
	
	СписокЗапросов = Новый СписокЗначений;
	
	СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	Товары.Ссылка         КАК Ссылка,
		|	Товары.Номенклатура   КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.Количество     КАК Количество
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	&Товары КАК Товары
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика
		|");
	
	СписокЗапросов.Добавить(
		"ВЫБРАТЬ
		|	Товары.Ссылка         КАК Ссылка,
		|	Товары.Номенклатура   КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	СУММА(Товары.Количество) КАК Количество
		|ПОМЕСТИТЬ ДанныеШтрихкодовУпаковок
		|ИЗ
		|	Товары КАК Товары
		|СГРУППИРОВАТЬ ПО
		|	Товары.Ссылка,
		|	Товары.Номенклатура,
		|	Товары.Характеристика
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика
		|");
	
	СписокЗапросов.Добавить(
		ШтрихкодированиеОбщегоНазначенияИС.ТекстЗапросаСвойстваМаркируемойПродукции());
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДанныеШтрихкодовУпаковок.Ссылка           КАК Ссылка,
		|	ДанныеШтрихкодовУпаковок.Номенклатура     КАК Номенклатура,
		|	ДанныеШтрихкодовУпаковок.Характеристика   КАК Характеристика,
		|	ДанныеШтрихкодовУпаковок.Количество       КАК Количество,
		|	СвойстваМаркируемойПродукции.ВидПродукции КАК ВидПродукцииИС,
		|	&ТребуетВзвешивания КАК ТребуетВзвешивания,
		|	ЛОЖЬ                                      КАК АвтоматическийОСУИС,
		|	""00000000000000""                        КАК GTIN
		|ИЗ
		|	ДанныеШтрихкодовУпаковок КАК ДанныеШтрихкодовУпаковок
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СвойстваМаркируемойПродукции КАК СвойстваМаркируемойПродукции
		|		ПО ДанныеШтрихкодовУпаковок.Номенклатура = СвойстваМаркируемойПродукции.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеНоменклатурыИС
		|		ПО &ТоварыОписаниеНоменклатурыИС
		|ГДЕ
		|	СвойстваМаркируемойПродукции.ВидПродукции В (&ВидыПродукцииПоддерживаютОСУ)
		|";
	
	ОбщегоНазначенияИС.ОбновитьТекстЗапросаСРегистромОписаниеНоменклатурыИС(ТекстЗапроса, "ОписаниеНоменклатурыИС", "ДанныеШтрихкодовУпаковок.Номенклатура");
	СписокЗапросов.Добавить(ТекстЗапроса, "Результат");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Товары", ТаблицаТоваров.Скопировать(, "Ссылка,Номенклатура,Характеристика,Количество"));
	Запрос.УстановитьПараметр("ВидыПродукцииПоддерживаютОСУ", ОбщегоНазначенияИСКлиентСервер.ВидыПродукцииОбъемноСортовогоУчета());
	РезультатыЗапроса = ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов, Ложь);
	
	МаркируемаяПродукцияДляОСУ = РезультатыЗапроса["Результат"].Выгрузить();
	ПроверкаИПодборПродукцииИСМП.ЗаполнитьПризнакАвтоматическийОСУИСВТаблице(МаркируемаяПродукцияДляОСУ, Истина);
	
	Поиск = Новый Структура("Номенклатура,Характеристика");
	НоменклатураДляОСУ = Новый Массив;
	НоменклатураДляВесовыхКодов = Новый Соответствие;
	ДобавленныеСтроки = Новый Массив;
	ПроверятьНаДату = Неопределено;

	// Замена явно указанного кода ОСУ на автоматический, если он подходит 1 для строки
	Для Каждого СтрокаТЧ Из МаркируемаяПродукцияДляОСУ Цикл
		Если Не ИнтеграцияИСМПКлиентСерверПовтИсп.ВестиУчетМаркируемойПродукции(СтрокаТЧ.ВидПродукцииИС) Тогда
			Продолжить;
		КонецЕсли;
		Если ПроверятьНаДату = Неопределено Тогда
			ПроверятьНаДату = ?(ЗначениеЗаполнено(ПараметрыСканирования.ДатаДокумента), ПараметрыСканирования.ДатаДокумента, ТекущаяДатаСеанса());
		КонецЕсли;
		Если Не ИнтеграцияИСМПКлиентСерверПовтИсп.ОбязательнаяРегистрацияОборотаМаркируемойПродукции(СтрокаТЧ.ВидПродукцииИС, ПроверятьНаДату) Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(Поиск, СтрокаТЧ);
		СтрокиРезультат = ЧастичноеСодержимоеРезультат.НайтиСтроки(Поиск);
		Если СтрокиРезультат.Количество() <> 1 Тогда
			Продолжить;
		КонецЕсли;
		СтрокаЗамены = СтрокиРезультат[0];
		Если СтрокаЗамены.ВидУпаковки <> Перечисления.ВидыУпаковокИС.ОбъемноСортовойУчет Тогда
			Продолжить;
		КонецЕсли;
		СтрокаЗамены.Штрихкод = Неопределено;
		КоличествоКодов = Число(Сред(СтрокаЗамены.ЗначениеШтрихкода, 19));
		СтрокаЗамены.КоличествоВПотребительскойУпаковке = СтрокаТЧ.Количество / КоличествоКодов;
		СтрокаЗамены.ЗначениеШтрихкода = Сред(СтрокаЗамены.ЗначениеШтрихкода, 3, 14);//GTIN
	КонецЦикла;
	
	// Добавление автоматического кода ОСУ, если его нет
	Для Каждого СтрокаТЧ Из МаркируемаяПродукцияДляОСУ Цикл
		Если Не ИнтеграцияИСМПКлиентСерверПовтИсп.ОбязательнаяРегистрацияОборотаМаркируемойПродукции(СтрокаТЧ.ВидПродукцииИС, ПроверятьНаДату) Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(Поиск, СтрокаТЧ);
		Если ЧастичноеСодержимоеРезультат.НайтиСтроки(Поиск).Количество() Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ЧастичноеСодержимоеРезультат.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
		НоваяСтрока.ВидУпаковки = Перечисления.ВидыУпаковокИС.ОбъемноСортовойУчет;
		НоваяСтрока.Обработан = Ложь;
		НоваяСтрока.ВидПродукции = СтрокаТЧ.ВидПродукцииИС;
		НоваяСтрока.Мультитоварная = Ложь;
		НоваяСтрока.ЗначениеШтрихкода = СтрокаТЧ.GTIN;
		Если СтрокаТЧ.ТребуетВзвешивания
			И СтрокаТЧ.Количество <> 0 Тогда
			ДанныеДляЗапросаПоВидуПродукции = НоменклатураДляВесовыхКодов.Получить(СтрокаТЧ.ВидПродукцииИС);
			Если ДанныеДляЗапросаПоВидуПродукции = Неопределено Тогда
				ДанныеДляЗапросаПоВидуПродукции = Новый Соответствие;
				НоменклатураДляВесовыхКодов.Вставить(СтрокаТЧ.ВидПродукцииИС, ДанныеДляЗапросаПоВидуПродукции);
			КонецЕсли;
			ДанныеДляЗапросаПоВидуПродукции.Вставить(СтрокаТЧ.GTIN,
				Новый Структура("Номенклатура,Количество,ТребуетВзвешивания",
					СтрокаТЧ.Номенклатура, СтрокаТЧ.Количество, Истина));
		Иначе 
			НоменклатураДляОСУ.Добавить(СтрокаТЧ.Номенклатура);
		КонецЕсли;
		ДобавленныеСтроки.Добавить(НоваяСтрока);
		Если Не ЗначениеЗаполнено(СтрокаТЧ.GTIN) Тогда
			ЕстьОшибки = Истина;
			НоваяСтрока.ТекстОшибкиИС = СтрШаблон(
				НСтр("ru = 'Требуется код маркировки или GTIN потребительской упаковки для автоматического формирования кода ОСУ: %1'"),
				ОбщегоНазначенияИС.ПредставлениеНоменклатуры(СтрокаТЧ.Номенклатура, СтрокаТЧ.Характеристика));
		ИначеЕсли Не СтрокаТЧ.АвтоматическийОСУИС Тогда
			ЕстьОшибки = Истина;
			НоваяСтрока.ТекстОшибкиИС = СтрШаблон(
				НСтр("ru = 'Требуется код маркировки или единственный GTIN потребительской упаковки для автоматического формирования кода ОСУ: %1'"),
				ОбщегоНазначенияИС.ПредставлениеНоменклатуры(СтрокаТЧ.Номенклатура, СтрокаТЧ.Характеристика));
		КонецЕсли;
	КонецЦикла;
	Если ЕстьОшибки Тогда
		Возврат;
	КонецЕсли;
	Описание = ОбщегоНазначенияИС.ОписаниеНоменклатуры(НоменклатураДляОСУ);
	КоличествоКодовПоGTINВесовыхТоваров = Новый Соответствие;
	Для Каждого КлючИЗначение Из НоменклатураДляВесовыхКодов Цикл
		КоличествоКодовПоGTINВесовыхТоваров.Вставить(КлючИЗначение.Ключ,
			ПроверкаИПодборПродукцииИСМПВызовСервера.КоличествоПотребительскихУпаковокПоGTIN(
				КлючИЗначение.Значение, КлючИЗначение.Ключ, ПараметрыСканирования));
	КонецЦикла;
	Для Каждого СтрокаТЧ Из ДобавленныеСтроки Цикл
		Если СтрокаТЧ.ТребуетВзвешивания
			И СтрокаТЧ.Количество <> 0 Тогда
			ДанныеПоВидуПродукции = КоличествоКодовПоGTINВесовыхТоваров.Получить(СтрокаТЧ.ВидПродукции);
			Если ДанныеПоВидуПродукции.ТребуетсяОбновлениеКлючаСессии Тогда
				ЕстьОшибки = Истина;
				СтрокаТЧ.ТекстОшибкиИС = СтрШаблон(
					НСтр("ru ='Требуется код маркировки или доступный ключ сессии сервиса ИС МП для автоматического формирования кода ОСУ весового товара: %1'"),
					ОбщегоНазначенияИС.ПредставлениеНоменклатуры(СтрокаТЧ.Номенклатура, СтрокаТЧ.Характеристика));
				Продолжить;
			КонецЕсли;
			Если ДанныеПоВидуПродукции.КоличествоПотребительскихУпаковокПоGTIN = Неопределено Тогда
				ЕстьОшибки = Истина;
				СтрокаТЧ.ТекстОшибкиИС = СтрШаблон(
					НСтр("ru ='Требуется код маркировки. Сервис ИС МП не предоставил количество по GTIN %1 для автоматического формирования кода ОСУ весового товара: %2'"),
					СтрокаТЧ.ЗначениеШтрихкода,
					ОбщегоНазначенияИС.ПредставлениеНоменклатуры(СтрокаТЧ.Номенклатура, СтрокаТЧ.Характеристика));
				Продолжить;
			Иначе
				ОписаниеШтрихкода = ДанныеПоВидуПродукции.КоличествоПотребительскихУпаковокПоGTIN.Получить(СтрокаТЧ.ЗначениеШтрихкода);
				Если ОписаниеШтрихкода = Неопределено Тогда
					ЕстьОшибки  = Истина;
					СтрокаТЧ.ТекстОшибкиИС = СтрШаблон(
						НСтр("ru ='Требуется код маркировки. Сервис ИС МП не предоставил количество по GTIN %1 для автоматического формирования кода ОСУ весового товара: %2'"),
						СтрокаТЧ.ЗначениеШтрихкода,
						ОбщегоНазначенияИС.ПредставлениеНоменклатуры(СтрокаТЧ.Номенклатура, СтрокаТЧ.Характеристика));
				Иначе
					СтрокаТЧ.КоличествоВПотребительскойУпаковке = ОписаниеШтрихкода.КоличествоПодобрано / ОписаниеШтрихкода.КоличествоВложенныхЕдиниц;
				КонецЕсли;
			КонецЕсли;
		Иначе
			СтрокаТЧ.КоличествоВПотребительскойУпаковке = Описание.Получить(СтрокаТЧ.Номенклатура).КоличествоВПотребительскойУпаковке;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
