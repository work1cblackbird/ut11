
#Область СлужебныеПроцедурыИФункции

// Серверный обработчик события ОбработкаПолученияФормы справочника Организации.
//
Процедура ОрганизацииОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка) Экспорт
	
	Если ВидФормы = "ФормаСписка" И Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		
		Параметры.Вставить("Ключ", ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию());
		ВыбраннаяФорма = "ФормаЭлемента";
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

// Серверный обработчик события ОбработкаПолученияДанныхВыбора справочника Организации.
//
Процедура ОрганизацииОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	
	Если Параметры.Свойство("ВыборКонтрагентовИОрганизаций") 
		И ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями") 
		И Параметры.СтрокаПоиска <> Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = Справочники.Контрагенты.ПолучитьДанныеВыбора(Параметры);
		Возврат;
	КонецЕсли;
	
	ИспользоватьУправленческуюОрганизацию = ПолучитьФункциональнуюОпцию("ИспользоватьУправленческуюОрганизацию");
	
	Если Параметры.Свойство("Отбор") Тогда
		Параметры.Отбор.Вставить("Статус", Перечисления.СтатусыОрганизаций.Действует);
	Иначе
		Параметры.Вставить("Отбор", Новый Структура("Статус", Перечисления.СтатусыОрганизаций.Действует));
	КонецЕсли;

	Если НЕ ИспользоватьУправленческуюОрганизацию
		ИЛИ Параметры.Свойство("ВыборУправленческойОрганизации")
		ИЛИ Параметры.Свойство("ХозяйственнаяОперация")
		ИЛИ Параметры.Свойство("РежимВыбораВзаимосвязанныхОрганизаций") 
		ИЛИ Параметры.Свойство("НеВыбиратьОрганизацию") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Запрос 		= Новый Запрос;
		
		МассивДополнительныйОтбор = Новый Массив;
		НомерОтбора = 0;
		
		Если Параметры.Свойство("Отбор") Тогда
			
			Реквизиты 			 = Метаданные.Справочники.Организации.Реквизиты;
			СтандартныеРеквизиты = Новый Массив;
			Для Каждого Элемент Из Метаданные.Справочники.Организации.СтандартныеРеквизиты Цикл
				СтандартныеРеквизиты.Добавить(Элемент.Имя);
			КонецЦикла;
			
			Для Каждого ТекущийОтбор Из Параметры.Отбор Цикл
				
				Если Реквизиты.Найти(ТекущийОтбор.Ключ) = Неопределено
				 И СтандартныеРеквизиты.Найти(ТекущийОтбор.Ключ) = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				НомерОтбора = НомерОтбора + 1;
				
				МассивДополнительныйОтбор.Добавить("ДанныеДляОбщейФильтрации.Организация." + ТекущийОтбор.Ключ + " В (&ЗначениеОтбора" + СокрЛП(НомерОтбора) + ")");
				Запрос.УстановитьПараметр("ЗначениеОтбора" + СокрЛП(НомерОтбора), ТекущийОтбор.Значение);
				
			КонецЦикла;
			
		КонецЕсли;
		
		ТекстОтбора = СтрСоединить(МассивДополнительныйОтбор, Символы.ПС + Символы.Таб + "И" + " ");
		
		ТекстИсключенияОрганизации = "";
		Если Параметры.Свойство("НеВыбиратьОрганизацию") Тогда
			ТекстИсключенияОрганизации = "ДанныеДляОбщейФильтрации.Организация <> &НеВыбиратьОрганизацию"
		КонецЕсли;
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
		|	ОбособленныеПодразделения.ГоловнаяОрганизация КАК Ссылка
		|ПОМЕСТИТЬ ГоловныеОрганизации
		|ИЗ
		|	Справочник.Организации КАК ОбособленныеПодразделения
		|ГДЕ
		|	ОбособленныеПодразделения.ОбособленноеПодразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ  РАЗРЕШЕННЫЕ
		|	Организации.Ссылка КАК Ссылка,
		|	Организации.Представление,
		|	Организации.Предопределенный,
		|	Организации.Наименование,
		|	ВЫБОР
		|		КОГДА Организации.ОбособленноеПодразделение
		|				ИЛИ ГоловныеОрганизации.Ссылка ЕСТЬ NULL 
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЭтоГоловнаяОрганизация,
		|	Организации.ОбособленноеПодразделение КАК ЭтоОбособленноеПодразделение,
		|	Организации.ГоловнаяОрганизация
		|ПОМЕСТИТЬ ДанныеОрганизаций
		|ИЗ
		|	Справочник.Организации КАК Организации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ГоловныеОрганизации КАК ГоловныеОрганизации
		|		ПО Организации.Ссылка = ГоловныеОрганизации.Ссылка
		|ГДЕ
		|	Организации.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрганизаций.Действует)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляФильтрации.Ссылка,
		|	ДанныеДляФильтрации.Представление,
		|	ДанныеДляФильтрации.Предопределенный,
		|	ДанныеДляФильтрации.Наименование,
		|	ДанныеДляФильтрации.ЭтоГоловнаяОрганизация,
		|	ДанныеДляФильтрации.ЭтоОбособленноеПодразделение,
		|	ДанныеДляФильтрации.ГоловнаяОрганизация,
		|	ВЫБОР
		|		КОГДА ДанныеДляФильтрации.Ссылка <> &Организация
		|				И (ДанныеДляФильтрации.ЭтоГоловнаяОрганизация
		|					ИЛИ ДанныеДляФильтрации.ЭтоОбособленноеПодразделение)
		|				И (&Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|					ИЛИ (ДанныеДляФильтрации.Ссылка = &ГоловнаяОрганизация
		|						ИЛИ ДанныеДляФильтрации.ГоловнаяОрганизация = &Организация
		|						ИЛИ &ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|							И ДанныеДляФильтрации.ГоловнаяОрганизация = &ГоловнаяОрганизация))
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Взаимосвязана
		|ПОМЕСТИТЬ ДанныеДляАльтернативнойФильтрации
		|ИЗ
		|	ДанныеОрганизаций КАК ДанныеДляФильтрации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляФильтрации.Ссылка КАК Организация,
		|	ДанныеДляФильтрации.Предопределенный,
		|	ДанныеДляФильтрации.Наименование,
		|	ДанныеДляФильтрации.Представление
		|ПОМЕСТИТЬ ДанныеДляОбщейФильтрации
		|ИЗ
		|	ДанныеДляАльтернативнойФильтрации КАК ДанныеДляФильтрации
		|ГДЕ
		|	&РежимВыбораВзаимосвязанныхОрганизаций = ""БезОбособленныхПодразделений""
		|	И ДанныеДляФильтрации.Ссылка <> &Организация
		|	И НЕ ДанныеДляФильтрации.ЭтоОбособленноеПодразделение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДляФильтрации.Ссылка,
		|	ДанныеДляФильтрации.Предопределенный,
		|	ДанныеДляФильтрации.Наименование,
		|	ДанныеДляФильтрации.Представление
		|ИЗ
		|	ДанныеДляАльтернативнойФильтрации КАК ДанныеДляФильтрации
		|ГДЕ
		|	(&РежимВыбораВзаимосвязанныхОрганизаций = ""ТолькоВзаимосвязанные""
		|				И ДанныеДляФильтрации.Взаимосвязана)
		|			ИЛИ (&РежимВыбораВзаимосвязанныхОрганизаций = ""ТолькоНеВзаимосвязанные""
		|				И НЕ ДанныеДляФильтрации.Взаимосвязана
		|				И ДанныеДляФильтрации.Ссылка <> &Организация)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДляАльтернативнойФильтрации.Ссылка,
		|	ДанныеДляАльтернативнойФильтрации.Предопределенный,
		|	ДанныеДляАльтернативнойФильтрации.Наименование,
		|	ДанныеДляАльтернативнойФильтрации.Представление
		|ИЗ
		|	ДанныеДляАльтернативнойФильтрации КАК ДанныеДляАльтернативнойФильтрации
		|ГДЕ
		|	&РежимВыбораВзаимосвязанныхОрганизаций = """"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ 
		|	ДанныеДляОбщейФильтрации.Организация
		|ИЗ
		|	ДанныеДляОбщейФильтрации КАК ДанныеДляОбщейФильтрации
		|ГДЕ
		|	НЕ((НЕ &ИспользоватьУправленческуюОрганизацию
		|				ИЛИ &ЗапрещенВыборУправленческойОрганизации)
		|				И ДанныеДляОбщейФильтрации.Предопределенный)
		|	И ДанныеДляОбщейФильтрации.Наименование ПОДОБНО &СтрокаПоиска
		|	И &ДополнительныйОтбор
		|	И &НеВыбиратьОрганизацию
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДанныеДляОбщейФильтрации.Наименование";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДополнительныйОтбор", ?(ТекстОтбора <> "", ТекстОтбора, "ИСТИНА"));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&НеВыбиратьОрганизацию", 
											?(ТекстИсключенияОрганизации <> "", ТекстИсключенияОрганизации, "ИСТИНА"));
											
		Запрос.Текст = ТекстЗапроса;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		// В параметрах выбора запрещен выбор управленческой организации
		ЗапрещенВыборУправленческойОрганизации = (Параметры.Свойство("ВыборУправленческойОрганизации") 
													И НЕ Параметры.ВыборУправленческойОрганизации);
													
		СтрокаПоиска = ?(Параметры.СтрокаПоиска = Неопределено, "", Параметры.СтрокаПоиска);
		
		РежимВыбораВзаимосвязанныхОрганизаций = "";
		Если Параметры.Свойство("РежимВыбораВзаимосвязанныхОрганизаций") Тогда
			Если Параметры.РежимВыбораВзаимосвязанныхОрганизаций = "ДляРегистрацииВНалоговомОргане" Тогда
				Если ОрганизацииЛокализация.ВозможнаРегистрацияДляОбособленныхПодразделений() Тогда
					РежимВыбораВзаимосвязанныхОрганизаций = "";
				Иначе
					РежимВыбораВзаимосвязанныхОрганизаций = "БезОбособленныхПодразделений";
				КонецЕсли;
			Иначе
				РежимВыбораВзаимосвязанныхОрганизаций = Параметры.РежимВыбораВзаимосвязанныхОрганизаций;
			КонецЕсли;
		ИначеЕсли Параметры.Свойство("ХозяйственнаяОперация") Тогда
			Если Параметры.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеТоваровМеждуФилиалами
				ИЛИ Параметры.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПрочихРасходовМеждуФилиалами
				ИЛИ Параметры.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеПолуфабрикатовМеждуФилиалами
				ИЛИ Параметры.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПриемПередачаРаботМеждуФилиалами Тогда
				РежимВыбораВзаимосвязанныхОрганизаций = "ТолькоВзаимосвязанные";
			КонецЕсли;
		КонецЕсли;
		
		Организация			= ?(Параметры.Свойство("Организация"), Параметры.Организация, Справочники.Организации.ПустаяСсылка());
		ГоловнаяОрганизация	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ГоловнаяОрганизация");
		
		Запрос.УстановитьПараметр("ИспользоватьУправленческуюОрганизацию", 	ИспользоватьУправленческуюОрганизацию);
		Запрос.УстановитьПараметр("ЗапрещенВыборУправленческойОрганизации", ЗапрещенВыборУправленческойОрганизации);
		Запрос.УстановитьПараметр("СтрокаПоиска", 							СтрокаПоиска + "%");
		Запрос.УстановитьПараметр("РежимВыбораВзаимосвязанныхОрганизаций", 	РежимВыбораВзаимосвязанныхОрганизаций);
		Запрос.УстановитьПараметр("Организация", 							Организация);
		Запрос.УстановитьПараметр("ГоловнаяОрганизация", 					ГоловнаяОрганизация);
		
		Если Параметры.Свойство("НеВыбиратьОрганизацию") Тогда
			Запрос.УстановитьПараметр("НеВыбиратьОрганизацию", Параметры.НеВыбиратьОрганизацию);
		КонецЕсли;
			 
		
		ОрганизацииЛокализация.УстановитьПараметрыЗапросаПриОбработкеПолученияДанныхВыбора(Параметры, Запрос);
		
		ДанныеВыбора = Новый СписокЗначений;
		ДанныеВыбора.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Организация"));

	КонецЕсли;	
	
КонецПроцедуры


#КонецОбласти