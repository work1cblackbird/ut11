#Область ПрограммныйИнтерфейс

#Область РегистрыЗаданий

// Формирует параметры открытия форм из формы закрытия месяца
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - форма закрытия месяца.
// Возвращаемое значение:
// 	Структура, Неопределено - структура параметров:
// * Отбор - Структура - структура отбора:
// ** Организация - СправочникСсылка.Организации - организация для отбора.
Функция ПараметрыФормыРегистраЗаданий(Форма) Экспорт
	
	ОрганизацияДляОтбора = ОрганизацияИзФиксированногоМассива(Форма.ОбщийМассивОрганизаций);
	
	Если ЗначениеЗаполнено(ОрганизацияДляОтбора) Тогда
		ПараметрыОтбораРегистра = Новый Структура("Организация", ОрганизацияДляОтбора);
		ПараметрыФормыРегистра  = Новый Структура("Отбор", ПараметрыОтбораРегистра);
	Иначе
		ПараметрыФормыРегистра = Неопределено;
	КонецЕсли;
	
	Возврат ПараметрыФормыРегистра;
	
КонецФункции

// Получает организацию из массива организация
// 
// Параметры:
// 	ОбщийМассивОрганизаций - Произвольный - реквизит формы
// Возвращаемое значение:
// 	СправочникСсылка.Организации - ссылка на организацию
Функция ОрганизацияИзФиксированногоМассива(ОбщийМассивОрганизаций) Экспорт
	Если НЕ ЗначениеЗаполнено(ОбщийМассивОрганизаций)
	 ИЛИ ОбщийМассивОрганизаций.Количество() <> 1 Тогда
		Организация = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
	Иначе
		Организация = ОбщийМассивОрганизаций[0];
	КонецЕсли;
	
	Возврат Организация;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиКоманд


// Выполнение команды формы операций закрытия месяца.
// 
// Параметры:
// 	Команда - КомандаФормы - 
// 	Форма - ФормаКлиентскогоПриложения - 
//
Процедура ВыполнитьКомандуЛокализации(Команда, Форма) Экспорт
	
	//++ Локализация

	
	Если Команда.Имя = "ОтчетСверкаДанныхПартионногоУчетаИСебестоимости" Тогда
		ОтчетСверкаДанныхПартионногоУчетаИСебестоимости(Команда, Форма);
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиКомандФормы_Служебные

//++ Локализация

Процедура ОтчетСверкаДанныхПартионногоУчетаИСебестоимости(Команда, Форма)
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Организация", ОрганизацияИзФиксированногоМассива(Форма.ОбщийМассивОрганизаций));
	ПараметрыОтчета.Вставить("Период",
		Новый СтандартныйПериод(НачалоМесяца(Форма.Объект.ПериодРегистрации), КонецМесяца(Форма.Объект.ПериодРегистрации)));
		
	ПараметрыФормы = Новый Структура("Отбор, СформироватьПриОткрытии", ПараметрыОтчета, Истина);
	
	ОткрытьФорму(
		"Отчет.СверкаДанныхПартионногоУчетаИСебестоимости.ФормаОбъекта",
		ПараметрыФормы,
		Форма,
		Форма.УникальныйИдентификатор);
	
КонецПроцедуры

//-- Локализация

#КонецОбласти

#КонецОбласти
