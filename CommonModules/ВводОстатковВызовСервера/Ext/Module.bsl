#Область ПрограммныйИнтерфейс

// Значения реквизитов для заполнения документа ввода остатков.
// 
// Возвращаемое значение:
// 	Структура - Структура содержащая имена реквизитов документа ввода остатков и значения их заполнения:
//       * Комментарий                                - Строка - Комментарий, который будет установлен создаваемого документа
//       * ДатаДокумента                             - Дата - Дата, которая будет установлена создаваемому документу
//       * ОтражатьВУУ                                - Булево - Признак отражения в управленческом учете
//       * ОтражатьВБУиНУ - Булево        - Признак отражения в регламентированном и налоговом учете
//       * ОтражатьВОперативномУчете - Булево - Признак отражения в оперативном учете
//       * Организация                                 - СправочникСсылка.Организации - Организация, для которой вводится остатки
//       * ХозяйственнаяОперация            - ПеречислениеСсылка.ХозяйственныеОперации - Хозяйственная операция ввода остатков.
//
Функция ИнициализироватьЗначенияЗаполненияДокументаВводОстатков() Экспорт

	Возврат ВводОстатковСервер.ИнициализироватьЗначенияЗаполненияДокументаВводОстатков();

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает представление документа ввода остатков.
// 
// Параметры:
// 	Документ - ДокументСсылка.ВводОстатков - Ссылка на документ ввода остатков.
// 	НомерДокумента - Строка - Номер документа ввода остатков.
// 	ДатаДокумента - Дата - Дата документа ввода остатков.
// 	ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - Хозяйственная операция документа ввода остатков.
// Возвращаемое значение:
// 	Строка - Строковое представление документа ввода остатков.
//
Функция ЗаголовокДокументаВводОстатковПоХозяйственнойОперации(Документ, НомерДокумента, ДатаДокумента, ХозяйственнаяОперация) Экспорт
	
	Если Не ЗначениеЗаполнено(ХозяйственнаяОперация) Тогда
		Возврат "";
	КонецЕсли;
	
	ШаблонЗаголовка = "%1";
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковВАвтономныхКассахККМКОформлениюОтчетовОРозничныхПродажах
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковВАвтономныхКассахККМПоРозничнойВыручке
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковВКассах Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод начальных остатков в кассах %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковЗадолженностиКлиентов Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод начальных остатков задолженности клиентов %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковЗадолженностиПоставщикам Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод начальных остатков задолженности перед поставщиками %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковЗадолженностиПодотчетников Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод начальных остатков задолженности подотчетных лиц %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковПремийКлиентам Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод начальных остатков премий клиентам %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковПерерасходовПодотчетныхСредств Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод начальных остатков перерасходов подотчетных средств %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковНаБанковскихСчетах Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод начальных остатков на банковских счетах %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковАвансовКлиентов Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод начальных остатков авансов, полученных от клиентов %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковАвансовПоставщикам Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод начальных остатков авансов, выданных поставщикам %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковРасчетовМеждуОрганизациямиПоАвансам Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод начальных остатков расчетов между организациями по авансам %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковРасчетовМеждуОрганизациямиПоРеализациям Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод начальных остатков расчетов между организациями по реализациям %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковСобственныхТоваров Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод начальных остатков собственных товаров %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковТоваровПолученныхНаКомиссию Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод остатков товаров, принятых на комиссию %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковТоваровПринятыхНаОтветственноеХранение Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод остатков товаров, принятых на ответственное хранение %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковТоваровПереданныхНаОтветственноеХранение Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод остатков товаров, переданных на ответственное хранение %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковТоваровПереданныхНаКомиссию Тогда
		Постфикс = КомиссионнаяТорговляСервер.ПостфиксСхемыКомиссии20();
		ШаблонЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ввод остатков товаров, переданных на комиссию %1 %2'"),
			Постфикс,
			"%1");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковТоваровПереданныхНаКомиссию2_5 Тогда
		Если Не ПолучитьФункциональнуюОпцию("ИспользуетсяКомиссионнаяПродажа20")
			Или ПолучитьФункциональнуюОпцию("ТолькоКомиссионныеПродажи25") Тогда
			ШаблонЗаголовка = НСтр("ru = 'Ввод остатков товаров, переданных на комиссию %1'");
		Иначе
			Постфикс = КомиссионнаяТорговляСервер.ПостфиксСхемыКомиссии25();
			ШаблонЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ввод остатков товаров, переданных на комиссию %1 %2'"),
				Постфикс,
				"%1");
		КонецЕсли;
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковВозвратнойТарыПереданнойКлиентам Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод начальных остатков переданной клиентам возвратной тары %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковВозвратнойТарыПринятойОтПоставщиков Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод начальных остатков принятой от поставщиков возвратной тары %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковПриПереходеНаИспользованиеАдресногоХраненияОстатков Тогда
		ШаблонЗаголовка = НСтр("ru = 'Переход на использование адресного хранения остатков %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковПриПереходеНаИспользованиеСкладскихПомещений Тогда
		ШаблонЗаголовка = НСтр("ru = 'Переход на использование складских помещений %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковМатериаловПереданныхВПроизводство Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод начальных остатков переданных в производство материалов %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковДенежныхСредствКПоступлениюОтЭквайера Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод остатков денежных средств к поступлению от эквайера %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковПрочихАктивовПассивов Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод начальных остатков прочих активов и пассивов %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковПодарочныхСертификатов Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод начальных остатков подарочных сертификатов %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковПоДоговорамКредитовИДепозитов Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод начальных остатков расчетов по кредитам, депозитам и выданным займам %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковТМЦВЭксплуатации Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод начальных остатков ТМЦ в эксплуатации %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковПрочихРасходов Тогда
		ШаблонЗаголовка = НСтр("ru = 'Ввод начальных остатков прочих расходов %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковФинансовогоРезультатаЗаПрошлыеПериоды Тогда
		ШаблонЗаголовка = НСтр("ru = 'Финансовый результат за прошлые периоды %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковОптовыхПродажЗаПрошлыеПериоды Тогда
		ШаблонЗаголовка = НСтр("ru = 'Оптовые продажи за прошлые периоды %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковРозничныхПродажЗаПрошлыеПериоды Тогда
		ШаблонЗаголовка = НСтр("ru = 'Розничные продажи за прошлые периоды %1'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковНДСПоПриобретеннымЦенностям Тогда
		ШаблонЗаголовка = НСтр("ru = 'Входящий НДС по товарам, работам и услугам %1'");
	
	КонецЕсли;

	Если ТипЗнч(Документ) = Тип("Структура") И ЗначениеЗаполнено(Документ.Ссылка) Тогда
		ОкончаниеЗаголовка = НСтр("ru = '%1 от %2'");
		ОкончаниеЗаголовка = СтрШаблон(ОкончаниеЗаголовка, НомерДокумента, ДатаДокумента);
	ИначеЕсли ЗначениеЗаполнено(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Ссылка")) Тогда
		ОкончаниеЗаголовка = НСтр("ru = '%1 от %2'");
		ОкончаниеЗаголовка = СтрШаблон(ОкончаниеЗаголовка, НомерДокумента, ДатаДокумента);
	Иначе
		ОкончаниеЗаголовка = НСтр("ru = '(создание)'");
	КонецЕсли;
	
	Возврат СтрШаблон(ШаблонЗаголовка, ОкончаниеЗаголовка);

КонецФункции

Функция ПолучитьПараметрыКопированияДокумента(ХозяйственнаяОперация) Экспорт
	Возврат ВводОстатковСервер.ПолучитьПараметрыКопированияДокумента(ХозяйственнаяОперация);
КонецФункции
	
Функция ПолучениеФормыВводаОстатковОПродажахЗаПрошлыеПериоды(ХозяйственнаяОперация) Экспорт
	Возврат ВводОстатковСервер.ПолучениеФормыВводаОстатковОПродажахЗаПрошлыеПериоды(ХозяйственнаяОперация);
КонецФункции
#КонецОбласти
