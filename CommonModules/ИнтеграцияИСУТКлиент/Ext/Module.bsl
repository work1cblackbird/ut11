// Универсальные механизмы интеграции ИС (ЕГАИС, ГИСМ, ВЕТИС, ...)

#Область ПрограммныйИнтерфейс

#Область Прочие

// Обработчик команды "Разбить строку".
//
//	Параметры:
//		ТЧ - ДанныеФормыКоллекция
//		ЭлементФормы - ТаблицаФормы
//		ОповещениеПослеРазбиения - ОписаниеОповещения
//		ПараметрыРазбиенияСтроки - см. ОбщегоНазначенияУТКлиент.ПараметрыРазбиенияСтроки.
//
Процедура РазбитьСтрокуТабличнойЧасти(ТЧ, ЭлементФормы, ОповещениеПослеРазбиения, ПараметрыРазбиенияСтроки) Экспорт
	
	ПараметрыРазбиения = РаботаСТабличнымиЧастямиКлиент.ПараметрыРазбиенияСтроки();
	ЗаполнитьЗначенияСвойств(ПараметрыРазбиения, ПараметрыРазбиенияСтроки);
	РаботаСТабличнымиЧастямиКлиент.РазбитьСтроку(ТЧ, ЭлементФормы, ОповещениеПослеРазбиения, ПараметрыРазбиения);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьВыборФормыСканирования(Выбор, ДополнительныеПараметры) Экспорт
	
	Если Выбор = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранныйВидПродукции = Выбор.Значение;
	Если ВыбранныйВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная") Тогда
		ПроверкаИПодборПродукцииЕГАИСКлиент.ОткрытьФормуСканированияАлкогольнойПродукции(ДополнительныеПараметры.Форма);
	Иначе
		ПроверкаИПодборПродукцииИСМПКлиент.ОткрытьФормуПроверкиИПодбора(
			ДополнительныеПараметры.Форма, ВыбранныйВидПродукции, ДополнительныеПараметры.Форма.ПараметрыУказанияСерий);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьВыборДокументовМаркировки(ДанныеВыбора, ДополнительныеПараметры) Экспорт
	
	Если ДанныеВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("ИмяКоманды", "ЗаполнитьПоДокументамМаркировки");
	ПараметрыОбработки.Вставить("ДанныеВыбора", ДанныеВыбора);
	ДополнительныеПараметры.Форма.Подключаемый_ВыполнитьПереопределяемуюКомандуНаСервере(ПараметрыОбработки);
	
КонецПроцедуры

Функция ПользовательРазрешилРедактированиеРеквизита(Форма, ИмяРеквизита = "ЕдиницаИзмерения") Экспорт
	
	ЗаблокированныеРеквизиты           = ЗапретРедактированияРеквизитовОбъектовКлиент.Реквизиты(Форма, Истина);
	ПользовательРазрешилРедактирование = (ЗаблокированныеРеквизиты.Найти(ИмяРеквизита) = Неопределено);
	
	Возврат ПользовательРазрешилРедактирование;
	
КонецФункции

Процедура ЗаполнениеУпаковкиЧастичногоВыбытияПоДаннымиГИСМТ(Форма) Экспорт
	
	ПараметрыЗапроса = ИнтерфейсИСМПОбщегоНазначенияКлиент.ПараметрыЗапросаДанных();
	ПараметрыЗапроса.Параметры = Новый Структура();
	ПараметрыЗапроса.Параметры.Вставить("Номенклатура", Форма.Объект.Ссылка);
	
	ПараметрыЗавершения = Новый Структура();
	ПараметрыЗавершения.Вставить("Форма",                   Форма);
	ПараметрыЗавершения.Вставить("ПотребительскаяУпаковка", Форма.ПотребительскаяУпаковкаИС);
	ПараметрыЗавершения.Вставить("Номенклатура",            Форма.Объект.Ссылка);
	
	ПараметрыЗапроса.ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ДанныеПродукцииПоШтрихкодуЗавершение",
		ЭтотОбъект,
		ПараметрыЗавершения);
	
	ИнтерфейсИСМПОбщегоНазначенияКлиент.ЕмкостьУпаковкиПоGTIN(ПараметрыЗапроса);
	
КонецПроцедуры

Процедура ДанныеПродукцииПоШтрихкодуЗавершение(ДанныеРезультата, ПараметрыЗавершения) Экспорт
	
	Если ДанныеРезультата.ЕстьОшибки Тогда
		ПоказатьПредупреждение(, ДанныеРезультата.ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	ЕмкостиУпаковки = Новый Соответствие();
	Для Каждого КлючИЗначение Из ДанныеРезультата.Результат Цикл
		Если ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
			ЕмкостиУпаковки.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЕсли;
	КонецЦикла;
	
	ЕмкостьУпаковки = Неопределено;
	Если ЕмкостиУпаковки.Количество() > 1 Тогда
		Тексты = Новый Массив();
		Для Каждого КлючИЗначение Из ЕмкостиУпаковки Цикл
			Тексты.Добавить(СтрШаблон("%1: %2", КлючИЗначение.Ключ, КлючИЗначение.Значение));
		КонецЦикла;
		ПоказатьПредупреждение(, СтрШаблон(
			НСтр("ru = 'В ГИС МТ по GTIN найдено более одной емкости потребительской упаковки:
				       |%1'"),
			СтрСоединить(Тексты, Символы.ПС)));
		Возврат;
	ИначеЕсли ЕмкостиУпаковки.Количество() = 1 Тогда
		Для Каждого КлючИЗначение Из ЕмкостиУпаковки Цикл
			ЕмкостьУпаковки = КлючИЗначение.Значение;
		КонецЦикла;
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'В ГИС МТ по GTIN не найдено ни одного значения емкости потребительской упаковки.'"));
		Возврат;
	КонецЕсли;
	
	Номенклатура   = ПараметрыЗавершения.Номенклатура;
	Форма          = ПараметрыЗавершения.Форма;
	ДанныеУпаковки = ИнтеграцияИСВызовСервераУТ.ДанныеУпаковкиНоменклатурыЧастичногоВыбытия(
		Номенклатура,
		ПараметрыЗавершения.ПотребительскаяУпаковка,
		ЕмкостьУпаковки);
	
	Если ДанныеУпаковки.Упаковка = Неопределено Тогда
		
		ПараметрыСоздания = Новый Структура( );
		ПараметрыСоздания.Вставить("Владелец",              Номенклатура);
		ПараметрыСоздания.Вставить("ЗначенияЗаполнения",    Новый Структура());
		
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("ФормаНоменклатуры", Форма);
		
		ОповещениеЗакрытияНовойУпаковки = Новый ОписаниеОповещения(
			"ЗаписьУпаковкиЧастичногоВыбытияЗавершение",
			ЭтотОбъект,
			ДополнительныеПараметры);
		
		ПараметрыСоздания.ЗначенияЗаполнения.Вставить("ТипУпаковки", ДанныеУпаковки.ТипУпаковки);
		ПараметрыСоздания.ЗначенияЗаполнения.Вставить("Числитель",   ДанныеУпаковки.Числитель);
		ПараметрыСоздания.ЗначенияЗаполнения.Вставить("Знаменатель", ДанныеУпаковки.Знаменатель);
		ПараметрыСоздания.ЗначенияЗаполнения.Вставить("КоличествоУпаковок",    ДанныеУпаковки.КоличествоУпаковок);
		ПараметрыСоздания.ЗначенияЗаполнения.Вставить("ТипИзмеряемойВеличины", ПредопределенноеЗначение("Перечисление.ТипыИзмеряемыхВеличин.Упаковка"));
		ПараметрыСоздания.ЗначенияЗаполнения.Вставить("Владелец",              Номенклатура);
		
		Если ДанныеУпаковки.ТипУпаковки = ПредопределенноеЗначение("Перечисление.ТипыУпаковокНоменклатуры.Составная") Тогда
			ПараметрыСоздания.ЗначенияЗаполнения.Вставить("Родитель", ПараметрыЗавершения.ПотребительскаяУпаковка);
		КонецЕсли;
		
		ОткрытьФорму(
			"Справочник.УпаковкиЕдиницыИзмерения.Форма.ФормаЭлементаУпаковки",
			ПараметрыСоздания,
			Форма,,,,
			ОповещениеЗакрытияНовойУпаковки,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
		Оповестить("ЗаполнитьПараметрыНовойУпаковкиЧастичногоВыбытия", ПараметрыСоздания.ЗначенияЗаполнения, Форма);
		
	Иначе
		Форма.УпаковкаЧастичногоВыбытияИС = ДанныеУпаковки.Упаковка;
		Форма.Модифицированность          = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыНовойУпаковкиЧастичногоВыбытия(Форма, Параметры) Экспорт
	
	Объект                    = Форма.Объект;
	Объект.КоличествоУпаковок = Параметры.КоличествоУпаковок;
	Объект.Числитель          = Параметры.Числитель;
	Объект.Знаменатель        = Параметры.Знаменатель;
	
	Если ТипЗнч(Форма.ОписаниеОповещенияОЗакрытии.ДополнительныеПараметры) = Тип("Структура") Тогда
		Форма.ОписаниеОповещенияОЗакрытии.ДополнительныеПараметры.Вставить("ФормаУпаковки", Форма);
	КонецЕсли;
	
	Оповестить("УпаковкиНоменклатуры_ОтработатьЛогикуСвязиРеквизитов",, Форма);
	
КонецПроцедуры

Процедура ЗаписьУпаковкиЧастичногоВыбытияЗавершение(Упаковка, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(ДополнительныеПараметры) <> Тип("Структура")
		Или Не ДополнительныеПараметры.Свойство("ФормаУпаковки")
		Или ДополнительныеПараметры.ФормаУпаковки.Объект.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Упаковка = ДополнительныеПараметры.ФормаУпаковки.Объект.Ссылка;
	ДополнительныеПараметры.ФормаНоменклатуры.УпаковкаЧастичногоВыбытияИС = Упаковка;
	ДополнительныеПараметры.ФормаНоменклатуры.Модифицированность          = Истина;
	
КонецПроцедуры

#КонецОбласти
