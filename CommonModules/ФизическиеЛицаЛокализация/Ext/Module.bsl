////////////////////////////////////////////////////////////////////////////////
// Расширение подсистемы "Физические лица" для УТ и ERP (локализация).
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ФИОФизическихЛиц

// Формирует структуру, содержащую информацию о фамилии, имени и отчестве физического лица.
// Строка формируется в зависимости от типа переданного параметра:
//	- либо по истории ФИО элемента справочника ФизическиеЛица,
// 	- либо разбором строки, содержащей полную ФИО.
//
// Параметры:
//  ФизическоеЛицо - Строка, СправочникСсылка.ФизическиеЛица - источник информации о ФИО
//  ФИОФизическогоЛица - Структура - со свойствами
//		Фамилия  - Строка;
//		Имя      - Строка;
//		Отчество - Строка.
//  Дата - Дата, Неопределено - дата для получения истории ФИО, имеет смысл если в первом параметре передана ссылка.
//
Процедура ДополнитьФИО(ФизическоеЛицо, ФИОФизическогоЛица, Дата = Неопределено) Экспорт
	//++ Локализация
	Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		
		Если ТипЗнч(ФизическоеЛицо) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			
			// Получим срез регистра сведений ФИОФизическихЛиц на нужную дату
			УстановитьПривилегированныйРежим(Истина);
			
			ТаблицаСрезПоследних = РегистрыСведений.ФИОФизическихЛиц.СрезПоследних(
				?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса()),
				Новый Структура("ФизическоеЛицо", ФизическоеЛицо));
			
			УстановитьПривилегированныйРежим(Ложь);
			
			Если ТаблицаСрезПоследних.Количество() > 0 Тогда
				// Если в регистре есть данные, то заполним возвращаемую структуру
				ЗаполнитьЗначенияСвойств(ФИОФизическогоЛица, ТаблицаСрезПоследних[0]);
			КонецЕсли;
			
		Иначе
			
			// Результат получаем разбирая строку, содержащую полную ФИО
			ЗаполнитьЗначенияСвойств(
				ФИОФизическогоЛица,
				ФизическиеЛицаКлиентСервер.ЧастиИмени(СокрЛП(ФизическоеЛицо)));
			
		КонецЕсли;
		
	КонецЕсли;
	//-- Локализация
КонецПроцедуры

// Формирует строку, содержащую фамилию и инициалы физического лица.
// Строка формируется в зависимости от типа переданного параметра:
//	- либо по истории ФИО элемента справочника ФизическиеЛица,
// 	- либо разбором строки, содержащей полную ФИО.
//
// Параметры:
//  ФизическоеЛицо - Строка, СправочникСсылка.ФизическиеЛица - источник информации о ФИО
//  ФамилияИнициалы - Строка - Фамилия И. О.
//  Дата - Дата, Неопределено - дата для получения истории ФИО, имеет смысл если в первом параметре передана ссылка.
//
Процедура ДополнитьФамилияИнициалыФизЛица(ФизическоеЛицо, ФамилияИнициалы, Дата = Неопределено) Экспорт
	
	//++ Локализация
	Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		
		Если ТипЗнч(ФизическоеЛицо) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			
			// Получаем фамилию, имя, отчество на указанную дату
			ФИОФизическогоЛица = ФизическиеЛицаУТ.ФамилияИмяОтчество(ФизическоеЛицо, Дата);
			
			// Результат получаем из отдельных частей - фамилии, имени, отчества
			ФамилияИнициалы = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ФИОФизическогоЛица);
			
		Иначе	
			
			// Результат получаем разбирая строку, содержащую полную ФИО
			ФИОФизическогоЛица = ФизическиеЛицаКлиентСервер.ЧастиИмени(СокрЛП(ФизическоеЛицо));
			ФамилияИнициалы = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ФИОФизическогоЛица);
			
		КонецЕсли;
		
	КонецЕсли;
	//-- Локализация
КонецПроцедуры

#КонецОбласти

#Область ДокументыФизическихЛиц

// Заполнить структуру данными документа физического лица.
// 
// Параметры:
// Параметры:
//  ФизическоеЛицо - СправочникСсылка.ФизическиеЛица - источник информации о документе
//  ДокументФизическогоЛица - Структура -- данные документа физического лица,
// 				поля соответствуют ресурсам регистра ДокументыФизическихЛиц.
//  ВидДокумента - СправочникСсылка.ВидыДокументовФизическихЛиц, Неопределено - вид документа физического лица
//  Дата - Дата, Неопределено - дата для получения истории документов физического лица.
//
Процедура ЗаполнитьДокументФизическогоЛицаПоУмолчанию(ФизическоеЛицо, ДокументФизическогоЛица, ВидДокумента = Неопределено, Дата = Неопределено) Экспорт
	
	//++ Локализация
	Если ЗначениеЗаполнено(ФизическоеЛицо)
		И Не ЗначениеЗаполнено(ВидДокумента)
		И Не ЗначениеЗаполнено(ДокументФизическогоЛица.ВидДокумента) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 2
		|	ДокументыФизическихЛиц.ВидДокумента,
		|	ДокументыФизическихЛиц.ВидДокумента.КодМВД КАК КодМВД,
		|	ДокументыФизическихЛиц.Серия,
		|	ДокументыФизическихЛиц.Номер,
		|	ДокументыФизическихЛиц.ДатаВыдачи,
		|	ДокументыФизическихЛиц.СрокДействия,
		|	ДокументыФизическихЛиц.КемВыдан,
		|	ДокументыФизическихЛиц.КодПодразделения,
		|	ДокументыФизическихЛиц.ЯвляетсяДокументомУдостоверяющимЛичность,
		|	ДокументыФизическихЛиц.Представление
		|ИЗ
		|	РегистрСведений.ДокументыФизическихЛиц.СрезПоследних(
		|			&Дата,
		|			Физлицо = &ФизическоеЛицо
		|				И ВидДокумента = &ВидДокумента) КАК ДокументыФизическихЛиц";
		
		Запрос.УстановитьПараметр("Дата", ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса()));
		Запрос.УстановитьПараметр("ВидДокумента", Справочники.ВидыДокументовФизическихЛиц.ПаспортРФ);
		Запрос.УстановитьПараметр("ФизическоеЛицо", ФизическоеЛицо);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(ДокументФизическогоЛица, Выборка);
		КонецЕсли;
		
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

// Обрабатывает свойства элемента ВидДокумента формы обработки ПечатьЗаявленияНаВозвратТоваровОтКлиента.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
Процедура РеквизитыПечатиЗаявленияНаВозвратОбработатьВидДокумента(Форма) Экспорт
	
	//++ Локализация
	ЭлементыФормы = Форма.Элементы;
	ЭлементыФормы.ВидДокумента.КнопкаВыбора = Ложь; 
	ЭлементыФормы.ВидДокумента.КнопкаОткрытия = Ложь;
	ЭлементыФормы.ВидДокумента.РежимВыбораИзСписка = Истина;
	
	СписокВыбора = ЭлементыФормы.ВидДокумента.СписокВыбора; //СписокЗначений
	СписокВыбора.Добавить(Справочники.ВидыДокументовФизическихЛиц.ВодительскоеУдостоверение);
	СписокВыбора.Добавить(Справочники.ВидыДокументовФизическихЛиц.ВоенныйБилет);
	СписокВыбора.Добавить(Справочники.ВидыДокументовФизическихЛиц.ВоенныйБилетОфицераЗапаса);
	СписокВыбора.Добавить(Справочники.ВидыДокументовФизическихЛиц.ДипломатическийПаспорт);
	СписокВыбора.Добавить(Справочники.ВидыДокументовФизическихЛиц.ЗагранпаспортРФ);
	СписокВыбора.Добавить(Справочники.ВидыДокументовФизическихЛиц.ЗагранпаспортСССР);
	СписокВыбора.Добавить(Справочники.ВидыДокументовФизическихЛиц.ПаспортРФ);
	СписокВыбора.Добавить(Справочники.ВидыДокументовФизическихЛиц.ПаспортСССР);
	СписокВыбора.Добавить(Справочники.ВидыДокументовФизическихЛиц.ПаспортМинморфлота);
	СписокВыбора.Добавить(Справочники.ВидыДокументовФизическихЛиц.ПаспортМоряка);
	СписокВыбора.Добавить(Справочники.ВидыДокументовФизическихЛиц.СвидетельствоОРождении);
	СписокВыбора.Добавить(Справочники.ВидыДокументовФизическихЛиц.УдостоверениеОфицера);
	//-- Локализация

КонецПроцедуры

#КонецОбласти

#КонецОбласти
