#Область ПрограммныйИнтерфейс

// Заполняет признак использования торговых объектов для контрагентов.
//
// Параметры:
//  Использовать - Булево - признак использования торговых объектов.
Процедура ИспользоватьТорговыеОбъектыКонтрагентов(Использовать) Экспорт
	
	//++ НЕ ГОСИС
	Использовать = Не ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов");
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

// Заполняет торговый объект из контрагента.
//
// Параметры:
//  ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС        - Торговый объект.
//  Контрагент     - ОпределяемыйТип.ОрганизацияКонтрагентГосИС - Контрагент, для которого необходимо получить торговый объект.
Процедура ТорговыйОбъектКонтрагента(ТорговыйОбъект, Контрагент) Экспорт
	
	//++ НЕ ГОСИС
	ТорговыйОбъект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "Партнер");
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

// Заполняет контрагента из торгового объекта.
//
// Параметры:
//  Контрагент     - ОпределяемыйТип.ОрганизацияКонтрагентГосИС - контрагент торгового объекта.
//  ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС - Торговый объект, для которого необходимо получить контрагента.
Процедура КонтрагентТорговогоОбъекта(Контрагент, ТорговыйОбъект) Экспорт
	
	//++ НЕ ГОСИС
	Контрагент = ПартнерыИКонтрагенты.ПолучитьКонтрагентаПартнераПоУмолчанию(ТорговыйОбъект);
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

// Поиск контрагента и торгового объекта контрагента (партнера) по ИНН и/или КПП.
//
// Параметры:
//  ВозвращаемоеЗначение - Неопределено - результат поиска (не найден),
//                       - Структура    - результат поиска:
//   * Контрагент     - ОпределяемыйТип.ОрганизацияКонтрагентГосИС - найденный контрагент,
//   * ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС        - найденный торговый объект контрагента (партнер).
//  ИНН                  - Строка       - ИНН искомого контрагента,
//  КПП                  - Строка       - КПП искомого контрагента.
Процедура КонтрагентТорговыйОбъектПоИННКПП(ВозвращаемоеЗначение, ИНН, КПП) Экспорт
	
	//++ НЕ ГОСИС
	ВозвращаемоеЗначение = Неопределено;
	
	РезультатПоиска = ПартнерыИКонтрагентыЛокализация.ИННКППУжеИспользуетсяВИнформационнойБазе(ИНН, КПП);
	Если РезультатПоиска.НайденныеСовпадения.Количество() > 0  Тогда
		
		ВозвращаемоеЗначение = Новый Структура;
		ВозвращаемоеЗначение.Вставить("Контрагент",     РезультатПоиска.НайденныеСовпадения[0].Ссылка);
		ВозвращаемоеЗначение.Вставить("ТорговыйОбъект", РезультатПоиска.НайденныеСовпадения[0].Партнер);
		
	КонецЕсли;
	//-- НЕ ГОСИС
	Возврат;
	
КонецПроцедуры

#КонецОбласти