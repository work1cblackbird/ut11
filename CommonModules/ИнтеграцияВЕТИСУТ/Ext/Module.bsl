#Область ПрограммныйИнтерфейс

// Копирует связь между номенклатурой и продукцией ВЕТИС для новой позиции номенклатуры.
//
// Параметры:
//	Приемник						  - СправочникОбъект.Номенклатура - Номенклатура, для которой копируется связь.
//	Источник						  - СправочникСсылка.Номенклатура - Номенклатура для которой существует связь.
//	СоответствияСкопированныхОбъектов - Соответствие Из Произвольный - соответствие характеристик номенклатуры-приемника характеристикам номенклатуры-источника.
//
Процедура КопироватьСвязьСКлассификаторомПродукцииВЕТИС(Приемник, Источник, СоответствияСкопированныхОбъектов) Экспорт
	
	ВариантПереносаНастроекПоХарактеристикам =
	НоменклатураСервер.ВариантПереносаНастроекПоХарактеристикам(Приемник,
		Источник,
		СоответствияСкопированныхОбъектов);
	
	Если ВариантПереносаНастроекПоХарактеристикам = "НеПереносить" Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоответствиеНоменклатурыЕГАИС.Продукция КАК Продукция,
	|	СоответствиеНоменклатурыЕГАИС.Характеристика КАК Характеристика
	|ИЗ
	|	РегистрСведений.СоответствиеНоменклатурыВЕТИС КАК СоответствиеНоменклатурыЕГАИС
	|ГДЕ
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура = &НоменклатураИсточник";
	
	Запрос.УстановитьПараметр("НоменклатураИсточник", Источник);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	НаборЗаписей = РегистрыСведений.СоответствиеНоменклатурыВЕТИС.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Номенклатура.Установить(Приемник.Ссылка);
	
	Пока Выборка.Следующий() Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Номенклатура         = Приемник.Ссылка;
		Если ВариантПереносаНастроекПоХарактеристикам = "ВзятьИзИсточника" Тогда
			НоваяЗапись.Характеристика       = Выборка.Характеристика;
		ИначеЕсли ВариантПереносаНастроекПоХарактеристикам = "ВзятьИзСоответствия" Тогда
			Характеристика = СоответствияСкопированныхОбъектов.Характеристики.Получить(Выборка.Характеристика);
			Если Характеристика = Неопределено Тогда
				Продолжить;
			Иначе
				НоваяЗапись.Характеристика = Характеристика;
			КонецЕсли;
		КонецЕсли;
		НоваяЗапись.Продукция      = Выборка.Продукция;
	КонецЦикла;
	
	НаборЗаписей.Записать();
		
КонецПроцедуры

// Возвращает единицу измерения по ОКЕИ.
//
// Параметры:
//	КодОКЕИ - ОпределяемыйТип.СтрокаВЕТИС - Код ОКЕИ.
//
// Возвращаемое значение:
//	СправочникСсылка.УпаковкиЕдиницыИзмерения, Неопределено - Единица измерения по ОКЕИ.
//
Функция ЕдиницаИзмеренияОКЕИ(Знач КодОКЕИ) Экспорт
	
	КодОКЕИ = СокрЛП(КодОКЕИ);
		
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕдиницыИзмерения.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.УпаковкиЕдиницыИзмерения КАК ЕдиницыИзмерения
	|ГДЕ
	|	ЕдиницыИзмерения.Код = &КодОКЕИ";
	
	Запрос.УстановитьПараметр("КодОКЕИ", КодОКЕИ);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ЕдиницаИзмерения = Неопределено;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ЕдиницаИзмерения = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат ЕдиницаИзмерения;
	
КонецФункции

// Создает единицу измерения по ОКЕИ.
//
// Параметры:
//	КодОКЕИ - ОпределяемыйТип.СтрокаВЕТИС - Код ОКЕИ.
//
// Возвращаемое значение:
//	СправочникСсылка.УпаковкиЕдиницыИзмерения, Неопределено - Единица измерения по ОКЕИ.
//
Функция СоздатьЕдиницуИзмеренияПоКлассификаторуОКЕИ(КодОКЕИ) Экспорт
	
	ДеревоДанныхКлассификатора = Справочники.УпаковкиЕдиницыИзмерения.ПолучитьДанныеКлассификатора();
	
	ДанныеКлассификатора = ОбщегоНазначенияУТ.ДанныеДерева(ДеревоДанныхКлассификатора, 2);
	СтрокаКлассификатора = ДанныеКлассификатора.Найти(КодОКЕИ, "КодЧисловой");
	
	ЕдиницаИзмерения = Неопределено;
	
	НачатьТранзакцию();
	
	Попытка
		
		Если ЗначениеЗаполнено(СтрокаКлассификатора.УсловноеОбозначениеНациональное) Тогда
			Наименование = СтрокаКлассификатора.УсловноеОбозначениеНациональное;
		ИначеЕсли ЗначениеЗаполнено(СтрокаКлассификатора.УсловноеОбозначениеМеждународное) Тогда
			Наименование = СтрокаКлассификатора.УсловноеОбозначениеМеждународное;
		ИначеЕсли ЗначениеЗаполнено(СтрокаКлассификатора.КодовоеБуквенноеОбозначениеНациональное) Тогда
			Наименование = СтрокаКлассификатора.КодовоеБуквенноеОбозначениеНациональное;
		ИначеЕсли ЗначениеЗаполнено(СтрокаКлассификатора.КодовоеБуквенноеОбозначениеМеждународное) Тогда
			Наименование = СтрокаКлассификатора.КодовоеБуквенноеОбозначениеМеждународное;
		Иначе
			Наименование = СтрокаКлассификатора.Наименование;
		КонецЕсли;
		
		СправочникОбъект = Справочники.УпаковкиЕдиницыИзмерения.СоздатьЭлемент();
		СправочникОбъект.Заполнить(Неопределено);
		
		СправочникОбъект.Владелец                = Справочники.НаборыУпаковок.БазовыеЕдиницыИзмерения;
		СправочникОбъект.Код                     = СокрЛП(СтрокаКлассификатора.КодЧисловой);
		СправочникОбъект.Наименование            = СтрЗаменить(Наименование, Символы.ПС, "/");
		СправочникОбъект.НаименованиеПолное      = СтрЗаменить(СтрокаКлассификатора.Наименование, Символы.ПС, "/");
		СправочникОбъект.МеждународноеСокращение = СтрЗаменить(СтрокаКлассификатора.КодовоеБуквенноеОбозначениеМеждународное,
																Символы.ПС,
																"/");
		СправочникОбъект.Числитель               = СтрокаКлассификатора.Числитель;
		СправочникОбъект.Знаменатель             = СтрокаКлассификатора.Знаменатель;
		
		Если ЗначениеЗаполнено(СтрокаКлассификатора.ТипИзмеряемойВеличины) Тогда
			ТипИзмеряемойВеличины = Перечисления.ТипыИзмеряемыхВеличин[СтрокаКлассификатора.ТипИзмеряемойВеличины];
			
			СправочникОбъект.ТипИзмеряемойВеличины = ТипИзмеряемойВеличины;
		КонецЕсли;
		
		СправочникОбъект.Записать();
		
		ЕдиницаИзмерения = СправочникОбъект.Ссылка;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
		ИмяСобытия = НСтр("ru = 'Создание единицы измерения по классификатору.'", КодОсновногоЯзыка);
		
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, , ,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
	Возврат ЕдиницаИзмерения;
	
КонецФункции

// Возвращает сведения о коэффициенте пересчета единицы измерения ВЕТИС.
//
// Параметры:
// ЕдиницаИзмеренияВЕТИС - СправочникСсылка.ЕдиницыИзмеренияВЕТИС - Единица измерения ВЕТИС, коэффициент которой нужно 
//                                                                  получить.
// Номенклатура - СправочникСсылка.Номенклатура - Номенклатура для единицы хранения, которой осуществляется 
//                                                получение коэффициента пересчета.
// Возвращаемое значение:
// Структура - Структура со свойствами:
//  * КодОшибки      - Число    - Код ошибки получения коэффициента.
//      0 - Нет ошибок;
//      1 - Не заполнена единица измерения в справочнике 'ЕдиницыИзмеренияВЕТИС';
//      2 - В справочнике 'Номенклатура' выключена возможность пересчета количества 
//          в соответствующую мерную единицу измерения;
//      3 - Не удалось сопоставить единицу хранения справочника 'Номенклатура' 
//          с единицей измерения справочника 'ЕдиницыИзмеренияВЕТИС'.
//  * Коэффициент      - Число  - Коэффициент пересчета единицы измерения ВЕТИС.
//  * КэшироватьДанные - Булево - Истина если требуется кэшировать сведения о коэффициенте пересчета.
//  * ТипИзмеряемойВеличины   - ПеречислениеСсылка.ТипыИзмеряемыхВеличин - Тип измеряемой величины единицы измерения 
//                      справочника 'ЕдиницыИзмеренияВЕТИС'.
//                            - Неопределено - любая
//  * НужноОкруглятьКоличество      - Булево - Истина если необходимо округление количества при пересчете.
//  * НужноОкруглятьКоличествоВЕТИС - Булево - Истина если необходимо округление количества ВетИС при пересчете.
Функция КоэффициентЕдиницыИзмеренияПоВЕТИС(ЕдиницаИзмеренияВЕТИС, Номенклатура) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("КодОшибки",                     0);
	Результат.Вставить("Коэффициент",                   1);
	Результат.Вставить("КэшироватьДанные",              Ложь);
	Результат.Вставить("ТипИзмеряемойВеличины",         Неопределено);
	Результат.Вставить("НужноОкруглятьКоличество",      Ложь);
	Результат.Вставить("НужноОкруглятьКоличествоВЕТИС", Ложь);
	
	ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(ЕдиницаИзмеренияВЕТИС) Тогда
		
		РеквизитыЕдиницыИзмеренияВЕТИС = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ЕдиницаИзмеренияВЕТИС,
			"ЕдиницаИзмерения, Коэффициент");
		
		ЕдиницаИзмерения = РеквизитыЕдиницыИзмеренияВЕТИС.ЕдиницаИзмерения;
		
		Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
			Результат.КодОшибки   = 1;
			Результат.Коэффициент = 1;
			
			Возврат Результат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЕдиницаИзмеренияВЕТИС) Тогда
		Результат.Коэффициент      = 1;
		Результат.КэшироватьДанные = Истина;
		
		Возврат Результат;
	Иначе
		
		Запрос = Новый Запрос;
		
		Если Не ЗначениеЗаполнено(Номенклатура) Тогда
			
			ТекстЗапроса = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Упаковка.ТипИзмеряемойВеличины                КАК ТипИзмеряемойВеличиныДляПроверки,
			|	НЕОПРЕДЕЛЕНО                                  КАК ТипИзмеряемойВеличиныНоменклатуры,
			|	ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК Коэффициент
			|ИЗ
			|	Справочник.УпаковкиЕдиницыИзмерения КАК Упаковка
			|ГДЕ
			|	Упаковка.Ссылка = &ЕдиницаИзмерения";
			
			ИсточникНоменклатуры = Неопределено;
			ИсточникУпаковки     = "Упаковка";
			
		Иначе
			
			ТекстЗапроса = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Упаковка.ТипИзмеряемойВеличины КАК ТипИзмеряемойВеличиныДляПроверки,
			|	ЕСТЬNULL(ЕдиницыХранения.ТипИзмеряемойВеличины, НЕОПРЕДЕЛЕНО) КАК ТипИзмеряемойВеличиныНоменклатуры,
			|	ВЫБОР
			|		КОГДА Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.КоличествоШтук)
			|			ТОГДА ВЫБОР
			|					КОГДА Упаковка.Ссылка = Номенклатура.ЕдиницаИзмерения
			|						ТОГДА 1
			|					КОГДА ЕСТЬNULL(ЕдиницыИзмеренияВЕТИС.БазоваяЕдиницаИзмерения.ЕдиницаИзмерения, НЕОПРЕДЕЛЕНО) = Номенклатура.ЕдиницаИзмерения
			|						ТОГДА ЕдиницыИзмеренияВЕТИС.Коэффициент
			|					ИНАЧЕ 0
			|				КОНЕЦ
			|		ИНАЧЕ ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 0)
			|	КОНЕЦ КАК Коэффициент
			|ИЗ
			|	Справочник.УпаковкиЕдиницыИзмерения КАК Упаковка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
			|		ПО Номенклатура.Ссылка = &Номенклатура
			|			И Упаковка.Ссылка  = &ЕдиницаИзмерения
			|		
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмеренияВЕТИС КАК ЕдиницыИзмеренияВЕТИС
			|		ПО ЕдиницыИзмеренияВЕТИС.Ссылка = &ЕдиницаИзмеренияВЕТИС
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК ЕдиницыХранения
			|		ПО Номенклатура.ЕдиницаИзмерения = ЕдиницыХранения.Ссылка";
			
			ИсточникНоменклатуры = "Номенклатура";
			ИсточникУпаковки     = "Упаковка";
			
			Запрос.УстановитьПараметр("Номенклатура",          Номенклатура);
			Запрос.УстановитьПараметр("ЕдиницаИзмеренияВЕТИС", ЕдиницаИзмеренияВЕТИС);
			
		КонецЕсли;
		
		Запрос.Текст = СтрЗаменить(
			ТекстЗапроса,
			"&ТекстЗапросаКоэффициентУпаковки",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
				ИсточникУпаковки, ИсточникНоменклатуры));
		
		Запрос.УстановитьПараметр("ЕдиницаИзмерения", ЕдиницаИзмерения);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		МерныеТипыЕдиницИзмерений = Справочники.УпаковкиЕдиницыИзмерения.МерныеТипыЕдиницИзмерений();
		
		Если Выборка.Следующий() Тогда
			
			КодОшибки             = 0;
			Коэффициент           = Выборка.Коэффициент;
			КэшироватьДанные      = Истина;
			ТипИзмеряемойВеличины = Неопределено;
			НужноОкруглять        = ЗначениеЗаполнено(Номенклатура)
			                        И Выборка.ТипИзмеряемойВеличиныНоменклатуры = Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук
			                        И МерныеТипыЕдиницИзмерений.Найти(Выборка.ТипИзмеряемойВеличиныДляПроверки) <> Неопределено;
			НужноОкруглятьВЕТИС   = ЗначениеЗаполнено(Номенклатура)
			                        И Выборка.ТипИзмеряемойВеличиныДляПроверки = Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук
			                        И МерныеТипыЕдиницИзмерений.Найти(Выборка.ТипИзмеряемойВеличиныНоменклатуры) <> Неопределено;
			
			Если Коэффициент = 0 Тогда
				
				Коэффициент      = 1;
				КэшироватьДанные = Ложь;
				
				Если МерныеТипыЕдиницИзмерений.Найти(Выборка.ТипИзмеряемойВеличиныДляПроверки) <> Неопределено Тогда
					КодОшибки = 2;
					ТипИзмеряемойВеличины = Выборка.ТипИзмеряемойВеличиныДляПроверки;
				Иначе
					КодОшибки = 3;
				КонецЕсли;
				
			КонецЕсли;
			
			Результат.КодОшибки                     = КодОшибки;
			Результат.Коэффициент                   = Коэффициент;
			Результат.КэшироватьДанные              = КэшироватьДанные;
			Результат.ТипИзмеряемойВеличины         = ТипИзмеряемойВеличины;
			Результат.НужноОкруглятьКоличество      = НужноОкруглять;
			Результат.НужноОкруглятьКоличествоВЕТИС = НужноОкруглятьВЕТИС;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает сведения для пересчета единиц измерения ВЕТИС.
//
// Параметры:
//   ДанныеСтрокВЕТИС - Массив Из Структура - исходные данные для пересчета:
//    * Номенклатура - СправочникСсылка.Номенклатура - номенклатура строки
//    * ЕдиницаИзмеренияВЕТИС - СправочникСсылка.ЕдиницыИзмеренияВЕТИС - единица ВетИС строки
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - ключ ссылка на номенклатуру, значение соответствие единица измерения ВЕТИС и структура:
//   * КодОшибки - Число  - Код ошибки получения коэффициента:
//        0 - Нет ошибок;
//        1 - Не заполнена единица измерения в справочнике 'ЕдиницыИзмеренияВЕТИС';
//        2 - В справочнике 'Номенклатура' выключена возможность пересчета количества в соответствующую мерную единицу измерения;
//        3 - Не удалось сопоставить единицу хранения справочника 'Номенклатура' с единицей измерения справочника 'ЕдиницыИзмеренияВЕТИС'.
//   * Коэффициент                   - Число  - Коэффициент пересчета единицы измерения ВЕТИС.
//   * ТипИзмеряемойВеличины         - ПеречислениеСсылка.ТипыИзмеряемыхВеличин - Тип измеряемой величины единицы измерения
//       справочника 'ЕдиницыИзмеренияВЕТИС'.
//   * НужноОкруглятьКоличество      - Булево - Признак необходимости округления количества при пересчете.
//   * НужноОкруглятьКоличествоВЕТИС - Булево - Признак необходимости округления количества ВетИС при пересчете.
Функция КоэффициентыЕдиницИзмеренияПоВЕТИС(ДанныеСтрокВЕТИС) Экспорт
	
	Если ДанныеСтрокВЕТИС.Количество() = 0 Тогда
		Возврат Новый Соответствие;
	КонецЕсли;
	
	ШаблонРезультата = Новый Структура;
	ШаблонРезультата.Вставить("КодОшибки",                     0);
	ШаблонРезультата.Вставить("Коэффициент",                   1);
	ШаблонРезультата.Вставить("ТипИзмеряемойВеличины",         Неопределено);
	ШаблонРезультата.Вставить("НужноОкруглятьКоличество",      Ложь);
	ШаблонРезультата.Вставить("НужноОкруглятьКоличествоВЕТИС", Ложь);
	
	ДанныеУпаковокТаблица = Новый ТаблицаЗначений;
	ДанныеУпаковокТаблица.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ДанныеУпаковокТаблица.Колонки.Добавить("ЕдиницаИзмеренияВЕТИС", Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмеренияВЕТИС"));
	ОбщегоНазначенияУТ.ДобавитьСтрокиВТаблицу(ДанныеУпаковокТаблица, ДанныеСтрокВЕТИС);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ДанныеЕдиницИзмеренияВЕТИС.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ДанныеЕдиницИзмеренияВЕТИС.ЕдиницаИзмеренияВЕТИС
	|ПОМЕСТИТЬ ДанныеЕдиницИзмеренияВЕТИС
	|ИЗ
	|	&ДанныеЕдиницИзмеренияВЕТИС КАК ДанныеЕдиницИзмеренияВЕТИС
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеЕдиницИзмеренияВЕТИС.Номенклатура,
	|	ДанныеЕдиницИзмеренияВЕТИС.ЕдиницаИзмеренияВЕТИС,
	|	ЕСТЬNULL(ЕдиницыИзмеренияВЕТИС.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК
	|		Упаковка,
	|	ЕСТЬNULL(ЕдиницыИзмеренияВЕТИС.БазоваяЕдиницаИзмерения.ЕдиницаИзмерения, НЕОПРЕДЕЛЕНО) КАК Базовая,
	|	ЕСТЬNULL(ЕдиницыИзмеренияВЕТИС.Коэффициент, 1) КАК КоэффициентВЕТИС
	|ПОМЕСТИТЬ ДанныеУпаковок
	|ИЗ
	|	ДанныеЕдиницИзмеренияВЕТИС КАК ДанныеЕдиницИзмеренияВЕТИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмеренияВЕТИС КАК ЕдиницыИзмеренияВЕТИС
	|		ПО ЕдиницыИзмеренияВЕТИС.Ссылка = ДанныеЕдиницИзмеренияВЕТИС.ЕдиницаИзмеренияВЕТИС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеУпаковок.Номенклатура КАК Номенклатура,
	|	ДанныеУпаковок.Упаковка КАК Упаковка,
	|	ДанныеУпаковок.ЕдиницаИзмеренияВЕТИС КАК ЕдиницаИзмеренияВЕТИС,
	|	ВЫБОР
	|		КОГДА ДанныеУпаковок.ЕдиницаИзмеренияВЕТИС = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмеренияВЕТИС.ПустаяСсылка)
	|			ТОГДА 1
	|		КОГДА ДанныеУпаковок.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА 1
	|		КОГДА ДанныеУпаковок.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1)
	|		КОГДА ДанныеУпаковок.Упаковка.ТипИзмеряемойВеличины <> ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.КоличествоШтук)
	|			ТОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 0)
	|		КОГДА ДанныеУпаковок.Упаковка = Номенклатура.ЕдиницаИзмерения
	|			ТОГДА 1
	|		КОГДА ДанныеУпаковок.Базовая = Номенклатура.ЕдиницаИзмерения
	|			ТОГДА ДанныеУпаковок.КоэффициентВЕТИС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Коэффициент,
	|	ВЫБОР
	|		КОГДА ДанныеУпаковок.ЕдиницаИзмеренияВЕТИС = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмеренияВЕТИС.ПустаяСсылка)
	|			ТОГДА Неопределено
	|		КОГДА ДанныеУпаковок.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА -1
	|		ИНАЧЕ ДанныеУпаковок.Упаковка.ТипИзмеряемойВеличины
	|	КОНЕЦ КАК ТипИзмеряемойВеличиныДляПроверки,
	|	ВЫБОР
	|		КОГДА ДанныеУпаковок.ЕдиницаИзмеренияВЕТИС = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмеренияВЕТИС.ПустаяСсылка)
	|			ТОГДА Неопределено
	|		КОГДА ДанныеУпаковок.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА Неопределено
	|		КОГДА ДанныеУпаковок.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА Неопределено
	|		ИНАЧЕ ЕСТЬNULL(ЕдиницыХранения.ТипИзмеряемойВеличины, НЕОПРЕДЕЛЕНО)
	|	КОНЕЦ КАК ТипИзмеряемойВеличиныНоменклатуры
	|ИЗ
	|	ДанныеУпаковок КАК ДанныеУпаковок
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК ЕдиницыХранения
	|		ПО Номенклатура.ЕдиницаИзмерения = ЕдиницыХранения.Ссылка";

	ИсточникНоменклатуры = "Номенклатура";
	ИсточникУпаковки     = "Упаковка";
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(ИсточникУпаковки, ИсточникНоменклатуры));

	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДанныеЕдиницИзмеренияВЕТИС", ДанныеУпаковокТаблица);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	МерныеТипыЕдиницИзмерений = Справочники.УпаковкиЕдиницыИзмерения.МерныеТипыЕдиницИзмерений();
	
	КоэффициентыУпаковокНоменклатуры = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл
		
		ПолеСоответствияНоменклатуры = КоэффициентыУпаковокНоменклатуры.Получить(Выборка.Номенклатура);
		Если ПолеСоответствияНоменклатуры = Неопределено Тогда
			ПолеСоответствияНоменклатуры = Новый Соответствие;
		КонецЕсли;
		
		Результат = ОбщегоНазначения.СкопироватьРекурсивно(ШаблонРезультата);
		
		Если Выборка.ТипИзмеряемойВеличиныДляПроверки = -1 Тогда
			Результат.КодОшибки = 1;
		ИначеЕсли НЕ ЗначениеЗаполнено(Выборка.ЕдиницаИзмеренияВЕТИС) Тогда
		Иначе
			Результат.НужноОкруглятьКоличество = ЗначениеЗаполнено(Выборка.Номенклатура)
				И Выборка.ТипИзмеряемойВеличиныНоменклатуры = Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук
				И МерныеТипыЕдиницИзмерений.Найти(Выборка.ТипИзмеряемойВеличиныДляПроверки) <> Неопределено;
			Результат.НужноОкруглятьКоличествоВЕТИС = ЗначениеЗаполнено(Выборка.Номенклатура)
				И Выборка.ТипИзмеряемойВеличиныДляПроверки = Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук
				И МерныеТипыЕдиницИзмерений.Найти(Выборка.ТипИзмеряемойВеличиныНоменклатуры) <> Неопределено;
			Если Выборка.Коэффициент = 0 Тогда
				Результат.Коэффициент = 1;
				Если МерныеТипыЕдиницИзмерений.Найти(Выборка.ТипИзмеряемойВеличиныДляПроверки) <> Неопределено Тогда
					Результат.КодОшибки = 2;
					Результат.ТипИзмеряемойВеличины = Выборка.ТипИзмеряемойВеличиныДляПроверки;
				Иначе
					Результат.КодОшибки = 3;
				КонецЕсли;
			Иначе
				Результат.Коэффициент = Выборка.Коэффициент;
			КонецЕсли;
		КонецЕсли;
		
		ПолеСоответствияНоменклатуры.Вставить(Выборка.ЕдиницаИзмеренияВЕТИС, Результат);
		КоэффициентыУпаковокНоменклатуры.Вставить(Выборка.Номенклатура, ПолеСоответствияНоменклатуры);
		
	КонецЦикла;
	
	Возврат КоэффициентыУпаковокНоменклатуры;
	
КонецФункции

// Возвращает массив особенностей учета номенклатуры подсистемы
//
// Возвращаемое значение:
//   Массив из ПеречислениеСсылка.ОсобенностиУчетаНоменклатуры
Функция ОсобенностиУчетаНоменклатуры() Экспорт
	
	Массив = Новый Массив;
	
	Массив.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС);
	Массив.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС);
	Массив.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.ЗерноВЕТИС);
	Массив.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗернаВЕТИС);
	Массив.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС);
	Массив.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС);
	Массив.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС);
	Массив.Добавить(Перечисления.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС);
	
	Возврат Массив;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаЗаполнения

Процедура ОбработатьВводПоГруппеСкладов(РеквизитыДокумента)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(РеквизитыДокумента, "ЭтоГруппаСкладов")
		И РеквизитыДокумента.ЭтоГруппаСкладов Тогда
		
		ТекстОшибки = НСтр("ru = 'Документ %Документ% оформлен по группе складов. Ввод на основании не поддерживается.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", РеквизитыДокумента.ДокументОснование);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьДокумента(ТабличнаяЧасть, РезультатЗапроса, ДанныеЗаполнения, СоответствиеКолонокКоличествоВЕТИС, БезСопоставления = Истина, ДополнительныеПараметры = Неопределено)
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Количество() = 0 Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'В %1 отсутствует продукция для заполнения.'"),
			ДанныеЗаполнения);
		
	КонецЕсли;
	
	КолонкиТабличнойЧасти              = ТабличнаяЧасть.Выгрузить(Новый Массив);
	ЕстьЕдиницаИзмеренияВЕТИС          = КолонкиТабличнойЧасти.Колонки.Найти("ЕдиницаИзмеренияВЕТИС") <> Неопределено;
	ЕстьИдентификаторСтроки            = КолонкиТабличнойЧасти.Колонки.Найти("ИдентификаторСтроки") <> Неопределено;
	ЕстьСрокГодностиТочностьЗаполнения = КолонкиТабличнойЧасти.Колонки.Найти("СрокГодностиТочностьЗаполнения") <> Неопределено;
	ЕстьСрокГодностиСтрока             = КолонкиТабличнойЧасти.Колонки.Найти("СрокГодностиСтрока") <> Неопределено;
	ЕстьСрокГодностиНачалоПериода      = КолонкиТабличнойЧасти.Колонки.Найти("СрокГодностиНачалоПериода") <> Неопределено;
	ЕстьСрокГодностиКонецПериода       = КолонкиТабличнойЧасти.Колонки.Найти("СрокГодностиКонецПериода") <> Неопределено;
	ЕстьСерии                          = КолонкиТабличнойЧасти.Колонки.Найти("Серия") <> Неопределено;
	
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.Продукция) ИЛИ БезСопоставления Тогда
			
			НоваяСтрока = ТабличнаяЧасть.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
			Если ЗначениеЗаполнено(Выборка.Продукция)
				И (ИнтеграцияВЕТИСВызовСервера.ПродукцияПринадлежитТипуЖивыеЖивотные(Выборка.Продукция)
				Или (ЕстьСрокГодностиТочностьЗаполнения
				И ИнтеграцияВЕТИСКлиентСервер.ТочностьЗаполненияБезДаты(НоваяСтрока.СрокГодностиТочностьЗаполнения))) Тогда
				
				Если ЕстьСрокГодностиТочностьЗаполнения Тогда
					НоваяСтрока.СрокГодностиТочностьЗаполнения = Перечисления.ТочностьЗаполненияПериодаВЕТИС.Неприменимо;
				КонецЕсли;
				
				Если ЕстьСрокГодностиСтрока Тогда
					НоваяСтрока.СрокГодностиСтрока = "";
				КонецЕсли;
				
				Если ЕстьСрокГодностиНачалоПериода Тогда
					НоваяСтрока.СрокГодностиНачалоПериода = 0;
				КонецЕсли;
				
				Если ЕстьСрокГодностиКонецПериода Тогда
					НоваяСтрока.СрокГодностиКонецПериода = 0;
				КонецЕсли;
			
			КонецЕсли;
			
			Если ЕстьЕдиницаИзмеренияВЕТИС Тогда
				ИнтеграцияВЕТИС.ПроверитьОчиститьЕдиницуИзмеренияВЕТИС(НоваяСтрока);
			КонецЕсли;
			
			Если ЕстьИдентификаторСтроки Тогда
				НоваяСтрока.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
			КонецЕсли;
			
			Если ЕстьСерии И ЗначениеЗаполнено(НоваяСтрока.Серия) Тогда
				НоваяСтрока.СтатусУказанияСерий = 2;
			КонецЕсли;
			
			//Связанные табличные части
			Если НЕ(ДополнительныеПараметры = Неопределено) Тогда
				Если ДополнительныеПараметры.Свойство("Производители")И ЗначениеЗаполнено(Выборка.Производитель) Тогда
					НоваяСтрокаПроизводитель = ДополнительныеПараметры.Производители.Добавить();
					НоваяСтрокаПроизводитель.Производитель             = Выборка.Производитель;
					НоваяСтрокаПроизводитель.ИдентификаторСтрокиТовары = НоваяСтрока.ИдентификаторСтроки;
					Если ЗначениеЗаполнено(Выборка.ИдентификаторСтраны) Тогда
						НоваяСтрока.СтранаПроизводства = ИнтеграцияВЕТИСПовтИсп.СтранаМира(Выборка.ИдентификаторСтраны);
					КонецЕсли;
				КонецЕсли;
				Если ДополнительныеПараметры.Свойство("ПроизводственныеПартии")И ЗначениеЗаполнено(Выборка.ИдентификаторПартии) Тогда
					НоваяСтрокаПартия = ДополнительныеПараметры.ПроизводственныеПартии.Добавить();
					НоваяСтрокаПартия.ИдентификаторПартии       = Выборка.ИдентификаторПартии;
					НоваяСтрокаПартия.ИдентификаторСтрокиТовары = НоваяСтрока.ИдентификаторСтроки;
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Выборка, "Номенклатура") Тогда
				
				ТабличнаяЧасть.Очистить();
				
				ТекстОшибки = НСтр("ru = 'Не выполнено сопоставление %Номенклатура% классификатору номенклатуры ВетИС.'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Номенклатура%", Выборка.Номенклатура);
				
				ВызватьИсключение ТекстОшибки;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЕстьЕдиницаИзмеренияВЕТИС Тогда
		
		ДанныеДляПересчета = КоэффициентыЕдиницИзмеренияПоВЕТИС(ТабличнаяЧасть);
		
		Для Каждого СтрокаТабличнойЧасти Из ТабличнаяЧасть Цикл
			Для Каждого Колонка Из СоответствиеКолонокКоличествоВЕТИС Цикл
				
				Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура) Тогда
					СтрокаТабличнойЧасти[Колонка.Значение] = Неопределено;
				ИначеЕсли НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЕдиницаИзмеренияВЕТИС) Тогда
					СтрокаТабличнойЧасти[Колонка.Значение] = Неопределено;
				Иначе
					ДанныеЕдиницыИзмерения = ДанныеДляПересчета.Получить(СтрокаТабличнойЧасти.Номенклатура).Получить(СтрокаТабличнойЧасти.ЕдиницаИзмеренияВЕТИС);
					Если ДанныеЕдиницыИзмерения.КодОшибки <> 0 Тогда
						СтрокаТабличнойЧасти[Колонка.Значение] = Неопределено;
					КонецЕсли;
					
					Если ДанныеЕдиницыИзмерения.Коэффициент > 1 Тогда
						СтрокаТабличнойЧасти[Колонка.Значение] = СтрокаТабличнойЧасти[Колонка.Ключ] / ДанныеЕдиницыИзмерения.Коэффициент;
					Иначе
						СтрокаТабличнойЧасти[Колонка.Значение] = СтрокаТабличнойЧасти[Колонка.Ключ] * Окр(1 / ДанныеЕдиницыИзмерения.Коэффициент, 6);
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#Область ВходящаяТранспортнаяОперацияВЕТИС

Процедура ОбработкаЗаполненияДокументаВходящаяТранспортнаяОперацияВЕТИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Данные прикладных документов из входящей транспортной операции ВЕТИС.
// 
// Параметры:
//  ДокументСсылка - ДокументСсылка.ВходящаяТранспортнаяОперацияВЕТИС - Документ ссылка
// 
// Возвращаемое значение:
//  Массив Из ТаблицаЗначений - Данные прикладных документов из входящей транспортной операции ВЕТИС
Функция ДанныеПрикладныхДокументовИзВходящейТранспортнойОперацииВЕТИС(ДокументСсылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Запрос.УстановитьПараметр("Период", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Дата"));
	Запрос.УстановитьПараметр("СтранаРегистрации", ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана());
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВходящаяТранспортнаяОперацияВЕТИС.ГрузоотправительХозяйствующийСубъект.Контрагент КАК Грузоотправитель,
	|	ВходящаяТранспортнаяОперацияВЕТИС.ГрузоотправительХозяйствующийСубъект.Контрагент КАК Контрагент,
	|	ВходящаяТранспортнаяОперацияВЕТИС.ГрузополучательХозяйствующийСубъект.Контрагент  КАК Грузополучатель,
	|	ВходящаяТранспортнаяОперацияВЕТИС.ГрузополучательХозяйствующийСубъект.Контрагент  КАК Организация,
	|	ПредприятияОтправителя.ТорговыйОбъект                                             КАК ГрузоотправительПредприятие,
	|	ПредприятияОтправителя.ТорговыйОбъект                                             КАК Партнер,
	|	ПредприятияПолучателя.ТорговыйОбъект                                              КАК ГрузополучательПредприятие,
	|	ПредприятияПолучателя.ТорговыйОбъект                                              КАК Склад,
	|	ПредприятияОтправителя.ПроизводственныйОбъект                                     КАК ГрузоотправительПодразделение,
	|	ПредприятияПолучателя.ПроизводственныйОбъект                                      КАК ГрузополучательПодразделение,
	|	ПредприятияПолучателя.ПроизводственныйОбъект                                      КАК Подразделение,
	|	ВходящаяТранспортнаяОперацияВЕТИС.ПеревозчикХозяйствующийСубъект.Контрагент       КАК Перевозчик,
	|	ВходящаяТранспортнаяОперацияВЕТИС.СерияТТН                                        КАК СерияТТН,
	|	ВходящаяТранспортнаяОперацияВЕТИС.ДатаТТН                                         КАК ДатаТТН,
	|	ВходящаяТранспортнаяОперацияВЕТИС.НомерТТН                                        КАК НомерТТН,
	|	ВходящаяТранспортнаяОперацияВЕТИС.ТипТТН                                          КАК ТипТТН,
	|	ВходящаяТранспортнаяОперацияВЕТИС.Комментарий                                     КАК Комментарий,
	|	ВходящаяТранспортнаяОперацияВЕТИС.Дата                                            КАК Дата
	|ИЗ
	|	Документ.ВходящаяТранспортнаяОперацияВЕТИС КАК ВходящаяТранспортнаяОперацияВЕТИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХозяйствующиеСубъектыВЕТИС.Предприятия КАК ПредприятияОтправителя
	|		ПО ВходящаяТранспортнаяОперацияВЕТИС.ГрузоотправительХозяйствующийСубъект = ПредприятияОтправителя.Ссылка
	|			И ВходящаяТранспортнаяОперацияВЕТИС.ГрузоотправительПредприятие = ПредприятияОтправителя.Предприятие
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХозяйствующиеСубъектыВЕТИС.Предприятия КАК ПредприятияПолучателя
	|		ПО ВходящаяТранспортнаяОперацияВЕТИС.ГрузополучательХозяйствующийСубъект = ПредприятияПолучателя.Ссылка
	|			И ВходящаяТранспортнаяОперацияВЕТИС.ГрузополучательПредприятие = ПредприятияПолучателя.Предприятие
	|ГДЕ
	|	ВходящаяТранспортнаяОперацияВЕТИС.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВходящаяТранспортнаяОперацияВЕТИСТовары.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(СтавкиНДСНоменклатуры.СтавкаНДС, ЕСТЬNULL(ОсновныеСтавкиНДС.СтавкаНДС,
	|		ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка))) КАК СтавкаНДС,
	|	ВходящаяТранспортнаяОперацияВЕТИСТовары.Характеристика КАК Характеристика,
	|	ВходящаяТранспортнаяОперацияВЕТИСТовары.Серия КАК Серия,
	|	ВходящаяТранспортнаяОперацияВЕТИСТовары.Количество * ВЫБОР
	|		КОГДА КоличествоВЕТИС > 0
	|			ТОГДА (ВходящаяТранспортнаяОперацияВЕТИСТовары.КоличествоВЕТИС -
	|				ВходящаяТранспортнаяОперацияВЕТИСТовары.ВозвращаемоеКоличествоВЕТИС) / КоличествоВЕТИС
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Количество,
	|	ВходящаяТранспортнаяОперацияВЕТИСТовары.Количество * ВЫБОР
	|		КОГДА ВходящаяТранспортнаяОперацияВЕТИСТовары.КоличествоВЕТИС > 0
	|			ТОГДА (ВходящаяТранспортнаяОперацияВЕТИСТовары.КоличествоВЕТИС -
	|				ВходящаяТранспортнаяОперацияВЕТИСТовары.ВозвращаемоеКоличествоВЕТИС) /
	|				ВходящаяТранспортнаяОперацияВЕТИСТовары.КоличествоВЕТИС
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК КоличествоУпаковок,
	|	ВходящаяТранспортнаяОперацияВЕТИСТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ЛОЖЬ КАК БылоУточнение
	|ИЗ
	|	Документ.ВходящаяТранспортнаяОперацияВЕТИС.Товары КАК ВходящаяТранспортнаяОперацияВЕТИСТовары
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНДСНоменклатуры.СрезПоследних(&Период,
	|		Страна = &СтранаРегистрации ИЛИ Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)) КАК СтавкиНДСНоменклатуры
	|	ПО ВходящаяТранспортнаяОперацияВЕТИСТовары.Номенклатура = СтавкиНДСНоменклатуры.Номенклатура
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСтавкиНДС.СрезПоследних(&Период, Страна = &СтранаРегистрации) КАК ОсновныеСтавкиНДС
	|	ПО (ИСТИНА)
	|ГДЕ
	|	ВходящаяТранспортнаяОперацияВЕТИСТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВходящаяТранспортнаяОперацияВЕТИСТоварыУточнение.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(СтавкиНДСНоменклатуры.СтавкаНДС, ЕСТЬNULL(ОсновныеСтавкиНДС.СтавкаНДС,
	|		ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)))  КАК СтавкаНДС,
	|	ВходящаяТранспортнаяОперацияВЕТИСТоварыУточнение.Характеристика КАК Характеристика,
	|	ВходящаяТранспортнаяОперацияВЕТИСТоварыУточнение.Серия КАК Серия,
	|	ВходящаяТранспортнаяОперацияВЕТИСТоварыУточнение.Количество КАК Количество,
	|	ВходящаяТранспортнаяОперацияВЕТИСТоварыУточнение.Количество КАК КоличествоУпаковок,
	|	ВходящаяТранспортнаяОперацияВЕТИСТоварыУточнение.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	Документ.ВходящаяТранспортнаяОперацияВЕТИС.ТоварыУточнение КАК ВходящаяТранспортнаяОперацияВЕТИСТоварыУточнение
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНДСНоменклатуры.СрезПоследних(&Период,
	|		Страна = &СтранаРегистрации ИЛИ Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)) КАК СтавкиНДСНоменклатуры
	|	ПО ВходящаяТранспортнаяОперацияВЕТИСТоварыУточнение.Номенклатура = СтавкиНДСНоменклатуры.Номенклатура
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСтавкиНДС.СрезПоследних(&Период, Страна = &СтранаРегистрации) КАК ОсновныеСтавкиНДС
	|	ПО (ИСТИНА)
	|ГДЕ
	|	ВходящаяТранспортнаяОперацияВЕТИСТоварыУточнение.Ссылка = &Ссылка";
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат Запрос.ВыполнитьПакет();
	
КонецФункции

Функция СтруктураЗаполненияТабличныхЧастейПоВходящейТранспортнойОперации(ДанныеЗаполнения, ЕстьСерии = Ложь, ЕстьСкладВТабличнойЧасти = Ложь)
	
	Результат = Новый Структура;
	Результат.Вставить("Товары",                        ДанныеЗаполнения[1].Выгрузить());
	Результат.Вставить("ТоварыУточнение",               ДанныеЗаполнения[2].Выгрузить());
	Если ЕстьСерии Тогда
		Результат.Вставить("ЕстьСерии");
	КонецЕсли;
	Если ЕстьСкладВТабличнойЧасти Тогда
		Результат.Вставить("ЗаполнитьСклад");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииВходящейТранспортнойОперации(ДокументОбъект,ДанныеЗаполнения)
	
	ДокументОбъект.Товары.Очистить();
	Если ДанныеЗаполнения.Свойство("ЕстьСерии") Тогда
		ИмяТЧСерии = ДанныеЗаполнения.ЕстьСерии;
		Если ИмяТЧСерии = Неопределено Тогда 
			ИмяТЧСерии = "Серии";
		КонецЕсли;
		ДокументОбъект[ИмяТЧСерии].Очистить();
	КонецЕсли;
	
	ДанныеЗаполнения.Товары.Индексы.Добавить("ИдентификаторСтроки");
	
	Для Каждого СтрокаТовары Из ДанныеЗаполнения.ТоварыУточнение Цикл 
		
		Если НЕ ЗначениеЗаполнено(СтрокаТовары.Номенклатура) Тогда
			Продолжить;
		ИначеЕсли СтрокаТовары.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;
			
		ИсходнаяСтрока = ДанныеЗаполнения.Товары.НайтиСтроки(Новый Структура("ИдентификаторСтроки",СтрокаТовары.ИдентификаторСтроки));
		Если ИсходнаяСтрока.Количество() = 0 Тогда 
			ИсходнаяСтрока = Новый Структура;
		Иначе 
			ИсходнаяСтрока = ИсходнаяСтрока[0];
			ИсходнаяСтрока.БылоУточнение = Истина;
		КонецЕсли;
		
		НоваяСтрокаТовары = ДокументОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары,ИсходнаяСтрока);
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары,СтрокаТовары);
		
		Если ДанныеЗаполнения.Свойство("ЗаполнитьСклад")Тогда 
			НоваяСтрокаТовары.Склад = ДокументОбъект.Склад;
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаТовары Из ДанныеЗаполнения.Товары.НайтиСтроки(Новый Структура("БылоУточнение",Ложь))Цикл
		
		НоваяСтрокаТовары = ДокументОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары,СтрокаТовары);
		Если ДанныеЗаполнения.Свойство("ЗаполнитьСклад")Тогда 
			НоваяСтрокаТовары.Склад = ДокументОбъект.Склад;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ДанныеЗаполнения.Свойство("ЕстьСерии") Тогда
		Для Каждого СтрокаТовары Из ДокументОбъект.Товары Цикл
			Если ЗначениеЗаполнено(СтрокаТовары.Серия) Тогда
				НоваяСтрокаСерия = ДокументОбъект[ИмяТЧСерии].Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаСерия,СтрокаТовары);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ИсходящаяТранспортнаяОперацияВЕТИС

Процедура ЗаполнитьШапкуИсходящейТранспортнойОперации(ДокументОбъект, ДанныеЗаполнения, Запрос)
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоОформлениеПередачиМеждуОрганизациями = Ложь;
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ДокументОснование = ДанныеЗаполнения.Основание;
		ЭтоОформлениеПередачиМеждуОрганизациями = ДанныеЗаполнения.Свойство("Товары");
	Иначе
		ДокументОснование = ДанныеЗаполнения;
	КонецЕсли;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	РеквизитыДокумента = Запрос.Выполнить().Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		Если Не ЭтоОформлениеПередачиМеждуОрганизациями Тогда
			ИнтеграцияИСУТ.ПроверитьВозможностьВводаНаОсновании(РеквизитыДокумента);
		КонецЕсли;
		
		ОбработатьВводПоГруппеСкладов(РеквизитыДокумента);
			
		ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
		
		ДокументОбъект.ТорговыйОбъект = РеквизитыДокумента.СкладОтправитель;
		
		Грузоотправитель = Справочники.ХозяйствующиеСубъектыВЕТИС.ХозяйствующийСубъектИПредприятиеПоПрикладнымРеквизитам(
			РеквизитыДокумента.Грузоотправитель, РеквизитыДокумента.СкладОтправитель);
		ДокументОбъект.ГрузоотправительХозяйствующийСубъект = Грузоотправитель.ХозяйствующийСубъект;
		ДокументОбъект.ГрузоотправительПредприятие          = Грузоотправитель.Предприятие;
		
		Грузополучатель = Справочники.ХозяйствующиеСубъектыВЕТИС.ХозяйствующийСубъектИПредприятиеПоПрикладнымРеквизитам(
			РеквизитыДокумента.Грузополучатель, РеквизитыДокумента.СкладПолучатель);
		ДокументОбъект.ГрузополучательХозяйствующийСубъект = Грузополучатель.ХозяйствующийСубъект;
		ДокументОбъект.ГрузополучательПредприятие          = Грузополучатель.Предприятие;
		
		ТранспортныеНакладные = Документы.ТранспортнаяНакладная.ТранспортныеНакладныеДокументовОснований(РеквизитыДокумента.ДокументОснование);
		ТранспортнаяНакладная = Неопределено;
		
		Если ТранспортныеНакладные.Количество() = 0 Тогда 
			Если ЗначениеЗаполнено(ДокументОбъект.НомерТТН) Тогда 
				ДокументОбъект.НомерТТН = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДокументОбъект.НомерТТН);
			КонецЕсли;
		ИначеЕсли ТранспортныеНакладные.Количество() = 1 Тогда 
			ДокументОбъект.НомерТТН = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ТранспортныеНакладные[0].Номер);
			ДокументОбъект.ДатаТТН  = ТранспортныеНакладные[0].Дата;
			ТранспортнаяНакладная   = ТранспортныеНакладные[0].ТранспортнаяНакладная;
		Иначе
			ДокументОбъект.НомерТТН = Неопределено;
			ДокументОбъект.ДатаТТН  = Неопределено;
		КонецЕсли;
		ДокументОбъект.ТТНУказан = ЗначениеЗаполнено(ДокументОбъект.НомерТТН)
			И Грузоотправитель.Предприятие <> Грузополучатель.Предприятие;
		
		Если ЗначениеЗаполнено(ДокументОбъект.ГрузоотправительПредприятие) И ДокументОбъект.Маршрут.Количество() = 0 Тогда
			
			ДанныеПеревозки = ИнтеграцияИСУТ.ДанныеПеревозки(РеквизитыДокумента.ДокументОснование, ТранспортнаяНакладная);
			
			НоваяСтрока = ДокументОбъект.Маршрут.Добавить();
			НоваяСтрока.Предприятие  = ДокументОбъект.ГрузоотправительПредприятие;
			НоваяСтрока.СПерегрузкой = Истина;
			Если ЗначениеЗаполнено(ДанныеПеревозки.АдресПогрузки) И Не ЗначениеЗаполнено(НоваяСтрока.Предприятие) Тогда
				
				НоваяСтрока.Адрес              = ДанныеПеревозки.АдресПогрузкиЗначенияПолей;
				НоваяСтрока.АдресПредставление = ДанныеПеревозки.АдресПогрузки;
				
				АдресПоКлассификаторуВЕТИС = "";
				Попытка
					АдресПоКлассификаторуВЕТИС = ИнтеграцияВЕТИСВызовСервера.ДанныеАдресаПоАдресуXML(
						ДанныеПеревозки.АдресПогрузкиЗначенияПолей, ДанныеПеревозки.АдресПогрузки);
				Исключение
					// Обработка исключения не требуется
				КонецПопытки;
				
				НоваяСтрока.ДанныеАдреса = Новый ХранилищеЗначения(АдресПоКлассификаторуВЕТИС);
				
			КонецЕсли;
			Если ЗначениеЗаполнено(ДанныеПеревозки.АвтомобильГосударственныйНомер) Тогда
				НоваяСтрока.ТипТранспорта = Перечисления.ТипыТранспортаВЕТИС.Автомобиль;
			КонецЕсли;
			НоваяСтрока.НомерТранспортногоСредства = ДанныеПеревозки.АвтомобильГосударственныйНомер;
			НоваяСтрока.НомерАвтомобильногоПрицепа = ДанныеПеревозки.ГосударственныйНомерПрицепа;
			НоваяСтрока.ТранспортноеСредство       = ДанныеПеревозки.ТранспортноеСредство;
			
			//Из распоряжений (реализаций товаров) попробуем вытащить соответствующие предприятия ВетИС
			ВсеРаспоряженияМаршрута = Новый Массив;
			Для Каждого ПунктМаршрута Из ДанныеПеревозки.Маршрут Цикл
				Для Каждого Распоряжение Из ПунктМаршрута.Распоряжения Цикл
					ВсеРаспоряженияМаршрута.Добавить(Распоряжение);
				КонецЦикла;
			КонецЦикла;
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Распоряжения", ВсеРаспоряженияМаршрута);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	РеализацияТоваровУслуг.Ссылка КАК Распоряжение,
			|	ХозяйствующиеСубъектыВЕТИСПредприятия.Предприятие КАК Предприятие
			|ИЗ
			|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ХозяйствующиеСубъектыВЕТИС.Предприятия КАК ХозяйствующиеСубъектыВЕТИСПредприятия
			|		ПО РеализацияТоваровУслуг.Контрагент = ХозяйствующиеСубъектыВЕТИСПредприятия.Ссылка.Контрагент
			|		И РеализацияТоваровУслуг.Партнер = ХозяйствующиеСубъектыВЕТИСПредприятия.ТорговыйОбъект
			|ГДЕ
			|	РеализацияТоваровУслуг.Ссылка В (&Распоряжения)";
			ВсеТочкиИзРаспоряжений = Запрос.Выполнить().Выгрузить();
			ВсеТочкиИзРаспоряжений.Индексы.Добавить("Распоряжение");
			
			Если ДанныеПеревозки.Маршрут.Количество() Тогда
				ТочкаОтправления = Истина;
				Для Каждого ПунктМаршрута Из ДанныеПеревозки.Маршрут Цикл
					
					ЕстьАдресТочкиДляВЕТИС = Ложь;
					АдресПоКлассификаторуВЕТИС = "";
					Попытка
						АдресПоКлассификаторуВЕТИС = ИнтеграцияВЕТИСВызовСервера.ДанныеАдресаПоАдресуXML(
							ПунктМаршрута.АдресЗначенияПолей, ПунктМаршрута.ПредставлениеАдреса);
						ЕстьАдресТочкиДляВЕТИС = Истина;
					Исключение КонецПопытки;
					
					ЕстьПредприятиеТочкиДляВЕТИС = Ложь;
					ПредприятиеВЕТИС = Неопределено;
					Для Каждого Распоряжение Из ПунктМаршрута.Распоряжения Цикл
						ПредприятиеВЕТИС = ВсеТочкиИзРаспоряжений.Найти(Распоряжение,"Распоряжение");
						Если ПредприятиеВЕТИС <> Неопределено Тогда
							ПредприятиеВЕТИС = ПредприятиеВЕТИС.Предприятие;
							ЕстьПредприятиеТочкиДляВЕТИС = Истина;
							Прервать;
						КонецЕсли;
					КонецЦикла;
					
					Если ЕстьАдресТочкиДляВЕТИС Или ЕстьПредприятиеТочкиДляВЕТИС Тогда
						
						//Первый пункт может быть точка отправления - уже заполнена
						Если ТочкаОтправления Тогда
							ТочкаОтправления = Ложь;
							Если ПунктМаршрута.ПредставлениеАдреса = НоваяСтрока.АдресПредставление Тогда
								Продолжить;
							КонецЕсли;
						КонецЕсли;
						
						ПромежуточнаяТочка = ДокументОбъект.Маршрут.Добавить();
						ЗаполнитьЗначенияСвойств(ПромежуточнаяТочка, НоваяСтрока);
						ПромежуточнаяТочка.Идентификатор = Новый УникальныйИдентификатор;
						ПромежуточнаяТочка.СПерегрузкой = Ложь;
						
						ПромежуточнаяТочка.АдресПредставление = ПунктМаршрута.ПредставлениеАдреса;
						ПромежуточнаяТочка.Адрес = ПунктМаршрута.АдресЗначенияПолей;
						ПромежуточнаяТочка.ДанныеАдреса = Новый ХранилищеЗначения(АдресПоКлассификаторуВЕТИС);
						
						ПромежуточнаяТочка.Предприятие = ПредприятиеВЕТИС;
						
					КонецЕсли;
					
				КонецЦикла;
			КонецЕсли;
			
			Если ДокументОбъект.Маршрут.Количество() И ЗначениеЗаполнено(ДокументОбъект.Маршрут[0].НомерТранспортногоСредства) Тогда
				РеквизитыПеревозки = ЗаполнениеОбъектовПоСтатистикеВЕТИС.РеквизитыПеревозкиПоТранспортномуСредству(НоваяСтрока);
				ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыПеревозки);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьИсходящейТранспортнойОперации(ДокументОбъект, ДанныеЗаполнения, ИмяДокумента, Описание)
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.УпаковкиВЕТИС.Очистить();
	
	Запрос = Новый Запрос;
	Если ТипЗнч(Описание) = Тип("Булево") Тогда
		Запрос.Текст = ТекстЗапросаЗаполненияИсходящейТранспортнойОперацииИзПрикладногоДокумента(ИмяДокумента,Описание);
	ИначеЕсли ТипЗнч(Описание) = Тип("Строка") Тогда
		Запрос.Текст = ТекстЗапросаТоварыИсходящейТранспортнойОперацииИзОснованияИОрдеров(ИмяДокумента, Описание);
	Иначе
		Запрос.Текст = ТекстЗапросаТоварыИсходящейТранспортнойОперацииИзТаблицы();
		Запрос.УстановитьПараметр("Таблица", Описание);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОбъект.ДокументОснование);
	Запрос.УстановитьПараметр("Документ",          ДокументОбъект.ДокументОснование);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.ИсходящаяТранспортнаяОперацияВЕТИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия",       Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Тип.ПривестиЗначение());
	Запрос.УстановитьПараметр("ОсобенностьУчета",  ОсобенностиУчетаНоменклатуры());
	
	БлагополучиеМестности = ЗаполнениеОбъектовПоСтатистикеВЕТИС.БлагополучиеМестностиПоПредприятию(ДокументОбъект.ГрузоотправительПредприятие);
	Запрос.УстановитьПараметр("БлагополучиеМестности", БлагополучиеМестности[0]);
	
	КолонкиКоличествоВЕТИС = Новый Структура;
	КолонкиКоличествоВЕТИС.Вставить("Количество","КоличествоВЕТИС");
	
	ЗаполнитьТабличнуюЧастьДокумента(ДокументОбъект.Товары, Запрос.Выполнить(), ДанныеЗаполнения, КолонкиКоличествоВЕТИС);
	
КонецПроцедуры

Функция ТекстЗапросаЗаполненияИсходящейТранспортнойОперацииИзПрикладногоДокумента(ИмяДокумента, ЕстьСерии)
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ИсходящаяТранспортнаяОперацияВЕТИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовВЕТИС КАК СтатусыДокументовВЕТИС
	|		ПО СтатусыДокументовВЕТИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовВЕТИС.Статус НЕ В(&КонечныеСтатусы)
	|;
	|"
	+ ?(ЕстьСерии = Ложь,
	"
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура                                КАК Номенклатура,
	|	Товары.Характеристика                              КАК Характеристика,
	|	Товары.Серия                                       КАК Серия,
	|	СУММА(Товары.Количество)                           КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.ИмяПрикладногоДокумента.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И (СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЗерноВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗернаВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС))
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|"
	,
	"
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура                                КАК Номенклатура,
	|	Товары.Характеристика                              КАК Характеристика,
	|	Товары.Серия                                       КАК Серия,
	|	СУММА(Товары.Количество)                           КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.ИмяПрикладногоДокумента.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И (СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЗерноВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗернаВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС))
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.Номенклатура                                КАК Номенклатура,
	|	Серии.Характеристика                              КАК Характеристика,
	|	Серии.Серия                                       КАК Серия,
	|	СУММА(Серии.Количество)                           КАК Количество
	|ПОМЕСТИТЬ Серии
	|ИЗ
	|	Документ.ИмяПрикладногоДокумента.Серии КАК Серии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Серии.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Серии.Ссылка = &ДокументОснование
	|	И (СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЗерноВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗернаВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС))
	|СГРУППИРОВАТЬ ПО
	|	Серии.Номенклатура,
	|	Серии.Характеристика,
	|	Серии.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура                                КАК Номенклатура,
	|	Товары.Характеристика                              КАК Характеристика,
	|	ЕСТЬNULL(Серии.Серия,Товары.Серия)                 КАК Серия,
	|	ЕСТЬNULL(Серии.Количество, Товары.Количество)      КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Серии КАК Серии
	|			ПО Серии.Номенклатура = Товары.Номенклатура
	|			И Серии.Характеристика = Товары.Характеристика
	|;
	|"
	)
	+
	"
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыСерии.Номенклатура          КАК Номенклатура,
	|	ТоварыСерии.Характеристика        КАК Характеристика,
	|	ТоварыСерии.Серия                 КАК Серия,
	|	ТоварыСерии.Количество            КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыСерии КАК ТоварыСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура,
	|	ОформленныеТовары.Характеристика,
	|	ОформленныеТовары.Серия,
	|	-ОформленныеТовары.Количество
	|ИЗ
	|	Документ.ИсходящаяТранспортнаяОперацияВЕТИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка ИЗ ОформленныеДокументы КАК Т)
	|	И ЕСТЬNULL(ОформленныеТовары.ВетеринарноСопроводительныйДокумент.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыВетеринарныхДокументовВЕТИС.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.СтатусыВетеринарныхДокументовВЕТИС.Аннулирован)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыВЕТИС.Продукция,СоответствиеНоменклатурыВЕТИС2.Продукция)) КАК Продукция,
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия
	|ПОМЕСТИТЬ СопоставленныеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыВЕТИС КАК СоответствиеНоменклатурыВЕТИС
	|		ПО СоответствиеНоменклатурыВЕТИС.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыВЕТИС.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыВЕТИС.Серия = ТабличнаяЧасть.Серия)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыВЕТИС КАК СоответствиеНоменклатурыВЕТИС2
	|		ПО СоответствиеНоменклатурыВЕТИС2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыВЕТИС2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыВЕТИС2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыВЕТИС.Продукция,СоответствиеНоменклатурыВЕТИС2.Продукция)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура                                                  КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика                                                КАК Характеристика,
	|	ТоварыКОформлению.Серия                                                         КАК Серия,
	|	СопоставленныеПозиции.Продукция                                                 КАК Продукция,
	|	ЗНАЧЕНИЕ(Перечисление.РезультатыЛабораторныхИсследованийВЕТИС.НеПодвергнутаВСЭ) КАК ЭкспертизаРезультат,
	|	&БлагополучиеМестности                                                          КАК БлагополучиеМестности,
	|	СУММА(ТоварыКОформлению.Количество)                                             КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество)                                             КАК КоличествоВЕТИС
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленныеПозиции КАК СопоставленныеПозиции
	|		ПО СопоставленныеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленныеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленныеПозиции.Серия = ТоварыКОформлению.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленныеПозиции.Продукция
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ИмяПрикладногоДокумента",ИмяДокумента);
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаТоварыИсходящейТранспортнойОперацииИзОснованияИОрдеров(ИмяДокумента, ТекстЗапросаТоварыОснования)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ИсходящаяТранспортнаяОперацияВЕТИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовВЕТИС КАК СтатусыДокументовВЕТИС
	|		ПО СтатусыДокументовВЕТИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовВЕТИС.Статус НЕ В(&КонечныеСтатусы)
	|;
	|"
	 + ТекстЗапросаТоварыОснования + 
	"
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура                                КАК Номенклатура,
	|	Товары.Характеристика                              КАК Характеристика,
	|	Товары.Серия                                       КАК Серия,
	|	СУММА(Товары.Количество)                           КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	ТаблицаТоварыИмяПрикладногоДокумента КАК Товары
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|"
	+
	"
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыСерии.Номенклатура          КАК Номенклатура,
	|	ТоварыСерии.Характеристика        КАК Характеристика,
	|	ТоварыСерии.Серия                 КАК Серия,
	|	ТоварыСерии.Количество            КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыСерии КАК ТоварыСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура,
	|	ОформленныеТовары.Характеристика,
	|	ОформленныеТовары.Серия,
	|	-ОформленныеТовары.Количество
	|ИЗ
	|	Документ.ИсходящаяТранспортнаяОперацияВЕТИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка ИЗ ОформленныеДокументы КАК Т)
	|	И ЕСТЬNULL(ОформленныеТовары.ВетеринарноСопроводительныйДокумент.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыВетеринарныхДокументовВЕТИС.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.СтатусыВетеринарныхДокументовВЕТИС.Аннулирован)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыВЕТИС.Продукция,СоответствиеНоменклатурыВЕТИС2.Продукция)) КАК Продукция,
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия
	|ПОМЕСТИТЬ СопоставленныеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыВЕТИС КАК СоответствиеНоменклатурыВЕТИС
	|		ПО СоответствиеНоменклатурыВЕТИС.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыВЕТИС.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыВЕТИС.Серия = ТабличнаяЧасть.Серия)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыВЕТИС КАК СоответствиеНоменклатурыВЕТИС2
	|		ПО СоответствиеНоменклатурыВЕТИС2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыВЕТИС2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыВЕТИС2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыВЕТИС.Продукция,СоответствиеНоменклатурыВЕТИС2.Продукция)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура                                                  КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика                                                КАК Характеристика,
	|	ТоварыКОформлению.Серия                                                         КАК Серия,
	|	СопоставленныеПозиции.Продукция                                                  КАК Продукция,
	|	ЗНАЧЕНИЕ(Перечисление.РезультатыЛабораторныхИсследованийВЕТИС.НеПодвергнутаВСЭ) КАК ЭкспертизаРезультат,
	|	&БлагополучиеМестности                                                          КАК БлагополучиеМестности,
	|	СУММА(ТоварыКОформлению.Количество)                                             КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество)                                             КАК КоличествоВЕТИС
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленныеПозиции КАК СопоставленныеПозиции
	|		ПО СопоставленныеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленныеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленныеПозиции.Серия = ТоварыКОформлению.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленныеПозиции.Продукция
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ИмяПрикладногоДокумента",ИмяДокумента);
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаТоварыИсходящейТранспортнойОперацииИзТаблицы()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Таблица.Номенклатура            КАК Номенклатура,
	|	Таблица.Характеристика          КАК Характеристика,
	|	Таблица.Серия                   КАК Серия,
	|	Таблица.Количество              КАК Количество,
	|	Таблица.КоличествоВЕТИС         КАК КоличествоВЕТИС,
	|	Таблица.ЗаписьСкладскогоЖурнала КАК ЗаписьСкладскогоЖурнала
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	&Таблица КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура    КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика  КАК Характеристика,
	|	ТоварыКОформлению.Серия           КАК Серия,
	|	ТоварыКОформлению.Количество      КАК Количество,
	|	ТоварыКОформлению.КоличествоВЕТИС КАК КоличествоВЕТИС,
	|	ЗаписьЖурнала.Ссылка                             КАК ЗаписьСкладскогоЖурнала,
	|	ЗаписьЖурнала.Продукция                          КАК Продукция,
	|	ЗаписьЖурнала.ЕдиницаИзмеренияВЕТИС              КАК ЕдиницаИзмеренияВЕТИС,
	|	ЗаписьЖурнала.СкоропортящаясяПродукция           КАК СкоропортящаясяПродукция,
	|	ЗаписьЖурнала.НизкокачественнаяПродукция         КАК НизкокачественнаяПродукция,
	|	ЗаписьЖурнала.ДатаПроизводстваСтрока             КАК ДатаПроизводстваСтрока,
	|	ЗаписьЖурнала.ДатаПроизводстваТочностьЗаполнения КАК ДатаПроизводстваТочностьЗаполнения,
	|	ЗаписьЖурнала.ДатаПроизводстваНачалоПериода      КАК ДатаПроизводстваНачалоПериода,
	|	ЗаписьЖурнала.ДатаПроизводстваКонецПериода       КАК ДатаПроизводстваКонецПериода,
	|	ЗаписьЖурнала.СрокГодностиСтрока                 КАК СрокГодностиСтрока,
	|	ЗаписьЖурнала.СрокГодностиТочностьЗаполнения     КАК СрокГодностиТочностьЗаполнения,
	|	ЗаписьЖурнала.СрокГодностиНачалоПериода          КАК СрокГодностиНачалоПериода,
	|	ЗаписьЖурнала.СрокГодностиКонецПериода           КАК СрокГодностиКонецПериода,
	|	&БлагополучиеМестности                                                          КАК БлагополучиеМестности,
	|	ЗНАЧЕНИЕ(Перечисление.РезультатыЛабораторныхИсследованийВЕТИС.НеПодвергнутаВСЭ) КАК ЭкспертизаРезультат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗаписиСкладскогоЖурналаВЕТИС КАК ЗаписьЖурнала
	|		ПО ЗаписьЖурнала.Ссылка = ТоварыКОформлению.ЗаписьСкладскогоЖурнала
	|ГДЕ
	|	ТоварыКОформлению.Количество > 0
	|";
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ДанныеПрикладныхДокументовИзИсходящейТранспортнойОперацииВЕТИС(ДокументСсылка)
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Дата, ГрузоотправительХозяйствующийСубъект");
	Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначенияРеквизитов.ГрузоотправительХозяйствующийСубъект, "Контрагент");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Запрос.УстановитьПараметр("Период", ЗначенияРеквизитов.Дата);
	Запрос.УстановитьПараметр("СтранаРегистрации", ЗначениеНастроекКлиентСерверПовтИсп.СтранаРегистрацииОрганизации(Организация));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИсходящаяТранспортнаяОперацияВЕТИС.ГрузоотправительХозяйствующийСубъект.Контрагент КАК Грузоотправитель,
	|	ИсходящаяТранспортнаяОперацияВЕТИС.ГрузоотправительХозяйствующийСубъект.Контрагент КАК Организация,
	|	ИсходящаяТранспортнаяОперацияВЕТИС.ГрузополучательХозяйствующийСубъект.Контрагент  КАК Грузополучатель,
	|	ИсходящаяТранспортнаяОперацияВЕТИС.ГрузополучательХозяйствующийСубъект.Контрагент  КАК Контрагент,
	|	ПредприятияОтправителя.ТорговыйОбъект                                              КАК ГрузоотправительПредприятие,
	|	ПредприятияОтправителя.ТорговыйОбъект                                              КАК Склад,
	|	ПредприятияПолучателя.ТорговыйОбъект                                               КАК ГрузополучательПредприятие,
	|	ПредприятияПолучателя.ТорговыйОбъект                                               КАК Партнер,
	|	ПредприятияОтправителя.ПроизводственныйОбъект                                      КАК ГрузоотправительПодразделение,
	|	ПредприятияОтправителя.ПроизводственныйОбъект                                      КАК Подразделение,
	|	ПредприятияПолучателя.ПроизводственныйОбъект                                       КАК ГрузополучательПодразделение,
	|	ИсходящаяТранспортнаяОперацияВЕТИС.ПеревозчикХозяйствующийСубъект.Контрагент       КАК Перевозчик,
	|	ИсходящаяТранспортнаяОперацияВЕТИС.СерияТТН                                        КАК СерияТТН,
	|	ИсходящаяТранспортнаяОперацияВЕТИС.ДатаТТН                                         КАК ДатаТТН,
	|	ИсходящаяТранспортнаяОперацияВЕТИС.НомерТТН                                        КАК НомерТТН,
	|	ИсходящаяТранспортнаяОперацияВЕТИС.ТипТТН                                          КАК ТипТТН,
	|	ИсходящаяТранспортнаяОперацияВЕТИС.Комментарий                                     КАК Комментарий,
	|	ИсходящаяТранспортнаяОперацияВЕТИС.Дата                                            КАК Дата
	|ИЗ
	|	Документ.ИсходящаяТранспортнаяОперацияВЕТИС КАК ИсходящаяТранспортнаяОперацияВЕТИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХозяйствующиеСубъектыВЕТИС.Предприятия КАК ПредприятияОтправителя
	|		ПО ИсходящаяТранспортнаяОперацияВЕТИС.ГрузоотправительХозяйствующийСубъект = ПредприятияОтправителя.Ссылка
	|			И ИсходящаяТранспортнаяОперацияВЕТИС.ГрузоотправительПредприятие = ПредприятияОтправителя.Предприятие
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХозяйствующиеСубъектыВЕТИС.Предприятия КАК ПредприятияПолучателя
	|		ПО ИсходящаяТранспортнаяОперацияВЕТИС.ГрузополучательХозяйствующийСубъект = ПредприятияПолучателя.Ссылка
	|			И ИсходящаяТранспортнаяОперацияВЕТИС.ГрузополучательПредприятие = ПредприятияПолучателя.Предприятие
	|ГДЕ
	|	ИсходящаяТранспортнаяОперацияВЕТИС.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходящаяТранспортнаяОперацияВЕТИСТовары.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(СтавкиНДСНоменклатуры.СтавкаНДС, ЕСТЬNULL(ОсновныеСтавкиНДС.СтавкаНДС,
	|		ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка))) КАК СтавкаНДС,
	|	ИсходящаяТранспортнаяОперацияВЕТИСТовары.Характеристика КАК Характеристика,
	|	ИсходящаяТранспортнаяОперацияВЕТИСТовары.Серия КАК Серия,
	|	ИсходящаяТранспортнаяОперацияВЕТИСТовары.Количество КАК Количество,
	|	ИсходящаяТранспортнаяОперацияВЕТИСТовары.Количество КАК КоличествоУпаковок
	|ИЗ
	|	Документ.ИсходящаяТранспортнаяОперацияВЕТИС.Товары КАК ИсходящаяТранспортнаяОперацияВЕТИСТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНДСНоменклатуры.СрезПоследних(&Период,
	|			Страна = &СтранаРегистрации ИЛИ Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)) КАК СтавкиНДСНоменклатуры
	|		ПО ИсходящаяТранспортнаяОперацияВЕТИСТовары.Номенклатура = СтавкиНДСНоменклатуры.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСтавкиНДС.СрезПоследних(&Период, Страна = &СтранаРегистрации) КАК ОсновныеСтавкиНДС
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ИсходящаяТранспортнаяОперацияВЕТИСТовары.Ссылка = &Ссылка";
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат Запрос.ВыполнитьПакет();
	
КонецФункции

Функция СтруктураЗаполненияТабличныхЧастейПоИсходящейТранспортнойОперации(ДанныеЗаполнения,ЕстьСерии = Ложь,ЕстьСкладВТабличнойЧасти = Ложь)
	
	Результат = Новый Структура;
	Результат.Вставить("Товары",              ДанныеЗаполнения[1].Выбрать());
	Если ЕстьСерии Тогда
		Результат.Вставить("ЕстьСерии");
	КонецЕсли;
	Если ЕстьСкладВТабличнойЧасти Тогда
		Результат.Вставить("ЗаполнитьСклад");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииИсходящейТранспортнойОперации(ДокументОбъект,ДанныеЗаполнения)
	
	ДокументОбъект.Товары.Очистить();
	Если ДанныеЗаполнения.Свойство("ЕстьСерии") Тогда
		ИмяТЧСерии = ДанныеЗаполнения.ЕстьСерии;
		Если ИмяТЧСерии = Неопределено Тогда 
			ИмяТЧСерии = "Серии";
		КонецЕсли;
		ДокументОбъект[ИмяТЧСерии].Очистить();
	КонецЕсли;
	
	Пока ДанныеЗаполнения.Товары.Следующий() Цикл
		
		НоваяСтрокаТовары = ДокументОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары,ДанныеЗаполнения.Товары);
		Если ДанныеЗаполнения.Свойство("ЗаполнитьСклад")Тогда 
			НоваяСтрокаТовары.Склад = ДокументОбъект.Склад;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("ЕстьСерии") И ЗначениеЗаполнено(НоваяСтрокаТовары.Серия) Тогда
			НоваяСтрокаСерия = ДокументОбъект[ИмяТЧСерии].Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаСерия,НоваяСтрокаТовары);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаЗаполненияДокументаИсходящаяТранспортнаяОперацияВЕТИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	ТипОснования = ТипЗнч(ДанныеЗаполнения);
	Если ТипОснования = Тип("Структура") Тогда
		Если Не ДанныеЗаполнения.Свойство("Основание") Тогда
			Возврат;
		Иначе
			ТипОснования = ТипЗнч(ДанныеЗаполнения.Основание);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипОснования = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями")Тогда
		ЗаполнитьИсходящуюТранспортнуюОперациюВЕТИСНаОснованииВозвратаТоваровМеждуОрганизациями(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ВозвратТоваровПоставщику")Тогда
		ЗаполнитьИсходящуюТранспортнуюОперациюВЕТИСНаОснованииВозвратаТоваровПоставщику(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")Тогда
		ЗаполнитьИсходящуюТранспортнуюОперациюВЕТИСНаОснованииПередачиТоваровМеждуОрганизациями(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПеремещениеТоваров")Тогда
		ЗаполнитьИсходящуюТранспортнуюОперациюВЕТИСНаОснованииПеремещенияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.РеализацияТоваровУслуг")Тогда
		ЗаполнитьИсходящуюТранспортнуюОперациюВЕТИСНаОснованииРеализацииТоваровУслуг(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	
	
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПередачаТоваровХранителю")Тогда
		ЗаполнитьИсходящуюТранспортнуюОперациюВЕТИСНаОснованииПередачиТоваровХранителю(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ВыкупТоваровХранителем")Тогда
		ЗаполнитьИсходящуюТранспортнуюОперациюВЕТИСНаОснованииВыкупаТоваровХранителем(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ОтгрузкаТоваровСХранения")Тогда
		ЗаполнитьИсходящуюТранспортнуюОперациюВЕТИСНаОснованииОтгрузкиТоваровСХранения(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	КонецЕсли;

	Документы.ИсходящаяТранспортнаяОперацияВЕТИС.ЗаполнитьЗаписиСкладскогоЖурнала(ДокументОбъект);
	Документы.ИсходящаяТранспортнаяОперацияВЕТИС.ЗаполнитьПотребительскиеУпаковки(ДокументОбъект);
	
КонецПроцедуры

Процедура ЗаполнитьИсходящуюТранспортнуюОперациюВЕТИСНаОснованииВозвратаТоваровМеждуОрганизациями(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументОснование.Ссылка                КАК ДокументОснование,
	|	ДокументОснование.Номер                 КАК НомерТТН,
	|	ДокументОснование.Дата                  КАК ДатаТТН,
	|	НЕ ДокументОснование.Проведен           КАК ЕстьОшибкиПроведен,
	|	ДокументОснование.Менеджер              КАК Ответственный,
	|	ДокументОснование.ОрганизацияПолучатель КАК Грузополучатель,
	|	ДокументОснование.Организация           КАК Грузоотправитель,
	|	ДокументОснование.Склад                 КАК СкладОтправитель,
	|	ДокументОснование.Склад                 КАК СкладПолучатель
	|ИЗ
	|	Документ.ВозвратТоваровМеждуОрганизациями КАК ДокументОснование
	|ГДЕ
	|	ДокументОснование.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуИсходящейТранспортнойОперации(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ЗаполнитьТабличнуюЧастьИсходящейТранспортнойОперации(ДокументОбъект, ДанныеЗаполнения, "ВозвратТоваровМеждуОрганизациями", Ложь)
	
КонецПроцедуры

Процедура ЗаполнитьИсходящуюТранспортнуюОперациюВЕТИСНаОснованииВозвратаТоваровПоставщику(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументОснование.Ссылка                КАК ДокументОснование,
	|	ДокументОснование.Номер                 КАК НомерТТН,
	|	ДокументОснование.Дата                  КАК ДатаТТН,
	|	НЕ ДокументОснование.Проведен           КАК ЕстьОшибкиПроведен,
	|	ДокументОснование.Менеджер              КАК Ответственный,
	|	ДокументОснование.Контрагент            КАК Грузополучатель,
	|	ДокументОснование.Организация           КАК Грузоотправитель,
	|	ДокументОснование.Склад                 КАК СкладОтправитель,
	|	ДокументОснование.Партнер               КАК СкладПолучатель
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику КАК ДокументОснование
	|ГДЕ
	|	ДокументОснование.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуИсходящейТранспортнойОперации(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ЗаполнитьТабличнуюЧастьИсходящейТранспортнойОперации(
		ДокументОбъект, ДанныеЗаполнения, "ВозвратТоваровПоставщику", ИнтеграцияИСУТ.ТекстЗапросаВозвратТоваровПоставщику());
	
КонецПроцедуры

Процедура ЗаполнитьИсходящуюТранспортнуюОперациюВЕТИСНаОснованииПередачиТоваровМеждуОрганизациями(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументОснование.Ссылка                КАК ДокументОснование,
	|	ДокументОснование.Номер                 КАК НомерТТН,
	|	ДокументОснование.Дата                  КАК ДатаТТН,
	|	НЕ ДокументОснование.Проведен           КАК ЕстьОшибкиПроведен,
	|	ДокументОснование.Менеджер              КАК Ответственный,
	|	ДокументОснование.ОрганизацияПолучатель КАК Грузополучатель,
	|	ДокументОснование.Организация           КАК Грузоотправитель,
	|	ДокументОснование.Склад                 КАК СкладОтправитель,
	|	ДокументОснование.Склад                 КАК СкладПолучатель
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями КАК ДокументОснование
	|ГДЕ
	|	ДокументОснование.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуИсходящейТранспортнойОперации(ДокументОбъект, ДанныеЗаполнения, Запрос);
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Товары") Тогда
		ЗаполнитьТабличнуюЧастьИсходящейТранспортнойОперации(ДокументОбъект, ДанныеЗаполнения, "ПередачаТоваровМеждуОрганизациями", ДанныеЗаполнения.Товары);
	Иначе
		ЗаполнитьТабличнуюЧастьИсходящейТранспортнойОперации(ДокументОбъект, ДанныеЗаполнения, "ПередачаТоваровМеждуОрганизациями", Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьИсходящуюТранспортнуюОперациюВЕТИСНаОснованииПеремещенияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПеремещениеТоваров.Ссылка           КАК ДокументОснование,
	|	ПеремещениеТоваров.Номер            КАК НомерТТН,
	|	ПеремещениеТоваров.Дата             КАК ДатаТТН,
	|	НЕ ПеремещениеТоваров.Проведен      КАК ЕстьОшибкиПроведен,
	|	ПеремещениеТоваров.Организация      КАК Грузоотправитель,
	|	ВЫБОР
	|		КОГДА ПеремещениеТоваров.ОрганизацияПолучатель = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ПеремещениеТоваров.Организация
	|		ИНАЧЕ ПеремещениеТоваров.ОрганизацияПолучатель
	|	КОНЕЦ                               КАК Грузополучатель,
	|	ПеремещениеТоваров.СкладОтправитель КАК СкладОтправитель,
	|	ПеремещениеТоваров.СкладПолучатель  КАК СкладПолучатель,
	|	ПеремещениеТоваров.Ответственный    КАК Ответственный
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	|ГДЕ
	|	ПеремещениеТоваров.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуИсходящейТранспортнойОперации(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ЗаполнитьТабличнуюЧастьИсходящейТранспортнойОперации(
		ДокументОбъект, ДанныеЗаполнения, "ПеремещениеТоваров", ИнтеграцияИСУТ.ТекстЗапросаПеремещениеТоваров());
	
КонецПроцедуры

Процедура ЗаполнитьИсходящуюТранспортнуюОперациюВЕТИСНаОснованииРеализацииТоваровУслуг(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументОснование.Ссылка                КАК ДокументОснование,
	|	ДокументОснование.Номер                 КАК НомерТТН,
	|	ДокументОснование.Дата                  КАК ДатаТТН,
	|	НЕ ДокументОснование.Проведен           КАК ЕстьОшибкиПроведен,
	|	ДокументОснование.Менеджер              КАК Ответственный,
	|	ДокументОснование.Контрагент            КАК Грузополучатель,
	|	ДокументОснование.Организация           КАК Грузоотправитель,
	|	ДокументОснование.Склад                 КАК СкладОтправитель,
	|	ДокументОснование.Партнер               КАК СкладПолучатель,
	|	ДокументОснование.Склад.ЭтоГруппа       КАК ЭтоГруппаСкладов
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДокументОснование
	|ГДЕ
	|	ДокументОснование.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуИсходящейТранспортнойОперации(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ЗаполнитьТабличнуюЧастьИсходящейТранспортнойОперации(
		ДокументОбъект, ДанныеЗаполнения, "РеализацияТоваровУслуг", ИнтеграцияИСУТ.ТекстЗапросаРеализацияТоваровУслуг());
	
КонецПроцедуры


Процедура ЗаполнитьИсходящуюТранспортнуюОперациюВЕТИСНаОснованииПередачиТоваровХранителю(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументПередачи.Ссылка          КАК ДокументОснование,
	|	ДокументПередачи.Номер           КАК НомерТТН,
	|	ДокументПередачи.Дата            КАК ДатаТТН,
	|	НЕ ДокументПередачи.Проведен     КАК ЕстьОшибкиПроведен,
	|	ДокументПередачи.Менеджер        КАК Ответственный,
	|	ДокументПередачи.Организация     КАК Грузополучатель,
	|	ДокументПередачи.Организация     КАК Грузоотправитель,
	|	ДокументПередачи.Склад           КАК СкладОтправитель,
	|	ДокументПередачи.Партнер         КАК СкладПолучатель,
	|	ДокументПередачи.Склад.ЭтоГруппа КАК ЭтоГруппаСкладов
	|ИЗ
	|	Документ.ПередачаТоваровХранителю КАК ДокументПередачи
	|ГДЕ
	|	ДокументПередачи.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуИсходящейТранспортнойОперации(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ЗаполнитьТабличнуюЧастьИсходящейТранспортнойОперации(ДокументОбъект, ДанныеЗаполнения, "ПередачаТоваровХранителю", Истина);
	
КонецПроцедуры

Процедура ЗаполнитьИсходящуюТранспортнуюОперациюВЕТИСНаОснованииВыкупаТоваровХранителем(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументВыкупа.Ссылка      КАК ДокументОснование,
	|	ДокументВыкупа.Номер       КАК НомерТТН,
	|	ДокументВыкупа.Дата        КАК ДатаТТН,
	|	НЕ ДокументВыкупа.Проведен КАК ЕстьОшибкиПроведен,
	|	ДокументВыкупа.Менеджер    КАК Ответственный,
	|	ДокументВыкупа.Контрагент  КАК Грузополучатель,
	|	ДокументВыкупа.Организация КАК Грузоотправитель,
	|	ДокументВыкупа.Партнер     КАК СкладОтправитель,
	|	ДокументВыкупа.Партнер     КАК СкладПолучатель,
	|	ЛОЖЬ                       КАК ЭтоГруппаСкладов
	|ИЗ
	|	Документ.ВыкупТоваровХранителем КАК ДокументВыкупа
	|ГДЕ
	|	ДокументВыкупа.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуИсходящейТранспортнойОперации(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ЗаполнитьТабличнуюЧастьИсходящейТранспортнойОперации(ДокументОбъект, ДанныеЗаполнения, "ВыкупТоваровХранителем", Ложь);
	
КонецПроцедуры

Процедура ЗаполнитьИсходящуюТранспортнуюОперациюВЕТИСНаОснованииОтгрузкиТоваровСХранения(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументОтгрузки.Ссылка                КАК ДокументОснование,
	|	ДокументОтгрузки.Номер                 КАК НомерТТН,
	|	ДокументОтгрузки.Дата                  КАК ДатаТТН,
	|	НЕ ДокументОтгрузки.Проведен           КАК ЕстьОшибкиПроведен,
	|	ДокументОтгрузки.Менеджер              КАК Ответственный,
	|	ДокументОтгрузки.Контрагент            КАК Грузополучатель,
	|	ДокументОтгрузки.Организация           КАК Грузоотправитель,
	|	ДокументОтгрузки.Склад                 КАК СкладОтправитель,
	|	ДокументОтгрузки.Склад                 КАК СкладПолучатель,
	|	ДокументОтгрузки.Склад.ЭтоГруппа       КАК ЭтоГруппаСкладов
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения КАК ДокументОтгрузки
	|ГДЕ
	|	ДокументОтгрузки.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуИсходящейТранспортнойОперации(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ЗаполнитьТабличнуюЧастьИсходящейТранспортнойОперации(ДокументОбъект, ДанныеЗаполнения, "ОтгрузкаТоваровСХранения", Истина);
	
КонецПроцедуры


#КонецОбласти

#Область ИнвентаризацияПродукцииВЕТИС

Процедура ЗаполнитьШапкуИнвентаризацииПродукции(ДокументОбъект, ДанныеЗаполнения, Запрос)
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	РеквизитыДокумента = Запрос.Выполнить().Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение
		И Не ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		
		ИнтеграцияИСУТ.ПроверитьВозможностьВводаНаОсновании(РеквизитыДокумента);
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
		
		РеквизитыВЕТИС = Справочники.ХозяйствующиеСубъектыВЕТИС.ХозяйствующийСубъектИПредприятиеПоПрикладнымРеквизитам(
			РеквизитыДокумента.Организация, РеквизитыДокумента.ТорговыйОбъект);
		ЗаполнитьЗначенияСвойств(ДокументОбъект,РеквизитыВЕТИС);
		
	Иначе
		ДокументОбъект.ДатаАктаНесоответствия = ?(ЗначениеЗаполнено(ДокументОбъект.ДатаАктаНесоответствия),
			ДокументОбъект.ДатаАктаНесоответствия,
			РеквизитыДокумента.ДатаАктаНесоответствия);
			
		ДокументОбъект.НомерАктаНесоответствия = ?(ЗначениеЗаполнено(ДокументОбъект.НомерАктаНесоответствия),
			ДокументОбъект.НомерАктаНесоответствия,
			РеквизитыДокумента.НомерАктаНесоответствия);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьИнвентаризацииПродукции(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, ДопустимыйЗнак)
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.УпаковкиВЕТИС.Очистить();
	ДокументОбъект.Производители.Очистить();
	ДокументОбъект.ПроизводственныеПартии.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаЗаполненияИнвентаризацииПродукцииИзПрикладногоДокумента(ТекстТоварыДокумента, ДопустимыйЗнак);
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("Документ",          ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.ИнвентаризацияПродукцииВЕТИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия",       Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Тип.ПривестиЗначение());
	Запрос.УстановитьПараметр("ОсобенностьУчета",  ОсобенностиУчетаНоменклатуры());
	
	КолонкиКоличестваВЕТИС = Новый Структура;
	КолонкиКоличестваВЕТИС.Вставить("КоличествоИзменение","КоличествоИзменениеВЕТИС");
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Производители",ДокументОбъект.Производители);
	ДополнительныеПараметры.Вставить("ПроизводственныеПартии",ДокументОбъект.ПроизводственныеПартии);
	
	ЗаполнитьТабличнуюЧастьДокумента(ДокументОбъект.Товары, Запрос.Выполнить(), ДанныеЗаполнения, КолонкиКоличестваВЕТИС,,ДополнительныеПараметры);
	
КонецПроцедуры

Функция ТекстЗапросаЗаполненияИнвентаризацииПродукцииИзПрикладногоДокумента(ТекстТоварыДокумента, ДопустимыйЗнак)
	Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ИнвентаризацияПродукцииВЕТИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовВЕТИС КАК СтатусыДокументовВЕТИС
	|		ПО СтатусыДокументовВЕТИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовВЕТИС.Статус НЕ В(&КонечныеСтатусы)
	|;
	|"
	+
	ТекстТоварыДокумента
	+
	"
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыСерии.Номенклатура                    КАК Номенклатура,
	|	ТоварыСерии.Характеристика                  КАК Характеристика,
	|	ТоварыСерии.Серия                           КАК Серия,
	|	ТоварыСерии.Количество                      КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыСерии КАК ТоварыСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура              КАК Номенклатура,
	|	ОформленныеТовары.Характеристика            КАК Характеристика,
	|	ОформленныеТовары.Серия                     КАК Серия,
	|	-ОформленныеТовары.КоличествоИзменение      КАК Количество
	|ИЗ
	|	Документ.ИнвентаризацияПродукцииВЕТИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка ИЗ ОформленныеДокументы КАК Т)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыВЕТИС.Продукция,СоответствиеНоменклатурыВЕТИС2.Продукция)) КАК Продукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленныеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыВЕТИС КАК СоответствиеНоменклатурыВЕТИС
	|		ПО СоответствиеНоменклатурыВЕТИС.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыВЕТИС.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыВЕТИС.Серия = ТабличнаяЧасть.Серия)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыВЕТИС КАК СоответствиеНоменклатурыВЕТИС2
	|		ПО СоответствиеНоменклатурыВЕТИС2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыВЕТИС2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыВЕТИС2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыВЕТИС.Продукция,СоответствиеНоменклатурыВЕТИС2.Продукция)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура                                        КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика                                      КАК Характеристика,
	|	ТоварыКОформлению.Серия                                               КАК Серия,
	|	СопоставленныеПозиции.Продукция                                        КАК Продукция,
	|	СУММА(ТоварыКОформлению.Количество)                                   КАК КоличествоИзменение,
	|	СУММА(ТоварыКОформлению.Количество)                                   КАК КоличествоИзменениеВЕТИС,
//
	|	ТоварыКОформлению.Серия.ИдентификаторПартииВЕТИС                      КАК ИдентификаторПартии,
	|	ТоварыКОформлению.Серия.ПроизводительВЕТИС                            КАК Производитель,
	|	ЕСТЬNULL(ТоварыКОформлению.Серия.ПроизводительВЕТИС.СтранаИдентификатор,"""") КАК ИдентификаторСтраны,
	|
	|	ВЫБОР СправочникНоменклатура.ЕдиницаИзмеренияСрокаГодности
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ДДММГГГГЧЧ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Сутки)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ДДММГГГГ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Месяц)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ММГГГГ)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ДДММГГГГ)
	|	КОНЕЦ                                                                 КАК ДатаПроизводстваТочностьЗаполнения,
	|	ТоварыКОформлению.Серия.ДатаПроизводства                              КАК ДатаПроизводстваНачалоПериода,
	|
	|	ВЫБОР СправочникНоменклатура.ЕдиницаИзмеренияСрокаГодности
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ДДММГГГГЧЧ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Сутки)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ДДММГГГГ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Месяц)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ММГГГГ)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ДДММГГГГ)
	|	КОНЕЦ                                                                 КАК СрокГодностиТочностьЗаполнения,
	|	ТоварыКОформлению.Серия.ГоденДо                                       КАК СрокГодностиНачалоПериода,
	|
	|	СправочникНоменклатура.ЕдиницаИзмеренияСрокаГодности = ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час) КАК СкоропортящаясяПродукция,
	|	
	|	ВЫБОР
	|		КОГДА ТоварыДругогоКачества.НоменклатураБрак ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК НизкокачественнаяПродукция,
//
	|	ВЫБОР
	|		КОГДА СУММА(ТоварыКОформлению.Количество) > 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ОперацииИнвентаризацииПродукцииВЕТИС.Добавление)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ОперацииИнвентаризацииПродукцииВЕТИС.Изменение)
	|	КОНЕЦ                               КАК Операция
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленныеПозиции КАК СопоставленныеПозиции
	|		ПО СопоставленныеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленныеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленныеПозиции.Серия = ТоварыКОформлению.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|			ПО ТоварыКОформлению.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТоварыДругогоКачества КАК ТоварыДругогоКачества
	|			ПО ТоварыДругогоКачества.ГрадацияКачества <> ЗНАЧЕНИЕ(Перечисление.ГрадацииКачества.Новый)
	|			И ТоварыДругогоКачества.НоменклатураБрак = ТоварыКОформлению.Номенклатура
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленныеПозиции.Продукция,
//	
	|	ТоварыКОформлению.Серия.ИдентификаторПартииВЕТИС,
	|	ТоварыКОформлению.Серия.ПроизводительВЕТИС,
	|	ЕСТЬNULL(ТоварыКОформлению.Серия.ПроизводительВЕТИС.СтранаИдентификатор,""""),
	|
	|	ВЫБОР СправочникНоменклатура.ЕдиницаИзмеренияСрокаГодности
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ДДММГГГГЧЧ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Сутки)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ДДММГГГГ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Месяц)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ММГГГГ)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ДДММГГГГ)
	|	КОНЕЦ,
	|	ТоварыКОформлению.Серия.ДатаПроизводства,
	|
	|	ВЫБОР СправочникНоменклатура.ЕдиницаИзмеренияСрокаГодности
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ДДММГГГГЧЧ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Сутки)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ДДММГГГГ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Месяц)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ММГГГГ)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ДДММГГГГ)
	|	КОНЕЦ,
	|	ТоварыКОформлению.Серия.ГоденДо,
	|
	|	СправочникНоменклатура.ЕдиницаИзмеренияСрокаГодности = ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час),
	|	
	|	ВЫБОР
	|		КОГДА ТоварыДругогоКачества.НоменклатураБрак ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
//
	|ИМЕЮЩИЕ ДопустимыйЗнак
	|";
	Если ДопустимыйЗнак = -1 Тогда
		Текст = СтрЗаменить(Текст, "ДопустимыйЗнак", "СУММА(ТоварыКОформлению.Количество)<0");
	ИначеЕсли ДопустимыйЗнак = 1 Тогда
		Текст = СтрЗаменить(Текст, "ДопустимыйЗнак", "СУММА(ТоварыКОформлению.Количество)>0");
	Иначе
		Текст = СтрЗаменить(Текст, "ДопустимыйЗнак", "СУММА(ТоварыКОформлению.Количество)<>0");
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

Функция ДанныеПрикладныхДокументовИзИнвентаризацииПродукцииВЕТИС(ДокументСсылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Запрос.УстановитьПараметр("Период", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Дата"));
	Запрос.УстановитьПараметр("СтранаРегистрации", ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Предприятия.ТорговыйОбъект                                   КАК ТорговыйОбъект,
	|	Предприятия.ТорговыйОбъект                                   КАК Склад,
	|	Предприятия.ПроизводственныйОбъект                           КАК Подразделение,
	|	ИнвентаризацияПродукцииВЕТИС.Комментарий                     КАК Комментарий,
	|	ИнвентаризацияПродукцииВЕТИС.Дата                            КАК Дата,
	|	ИнвентаризацияПродукцииВЕТИС.ХозяйствующийСубъект.Контрагент КАК ХозяйствующийСубъект,
	|	ИнвентаризацияПродукцииВЕТИС.ХозяйствующийСубъект.Контрагент КАК Организация
	|ИЗ
	|	Документ.ИнвентаризацияПродукцииВЕТИС КАК ИнвентаризацияПродукцииВЕТИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХозяйствующиеСубъектыВЕТИС.Предприятия КАК Предприятия
	|		ПО ИнвентаризацияПродукцииВЕТИС.ХозяйствующийСубъект = Предприятия.Ссылка
	|			И ИнвентаризацияПродукцииВЕТИС.Предприятие = Предприятия.Предприятие
	|ГДЕ
	|	ИнвентаризацияПродукцииВЕТИС.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнвентаризацияПродукцииВЕТИСТовары.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(СтавкиНДСНоменклатуры.СтавкаНДС, ЕСТЬNULL(ОсновныеСтавкиНДС.СтавкаНДС,
	|		зНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка))) КАК СтавкаНДС,
	|	ИнвентаризацияПродукцииВЕТИСТовары.Характеристика КАК Характеристика,
	|	ИнвентаризацияПродукцииВЕТИСТовары.Серия КАК Серия,
	|	ИнвентаризацияПродукцииВЕТИСТовары.КоличествоИзменение КАК Количество,
	|	ИнвентаризацияПродукцииВЕТИСТовары.КоличествоИзменение КАК КоличествоУпаковок
	|ИЗ
	|	Документ.ИнвентаризацияПродукцииВЕТИС.Товары КАК ИнвентаризацияПродукцииВЕТИСТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНДСНоменклатуры.СрезПоследних(&Период,
	|			Страна = &СтранаРегистрации ИЛИ Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)) КАК СтавкиНДСНоменклатуры
	|		ПО ИнвентаризацияПродукцииВЕТИСТовары.Номенклатура = СтавкиНДСНоменклатуры.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСтавкиНДС.СрезПоследних(&Период, Страна = &СтранаРегистрации) КАК
	|			ОсновныеСтавкиНДС
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ИнвентаризацияПродукцииВЕТИСТовары.Ссылка = &Ссылка";
	Возврат Запрос.ВыполнитьПакет();
	
КонецФункции

Функция СтруктураЗаполненияТабличныхЧастейПоИнвентаризацииПродукции(ДанныеЗаполнения, ЕстьСерии = Ложь, ДопустимыйЗнакСтроки = -1)
	
	Результат = Новый Структура;
	Результат.Вставить("Товары",               ДанныеЗаполнения[1].Выбрать());
	Результат.Вставить("ДопустимыйЗнакСтроки", ДопустимыйЗнакСтроки);
	Если ЕстьСерии Тогда
		Результат.Вставить("ЕстьСерии");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииИнвентаризацииПродукции(ДокументОбъект,ДанныеЗаполнения)
	
	ДокументОбъект.Товары.Очистить();
	Если ДанныеЗаполнения.Свойство("ЕстьСерии") Тогда
		ИмяТЧСерии = ДанныеЗаполнения.ЕстьСерии;
		Если ИмяТЧСерии = Неопределено Тогда 
			ИмяТЧСерии = "Серии";
		КонецЕсли;
		ДокументОбъект[ИмяТЧСерии].Очистить();
	КонецЕсли;
	
	Пока ДанныеЗаполнения.Товары.Следующий() Цикл
		
		СтруктураЗаполненияКоличества = Новый Структура("Количество,КоличествоУпаковок");
		СтруктураЗаполненияКоличества.Количество = ДанныеЗаполнения.Товары.Количество*ДанныеЗаполнения.ДопустимыйЗнакСтроки;
		СтруктураЗаполненияКоличества.КоличествоУпаковок = ДанныеЗаполнения.Товары.КоличествоУпаковок*ДанныеЗаполнения.ДопустимыйЗнакСтроки;
		
		Если СтруктураЗаполненияКоличества.Количество <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрокаТовары = ДокументОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары,ДанныеЗаполнения.Товары);
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары,СтруктураЗаполненияКоличества);
		
		Если ДанныеЗаполнения.Свойство("ЕстьСерии") И ЗначениеЗаполнено(НоваяСтрокаТовары.Серия) Тогда
			НоваяСтрокаСерия = ДокументОбъект[ИмяТЧСерии].Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаСерия,НоваяСтрокаТовары);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаЗаполненияДокументаИнвентаризацияПродукцииВЕТИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВнутреннееПотребление")Тогда 
		ЗаполнитьИнвентаризациюПродукцииВЕТИСНаОснованииВнутреннегоПотребленияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОприходованиеИзлишковТоваров")Тогда 
		ЗаполнитьИнвентаризациюПродукцииВЕТИСНаОснованииОприходованияИзлишковТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПересортицаТоваров")Тогда 
		ЗаполнитьИнвентаризациюПродукцииВЕТИСНаОснованииПересортицыТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПорчаТоваров")Тогда 
		ЗаполнитьИнвентаризациюПродукцииВЕТИСНаОснованииПорчиТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.СписаниеНедостачТоваров")Тогда 
		ЗаполнитьИнвентаризациюПродукцииВЕТИСНаОснованииСписанияНедостачТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПрочееОприходованиеТоваров")Тогда 
		ЗаполнитьИнвентаризациюПродукцииВЕТИСНаОснованииПрочегоОприходованияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьИнвентаризациюПродукцииВЕТИСНаОснованииВнутреннегоПотребленияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	ДокументСсылка.Номер         КАК НомерАктаНесоответствия,
	|	ДокументСсылка.Дата          КАК ДатаАктаНесоответствия,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный КАК Ответственный,
	|	ДокументСсылка.Организация   КАК Организация,
	|	ДокументСсылка.Склад         КАК ТорговыйОбъект
	|ИЗ
	|	Документ.ВнутреннееПотребление КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуИнвентаризацииПродукции(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = ИнтеграцияИСУТ.ТекстЗапросаВнутреннееПотребление()
	+"
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура                                КАК Номенклатура,
	|	Товары.Характеристика                              КАК Характеристика,
	|	Товары.Серия                                       КАК Серия,
	|	-СУММА(Товары.Количество)                           КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	ТаблицаТоварыВнутреннееПотребление КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЗерноВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗернаВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС)
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьИнвентаризацииПродукции(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, -1);
	
КонецПроцедуры

Процедура ЗаполнитьИнвентаризациюПродукцииВЕТИСНаОснованииОприходованияИзлишковТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	ДокументСсылка.Номер         КАК НомерАктаНесоответствия,
	|	ДокументСсылка.Дата          КАК ДатаАктаНесоответствия,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный КАК Ответственный,
	|	ДокументСсылка.Организация   КАК Организация,
	|	ДокументСсылка.Склад         КАК ТорговыйОбъект
	|ИЗ
	|	Документ.ОприходованиеИзлишковТоваров КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуИнвентаризацииПродукции(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура      КАК Номенклатура,
	|	Товары.Характеристика    КАК Характеристика,
	|	Товары.Серия             КАК Серия,
	|	СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.ОприходованиеИзлишковТоваров.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И (СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЗерноВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗернаВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС))
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьИнвентаризацииПродукции(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, 1);
	
КонецПроцедуры

Процедура ЗаполнитьИнвентаризациюПродукцииВЕТИСНаОснованииПересортицыТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	ДокументСсылка.Номер         КАК НомерАктаНесоответствия,
	|	ДокументСсылка.Дата          КАК ДатаАктаНесоответствия,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный КАК Ответственный,
	|	ДокументСсылка.Организация   КАК Организация,
	|	ДокументСсылка.Склад         КАК ТорговыйОбъект
	|ИЗ
	|	Документ.ПересортицаТоваров КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуИнвентаризацииПродукции(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура       КАК Номенклатура,
	|	Товары.Характеристика     КАК Характеристика,
	|	Товары.Серия              КАК Серия,
	|	-СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.ПересортицаТоваров.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И (СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЗерноВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗернаВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС))
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.НоменклатураОприходование,
	|	Товары.ХарактеристикаОприходование,
	|	Товары.СерияОприходование,
	|	СУММА(Товары.Количество)
	|ИЗ
	|	Документ.ПересортицаТоваров.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.НоменклатураОприходование = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И (СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЗерноВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗернаВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС))
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.НоменклатураОприходование,
	|	Товары.ХарактеристикаОприходование,
	|	Товары.СерияОприходование
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьИнвентаризацииПродукции(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, 0);
	
КонецПроцедуры

Процедура ЗаполнитьИнвентаризациюПродукцииВЕТИСНаОснованииПорчиТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	ДокументСсылка.Номер         КАК НомерАктаНесоответствия,
	|	ДокументСсылка.Дата          КАК ДатаАктаНесоответствия,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный КАК Ответственный,
	|	ДокументСсылка.Организация   КАК Организация,
	|	ДокументСсылка.Склад         КАК ТорговыйОбъект
	|ИЗ
	|	Документ.ПорчаТоваров КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуИнвентаризацииПродукции(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура       КАК Номенклатура,
	|	Товары.Характеристика     КАК Характеристика,
	|	Товары.Серия              КАК Серия,
	|	-СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.ПорчаТоваров.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И (СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЗерноВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗернаВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС))
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.НоменклатураОприходование,
	|	Товары.ХарактеристикаОприходование,
	|	Товары.Серия,
	|	СУММА(Товары.Количество)
	|ИЗ
	|	Документ.ПорчаТоваров.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.НоменклатураОприходование = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И (СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЗерноВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗернаВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС))
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.НоменклатураОприходование,
	|	Товары.ХарактеристикаОприходование,
	|	Товары.Серия
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьИнвентаризацииПродукции(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, 0);
	
КонецПроцедуры

Процедура ЗаполнитьИнвентаризациюПродукцииВЕТИСНаОснованииСписанияНедостачТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	ДокументСсылка.Номер         КАК НомерАктаНесоответствия,
	|	ДокументСсылка.Дата          КАК ДатаАктаНесоответствия,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный КАК Ответственный,
	|	ДокументСсылка.Организация   КАК Организация,
	|	ДокументСсылка.Склад         КАК ТорговыйОбъект
	|ИЗ
	|	Документ.СписаниеНедостачТоваров КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуИнвентаризацииПродукции(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура       КАК Номенклатура,
	|	Товары.Характеристика     КАК Характеристика,
	|	Товары.Серия              КАК Серия,
	|	-СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.СписаниеНедостачТоваров.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И (СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЗерноВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗернаВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС))
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьИнвентаризацииПродукции(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, -1);
	
КонецПроцедуры

Процедура ЗаполнитьИнвентаризациюПродукцииВЕТИСНаОснованииПрочегоОприходованияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка        КАК ДокументОснование,
	|	ДокументСсылка.Номер         КАК НомерАктаНесоответствия,
	|	ДокументСсылка.Дата          КАК ДатаАктаНесоответствия,
	|	НЕ ДокументСсылка.Проведен   КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Ответственный КАК Ответственный,
	|	ДокументСсылка.Организация   КАК Организация,
	|	ДокументСсылка.Склад         КАК ТорговыйОбъект
	|ИЗ
	|	Документ.ПрочееОприходованиеТоваров КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ЗаполнитьШапкуИнвентаризацииПродукции(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	ТекстТоварыДокумента = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура                                КАК Номенклатура,
	|	Товары.Характеристика                              КАК Характеристика,
	|	Товары.Серия                                       КАК Серия,
	|	СУММА(Товары.Количество)                           КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.ПрочееОприходованиеТоваров.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И (СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЗерноВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗернаВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС))
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.Номенклатура                                КАК Номенклатура,
	|	Серии.Характеристика                              КАК Характеристика,
	|	Серии.Серия                                       КАК Серия,
	|	СУММА(Серии.Количество)                          КАК Количество
	|ПОМЕСТИТЬ Серии
	|ИЗ
	|	Документ.ПрочееОприходованиеТоваров.Серии КАК Серии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Серии.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Серии.Ссылка = &ДокументОснование
	|	И (СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЗерноВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗернаВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС))
	|СГРУППИРОВАТЬ ПО
	|	Серии.Номенклатура,
	|	Серии.Характеристика,
	|	Серии.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура                                КАК Номенклатура,
	|	Товары.Характеристика                              КАК Характеристика,
	|	ЕСТЬNULL(Серии.Серия,Товары.Серия)                 КАК Серия,
	|	ЕСТЬNULL(Серии.Количество, Товары.Количество)      КАК Количество
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Серии КАК Серии
	|			ПО Серии.Номенклатура = Товары.Номенклатура
	|			И Серии.Характеристика = Товары.Характеристика
	|;
	|";
	
	ЗаполнитьТабличнуюЧастьИнвентаризацииПродукции(ДокументОбъект, ДанныеЗаполнения, ТекстТоварыДокумента, 1);
	
КонецПроцедуры

#КонецОбласти

#Область ПроизводственнаяОперацияВЕТИС

Функция ДанныеПрикладныхДокументовИзПроизводственнойОперацииВЕТИС(ДокументСсылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Запрос.УстановитьПараметр("Период", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Дата"));
	Запрос.УстановитьПараметр("СтранаРегистрации", ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПроизводственнаяОперацияВЕТИС.Ссылка КАК ДокументОснование,
	|	ПроизводственнаяОперацияВЕТИС.Дата КАК Дата,
	|	ХозяйствующиеСубъектыВЕТИСПредприятия.Ссылка.Контрагент КАК Организация,
	|	ХозяйствующиеСубъектыВЕТИСПредприятия.ТорговыйОбъект КАК ТорговыйОбъект,
	|	ХозяйствующиеСубъектыВЕТИСПредприятия.ПроизводственныйОбъект КАК ПроизводственныйОбъект,
	|	ПроизводственнаяОперацияВЕТИС.Комментарий КАК Комментарий,
	|	НЕ ПроизводственнаяОперацияВЕТИС.Проведен КАК ЕстьОшибкиПроведен
	|ИЗ
	|	Документ.ПроизводственнаяОперацияВЕТИС КАК ПроизводственнаяОперацияВЕТИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХозяйствующиеСубъектыВЕТИС.Предприятия КАК ХозяйствующиеСубъектыВЕТИСПредприятия
	|		ПО ПроизводственнаяОперацияВЕТИС.ХозяйствующийСубъект = ХозяйствующиеСубъектыВЕТИСПредприятия.Ссылка
	|			И ПроизводственнаяОперацияВЕТИС.Предприятие = ХозяйствующиеСубъектыВЕТИСПредприятия.Предприятие
	|ГДЕ
	|	ПроизводственнаяОперацияВЕТИС.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроизводственнаяОперацияВЕТИСТовары.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(СтавкиНДСНоменклатуры.СтавкаНДС, ЕСТЬNULL(ОсновныеСтавкиНДС.СтавкаНДС,
	|		ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка))) КАК СтавкаНДС,
	|	ПроизводственнаяОперацияВЕТИСТовары.Характеристика КАК Характеристика,
	|	ПроизводственнаяОперацияВЕТИСТовары.Серия КАК Серия,
	|	ПроизводственнаяОперацияВЕТИСТовары.Количество КАК Количество,
	|	ПроизводственнаяОперацияВЕТИСТовары.Количество КАК КоличествоУпаковок
	|ИЗ
	|	Документ.ПроизводственнаяОперацияВЕТИС.Товары КАК ПроизводственнаяОперацияВЕТИСТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНДСНоменклатуры.СрезПоследних(&Период,
	|			Страна = &СтранаРегистрации ИЛИ Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)) КАК СтавкиНДСНоменклатуры
	|		ПО ПроизводственнаяОперацияВЕТИСТовары.Номенклатура = СтавкиНДСНоменклатуры.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСтавкиНДС.СрезПоследних(&Период, Страна = &СтранаРегистрации) КАК
	|			ОсновныеСтавкиНДС
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ПроизводственнаяОперацияВЕТИСТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроизводственнаяОперацияВЕТИССырье.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(СтавкиНДСНоменклатуры.СтавкаНДС, ЕСТЬNULL(ОсновныеСтавкиНДС.СтавкаНДС,
	|		ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка))) КАК СтавкаНДС,
	|	ПроизводственнаяОперацияВЕТИССырье.Характеристика КАК Характеристика,
	|	ПроизводственнаяОперацияВЕТИССырье.Серия КАК Серия,
	|	ПроизводственнаяОперацияВЕТИССырье.Количество КАК Количество,
	|	ПроизводственнаяОперацияВЕТИССырье.Количество КАК КоличествоУпаковок
	|ИЗ
	|	Документ.ПроизводственнаяОперацияВЕТИС.Сырье КАК ПроизводственнаяОперацияВЕТИССырье
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНДСНоменклатуры.СрезПоследних(&Период,
	|			Страна = &СтранаРегистрации ИЛИ Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)) КАК СтавкиНДСНоменклатуры
	|		ПО ПроизводственнаяОперацияВЕТИССырье.Номенклатура = СтавкиНДСНоменклатуры.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСтавкиНДС.СрезПоследних(&Период, Страна = &СтранаРегистрации) КАК
	|			ОсновныеСтавкиНДС
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ПроизводственнаяОперацияВЕТИССырье.Ссылка = &Ссылка";
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат Запрос.ВыполнитьПакет();
	
КонецФункции

Процедура ОбработкаЗаполненияДокументаПроизводственнаяОперацияВЕТИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	ДанныеПрикладногоДокумента = СтруктураЗапросаДанныеПрикладногоДокумента(ДанныеЗаполнения);
	
	Если Не ЗначениеЗаполнено(ДанныеПрикладногоДокумента) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(ДанныеПрикладногоДокумента.Реквизиты);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	РеквизитыДокумента = Запрос.Выполнить().Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение И Не ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		
		ИнтеграцияИСУТ.ПроверитьВозможностьВводаНаОсновании(РеквизитыДокумента);
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
		
		Данные = ДанныеПартииПроизводстваПоДокументуОснования(ДанныеЗаполнения);
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Данные);
		
		СоответствиеСубъектов = Справочники.ХозяйствующиеСубъектыВЕТИС.ХозяйствующийСубъектИПредприятиеПоПрикладнымРеквизитам(
												РеквизитыДокумента.ОрганизацияКонтрагент,
												РеквизитыДокумента.ТорговыйПроизводственныйОбъект);
	
		ЗаполнитьЗначенияСвойств(ДокументОбъект, СоответствиеСубъектов);
		
	КонецЕсли;
	
	Запрос = Новый Запрос();
	СписокЗапросов = Новый СписокЗначений();
	
	СписокЗапросов.Добавить(ДанныеПрикладногоДокумента.Товары);
	СписокЗапросов.Добавить(ДанныеПрикладногоДокумента.Сырье);
	СписокЗапросов.Добавить(ДанныеПрикладногоДокумента.Штрихкоды);
	
	СтруктураЗапроса = Документы.ПроизводственнаяОперацияВЕТИС.СтруктураЗапросаДанныеОформленныхДокументов();
	
	СписокЗапросов.Добавить(СтруктураЗапроса.ОформленныеДокументы);
	СписокЗапросов.Добавить(СтруктураЗапроса.ОформленныеТовары);
	СписокЗапросов.Добавить(СтруктураЗапроса.ОформленноеСырье);
	СписокЗапросов.Добавить(СтруктураЗапроса.ОформленныеШтрихкоды);
	
	СтруктураЗапроса = СтруктураЗапросаДанныеЗаполненияПроизводственнойОперации(ЗначениеЗаполнено(ДокументОбъект.Предприятие));
	
	СписокЗапросов.Добавить(СтруктураЗапроса.ТоварыКОформлению);
	СписокЗапросов.Добавить(СтруктураЗапроса.СырьеКОформлению);
	СписокЗапросов.Добавить(СтруктураЗапроса.СопоставленныеПозиции);
	
	СписокЗапросов.Добавить(СтруктураЗапроса.Товары, "Товары");
	СписокЗапросов.Добавить(СтруктураЗапроса.Сырье, "Сырье");
	СписокЗапросов.Добавить(СтруктураЗапроса.Штрихкоды, "Штрихкоды");
	СписокЗапросов.Добавить(СтруктураЗапроса.ТехнологическийПроцесс, "ТехнологическийПроцесс");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы", Документы.ПроизводственнаяОперацияВЕТИС.КонечныеСтатусы());
	
	Запрос.УстановитьПараметр("ДатаОперацииНачалоПериода", РеквизитыДокумента.ДатаОперацииНачалоПериода);
	Запрос.УстановитьПараметр("ДатаОперацииКонецПериода",  РеквизитыДокумента.ДатаОперацииКонецПериода);
	
	Запрос.УстановитьПараметр("ПустаяСерия", Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Тип.ПривестиЗначение());
	Запрос.УстановитьПараметр("ХозяйствующийСубъект", ДокументОбъект.ХозяйствующийСубъект);
	Запрос.УстановитьПараметр("Предприятие", ДокументОбъект.Предприятие);
	
	Если ДанныеПрикладногоДокумента.Свойство("ТаблицаСырьеПоСпецификациям") Тогда
		Запрос.УстановитьПараметр("ТаблицаСырьеПоСпецификациям", ДанныеПрикладногоДокумента.ТаблицаСырьеПоСпецификациям);
	КонецЕсли;
	
	РезультатЗапроса = ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов);
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.Сырье.Очистить();
	
	ДокументОбъект.УпаковкиВЕТИС.Очистить();
	ДокументОбъект.ШтрихкодыУпаковок.Очистить();
	
	ДокументОбъект.ТехнологическийПроцесс.Очистить();
	
	КолонкиКоличествоВЕТИС = Новый Структура;
	КолонкиКоличествоВЕТИС.Вставить("Количество","КоличествоВЕТИС");
	
	Если Не РезультатЗапроса["Товары"].Пустой() Тогда
		ЗаполнитьТабличнуюЧастьДокумента(
			ДокументОбъект["Товары"],
			РезультатЗапроса["Товары"],
			ДанныеЗаполнения,
			КолонкиКоличествоВЕТИС,
			Истина);
	КонецЕсли;
	
	Если Не РезультатЗапроса["Сырье"].Пустой() Тогда
		ЗаполнитьТабличнуюЧастьДокумента(
			ДокументОбъект["Сырье"],
			РезультатЗапроса["Сырье"],
			ДанныеЗаполнения,
			КолонкиКоличествоВЕТИС,
			Истина);
	КонецЕсли;
	
	Если Не РезультатЗапроса["Штрихкоды"].Пустой() Тогда
		ШтрихкодыУпаковок = РезультатЗапроса["Штрихкоды"].Выгрузить();
		Содержимое = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое(ШтрихкодыУпаковок);
		Содержимое.Индексы.Добавить("Обработан,Номенклатура,Характеристика,Серия");
		Отбор = Новый Структура("Обработан,Номенклатура,Характеристика,Серия", Ложь);
		Для Каждого СтрокаТЧ Из ДокументОбъект.Товары Цикл
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаТЧ);
			Штрихкоды = Содержимое.НайтиСтроки(Отбор);
			Для Каждого Штрихкод Из Штрихкоды Цикл
				Штрихкод.Обработан = Истина;
				
				ОтборСтрокаУпаковки = Новый Структура;
				ОтборСтрокаУпаковки.Вставить("ИдентификаторСтрокиТовары", СтрокаТЧ.ИдентификаторСтроки);
				Если Штрихкод.ВидУпаковки = Перечисления.ВидыУпаковокИС.Потребительская Тогда
					ОтборСтрокаУпаковки.Вставить("УровеньУпаковки", Перечисления.УровниУпаковокВЕТИС.ПотребительскийУровень);
				Иначе
					ОтборСтрокаУпаковки.Вставить("УровеньУпаковки", Перечисления.УровниУпаковокВЕТИС.ТранспортныйУровень);
				КонецЕсли;
				СтрокиУпаковки = ДокументОбъект.УпаковкиВЕТИС.НайтиСтроки(ОтборСтрокаУпаковки);
				Если СтрокиУпаковки.Количество() Тогда
					СтрокаУпаковки = СтрокиУпаковки[0];
				Иначе
					СтрокаУпаковки = ДокументОбъект.УпаковкиВЕТИС.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаУпаковки, ОтборСтрокаУпаковки);
					СтрокаУпаковки.ИдентификаторСтроки = Новый УникальныйИдентификатор;
				КонецЕсли;
				СтрокаУпаковки.КоличествоУпаковокВЕТИС = СтрокаУпаковки.КоличествоУпаковокВЕТИС + 1;
				
				НоваяСтрока = ДокументОбъект.ШтрихкодыУпаковок.Добавить();
				НоваяСтрока.ШтрихкодУпаковки = Штрихкод.Штрихкод;
				НоваяСтрока.Штрихкод = Штрихкод.ЗначениеШтрихкода;
				НоваяСтрока.ИдентификаторСтроки = СтрокаУпаковки.ИдентификаторСтроки;
				НоваяСтрока.ТипМаркировки = Перечисления.ТипыМаркировкиУпаковокВЕТИС.Произвольная;
				
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	ДокументОбъект.ТехнологическийПроцесс.Загрузить(РезультатЗапроса["ТехнологическийПроцесс"].Выгрузить());
	
	Документы.ПроизводственнаяОперацияВетис.ЗаполнитьЗаписиСкладскогоЖурнала(
			ДокументОбъект);
	
КонецПроцедуры

Функция СтруктураЗапросаДанныеЗаполненияПроизводственнойОперации(ЕстьОтборПоПредприятию)
	
	СтруктураЗапроса = Новый Структура();
	
	// Товары к оформлению
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура          КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика        КАК Характеристика,
	|	ТабличнаяЧасть.Серия                 КАК Серия,
	|	ТабличнаяЧасть.ДатаПроизводства      КАК ДатаПроизводства,
	|	СУММА(ТабличнаяЧасть.Количество)     КАК Количество
	|ПОМЕСТИТЬ ТоварыКРаспределению
	|ИЗ (
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура     КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика   КАК Характеристика,
	|	ТабличнаяЧасть.Серия            КАК Серия,
	|	ТабличнаяЧасть.ДатаПроизводства КАК ДатаПроизводства,
	|	ТабличнаяЧасть.Количество       КАК Количество
	|ИЗ
	|	Товары КАК ТабличнаяЧасть
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура     КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика   КАК Характеристика,
	|	ТабличнаяЧасть.Серия            КАК Серия,
	|	ТабличнаяЧасть.ДатаПроизводства КАК ДатаПроизводства,
	|	-ТабличнаяЧасть.Количество      КАК Количество
	|ИЗ
	|	ОформленныеТовары КАК ТабличнаяЧасть
	|
	|) КАК ТабличнаяЧасть
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.ДатаПроизводства,
	|	ТабличнаяЧасть.Серия
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТабличнаяЧасть.Количество) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Серия,
	|	ДатаПроизводства
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура          КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика        КАК Характеристика,
	|	ТабличнаяЧасть.Серия                 КАК Серия,
	|	СУММА(ТабличнаяЧасть.Количество)     КАК Количество
	|ПОМЕСТИТЬ ТоварыБазаРаспределения
	|ИЗ (
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура     КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика   КАК Характеристика,
	|	ТабличнаяЧасть.Серия            КАК Серия,
	|	ТабличнаяЧасть.Количество       КАК Количество
	|ИЗ
	|	Товары КАК ТабличнаяЧасть
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура     КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика   КАК Характеристика,
	|	ТабличнаяЧасть.Серия            КАК Серия,
	|	-ТабличнаяЧасть.Количество      КАК Количество
	|ИЗ
	|	ОформленныеТовары КАК ТабличнаяЧасть
	|
	|) КАК ТабличнаяЧасть
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТабличнаяЧасть.Количество) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Серия
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|
	|	ТоварыКРаспределению.Номенклатура     КАК Номенклатура,
	|	ТоварыКРаспределению.Характеристика   КАК Характеристика,
	|	ТоварыКРаспределению.Серия            КАК Серия,
	|	ТоварыКРаспределению.ДатаПроизводства КАК ДатаПроизводства,
	|
	|	СУММА(ТоварыКРаспределениюДополнение.Количество) - МИНИМУМ(ТоварыКРаспределению.Количество) КАК ЛеваяГраница,
	|	СУММА(ТоварыКРаспределениюДополнение.Количество)                                            КАК ПраваяГраница
	|
	|ПОМЕСТИТЬ ТоварыКРаспределениюПоДатам
	|ИЗ
	|	ТоварыКРаспределению КАК ТоварыКРаспределению
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТоварыКРаспределению КАК ТоварыКРаспределениюДополнение
	|
	|			ПО ТоварыКРаспределению.Номенклатура      = ТоварыКРаспределениюДополнение.Номенклатура
	|			 И ТоварыКРаспределению.Характеристика    = ТоварыКРаспределениюДополнение.Характеристика
	|			 И ТоварыКРаспределению.Серия             = ТоварыКРаспределениюДополнение.Серия
	|			 И ТоварыКРаспределению.ДатаПроизводства <= ТоварыКРаспределениюДополнение.ДатаПроизводства
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКРаспределению.Номенклатура,
	|	ТоварыКРаспределению.Характеристика,
	|	ТоварыКРаспределению.Серия,
	|	ТоварыКРаспределению.ДатаПроизводства
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКРаспределениюПоДатам.Номенклатура     КАК Номенклатура,
	|	ТоварыКРаспределениюПоДатам.Характеристика   КАК Характеристика,
	|	ТоварыКРаспределениюПоДатам.Серия            КАК Серия,
	|	ТоварыКРаспределениюПоДатам.ДатаПроизводства КАК ДатаПроизводства,
	|	(ВЫБОР
	|		КОГДА ТоварыКРаспределению.Количество < ТоварыКРаспределениюПоДатам.ПраваяГраница
	|			ТОГДА ТоварыКРаспределению.Количество
	|			ИНАЧЕ ТоварыКРаспределениюПоДатам.ПраваяГраница
	|		КОНЕЦ - ТоварыКРаспределениюПоДатам.ЛеваяГраница
	|	)                                            КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыКРаспределениюПоДатам КАК ТоварыКРаспределениюПоДатам
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТоварыБазаРаспределения КАК ТоварыКРаспределению
	|
	|			ПО ТоварыКРаспределениюПоДатам.Номенклатура   = ТоварыКРаспределению.Номенклатура
	|			 И ТоварыКРаспределениюПоДатам.Характеристика = ТоварыКРаспределению.Характеристика
	|			 И ТоварыКРаспределениюПоДатам.Серия          = ТоварыКРаспределению.Серия
	|			 И ТоварыКРаспределениюПоДатам.ЛеваяГраница   < ТоварыКРаспределению.Количество
	|";
	СтруктураЗапроса.Вставить("ТоварыКОформлению", ТекстЗапроса);
	
	// Сырье к оформлению
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура          КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика        КАК Характеристика,
	|	ТоварыКОформлению.Серия                 КАК Серия,
	|	СУММА(ТоварыКОформлению.Количество)     КАК Количество
	|ПОМЕСТИТЬ СырьеКОформлению
	|ИЗ (
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество     КАК Количество
	|ИЗ
	|	Сырье КАК ТабличнаяЧасть
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	-ТабличнаяЧасть.Количество    КАК Количество
	|ИЗ
	|	ОформленноеСырье КАК ТабличнаяЧасть
	|
	|) КАК ТоварыКОформлению
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|";
	СтруктураЗапроса.Вставить("СырьеКОформлению", ТекстЗапроса);
	
	ТекстЗапроса = ТекстЗапросаСоответствиеНоменклатурыВЕТИС("СопоставленныеПозиции", Новый Структура("ТоварыКОформлению, СырьеКОформлению", ЕстьОтборПоПредприятию, Ложь));
	СтруктураЗапроса.Вставить("СопоставленныеПозиции", ТекстЗапроса);
	
	// Товары
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СопоставленныеПозиции.Продукция         КАК Продукция,
	|
	|	ТоварыКОформлению.Номенклатура         КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика       КАК Характеристика,
	|	ТоварыКОформлению.Серия                КАК Серия,
	|
	|	ТоварыКОформлению.Количество    КАК Количество,
	|	ТоварыКОформлению.Количество    КАК КоличествоВЕТИС,
	|
	|	ISNULL(ТоварыКОформлению.Серия.ИдентификаторПартииВЕТИС, """") КАК ИдентификаторПартии,
	|
	|	ВЫБОР СправочникНоменклатура.ЕдиницаИзмеренияСрокаГодности
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ДДММГГГГЧЧ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Сутки)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ДДММГГГГ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Месяц)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ММГГГГ)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ДДММГГГГ)
	|	КОНЕЦ                                                          КАК ДатаПроизводстваТочностьЗаполнения,
	|	ВЫБОР СправочникНоменклатура.ЕдиницаИзмеренияСрокаГодности
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
	|			ТОГДА НАЧАЛОПЕРИОДА(ТоварыКОформлению.ДатаПроизводства, ЧАС)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Сутки)
	|			ТОГДА НАЧАЛОПЕРИОДА(ТоварыКОформлению.ДатаПроизводства, ДЕНЬ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Месяц)
	|			ТОГДА НАЧАЛОПЕРИОДА(ТоварыКОформлению.ДатаПроизводства, МЕСЯЦ)
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ТоварыКОформлению.ДатаПроизводства, ДЕНЬ)
	|	КОНЕЦ                                                          КАК ДатаПроизводстваНачалоПериода,
	|	ДАТАВРЕМЯ(1,1,1)                                               КАК ДатаПроизводстваКонецПериода,
	|
	|	ВЫБОР СправочникНоменклатура.ЕдиницаИзмеренияСрокаГодности
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ДДММГГГГЧЧ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Сутки)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ДДММГГГГ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Месяц)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ММГГГГ)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТочностьЗаполненияПериодаВЕТИС.ДДММГГГГ)
	|	КОНЕЦ                                                          КАК СрокГодностиТочностьЗаполнения,
	|	ISNULL(ВЫБОР СправочникНоменклатура.ЕдиницаИзмеренияСрокаГодности
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
	|			ТОГДА НАЧАЛОПЕРИОДА(ТоварыКОформлению.Серия.ГоденДо, ЧАС)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Сутки)
	|			ТОГДА НАЧАЛОПЕРИОДА(ТоварыКОформлению.Серия.ГоденДо, ДЕНЬ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Месяц)
	|			ТОГДА НАЧАЛОПЕРИОДА(ТоварыКОформлению.Серия.ГоденДо, МЕСЯЦ)
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ТоварыКОформлению.Серия.ГоденДо, ДЕНЬ)
	|	КОНЕЦ, ДАТАВРЕМЯ(1,1,1))                                       КАК СрокГодностиНачалоПериода,
	|	ДАТАВРЕМЯ(1,1,1)                                               КАК СрокГодностиКонецПериода,
	|
	|	СправочникНоменклатура.ЕдиницаИзмеренияСрокаГодности = ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час) КАК СкоропортящаясяПродукция,
	|	
	|	ВЫБОР
	|		КОГДА ИСТИНА В
	|				(ВЫБРАТЬ ПЕРВЫЕ 1
	|					ИСТИНА
	|				ИЗ
	|					РегистрСведений.ТоварыДругогоКачества КАК ТоварыДругогоКачества
	|				ГДЕ
	|					ТоварыДругогоКачества.НоменклатураБрак = ТоварыКОформлению.Номенклатура
	|					И ТоварыДругогоКачества.ГрадацияКачества <> ЗНАЧЕНИЕ(Перечисление.ГрадацииКачества.Новый))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НизкокачественнаяПродукция,
	|
	|	ЗНАЧЕНИЕ(Перечисление.РезультатыЛабораторныхИсследованийВЕТИС.ИзготовленаИзСырьяПрошедшегоВСЭ) КАК ЭкспертизаРезультат
	|
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленныеПозиции КАК СопоставленныеПозиции
	|		ПО СопоставленныеПозиции.Номенклатура   = ТоварыКОформлению.Номенклатура
	|		 И СопоставленныеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|		 И СопоставленныеПозиции.Серия          = ТоварыКОформлению.Серия
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТоварыКОформлению.Номенклатура      = СправочникНоменклатура.Ссылка
	|";
	СтруктураЗапроса.Вставить("Товары", ТекстЗапроса);
	
	// Сырье
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СопоставленныеПозиции.Продукция         КАК Продукция,
	|
	|	СырьеКОформлению.Номенклатура         КАК Номенклатура,
	|	СырьеКОформлению.Характеристика       КАК Характеристика,
	|	СырьеКОформлению.Серия                КАК Серия,
	|
	|	СырьеКОформлению.Количество КАК Количество,
	|	СырьеКОформлению.Количество КАК КоличествоВЕТИС
	|
	|ИЗ
	|	СырьеКОформлению КАК СырьеКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленныеПозиции КАК СопоставленныеПозиции
	|		ПО СопоставленныеПозиции.Номенклатура   = СырьеКОформлению.Номенклатура
	|		 И СопоставленныеПозиции.Характеристика = СырьеКОформлению.Характеристика
	|		 И СопоставленныеПозиции.Серия          = СырьеКОформлению.Серия
	|";
	СтруктураЗапроса.Вставить("Сырье", ТекстЗапроса);
	
	// Технологический процесс
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&ДатаОперацииНачалоПериода КАК ДатаОперацииНачалоПериода,
	|	&ДатаОперацииКонецПериода  КАК ДатаОперацииКонецПериода,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхОперацийВЕТИС.Производство) КАК ТипОперации
	|";
	СтруктураЗапроса.Вставить("ТехнологическийПроцесс", ТекстЗапроса);
	
	// Штрихкоды
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	"""" КАК Ссылка,
	|	Штрихкоды.ШтрихкодУпаковки КАК Штрихкод
	|ИЗ
	|	Штрихкоды КАК Штрихкоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОформленныеШтрихкоды КАК ОформленныеШтрихкоды
	|		ПО ОформленныеШтрихкоды.ШтрихкодУпаковки = Штрихкоды.ШтрихкодУпаковки
	|ГДЕ
	|	ОформленныеШтрихкоды.ШтрихкодУпаковки ЕСТЬ NULL
	|";
	СтруктураЗапроса.Вставить("Штрихкоды", ТекстЗапроса);
	
	Возврат СтруктураЗапроса;
	
КонецФункции

Функция СтруктураЗапросаДанныеПрикладногоДокумента(ДанныеЗаполнения)
	
	СтруктураЗапроса = Неопределено;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.СборкаТоваров")Тогда
		
		СтруктураЗапроса = СтруктураЗапросаДанныеДокументаСборкаТоваров();
		

	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПрочееОприходованиеТоваров")Тогда
		
		СтруктураЗапроса = СтруктураЗапросаДанныеДокументаПрочееОприходованиеТоваров();
		
	КонецЕсли;
	
	Возврат СтруктураЗапроса;
	
КонецФункции

Функция СтруктураЗапросаДанныеДокументаСборкаТоваров()
	
	СтруктураЗапроса = Новый Структура();
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК ДокументОснование,
	|
	|	Таблица.Организация КАК ОрганизацияКонтрагент,
	|	Таблица.Склад КАК ТорговыйПроизводственныйОбъект,
	|
	|	НАЧАЛОПЕРИОДА(Таблица.Дата, ДЕНЬ) КАК ДатаОперацииНачалоПериода,
	|	КОНЕЦПЕРИОДА(Таблица.Дата, ДЕНЬ) КАК ДатаОперацииКонецПериода,
	|
	|	(НЕ Таблица.Проведен) КАК ЕстьОшибкиПроведен
	|
	|ИЗ
	|	Документ.СборкаТоваров КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &ДокументОснование
	|";
	СтруктураЗапроса.Вставить("Реквизиты", ТекстЗапроса);
	
	ШаблонЗапроса =
	"ВЫБРАТЬ
	|
	|	ТабличнаяЧасть.Номенклатура                                КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика                              КАК Характеристика,
	|	ТабличнаяЧасть.Серия                                       КАК Серия,
	|	ВЫБОР
	|		КОГДА ISNULL(ТабличнаяЧасть.Серия.ДатаПроизводства, ДАТАВРЕМЯ(1,1,1)) <> ДАТАВРЕМЯ(1,1,1) ТОГДА
	|			ТабличнаяЧасть.Серия.ДатаПроизводства
	|	ИНАЧЕ
	|			НАЧАЛОПЕРИОДА(ТабличнаяЧасть.Ссылка.Дата, ДЕНЬ)
	|	КОНЕЦ                                             КАК ДатаПроизводства,
	|	ТабличнаяЧасть.Количество                                  КАК Количество
	|
	|ПОМЕСТИТЬ ВременнаяТаблица
	|ИЗ
	|	Документ.СборкаТоваров КАК ТабличнаяЧасть
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		 ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СборкаТоваров.Серии КАК ТаблицаСерии
	|		 ПО ТаблицаСерии.Ссылка = ТабличнаяЧасть.Ссылка
	|			И ТаблицаСерии.Номенклатура   = ТабличнаяЧасть.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТабличнаяЧасть.Характеристика
	|
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И (СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЗерноВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗернаВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС))
	|	И ТаблицаСерии.Номенклатура ЕСТЬ NULL
	|	И &Отбор1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|
	|	ТабличнаяЧасть.Номенклатура                                КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика                              КАК Характеристика,
	|	ТабличнаяЧасть.Серия                                       КАК Серия,
	|	ВЫБОР
	|		КОГДА ISNULL(ТабличнаяЧасть.Серия.ДатаПроизводства, ДАТАВРЕМЯ(1,1,1)) <> ДАТАВРЕМЯ(1,1,1) ТОГДА
	|			ТабличнаяЧасть.Серия.ДатаПроизводства
	|	ИНАЧЕ
	|			НАЧАЛОПЕРИОДА(ТабличнаяЧасть.Ссылка.Дата, ДЕНЬ)
	|	КОНЕЦ                                                      КАК ДатаПроизводства,
	|	ТабличнаяЧасть.Количество                                  КАК Количество
	|
	|ИЗ
	|	Документ.СборкаТоваров.Серии КАК ТабличнаяЧасть
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		 ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СборкаТоваров КАК ТаблицаОграничения
	|		 ПО ТабличнаяЧасть.Ссылка = ТаблицаОграничения.Ссылка
	|			И ТабличнаяЧасть.Номенклатура = ТаблицаОграничения.Номенклатура
	|
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И (СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЗерноВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗернаВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС))
	|	И &Отбор1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|
	|	ТабличнаяЧасть.Номенклатура                                КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика                              КАК Характеристика,
	|	ТабличнаяЧасть.Серия                                       КАК Серия,
	|	ВЫБОР
	|		КОГДА ISNULL(ТабличнаяЧасть.Серия.ДатаПроизводства, ДАТАВРЕМЯ(1,1,1)) <> ДАТАВРЕМЯ(1,1,1) ТОГДА
	|			ТабличнаяЧасть.Серия.ДатаПроизводства
	|	ИНАЧЕ
	|			НАЧАЛОПЕРИОДА(ТабличнаяЧасть.Ссылка.Дата, ДЕНЬ)
	|	КОНЕЦ                                                      КАК ДатаПроизводства,
	|	ТабличнаяЧасть.Количество                                  КАК Количество
	|
	|ИЗ
	|	Документ.СборкаТоваров.Товары КАК ТабличнаяЧасть
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		 ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СборкаТоваров.Серии КАК ТаблицаСерии
	|		 ПО ТаблицаСерии.Ссылка = ТабличнаяЧасть.Ссылка
	|			И ТаблицаСерии.Номенклатура   = ТабличнаяЧасть.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТабличнаяЧасть.Характеристика
	|			И ТаблицаСерии.Назначение     = ТабличнаяЧасть.Назначение
	|
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И (СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЗерноВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗернаВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС))
	|	И ТаблицаСерии.Номенклатура ЕСТЬ NULL
	|	И &Отбор2
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|
	|	ТабличнаяЧасть.Номенклатура                                КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика                              КАК Характеристика,
	|	ТабличнаяЧасть.Серия                                       КАК Серия,
	|	ВЫБОР
	|		КОГДА ISNULL(ТабличнаяЧасть.Серия.ДатаПроизводства, ДАТАВРЕМЯ(1,1,1)) <> ДАТАВРЕМЯ(1,1,1) ТОГДА
	|			ТабличнаяЧасть.Серия.ДатаПроизводства
	|	ИНАЧЕ
	|			НАЧАЛОПЕРИОДА(ТабличнаяЧасть.Ссылка.Дата, ДЕНЬ)
	|	КОНЕЦ                                                      КАК ДатаПроизводства,
	|	ТабличнаяЧасть.Количество                                  КАК Количество
	|
	|ИЗ
	|	Документ.СборкаТоваров.Серии КАК ТабличнаяЧасть
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		 ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СборкаТоваров.Товары КАК ТаблицаОграничения
	|		 ПО ТабличнаяЧасть.Ссылка = ТаблицаОграничения.Ссылка
	|			И ТабличнаяЧасть.Номенклатура = ТаблицаОграничения.Номенклатура
	|
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И (СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЗерноВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗернаВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС))
	|	И &Отбор2
	|";
	
	ТекстЗапроса = ШаблонЗапроса;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВременнаяТаблица", "Товары");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Отбор1", "(ТабличнаяЧасть.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров))");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Отбор2", "(ТабличнаяЧасть.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазборкаТоваров))");
	СтруктураЗапроса.Вставить("Товары", ТекстЗапроса);
	
	ТекстЗапроса = ШаблонЗапроса;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВременнаяТаблица", "Сырье");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Отбор1", "(ТабличнаяЧасть.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазборкаТоваров))");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Отбор2", "(ТабличнаяЧасть.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СборкаТоваров))");
	СтруктураЗапроса.Вставить("Сырье", ТекстЗапроса);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ ПЕРВЫЕ 0
	|	ЗНАЧЕНИЕ(Справочник.ШтрихкодыУпаковокТоваров.ПустаяСсылка) КАК ШтрихкодУпаковки
	|ПОМЕСТИТЬ Штрихкоды
	|";
	СтруктураЗапроса.Вставить("Штрихкоды", ТекстЗапроса);
	
	Возврат СтруктураЗапроса;
	
КонецФункции

Функция СтруктураЗапросаДанныеДокументаПрочееОприходованиеТоваров()
	
	СтруктураЗапроса = Новый Структура();
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК ДокументОснование,
	|
	|	Таблица.Организация КАК ОрганизацияКонтрагент,
	|	Таблица.Склад КАК ТорговыйПроизводственныйОбъект,
	|
	|	НАЧАЛОПЕРИОДА(Таблица.Дата, ДЕНЬ) КАК ДатаОперацииНачалоПериода,
	|	КОНЕЦПЕРИОДА(Таблица.Дата, ДЕНЬ) КАК ДатаОперацииКонецПериода,
	|
	|	(НЕ Таблица.Проведен) КАК ЕстьОшибкиПроведен
	|
	|ИЗ
	|	Документ.ПрочееОприходованиеТоваров КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &ДокументОснование
	|";
	СтруктураЗапроса.Вставить("Реквизиты", ТекстЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.Номенклатура   КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия          КАК Серия,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Серии.Серия.ДатаПроизводства, ДАТАВРЕМЯ(1,1,1)) <> ДАТАВРЕМЯ(1,1,1)
	|			ТОГДА Серии.Серия.ДатаПроизводства
	|		КОГДА ЕСТЬNULL(Товары.Серия.ДатаПроизводства, ДАТАВРЕМЯ(1,1,1)) <> ДАТАВРЕМЯ(1,1,1)
	|			ТОГДА Товары.Серия.ДатаПроизводства
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(Товары.Ссылка.Дата, ДЕНЬ)
	|	КОНЕЦ                 КАК ДатаПроизводства,
	|	ЕСТЬNULL(Серии.Количество,Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.ПрочееОприходованиеТоваров.Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПрочееОприходованиеТоваров.Серии КАК Серии
	|			ПО Серии.Ссылка = &ДокументОснование
	|			И Серии.Номенклатура   = Товары.Номенклатура
	|			И Серии.Характеристика = Товары.Характеристика
	|			И Серии.Назначение     = Товары.Назначение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|			ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|
	|
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И (СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПодконтрольнаяПродукцияВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЗерноВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродуктыПереработкиЗернаВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС)
	|		ИЛИ СправочникНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС))
	|";
	СтруктураЗапроса.Вставить("Товары", ТекстЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 0
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|	ДАТАВРЕМЯ(1,1,1) КАК ДатаПроизводства,
	|	0 КАК Количество
	|ПОМЕСТИТЬ Серии
	|";
	СтруктураЗапроса.Вставить("Сырье", ТекстЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.ШтрихкодУпаковки КАК ШтрихкодУпаковки
	|ПОМЕСТИТЬ Штрихкоды
	|ИЗ
	|	Документ.ПрочееОприходованиеТоваров.ШтрихкодыУпаковок КАК ТабличнаяЧасть
	|
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|";
	СтруктураЗапроса.Вставить("Штрихкоды", ТекстЗапроса);
	
	Возврат СтруктураЗапроса;
	
КонецФункции



Функция ДанныеПартииПроизводстваПоДокументуОснования(ДокументОснования) Экспорт

	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторПроизводственнойТранзакции", ОбщегоНазначенияИС.ПустоеЗначениеОпределяемогоТипа("ИдентификаторПроизводственнойТранзакцииВЕТИС"));
	Результат.Вставить("ЗавершениеПроизводственнойТранзакции", Ложь);

	Возврат Результат;

КонецФункции

Процедура ПроверитьЗаполнениеИдентификатораПартии(ДокументОбъект, Отказ, МассивНепроверяемыхРеквизитов) Экспорт
	
	ШаблонСообщения = НСтр("ru = 'Не заполнено поле ""Идентификатор партии"" в строке %1 списка ""Продукция"".'");
	
	Если ЗначениеЗаполнено(ДокументОбъект.ИдентификаторПроизводственнойТранзакции) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапросаИспользованиеИдентификаторовПартийТоваров();
	Запрос.УстановитьПараметр("ТабличнаяЧастьТовары", ДокументОбъект.Товары.Выгрузить(, "НомерСтроки,Номенклатура,СтатусУказанияСерий"));
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрокаТЧ = ДокументОбъект.Товары.Найти(Выборка.НомерСтроки, "НомерСтроки");
		Если СтрокаТЧ <> Неопределено
			И Выборка.ИспользоватьИдентификаторПартии
			И НЕ ЗначениеЗаполнено(СтрокаТЧ.ИдентификаторПартии) Тогда
			
			ТекстСообщения = СтрШаблон(ШаблонСообщения, Выборка.НомерСтроки);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары",
				Выборка.НомерСтроки,
				"ИдентификаторПартии");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
				ДокументОбъект.Ссылка,
				Поле,
				"Объект",
				Отказ);
			
		КонецЕсли;
	КонецЦикла;
	
	МассивНепроверяемыхРеквизитов.Добавить("Товары.ИдентификаторПартии");
	
КонецПроцедуры

#КонецОбласти

#Область ПрикладныеДокументы

#Область ПриобретениеТоваровУслуг

Процедура ЗаполнитьПриобретениеТоваровУслугНаОснованииВходящейТранспортнойОперацииВЕТИС(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	ПакетРезультатовЗапроса = ДанныеПрикладныхДокументовИзВходящейТранспортнойОперацииВЕТИС(ДанныеЗаполнения);
	Реквизиты = ПакетРезультатовЗапроса[0].Выбрать();
	Реквизиты.Следующий();
	
	Если Не ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты);
		
	КонецЕсли;
	
	СтруктураЗаполненияТЧ = СтруктураЗаполненияТабличныхЧастейПоВходящейТранспортнойОперации(ПакетРезультатовЗапроса, Истина, Истина);
	ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииВходящейТранспортнойОперации(ДокументОбъект,СтруктураЗаполненияТЧ);
	ДокументОбъект.ЗаполнитьУсловияЗакупокПоУмолчанию();
	
КонецПроцедуры

#КонецОбласти

#Область ВозвратТоваровОтКлиента

Процедура ЗаполнитьВозвратТоваровОтКлиентаНаОснованииВходящейТранспортнойОперацииВЕТИС(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	ПакетРезультатовЗапроса = ДанныеПрикладныхДокументовИзВходящейТранспортнойОперацииВЕТИС(ДанныеЗаполнения);
	Реквизиты = ПакетРезультатовЗапроса[0].Выбрать();
	Реквизиты.Следующий();
	
	Если Не ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты);
		
	КонецЕсли;
	
	СтруктураЗаполненияТЧ = СтруктураЗаполненияТабличныхЧастейПоВходящейТранспортнойОперации(ПакетРезультатовЗапроса, Истина);
	ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииВходящейТранспортнойОперации(ДокументОбъект,СтруктураЗаполненияТЧ);
	ДокументОбъект.ЗаполнитьУсловияПродажПоУмолчанию();
	
КонецПроцедуры

#КонецОбласти

#Область РеализацияТоваровУслуг

Процедура ЗаполнитьРеализациюТоваровУслугНаОснованииИсходящейТранспортнойОперацииВЕТИС(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	ПакетРезультатовЗапроса = ДанныеПрикладныхДокументовИзИсходящейТранспортнойОперацииВЕТИС(ДанныеЗаполнения);
	Реквизиты = ПакетРезультатовЗапроса[0].Выбрать();
	Реквизиты.Следующий();
	
	Если Не ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты);
		
	КонецЕсли;
	
	СтруктураЗаполненияТЧ = СтруктураЗаполненияТабличныхЧастейПоИсходящейТранспортнойОперации(ПакетРезультатовЗапроса,Истина,Истина);
	ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииИсходящейТранспортнойОперации(ДокументОбъект,СтруктураЗаполненияТЧ);
	ДокументОбъект.ЗаполнитьУсловияПродажПоУмолчанию();
	
КонецПроцедуры

#КонецОбласти

#Область ВозвратТоваровПоставщику

Процедура ЗаполнитьВозвратТоваровПоставщикуНаОснованииИсходящейТранспортнойОперацииВЕТИС(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	ПакетРезультатовЗапроса = ДанныеПрикладныхДокументовИзИсходящейТранспортнойОперацииВЕТИС(ДанныеЗаполнения);
	Реквизиты = ПакетРезультатовЗапроса[0].Выбрать();
	Реквизиты.Следующий();
	
	Если Не ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты);
		
	КонецЕсли;
	
	СтруктураЗаполненияТЧ = СтруктураЗаполненияТабличныхЧастейПоИсходящейТранспортнойОперации(ПакетРезультатовЗапроса,Истина);
	ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииИсходящейТранспортнойОперации(ДокументОбъект,СтруктураЗаполненияТЧ);
	ДокументОбъект.ЗаполнитьУсловияЗакупокПоУмолчанию();
	
КонецПроцедуры

#КонецОбласти

#Область ПеремещениеТоваров

Процедура ЗаполнитьПеремещениеТоваровНаОснованииИсходящейТранспортнойОперацииВЕТИС(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	ПакетРезультатовЗапроса = ДанныеПрикладныхДокументовИзИсходящейТранспортнойОперацииВЕТИС(ДанныеЗаполнения);
	Реквизиты = ПакетРезультатовЗапроса[0].Выбрать();
	Реквизиты.Следующий();
	
	Если Не ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты);
		
		ДокументОбъект.СкладОтправитель      = Реквизиты.ГрузоотправительПредприятие;
		ДокументОбъект.ОрганизацияПолучатель = Реквизиты.Грузополучатель;
		ДокументОбъект.СкладПолучатель       = Реквизиты.ГрузополучательПредприятие;
		
	КонецЕсли;
	
	СтруктураЗаполненияТЧ = СтруктураЗаполненияТабличныхЧастейПоИсходящейТранспортнойОперации(ПакетРезультатовЗапроса,Истина);
	ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииИсходящейТранспортнойОперации(ДокументОбъект,СтруктураЗаполненияТЧ);
	
КонецПроцедуры

#КонецОбласти

#Область ПередачаТоваровМеждуОрганизациями

Процедура ЗаполнитьПередачуТоваровМеждуОрганизациямиНаОснованииИсходящейТранспортнойОперацииВЕТИС(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	ПакетРезультатовЗапроса = ДанныеПрикладныхДокументовИзИсходящейТранспортнойОперацииВЕТИС(ДанныеЗаполнения);
	Реквизиты = ПакетРезультатовЗапроса[0].Выбрать();
	Реквизиты.Следующий();
	
	Если Не ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты);
		
		ДокументОбъект.ОрганизацияПолучатель = Реквизиты.Грузополучатель;
		
	КонецЕсли;
	
	СтруктураЗаполненияТЧ = СтруктураЗаполненияТабличныхЧастейПоИсходящейТранспортнойОперации(ПакетРезультатовЗапроса);
	ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииИсходящейТранспортнойОперации(ДокументОбъект,СтруктураЗаполненияТЧ);
	
КонецПроцедуры

#КонецОбласти

#Область ВозвратТоваровМеждуОрганизациями

Процедура ЗаполнитьВозвратТоваровМеждуОрганизациямиНаОснованииИсходящейТранспортнойОперацииВЕТИС(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	ПакетРезультатовЗапроса = ДанныеПрикладныхДокументовИзИсходящейТранспортнойОперацииВЕТИС(ДанныеЗаполнения);
	Реквизиты = ПакетРезультатовЗапроса[0].Выбрать();
	Реквизиты.Следующий();
	
	Если Не ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты);
		
		ДокументОбъект.ОрганизацияПолучатель = Реквизиты.Грузополучатель;
		
	КонецЕсли;
	
	СтруктураЗаполненияТЧ = СтруктураЗаполненияТабличныхЧастейПоИсходящейТранспортнойОперации(ПакетРезультатовЗапроса);
	ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииИсходящейТранспортнойОперации(ДокументОбъект,СтруктураЗаполненияТЧ);
	
КонецПроцедуры

#КонецОбласти




#Область СборкаТоваров

Процедура ЗаполнитьСборкуТоваровНаОснованииПроизводственнойОперацииВЕТИС(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	ПакетРезультатовЗапроса = ДанныеПрикладныхДокументовИзПроизводственнойОперацииВЕТИС(ДанныеЗаполнения);
	Реквизиты = ПакетРезультатовЗапроса[0].Выбрать();
	Реквизиты.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ИнтеграцияИСУТ.ПроверитьВозможностьВводаНаОсновании(Реквизиты);
		
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
		
		ДокументОбъект.Статус = Перечисления.СтатусыСборокТоваров.СобраноРазобрано;
		ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СборкаТоваров;
		
		ДокументОбъект.Организация = Реквизиты.Организация;
		ДокументОбъект.Склад = Реквизиты.ТорговыйОбъект;
		
	КонецЕсли;
	
	ТаблицаШапка = ПакетРезультатовЗапроса[1].Выгрузить();
	
	Комплект = ТаблицаШапка.Скопировать();
	Комплект.Свернуть("Номенклатура, Характеристика");
	
	Если Комплект.Количество() > 1 Тогда
		
		ТекстОшибки = НСтр("ru = 'Документ %Документ% содержит несколько строк в табличной части ""Продукция"". Ввод на основании документа ""Сборка (разборка) товаров"" невозможен.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ДокументОбъект, Документы.СборкаТоваров);
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.Серии.Очистить();
	
	Если ТаблицаШапка.Количество() > 0 Тогда
		
		ТаблицаШапка.Свернуть("Номенклатура, Характеристика, Серия, СтавкаНДС", "Количество, КоличествоУпаковок");
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ТаблицаШапка[0]);
		
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ДокументОбъект, ПараметрыУказанияСерий.Шапка);
		
		Если НоменклатураКлиентСервер.ВЭтомСтатусеСерииУказываютсяВТЧТовары(ДокументОбъект.СтатусУказанияСерий, ПараметрыУказанияСерий.Шапка)
			И ТаблицаШапка.Количество() > 1 Тогда
			
			ТекстОшибки = НСтр("ru = 'Документ %Документ% содержит несколько серий в табличной части ""Продукция"". Ввод на основании документа ""Сборка (разборка) товаров"" невозможен.'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
			
			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		
		Для каждого Строка Из ТаблицаШапка Цикл
			Если ЗначениеЗаполнено(Строка.Серия) И Строка.Количество > 0 Тогда
				НоваяСерия = ДокументОбъект.Серии.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСерия, Строка);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	ВыборкаТЧ = ПакетРезультатовЗапроса[2].Выбрать();
	Пока ВыборкаТЧ.Следующий() Цикл
		
		НоваяСтрока = ДокументОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,ВыборкаТЧ);
		
		Если ЗначениеЗаполнено(НоваяСтрока.Серия) И НоваяСтрока.Количество > 0 Тогда
			НоваяСтрокаСерии = ДокументОбъект.Серии.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаСерии, НоваяСтрока);
		КонецЕсли;
		
	КонецЦикла;
	
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ДокументОбъект, ПараметрыУказанияСерий.ТЧ);
	НоменклатураСервер.ОчиститьНеиспользуемыеСерии(ДокументОбъект, ПараметрыУказанияСерий);
	
КонецПроцедуры

#КонецОбласти


#Область ВнутреннееПотребление

Процедура ЗаполнитьВнутреннееПотреблениеНаОснованииИнвентаризацииПродукцииВЕТИС (ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	ПакетРезультатовЗапроса = ДанныеПрикладныхДокументовИзИнвентаризацииПродукцииВЕТИС(ДанныеЗаполнения);
	Реквизиты = ПакетРезультатовЗапроса[0].Выбрать();
	Реквизиты.Следующий();
	
	Если Не ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты);
		
	КонецЕсли;
	
	СтруктураЗаполненияТЧ = СтруктураЗаполненияТабличныхЧастейПоИнвентаризацииПродукции(ПакетРезультатовЗапроса, Истина);
	ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииИнвентаризацииПродукции(ДокументОбъект,СтруктураЗаполненияТЧ);
	
КонецПроцедуры

#КонецОбласти

#Область СписаниеНедостачТоваров

Процедура ЗаполнитьСписаниеНедостачТоваровНаОснованииИнвентаризацииПродукцииВЕТИС(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	ПакетРезультатовЗапроса = ДанныеПрикладныхДокументовИзИнвентаризацииПродукцииВЕТИС(ДанныеЗаполнения);
	Реквизиты = ПакетРезультатовЗапроса[0].Выбрать();
	Реквизиты.Следующий();
	
	Если Не ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты);
		
	КонецЕсли;
	
	СтруктураЗаполненияТЧ = СтруктураЗаполненияТабличныхЧастейПоИнвентаризацииПродукции(ПакетРезультатовЗапроса);
	ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииИнвентаризацииПродукции(ДокументОбъект,СтруктураЗаполненияТЧ);
	
КонецПроцедуры

#КонецОбласти

#Область ОприходованиеИзлишковТоваров

Процедура ЗаполнитьОприходованиеИзлишковТоваровНаОснованииИнвентаризацииПродукцииВЕТИС (ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	ПакетРезультатовЗапроса = ДанныеПрикладныхДокументовИзИнвентаризацииПродукцииВЕТИС(ДанныеЗаполнения);
	Реквизиты = ПакетРезультатовЗапроса[0].Выбрать();
	Реквизиты.Следующий();
	
	Если Не ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты);
		
	КонецЕсли;
	
	СтруктураЗаполненияТЧ = СтруктураЗаполненияТабличныхЧастейПоИнвентаризацииПродукции(ПакетРезультатовЗапроса,,1);
	ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииИнвентаризацииПродукции(ДокументОбъект,СтруктураЗаполненияТЧ);
	
КонецПроцедуры

#КонецОбласти

#Область ПрочееОприходованиеТоваров

Процедура ЗаполнитьПрочееОприходованиеТоваровНаОснованииИнвентаризацииПродукцииВЕТИС(ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	ПакетРезультатовЗапроса = ДанныеПрикладныхДокументовИзИнвентаризацииПродукцииВЕТИС(ДанныеЗаполнения);
	Реквизиты = ПакетРезультатовЗапроса[0].Выбрать();
	Реквизиты.Следующий();
	
	Если Не ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Реквизиты);
		
	КонецЕсли;
	
	СтруктураЗаполненияТЧ = СтруктураЗаполненияТабличныхЧастейПоИнвентаризацииПродукции(ПакетРезультатовЗапроса, Истина, 1);
	ЗаполнитьТабличныеЧастиПрикладногоДокументаНаОснованииИнвентаризацииПродукции(ДокументОбъект,СтруктураЗаполненияТЧ);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

#КонецОбласти

#Область Прочее

#Область СоответствиеНоменклатуры

Функция ТекстЗапросаСоответствиеНоменклатурыВЕТИС(ИмяВременнойТаблицы = "СопоставленныеПозиции", ИменаТЧТовары = "ТоварыКОформлению")
	
	Если ТипЗнч(ИменаТЧТовары) = Тип("Структура") Тогда
		МассивТЧТовары = Новый Массив;
		Для Каждого КлючИЗначение Из ИменаТЧТовары Цикл 
			МассивТЧТовары.Добавить(КлючИЗначение.Ключ);
		КонецЦикла;
	Иначе
		МассивТЧТовары = СтрРазделить(ИменаТЧТовары, ",", Ложь);
	КонецЕсли;
	ТекстЗапроса = "";
	
	Если МассивТЧТовары.Количество() > 1 Или ТипЗнч(ИменаТЧТовары) = Тип("Структура") Тогда
		
		ИмяВременнойТаблицыДляСопоставленияНоменклатуры = "ВтОбъединениеТЧДляСопоставления";
		
		ШаблонЗапросаТовары =
		"ВЫБРАТЬ
		|	ТаблицаТовары.Номенклатура   КАК Номенклатура,
		|	ТаблицаТовары.Характеристика КАК Характеристика,
		|	ТаблицаТовары.Серия          КАК Серия,
		|	%3                           КАК ФильтрВладельца
		|%1
		|ИЗ
		|	%2 КАК ТаблицаТовары
		|";
		ШаблонОбъединениеЗапросов = "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|";
		
		Для НомерТаблицы = 0 По МассивТЧТовары.Количество() - 1 Цикл
			
			ТекущаяТЧТовары = МассивТЧТовары[НомерТаблицы];
			
			Если НомерТаблицы > 0 Тогда
				ТекстЗапроса = ТекстЗапроса + ШаблонОбъединениеЗапросов;
			КонецЕсли;
			
			ФильтрВладельца = "ЛОЖЬ";
			Если ТипЗнч(ИменаТЧТовары) = Тип("Структура") И ИменаТЧТовары[ТекущаяТЧТовары] = Истина Тогда
				ФильтрВладельца = "ИСТИНА";
			КонецЕсли;
			
			ТекстЗапроса = ТекстЗапроса
				+ СтрШаблон(
					ШаблонЗапросаТовары,
					?(НомерТаблицы = 0,
						"ПОМЕСТИТЬ " + ИмяВременнойТаблицыДляСопоставленияНоменклатуры,
						""),
					ТекущаяТЧТовары,
					ФильтрВладельца);
			
		КонецЦикла;
		
		ТекстЗапроса = ТекстЗапроса + "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|";
		
	Иначе
		ИмяВременнойТаблицыДляСопоставленияНоменклатуры = МассивТЧТовары[0];
	КонецЕсли;
	
	Если ТипЗнч(ИменаТЧТовары) = Тип("Структура") Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
		|	ТабличнаяЧасть.Характеристика КАК Характеристика,
		|	ТабличнаяЧасть.Серия          КАК Серия,
		|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыВЕТИС.Продукция, СоответствиеНоменклатурыВЕТИС2.Продукция)) КАК Продукция
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	%2 КАК ТабличнаяЧасть
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыВЕТИС КАК СоответствиеНоменклатурыВЕТИС
		|		ПО СоответствиеНоменклатурыВЕТИС.Номенклатура = ТабличнаяЧасть.Номенклатура
		|			И СоответствиеНоменклатурыВЕТИС.Характеристика = ТабличнаяЧасть.Характеристика
		|			И (СоответствиеНоменклатурыВЕТИС.Серия = ТабличнаяЧасть.Серия)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыВЕТИС КАК СоответствиеНоменклатурыВЕТИС2
		|		ПО СоответствиеНоменклатурыВЕТИС2.Номенклатура = ТабличнаяЧасть.Номенклатура
		|			И СоответствиеНоменклатурыВЕТИС2.Характеристика = ТабличнаяЧасть.Характеристика
		|			И (СоответствиеНоменклатурыВЕТИС2.Серия = &ПустаяСерия)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПродукцияВЕТИС.Производители КАК Производители
		|			ПО Производители.Производитель = &Предприятие
		|			И Производители.Ссылка.ХозяйствующийСубъектПроизводитель = &ХозяйствующийСубъект
		|			И Производители.Ссылка = ЕСТЬNULL(СоответствиеНоменклатурыВЕТИС.Продукция, СоответствиеНоменклатурыВЕТИС2.Продукция)
		|ГДЕ
		|	ТабличнаяЧасть.ФильтрВладельца = ЛОЖЬ
		|		ИЛИ НЕ Производители.Ссылка ЕСТЬ NULL 
		|СГРУППИРОВАТЬ ПО
		|	ТабличнаяЧасть.Номенклатура,
		|	ТабличнаяЧасть.Характеристика,
		|	ТабличнаяЧасть.Серия
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыВЕТИС.Продукция,СоответствиеНоменклатурыВЕТИС2.Продукция)) = 1
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	Серия
		|";
	Иначе
		ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
		|	ТабличнаяЧасть.Характеристика КАК Характеристика,
		|	ТабличнаяЧасть.Серия          КАК Серия,
		|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыВЕТИС.Продукция, СоответствиеНоменклатурыВЕТИС2.Продукция)) КАК Продукция
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	%2 КАК ТабличнаяЧасть
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыВЕТИС КАК СоответствиеНоменклатурыВЕТИС
		|		ПО СоответствиеНоменклатурыВЕТИС.Номенклатура = ТабличнаяЧасть.Номенклатура
		|			И СоответствиеНоменклатурыВЕТИС.Характеристика = ТабличнаяЧасть.Характеристика
		|			И (СоответствиеНоменклатурыВЕТИС.Серия = ТабличнаяЧасть.Серия)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыВЕТИС КАК СоответствиеНоменклатурыВЕТИС2
		|		ПО СоответствиеНоменклатурыВЕТИС2.Номенклатура = ТабличнаяЧасть.Номенклатура
		|			И СоответствиеНоменклатурыВЕТИС2.Характеристика = ТабличнаяЧасть.Характеристика
		|			И (СоответствиеНоменклатурыВЕТИС2.Серия = &ПустаяСерия)
		|СГРУППИРОВАТЬ ПО
		|	ТабличнаяЧасть.Номенклатура,
		|	ТабличнаяЧасть.Характеристика,
		|	ТабличнаяЧасть.Серия
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыВЕТИС.Продукция,СоответствиеНоменклатурыВЕТИС2.Продукция)) = 1
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	Серия
		|";
	КонецЕсли;
	ТекстЗапроса = СтрШаблон(ТекстЗапроса, ИмяВременнойТаблицы, ИмяВременнойТаблицыДляСопоставленияНоменклатуры);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область СопоставлениеСправочниковПриСвязиДокументов

// В процедуре необходимо реализовать запись сопоставления хозяйствующих субъектов и предприятий с прикладными
//   справочниками конфигурации.
//
// Параметры:
//  ДокументОснование - ДокументСсылка, ДокументОбъект - прикладной документ конфигурации,
//  ДокументОбъект    - ДокументСсылка, ДокументОбъект - связанный с ним документ библиотеки.
//
Процедура ЗаполнитьСоответствиеШапкиОбъектов(ДокументОснование, ДокументОбъект) Экспорт
	
	ТипДокументаВЕТИС = ТипЗнч(ДокументОбъект);
	Если ТипДокументаВЕТИС = Тип("ДокументСсылка.ВходящаяТранспортнаяОперацияВЕТИС")
		Или ТипДокументаВЕТИС = Тип("ДокументОбъект.ВходящаяТранспортнаяОперацияВЕТИС") Тогда
			ЗаполнитьСоответствиеШапкиДляВходящейТранспортнойОперацииВЕТИС(ДокументОснование, ДокументОбъект);
	ИначеЕсли ТипДокументаВЕТИС = Тип("ДокументСсылка.ИсходящаяТранспортнаяОперацияВЕТИС")
		Или ТипДокументаВЕТИС = Тип("ДокументОбъект.ИсходящаяТранспортнаяОперацияВЕТИС") Тогда
			ЗаполнитьСоответствиеШапкиДляИсходящейТранспортнойОперацииВЕТИС(ДокументОснование, ДокументОбъект);
	ИначеЕсли ТипДокументаВЕТИС = Тип("ДокументСсылка.ПроизводственнаяОперацияВЕТИС")
		Или ТипДокументаВЕТИС = Тип("ДокументОбъект.ПроизводственнаяОперацияВЕТИС") Тогда
			ЗаполнитьСоответствиеШапкиДляПроизводственнойОперацииВЕТИС(ДокументОснование, ДокументОбъект);
	ИначеЕсли ТипДокументаВЕТИС = Тип("ДокументСсылка.ИнвентаризацияПродукцииВЕТИС")
		Или ТипДокументаВЕТИС = Тип("ДокументОбъект.ИнвентаризацияПродукцииВЕТИС") Тогда
			ЗаполнитьСоответствиеШапкиДляИнвентаризацииПродукцииВЕТИС(ДокументОснование, ДокументОбъект);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСоответствиеШапкиДляВходящейТранспортнойОперацииВЕТИС(ДокументОснование, ДокументОбъект)
	
	ЗаписьДокументаВЕТИС = ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ВходящаяТранспортнаяОперацияВЕТИС");
	РеквизитыВЕТИС = Реквизиты(ДокументОбъект,
		"ГрузоотправительПредприятие,ГрузоотправительХозяйствующийСубъект,ГрузополучательПредприятие,ГрузополучательХозяйствующийСубъект",
		ЗаписьДокументаВЕТИС);
	ТипОснования = ТипЗнч(ДокументОснование);
	
	Если ТипОснования = Тип("ДокументСсылка.ПеремещениеТоваров")
		Или ТипОснования = Тип("ДокументОбъект.ПеремещениеТоваров") Тогда
			РеквизитыШапки = Реквизиты(ДокументОснование,
				"СкладОтправитель,Организация,СкладПолучатель,ОрганизацияПолучатель",
				Не ЗаписьДокументаВЕТИС);
			Справочники.ХозяйствующиеСубъектыВЕТИС.СопоставитьСПрикладнымиРеквизитами(
				РеквизитыВЕТИС.ГрузоотправительХозяйствующийСубъект,
				РеквизитыВЕТИС.ГрузоотправительПредприятие,
				РеквизитыШапки.Организация,
				РеквизитыШапки.СкладОтправитель,
				Истина);
			Справочники.ХозяйствующиеСубъектыВЕТИС.СопоставитьСПрикладнымиРеквизитами(
				РеквизитыВЕТИС.ГрузополучательХозяйствующийСубъект,
				РеквизитыВЕТИС.ГрузополучательПредприятие,
				РеквизитыШапки.ОрганизацияПолучатель,
				РеквизитыШапки.СкладПолучатель,
				Истина);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
		Или ТипОснования = Тип("ДокументОбъект.ПриобретениеТоваровУслуг")
		Или ТипОснования = Тип("ДокументСсылка.ВыкупПринятыхНаХранениеТоваров")
		Или ТипОснования = Тип("ДокументОбъект.ВыкупПринятыхНаХранениеТоваров")
		Или ТипОснования = Тип("ДокументСсылка.ВозвратТоваровОтКлиента")
		Или ТипОснования = Тип("ДокументОбъект.ВозвратТоваровОтКлиента") Тогда
				РеквизитыШапки = Реквизиты(ДокументОснование,
				"Контрагент,Партнер,Склад,Организация",
				Не ЗаписьДокументаВЕТИС);
			Справочники.ХозяйствующиеСубъектыВЕТИС.СопоставитьСПрикладнымиРеквизитами(
				РеквизитыВЕТИС.ГрузоотправительХозяйствующийСубъект,
				РеквизитыВЕТИС.ГрузоотправительПредприятие,
				РеквизитыШапки.Контрагент,
				РеквизитыШапки.Партнер,
				Ложь);
			Справочники.ХозяйствующиеСубъектыВЕТИС.СопоставитьСПрикладнымиРеквизитами(
				РеквизитыВЕТИС.ГрузополучательХозяйствующийСубъект,
				РеквизитыВЕТИС.ГрузополучательПредприятие,
				РеквизитыШапки.Организация,
				РеквизитыШапки.Склад,
				Истина);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")
		Или ТипОснования = Тип("ДокументОбъект.ПередачаТоваровМеждуОрганизациями")
		Или ТипОснования = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями")
		Или ТипОснования = Тип("ДокументОбъект.ВозвратТоваровМеждуОрганизациями") Тогда
				РеквизитыШапки = Реквизиты(ДокументОснование,
				"Организация,ОрганизацияПолучатель,Склад",
				Не ЗаписьДокументаВЕТИС);
			Справочники.ХозяйствующиеСубъектыВЕТИС.СопоставитьСПрикладнымиРеквизитами(
				РеквизитыВЕТИС.ГрузоотправительХозяйствующийСубъект,
				РеквизитыВЕТИС.ГрузоотправительПредприятие,
				РеквизитыШапки.Организация,
				РеквизитыШапки.Склад,
				Истина);
			Справочники.ХозяйствующиеСубъектыВЕТИС.СопоставитьСПрикладнымиРеквизитами(
				РеквизитыВЕТИС.ГрузополучательХозяйствующийСубъект,
				РеквизитыВЕТИС.ГрузополучательПредприятие,
				РеквизитыШапки.ОрганизацияПолучатель,
				РеквизитыШапки.Склад,
				Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСоответствиеШапкиДляИсходящейТранспортнойОперацииВЕТИС(ДокументОснование, ДокументОбъект)
	
	ЗаписьДокументаВЕТИС = ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ИсходящаяТранспортнаяОперацияВЕТИС");
	РеквизитыВЕТИС = Реквизиты(ДокументОбъект,
		"ГрузоотправительПредприятие,ГрузоотправительХозяйствующийСубъект,ГрузополучательПредприятие,ГрузополучательХозяйствующийСубъект",
		ЗаписьДокументаВЕТИС);
	ТипОснования = ТипЗнч(ДокументОснование);
	Если ТипОснования = Тип("ДокументСсылка.ПеремещениеТоваров")
		Или ТипОснования = Тип("ДокументОбъект.ПеремещениеТоваров") Тогда
			РеквизитыШапки = Реквизиты(ДокументОснование,
				"СкладОтправитель,Организация,СкладПолучатель,ОрганизацияПолучатель",
				Не ЗаписьДокументаВЕТИС);
			Справочники.ХозяйствующиеСубъектыВЕТИС.СопоставитьСПрикладнымиРеквизитами(
				РеквизитыВЕТИС.ГрузоотправительХозяйствующийСубъект,
				РеквизитыВЕТИС.ГрузоотправительПредприятие,
				РеквизитыШапки.Организация,
				РеквизитыШапки.СкладОтправитель,
				Истина);
			Справочники.ХозяйствующиеСубъектыВЕТИС.СопоставитьСПрикладнымиРеквизитами(
				РеквизитыВЕТИС.ГрузополучательХозяйствующийСубъект,
				РеквизитыВЕТИС.ГрузополучательПредприятие,
				РеквизитыШапки.ОрганизацияПолучатель,
				РеквизитыШапки.СкладПолучатель,
				Истина);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ВозвратТоваровПоставщику")
		Или ТипОснования = Тип("ДокументОбъект.ВозвратТоваровПоставщику")
		Или ТипОснования = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		Или ТипОснования = Тип("ДокументОбъект.РеализацияТоваровУслуг") Тогда
				РеквизитыШапки = Реквизиты(ДокументОснование,
				"Контрагент,Партнер,Склад,Организация",
				Не ЗаписьДокументаВЕТИС);
			Справочники.ХозяйствующиеСубъектыВЕТИС.СопоставитьСПрикладнымиРеквизитами(
				РеквизитыВЕТИС.ГрузополучательХозяйствующийСубъект,
				РеквизитыВЕТИС.ГрузополучательПредприятие,
				РеквизитыШапки.Контрагент,
				РеквизитыШапки.Партнер,
				Ложь);
			Справочники.ХозяйствующиеСубъектыВЕТИС.СопоставитьСПрикладнымиРеквизитами(
				РеквизитыВЕТИС.ГрузоотправительХозяйствующийСубъект,
				РеквизитыВЕТИС.ГрузоотправительПредприятие,
				РеквизитыШапки.Организация,
				РеквизитыШапки.Склад,
				Истина);
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")
		Или ТипОснования = Тип("ДокументОбъект.ПередачаТоваровМеждуОрганизациями")
		Или ТипОснования = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями")
		Или ТипОснования = Тип("ДокументОбъект.ВозвратТоваровМеждуОрганизациями") Тогда
				РеквизитыШапки = Реквизиты(ДокументОснование,
				"Организация,ОрганизацияПолучатель,Склад",
				Не ЗаписьДокументаВЕТИС);
			Справочники.ХозяйствующиеСубъектыВЕТИС.СопоставитьСПрикладнымиРеквизитами(
				РеквизитыВЕТИС.ГрузоотправительХозяйствующийСубъект,
				РеквизитыВЕТИС.ГрузоотправительПредприятие,
				РеквизитыШапки.Организация,
				РеквизитыШапки.Склад,
				Истина);
			Справочники.ХозяйствующиеСубъектыВЕТИС.СопоставитьСПрикладнымиРеквизитами(
				РеквизитыВЕТИС.ГрузополучательХозяйствующийСубъект,
				РеквизитыВЕТИС.ГрузополучательПредприятие,
				РеквизитыШапки.ОрганизацияПолучатель,
				РеквизитыШапки.Склад,
				Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСоответствиеШапкиДляПроизводственнойОперацииВЕТИС(ДокументОснование, ДокументОбъект)
	
	ЗаписьДокументаВЕТИС = ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПроизводственнаяОперацияВЕТИС");
	РеквизитыВЕТИС = Реквизиты(ДокументОбъект,
		"ХозяйствующийСубъект,Предприятие",
		ЗаписьДокументаВЕТИС);
	ТипОснования = ТипЗнч(ДокументОснование);
	Если ТипОснования = Тип("ДокументСсылка.СборкаТоваров")
		Или ТипОснования = Тип("ДокументОбъект.СборкаТоваров")

		Тогда
			РеквизитыШапки = Реквизиты(ДокументОснование,
				"Организация,Подразделение",
				Не ЗаписьДокументаВЕТИС);
			Справочники.ХозяйствующиеСубъектыВЕТИС.СопоставитьСПрикладнымиРеквизитами(
				РеквизитыВЕТИС.ХозяйствующийСубъект,
				РеквизитыВЕТИС.Предприятие,
				РеквизитыШапки.Организация,
				РеквизитыШапки.Подразделение,
				Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСоответствиеШапкиДляИнвентаризацииПродукцииВЕТИС(ДокументОснование, ДокументОбъект)
	
	ЗаписьДокументаВЕТИС = ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ИнвентаризацияПродукцииВЕТИС");
	РеквизитыВЕТИС = Реквизиты(ДокументОбъект,
		"ХозяйствующийСубъект,Предприятие",
		ЗаписьДокументаВЕТИС);
	ТипОснования = ТипЗнч(ДокументОснование);
	Если ТипОснования = Тип("ДокументСсылка.ВнутреннееПотребление")
		Или ТипОснования = Тип("ДокументОбъект.ВнутреннееПотребление")
		Или ТипОснования = Тип("ДокументСсылка.СписаниеНедостачТоваров")
		Или ТипОснования = Тип("ДокументОбъект.СписаниеНедостачТоваров")
		Или ТипОснования = Тип("ДокументСсылка.ПересортицаТоваров")
		Или ТипОснования = Тип("ДокументОбъект.ПересортицаТоваров")
		Или ТипОснования = Тип("ДокументСсылка.ОприходованиеИзлишковТоваров")
		Или ТипОснования = Тип("ДокументОбъект.ОприходованиеИзлишковТоваров")
		Или ТипОснования = Тип("ДокументСсылка.ПрочееОприходованиеТоваров")
		Или ТипОснования = Тип("ДокументОбъект.ПрочееОприходованиеТоваров")
		Или ТипОснования = Тип("ДокументСсылка.ПорчаТоваров")
		Или ТипОснования = Тип("ДокументОбъект.ПорчаТоваров")
		 Тогда
			РеквизитыШапки = Реквизиты(ДокументОснование,
				"Организация,Склад",
				Не ЗаписьДокументаВЕТИС);
			Справочники.ХозяйствующиеСубъектыВЕТИС.СопоставитьСПрикладнымиРеквизитами(
				РеквизитыВЕТИС.ХозяйствующийСубъект,
				РеквизитыВЕТИС.Предприятие,
				РеквизитыШапки.Организация,
				РеквизитыШапки.Склад,
				Истина);
	КонецЕсли;
	
КонецПроцедуры

Функция Реквизиты(СсылкаИлиОбъект, ИменаРеквизитов, ЭтоОбъект)
	
	Если ЭтоОбъект Тогда
		Результат = Новый Структура(ИменаРеквизитов);
		ЗаполнитьЗначенияСвойств(Результат, СсылкаИлиОбъект);
	Иначе
		Результат = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаИлиОбъект, ИменаРеквизитов);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СерииНоменклатуры

//Возвращает параметры для заполнения серий в объектах ВетИС.
//
//Возвращаемое значение:
//  Структура - параметры заполнения серий.
Функция ПараметрыЗаполненияСерий() Экспорт
	
	ПараметрыЗаполнения = Новый Структура();
	ПараметрыЗаполнения.Вставить("ПараметрыУказанияСерий", Неопределено);
	ПараметрыЗаполнения.Вставить("Склад",                  Неопределено);
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

// Заполняет свойство "Склад" в структуре параметров заполнения серий.
//
// Параметры:
//   Объект              - ДокументОбъект, ДанныеФормыКоллекция - Объект, хранящий значение склада.
//   ПараметрыЗаполнения - Структура                            - Параметры заполнения серий.
//
Процедура ЗаполнитьПараметрЗаполненияСклад(Объект, ПараметрыЗаполнения) Экспорт

	ПараметрыУказанияСерий = ПараметрыЗаполнения.ПараметрыУказанияСерий;
	
	Если ПараметрыУказанияСерий = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыУказанияСерий.ИмяПоляСклад)
		И ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(Объект, ПараметрыУказанияСерий.ИмяПоляСклад) Тогда
		
		ПараметрыЗаполнения.Склад = Объект[ПараметрыУказанияСерий.ИмяПоляСклад];
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПараметрыУказанияСерийСоответствиеНоменклатурыВЕТИС(Объект) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "РегистрСведений.СоответствиеНоменклатурыВЕТИС";
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	ПараметрыУказанияСерий.ТоварВШапке = Истина;
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии = "";
	ПараметрыУказанияСерий.ИмяТЧТовары = "";
	ПараметрыУказанияСерий.ИмяПоляКоличество = Неопределено;
	ПараметрыУказанияСерий.ИмяПоляСклад = Неопределено;
	ПараметрыУказанияСерий.ИмяИсточникаЗначенийВФормеОбъекта = "ЭтаФорма";
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Возвращает параметры указания серий в документе 'ВходящаяТранспортнаяОперацияВЕТИС'.
//
// Параметры:
//	Объект - Структура - Значения реквизитов объекта, необходимых для заполнения параметров указания серий.
//
// Возвращаемое значение:
//	Структура - Состав полей определен в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//
Функция ПараметрыУказанияСерийВходящаяТранспортнаяОперацияВЕТИС(Объект) Экспорт
	
	Если ТорговыйОбъектЭтоСклад(Объект.ТорговыйОбъект) Тогда
			
		ПараметрыСерийСклада = СкладыСервер.ИспользованиеСерийНаСкладе(Объект.ТорговыйОбъект, Ложь);
		
		ИспользоватьСерииНоменклатуры = ПараметрыСерийСклада.ИспользоватьСерииНоменклатуры
										Или ПараметрыСерийСклада.УчитыватьСебестоимостьПоСериям;
		УчитыватьСебестоимостьПоСериям = ПараметрыСерийСклада.УчитыватьСебестоимостьПоСериям;
		
		ИмяПоляСклад = "ТорговыйОбъект";
		
	Иначе
		
		ИспользоватьСерииНоменклатуры  = Ложь;
		УчитыватьСебестоимостьПоСериям = Ложь;
		
		ИмяПоляСклад = Неопределено;
		
	КонецЕсли;
	
	ПараметрыУказанияСерийТовары = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	
	ПараметрыУказанияСерийТовары.ПолноеИмяОбъекта = "Документ.ВходящаяТранспортнаяОперацияВЕТИС";
	ПараметрыУказанияСерийТовары.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерийТовары.УчитыватьСебестоимостьПоСериям = УчитыватьСебестоимостьПоСериям;
	
	ПараметрыУказанияСерийТовары.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ВзаимодействиеСВЕТИС);
	ПараметрыУказанияСерийТовары.ИменаПолейДополнительные.Добавить("ЕстьУточнения");
	ПараметрыУказанияСерийТовары.ИменаПолейДополнительные.Добавить("СрокГодностиНачалоПериода");
	ПараметрыУказанияСерийТовары.ИменаПолейДополнительные.Добавить("ЗаписьСкладскогоЖурнала");
	ПараметрыУказанияСерийТовары.ИменаПолейДополнительные.Добавить("ИдентификаторПартии");
	
	ПараметрыУказанияСерийТовары.ИмяТЧСерии   = "Товары";
	ПараметрыУказанияСерийТовары.ИмяПоляСклад = ИмяПоляСклад;
	ПараметрыУказанияСерийТовары.Дата         = Объект.Дата;
	
	ПараметрыУказанияСерийТоварыУточнение = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	
	ПараметрыУказанияСерийТоварыУточнение.ПолноеИмяОбъекта = "Документ.ВходящаяТранспортнаяОперацияВЕТИС";
	ПараметрыУказанияСерийТоварыУточнение.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерийТоварыУточнение.УчитыватьСебестоимостьПоСериям = УчитыватьСебестоимостьПоСериям;
	
	ПараметрыУказанияСерийТоварыУточнение.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ВзаимодействиеСВЕТИС);
	
	ПараметрыУказанияСерийТоварыУточнение.ИмяТЧТовары  = "ТоварыУточнение";
	ПараметрыУказанияСерийТоварыУточнение.ИмяТЧСерии   = "ТоварыУточнение";
	ПараметрыУказанияСерийТоварыУточнение.ИмяПоляСклад = ИмяПоляСклад;
	ПараметрыУказанияСерийТоварыУточнение.Дата         = Объект.Дата;
	ПараметрыУказанияСерийТоварыУточнение.ИмяИсточникаЗначенийВФормеОбъекта = "ЭтаФорма";
	
	ПараметрыУказанияСерий = Новый Структура;
	ПараметрыУказанияСерий.Вставить("Товары", ПараметрыУказанияСерийТовары);
	ПараметрыУказанияСерий.Вставить("ТоварыУточнение", ПараметрыУказанияСерийТоварыУточнение);
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Возвращает параметры указания серий в документе 'ИсходящаяТранспортнаяОперацияВЕТИС'.
//
// Параметры:
//	Объект - Структура - Значения реквизитов объекта, необходимых для заполнения параметров указания серий.
//
// Возвращаемое значение:
//	Структура - Состав полей определен в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//
Функция ПараметрыУказанияСерийИсходящаяТранспортнаяОперацияВЕТИС(Объект) Экспорт
	
	Если ТорговыйОбъектЭтоСклад(Объект.ТорговыйОбъект) Тогда
			
		ПараметрыСерийСклада = СкладыСервер.ИспользованиеСерийНаСкладе(Объект.ТорговыйОбъект, Ложь);
		
		ИспользоватьСерииНоменклатуры = ПараметрыСерийСклада.ИспользоватьСерииНоменклатуры
										Или ПараметрыСерийСклада.УчитыватьСебестоимостьПоСериям;
		УчитыватьСебестоимостьПоСериям = ПараметрыСерийСклада.УчитыватьСебестоимостьПоСериям;
		
		ИмяПоляСклад = "ТорговыйОбъект";
		
	Иначе
		
		ИспользоватьСерииНоменклатуры  = Ложь;
		УчитыватьСебестоимостьПоСериям = Ложь;
		
		ИмяПоляСклад = Неопределено;
		
	КонецЕсли;
	
	ПараметрыУказанияСерийТовары = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	
	ПараметрыУказанияСерийТовары.ПолноеИмяОбъекта = "Документ.ИсходящаяТранспортнаяОперацияВЕТИС";
	ПараметрыУказанияСерийТовары.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерийТовары.УчитыватьСебестоимостьПоСериям = УчитыватьСебестоимостьПоСериям;
	
	ПараметрыУказанияСерийТовары.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ВзаимодействиеСВЕТИС);
	
	ПараметрыУказанияСерийТовары.ИмяТЧСерии   = "Товары";
	ПараметрыУказанияСерийТовары.ИмяПоляСклад = ИмяПоляСклад;
	ПараметрыУказанияСерийТовары.Дата         = Объект.Дата;
	
	Возврат ПараметрыУказанияСерийТовары;
	
КонецФункции

// Возвращает параметры указания серий в документе 'ИнвентаризацияПродукцииВЕТИС'.
//
// Параметры:
//	Объект - Структура - Значения реквизитов объекта, необходимых для заполнения параметров указания серий.
//
// Возвращаемое значение:
//	Структура - Состав полей определен в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//
Функция ПараметрыУказанияСерийИнвентаризацияПродукцииВЕТИС(Объект) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ИнвентаризацияПродукцииВЕТИС";
	
	Если ТорговыйОбъектЭтоСклад(Объект.ТорговыйОбъект) Тогда
		
		ПараметрыСерий = СкладыСервер.ИспользованиеСерийНаСкладе(Объект.ТорговыйОбъект, Ложь);
		
		ИспользоватьСерииНоменклатуры = ПараметрыСерий.ИспользоватьСерииНоменклатуры
										Или ПараметрыСерий.УчитыватьСебестоимостьПоСериям;
		УчитыватьСебестоимостьПоСериям = ПараметрыСерий.УчитыватьСебестоимостьПоСериям;
		
		ИмяПоляСклад = "ТорговыйОбъект";
		
	Иначе
		
		ИспользоватьСерииНоменклатуры  = Ложь;
		УчитыватьСебестоимостьПоСериям = Ложь;
		
		ИмяПоляСклад = Неопределено;
		
	КонецЕсли;
	
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = УчитыватьСебестоимостьПоСериям;
	
	ПараметрыУказанияСерий.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ВзаимодействиеСВЕТИС);
	
	ПараметрыУказанияСерий.ИмяТЧСерии        = "Товары";
	ПараметрыУказанияСерий.ИмяПоляКоличество = "КоличествоИзменение";
	ПараметрыУказанияСерий.ИмяПоляСклад      = ИмяПоляСклад;
	ПараметрыУказанияСерий.Дата              = Объект.Дата;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Возвращает параметры указания серий в документе 'ПроизводственнаяОперацияВЕТИС'.
//
// Параметры:
//	Объект - Структура - Значения реквизитов объекта, необходимых для заполнения параметров указания серий.
//
// Возвращаемое значение:
//	Структура - Состав полей определен в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//
Функция ПараметрыУказанияСерийПроизводственнаяОперацияВЕТИС(Объект) Экспорт
	
	ПараметрыУказанияСерий = Новый Структура;
	
	ИспользоватьСерииНоменклатуры  = Ложь;
	УчитыватьСебестоимостьПоСериям = Ложь;
	ИмяПоляСклад                   = Неопределено;
	
	Если ТорговыйОбъектЭтоСклад(Объект.ТорговыйОбъект) Тогда
		ИмяПоляСклад = "ТорговыйОбъект";
	КонецЕсли;
	
	Если ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.СборкаТоваров")
		И ТипЗнч(Объект.ТорговыйОбъект) = Тип("СправочникСсылка.Склады") Тогда
		
		Если ИмяПоляСклад <> Неопределено Тогда
			
			ПараметрыСерий = СкладыСервер.ИспользованиеСерийНаСкладе(Объект.ТорговыйОбъект, Ложь);
			
			ИспользоватьСерииНоменклатуры = ПараметрыСерий.ИспользоватьСерииНоменклатуры
				Или ПараметрыСерий.УчитыватьСебестоимостьПоСериям;
			УчитыватьСебестоимостьПоСериям = ПараметрыСерий.УчитыватьСебестоимостьПоСериям;
		КонецЕсли;
		
	КонецЕсли;

	// Параметры ТЧ Товары
	ПараметрыУказанияСерийТЧ = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	
	ПараметрыУказанияСерийТЧ.ПолноеИмяОбъекта = "Документ.ПроизводственнаяОперацияВЕТИС";
	ПараметрыУказанияСерийТЧ.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерийТЧ.УчитыватьСебестоимостьПоСериям = УчитыватьСебестоимостьПоСериям;
	
	ПараметрыУказанияСерийТЧ.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ВзаимодействиеСВЕТИС);
	
	ПараметрыУказанияСерийТЧ.ИмяТЧСерии   = "Товары";
	ПараметрыУказанияСерийТЧ.ИмяПоляСклад = ИмяПоляСклад;
	ПараметрыУказанияСерийТЧ.Дата         = Объект.Дата;
	
	ПараметрыУказанияСерийТЧ.ИменаПолейДополнительные.Добавить("ДатаПроизводстваНачалоПериода");
	ПараметрыУказанияСерийТЧ.ИменаПолейДополнительные.Добавить("СрокГодностиНачалоПериода");
	ПараметрыУказанияСерийТЧ.ИменаПолейДополнительные.Добавить("ЗаписьСкладскогоЖурнала");
	ПараметрыУказанияСерийТЧ.ИменаПолейДополнительные.Добавить("ИдентификаторПартии");
	
	ПараметрыУказанияСерий.Вставить("Товары", ПараметрыУказанияСерийТЧ);
	
	// Параметры ТЧ Сырье
	ПараметрыУказанияСерийТЧ = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	
	ПараметрыУказанияСерийТЧ.ПолноеИмяОбъекта = "Документ.ПроизводственнаяОперацияВЕТИС";
	ПараметрыУказанияСерийТЧ.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерийТЧ.УчитыватьСебестоимостьПоСериям = УчитыватьСебестоимостьПоСериям;
	
	ПараметрыУказанияСерийТЧ.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ВзаимодействиеСВЕТИС);
	
	ПараметрыУказанияСерийТЧ.ИмяТЧТовары  = "Сырье";
	ПараметрыУказанияСерийТЧ.ИмяТЧСерии   = "Сырье";
	ПараметрыУказанияСерийТЧ.ИмяПоляСклад = ИмяПоляСклад;
	ПараметрыУказанияСерийТЧ.Дата         = Объект.Дата;
	
	ПараметрыУказанияСерий.Вставить("Сырье", ПараметрыУказанияСерийТЧ);
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Возвращает параметры указания серий в документе 'ЗапросСкладскогоЖурналаВЕТИС'.
//
// Возвращаемое значение:
//	Структура - Состав полей определен в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//
Функция ПараметрыУказанияСерийЗапросСкладскогоЖурналаВЕТИС(Объект) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ЗапросСкладскогоЖурналаВЕТИС";
	
	Если ТорговыйОбъектЭтоСклад(Объект.ТорговыйОбъект) Тогда
			
		ПараметрыСерий = СкладыСервер.ИспользованиеСерийНаСкладе(Объект.ТорговыйОбъект, Ложь);
		
		ИспользоватьСерииНоменклатуры = ПараметрыСерий.ИспользоватьСерииНоменклатуры
										Или ПараметрыСерий.УчитыватьСебестоимостьПоСериям;
		УчитыватьСебестоимостьПоСериям = ПараметрыСерий.УчитыватьСебестоимостьПоСериям;
		
		ИмяПоляСклад = "ТорговыйОбъект";
		
	Иначе
		
		ИспользоватьСерииНоменклатуры  = Ложь;
		УчитыватьСебестоимостьПоСериям = Ложь;
		
		ИмяПоляСклад = Неопределено;
		
	КонецЕсли;
	
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = УчитыватьСебестоимостьПоСериям;
	
	ПараметрыУказанияСерий.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ВзаимодействиеСВЕТИС);
	
	ПараметрыУказанияСерий.ИмяТЧСерии                     = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад                   = ИмяПоляСклад;
	ПараметрыУказанияСерий.Дата                           = Объект.Дата;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

Функция ТекстЗапросаЗаполненияСтатусовУказанияСерийСоответствиеНоменклатурыВЕТИС(ПараметрыУказанияСерий) Экспорт
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Товары.Номенклатура,
		|	Товары.Серия,
		|	Товары.СтатусУказанияСерий,
		|	Товары.НомерСтроки
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	&Товары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.НомерСтроки КАК НомерСтроки,
		|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
		|	ВЫБОР
		|		КОГДА ВидыНоменклатуры.НастройкаИспользованияСерий = ЗНАЧЕНИЕ(Перечисление.НастройкиИспользованияСерийНоменклатуры.ПустаяСсылка)
		|			ИЛИ ВидыНоменклатуры.НастройкаИспользованияСерий ЕСТЬ NULL
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|					ТОГДА 14
		|				ИНАЧЕ 13
		|			КОНЕЦ
		|	КОНЕЦ КАК СтатусУказанияСерий
		|ПОМЕСТИТЬ ТаблицаСтатусов
		|ИЗ
		|	Товары КАК Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
		|		ПО (ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры = ВидыНоменклатуры.Ссылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСтатусов.НомерСтроки КАК НомерСтроки,
		|	ТаблицаСтатусов.СтатусУказанияСерий КАК СтатусУказанияСерий
		|ИЗ
		|	ТаблицаСтатусов КАК ТаблицаСтатусов
		|ГДЕ
		|	ТаблицаСтатусов.СтарыйСтатусУказанияСерий <> ТаблицаСтатусов.СтатусУказанияСерий
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";

	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса заполнения статусов указания серий в документе 'ВходящаяТранспортнаяОперацияВЕТИС'.
//
// Параметры:
//	ПараметрыУказанияСерий - Структура - Состав полей определен в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//
// Возвращаемое значение:
//	Строка - Текст запроса заполнения статусов указания серий в документе 'ВходящаяТранспортнаяОперацияВЕТИС'.
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерийВходящаяТранспортнаяОперацияВЕТИС(ПараметрыУказанияСерий) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	&ТекстПоляТоварыЕстьУточнения КАК ЕстьУточнения,
	|	Товары.Серия КАК Серия,
	|	Товары.Количество КАК Количество,
	|	Товары.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.ВидНоменклатуры КАК ВидНоменклатуры,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыДляЗапроса
	|ИЗ
	|	Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТаблицаНоменклатуры
	|		ПО Товары.Номенклатура = ТаблицаНоменклатуры.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНоменклатуры.ВидНоменклатуры,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаблицаНоменклатуры.ВидНоменклатуры,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий ЕСТЬ NULL
	|				ИЛИ ПолитикиУчетаСерий.ПолитикаУчетаСерий = ЗНАЧЕНИЕ(Справочник.ПолитикиУчетаСерий.ПустаяСсылка)
	|				ИЛИ ПолитикиУчетаСерий.ПолитикаУчетаСерий = ЗНАЧЕНИЕ(Справочник.ПолитикиУчетаСерий.СерииНеИспользуются)
	|				ИЛИ Товары.ЕстьУточнения
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтгрузки
	|					ТОГДА ВЫБОР
	|							КОГДА ТоварыДляЗапроса.Количество > 0
	|									И ТоварыДляЗапроса.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|								ТОГДА ВЫБОР
	|										КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|											ТОГДА 14
	|										ИНАЧЕ 10
	|									КОНЕЦ
	|							ИНАЧЕ ВЫБОР
	|									КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|										ТОГДА 15
	|									ИНАЧЕ 11
	|								КОНЕЦ
	|						КОНЕЦ
	|				КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтбора
	|					ТОГДА ВЫБОР
	|							КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчетСерийПоFEFO
	|								ТОГДА ВЫБОР
	|										КОГДА ТоварыДляЗапроса.Количество > 0
	|												И ТоварыДляЗапроса.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|											ТОГДА 6
	|										ИНАЧЕ 25
	|									КОНЕЦ
	|							ИНАЧЕ ВЫБОР
	|									КОГДА ТоварыДляЗапроса.Количество > 0
	|											И ТоварыДляЗапроса.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|										ТОГДА 8
	|									ИНАЧЕ 27
	|								КОНЕЦ
	|						КОНЕЦ
	|				КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьОстаткиСерий
	|					ТОГДА ВЫБОР
	|							КОГДА ТоварыДляЗапроса.Количество > 0
	|									И ТоварыДляЗапроса.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|								ТОГДА 4
	|							ИНАЧЕ 23
	|						КОНЕЦ
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ ТаблицаСтатусов
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыДляЗапроса КАК ТоварыДляЗапроса
	|		ПО Товары.Номенклатура = ТоварыДляЗапроса.Номенклатура
	|			И Товары.Характеристика = ТоварыДляЗапроса.Характеристика
	|			И Товары.Серия = ТоварыДляЗапроса.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|		ПО (ПолитикиУчетаСерий.Склад = &Склад)
	|			И (ТоварыДляЗапроса.ВидНоменклатуры = ПолитикиУчетаСерий.Ссылка)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ
	|	ТаблицаСтатусов.НомерСтроки КАК НомерСтроки,
	|	ТаблицаСтатусов.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ИЗ
	|	ТаблицаСтатусов КАК ТаблицаСтатусов
	|ГДЕ
	|	ТаблицаСтатусов.СтарыйСтатусУказанияСерий <> ТаблицаСтатусов.СтатусУказанияСерий
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Если ПараметрыУказанияСерий.ИменаПолейДополнительные.Найти("ЕстьУточнения") <> Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПоляТоварыЕстьУточнения", "Товары.ЕстьУточнения");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПоляТоварыЕстьУточнения", "ЛОЖЬ");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса заполнения статусов указания серий в документе 'ЗапросСкладскогоЖурналаВЕТИС'.
//
// Параметры:
//	ПараметрыУказанияСерий - Структура - Состав полей определен в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//
// Возвращаемое значение:
//	Строка - Текст запроса заполнения статусов указания серий в документе 'ИсходящаяТранспортнаяОперацияВЕТИС'.
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерийЗапросСкладскогоЖурналаВЕТИС(ПараметрыУказанияСерий) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	Товары.Количество КАК Количество,
	|	Товары.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.ВидНоменклатуры КАК ВидНоменклатуры,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыДляЗапроса
	|ИЗ
	|	Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТаблицаНоменклатуры
	|		ПО Товары.Номенклатура = ТаблицаНоменклатуры.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНоменклатуры.ВидНоменклатуры,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаблицаНоменклатуры.ВидНоменклатуры,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий ЕСТЬ NULL
	|				ИЛИ ПолитикиУчетаСерий.ПолитикаУчетаСерий = ЗНАЧЕНИЕ(Справочник.ПолитикиУчетаСерий.ПустаяСсылка)
	|				ИЛИ ПолитикиУчетаСерий.ПолитикаУчетаСерий = ЗНАЧЕНИЕ(Справочник.ПолитикиУчетаСерий.СерииНеИспользуются)
	|			ТОГДА ВЫБОР
	|				КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|					ТОГДА 2
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтгрузки
	|					ТОГДА ВЫБОР
	|							КОГДА ТоварыДляЗапроса.Количество > 0
	|									И ТоварыДляЗапроса.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|								ТОГДА ВЫБОР
	|										КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|											ТОГДА 14
	|										ИНАЧЕ 10
	|									КОНЕЦ
	|							ИНАЧЕ ВЫБОР
	|									КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|										ТОГДА 15
	|									ИНАЧЕ 11
	|								КОНЕЦ
	|						КОНЕЦ
	|				КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтбора
	|					ТОГДА ВЫБОР
	|							КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчетСерийПоFEFO
	|								ТОГДА ВЫБОР
	|										КОГДА ТоварыДляЗапроса.Количество > 0
	|												И ТоварыДляЗапроса.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|											ТОГДА 6
	|										ИНАЧЕ 25
	|									КОНЕЦ
	|							ИНАЧЕ ВЫБОР
	|									КОГДА ТоварыДляЗапроса.Количество > 0
	|											И ТоварыДляЗапроса.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|										ТОГДА 8
	|									ИНАЧЕ 27
	|								КОНЕЦ
	|						КОНЕЦ
	|				КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьОстаткиСерий
	|					ТОГДА ВЫБОР
	|							КОГДА ТоварыДляЗапроса.Количество > 0
	|									И ТоварыДляЗапроса.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|								ТОГДА 4
	|							ИНАЧЕ 23
	|						КОНЕЦ
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ ТаблицаСтатусов
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыДляЗапроса КАК ТоварыДляЗапроса
	|		ПО Товары.Номенклатура = ТоварыДляЗапроса.Номенклатура
	|			И Товары.Характеристика = ТоварыДляЗапроса.Характеристика
	|			И Товары.Серия = ТоварыДляЗапроса.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|		ПО (ПолитикиУчетаСерий.Склад = &Склад)
	|			И (ТоварыДляЗапроса.ВидНоменклатуры = ПолитикиУчетаСерий.Ссылка)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ
	|	ТаблицаСтатусов.НомерСтроки КАК НомерСтроки,
	|	ТаблицаСтатусов.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ИЗ
	|	ТаблицаСтатусов КАК ТаблицаСтатусов
	|ГДЕ
	|	ТаблицаСтатусов.СтарыйСтатусУказанияСерий <> ТаблицаСтатусов.СтатусУказанияСерий
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса заполнения статусов указания серий в документе 'ИсходящаяТранспортнаяОперацияВЕТИС'.
//
// Параметры:
//	ПараметрыУказанияСерий - Структура - Состав полей определен в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//
// Возвращаемое значение:
//	Строка - Текст запроса заполнения статусов указания серий в документе 'ИсходящаяТранспортнаяОперацияВЕТИС'.
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерийИсходящаяТранспортнаяОперацияВЕТИС() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	Товары.Количество КАК Количество,
	|	Товары.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.ВидНоменклатуры КАК ВидНоменклатуры,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыДляЗапроса
	|ИЗ
	|	Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТаблицаНоменклатуры
	|		ПО Товары.Номенклатура = ТаблицаНоменклатуры.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНоменклатуры.ВидНоменклатуры,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаблицаНоменклатуры.ВидНоменклатуры,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий ЕСТЬ NULL
	|				ИЛИ ПолитикиУчетаСерий.ПолитикаУчетаСерий = ЗНАЧЕНИЕ(Справочник.ПолитикиУчетаСерий.ПустаяСсылка)
	|				ИЛИ ПолитикиУчетаСерий.ПолитикаУчетаСерий = ЗНАЧЕНИЕ(Справочник.ПолитикиУчетаСерий.СерииНеИспользуются)
	|			ТОГДА ВЫБОР
	|				КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|					ТОГДА 2
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтгрузки
	|					ТОГДА ВЫБОР
	|							КОГДА ТоварыДляЗапроса.Количество > 0
	|									И ТоварыДляЗапроса.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|								ТОГДА ВЫБОР
	|										КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|											ТОГДА 14
	|										ИНАЧЕ 10
	|									КОНЕЦ
	|							ИНАЧЕ ВЫБОР
	|									КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|										ТОГДА 15
	|									ИНАЧЕ 11
	|								КОНЕЦ
	|						КОНЕЦ
	|				КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтбора
	|					ТОГДА ВЫБОР
	|							КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчетСерийПоFEFO
	|								ТОГДА ВЫБОР
	|										КОГДА ТоварыДляЗапроса.Количество > 0
	|												И ТоварыДляЗапроса.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|											ТОГДА 6
	|										ИНАЧЕ 25
	|									КОНЕЦ
	|							ИНАЧЕ ВЫБОР
	|									КОГДА ТоварыДляЗапроса.Количество > 0
	|											И ТоварыДляЗапроса.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|										ТОГДА 8
	|									ИНАЧЕ 27
	|								КОНЕЦ
	|						КОНЕЦ
	|				КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьОстаткиСерий
	|					ТОГДА ВЫБОР
	|							КОГДА ТоварыДляЗапроса.Количество > 0
	|									И ТоварыДляЗапроса.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|								ТОГДА 4
	|							ИНАЧЕ 23
	|						КОНЕЦ
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ ТаблицаСтатусов
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыДляЗапроса КАК ТоварыДляЗапроса
	|		ПО Товары.Номенклатура = ТоварыДляЗапроса.Номенклатура
	|			И Товары.Характеристика = ТоварыДляЗапроса.Характеристика
	|			И Товары.Серия = ТоварыДляЗапроса.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|		ПО (ПолитикиУчетаСерий.Склад = &Склад)
	|			И (ТоварыДляЗапроса.ВидНоменклатуры = ПолитикиУчетаСерий.Ссылка)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ
	|	ТаблицаСтатусов.НомерСтроки КАК НомерСтроки,
	|	ТаблицаСтатусов.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ИЗ
	|	ТаблицаСтатусов КАК ТаблицаСтатусов
	|ГДЕ
	|	ТаблицаСтатусов.СтарыйСтатусУказанияСерий <> ТаблицаСтатусов.СтатусУказанияСерий
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса заполнения статусов указания серий в документе 'ИнвентаризацияПродукцииВЕТИС'.
//
// Параметры:
//	ПараметрыУказанияСерий - Структура - Состав полей определен в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//
// Возвращаемое значение:
//	Строка - Текст запроса заполнения статусов указания серий в документе 'ИнвентаризацияПродукцииВЕТИС'.
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерийИнвентаризацияПродукцииВЕТИС(ПараметрыУказанияСерий) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.НомерСтроки    КАК НомерСтроки,
	|	Товары.Номенклатура   КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия          КАК Серия,
	|	Товары.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.ВидНоменклатуры КАК ВидНоменклатуры,
	|	Товары.Номенклатура   КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия          КАК Серия
	|ПОМЕСТИТЬ ТоварыДляЗапроса
	|ИЗ
	|	Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТаблицаНоменклатуры
	|		ПО Товары.Номенклатура = ТаблицаНоменклатуры.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаблицаНоменклатуры.ВидНоменклатуры,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий ЕСТЬ NULL
	|				ИЛИ ПолитикиУчетаСерий.ПолитикаУчетаСерий = ЗНАЧЕНИЕ(Справочник.ПолитикиУчетаСерий.ПустаяСсылка)
	|				ИЛИ ПолитикиУчетаСерий.ПолитикаУчетаСерий = ЗНАЧЕНИЕ(Справочник.ПолитикиУчетаСерий.СерииНеИспользуются)
	|			ТОГДА ВЫБОР
	|				КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|					ТОГДА 2
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтгрузки
	|					ТОГДА ВЫБОР
	|							КОГДА ТоварыДляЗапроса.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|								ТОГДА ВЫБОР
	|										КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|											ТОГДА 14
	|										ИНАЧЕ 10
	|									КОНЕЦ
	|							ИНАЧЕ ВЫБОР
	|									КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|										ТОГДА 15
	|									ИНАЧЕ 11
	|								КОНЕЦ
	|						КОНЕЦ
	|				КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтбора
	|					ТОГДА ВЫБОР
	|							КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчетСерийПоFEFO
	|								ТОГДА ВЫБОР
	|										КОГДА ТоварыДляЗапроса.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|											ТОГДА 6
	|										ИНАЧЕ 25
	|									КОНЕЦ
	|							ИНАЧЕ ВЫБОР
	|									КОГДА ТоварыДляЗапроса.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|										ТОГДА 8
	|									ИНАЧЕ 27
	|								КОНЕЦ
	|						КОНЕЦ
	|				КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьОстаткиСерий
	|					ТОГДА ВЫБОР
	|							КОГДА ТоварыДляЗапроса.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|								ТОГДА 4
	|							ИНАЧЕ 23
	|						КОНЕЦ
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ ТаблицаСтатусов
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыДляЗапроса КАК ТоварыДляЗапроса
	|		ПО Товары.Номенклатура = ТоварыДляЗапроса.Номенклатура
	|			И Товары.Характеристика = ТоварыДляЗапроса.Характеристика
	|			И Товары.Серия = ТоварыДляЗапроса.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|		ПО (ПолитикиУчетаСерий.Склад = &Склад)
	|			И (ТоварыДляЗапроса.ВидНоменклатуры = ПолитикиУчетаСерий.Ссылка)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ
	|	ТаблицаСтатусов.НомерСтроки КАК НомерСтроки,
	|	ТаблицаСтатусов.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ИЗ
	|	ТаблицаСтатусов КАК ТаблицаСтатусов
	|ГДЕ
	|	ТаблицаСтатусов.СтарыйСтатусУказанияСерий <> ТаблицаСтатусов.СтатусУказанияСерий
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса заполнения статусов указания серий в документе 'ПроизводственнаяОперацияВЕТИС'.
//
// Параметры:
//	ПараметрыУказанияСерий - Структура - Состав полей определен в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//
// Возвращаемое значение:
//	Строка - Текст запроса заполнения статусов указания серий в документе 'ПроизводственнаяОперацияВЕТИС'.
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерийПроизводственнаяОперацияВЕТИС(ПараметрыУказанияСерий) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.НомерСтроки    КАК НомерСтроки,
	|	Товары.Номенклатура   КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия          КАК Серия,
	|	Товары.Количество     КАК Количество,
	|	Товары.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.ВидНоменклатуры КАК ВидНоменклатуры,
	|	Товары.Номенклатура      КАК Номенклатура,
	|	Товары.Характеристика    КАК Характеристика,
	|	Товары.Серия             КАК Серия,
	|	СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыДляЗапроса
	|ИЗ
	|	Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТаблицаНоменклатуры
	|		ПО Товары.Номенклатура = ТаблицаНоменклатуры.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНоменклатуры.ВидНоменклатуры,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаблицаНоменклатуры.ВидНоменклатуры,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ПолитикиСерий.ПолитикаУчетаСерий ЕСТЬ NULL
	|				ИЛИ ПолитикиСерий.ПолитикаУчетаСерий = ЗНАЧЕНИЕ(Справочник.ПолитикиУчетаСерий.ПустаяСсылка)
	|				ИЛИ ПолитикиСерий.ПолитикаУчетаСерий = ЗНАЧЕНИЕ(Справочник.ПолитикиУчетаСерий.СерииНеИспользуются)
	|			ТОГДА ВЫБОР
	|				КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|					ТОГДА 2
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПолитикиСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтгрузки
	|					ТОГДА ВЫБОР
	|							КОГДА ТоварыДляЗапроса.Количество > 0
	|									И ТоварыДляЗапроса.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|								ТОГДА ВЫБОР
	|										КОГДА ПолитикиСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|											ТОГДА 14
	|										ИНАЧЕ 10
	|									КОНЕЦ
	|							ИНАЧЕ ВЫБОР
	|									КОГДА ПолитикиСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|										ТОГДА 15
	|									ИНАЧЕ 11
	|								КОНЕЦ
	|						КОНЕЦ
	|				КОГДА ПолитикиСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтбора
	|					ТОГДА ВЫБОР
	|							КОГДА ПолитикиСерий.ПолитикаУчетаСерий.УчетСерийПоFEFO
	|								ТОГДА ВЫБОР
	|										КОГДА ТоварыДляЗапроса.Количество > 0
	|												И ТоварыДляЗапроса.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|											ТОГДА 6
	|										ИНАЧЕ 25
	|									КОНЕЦ
	|							ИНАЧЕ ВЫБОР
	|									КОГДА ТоварыДляЗапроса.Количество > 0
	|											И ТоварыДляЗапроса.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|										ТОГДА 8
	|									ИНАЧЕ 27
	|								КОНЕЦ
	|						КОНЕЦ
	|				КОГДА ПолитикиСерий.ПолитикаУчетаСерий.УчитыватьОстаткиСерий
	|					ТОГДА ВЫБОР
	|							КОГДА ТоварыДляЗапроса.Количество > 0
	|									И ТоварыДляЗапроса.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|								ТОГДА 4
	|							ИНАЧЕ 23
	|						КОНЕЦ
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ ТаблицаСтатусов
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыДляЗапроса КАК ТоварыДляЗапроса
	|		ПО Товары.Номенклатура = ТоварыДляЗапроса.Номенклатура
	|			И Товары.Характеристика = ТоварыДляЗапроса.Характеристика
	|			И Товары.Серия = ТоварыДляЗапроса.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПолитикУчетаСерий КАК ПолитикиСерий
	|		ПО ТоварыДляЗапроса.ВидНоменклатуры = ПолитикиСерий.Ссылка
	|			//&УсловиеСоединенияТаблиц
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ
	|	ТаблицаСтатусов.НомерСтроки КАК НомерСтроки,
	|	ТаблицаСтатусов.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ИЗ
	|	ТаблицаСтатусов КАК ТаблицаСтатусов
	|ГДЕ
	|	ТаблицаСтатусов.СтарыйСтатусУказанияСерий <> ТаблицаСтатусов.СтатусУказанияСерий
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Если ПараметрыУказанияСерий.ИмяПоляСклад = Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаПолитикУчетаСерий",   "Справочник.ВидыНоменклатуры");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//&УсловиеСоединенияТаблиц", "");
	Иначе
		ТекстУсловияСоединенияТаблиц = "И ПолитикиСерий.Склад = &Склад";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаПолитикУчетаСерий",   "Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//&УсловиеСоединенияТаблиц", ТекстУсловияСоединенияТаблиц);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Заполняет реквизит "Серия" в товарной табличной части.
//
// Параметры:
//   ТабличнаяЧастьТовары    - ДанныеФормыКоллекция, ТабличнаяЧасть, ТаблицаЗначений - Товарная табличная часть объекта.
//   ВыделенныеСтрокиТоваров - Массив    - Обрабатываемые строки таблицы.
//   ПараметрыЗаполнения    - Структура - См ИнтеграцияВЕТИСПереопределяемый.ПараметрыЗаполненияСерий.
//   ТабличнаяЧастьУточнения - ДанныеФормыКоллекция, ТабличнаяЧасть, ТаблицаЗначений - Дополнительная товарная табличная часть объекта.
//
// Возвращаемое значение:
//   Структура - Структура со свойствами:
//     * ЗаполнениеЗавершено - Булево - Признак успешного выполнения генераций и заполнения серий.
//     * СписокОшибок        - Массив, Неопределено - Список ошибок.
//
Функция ЗаполнитьСгенерироватьСерии(ТабличнаяЧастьТовары,
									ВыделенныеСтрокиТоваров,
									ПараметрыЗаполнения,
									ТабличнаяЧастьУточнения = Неопределено) Экспорт
									
	ПараметрыУказанияСерий = ПараметрыЗаполнения.ПараметрыУказанияСерий;
	Склад = ПараметрыЗаполнения.Склад;
	
	ПроверитьСериюРассчитатьСтатус = Новый Структура("ПараметрыУказанияСерий, Склад", ПараметрыУказанияСерий, Склад);
	
	СтруктураДействий = Новый Структура();
	СтруктураДействий.Вставить("ПроверитьСериюРассчитатьСтатус", ПроверитьСериюРассчитатьСтатус);
	
	// Инициализация результирующей структуры.
	Результат = Новый Структура("ЗаполнениеЗавершено, СписокОшибок", Ложь, Неопределено);
	
	Запрос = Новый Запрос;
	
	Если ТабличнаяЧастьУточнения = Неопределено Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
		|	"""" КАК ИдентификаторСтроки,
		|	ТаблицаТовары.ИдентификаторПартии КАК ИдентификаторПартии,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаТовары.Характеристика КАК Характеристика,
		|	ТаблицаТовары.Серия КАК Серия,
		|	ТаблицаТовары.ЗаписьСкладскогоЖурнала КАК ЗаписьСкладскогоЖурнала,
		|	ТаблицаТовары.СтатусУказанияСерий КАК СтатусУказанияСерий,
		|	ЛОЖЬ КАК ЕстьУточнения
		|ПОМЕСТИТЬ ВтТовары
		|ИЗ
		|	&ТаблицаТовары КАК ТаблицаТовары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	-1 КАК НомерСтроки,
		|	-1 КАК ИдентификаторСтроки,
		|	"""" КАК ИдентификаторПартии,
		|	"""" КАК Номенклатура,
		|	"""" КАК Характеристика,
		|	"""" КАК СтатусУказанияСерий,
		|	"""" КАК Серия
		|ПОМЕСТИТЬ ВтУточнения";
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ТаблицаТовары.ИдентификаторПартии КАК ИдентификаторПартии,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаТовары.Характеристика КАК Характеристика,
		|	ТаблицаТовары.Серия КАК Серия,
		|	ТаблицаТовары.ЗаписьСкладскогоЖурнала КАК ЗаписьСкладскогоЖурнала,
		|	ТаблицаТовары.СтатусУказанияСерий КАК СтатусУказанияСерий,
		|	ТаблицаТовары.ЕстьУточнения КАК ЕстьУточнения
		|ПОМЕСТИТЬ ВтТовары
		|ИЗ
		|	&ТаблицаТовары КАК ТаблицаТовары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыУточнение.НомерСтроки КАК НомерСтроки,
		|	ТоварыУточнение.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ТоварыУточнение.ИдентификаторПартии КАК ИдентификаторПартии,
		|	ТоварыУточнение.Номенклатура КАК Номенклатура,
		|	ТоварыУточнение.Характеристика КАК Характеристика,
		|	ТоварыУточнение.СтатусУказанияСерий КАК СтатусУказанияСерий,
		|	ТоварыУточнение.Серия КАК Серия
		|ПОМЕСТИТЬ ВтУточнения
		|ИЗ
		|	&ТоварыУточнение КАК ТоварыУточнение
		|ИНДЕКСИРОВАТЬ ПО
		|	ИдентификаторСтроки";
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов();
	
	ТекстЗапроса = 	ТекстЗапроса +
	"ВЫБРАТЬ
	|	ВтТовары.НомерСтроки КАК НомерСтрокиТовары,
	|	ЕСТЬNULL(ВтУточнения.НомерСтроки, 0) КАК НомерСтрокиУточнения,
	|	ВЫБОР
	|		КОГДА ВтТовары.ЕстьУточнения
	|			ТОГДА ЕСТЬNULL(ВтУточнения.ИдентификаторПартии, """""""")
	|		ИНАЧЕ ВтТовары.ИдентификаторПартии
	|	КОНЕЦ КАК ИдентификаторПартии,
	|	ВЫБОР
	|		КОГДА ВтТовары.ЕстьУточнения
	|			ТОГДА ЕСТЬNULL(ВтУточнения.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	|		ИНАЧЕ ВтТовары.Номенклатура
	|	КОНЕЦ КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ВтТовары.ЕстьУточнения
	|			ТОГДА ЕСТЬNULL(ВтУточнения.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|		ИНАЧЕ ВтТовары.Характеристика
	|	КОНЕЦ КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ВтТовары.ЕстьУточнения
	|			ТОГДА ЕСТЬNULL(ВтУточнения.СтатусУказанияСерий, 0)
	|		ИНАЧЕ ВтТовары.СтатусУказанияСерий
	|	КОНЕЦ КАК СтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ВтТовары.ЕстьУточнения
	|			ТОГДА ЕСТЬNULL(ВтУточнения.Серия, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
	|		ИНАЧЕ ВтТовары.Серия
	|	КОНЕЦ КАК Серия,
	|	ВтТовары.ЗаписьСкладскогоЖурнала КАК ЗаписьСкладскогоЖурнала,
	|	ЕСТЬNULL(ЗаписиСкладскогоЖурналаВЕТИС.ДатаПроизводстваНачалоПериода, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПроизводства,
	|	ЕСТЬNULL(ЗаписиСкладскогоЖурналаВЕТИС.СрокГодностиНачалоПериода, ДАТАВРЕМЯ(1, 1, 1)) КАК ГоденДо,
	|	ВтТовары.ЕстьУточнения
	|		И ВтУточнения.ИдентификаторСтроки ЕСТЬ NULL КАК ОшибкаНетУточнения,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаписиСкладскогоЖурналаВЕТИСПроизводители.Производитель) КАК КоличествоПроизводителей,
	|	ВтТовары.ЕстьУточнения КАК ЕстьУточнения,
	|	ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьХарактеристики, ЛОЖЬ) КАК НастройкаВидаИспользоватьХарактеристики,
	|	ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьЗаписьСкладскогоЖурналаВЕТИССерии, ЛОЖЬ) КАК НастройкаВидаИспользоватьЗаписьСкладскогоЖурналаВЕТИССерии,
	|	ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьДатуПроизводстваСерии, ЛОЖЬ) КАК НастройкаВидаИспользоватьДатуПроизводстваСерии,
	|	ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьСрокГодностиСерии, ЛОЖЬ) КАК НастройкаВидаИспользоватьСрокГодностиСерии,
	|	ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьИдентификаторПартииВЕТИССерии, ЛОЖЬ) КАК НастройкаВидаИспользоватьИдентификаторПартииВЕТИССерии,
	|	ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьПроизводителяВЕТИССерии, ЛОЖЬ) КАК НастройкаВидаИспользоватьПроизводителяВЕТИССерии,
	|	ЕСТЬNULL(ВидыНоменклатуры.АвтоматическиГенерироватьСерии, ЛОЖЬ) КАК НастройкаВидаАвтоматическиГенерироватьСерии,
	|	ВЫБОР
	|		КОГДА ВидыНоменклатуры.НастройкиСерийБерутсяИзДругогоВидаНоменклатуры
	|			ТОГДА ВидыНоменклатуры.ВладелецСерий
	|		ИНАЧЕ ВидыНоменклатуры.Ссылка
	|	КОНЕЦ КАК ВладелецСерий
	|ПОМЕСТИТЬ ВТОбъединеннаяТаблицаТоваров
	|ИЗ
	|	ВтТовары КАК ВтТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтУточнения КАК ВтУточнения
	|		ПО ВтТовары.ИдентификаторСтроки = ВтУточнения.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗаписиСкладскогоЖурналаВЕТИС.Производители КАК ЗаписиСкладскогоЖурналаВЕТИСПроизводители
	|		ПО ВтТовары.ЗаписьСкладскогоЖурнала = ЗаписиСкладскогоЖурналаВЕТИСПроизводители.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗаписиСкладскогоЖурналаВЕТИС КАК ЗаписиСкладскогоЖурналаВЕТИС
	|		ПО ВтТовары.ЗаписьСкладскогоЖурнала = ЗаписиСкладскогоЖурналаВЕТИС.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО (ВЫБОР
	|				КОГДА ВтТовары.ЕстьУточнения
	|					ТОГДА ЕСТЬNULL(ВтУточнения.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	|				ИНАЧЕ ВтТовары.Номенклатура
	|			КОНЕЦ = СпрНоменклатура.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО (СпрНоменклатура.ВидНоменклатуры = ВидыНоменклатуры.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ВтТовары.ЕстьУточнения
	|			ТОГДА ЕСТЬNULL(ВтУточнения.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|		ИНАЧЕ ВтТовары.Характеристика
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВтТовары.ЕстьУточнения
	|			ТОГДА ЕСТЬNULL(ВтУточнения.Серия, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
	|		ИНАЧЕ ВтТовары.Серия
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВтТовары.ЕстьУточнения
	|			ТОГДА ЕСТЬNULL(ВтУточнения.ИдентификаторПартии, """""""")
	|		ИНАЧЕ ВтТовары.ИдентификаторПартии
	|	КОНЕЦ,
	|	ВтТовары.ЕстьУточнения,
	|	ВтТовары.НомерСтроки,
	|	ЕСТЬNULL(ВтУточнения.НомерСтроки, 0),
	|	ВтТовары.ЕстьУточнения
	|		И ВтУточнения.ИдентификаторСтроки ЕСТЬ NULL,
	|	ВтТовары.ЗаписьСкладскогоЖурнала,
	|	ВЫБОР
	|		КОГДА ВтТовары.ЕстьУточнения
	|			ТОГДА ЕСТЬNULL(ВтУточнения.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	|		ИНАЧЕ ВтТовары.Номенклатура
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВтТовары.ЕстьУточнения
	|			ТОГДА ЕСТЬNULL(ВтУточнения.СтатусУказанияСерий, 0)
	|		ИНАЧЕ ВтТовары.СтатусУказанияСерий
	|	КОНЕЦ,
	|	ЕСТЬNULL(ЗаписиСкладскогоЖурналаВЕТИС.ДатаПроизводстваНачалоПериода, ДАТАВРЕМЯ(1, 1, 1)),
	|	ЕСТЬNULL(ЗаписиСкладскогоЖурналаВЕТИС.СрокГодностиНачалоПериода, ДАТАВРЕМЯ(1, 1, 1)),
	|	ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьХарактеристики, ЛОЖЬ),
	|	ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьЗаписьСкладскогоЖурналаВЕТИССерии, ЛОЖЬ),
	|	ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьДатуПроизводстваСерии, ЛОЖЬ),
	|	ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьСрокГодностиСерии, ЛОЖЬ),
	|	ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьИдентификаторПартииВЕТИССерии, ЛОЖЬ),
	|	ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьПроизводителяВЕТИССерии, ЛОЖЬ),
	|	ЕСТЬNULL(ВидыНоменклатуры.АвтоматическиГенерироватьСерии, ЛОЖЬ),
	|	ВЫБОР
	|		КОГДА ВидыНоменклатуры.НастройкиСерийБерутсяИзДругогоВидаНоменклатуры
	|			ТОГДА ВидыНоменклатуры.ВладелецСерий
	|		ИНАЧЕ ВидыНоменклатуры.Ссылка
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиТовары КАК НомерСтрокиТовары,
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиУточнения КАК НомерСтрокиУточнения,
	|	""ОшибкаНетУточнения"" КАК ТипОшибки,
	|	ЛОЖЬ КАК ЭтоНомерСтрокиУточнений
	|ПОМЕСТИТЬ ВТСтрокиСОшибками
	|ИЗ
	|	ВТОбъединеннаяТаблицаТоваров КАК ВТОбъединеннаяТаблицаТоваров
	|ГДЕ
	|	ВТОбъединеннаяТаблицаТоваров.ОшибкаНетУточнения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиТовары,
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиУточнения,
	|	""ОшибкаНетНоменклатуры"",
	|	ВТОбъединеннаяТаблицаТоваров.ЕстьУточнения
	|ИЗ
	|	ВТОбъединеннаяТаблицаТоваров КАК ВТОбъединеннаяТаблицаТоваров
	|ГДЕ
	|	НЕ ВТОбъединеннаяТаблицаТоваров.ОшибкаНетУточнения
	|	И ВТОбъединеннаяТаблицаТоваров.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиТовары,
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиУточнения,
	|	""ОшибкаНеНужноГенерировать"",
	|	ВТОбъединеннаяТаблицаТоваров.ЕстьУточнения
	|ИЗ
	|	ВТОбъединеннаяТаблицаТоваров КАК ВТОбъединеннаяТаблицаТоваров
	|ГДЕ
	|	НЕ ВТОбъединеннаяТаблицаТоваров.ОшибкаНетУточнения
	|	И НЕ ВТОбъединеннаяТаблицаТоваров.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И ВТОбъединеннаяТаблицаТоваров.СтатусУказанияСерий <> 0
	|	И НЕ ВТОбъединеннаяТаблицаТоваров.НастройкаВидаАвтоматическиГенерироватьСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиТовары,
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиУточнения,
	|	""ОшибкаНетХарактеристики"",
	|	ВТОбъединеннаяТаблицаТоваров.ЕстьУточнения
	|ИЗ
	|	ВТОбъединеннаяТаблицаТоваров КАК ВТОбъединеннаяТаблицаТоваров
	|ГДЕ
	|	НЕ ВТОбъединеннаяТаблицаТоваров.ОшибкаНетУточнения
	|	И НЕ ВТОбъединеннаяТаблицаТоваров.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И ВТОбъединеннаяТаблицаТоваров.СтатусУказанияСерий <> 0
	|	И ВТОбъединеннаяТаблицаТоваров.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	И ВТОбъединеннаяТаблицаТоваров.НастройкаВидаИспользоватьХарактеристики
	|	И ВТОбъединеннаяТаблицаТоваров.НастройкаВидаАвтоматическиГенерироватьСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиТовары,
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиУточнения,
	|	""ОшибкаНетЗаписиСкладскогоЖурнала"",
	|	ВТОбъединеннаяТаблицаТоваров.ЕстьУточнения
	|ИЗ
	|	ВТОбъединеннаяТаблицаТоваров КАК ВТОбъединеннаяТаблицаТоваров
	|ГДЕ
	|	НЕ ВТОбъединеннаяТаблицаТоваров.ОшибкаНетУточнения
	|	И НЕ ВТОбъединеннаяТаблицаТоваров.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И ВТОбъединеннаяТаблицаТоваров.СтатусУказанияСерий <> 0
	|	И (ВТОбъединеннаяТаблицаТоваров.НастройкаВидаИспользоватьЗаписьСкладскогоЖурналаВЕТИССерии
	|			ИЛИ ВТОбъединеннаяТаблицаТоваров.НастройкаВидаИспользоватьДатуПроизводстваСерии
	|			ИЛИ ВТОбъединеннаяТаблицаТоваров.НастройкаВидаИспользоватьСрокГодностиСерии)
	|	И ВТОбъединеннаяТаблицаТоваров.ЗаписьСкладскогоЖурнала = ЗНАЧЕНИЕ(Справочник.ЗаписиСкладскогоЖурналаВЕТИС.ПустаяСсылка)
	|	И ВТОбъединеннаяТаблицаТоваров.НастройкаВидаАвтоматическиГенерироватьСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиТовары,
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиУточнения,
	|	""ОшибкаНетДатыПроизводства"",
	|	ВТОбъединеннаяТаблицаТоваров.ЕстьУточнения
	|ИЗ
	|	ВТОбъединеннаяТаблицаТоваров КАК ВТОбъединеннаяТаблицаТоваров
	|ГДЕ
	|	НЕ ВТОбъединеннаяТаблицаТоваров.ОшибкаНетУточнения
	|	И НЕ ВТОбъединеннаяТаблицаТоваров.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И ВТОбъединеннаяТаблицаТоваров.СтатусУказанияСерий <> 0
	|	И ВТОбъединеннаяТаблицаТоваров.ЗаписьСкладскогоЖурнала <> ЗНАЧЕНИЕ(Справочник.ЗаписиСкладскогоЖурналаВЕТИС.ПустаяСсылка)
	|	И ВТОбъединеннаяТаблицаТоваров.НастройкаВидаИспользоватьДатуПроизводстваСерии
	|	И ВТОбъединеннаяТаблицаТоваров.ДатаПроизводства = ДАТАВРЕМЯ(1, 1, 1)
	|	И ВТОбъединеннаяТаблицаТоваров.НастройкаВидаАвтоматическиГенерироватьСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиТовары,
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиУточнения,
	|	""ОшибкаНетГоденДо"",
	|	ВТОбъединеннаяТаблицаТоваров.ЕстьУточнения
	|ИЗ
	|	ВТОбъединеннаяТаблицаТоваров КАК ВТОбъединеннаяТаблицаТоваров
	|ГДЕ
	|	НЕ ВТОбъединеннаяТаблицаТоваров.ОшибкаНетУточнения
	|	И НЕ ВТОбъединеннаяТаблицаТоваров.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И ВТОбъединеннаяТаблицаТоваров.СтатусУказанияСерий <> 0
	|	И ВТОбъединеннаяТаблицаТоваров.ЗаписьСкладскогоЖурнала <> ЗНАЧЕНИЕ(Справочник.ЗаписиСкладскогоЖурналаВЕТИС.ПустаяСсылка)
	|	И ВТОбъединеннаяТаблицаТоваров.НастройкаВидаИспользоватьСрокГодностиСерии
	|	И ВТОбъединеннаяТаблицаТоваров.ГоденДо = ДАТАВРЕМЯ(1, 1, 1)
	|	И ВТОбъединеннаяТаблицаТоваров.НастройкаВидаАвтоматическиГенерироватьСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиТовары,
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиУточнения,
	|	""ОшибкаНетИдентификатораПартии"",
	|	ВТОбъединеннаяТаблицаТоваров.ЕстьУточнения
	|ИЗ
	|	ВТОбъединеннаяТаблицаТоваров КАК ВТОбъединеннаяТаблицаТоваров
	|ГДЕ
	|	НЕ ВТОбъединеннаяТаблицаТоваров.ОшибкаНетУточнения
	|	И НЕ ВТОбъединеннаяТаблицаТоваров.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И ВТОбъединеннаяТаблицаТоваров.СтатусУказанияСерий <> 0
	|	И ВТОбъединеннаяТаблицаТоваров.НастройкаВидаИспользоватьИдентификаторПартииВЕТИССерии
	|	И ВТОбъединеннаяТаблицаТоваров.ИдентификаторПартии = """"
	|	И ВТОбъединеннаяТаблицаТоваров.НастройкаВидаАвтоматическиГенерироватьСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиТовары,
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиУточнения,
	|	""ОшибкаНетПроизводителя"",
	|	ВТОбъединеннаяТаблицаТоваров.ЕстьУточнения
	|ИЗ
	|	ВТОбъединеннаяТаблицаТоваров КАК ВТОбъединеннаяТаблицаТоваров
	|ГДЕ
	|	НЕ ВТОбъединеннаяТаблицаТоваров.ОшибкаНетУточнения
	|	И НЕ ВТОбъединеннаяТаблицаТоваров.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И ВТОбъединеннаяТаблицаТоваров.СтатусУказанияСерий <> 0
	|	И ВТОбъединеннаяТаблицаТоваров.НастройкаВидаИспользоватьПроизводителяВЕТИССерии
	|	И ВТОбъединеннаяТаблицаТоваров.КоличествоПроизводителей = 0
	|	И ВТОбъединеннаяТаблицаТоваров.ЗаписьСкладскогоЖурнала <> ЗНАЧЕНИЕ(Справочник.ЗаписиСкладскогоЖурналаВЕТИС.ПустаяСсылка)
	|	И ВТОбъединеннаяТаблицаТоваров.НастройкаВидаАвтоматическиГенерироватьСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиТовары,
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиУточнения,
	|	""ОшибкаМногоПроизводителей"",
	|	ВТОбъединеннаяТаблицаТоваров.ЕстьУточнения
	|ИЗ
	|	ВТОбъединеннаяТаблицаТоваров КАК ВТОбъединеннаяТаблицаТоваров
	|ГДЕ
	|	НЕ ВТОбъединеннаяТаблицаТоваров.ОшибкаНетУточнения
	|	И НЕ ВТОбъединеннаяТаблицаТоваров.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И ВТОбъединеннаяТаблицаТоваров.СтатусУказанияСерий <> 0
	|	И ВТОбъединеннаяТаблицаТоваров.НастройкаВидаИспользоватьПроизводителяВЕТИССерии
	|	И ВТОбъединеннаяТаблицаТоваров.КоличествоПроизводителей > 1
	|	И ВТОбъединеннаяТаблицаТоваров.ЗаписьСкладскогоЖурнала <> ЗНАЧЕНИЕ(Справочник.ЗаписиСкладскогоЖурналаВЕТИС.ПустаяСсылка)
	|	И ВТОбъединеннаяТаблицаТоваров.НастройкаВидаАвтоматическиГенерироватьСерии
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтрокиТовары,
	|	НомерСтрокиУточнения
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиТовары КАК НомерСтрокиТовары,
	|	ВТОбъединеннаяТаблицаТоваров.НомерСтрокиУточнения КАК НомерСтрокиУточнения,
	|	ВТОбъединеннаяТаблицаТоваров.ЕстьУточнения КАК ЕстьУточнения,
	|	"""" КАК Номер,
	|	ВЫБОР
	|		КОГДА ВТОбъединеннаяТаблицаТоваров.НастройкаВидаИспользоватьЗаписьСкладскогоЖурналаВЕТИССерии
	|			ТОГДА ВТОбъединеннаяТаблицаТоваров.ЗаписьСкладскогоЖурнала
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ЗаписиСкладскогоЖурналаВЕТИС.ПустаяСсылка)
	|	КОНЕЦ КАК ЗаписьСкладскогоЖурналаВЕТИС,
	|	ВЫБОР
	|		КОГДА ВТОбъединеннаяТаблицаТоваров.НастройкаВидаИспользоватьПроизводителяВЕТИССерии
	|			ТОГДА ЗаписиСкладскогоЖурналаВЕТИСПроизводители.Производитель
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПредприятияВЕТИС.ПустаяСсылка)
	|	КОНЕЦ КАК ПроизводительВЕТИС,
	|	ВЫБОР
	|		КОГДА ВТОбъединеннаяТаблицаТоваров.НастройкаВидаИспользоватьИдентификаторПартииВЕТИССерии
	|			ТОГДА ВТОбъединеннаяТаблицаТоваров.ИдентификаторПартии
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ИдентификаторПартииВЕТИС,
	|	ВЫБОР
	|		КОГДА ВТОбъединеннаяТаблицаТоваров.НастройкаВидаИспользоватьСрокГодностиСерии
	|			ТОГДА ВТОбъединеннаяТаблицаТоваров.ГоденДо
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ГоденДо,
	|	ВЫБОР
	|		КОГДА ВТОбъединеннаяТаблицаТоваров.НастройкаВидаИспользоватьДатуПроизводстваСерии
	|			ТОГДА ВТОбъединеннаяТаблицаТоваров.ДатаПроизводства
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ДатаПроизводства,
	|	ВТОбъединеннаяТаблицаТоваров.ВладелецСерий КАК ВладелецСерий
	|ПОМЕСТИТЬ ВТРеквизитыСерий
	|ИЗ
	|	ВТОбъединеннаяТаблицаТоваров КАК ВТОбъединеннаяТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗаписиСкладскогоЖурналаВЕТИС.Производители КАК ЗаписиСкладскогоЖурналаВЕТИСПроизводители
	|			ПО ВТОбъединеннаяТаблицаТоваров.ЗаписьСкладскогоЖурнала = ЗаписиСкладскогоЖурналаВЕТИСПроизводители.Ссылка
	|			И (ВТОбъединеннаяТаблицаТоваров.НастройкаВидаИспользоватьПроизводителяВЕТИССерии)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиСОшибками КАК ВТСтрокиСОшибками
	|			ПО ВТОбъединеннаяТаблицаТоваров.НомерСтрокиТовары = ВТСтрокиСОшибками.НомерСтрокиТовары
	|			И ВТОбъединеннаяТаблицаТоваров.НомерСтрокиУточнения = ВТСтрокиСОшибками.НомерСтрокиУточнения
	|ГДЕ
	|	ВТСтрокиСОшибками.НомерСтрокиТовары ЕСТЬ NULL
	|	И ВТОбъединеннаяТаблицаТоваров.СтатусУказанияСерий <> 0
	|	И ВТОбъединеннаяТаблицаТоваров.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТРеквизитыСерий.НомерСтрокиТовары КАК НомерСтрокиТовары,
	|	ВТРеквизитыСерий.НомерСтрокиУточнения КАК НомерСтрокиУточнения,
	|	ВТРеквизитыСерий.ЕстьУточнения КАК ЕстьУточнения,
	|	ВТРеквизитыСерий.Номер КАК Номер,
	|	ВТРеквизитыСерий.ЗаписьСкладскогоЖурналаВЕТИС КАК ЗаписьСкладскогоЖурналаВЕТИС,
	|	ВТРеквизитыСерий.ПроизводительВЕТИС КАК ПроизводительВЕТИС,
	|	ВТРеквизитыСерий.ИдентификаторПартииВЕТИС КАК ИдентификаторПартииВЕТИС,
	|	ВТРеквизитыСерий.ГоденДо КАК ГоденДо,
	|	ВТРеквизитыСерий.ДатаПроизводства КАК ДатаПроизводства,
	|	ВТРеквизитыСерий.ВладелецСерий КАК ВидНоменклатуры,
	|	ЕСТЬNULL(СерииНоменклатуры.Ссылка, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)) КАК Серия
	|ИЗ
	|	ВТРеквизитыСерий КАК ВТРеквизитыСерий
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО ВТРеквизитыСерий.Номер = СерииНоменклатуры.Номер
	|			И ВТРеквизитыСерий.ЗаписьСкладскогоЖурналаВЕТИС = СерииНоменклатуры.ЗаписьСкладскогоЖурналаВЕТИС
	|			И ВТРеквизитыСерий.ПроизводительВЕТИС = СерииНоменклатуры.ПроизводительВЕТИС
	|			И ВТРеквизитыСерий.ИдентификаторПартииВЕТИС = СерииНоменклатуры.ИдентификаторПартииВЕТИС
	|			И ВТРеквизитыСерий.ГоденДо = СерииНоменклатуры.ГоденДо
	|			И ВТРеквизитыСерий.ДатаПроизводства = СерииНоменклатуры.ДатаПроизводства
	|			И ВТРеквизитыСерий.ВладелецСерий = СерииНоменклатуры.ВидНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСтрокиСОшибками.НомерСтрокиТовары КАК НомерСтрокиТовары,
	|	ВТСтрокиСОшибками.НомерСтрокиУточнения КАК НомерСтрокиУточнения,
	|	ВТСтрокиСОшибками.ТипОшибки КАК ТипОшибки,
	|	ВТСтрокиСОшибками.ЭтоНомерСтрокиУточнений КАК ЭтоНомерСтрокиУточнений
	|ИЗ
	|	ВТСтрокиСОшибками КАК ВТСтрокиСОшибками
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтрокиТовары,
	|	НомерСтрокиУточнения";
	
	Запрос.Текст = ТекстЗапроса;
	Если ТипЗнч(ТабличнаяЧастьТовары) = Тип("ДанныеФормыКоллекция") Тогда
		Запрос.УстановитьПараметр("ТаблицаТовары", ТабличнаяЧастьТовары.Выгрузить(ВыделенныеСтрокиТоваров));
	ИначеЕсли ТипЗнч(ТабличнаяЧастьТовары) = Тип("ТаблицаЗначений") Тогда
		Запрос.УстановитьПараметр("ТаблицаТовары", ТабличнаяЧастьТовары.Скопировать(ВыделенныеСтрокиТоваров));
	Иначе
		ВызватьИсключение НСтр("ru = 'Неправильный тип параметра ТабличнаяЧастьТовары метода ИнтеграцияВЕТИСУТ.ЗаполнитьСгенерироватьСерии'");
	КонецЕсли;
	
	Если ТабличнаяЧастьУточнения <> Неопределено Тогда
		Запрос.УстановитьПараметр("ТоварыУточнение", ТабличнаяЧастьУточнения.Выгрузить());
	КонецЕсли;
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ТаблицаОшибок         = РезультатыЗапроса[РезультатыЗапроса.ВГраница()].Выгрузить();
	ТаблицаУстановкиСерий = РезультатыЗапроса[РезультатыЗапроса.ВГраница()-1].Выгрузить();
	
	Результат.ЗаполнениеЗавершено = ТаблицаОшибок.Количество() = 0 И ТаблицаУстановкиСерий.Количество() > 0;
	
	Для Каждого СтрТабл из ТаблицаОшибок Цикл
		
		Если СтрТабл.ТипОшибки = "ОшибкаНетУточнения" Тогда
			ПолеОшибки = "Объект.Товары.НомерСтроки";
			
			ТекстСообщения = НСтр("ru = 'По строке %НомерСтроки% указано, что должны быть уточнения, но строки уточнения не введены.'");
			
		Иначе
			
			Если СтрТабл.ТипОшибки = "ОшибкаНетНоменклатуры" Тогда
				
				Если СтрТабл.ЭтоНомерСтрокиУточнений Тогда
					ПолеОшибки = "Объект.Товары.НомерСтроки";
					ТекстСообщения = НСтр("ru = 'В одной из строк уточнения по строке %НомерСтроки% не указана номенклатура.'");
				Иначе
					ПолеОшибки = "Объект.Товары.Номенклатура";
					ТекстСообщения = НСтр("ru = 'В строке %НомерСтроки% не указана номенклатура.'");
				КонецЕсли;
				
			ИначеЕсли СтрТабл.ТипОшибки = "ОшибкаНеНужноГенерировать" Тогда
				
				Если СтрТабл.ЭтоНомерСтрокиУточнений Тогда
					ПолеОшибки = "Объект.Товары.НомерСтроки";
					ТекстСообщения = НСтр("ru = 'В одной из строк уточнения по строке %НомерСтроки% выбрана номенклатура, по которой не настроена автоматическая генерация серий.'");
				Иначе
					ПолеОшибки = "Объект.Товары.Номенклатура";
					ТекстСообщения = НСтр("ru = 'В строке %НомерСтроки% выбрана номенклатура, по которой не настроена автоматическая генерация серий'");
				КонецЕсли;
				
			ИначеЕсли СтрТабл.ТипОшибки = "ОшибкаНетХарактеристики" Тогда
				
				Если СтрТабл.ЭтоНомерСтрокиУточнений Тогда
					ПолеОшибки = "Объект.Товары.НомерСтроки";
					ТекстСообщения = НСтр("ru = 'В одной из строк уточнения по строке %НомерСтроки% не указана характеристика - серии нужно генерировать после указания характеристики.'");
				Иначе
					ПолеОшибки = "Объект.Товары.Характеристика";
					ТекстСообщения = НСтр("ru = 'В строке %НомерСтроки% не указана характеристика - серии нужно генерировать после указания характеристики.'");
				КонецЕсли;
				
			ИначеЕсли СтрТабл.ТипОшибки = "ОшибкаНетЗаписиСкладскогоЖурнала" Тогда
				
				ПолеОшибки = "Объект.Товары.ЗаписьСкладскогоЖурнала";
				ТекстСообщения = НСтр("ru = 'В строке %НомерСтроки% не указана запись складского журнала ВетИС - серии нужно генерировать после загрузки записи складского журнала из ВетИС.'");

			ИначеЕсли СтрТабл.ТипОшибки = "ОшибкаНетДатыПроизводства" Тогда
				
				ПолеОшибки = "Объект.Товары.ЗаписьСкладскогоЖурнала";
				ТекстСообщения = НСтр("ru = 'В строке %НомерСтроки% не указана запись складского журнала ВетИС, в которой не заполнена дата начала производства. Создайте серию вручную.'");
				
			ИначеЕсли СтрТабл.ТипОшибки = "ОшибкаНетГоденДо" Тогда
				
				ПолеОшибки = "Объект.Товары.ЗаписьСкладскогоЖурнала";
				ТекстСообщения = НСтр("ru = 'В строке %НомерСтроки% указана запись складского журнала ВетИС, в которой не заполнена дата начала периода срока годности. Создайте серию вручную.'");
				
			ИначеЕсли СтрТабл.ТипОшибки = "ОшибкаНетПроизводителя" Тогда
				
				ПолеОшибки = "Объект.Товары.ЗаписьСкладскогоЖурнала";
				ТекстСообщения = НСтр("ru = 'В строке %НомерСтроки% указана запись складского журнала ВетИС, в которой не заполнен производитель. Создайте серию вручную.'");
				
			ИначеЕсли СтрТабл.ТипОшибки = "ОшибкаМногоПроизводителей" Тогда
				
				ПолеОшибки = "Объект.Товары.ЗаписьСкладскогоЖурнала";
				ТекстСообщения = НСтр("ru = 'В строке %НомерСтроки% указана запись складского журнала ВетИС, в которой указано более одного производителя. Создайте серию вручную.'");
				
			ИначеЕсли СтрТабл.ТипОшибки = "ОшибкаНетИдентификатораПартии" Тогда
				
				Если СтрТабл.ЭтоНомерСтрокиУточнений Тогда
					ПолеОшибки = "Объект.Товары.НомерСтроки";
					ТекстСообщения = НСтр("ru = 'В одной из строк уточнения по строке %НомерСтроки% не указан идентификатор партии ВетИС. Создайте серию вручную.'");
				Иначе
					ПолеОшибки = "Объект.Товары.ИдентификаторПартии";
					ТекстСообщения = НСтр("ru = 'В строке %НомерСтроки% не указан идентификатор партии ВетИС. Создайте серию вручную.'");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
			
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", СтрТабл.НомерСтрокиТовары);
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Результат.СписокОшибок, ПолеОшибки, ТекстСообщения, Неопределено, СтрТабл.НомерСтрокиТовары - 1);
	КонецЦикла;
	
	СозданныеСерии = Новый ТаблицаЗначений;
	СозданныеСерии.Колонки.Добавить("Серия", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	
	СозданныеСерии.Колонки.Добавить("ВидНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.ВидыНоменклатуры"));
	СозданныеСерии.Колонки.Добавить("ЗаписьСкладскогоЖурналаВЕТИС", Новый ОписаниеТипов("СправочникСсылка.ЗаписиСкладскогоЖурналаВЕТИС"));
	СозданныеСерии.Колонки.Добавить("ПроизводительВЕТИС", Новый ОписаниеТипов("СправочникСсылка.ПредприятияВЕТИС"));
	СозданныеСерии.Колонки.Добавить("ИдентификаторПартииВЕТИС", Новый ОписаниеТипов("Строка"));
	СозданныеСерии.Колонки.Добавить("ГоденДо", Новый ОписаниеТипов("Дата"));
	СозданныеСерии.Колонки.Добавить("ДатаПроизводства", Новый ОписаниеТипов("Дата"));
	
	ПоляПоиска = "ЗаписьСкладскогоЖурналаВЕТИС,ПроизводительВЕТИС,ИдентификаторПартииВЕТИС,ГоденДо,ДатаПроизводства";
	
	СозданныеСерии.Индексы.Добавить(ПоляПоиска);
	
	СтруктураОтбора = Новый Структура(ПоляПоиска);
	
	Для Каждого СтрТабл из ТаблицаУстановкиСерий Цикл
		
		Если ЗначениеЗаполнено(СтрТабл.Серия) Тогда
			Серия = СтрТабл.Серия;
		Иначе
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрТабл);
			НайденныеСтроки = СозданныеСерии.НайтиСтроки(СтруктураОтбора);
			
			Если НайденныеСтроки.Количество() > 0 Тогда
				Серия = НайденныеСтроки[0].Серия;
			Иначе
				
				Серия = СоздатьСерию(СтрТабл, Результат);
				
				Если Не ЗначениеЗаполнено(Серия) Тогда
					Результат.ЗаполнениеЗавершено = Ложь;
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = СозданныеСерии.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрТабл);
				НоваяСтрока.Серия = Серия;
				
			КонецЕсли;
			
		КонецЕсли;
		
		СтруктураПоиска = Новый Структура();
		
		Если СтрТабл.ЕстьУточнения Тогда
			СтруктураПоиска.Вставить("НомерСтроки", СтрТабл.НомерСтрокиУточнения);
			НайденныеСтроки = ТабличнаяЧастьУточнения.НайтиСтроки(СтруктураПоиска);
		Иначе
			СтруктураПоиска.Вставить("НомерСтроки", СтрТабл.НомерСтрокиТовары);
			НайденныеСтроки = ТабличнаяЧастьТовары.НайтиСтроки(СтруктураПоиска);
		КонецЕсли;
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			Результат.ЗаполнениеЗавершено = Ложь;
		Иначе
			СтрокаТЧ = НайденныеСтроки[0];
			СтрокаТЧ.Серия = Серия;
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТЧ, СтруктураДействий, Неопределено);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция СоздатьСерию(Реквизиты, Результат)
	
	СерияСсылка = Справочники.СерииНоменклатуры.ПустаяСсылка();
	
	Попытка
		
		НоваяСерия = Справочники.СерииНоменклатуры.СоздатьЭлемент();
		НоваяСерия.Заполнить(Реквизиты);
		НоваяСерия.Записать();
		
		СерияСсылка = НоваяСерия.Ссылка;
		
	Исключение
		ПолеОшибки = "Объект.Товары.Серия";
		ТекстСообщения = НСтр("ru = 'Не удалось сгенерировать серию по причине: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Результат.СписокОшибок, ПолеОшибки, ТекстСообщения,
			Неопределено);
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ВетИС'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Справочники.СерииНоменклатуры,
			НоваяСерия.Ссылка,
			ТекстСообщения);
	КонецПопытки;
		
	Возврат СерияСсылка;
	
КонецФункции

Функция ТорговыйОбъектЭтоСклад(ТорговыйОбъект)
	
	Если ЗначениеЗаполнено(ТорговыйОбъект)
		И ТипЗнч(ТорговыйОбъект) = Тип("СправочникСсылка.Склады") Тогда
		МетаТорговыйОбъект = ТорговыйОбъект.Метаданные();
		
		Если НЕ МетаТорговыйОбъект.Иерархический Тогда
			Возврат Истина;
		ИначеЕсли МетаТорговыйОбъект.ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияЭлементов Тогда
			Возврат Истина;
		Иначе
			Возврат Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТорговыйОбъект,"ЭтоГруппа");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область ИдентификаторыПартий

// Устанавливает служебный признак необходимости заполнения идентификатора партии в строке табличной части объекта.
//
// Параметры:
// ТабличнаяЧастьТовары - ДанныеФормыКоллекция - Товарная табличная часть объекта.
//
Процедура ЗаполнитьИспользованиеИдентификаторовПартий(ТабличнаяЧастьТовары) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапросаИспользованиеИдентификаторовПартийТоваров();
	Запрос.УстановитьПараметр("ТабличнаяЧастьТовары", ТабличнаяЧастьТовары.Выгрузить(, "НомерСтроки,Номенклатура,СтатусУказанияСерий"));
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Для Каждого СтрокаТовары Из ТабличнаяЧастьТовары Цикл
		Выборка.Сбросить();
		Если Выборка.НайтиСледующий(СтрокаТовары.НомерСтроки, "НомерСтроки") Тогда
			СтрокаТовары.ИспользоватьИдентификаторПартии = Выборка.ИспользоватьИдентификаторПартии;
		Иначе
			СтрокаТовары.ИспользоватьИдентификаторПартии = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает текст запроса с данными для определения необходимости указания идентификатора партии.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаИспользованиеИдентификаторовПартийТоваров()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТабличнаяЧастьТовары.НомерСтроки         КАК НомерСтроки,
	|	ТабличнаяЧастьТовары.Номенклатура        КАК Номенклатура,
	|	ТабличнаяЧастьТовары.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ
	|	ТаблицаТовары
	|ИЗ
	|	&ТабличнаяЧастьТовары КАК ТабличнаяЧастьТовары
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки  КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерий = 0
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ СправочникВидыНоменклатуры.ИспользоватьСерии И СправочникВидыНоменклатуры.ИспользоватьИдентификаторПартииВЕТИССерии
	|	КОНЕЦ КАК ИспользоватьИдентификаторПартии
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК СправочникВидыНоменклатуры
	|		ПО СправочникНоменклатура.ВидНоменклатуры = СправочникВидыНоменклатуры.Ссылка
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти


#КонецОбласти

#КонецОбласти
