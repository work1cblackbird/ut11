///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "Облачный архива".
// ОбщийМодуль.ОблачныйАрхив20Глобальный.
//
// Глобальные клиентские процедуры и функции облачного архива:
//  - контроль приближения автоматического резервного копирования;
//  - обновление времени автоматического резервного копирования.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

// Выполняет завершение текущего сеанса при наступлении времени для автоматического резервного копирования.
//
Процедура ОблачныйАрхив20_КонтрольПриближенияАвтоматическогоРезервногоКопирования() Экспорт
	
	ДатаОбновленияНастроек    = ИнтернетПоддержкаПользователейКлиент.ЗначениеПараметраПриложения(
		ОблачныйАрхив20Клиент.ИмяПараметраПриложенияДатаОбновленияНастроек());	// Дата
	АвтоАрхивированиеВключено = ИнтернетПоддержкаПользователейКлиент.ЗначениеПараметраПриложения(
		ОблачныйАрхив20Клиент.ИмяПараметраПриложенияАвтоАрхивированиеВключено());	// Булево
		
	РезультатКонтроля = ОблачныйАрхив20ВызовСервера.ВыполнитьКонтрольНеобходимостиВыполнитьОбновлениеВремени(
		ДатаОбновленияНастроек,
		АвтоАрхивированиеВключено);
	
	Если РезультатКонтроля.АвтоАрхивированиеВключено
		И Не РезультатКонтроля.НастройкиИзменены Тогда
		
		ОблачныйАрхив20Клиент.ПодключитьОбработчикКонтроляПриближенияРезервногоКопирования(
			РезультатКонтроля.ЗавершатьРаботуПользователей);
		
	Иначе
		
		ОтключитьОбработчикОжидания("ОблачныйАрхив20_КонтрольПриближенияАвтоматическогоРезервногоКопирования");
		ОтключитьОбработчикОжидания("ОблачныйАрхив20_ОбновитьВремяДоАвтоматическогоРезервногоКопирования");
		
		ОблачныйАрхив20_ОбновитьВремяДоАвтоматическогоРезервногоКопирования();
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет запуск длительной операции по обновлению времени до автоматического резервного копирования.
//
Процедура ОблачныйАрхив20_ОбновитьВремяДоАвтоматическогоРезервногоКопирования() Экспорт
	
	ДлительнаяОперация    = ОблачныйАрхив20ВызовСервера.ОбновитьВремяДоАвтоматическогоРезервногоКопированияВФоне();
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ОбновитьВремяДоАвтоматическогоРезервногоКопирования",
		ОблачныйАрхив20Клиент,
		ДлительнаяОперация.ДополнительныеПараметры);
	
	Если ДлительнаяОперация.ДлительнаяОперацияЗапущена Тогда
		
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, НастройкиОжидания);
		
	Иначе
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет контроль изменения параметров резервного копирования и запуск процесса обновления времени до резервного
// копирования.
//
Процедура ОблачныйАрхив20_КонтрольИзмененияПараметровРезервногоКопирования() Экспорт
	
	ДатаОбновленияНастроек    = ИнтернетПоддержкаПользователейКлиент.ЗначениеПараметраПриложения(
		ОблачныйАрхив20Клиент.ИмяПараметраПриложенияДатаОбновленияНастроек());	// Дата
	АвтоАрхивированиеВключено = ИнтернетПоддержкаПользователейКлиент.ЗначениеПараметраПриложения(
		ОблачныйАрхив20Клиент.ИмяПараметраПриложенияАвтоАрхивированиеВключено());	// Булево
		
	РезультатКонтроля = ОблачныйАрхив20ВызовСервера.ВыполнитьКонтрольНеобходимостиВыполнитьОбновлениеВремени(
		ДатаОбновленияНастроек,
		АвтоАрхивированиеВключено);
	
	Если Не АвтоАрхивированиеВключено
		И РезультатКонтроля.АвтоАрхивированиеВключено Тогда
		
		ИнтернетПоддержкаПользователейКлиент.УстановитьЗначениеПараметраПриложения(
			ОблачныйАрхив20Клиент.ИмяПараметраПриложенияАвтоАрхивированиеВключено(),
			Истина);
		
	КонецЕсли;
	
	Если РезультатКонтроля.НастройкиИзменены
		И РезультатКонтроля.АвтоАрхивированиеВключено Тогда
		
		ОтключитьОбработчикОжидания("ОблачныйАрхив20_КонтрольПриближенияАвтоматическогоРезервногоКопирования");
		ОтключитьОбработчикОжидания("ОблачныйАрхив20_ОбновитьВремяДоАвтоматическогоРезервногоКопирования");
		
		ОблачныйАрхив20_ОбновитьВремяДоАвтоматическогоРезервногоКопирования();
		
	Иначе
		
		Если Не РезультатКонтроля.АвтоАрхивированиеВключено Тогда
			
			ОтключитьОбработчикОжидания("ОблачныйАрхив20_КонтрольПриближенияАвтоматическогоРезервногоКопирования");
			ОтключитьОбработчикОжидания("ОблачныйАрхив20_ОбновитьВремяДоАвтоматическогоРезервногоКопирования");
			
		КонецЕсли;
		
		ПодключитьОбработчикОжидания("ОблачныйАрхив20_КонтрольИзмененияПараметровРезервногоКопирования",43200, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти