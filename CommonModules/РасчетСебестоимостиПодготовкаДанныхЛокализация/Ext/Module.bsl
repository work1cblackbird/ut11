
#Область ПрограммныйИнтерфейс

// Этап подготовки к расчету
//
// Параметры:
//	ПараметрыРасчета - Структура - параметры расчета себестоимости
//
Процедура ПодготовкаИсходныхДанныхКРасчету(ПараметрыРасчета) Экспорт

	//++ Локализация
	
	// Скорректируем начальные остатки регистров накопления.
	КорректировкаНачальныхОстатковРегистров(ПараметрыРасчета);
	
	// При необходимости заполним служебный реквизит РасчетПартий:
	// если расчет текущего периода ранее был выполнен в партионном учете версии 2.1, то этот реквизит не заполнялся.
	РасчетСебестоимостиПартионныйУчет21.ЗаполнитьРеквизитРасчетПартийВДвижениях(ПараметрыРасчета);
	
	// Очистим данные партионного учета версии 2.1 в рассчитываемом периоде.
	РасчетСебестоимостиПартионныйУчет21.ОчиститьНеиспользуемыеДанныеПартионногоУчетаВерсии21(ПараметрыРасчета);
	//-- Локализация
	
КонецПроцедуры

#Область АнализСостоянияСистемы

// Заполняет проверки, выполняемые в рамках расчета партий и себестоимости.
//
// Параметры:
//	ТаблицаПроверок - см. АудитСостоянияСистемыПереопределяемый.ЗаполнитьПроверкиДляРегистрации.
//
Процедура ЗаполнитьПроверкиДляРегистрации(ТаблицаПроверок) Экспорт
	//++ Локализация
	
	// Переход на партионный учет версии 2.2.
	ОписаниеПроверки = ЗакрытиеМесяцаСервер.ДобавитьОписаниеНовойПроверки(ТаблицаПроверок,
		"КорректностьПереходаНаПУ22",
		Перечисления.ОперацииЗакрытияМесяца.РасчетПартийИСебестоимости,
		Перечисления.МоментЗапускаПроверкиОперацииЗакрытияМесяца.ПослеРасчета,
		"РасчетСебестоимостиПодготовкаДанныхЛокализация.ПроверкаКорректностиПереходаНаПартионныйУчетВерсии22",
		Перечисления.ВажностьПроблемыУчета.Ошибка);
	
	ЗакрытиеМесяцаСервер.ЗаполнитьПредставлениеНовойПроверки(ОписаниеПроверки,
		НСтр("ru='Корректность перехода на партионный учет версии 2.2'", ОбщегоНазначения.КодОсновногоЯзыка()),
		НСтр("ru='Проверяется корректность движений по регистрам, сформированных при переходе на партионный учет версии 2.2:
			|движения вида ""Перенос данных"" должны быть сформированы на конец месяца, предшествующего переходу.'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	РасчетСебестоимостиНДС.ЗаполнитьПроверкиДляРегистрации(ТаблицаПроверок);	
	//-- Локализация
КонецПроцедуры

#КонецОбласти

// Исправляет проблемы в движениях документов и в самих документах в рассчитываемом периоде.
//
// Есть ошибки, которые "портят" исходные данные для расчета,
// но для исправления их последствий не пишется обработчик обновления.
// Например, из-за какой-то старой ошибки в каких-то случаях движения документа были некорректны.
// - если для исправления этой ошибки написать обработчик обновления,
// то он изменит данные прошлых, "закрытых" периодов, из-за чего эти периоды перестанут быть "закрытыми".
// Придется повторно закрывать эти периоды, из-за чего в них может измениться себестоимость и, как следствие,
// регламентированная отчетность. Это плохо, т.к. по этим периодам отчетность уже может быть сдана.
// - с другой стороны, в тех периодах, которые пользователь будет пересчитывать, эту ошибку в движениях надо бы исправить,
// чтобы в этих пересчитанных периодах себестоимость основывалась на правильных исходных данных.
//
// Для исправления таких ошибок и предназначена данная процедура.
// По сути, она является аналогом обработчика обновления, исправляющего движения документов в периоде, указанном пользователем.
//
// Также эта процедура используется для корректировки движений периода при его перерасчете в партионном учете версии 2.2.
//
Процедура ИсправитьПроблемыВДвиженияхИДокументах(ПараметрыРасчета) Экспорт
	//++ Локализация
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	#Область ПоляПартионногоУчетаВерсии22
	
	// Формирование ВТДвиженияСебестоимостьТоваров должно происходить в этом запросе заново, чтобы при повторном выполнении
	// запроса после исправления ошибок были видны изменения в движениях по регистру.
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(Запрос,
		"ВТДвиженияСебестоимостьТоваров, ВТДокументыИсточникиИзСебестоимости, ВТДокументыИсточникиИзСебестоимостиСДатами,
		|ВТДокументыИсточникиИзСебестоимостиСДатамиИПериодамиПолитик, ВТУчетныеПолитикиДокументовИсточников");
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.ИмяРегистра 			КАК ИмяРегистра,
	|	Т.Ссылка 	  			КАК Ссылка,
	|	Т.Организация 			КАК Организация,
	|	Т.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Т.КодОшибки   			КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ (
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"" КАК ИмяРегистра,
	|		Т.Регистратор 			 КАК Ссылка,
	|		Т.Организация			 КАК Организация,
	|		НЕОПРЕДЕЛЕНО 			 КАК ХозяйственнаяОперация,
	|		20						 КАК КодОшибки
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровМеждуОрганизациями КАК Возврат
	|			ПО Возврат.Ссылка = Т.Регистратор
	|			И Возврат.Организация = Т.Организация
	|	ГДЕ
	|		Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровМеждуОрганизациями)
	|		И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление)
	|		И Т.КорВидДеятельностиНДС = Возврат.НалогообложениеНДС
	|		И Возврат.НалогообложениеНДС В (&ТипыНалогообложенияНДСНеУчитываетсяВСтоимости)
	|		И Т.Организация В (&ОрганизацииНаУСН)
	|		И Возврат.ОрганизацияПолучатель НЕ В (&ОрганизацииНаУСН)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		21
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК Приобретение
	|			ПО Приобретение.Ссылка = Т.ДокументИсточник
	|	ГДЕ
	|		Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|		И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаСтоимости)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ЗаявлениеОВвозеТоваров)
	|		И Приобретение.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки)
	|	) КАК Т
	|
	|ГДЕ
	|	Т.КодОшибки <> 0";
	
	Запрос.Текст = РасчетСебестоимостиПодготовкаДанных.ТекстЗапросаВТДвиженияСебестоимостьТоваров() + ТекстЗапроса;
	
	// Сформируем описание возможных кодов ошибок в движениях.
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(20, НСтр("ru='Не корректно заполнен кор. вид деятельности НДС для операции ""Возврат товаров между организациями"" для организации на УСН'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(21, НСтр("ru='Не корректно заполнен тип записи в движениях документа ""Заявление о ввозе товаров из ЕАЭС""'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	ДополнительныеПоля = Новый Структура("ХозяйственнаяОперация",
		НСтр("ru = 'Хозяйственная операция'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='переход на партионный учет версии 2.2'", ОбщегоНазначения.КодОсновногоЯзыка()),
		ДополнительныеПоля);
	
	#КонецОбласти
	
	#Область НДСПредъявленный
	
	// Исправление некорректного НДСУпр в регистре НДСПредъявленный.
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Различается заполненность ресурсов НДС (упр.) и НДС (регл.) в регистре НДСПредъявленный'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru='При отключенном управленческом учете заполнен ресурс НДС (упр.) в регистре НДСПредъявленный'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""НДСПредъявленный"" 	 КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютОрганизаций КАК Курсы
	|		ПО Т.Организация = Курсы.Организация
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РегламентнаяОперация
	|	И &УправленческийУчетОрганизаций
	|	И ((Т.НДС = 0 И Т.НДСУпр <> 0)
	|		ИЛИ (Т.НДС <> 0 И Т.НДСУпр = 0 И ВЫРАЗИТЬ(Т.НДС * Курсы.КоэффициентВалютыРегл КАК ЧИСЛО(31,2)) <> 0))
	|	И (ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.СчетФактураНалоговыйАгент)
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.ПустаяСсылка)
	|		ИЛИ ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.СчетФактураНалоговыйАгент))
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.СписаниеНДСНаРасходы)
	|	И НЕ Т.Сторно
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""НДСПредъявленный"" 	 КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	2 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РегламентнаяОперация
	|	И НЕ &УправленческийУчетОрганизаций
	|	И Т.НДСУпр <> 0
	|	И (ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.СчетФактураНалоговыйАгент)
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.ПустаяСсылка)
	|		ИЛИ ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.СчетФактураНалоговыйАгент))
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.СписаниеНДСНаРасходы)
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='некорректное заполнение НДС (упр.)'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти

	#Область СчетФактураПолученныйНалоговыйАгент
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Отсутствуют движения документа ""Счет-фактура полученный (налоговый агент)"" по регистру ""Прочие расходы""'"));
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	""ПрочиеРасходы"" 		 КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ИСТИНА КАК ЕстьПрочиеРасходы,
	|		Т.Регистратор,
	|		Т.Организация
	|	ИЗ
	|		РегистрНакопления.ПрочиеРасходы КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученныйНалоговыйАгент КАК Отбор
	|			ПО Т.Регистратор = Отбор.Ссылка
	|	ГДЕ
	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Т.Организация В (&МассивОрганизаций)
	|		И Т.Активность
	|		И НЕ Т.РасчетПартий
	|		И НЕ Т.РасчетСебестоимости
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ЛОЖЬ КАК ЕстьПрочиеРасходы,
	|		Т.Регистратор,
	|		Т.Организация
	|	ИЗ
	|		РегистрНакопления.ПартииПрочихРасходов КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученныйНалоговыйАгент КАК Отбор
	|			ПО Т.Регистратор = Отбор.Ссылка
	|	ГДЕ
	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Т.Организация В (&МассивОрганизаций)
	|		И Т.Активность
	|		И НЕ Т.РасчетПартий
	|		И НЕ Т.РасчетСебестоимости
	|	) КАК Т
	|СГРУППИРОВАТЬ ПО
	|	Т.Регистратор,
	|	Т.Организация
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(Т.ЕстьПрочиеРасходы) = ЛОЖЬ
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'отсутствуют движения по регистру ""Прочие расходы""'"));
	
	#КонецОбласти

	#Область ОприходованиеЗаСчетРасходов
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru='Некорректные движения документа ""Оприходование (за счет расходов)"" по регистру ""Прочие расходы""'"));
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПрочиеРасходы"" 		 КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОприходованиеЗаСчетРасходов)
	|	И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru='некорректные движения по регистру ""Прочие расходы""'"));
	
	#КонецОбласти

	#Область ДетализацияПартийТоваровДляНДСиУСН2_4
	
	// Удаление движений у документа ввода остатков без признака ОтражатьСебестоимость.
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Некорректные движения по регистру ""Детализация партий товаров для НДС и УСН (ФИФО скользящая оценка)""'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Ссылка				КАК Ссылка,
	|	Т.Организация			КАК Организация,
	|	1						КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыДляПерепроведения
	|ИЗ
	|	Документ.ВводОстатков КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДетализацияПартийТоваровДляНДСиУСН2_4 КАК Движения
	|	ПО Т.Ссылка = Движения.Регистратор
	|
	|ГДЕ
	|	Т.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И НЕ Т.ОтражатьСебестоимость
	|	И Т.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Ссылка				КАК Ссылка,
	|	Т.Организация			КАК Организация,
	|	1						КАК КодОшибки
	|ИЗ
	|	Документ.ВводОстатковТоваров КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДетализацияПартийТоваровДляНДСиУСН2_4 КАК Движения
	|	ПО Т.Ссылка = Движения.Регистратор
	|
	|ГДЕ
	|	Т.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И НЕ Т.ОтражатьСебестоимость
	|	И Т.Проведен
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументы(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'исправление некорректных движений документа ввода остатков'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти
	

	//-- Локализация
КонецПроцедуры

// Дополняет массив типами документов с "нестандартной" логикой формирования движений.
//
// Параметры:
//	ТипыРегистраторов - Массив - массив типов регистраторов
//
Процедура ДополнитьТипыРегистраторовСНестандартнойЛогикойФормированияДвижений(ТипыРегистраторов) Экспорт
	//++ Локализация
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.РаспределениеНДС"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.РаспределениеРасходовБудущихПериодов"));

	//-- Локализация
КонецПроцедуры

#КонецОбласти

//++ Локализация
#Область СлужебныеПроцедурыИФункции

#Область ПроцедурыЭтапа_ПодготовкаКРасчету

#Область КорректировкиНачальныхОстатковРегистров

// Выполняет все необходимые корректировки обслуживаемых регистров.
//
Процедура КорректировкаНачальныхОстатковРегистров(ПараметрыРасчета)
	
	КорректировкаНачальныхОстатковРегистраСебестоимостьТоваров(ПараметрыРасчета);
	КорректировкаНачальныхОстатковРегистровДетализацииПартийНДС(ПараметрыРасчета);
	КорректировкаНачальныхОстатковРегистраНДСПредъявленный(ПараметрыРасчета);
	КорректировкаНачальныхОстатковРегистраПартииПрочихРасходов(ПараметрыРасчета);
	
КонецПроцедуры

#Область СебестоимостьТоваров

// Выполняет все необходимые корректировки регистра СебестоимостьТоваров.
//
Процедура КорректировкаНачальныхОстатковРегистраСебестоимостьТоваров(ПараметрыРасчета)
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "КорректировкаНачальныхОстатковРегистраСебестоимостьТоваров");
	
	ПараметрыКорректировки = РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		Метаданные.РегистрыНакопления.СебестоимостьТоваров.ПолноеИмя());
	
	Если ПараметрыРасчета.ФормироватьНачальныеОстаткиПартий22 Тогда
		СформироватьНачальныеОстаткиПартийСебестоимостьТоваров(ПараметрыРасчета, ПараметрыКорректировки);
	КонецЕсли;
	
	КорректировкаВключитьУчетПоВидамЗапасовСебестоимостьТоваров(ПараметрыРасчета, ПараметрыКорректировки);
	
	Если РасчетСебестоимостиПовтИсп.СебестоимостьТоваровПоНазначениям(ПараметрыРасчета.РасчетныйПериод.НачалоПериода) Тогда
		Если РасчетСебестоимостиПовтИсп.ДатаВключенияОбособленногоУчетаСебестоимостиПоНазначениям() = ПараметрыРасчета.РасчетныйПериод.НачалоПериода Тогда
			КорректировкаВключитьУчетПоНазначениямСебестоимостьТоваров(ПараметрыРасчета, ПараметрыКорректировки);
		КонецЕсли;
	Иначе
		КорректировкаОтключитьУчетПоНазначениямСебестоимостьТоваров(ПараметрыРасчета, ПараметрыКорректировки);
	КонецЕсли;
		
	КорректировкаЗаменитьПартнераНаДоговорВАналитикеУчетаНоменклатурыСебестоимостьТоваров(ПараметрыРасчета, ПараметрыКорректировки);
	
	Если ПараметрыРасчета.УправленческийУчетОрганизаций Тогда
		Если РасчетСебестоимостиПовтИсп.ДатаНачалаВеденияУправленческогоУчетаОрганизаций() = ПараметрыРасчета.РасчетныйПериод.НачалоПериода Тогда
			КорректировкаЗаполнитьСуммыУпрУчетаСебестоимостьТоваров(ПараметрыРасчета, ПараметрыКорректировки);
		КонецЕсли;
	Иначе
		КорректировкаОчиститьСуммыУпрУчетаСебестоимостьТоваров(ПараметрыРасчета, ПараметрыКорректировки);
	КонецЕсли;
	
	СформироватьКорректировочныеДвиженияСебестоимостьПриИзмененииУчетнойПолитики(ПараметрыРасчета, ПараметрыКорректировки);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗавершитьКорректировкуОстатковРегистра(ПараметрыРасчета, ПараметрыКорректировки);
	
КонецПроцедуры

// При расчете первого месяца в партионном учете версии 2.2 формирует начальные остатки партий в регистре себестоимости товаров.
//
Процедура СформироватьНачальныеОстаткиПартийСебестоимостьТоваров(ПараметрыРасчета, ПараметрыКорректировки)
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		Перечисления.ТипыЗаписейПартий.СлужебноеПереходНаПартионныйУчет22);
	
	Если НЕ ПараметрыКорректировки.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	Запрос.УстановитьПараметр("ОрганизацииДляКорректировки", ПараметрыКорректировки.ОрганизацииДляКорректировки);
	Запрос.УстановитьПараметр("СторнироватьОстаткиСебестоимости",
		ПараметрыРасчета.НачальныеОстатки.ВзятьОстаткиСебестоимостиИзРегистровПартий);
	Запрос.УстановитьПараметр("РаспределятьРасхожденияВСуммах",
		ПараметрыРасчета.НачальныеОстатки.РаспределятьРасхожденияВСуммахПартийИСебестоимости);
	
	#Область ВыборкаОстатков
	
	// Остатки партий для организаций с "ФИФО скользящая" получим из партионных регистров.
	// По остальным организациям партий нет - просто заполним в остатках новые измерения ВидДеятельностиНДС и АналитикаФинансовогоУчета.
	// Остатки регистра себестоимости находятся в таблице ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров.
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.ДокументПоступления,
	|	Т.АналитикаУчетаПартий,
	|	Т.КоличествоОстаток,
	|	Т.СтоимостьОстаток,
	|	Т.СтоимостьБезНДСОстаток,
	|	Т.ПостояннаяРазницаОстаток,
	|	Т.ВременнаяРазницаОстаток,
	|	ВЫБОР
	|		КОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|			 ТОГДА Т.СтоимостьРеглОстаток + Т.НДСРеглОстаток
	|		ИНАЧЕ Т.СтоимостьРеглОстаток
	|	КОНЕЦ КАК СтоимостьРеглОстаток,
	|	ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций ТОГДА 0
	|		КОГДА &ВалютыУпрИРеглУчетаСовпадают ТОГДА 
	|			ВЫБОР
	|				КОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|			 	ТОГДА Т.СтоимостьРеглОстаток + Т.НДСРеглОстаток
	|				ИНАЧЕ Т.СтоимостьРеглОстаток
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР
	|				КОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|			 	ТОГДА Т.СтоимостьОстаток
	|				ИНАЧЕ Т.СтоимостьБезНДСОстаток
	|			КОНЕЦ
	|	КОНЕЦ КАК СтоимостьУпрОстаток
	|ПОМЕСТИТЬ ВТОстаткиПартииТоваровОрганизаций
	|ИЗ
	|	РегистрНакопления.ПартииТоваровОрганизаций.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииСФИФОСкользящаяВПрошломПериоде)) КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.ДокументПоступления,
	|	Т.АналитикаУчетаПартий,
	|	Т.КоличествоОстаток,
	|	Т.СтоимостьОстаток,
	|	Т.СтоимостьБезНДСОстаток,
	|	Т.ПостояннаяРазницаОстаток,
	|	Т.ВременнаяРазницаОстаток,
	|	ВЫБОР
	|		КОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|			 ТОГДА Т.СтоимостьРеглОстаток + Т.НДСРеглОстаток
	|		ИНАЧЕ Т.СтоимостьРеглОстаток
	|	КОНЕЦ КАК СтоимостьРеглОстаток,
	|	ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций ТОГДА 0
	|		КОГДА &ВалютыУпрИРеглУчетаСовпадают ТОГДА 
	|			ВЫБОР
	|				КОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|			 	ТОГДА Т.СтоимостьРеглОстаток + Т.НДСРеглОстаток
	|				ИНАЧЕ Т.СтоимостьРеглОстаток
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР
	|				КОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|			 	ТОГДА Т.СтоимостьОстаток
	|				ИНАЧЕ Т.СтоимостьБезНДСОстаток
	|			КОНЕЦ
	|	КОНЕЦ КАК СтоимостьУпрОстаток
	|ПОМЕСТИТЬ ВТОстаткиПартииТоваровПереданныеНаКомиссию
	|ИЗ
	|	РегистрНакопления.ПартииТоваровПереданныеНаКомиссию.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииСФИФОСкользящаяВПрошломПериоде)) КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.ДокументПоступления,
	|	Т.СтатьяРасходов,
	|	Т.СтоимостьОстаток,
	|	Т.СтоимостьБезНДСОстаток,
	|	Т.ПостояннаяРазницаОстаток,
	|	Т.ВременнаяРазницаОстаток,
	|	ВЫБОР
	|		КОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|			 ТОГДА Т.СтоимостьРеглОстаток + Т.НДСРеглОстаток
	|		ИНАЧЕ Т.СтоимостьРеглОстаток
	|	КОНЕЦ КАК СтоимостьРеглОстаток,
	|	ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций ТОГДА 0
	|		КОГДА СтатьяРасходов.ВариантРаспределенияРасходовУпр <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров) ТОГДА 0
	|		КОГДА ЕСТЬNULL(РеестрПоступление.Организация, НЕОПРЕДЕЛЕНО) <> ЕСТЬNULL(РеестрРасход.Организация, НЕОПРЕДЕЛЕНО) ТОГДА 0
	|		КОГДА &ВалютыУпрИРеглУчетаСовпадают ТОГДА 
	|			ВЫБОР
	|				КОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|			 	ТОГДА Т.СтоимостьРеглОстаток + Т.НДСРеглОстаток
	|				ИНАЧЕ Т.СтоимостьРеглОстаток
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР
	|				КОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|			 	ТОГДА Т.СтоимостьОстаток
	|				ИНАЧЕ Т.СтоимостьБезНДСОстаток
	|			КОНЕЦ
	|	КОНЕЦ КАК СтоимостьУпрОстаток
	|ПОМЕСТИТЬ ВТОстаткиПартииРасходовНаСебестоимостьТоваров
	|ИЗ
	|	РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииСФИФОСкользящаяВПрошломПериоде)) КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрПоступление
	|		ПО РеестрПоступление.Ссылка = Т.ДокументПоступления
	|		И НЕ РеестрПоступление.ДополнительнаяЗапись
	|		И РеестрПоступление.Ссылка <> НЕОПРЕДЕЛЕНО
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрРасход
	|		ПО РеестрРасход.Ссылка = Т.ДокументПоступленияРасходов
	|		И НЕ РеестрРасход.ДополнительнаяЗапись
	|		И РеестрРасход.Ссылка <> НЕОПРЕДЕЛЕНО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаПродукции,
	|	Т.ДокументВыпуска,
	|	Т.КоличествоОстаток,
	|	Т.СтоимостьОстаток,
	|	Т.СтоимостьБезНДСОстаток,
	|	Т.ПостояннаяРазницаОстаток,
	|	Т.ВременнаяРазницаОстаток,
	|	ВЫБОР
	|		КОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|			 ТОГДА Т.СтоимостьРеглОстаток + Т.НДСРеглОстаток
	|		ИНАЧЕ Т.СтоимостьРеглОстаток
	|	КОНЕЦ КАК СтоимостьРеглОстаток,
	|	ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций ТОГДА 0
	|		КОГДА ЕСТЬNULL(РеестрПоступление.Организация, НЕОПРЕДЕЛЕНО) <> ЕСТЬNULL(РеестрВыпуск.Организация, НЕОПРЕДЕЛЕНО) ТОГДА 0
	|		КОГДА &ВалютыУпрИРеглУчетаСовпадают ТОГДА 
	|			ВЫБОР
	|				КОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|			 	ТОГДА Т.СтоимостьРеглОстаток + Т.НДСРеглОстаток
	|				ИНАЧЕ Т.СтоимостьРеглОстаток
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР
	|				КОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|			 	ТОГДА Т.СтоимостьОстаток
	|				ИНАЧЕ Т.СтоимостьБезНДСОстаток
	|			КОНЕЦ
	|	КОНЕЦ КАК СтоимостьУпрОстаток
	|ПОМЕСТИТЬ ВТОстаткиПартииЗатратНаВыпуск
	|ИЗ
	|	РегистрНакопления.ПартииЗатратНаВыпуск.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииСФИФОСкользящаяВПрошломПериоде)
	|		И АналитикаУчетаПродукции <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)) КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрПоступление
	|		ПО РеестрПоступление.Ссылка = Т.ДокументПоступления
	|		И НЕ РеестрПоступление.ДополнительнаяЗапись
	|		И РеестрПоступление.Ссылка <> НЕОПРЕДЕЛЕНО
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрВыпуск
	|		ПО РеестрВыпуск.Ссылка = Т.ДокументВыпуска
	|		И НЕ РеестрВыпуск.ДополнительнаяЗапись
	|		И РеестрВыпуск.Ссылка <> НЕОПРЕДЕЛЕНО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.ДокументПоступления,
	|	Т.АналитикаУчетаПартий,
	|	Т.КоличествоОстаток,
	|	Т.СтоимостьОстаток,
	|	Т.СтоимостьБезНДСОстаток,
	|	Т.ПостояннаяРазницаОстаток,
	|	Т.ВременнаяРазницаОстаток,
	|	ВЫБОР
	|		КОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|			 ТОГДА Т.СтоимостьРеглОстаток + Т.НДСРеглОстаток
	|		ИНАЧЕ Т.СтоимостьРеглОстаток
	|	КОНЕЦ КАК СтоимостьРеглОстаток,
	|	ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций ТОГДА 0
	|		КОГДА &ВалютыУпрИРеглУчетаСовпадают ТОГДА 
	|			ВЫБОР
	|				КОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|			 	ТОГДА Т.СтоимостьРеглОстаток + Т.НДСРеглОстаток
	|				ИНАЧЕ Т.СтоимостьРеглОстаток
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР
	|				КОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|			 	ТОГДА Т.СтоимостьОстаток
	|				ИНАЧЕ Т.СтоимостьБезНДСОстаток
	|			КОНЕЦ
	|	КОНЕЦ КАК СтоимостьУпрОстаток
	|ПОМЕСТИТЬ ВТОстаткиПартииПроизводственныхЗатрат
	|ИЗ
	|	РегистрНакопления.ПартииПроизводственныхЗатрат.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииСФИФОСкользящаяВПрошломПериоде)) КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////

	|ВЫБРАТЬ
	|	Т.РазделУчета 					КАК РазделУчета,
	|	Т.Организация 					КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры 	КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов 					КАК ВидЗапасов,
	|	ВЫБОР КОГДА Т.Организация В (&ОрганизацииСФИФОСкользящаяВПрошломПериоде)
	|		ТОГДА Т.ДокументПоступления
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ							КАК Партия,
	|	ВЫБОР КОГДА Т.Организация В (&ОрганизацииСФИФОСкользящаяВПрошломПериоде)
	|		ТОГДА Т.АналитикаУчетаПартий
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
	|	КОНЕЦ							КАК АналитикаУчетаПартий,
	|	ВЫБОР КОГДА Т.Организация В (&ОрганизацииСФИФОСкользящаяВПрошломПериоде)
	|		ТОГДА Т.ВидЦенности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
	|	КОНЕЦ							КАК ВидЦенности,
	|	СУММА(Т.Количество) 			КАК Количество,
	|	СУММА(Т.Стоимость) 				КАК Стоимость,
	|	СУММА(Т.СтоимостьБезНДС) 		КАК СтоимостьБезНДС,
	|	СУММА(Т.ПостояннаяРазница) 		КАК ПостояннаяРазница,
	|	СУММА(Т.ВременнаяРазница) 		КАК ВременнаяРазница,
	|	СУММА(Т.СтоимостьРегл) 			КАК СтоимостьРегл,
	|	СУММА(Т.ДопРасходыРегл) 		КАК ДопРасходыРегл,
	|	СУММА(Т.ДопРасходы) 			КАК ДопРасходы,
	|	СУММА(Т.ДопРасходыБезНДС) 		КАК ДопРасходыБезНДС,
	|	СУММА(Т.СтоимостьУпр) 			КАК СтоимостьУпр,
	|	СУММА(Т.ДопРасходыУпр) 			КАК ДопРасходыУпр
	|ПОМЕСТИТЬ ВТОстаткиПартийПредварительная
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|			КОГДА Т.ВидЗапасов.ТипЗапасов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца), ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца))
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		КОНЕЦ КАК РазделУчета,
	|		Т.Организация КАК Организация,
	|		Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		Т.ВидЗапасов КАК ВидЗапасов,
	|		Т.ДокументПоступления КАК ДокументПоступления,
	|		Т.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|		ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары) КАК ВидЦенности,
	|		Т.КоличествоОстаток КАК Количество,
	|		Т.СтоимостьОстаток КАК Стоимость,
	|		Т.СтоимостьБезНДСОстаток КАК СтоимостьБезНДС,
	|		Т.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
	|		Т.ВременнаяРазницаОстаток КАК ВременнаяРазница,
	|		Т.СтоимостьРеглОстаток КАК СтоимостьРегл,
	|		0 КАК ДопРасходыРегл,
	|		0 КАК ДопРасходы,
	|		0 КАК ДопРасходыБезНДС,
	|		Т.СтоимостьУпрОстаток КАК СтоимостьУпр,
	|		0 КАК ДопРасходыУпр
	|	ИЗ
	|		ВТОстаткиПартииТоваровОрганизаций КАК Т
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию),
	|		Т.Организация,
	|		Т.АналитикаУчетаНоменклатуры,
	|		Т.ВидЗапасов,
	|		Т.ДокументПоступления,
	|		Т.АналитикаУчетаПартий,
	|		ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары),
	|		Т.КоличествоОстаток,
	|		Т.СтоимостьОстаток,
	|		Т.СтоимостьБезНДСОстаток,
	|		Т.ПостояннаяРазницаОстаток,
	|		Т.ВременнаяРазницаОстаток,
	|		Т.СтоимостьРеглОстаток,
	|		0,
	|		0,
	|		0,
	|		Т.СтоимостьУпрОстаток КАК СтоимостьУпр,
	|		0 КАК ДопРасходыУпр
	|	ИЗ
	|		ВТОстаткиПартииТоваровПереданныеНаКомиссию КАК Т
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|			КОГДА Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ,
	|		Т.Организация,
	|		Т.АналитикаУчетаНоменклатуры,
	|		Т.ВидЗапасов,
	|		Т.ДокументПоступления,
	|		(ВЫБОР
	|			КОГДА НЕ ПартииТоваров.АналитикаУчетаПартий ЕСТЬ NULL ТОГДА ПартииТоваров.АналитикаУчетаПартий
	|			КОГДА НЕ ПартииПроизводства.АналитикаУчетаПартий ЕСТЬ NULL ТОГДА ПартииПроизводства.АналитикаУчетаПартий
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КОНЕЦ) КАК АналитикаУчетаПартий,
	|		ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары) КАК ВидЦенности,
	|		0,
	|		0,
	|		0,
	|		Т.ПостояннаяРазницаОстаток,
	|		Т.ВременнаяРазницаОстаток,
	|		0,
	|		Т.СтоимостьРеглОстаток,
	|		Т.СтоимостьОстаток,
	|		Т.СтоимостьБезНДСОстаток,
	|		0 КАК СтоимостьУпр,
	|		Т.СтоимостьУпрОстаток КАК ДопРасходыУпр
	|	ИЗ
	|		ВТОстаткиПартииРасходовНаСебестоимостьТоваров КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиПартииТоваровОрганизаций КАК ПартииТоваров
	|			ПО Т.Организация = ПартииТоваров.Организация
	|			И Т.АналитикаУчетаНоменклатуры = ПартииТоваров.АналитикаУчетаНоменклатуры
	|			И Т.ВидЗапасов = ПартииТоваров.ВидЗапасов
	|			И Т.ДокументПоступления = ПартииТоваров.ДокументПоступления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиПартииПроизводственныхЗатрат КАК ПартииПроизводства
	|			ПО Т.Организация = ПартииПроизводства.Организация
	|			И Т.АналитикаУчетаНоменклатуры = ПартииПроизводства.АналитикаУчетаНоменклатуры
	|			И Т.ВидЗапасов = ПартииПроизводства.ВидЗапасов
	|			И Т.ДокументПоступления = ПартииПроизводства.ДокументПоступления
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА Т.АналитикаУчетаПродукции.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|			КОГДА Т.АналитикаУчетаПродукции.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ,
	|		Т.Организация,
	|		Т.АналитикаУчетаПродукции,
	|		ЕСТЬNULL(ПартииТоваров.ВидЗапасов, ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)),
	|		Т.ДокументВыпуска,
	|		ЕСТЬNULL(ПартииТоваров.АналитикаУчетаПартий, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)),
	|		ВЫБОР КОГДА Т.АналитикаУчетаПродукции.Номенклатура.ТипНоменклатуры В (
	|							ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары) 
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги) 
	|		КОНЕЦ,
	|		0 КАК КоличествоОстаток,
	|		Т.СтоимостьОстаток,
	|		Т.СтоимостьБезНДСОстаток,
	|		Т.ПостояннаяРазницаОстаток,
	|		Т.ВременнаяРазницаОстаток,
	|		Т.СтоимостьРеглОстаток,
	|		0,
	|		0,
	|		0,
	|		Т.СтоимостьУпрОстаток КАК СтоимостьУпр,
	|		0 КАК ДопРасходыУпр
	|	ИЗ
	|		ВТОстаткиПартииЗатратНаВыпуск КАК Т
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиПартииТоваровОрганизаций КАК ПартииТоваров
	|			ПО Т.Организация = ПартииТоваров.Организация
	|				И Т.АналитикаУчетаПродукции = ПартииТоваров.АналитикаУчетаНоменклатуры
	|				И Т.ДокументВыпуска = ПартииТоваров.ДокументПоступления
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеПереработчику)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		КОНЕЦ,
	|		Т.Организация,
	|		Т.АналитикаУчетаНоменклатуры,
	|		Т.ВидЗапасов,
	|		Т.ДокументПоступления,
	|		Т.АналитикаУчетаПартий,
	|		ВЫБОР КОГДА Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры В (
	|							ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары) 
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги) 
	|		КОНЕЦ,
	|		Т.КоличествоОстаток,
	|		Т.СтоимостьОстаток,
	|		Т.СтоимостьБезНДСОстаток,
	|		Т.ПостояннаяРазницаОстаток,
	|		Т.ВременнаяРазницаОстаток,
	|		Т.СтоимостьРеглОстаток,
	|		0,
	|		0,
	|		0,
	|		Т.СтоимостьУпрОстаток КАК СтоимостьУпр,
	|		0 КАК ДопРасходыУпр
	|	ИЗ
	|		ВТОстаткиПартииПроизводственныхЗатрат КАК Т
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Т.РазделУчета,
	|		Т.Организация,
	|		Т.АналитикаУчетаНоменклатуры,
	|		Т.ВидЗапасов,
	|		Т.Партия,
	|		Т.АналитикаУчетаПартий,
	|		ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка) КАК ВидЦенности,
	|		Т.Количество,
	|		Т.Стоимость + Т.СтоимостьЗабалансовая,
	|		Т.СтоимостьБезНДС,
	|		Т.ПостояннаяРазница,
	|		Т.ВременнаяРазница,
	|		Т.СтоимостьРегл + Т.СтоимостьЗабалансоваяРегл,
	|		Т.ДопРасходыРегл,
	|		Т.ДопРасходы,
	|		Т.ДопРасходыБезНДС,
	|		ВЫБОР
	|			КОГДА НЕ &УправленческийУчетОрганизаций ТОГДА 0
	|			КОГДА &ВалютыУпрИРеглУчетаСовпадают ТОГДА 
	|				Т.СтоимостьРегл + Т.СтоимостьЗабалансоваяРегл
	|			ИНАЧЕ
	|				Т.Стоимость + Т.СтоимостьЗабалансовая
	|		КОНЕЦ КАК СтоимостьУпр,
	|		ВЫБОР
	|			КОГДА НЕ &УправленческийУчетОрганизаций ТОГДА 0
	|			КОГДА &ВалютыУпрИРеглУчетаСовпадают ТОГДА 0
	|			ИНАЧЕ Т.ДопРасходы
	|		КОНЕЦ КАК ДопРасходыУпр
	|	ИЗ
	|		ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		НЕ Т.Организация В (&ОрганизацииСФИФОСкользящаяВПрошломПериоде)
	|	) КАК Т
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.РазделУчета,
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	ВЫБОР КОГДА Т.Организация В (&ОрганизацииСФИФОСкользящаяВПрошломПериоде)
	|		ТОГДА Т.ДокументПоступления
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР КОГДА Т.Организация В (&ОрганизацииСФИФОСкользящаяВПрошломПериоде)
	|		ТОГДА Т.АналитикаУчетаПартий
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР КОГДА Т.Организация В (&ОрганизацииСФИФОСкользящаяВПрошломПериоде)
	|		ТОГДА Т.ВидЦенности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	СУММА(Т.Количество) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПартий
	|";
	
	#КонецОбласти
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос); // формируем ВТОстаткиПартийПредварительная
	
	СформироватьАналитикиПартийСВидомЦенности(ПараметрыРасчета); // формируем ВТАналитикиПартийСВидомЦенности
	
	#Область КорректировкаРасхожденийВСуммах
	
	// В силу различных причин может получиться так, что суммы остатков в регистрах партий и в регистре себестоимости
	// по некоторым сочетаниям (РазделУчета, Организация, АналитикаУчетаНоменклатуры, ВидЗапасов) могут отличаться,
	// при том, что количество по ним будет одинаково.
	// Для того, чтобы в регистре себестоимости не остались суммы без количества (без партий),
	// надо распределить эти суммы по партиям, пропорционально количеству.
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Т.РазделУчета <> НЕОПРЕДЕЛЕНО
	|			ТОГДА Т.РазделУчета
	|		КОГДА НЕ ОстаткиКомиссии.Организация ЕСТЬ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеПереработчику)
	|	КОНЕЦ КАК РазделУчета,
	|	Т.Организация КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов КАК ВидЗапасов,
	|	Т.Партия,
	|	ЕСТЬNULL(АналитикиПартий.АналитикаУчетаПартийСВидомЦенности, Т.АналитикаУчетаПартий) КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА Аналитика.ГруппаПродукции ЕСТЬ NULL 
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА Аналитика.ГруппаПродукции <> ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА Аналитика.ГруппаПродукции
	|		КОГДА Аналитика.УстарелоПредназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляСделки)
	|			ТОГДА Аналитика.УстарелоСделка
	|		КОГДА Аналитика.УстарелоПредназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляПодразделения)
	|			ТОГДА Аналитика.УстарелоПодразделение
	|		КОГДА Аналитика.УстарелоПредназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляМенеджера)
	|			ТОГДА Аналитика.УстарелоМенеджер
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА НЕ Т.АналитикаУчетаПартий.НалогообложениеНДС ЕСТЬ NULL И Т.АналитикаУчетаПартий.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС
	|		КОГДА НЕ Аналитика.НалогообложениеНДС ЕСТЬ NULL И Аналитика.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА Аналитика.НалогообложениеНДС
	|		КОГДА ЕСТЬNULL(ПримененияЕНВД.РозничнаяТорговляОблагаетсяЕНВД, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|		КОГДА УчетныеПолитикиПрошлогоПериода.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Упрощенная)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|	КОНЕЦ КАК ВидДеятельностиНДС,
	|	Т.Количество,
	|	(ВЫБОР
	|		КОГДА Т.РазделУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)) ТОГДА 0
	|		ИНАЧЕ Т.Стоимость КОНЕЦ) КАК Стоимость,
	|	(ВЫБОР
	|		КОГДА Т.РазделУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)) ТОГДА 0
	|		ИНАЧЕ Т.СтоимостьБезНДС КОНЕЦ) КАК СтоимостьБезНДС,
	|	(ВЫБОР
	|		КОГДА Т.РазделУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)) ТОГДА Т.Стоимость
	|		ИНАЧЕ 0 КОНЕЦ) КАК СтоимостьЗабалансовая,
	|	Т.ДопРасходы,
	|	Т.ДопРасходыБезНДС,
	|	(ВЫБОР
	|		КОГДА Т.РазделУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)) ТОГДА 0
	|		ИНАЧЕ Т.СтоимостьРегл КОНЕЦ) КАК СтоимостьРегл,
	|	(ВЫБОР
	|		КОГДА Т.РазделУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)) ТОГДА Т.СтоимостьРегл
	|		ИНАЧЕ 0 КОНЕЦ) КАК СтоимостьЗабалансоваяРегл,
	|	Т.ДопРасходыРегл,
	|	(ВЫБОР
	|		КОГДА Т.РазделУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)) ТОГДА 0
	|		ИНАЧЕ Т.СтоимостьУпр КОНЕЦ) КАК СтоимостьУпр,
	|	Т.ДопРасходыУпр,
	|	Т.ПостояннаяРазница,
	|	Т.ВременнаяРазница
	|ПОМЕСТИТЬ ВТОстаткиПартий
	|ИЗ
	|	ВТОстаткиПартийПредварительная КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиПартииТоваровПереданныеНаКомиссию КАК ОстаткиКомиссии
	|		ПО (Т.РазделУчета = НЕОПРЕДЕЛЕНО)
	|			И Т.Организация = ОстаткиКомиссии.Организация
	|			И Т.АналитикаУчетаНоменклатуры = ОстаткиКомиссии.АналитикаУчетаНоменклатуры
	|			И Т.ВидЗапасов = ОстаткиКомиссии.ВидЗапасов
	|			И Т.Партия = ОстаткиКомиссии.ДокументПоступления
	|			И Т.АналитикаУчетаПартий = ОстаткиКомиссии.АналитикаУчетаПартий
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК Аналитика
	|		ПО Т.ВидЗапасов = Аналитика.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТАналитикиПартийСВидомЦенности КАК АналитикиПартий
	|		ПО Т.АналитикаУчетаПартий = АналитикиПартий.АналитикаУчетаПартий
	|			И Т.ВидЦенности = АналитикиПартий.ВидЦенности
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПримененияЕНВД.СрезПоследних(&КонецПредыдущегоПериода, Организация В (&МассивОрганизаций)) КАК ПримененияЕНВД
	|		ПО Т.Организация = ПримененияЕНВД.Организация
	|			И Т.АналитикаУчетаНоменклатуры.МестоХранения = ПримененияЕНВД.Склад
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУчетныеПолитикиПрошлогоПериода КАК УчетныеПолитикиПрошлогоПериода
	|		ПО Т.Организация = УчетныеПолитикиПрошлогоПериода.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	РазделУчета,
	|	ВидЗапасов,
	|	АналитикаУчетаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.РазделУчета,
	|	Т.ВидЗапасов,
	|	Т.АналитикаУчетаНоменклатуры,
	|	СУММА(Т.КоличествоИзПартий)					КАК КоличествоИзПартий,
	|	СУММА(Т.Знак * Т.Стоимость) 				КАК Стоимость,
	|	СУММА(Т.Знак * Т.СтоимостьБезНДС) 			КАК СтоимостьБезНДС,
	|	СУММА(Т.Знак * Т.СтоимостьЗабалансовая) 	КАК СтоимостьЗабалансовая,
	|	СУММА(Т.Знак * Т.ДопРасходы) 				КАК ДопРасходы,
	|	СУММА(Т.Знак * Т.ДопРасходыБезНДС) 			КАК ДопРасходыБезНДС,
	|	СУММА(Т.Знак * Т.СтоимостьРегл) 			КАК СтоимостьРегл,
	|	СУММА(Т.Знак * Т.СтоимостьЗабалансоваяРегл) КАК СтоимостьЗабалансоваяРегл,
	|	СУММА(Т.Знак * Т.ДопРасходыРегл) 			КАК ДопРасходыРегл,
	|	СУММА(Т.Знак * Т.СтоимостьУпр) 				КАК СтоимостьУпр,
	|	СУММА(Т.Знак * Т.ДопРасходыУпр) 			КАК ДопРасходыУпр,
	|	СУММА(Т.Знак * Т.ПостояннаяРазница) 		КАК ПостояннаяРазница,
	|	СУММА(Т.Знак * Т.ВременнаяРазница) 			КАК ВременнаяРазница
	|ПОМЕСТИТЬ ВТОстаткиСуммБезКоличества
	|ИЗ
	|	(ВЫБРАТЬ
	|		1 КАК Знак,
	|		Т.Организация,
	|		Т.РазделУчета,
	|		Т.ВидЗапасов,
	|		Т.АналитикаУчетаНоменклатуры,
	|		Т.Количество 					КАК КоличествоИзСебестоимости,
	|		0 								КАК КоличествоИзПартий,
	|		Т.Стоимость 					КАК Стоимость,
	|		Т.СтоимостьБезНДС 				КАК СтоимостьБезНДС,
	|		Т.СтоимостьЗабалансовая 		КАК СтоимостьЗабалансовая,
	|		Т.ДопРасходы 					КАК ДопРасходы,
	|		Т.ДопРасходыБезНДС 				КАК ДопРасходыБезНДС,
	|		Т.СтоимостьРегл 				КАК СтоимостьРегл,
	|		Т.СтоимостьЗабалансоваяРегл 	КАК СтоимостьЗабалансоваяРегл,
	|		Т.ДопРасходыРегл 				КАК ДопРасходыРегл,
	|		0								КАК СтоимостьУпр,
	|		0								КАК ДопРасходыУпр,
	|		Т.ПостояннаяРазница 			КАК ПостояннаяРазница,
	|		Т.ВременнаяРазница 				КАК ВременнаяРазница
	|	ИЗ
	|		ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		НЕ &СторнироватьОстаткиСебестоимости И &РаспределятьРасхожденияВСуммах
	|		И Т.Организация В (&ОрганизацииСФИФОСкользящаяВПрошломПериоде)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		-1 КАК Знак,
	|		Т.Организация,
	|		Т.РазделУчета,
	|		Т.ВидЗапасов,
	|		Т.АналитикаУчетаНоменклатуры,
	|		0 									  КАК КоличествоИзСебестоимости,
	|		Т.Количество						  КАК КоличествоИзПартий,
	|		Т.Стоимость 						  КАК Стоимость,
	|		Т.СтоимостьБезНДС 					  КАК СтоимостьБезНДС,
	|		Т.СтоимостьЗабалансовая 			  КАК СтоимостьЗабалансовая,
	|		Т.ДопРасходы 				      	  КАК ДопРасходы,
	|		Т.ДопРасходыБезНДС 			  		  КАК ДопРасходыБезНДС,
	|		Т.СтоимостьРегл + Т.ДопРасходыРегл 	  КАК СтоимостьРегл,
	|		Т.СтоимостьЗабалансоваяРегл 		  КАК СтоимостьЗабалансоваяРегл,
	|		0 									  КАК ДопРасходыРегл,
	|		0 	  								  КАК СтоимостьУпр,
	|		0 									  КАК ДопРасходыУпр,
	|		Т.ПостояннаяРазница 				  КАК ПостояннаяРазница,
	|		Т.ВременнаяРазница 				  	  КАК ВременнаяРазница
	|	ИЗ
	|		ВТОстаткиПартий КАК Т
	|	ГДЕ
	|		НЕ &СторнироватьОстаткиСебестоимости И &РаспределятьРасхожденияВСуммах
	|		И Т.Организация В (&ОрганизацииСФИФОСкользящаяВПрошломПериоде)
	|	) КАК Т
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация,
	|	Т.РазделУчета,
	|	Т.ВидЗапасов,
	|	Т.АналитикаУчетаНоменклатуры
	|
	|ИМЕЮЩИЕ
	|	СУММА(Т.КоличествоИзСебестоимости) = СУММА(Т.КоличествоИзПартий)
	|	И СУММА(Т.КоличествоИзПартий) <> 0
	|	И (СУММА(Т.Знак * Т.Стоимость) <> 0
	|		ИЛИ СУММА(Т.Знак * Т.СтоимостьБезНДС) <> 0
	|		ИЛИ СУММА(Т.Знак * Т.СтоимостьЗабалансовая) <> 0
	|		ИЛИ СУММА(Т.Знак * Т.ДопРасходы) <> 0
	|		ИЛИ СУММА(Т.Знак * Т.ДопРасходыБезНДС) <> 0
	|		ИЛИ СУММА(Т.Знак * Т.СтоимостьРегл) <> 0
	|		ИЛИ СУММА(Т.Знак * Т.СтоимостьЗабалансоваяРегл) <> 0
	|		ИЛИ СУММА(Т.Знак * Т.ДопРасходыРегл) <> 0
	|		ИЛИ СУММА(Т.Знак * Т.СтоимостьУпр) <> 0
	|		ИЛИ СУММА(Т.Знак * Т.ДопРасходыУпр) <> 0
	|		ИЛИ СУММА(Т.Знак * Т.ПостояннаяРазница) <> 0
	|		ИЛИ СУММА(Т.Знак * Т.ВременнаяРазница) <> 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	РазделУчета,
	|	ВидЗапасов,
	|	АналитикаУчетаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.РазделУчета КАК РазделУчета,
	|	Т.Организация КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов КАК ВидЗапасов,
	|	Т.Партия КАК Партия,
	|	Т.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	Т.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|	Т.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	ВЫРАЗИТЬ(Т.Количество / ОстаткиСумм.КоличествоИзПартий КАК ЧИСЛО (23,10)) 							    		 КАК Коэффициент,
	|	ВЫРАЗИТЬ(ОстаткиСумм.Стоимость * Т.Количество / ОстаткиСумм.КоличествоИзПартий КАК ЧИСЛО(31,2)) 				 КАК Стоимость,
	|	ВЫРАЗИТЬ(ОстаткиСумм.СтоимостьБезНДС * Т.Количество / ОстаткиСумм.КоличествоИзПартий КАК ЧИСЛО(31,2)) 			 КАК СтоимостьБезНДС,
	|	ВЫРАЗИТЬ(ОстаткиСумм.СтоимостьЗабалансовая * Т.Количество / ОстаткиСумм.КоличествоИзПартий КАК ЧИСЛО(31,2)) 	 КАК СтоимостьЗабалансовая,
	|	ВЫРАЗИТЬ(ОстаткиСумм.ДопРасходы * Т.Количество / ОстаткиСумм.КоличествоИзПартий КАК ЧИСЛО(31,2)) 		 		 КАК ДопРасходы,
	|	ВЫРАЗИТЬ(ОстаткиСумм.ДопРасходыБезНДС * Т.Количество / ОстаткиСумм.КоличествоИзПартий КАК ЧИСЛО(31,2)) 	 	 КАК ДопРасходыБезНДС,
	|	ВЫРАЗИТЬ(ОстаткиСумм.СтоимостьРегл * Т.Количество / ОстаткиСумм.КоличествоИзПартий КАК ЧИСЛО(31,2)) 			 КАК СтоимостьРегл,
	|	ВЫРАЗИТЬ(ОстаткиСумм.СтоимостьЗабалансоваяРегл * Т.Количество / ОстаткиСумм.КоличествоИзПартий КАК ЧИСЛО(31,2)) КАК СтоимостьЗабалансоваяРегл,
	|	ВЫРАЗИТЬ(ОстаткиСумм.ДопРасходыРегл * Т.Количество / ОстаткиСумм.КоличествоИзПартий КАК ЧИСЛО(31,2)) 			 КАК ДопРасходыРегл,
	|	ВЫРАЗИТЬ(ОстаткиСумм.СтоимостьУпр * Т.Количество / ОстаткиСумм.КоличествоИзПартий КАК ЧИСЛО(31,2)) 			 КАК СтоимостьУпр,
	|	ВЫРАЗИТЬ(ОстаткиСумм.ДопРасходыУпр * Т.Количество / ОстаткиСумм.КоличествоИзПартий КАК ЧИСЛО(31,2)) 			 КАК ДопРасходыУпр,
	|	ВЫРАЗИТЬ(ОстаткиСумм.ПостояннаяРазница * Т.Количество / ОстаткиСумм.КоличествоИзПартий КАК ЧИСЛО(31,2)) 		 КАК ПостояннаяРазница,
	|	ВЫРАЗИТЬ(ОстаткиСумм.ВременнаяРазница * Т.Количество / ОстаткиСумм.КоличествоИзПартий КАК ЧИСЛО(31,2)) 		 КАК ВременнаяРазница
	|ПОМЕСТИТЬ ВТКорректировкаПартий
	|ИЗ
	|	ВТОстаткиПартий КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОстаткиСуммБезКоличества КАК ОстаткиСумм
	|	ПО Т.Организация = ОстаткиСумм.Организация
	|		И Т.РазделУчета = ОстаткиСумм.РазделУчета
	|		И Т.ВидЗапасов = ОстаткиСумм.ВидЗапасов
	|		И Т.АналитикаУчетаНоменклатуры = ОстаткиСумм.АналитикаУчетаНоменклатуры
	|ГДЕ
	|	НЕ &СторнироватьОстаткиСебестоимости И &РаспределятьРасхожденияВСуммах
	|	И Т.Количество <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	РазделУчета,
	|	ВидЗапасов,
	|	АналитикаУчетаНоменклатуры
	|";
	
	#КонецОбласти
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос); // формируем ВТКорректировкаПартий 
	
	#Область КорректировкаЗависшихКопеек
	
	// Для каждого сочетания (РазделУчета, Организация, АналитикаУчетаНоменклатуры, ВидЗапасов) из ВТЗависшиеКопейки
	// найдем единственное сочетание (Партия, АналитикаУчетаПартий, АналитикаФинансовогоУчета, ВидДеятельностиНДС) из ВТКорректировкаПартий,
	// на которое и спишем "копейки", образовавшиеся в результате округления при корректировке расхождений в остатках
	// партий и себестоимости.
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.Организация,
	|	Т.РазделУчета,
	|	Т.ВидЗапасов,
	|	Т.АналитикаУчетаНоменклатуры,
	|	СУММА(Т.КоличествоИзПартий)					КАК КоличествоИзПартий,
	|	СУММА(Т.Знак * Т.Стоимость) 				КАК Стоимость,
	|	СУММА(Т.Знак * Т.СтоимостьБезНДС) 			КАК СтоимостьБезНДС,
	|	СУММА(Т.Знак * Т.СтоимостьЗабалансовая) 	КАК СтоимостьЗабалансовая,
	|	СУММА(Т.Знак * Т.ДопРасходы) 				КАК ДопРасходы,
	|	СУММА(Т.Знак * Т.ДопРасходыБезНДС) 			КАК ДопРасходыБезНДС,
	|	СУММА(Т.Знак * Т.СтоимостьРегл) 			КАК СтоимостьРегл,
	|	СУММА(Т.Знак * Т.СтоимостьЗабалансоваяРегл) КАК СтоимостьЗабалансоваяРегл,
	|	СУММА(Т.Знак * Т.ДопРасходыРегл) 			КАК ДопРасходыРегл,
	|	СУММА(Т.Знак * Т.СтоимостьУпр) 				КАК СтоимостьУпр,
	|	СУММА(Т.Знак * Т.ДопРасходыУпр) 			КАК ДопРасходыУпр,
	|	СУММА(Т.Знак * Т.ПостояннаяРазница) 		КАК ПостояннаяРазница,
	|	СУММА(Т.Знак * Т.ВременнаяРазница) 			КАК ВременнаяРазница
	|ПОМЕСТИТЬ ВТЗависшиеКопейки
	|ИЗ
	|	(ВЫБРАТЬ
	|		1 КАК Знак,
	|		Т.Организация,
	|		Т.РазделУчета,
	|		Т.ВидЗапасов,
	|		Т.АналитикаУчетаНоменклатуры,
	|		Т.КоличествоИзПартий			КАК КоличествоИзПартий,
	|		Т.Стоимость 					КАК Стоимость,
	|		Т.СтоимостьБезНДС 				КАК СтоимостьБезНДС,
	|		Т.СтоимостьЗабалансовая 		КАК СтоимостьЗабалансовая,
	|		Т.ДопРасходы 					КАК ДопРасходы,
	|		Т.ДопРасходыБезНДС 				КАК ДопРасходыБезНДС,
	|		Т.СтоимостьРегл 				КАК СтоимостьРегл,
	|		Т.СтоимостьЗабалансоваяРегл 	КАК СтоимостьЗабалансоваяРегл,
	|		Т.ДопРасходыРегл 				КАК ДопРасходыРегл,
	|		Т.СтоимостьУпр 					КАК СтоимостьУпр,
	|		Т.ДопРасходыУпр 				КАК ДопРасходыУпр,
	|		Т.ПостояннаяРазница 			КАК ПостояннаяРазница,
	|		Т.ВременнаяРазница 				КАК ВременнаяРазница
	|	ИЗ
	|		ВТОстаткиСуммБезКоличества КАК Т
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		-1 КАК Знак,
	|		Т.Организация,
	|		Т.РазделУчета,
	|		Т.ВидЗапасов,
	|		Т.АналитикаУчетаНоменклатуры,
	|		0								КАК КоличествоИзПартий,
	|		Т.Стоимость 					КАК Стоимость,
	|		Т.СтоимостьБезНДС 				КАК СтоимостьБезНДС,
	|		Т.СтоимостьЗабалансовая 		КАК СтоимостьЗабалансовая,
	|		Т.ДопРасходы 					КАК ДопРасходы,
	|		Т.ДопРасходыБезНДС 				КАК ДопРасходыБезНДС,
	|		Т.СтоимостьРегл 				КАК СтоимостьРегл,
	|		Т.СтоимостьЗабалансоваяРегл 	КАК СтоимостьЗабалансоваяРегл,
	|		Т.ДопРасходыРегл 				КАК ДопРасходыРегл,
	|		Т.СтоимостьУпр 					КАК СтоимостьУпр,
	|		Т.ДопРасходыУпр 				КАК ДопРасходыУпр,
	|		Т.ПостояннаяРазница 			КАК ПостояннаяРазница,
	|		Т.ВременнаяРазница 				КАК ВременнаяРазница
	|	ИЗ
	|		ВТКорректировкаПартий КАК Т
	|	) КАК Т
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация,
	|	Т.РазделУчета,
	|	Т.ВидЗапасов,
	|	Т.АналитикаУчетаНоменклатуры
	|
	|ИМЕЮЩИЕ
	|		СУММА(Т.Знак * Т.Стоимость) <> 0
	|	ИЛИ СУММА(Т.Знак * Т.СтоимостьБезНДС) <> 0
	|	ИЛИ СУММА(Т.Знак * Т.СтоимостьЗабалансовая) <> 0
	|	ИЛИ СУММА(Т.Знак * Т.ДопРасходы) <> 0
	|	ИЛИ СУММА(Т.Знак * Т.ДопРасходыБезНДС) <> 0
	|	ИЛИ СУММА(Т.Знак * Т.СтоимостьРегл) <> 0
	|	ИЛИ СУММА(Т.Знак * Т.СтоимостьЗабалансоваяРегл) <> 0
	|	ИЛИ СУММА(Т.Знак * Т.ДопРасходыРегл) <> 0
	|	ИЛИ СУММА(Т.Знак * Т.СтоимостьУпр) <> 0
	|	ИЛИ СУММА(Т.Знак * Т.ДопРасходыУпр) <> 0
	|	ИЛИ СУММА(Т.Знак * Т.ПостояннаяРазница) <> 0
	|	ИЛИ СУММА(Т.Знак * Т.ВременнаяРазница) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	РазделУчета,
	|	ВидЗапасов,
	|	АналитикаУчетаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.РазделУчета КАК РазделУчета,
	|	Т.Организация КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов КАК ВидЗапасов,
	|	Т.Партия КАК Партия,
	|	Т.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	Т.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|	Т.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	Т.Коэффициент КАК Коэффициент,
	|	ЗависшиеКопейки.Стоимость КАК Стоимость,
	|	ЗависшиеКопейки.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|	ЗависшиеКопейки.СтоимостьЗабалансовая КАК СтоимостьЗабалансовая,
	|	ЗависшиеКопейки.ДопРасходы КАК ДопРасходы,
	|	ЗависшиеКопейки.ДопРасходыБезНДС КАК ДопРасходыБезНДС,
	|	ЗависшиеКопейки.СтоимостьРегл КАК СтоимостьРегл,
	|	ЗависшиеКопейки.СтоимостьЗабалансоваяРегл КАК СтоимостьЗабалансоваяРегл,
	|	ЗависшиеКопейки.ДопРасходыРегл КАК ДопРасходыРегл,
	|	ЗависшиеКопейки.СтоимостьУпр КАК СтоимостьУпр,
	|	ЗависшиеКопейки.ДопРасходыУпр КАК ДопРасходыУпр,
	|	ЗависшиеКопейки.ПостояннаяРазница КАК ПостояннаяРазница,
	|	ЗависшиеКопейки.ВременнаяРазница КАК ВременнаяРазница
	|ПОМЕСТИТЬ ВТПартииДляСписанияКопеек
	|ИЗ
	|	ВТКорректировкаПартий КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЗависшиеКопейки КАК ЗависшиеКопейки
	|	ПО Т.Организация = ЗависшиеКопейки.Организация
	|		И Т.РазделУчета = ЗависшиеКопейки.РазделУчета
	|		И Т.ВидЗапасов = ЗависшиеКопейки.ВидЗапасов
	|		И Т.АналитикаУчетаНоменклатуры = ЗависшиеКопейки.АналитикаУчетаНоменклатуры
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	РазделУчета,
	|	ВидЗапасов,
	|	АналитикаУчетаНоменклатуры,
	|	Коэффициент,
	|	Партия,
	|	АналитикаУчетаПартий,
	|	АналитикаФинансовогоУчета,
	|	ВидДеятельностиНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.РазделУчета КАК РазделУчета,
	|	Т.Организация КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов КАК ВидЗапасов,
	|	Т.Партия КАК Партия,
	|	Т.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	Т.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|	Т.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	Т.Коэффициент КАК Коэффициент,
	|	Т.Стоимость КАК Стоимость,
	|	Т.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|	Т.СтоимостьЗабалансовая КАК СтоимостьЗабалансовая,
	|	Т.ДопРасходы КАК ДопРасходы,
	|	Т.ДопРасходыБезНДС КАК ДопРасходыБезНДС,
	|	Т.СтоимостьРегл КАК СтоимостьРегл,
	|	Т.СтоимостьЗабалансоваяРегл КАК СтоимостьЗабалансоваяРегл,
	|	Т.ДопРасходыРегл КАК ДопРасходыРегл,
	|	Т.СтоимостьУпр КАК СтоимостьУпр,
	|	Т.ДопРасходыУпр КАК ДопРасходыУпр,
	|	Т.ПостояннаяРазница КАК ПостояннаяРазница,
	|	Т.ВременнаяРазница КАК ВременнаяРазница
	|ПОМЕСТИТЬ ВТКорректировкаКопеек
	|ИЗ
	|	ВТПартииДляСписанияКопеек КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТПартииДляСписанияКопеек КАК Т2
	|	ПО Т.Организация = Т2.Организация
	|		И Т.РазделУчета = Т2.РазделУчета
	|		И Т.ВидЗапасов = Т2.ВидЗапасов
	|		И Т.АналитикаУчетаНоменклатуры = Т2.АналитикаУчетаНоменклатуры
	|		И ((Т.Коэффициент < Т2.Коэффициент)
	|			ИЛИ (Т.Коэффициент = Т2.Коэффициент И Т.Партия < Т2.Партия)
	|			ИЛИ (Т.Коэффициент = Т2.Коэффициент И Т.Партия = Т2.Партия И Т.АналитикаУчетаПартий < Т2.АналитикаУчетаПартий)
	|			ИЛИ (Т.Коэффициент = Т2.Коэффициент И Т.Партия = Т2.Партия И Т.АналитикаУчетаПартий = Т2.АналитикаУчетаПартий
	|					И Т.АналитикаФинансовогоУчета < Т2.АналитикаФинансовогоУчета)
	|			ИЛИ (Т.Коэффициент = Т2.Коэффициент И Т.Партия = Т2.Партия И Т.АналитикаУчетаПартий = Т2.АналитикаУчетаПартий
	|					И Т.АналитикаФинансовогоУчета = Т2.АналитикаФинансовогоУчета И Т.ВидДеятельностиНДС < Т2.ВидДеятельностиНДС))
	|ГДЕ
	|	Т2.Организация ЕСТЬ NULL
	|";
	
	#КонецОбласти
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос); // формируем ВТКорректировкаКопеек
	
	#Область ФормированиеИтоговыхТаблицОстатков
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.РазделУчета 						КАК РазделУчета,
	|	Т.Организация 						КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры 		КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов 						КАК ВидЗапасов,
	|	Т.Партия 							КАК Партия,
	|	Т.АналитикаУчетаПартий 				КАК АналитикаУчетаПартий,
	|	Т.АналитикаФинансовогоУчета 		КАК АналитикаФинансовогоУчета,
	|	Т.ВидДеятельностиНДС 				КАК ВидДеятельностиНДС,
	|	СУММА(Т.Количество) 				КАК Количество,
	|	СУММА(Т.Стоимость) 					КАК Стоимость,
	|	СУММА(Т.СтоимостьБезНДС) 			КАК СтоимостьБезНДС,
	|	СУММА(Т.СтоимостьЗабалансовая) 		КАК СтоимостьЗабалансовая,
	|	СУММА(Т.ДопРасходы) 				КАК ДопРасходы,
	|	СУММА(Т.ДопРасходыБезНДС) 			КАК ДопРасходыБезНДС,
	|	СУММА(Т.СтоимостьРегл) 				КАК СтоимостьРегл,
	|	СУММА(Т.СтоимостьЗабалансоваяРегл) 	КАК СтоимостьЗабалансоваяРегл,
	|	СУММА(Т.ДопРасходыРегл) 			КАК ДопРасходыРегл,
	|	СУММА(Т.СтоимостьУпр) 				КАК СтоимостьУпр,
	|	СУММА(Т.ДопРасходыУпр) 				КАК ДопРасходыУпр,
	|	СУММА(Т.ПостояннаяРазница) 			КАК ПостояннаяРазница,
	|	СУММА(Т.ВременнаяРазница) 			КАК ВременнаяРазница
	|ПОМЕСТИТЬ ВТОстаткиПартийСкорректированные
	|ИЗ
	|	(ВЫБРАТЬ
	|		Т.РазделУчета КАК РазделУчета,
	|		Т.Организация КАК Организация,
	|		Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		Т.ВидЗапасов КАК ВидЗапасов,
	|		Т.Партия КАК Партия,
	|		Т.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|		Т.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|		Т.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|		0 КАК Количество,
	|		Т.Стоимость КАК Стоимость,
	|		Т.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|		Т.СтоимостьЗабалансовая КАК СтоимостьЗабалансовая,
	|		Т.ДопРасходы КАК ДопРасходы,
	|		Т.ДопРасходыБезНДС КАК ДопРасходыБезНДС,
	|		Т.СтоимостьРегл КАК СтоимостьРегл,
	|		Т.СтоимостьЗабалансоваяРегл КАК СтоимостьЗабалансоваяРегл,
	|		Т.ДопРасходыРегл КАК ДопРасходыРегл,
	|		Т.СтоимостьУпр КАК СтоимостьУпр,
	|		Т.ДопРасходыУпр КАК ДопРасходыУпр,
	|		Т.ПостояннаяРазница КАК ПостояннаяРазница,
	|		Т.ВременнаяРазница КАК ВременнаяРазница
	|	ИЗ
	|		ВТКорректировкаПартий КАК Т
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Т.РазделУчета КАК РазделУчета,
	|		Т.Организация КАК Организация,
	|		Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		Т.ВидЗапасов КАК ВидЗапасов,
	|		Т.Партия КАК Партия,
	|		Т.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|		Т.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|		Т.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|		0 КАК Количество,
	|		Т.Стоимость КАК Стоимость,
	|		Т.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|		Т.СтоимостьЗабалансовая КАК СтоимостьЗабалансовая,
	|		Т.ДопРасходы КАК ДопРасходы,
	|		Т.ДопРасходыБезНДС КАК ДопРасходыБезНДС,
	|		Т.СтоимостьРегл КАК СтоимостьРегл,
	|		Т.СтоимостьЗабалансоваяРегл КАК СтоимостьЗабалансоваяРегл,
	|		Т.ДопРасходыРегл КАК ДопРасходыРегл,
	|		Т.СтоимостьУпр КАК СтоимостьУпр,
	|		Т.ДопРасходыУпр КАК ДопРасходыУпр,
	|		Т.ПостояннаяРазница КАК ПостояннаяРазница,
	|		Т.ВременнаяРазница КАК ВременнаяРазница
	|	ИЗ
	|		ВТКорректировкаКопеек КАК Т
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Т.РазделУчета КАК РазделУчета,
	|		Т.Организация КАК Организация,
	|		Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		Т.ВидЗапасов КАК ВидЗапасов,
	|		Т.Партия КАК Партия,
	|		Т.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|		Т.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|		Т.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|		Т.Количество КАК Количество,
	|		Т.Стоимость КАК Стоимость,
	|		Т.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|		Т.СтоимостьЗабалансовая КАК СтоимостьЗабалансовая,
	|		Т.ДопРасходы КАК ДопРасходы,
	|		Т.ДопРасходыБезНДС КАК ДопРасходыБезНДС,
	|		Т.СтоимостьРегл КАК СтоимостьРегл,
	|		Т.СтоимостьЗабалансоваяРегл КАК СтоимостьЗабалансоваяРегл,
	|		Т.ДопРасходыРегл КАК ДопРасходыРегл,
	|		Т.СтоимостьУпр КАК СтоимостьУпр,
	|		Т.ДопРасходыУпр КАК ДопРасходыУпр,
	|		Т.ПостояннаяРазница КАК ПостояннаяРазница,
	|		Т.ВременнаяРазница КАК ВременнаяРазница
	|	ИЗ
	|		ВТОстаткиПартий КАК Т
	|) КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.РазделУчета,
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.Партия,
	|	Т.АналитикаУчетаПартий,
	|	Т.АналитикаФинансовогоУчета,
	|	Т.ВидДеятельностиНДС";
	
	Если НЕ ПараметрыРасчета.НачальныеОстатки.ВзятьОстаткиСебестоимостиИзРегистровПартий Тогда
		
		Запрос.Текст = Запрос.Текст + "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|" + "
		|ВЫБРАТЬ
		|	Т.РазделУчета КАК РазделУчета,
		|	Т.Организация КАК Организация,
		|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	Т.ВидЗапасов КАК ВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК Партия,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаФинансовогоУчета,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК ВидДеятельностиНДС,
		|	Т.Количество КАК Количество,
		|	Т.Стоимость КАК Стоимость,
		|	Т.СтоимостьБезНДС КАК СтоимостьБезНДС,
		|	Т.СтоимостьЗабалансовая КАК СтоимостьЗабалансовая,
		|	Т.ДопРасходы КАК ДопРасходы,
		|	Т.ДопРасходыБезНДС КАК ДопРасходыБезНДС,
		|	Т.СтоимостьРегл + Т.ДопРасходыРегл КАК СтоимостьРегл,
		|	Т.СтоимостьЗабалансоваяРегл КАК СтоимостьЗабалансоваяРегл,
		|	0 КАК ДопРасходыРегл,
		|	0 КАК СтоимостьУпр,
		|	0 КАК ДопРасходыУпр,
		|	Т.ПостояннаяРазница КАК ПостояннаяРазница,
		|	Т.ВременнаяРазница КАК ВременнаяРазница
		|ПОМЕСТИТЬ ВТОстаткиПартийСкорректированныеСторно
		|ИЗ
		|	ВТОстаткиПартийСкорректированные КАК Т";
		
	КонецЕсли;
	
	#КонецОбласти
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос); // формируем ВТОстаткиПартийСкорректированные, ВТОстаткиПартийСкорректированныеСторно
	
	#Область ОшибкиВОстатках
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Т.Организация КАК Организация
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПартииТоваровОрганизаций"" КАК ИмяРегистра,
	|	Т.Организация КАК Организация
	|ИЗ
	|	ВТОстаткиПартииТоваровОрганизаций КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПартииТоваровПереданныеНаКомиссию"" КАК ИмяРегистра,
	|	Т.Организация КАК Организация
	|ИЗ
	|	ВТОстаткиПартииТоваровПереданныеНаКомиссию КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПартииПроизводственныхЗатрат"" КАК ИмяРегистра,
	|	Т.Организация КАК Организация
	|ИЗ
	|	ВТОстаткиПартииПроизводственныхЗатрат КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПартииРасходовНаСебестоимостьТоваров"" КАК ИмяРегистра,
	|	Т.Организация КАК Организация
	|ИЗ
	|	ВТОстаткиПартииРасходовНаСебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)";
	
	Выборка = РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	ЕстьПустаяАналитикаНоменклатуры = Ложь; 
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ИмяРегистра = Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя Тогда
			ЕстьПустаяАналитикаНоменклатуры = Истина;
		КонецЕсли;
		
		ТекстДляПротокола = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='В регистре накопления ""%1"" по организации ""%2"" на начало периода %3 есть остатки по пустой аналитике учета номенклатуры.'", ОбщегоНазначения.КодОсновногоЯзыка()),
			Выборка.ИмяРегистра,
			РасчетСебестоимостиПротоколРасчета.ПредставлениеЗначения(Выборка.Организация),
			РасчетСебестоимостиПротоколРасчета.ПредставлениеПериодаРасчета(ПараметрыРасчета));
		
		РасчетСебестоимостиПротоколРасчета.ЗафиксироватьОшибкуРасчета(
			ПараметрыРасчета,
			Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаВНачальныхОстаткахПУ22,
			ТекстДляПротокола);
			
		РасчетСебестоимостиПрикладныеАлгоритмы.ЗарегистрироватьПроблемуВыполненияРасчета(
			ПараметрыРасчета,
			Выборка.Организация,
			НСтр("ru='При переходе на партионный учет версии 2.2 диагностированы ошибки'", ОбщегоНазначения.КодОсновногоЯзыка()),
			ТекстДляПротокола);
		
	КонецЦикла;
	
	#КонецОбласти
	
	Если ЕстьПустаяАналитикаНоменклатуры И ПараметрыРасчета.НачальныеОстатки.ВзятьОстаткиСебестоимостиИзРегистровПартий Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	*
		|ПОМЕСТИТЬ ВТОстаткиСебестоимостьТоваров
		|ИЗ
		|	ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров КАК Т
		|ГДЕ
		|	Т.АналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)";
		
		РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
		
	КонецЕсли;
	
	// Добавляем данные к корректировочным движениям
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		"ВТОстаткиПартийСкорректированные",
		?(ПараметрыРасчета.НачальныеОстатки.ВзятьОстаткиСебестоимостиИзРегистровПартий,
			?(ЕстьПустаяАналитикаНоменклатуры, "ВТОстаткиСебестоимостьТоваров", "ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров"),
			"ВТОстаткиПартийСкорректированныеСторно"),
		Истина,
		Истина);
	
КонецПроцедуры

// Выполняет безусловное включение учета по видам запасов - заполняет виды запасов.
//
Процедура КорректировкаВключитьУчетПоВидамЗапасовСебестоимостьТоваров(ПараметрыРасчета, ПараметрыКорректировки)
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		Перечисления.ТипыЗаписейПартий.СлужебноеВключитьУчетПоВидамЗапасов);
	
	Если НЕ ПараметрыКорректировки.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	#Область ПодготовкаДанных
	
	ОписаниеАналитики = СформироватьОписаниеКлючейАналитикУчетаНоменклатуры(, "Назначение");
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	Запрос.УстановитьПараметр("ОрганизацииДляКорректировки", ПараметрыКорректировки.ОрганизацииДляКорректировки);
	Запрос.УстановитьПараметр("ТипДокументаИмпорта", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ТаможеннаяДекларацияИмпорт"));
	
	// Выберем данные для распределения
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	%ИзмеренияРегистра,
	|	%РесурсыРегистра,
	|	%ПоляАналитикиИменованные
	|ПОМЕСТИТЬ ВТОстаткиСебестоимостиБезВидовЗапасов
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|	И Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И НЕ (Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		  И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения В
	|			(ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер),
	|			 ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Организация)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация						КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов						КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи)
	|		КОГДА ЕСТЬNULL(Т.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		КОГДА Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	КОНЕЦ								КАК РазделУчета,
	|	Т.КоличествоОстаток					КАК Количество
	|ПОМЕСТИТЬ ВТОстаткиСВидамиЗапасовПредварительная
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииДляКорректировки)
	|	) КАК Т
	|ГДЕ
	|	Т.КоличествоОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Организация						КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов						КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию)
	|	КОНЕЦ								КАК РазделУчета,
	|	Т.КоличествоОстаток					КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеНаКомиссию.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииДляКорректировки)
	|	) КАК Т
	|ГДЕ
	|	Т.КоличествоОстаток > 0
	|
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.ВидЗапасов.Организация			КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов						ВидЗапасов,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Т.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	КОНЕЦ								КАК РазделУчета,
	|	Т.КоличествоОстаток					КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюДокументовИмпорта.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		ВидЗапасов.Организация В (&ОрганизацииДляКорректировки)
	|		И ТипДокументаИмпорта = &ТипДокументаИмпорта
	|	) КАК Т
	|ГДЕ
	|	Т.КоличествоОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументРеализации.Организация														  КАК Организация,
	|	ЕСТЬNULL(АналитикаПереданнойНоменклатуры.КлючАналитики, Т.АналитикаУчетаНоменклатуры) КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов																		  КАК ВидЗапасов,
	|	ВЫБОР 
	|		КОГДА ДокументРеализации.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыВПути)
	|		ИНАЧЕ ВЫБОР 
	|			КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссиюВПути)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссиюВПути)
	|		КОНЕЦ
	|	КОНЕЦ 																				  КАК РазделУчета,
	|	Т.Количество																		  КАК Количество
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ВидыЗапасов КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК ДокументРеализации
	|		ПО Т.Ссылка = ДокументРеализации.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО Т.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПереданнойНоменклатуры
	|	ПО Аналитика.Номенклатура = АналитикаПереданнойНоменклатуры.Номенклатура
	|		И Аналитика.Характеристика = АналитикаПереданнойНоменклатуры.Характеристика
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаПереданнойНоменклатуры.Назначение
	|		И ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = АналитикаПереданнойНоменклатуры.Серия
	|		И ДокументРеализации.Партнер = АналитикаПереданнойНоменклатуры.МестоХранения
	|ГДЕ
	|	ДокументРеализации.Организация В (&ОрганизацииДляКорректировки)
	|	И ДокументРеализации.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности))
	|	И ДокументРеализации.Проведен
	|	И ДокументРеализации.Дата < &НачалоПериода
	|	И ДокументРеализации.ДатаПереходаПраваСобственности >= &НачалоПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.РазделУчета,
	|	%ПоляАналитикиИменованные,
	|	СУММА(Т.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТОстаткиСВидамиЗапасов
	|ИЗ
	|	ВТОстаткиСВидамиЗапасовПредварительная КАК Т
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.РазделУчета,
	|	%ПоляАналитикиНеИменованные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТОстаткиСВидамиЗапасовПредварительная";
	
	РаспределяемыеИзмерения = РасчетСебестоимостиУниверсальныеАлгоритмы.УдалитьЭлементыИзСтрокиШаблона(
		ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра,
		"%1ВидЗапасов");
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИзмеренияРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(РаспределяемыеИзмерения, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%РесурсыРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляАналитикиИменованные",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеАналитики.ПоляИменованные, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляАналитикиНеИменованные",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеАналитики.ПоляНеИменованные, "Т."));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТОстаткиСебестоимостиБезВидовЗапасов") = 0 Тогда
		Возврат;
	КонецЕсли;
	
	#КонецОбласти 
	
	#Область РаспределениеДанных
	
	УчетПоНазначениям = РасчетСебестоимостиПовтИсп.СебестоимостьТоваровПоНазначениям(ПараметрыРасчета.РасчетныйПериод.НачалоПериода);
	
	// Инициализируем распределение
	ПараметрыРаспределения = РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПараметрыРаспределенияМетодомУменьшаемогоОстатка(
		"ВТОстаткиСебестоимостиБезВидовЗапасов",
		"ВТОстаткиСВидамиЗапасов",
		РасчетСебестоимостиУниверсальныеАлгоритмы.СоединитьСтроки("Организация, РазделУчета", ОписаниеАналитики.ПереченьПолей));
	
	РаспределяемыеРесурсы = РасчетСебестоимостиУниверсальныеАлгоритмы.УдалитьЭлементыИзСтрокиШаблона(
		ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра,
		"%1Количество");
	
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьЧисловыеПоляРаспределенияМетодомУменьшаемогоОстатка(
		ПараметрыРаспределения,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(РаспределяемыеРесурсы, ""));
		
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПрочиеПоляРаспределенияМетодомУменьшаемогоОстатка(
		ПараметрыРаспределения,
		"Партия, АналитикаУчетаПартий, АналитикаФинансовогоУчета, ВидДеятельностиНДС, АналитикаУчетаНоменклатуры",
		"ВидЗапасов" + ?(УчетПоНазначениям, ", АналитикаУчетаНоменклатуры", ""));
		
	// Распределяем
	РасчетСебестоимостиПрикладныеАлгоритмы.РаспределитьМетодомУменьшаемогоОстаткаСЗамеромВремени(
		ПараметрыРасчета,
		ПараметрыРаспределения,
		"РаспределитьМетодомУменьшаемогоОстатка",
		"КорректировкаВключитьУчетПоВидамЗапасовСебестоимостьТоваров");
	
	Если ПараметрыРаспределения.РезультатыРаспределения.РазмерРезультата = 0 Тогда
		Возврат; // нет распределенных данных
	КонецЕсли;
		
	Если НЕ УчетПоНазначениям Тогда
		ЕстьКорректировки = СформироватьАналитикиНоменклатурыБезНазначения(
			ПараметрыРасчета,
			"ВТРезультатРаспределения",
			"ВТРезультатРаспределенияСкорректированный");
	Иначе
		ЕстьКорректировки = Ложь;
	КонецЕсли;
	
	#КонецОбласти
	
	// Добавляем результаты распределения к корректировочным движениям
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		?(ЕстьКорректировки, "ВТРезультатРаспределенияСкорректированный", "ВТРезультатРаспределения") + ", ВТНераспределенныеДанныеИсточника",
		"ВТОстаткиСебестоимостиБезВидовЗапасов",
		Истина,
		Истина);
	
КонецПроцедуры

// Выполняет включение учета по назначениям - заполняет назначения в аналитике номенклатуры.
//
Процедура КорректировкаВключитьУчетПоНазначениямСебестоимостьТоваров(ПараметрыРасчета, ПараметрыКорректировки)
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		Перечисления.ТипыЗаписейПартий.СлужебноеВключитьУчетПоНазначениям);
	
	Если НЕ ПараметрыКорректировки.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	#Область ПодготовкаДанных
	
	ОписаниеАналитики = СформироватьОписаниеКлючейАналитикУчетаНоменклатуры(, "Назначение");
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	Запрос.УстановитьПараметр("ОрганизацииДляКорректировки", ПараметрыКорректировки.ОрганизацииДляКорректировки);
	
	// Выберем данные для распределения
	Запрос.Текст =
	"ВЫБРАТЬ
	|	%ИзмеренияРегистра,
	|	%РесурсыРегистра,
	|	%ПоляАналитикиИменованные
	|ПОМЕСТИТЬ ВТОстаткиСебестоимостиБезНазначений
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И Т.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИзмеренияРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%РесурсыРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляАналитикиИменованные",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеАналитики.ПоляИменованные, "Т."));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТОстаткиСебестоимостиБезНазначений") = 0 Тогда
		Возврат; // нет распределенных данных
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Ключи.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТАналитикиСНазначениями
	|ИЗ
	|	Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
	|ГДЕ
	|	Ключи.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация						КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов 						КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи)
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|		КОГДА ЕСТЬNULL(Т.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		КОГДА Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	КОНЕЦ								КАК РазделУчета,
	|	Т.КоличествоОстаток					КАК Количество
	|ПОМЕСТИТЬ ВТОстаткиСНазначениямиПредварительная
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииДляКорректировки)
	|		И АналитикаУчетаНоменклатуры В (ВЫБРАТЬ Т.Ссылка ИЗ ВТАналитикиСНазначениями КАК Т)
	|	) КАК Т
	|ГДЕ
	|	Т.КоличествоОстаток > 0
	|	ИЛИ Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Организация						КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов 						КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию)
	|	КОНЕЦ								КАК РазделУчета,
	|	Т.КоличествоОстаток					КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеНаКомиссию.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииДляКорректировки)
	|		И АналитикаУчетаНоменклатуры В (ВЫБРАТЬ Т.Ссылка ИЗ ВТАналитикиСНазначениями КАК Т)
	|	) КАК Т
	|ГДЕ
	|	Т.КоличествоОстаток > 0
	|
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.ВидЗапасов.Организация			КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов 						КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|		КОГДА ЕСТЬNULL(Т.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	КОНЕЦ								КАК РазделУчета,
	|	Т.КоличествоОстаток					КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюДокументовИмпорта.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		ВидЗапасов.Организация В (&ОрганизацииДляКорректировки)
	|		И АналитикаУчетаНоменклатуры В (ВЫБРАТЬ Т.Ссылка ИЗ ВТАналитикиСНазначениями КАК Т)
	|		И ТипДокументаИмпорта = &ТипДокументаИмпорта
	|	) КАК Т
	|ГДЕ
	|	Т.КоличествоОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.РазделУчета,
	|	%ПоляАналитикиИменованные,
	|	СУММА(Т.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТОстаткиСНазначениями
	|ИЗ
	|	ВТОстаткиСНазначениямиПредварительная КАК Т
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.РазделУчета,
	|	%ПоляАналитикиНеИменованные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.Номенклатура КАК АналитикаУчетаНоменклатуры_Номенклатура,
	|	Т.Характеристика КАК АналитикаУчетаНоменклатуры_Характеристика,
	|	Т.Серия КАК АналитикаУчетаНоменклатуры_Серия,
	|	Т.Назначение КАК АналитикаУчетаНоменклатуры_Назначение,
	|	Т.Подразделение КАК АналитикаУчетаНоменклатуры_МестоХранения,
	|	&ПустаяСтатьяКалькуляции КАК АналитикаУчетаНоменклатуры_СтатьяКалькуляции,
	|	Т.КоличествоОстаток КАК Количество
	|ПОМЕСТИТЬ ВТОстаткиМатериалыИРаботыБезАналитик
	|ИЗ
	|	РегистрНакопления.МатериалыИРаботыВПроизводстве.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&МассивОрганизаций)
	|		И Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	) КАК Т
	|ГДЕ
	|	Т.КоличествоОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТОстаткиСНазначениямиПредварительная";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляАналитикиИменованные",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеАналитики.ПоляИменованные, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляАналитикиНеИменованные",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеАналитики.ПоляНеИменованные, "Т."));
	
	Запрос.УстановитьПараметр("ПустаяСтатьяКалькуляции", "");
	Запрос.УстановитьПараметр("ТипДокументаИмпорта", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ТаможеннаяДекларацияИмпорт"));
		
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	#КонецОбласти 
	
	#Область РаспределениеДанных
	
	// Инициализируем распределение
	ПараметрыРаспределения = РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПараметрыРаспределенияМетодомУменьшаемогоОстатка(
		"ВТОстаткиСебестоимостиБезНазначений",
		"ВТОстаткиСНазначениями",
		РасчетСебестоимостиУниверсальныеАлгоритмы.СоединитьСтроки("Организация, РазделУчета, ВидЗапасов", ОписаниеАналитики.ПереченьПолей));
		
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьТаблицыРезультатовРаспределения(
		ПараметрыРаспределения,
		"ВТРезультатРаспределения",
		"НераспределенныеОстаткиСебестоимости");
		
	РаспределяемыеРесурсы = РасчетСебестоимостиУниверсальныеАлгоритмы.УдалитьЭлементыИзСтрокиШаблона(
		ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра,
		"%1Количество");
	
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьЧисловыеПоляРаспределенияМетодомУменьшаемогоОстатка(
		ПараметрыРаспределения,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(РаспределяемыеРесурсы, ""));
		
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПрочиеПоляРаспределенияМетодомУменьшаемогоОстатка(
		ПараметрыРаспределения,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра, ""),
		"АналитикаУчетаНоменклатуры");
	
	// Распределяем
	РасчетСебестоимостиПрикладныеАлгоритмы.РаспределитьМетодомУменьшаемогоОстаткаСЗамеромВремени(
		ПараметрыРасчета,
		ПараметрыРаспределения,
		"РаспределитьМетодомУменьшаемогоОстатка",
		"КорректировкаВключитьУчетПоНазначениямСебестоимостьТоваров_ОперативныйУчет");
	
	ТребуетсяРаспределениеМатериаловИРабот =
		РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТОстаткиМатериалыИРаботыБезАналитик") > 0
		И РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "НераспределенныеОстаткиСебестоимости") > 0;
	
	// Добавляем результаты распределения к корректировочным движениям
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		"ВТРезультатРаспределения" + ?(ТребуетсяРаспределениеМатериаловИРабот, "", ", НераспределенныеОстаткиСебестоимости"),
		"ВТОстаткиСебестоимостиБезНазначений",
		Истина,
		Истина);
		
	Если НЕ ТребуетсяРаспределениеМатериаловИРабот Тогда
		Возврат;
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ПодготовкаДанныхМатериалыИРаботы
	
	СформироватьАналитикиНоменклатурыПоЗначениямПолей(
		ПараметрыРасчета,
		"ВТОстаткиМатериалыИРаботыБезАналитик",
		"ВТОстаткиМатериалыИРаботыСАналитиками",
		"АналитикаУчетаНоменклатуры");
	
	#КонецОбласти
	
	#Область РаспределениеДанныхДанныхМатериалыИРаботы
	
	// Инициализируем распределение
	ПараметрыРаспределения = РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПараметрыРаспределенияМетодомУменьшаемогоОстатка(
		"НераспределенныеОстаткиСебестоимости",
		"ВТОстаткиМатериалыИРаботыСАналитиками",
		РасчетСебестоимостиУниверсальныеАлгоритмы.СоединитьСтроки("Организация", ОписаниеАналитики.ПереченьПолей));
		
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьЧисловыеПоляРаспределенияМетодомУменьшаемогоОстатка(
		ПараметрыРаспределения,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(РаспределяемыеРесурсы, ""));
		
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПрочиеПоляРаспределенияМетодомУменьшаемогоОстатка(
		ПараметрыРаспределения,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра, ""),
		"АналитикаУчетаНоменклатуры");
		
	// Распределяем
	РасчетСебестоимостиПрикладныеАлгоритмы.РаспределитьМетодомУменьшаемогоОстаткаСЗамеромВремени(
		ПараметрыРасчета,
		ПараметрыРаспределения,
		"РаспределитьМетодомУменьшаемогоОстатка",
		"КорректировкаВключитьУчетПоНазначениямСебестоимостьТоваров_Производство");
	
	// Добавляем результаты распределения к корректировочным движениям
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		"ВТРезультатРаспределения, ВТНераспределенныеДанныеИсточника",
		,
		Истина);
	
	#КонецОбласти
	
КонецПроцедуры

// Выполняет отключение учета по назначениям - очищает назначения в аналитике номенклатуры.
//
Процедура КорректировкаОтключитьУчетПоНазначениямСебестоимостьТоваров(ПараметрыРасчета, ПараметрыКорректировки)
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		Перечисления.ТипыЗаписейПартий.СлужебноеОтключитьУчетПоНазначениям);
	
	Если НЕ ПараметрыКорректировки.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	#Область ПодготовкаДанных
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	Запрос.УстановитьПараметр("ОрганизацииДляКорректировки", ПараметрыКорректировки.ОрганизацииДляКорректировки);
	
	// Выберем данные для корректировки
	Запрос.Текст =
	"ВЫБРАТЬ
	|	%ИзмеренияРегистра,
	|	%РесурсыРегистра
	|ПОМЕСТИТЬ ВТОстаткиСебестоимостиСНазначением
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИзмеренияРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%РесурсыРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра, "Т."));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	ЕстьКорректировки = СформироватьАналитикиНоменклатурыБезНазначения(
		ПараметрыРасчета,
		"ВТОстаткиСебестоимостиСНазначением",
		"ВТОстаткиСебестоимостиБезНазначения");
	
	Если НЕ ЕстьКорректировки Тогда
		Возврат;
	КонецЕсли;
	
	#КонецОбласти 
	
	// Добавляем результаты распределения к корректировочным движениям
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		"ВТОстаткиСебестоимостиБезНазначения",
		"ВТОстаткиСебестоимостиСНазначением",
		Истина,
		Истина);
	
КонецПроцедуры

// Выполняет безусловную корректировку аналитики номенклатуры - заменяет в аналитике партнера на его договор.
//
Процедура КорректировкаЗаменитьПартнераНаДоговорВАналитикеУчетаНоменклатурыСебестоимостьТоваров(ПараметрыРасчета, ПараметрыКорректировки)
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		Перечисления.ТипыЗаписейПартий.СлужебноеИзменениеПартнераВАналитикеУчетаНоменклатуры);
	
	Если НЕ ПараметрыКорректировки.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	#Область ПодготовкаДанных
	
	ОписаниеАналитики = СформироватьОписаниеКлючейАналитикУчетаНоменклатуры(, "МестоХранения");
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	Запрос.УстановитьПараметр("ОрганизацииДляКорректировки", ПараметрыКорректировки.ОрганизацииДляКорректировки);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.АналитикаУчетаНоменклатуры.Партнер КАК Партнер,
	|	&ПоляАналитикиИменованные,
	|	&ИзмеренияРегистра,
	|	&РесурсыРегистра
	|ПОМЕСТИТЬ ВТОстаткиСАналитикойПартнер
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|	И Т.АналитикаУчетаНоменклатуры.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|	И Т.РазделУчета В
	|		(ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки),
	|		 ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути))";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИзмеренияРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&РесурсыРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляАналитикиИменованные",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеАналитики.ПоляИменованные, "Т."));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТОстаткиСАналитикойПартнер") = 0 Тогда
		Возврат; // нет данных для распределения
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.АналитикаУчетаНоменклатуры.Договор.Партнер КАК Партнер,
	|	&ПоляАналитикиИменованные,
	|	Т.Организация 								 КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры 				 КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов 								 КАК ВидЗапасов,
	|	МАКСИМУМ(Т.Количество)				 		 КАК Количество
	|ПОМЕСТИТЬ ВТОстаткиСАналитикойДоговор
	|ИЗ
	|	(ВЫБРАТЬ
	|		Т.Организация				 КАК Организация,
	|		Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		Т.ВидЗапасов				 КАК ВидЗапасов,
	|		Т.КоличествоОстаток			 КАК Количество
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций.Остатки(&ГраницаКонецПредыдущегоПериода,
	|			Организация В (&ОрганизацииДляКорректировки)
	|			И АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.ДоговорКонтрагента)
	|			И АналитикаУчетаНоменклатуры.Договор <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаУчетаНоменклатуры.Договор.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|		) КАК Т
	|	ГДЕ
	|		Т.КоличествоОстаток > 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Т.Организация				 КАК Организация,
	|		Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		Т.ВидЗапасов				 КАК ВидЗапасов,
	|		Т.КоличествоОстаток			 КАК Количество
	|	ИЗ
	|		РегистрНакопления.ТоварыКОформлениюДокументовИмпорта.Остатки(&ГраницаКонецПредыдущегоПериода,
	|			Организация В (&ОрганизацииДляКорректировки)
	|			И АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.ДоговорКонтрагента)
	|			И АналитикаУчетаНоменклатуры.Договор <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаУчетаНоменклатуры.Договор.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|		) КАК Т
	|	ГДЕ
	|		Т.КоличествоОстаток > 0
	|	) КАК Т
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляАналитикиИменованные",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеАналитики.ПоляИменованные, "Т."));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	#КонецОбласти 
	
	#Область РаспределениеДанных
	
	// Инициализируем распределение
	ПараметрыРаспределения = РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПараметрыРаспределенияМетодомУменьшаемогоОстатка(
		"ВТОстаткиСАналитикойПартнер",
		"ВТОстаткиСАналитикойДоговор",
		РасчетСебестоимостиУниверсальныеАлгоритмы.СоединитьСтроки("Партнер, Организация, ВидЗапасов", ОписаниеАналитики.ПереченьПолей));
	
	РаспределяемыеРесурсы = РасчетСебестоимостиУниверсальныеАлгоритмы.УдалитьЭлементыИзСтрокиШаблона(
		ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра,
		"%1Количество");
	
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьЧисловыеПоляРаспределенияМетодомУменьшаемогоОстатка(
		ПараметрыРаспределения,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(РаспределяемыеРесурсы, ""));
		
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПрочиеПоляРаспределенияМетодомУменьшаемогоОстатка(
		ПараметрыРаспределения,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра, ""),
		"АналитикаУчетаНоменклатуры");
	
	// Распределяем
	РасчетСебестоимостиПрикладныеАлгоритмы.РаспределитьМетодомУменьшаемогоОстаткаСЗамеромВремени(
		ПараметрыРасчета,
		ПараметрыРаспределения,
		"РаспределитьМетодомУменьшаемогоОстатка",
		"КорректировкаЗаменитьПартнераНаДоговорВАналитикеУчетаНоменклатурыСебестоимостьТоваров");
	
	Если ПараметрыРаспределения.РезультатыРаспределения.РазмерРезультата = 0 Тогда
		Возврат; // нет распределенных данных
	КонецЕсли;
		
	#КонецОбласти
	
	// Добавляем результаты распределения к корректировочным движениям
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		"ВТРезультатРаспределения, ВТНераспределенныеДанныеИсточника",
		"ВТОстаткиСАналитикойПартнер",
		Истина,
		Истина);
	
КонецПроцедуры

// Выполняет включение упр. учета - заполняет упр. ресурсы регистра.
//
Процедура КорректировкаЗаполнитьСуммыУпрУчетаСебестоимостьТоваров(ПараметрыРасчета, ПараметрыКорректировки)
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		Перечисления.ТипыЗаписейПартий.СлужебноеВключитьУправленческийУчетПоПравиламМФУ);
	
	Если НЕ ПараметрыКорректировки.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	#Область ПодготовкаДанных
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	Запрос.УстановитьПараметр("ОрганизацииДляКорректировки", ПараметрыКорректировки.ОрганизацииДляКорректировки);
	
	// Выберем данные для формирования начальных остатков
	Запрос.Текст =
	"ВЫБРАТЬ
	|	%ИзмеренияРегистра,
	|	- Т.СтоимостьУпр + Т.СтоимостьБезНДС 						 КАК СтоимостьУпр,
	|	- Т.ДопРасходыУпр + Т.ДопРасходыБезНДС 						 КАК ДопРасходыУпр,
	|	- Т.ТрудозатратыУпр + Т.Трудозатраты 						 КАК ТрудозатратыУпр,
	|	- Т.ПостатейныеПостоянныеУпр + Т.ПостатейныеПостоянныеБезНДС КАК ПостатейныеПостоянныеУпр,
	|	- Т.ПостатейныеПеременныеУпр + Т.ПостатейныеПеременныеБезНДС КАК ПостатейныеПеременныеУпр
	|ПОМЕСТИТЬ ВТОстаткиСуммыУпр
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров КАК Т
	|ГДЕ
	|	- Т.СтоимостьУпр + Т.СтоимостьБезНДС <> 0
	|	ИЛИ - Т.ДопРасходыУпр + Т.ДопРасходыБезНДС <> 0
	|	ИЛИ - Т.ТрудозатратыУпр + Т.Трудозатраты <> 0
	|	ИЛИ - Т.ПостатейныеПостоянныеУпр + Т.ПостатейныеПостоянныеБезНДС <> 0
	|	ИЛИ - Т.ПостатейныеПеременныеУпр + Т.ПостатейныеПеременныеБезНДС <> 0";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИзмеренияРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра, "Т."));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	#КонецОбласти 
	
	// Добавляем данные к корректировочным движениям
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		"ВТОстаткиСуммыУпр",
		,
		Истина);
	
КонецПроцедуры

// Выполняет отключение упр. учета - очищает упр. ресурсы регистра.
//
Процедура КорректировкаОчиститьСуммыУпрУчетаСебестоимостьТоваров(ПараметрыРасчета, ПараметрыКорректировки)
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		Перечисления.ТипыЗаписейПартий.СлужебноеОтключитьУправленческийУчетПоПравиламМФУ);
	
	Если НЕ ПараметрыКорректировки.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	#Область ПодготовкаДанных
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	Запрос.УстановитьПараметр("ОрганизацииДляКорректировки", ПараметрыКорректировки.ОрганизацииДляКорректировки);
	
	// Выберем данные для очистки остатков сумм упр. учета
	Запрос.Текст =
	"ВЫБРАТЬ
	|	%ИзмеренияРегистра,
	|	- Т.СтоимостьУпр 			 КАК СтоимостьУпр,
	|	- Т.ДопРасходыУпр 			 КАК ДопРасходыУпр,
	|	- Т.ТрудозатратыУпр 		 КАК ТрудозатратыУпр,
	|	- Т.ПостатейныеПостоянныеУпр КАК ПостатейныеПостоянныеУпр,
	|	- Т.ПостатейныеПеременныеУпр КАК ПостатейныеПеременныеУпр
	|ПОМЕСТИТЬ ВТОстаткиОчисткаСуммУпр
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров КАК Т
	|ГДЕ
	|	- Т.СтоимостьУпр <> 0
	|	ИЛИ - Т.ДопРасходыУпр <> 0
	|	ИЛИ - Т.ТрудозатратыУпр <> 0
	|	ИЛИ - Т.ПостатейныеПостоянныеУпр <> 0
	|	ИЛИ - Т.ПостатейныеПеременныеУпр <> 0";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИзмеренияРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра, "Т."));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	#КонецОбласти 
	
	// Добавляем данные к корректировочным движениям
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		"ВТОстаткиОчисткаСуммУпр",
		,
		Истина);
	
КонецПроцедуры

// Выполняет заполнение/очистку партий в регистре Себестоимость товаров при изменении метода оценки. 
// Переход с/на ФИФОВзвешенная не поддерживается.
//
Процедура СформироватьКорректировочныеДвиженияСебестоимостьПриИзмененииУчетнойПолитики(ПараметрыРасчета, ПараметрыКорректировки)
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		Перечисления.ТипыЗаписейПартий.СлужебноеИзменениеМетодаОценкиСтоимости);
	
	Если НЕ ПараметрыКорректировки.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.Ссылка КАК Организация
	|ПОМЕСТИТЬ ВТОрганизацииСПереходом22_24
	|ИЗ
	|	Справочник.Организации КАК Т
	|ГДЕ
	|	Т.Ссылка В(&ОрганизацииСУчетомПартийНДСВерсии22ВПрошломПериоде)
	|	И Т.Ссылка В(&ОрганизацииСУчетомПартийНДСВерсии24)
	|	И НЕ Т.Ссылка В(&ОрганизацииСреднескользящая)
	|	И НЕ Т.Ссылка В(&ОрганизацииСреднескользящаяВПрошломПериоде)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Ссылка КАК Организация
	|ПОМЕСТИТЬ ВТОрганизацииСПереходом24_22
	|ИЗ
	|	Справочник.Организации КАК Т
	|ГДЕ
	|	Т.Ссылка В(&ОрганизацииСУчетомПартийНДСВерсии24ВПрошломПериоде)
	|	И Т.Ссылка В(&ОрганизацииСУчетомПартийНДСВерсии22)
	|	И НЕ Т.Ссылка В(&ОрганизацииСреднескользящая)
	|	И НЕ Т.Ссылка В(&ОрганизацииСреднескользящаяВПрошломПериоде)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Ссылка КАК Организация
	|ПОМЕСТИТЬ ВТОрганизацииСПереходомНаСреднескользящую
	|ИЗ
	|	Справочник.Организации КАК Т
	|ГДЕ
	|	Т.Ссылка В(&ОрганизацииСреднескользящая)
	|	И НЕ Т.Ссылка В(&ОрганизацииСреднескользящаяВПрошломПериоде)
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТОрганизацииСПереходом22_24") = 0
	 И РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТОрганизацииСПереходом24_22") = 0
	 И РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТОрганизацииСПереходомНаСреднескользящую") = 0 Тогда
		Возврат; // нет данных для распределения
	КонецЕсли;
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТОрганизацииСПереходом22_24") > 0 Тогда
	
		#Область ПодготовкаДанных
		
		ПараметрыКорректировкиНДССредняя = РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьКорректировкуОстатковРегистра(
			ПараметрыРасчета,
			Метаданные.РегистрыНакопления.ДетализацияПартийТоваровДляНДСиУСН.ПолноеИмя());
		
		РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
			ПараметрыРасчета,
			ПараметрыКорректировкиНДССредняя,
			Перечисления.ТипыЗаписейПартий.СлужебноеИзменениеМетодаОценкиСтоимости);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Т.Организация,
		|	Т.РазделУчета,
		|	ВЫРАЗИТЬ(Т.АналитикаУчетаНоменклатуры КАК Справочник.КлючиАналитикиУчетаНоменклатуры) КАК АналитикаУчетаНоменклатуры,
		|	Т.ВидЗапасов,
		|	Т.ВидДеятельностиНДС,
		|	Т.Партия,
		|	Т.АналитикаФинансовогоУчета,
		|	Т.Количество
		|ПОМЕСТИТЬ ВТОстаткиПартийНДССредняяПредварительная
		|ИЗ
		|	ВТРасчетныеНачальныеОстаткиДетализацияПартийТоваровДляНДСиУСН КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОрганизацииСПереходом22_24 КАК Отбор
		|		ПО Т.Организация = Отбор.Организация
		|ГДЕ
		|	Т.Партия <> НЕОПРЕДЕЛЕНО
		|	И Т.Количество <> 0
		|ИНДЕКСИРОВАТЬ ПО
		|	Партия,
		|	АналитикаУчетаНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Партия,
		|	Т.АналитикаУчетаНоменклатуры,
		|	Т.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|	Т.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	Т.АналитикаУчетаНоменклатуры.Серия КАК Серия
		|ПОМЕСТИТЬ ВТПартииНДССредняя
		|ИЗ
		|	ВТОстаткиПартийНДССредняяПредварительная КАК Т
		|ИНДЕКСИРОВАТЬ ПО
		|	Партия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Партия КАК Партия,
		|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	МАКСИМУМ(ИсходныеПартии.КорАналитикаУчетаПартий) КАК АналитикаУчетаПартий
		|ПОМЕСТИТЬ ВТАналитикиУчетаПартийНДССредняя
		|ИЗ
		|	ВТПартииНДССредняя КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК ИсходныеПартии
		|		ПО ИсходныеПартии.Регистратор = Т.Партия
		|		И ИсходныеПартии.АналитикаУчетаНоменклатуры.Номенклатура = Т.Номенклатура
		|		И ИсходныеПартии.АналитикаУчетаНоменклатуры.Характеристика = Т.Характеристика
		|		И ИсходныеПартии.АналитикаУчетаНоменклатуры.Серия = Т.Серия
		|СГРУППИРОВАТЬ ПО
		|	Т.Партия,
		|	Т.АналитикаУчетаНоменклатуры
		|ИНДЕКСИРОВАТЬ ПО
		|	Партия,
		|	АналитикаУчетаНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Организация,
		|	Т.РазделУчета,
		|	Т.АналитикаУчетаНоменклатуры,
		|	Т.ВидЗапасов,
		|	Т.ВидДеятельностиНДС,
		|	Т.Партия,
		|	ЕСТЬNULL(Аналитики.АналитикаУчетаПартий, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)) КАК АналитикаУчетаПартий,
		|	Т.АналитикаФинансовогоУчета,
		|	Т.Количество
		|ПОМЕСТИТЬ ВТОстаткиПартийНДССредняя
		|ИЗ
		|	ВТОстаткиПартийНДССредняяПредварительная КАК Т
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТАналитикиУчетаПартийНДССредняя КАК Аналитики
		|		ПО Т.Партия = Аналитики.Партия
		|		И Т.АналитикаУчетаНоменклатуры = Аналитики.АналитикаУчетаНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Организация,
		|	Т.РазделУчета,
		|	Т.АналитикаУчетаНоменклатуры,
		|	Т.ВидЗапасов,
		|	Т.ВидДеятельностиНДС,
		|	Т.АналитикаФинансовогоУчета,
		|	&РесурсыРегистра
		|ПОМЕСТИТЬ ВТОстаткиПартийСебестоимостиСредняя
		|ИЗ
		|	ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОрганизацииСПереходом22_24 КАК Отбор
		|		ПО Т.Организация = Отбор.Организация
		|ГДЕ
		|	Т.Партия = НЕОПРЕДЕЛЕНО
		|	И Т.АналитикаУчетаПартий = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
		|	И Т.Количество <> 0
		|";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&РесурсыРегистра",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра, "Т."));
		
		РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
		
		#КонецОбласти
		
		#Область РаспределениеДанных
		
		// Инициализируем распределение
		ПараметрыРаспределения = РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПараметрыРаспределенияМетодомУменьшаемогоОстатка(
			"ВТОстаткиПартийСебестоимостиСредняя",
			"ВТОстаткиПартийНДССредняя",
			"Организация, РазделУчета, АналитикаУчетаНоменклатуры, ВидЗапасов, ВидДеятельностиНДС, АналитикаФинансовогоУчета");
			
		РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьТаблицыРезультатовРаспределения(
			ПараметрыРаспределения,
			"ВТРезультатРаспределенияСредняяФИФО");
			
		РаспределяемыеРесурсы = РасчетСебестоимостиУниверсальныеАлгоритмы.УдалитьЭлементыИзСтрокиШаблона(
			ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра,
			"%1Количество");
		
		РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьЧисловыеПоляРаспределенияМетодомУменьшаемогоОстатка(
			ПараметрыРаспределения,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(РаспределяемыеРесурсы, ""));
			
		РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПрочиеПоляРаспределенияМетодомУменьшаемогоОстатка(
			ПараметрыРаспределения,
			,
			"Партия, АналитикаУчетаПартий");
		
		// Распределяем
		РасчетСебестоимостиПрикладныеАлгоритмы.РаспределитьМетодомУменьшаемогоОстаткаСЗамеромВремени(
			ПараметрыРасчета,
			ПараметрыРаспределения,
			"РаспределитьМетодомУменьшаемогоОстатка",
			"СформироватьКорректировочныеДвиженияСебестоимостьПриИзмененииУчетнойПолитики");
			
		// Сформируем записи сторно остатков себестоимости без партий
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Т.Организация,
		|	Т.РазделУчета,
		|	Т.АналитикаУчетаНоменклатуры,
		|	Т.ВидЗапасов,
		|	Т.ВидДеятельностиНДС,
		|	Т.АналитикаФинансовогоУчета,
		// заполним кор. часть для списание будущих доп. расходов на выбытия в прошлых периодах
		|	Т.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
		|	Т.ВидЗапасов КАК КорВидЗапасов,
		|	Т.РазделУчета КАК КорРазделУчета,
		|	Т.Партия КАК КорПартия,
		|	Т.АналитикаУчетаПартий КАК КорАналитикаУчетаПартий,
		|	Т.АналитикаФинансовогоУчета КАК КорАналитикаФинансовогоУчета,
		|	&РесурсыРегистра
		|ПОМЕСТИТЬ ВТСторноОстаткиПартийСебестоимостиСредняя
		|ИЗ
		|	ВТРезультатРаспределенияСредняяФИФО КАК Т";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&РесурсыРегистра",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра, "Т."));
		
		РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
		
		// Добавляем результаты распределения к корректировочным движениям
		РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
			ПараметрыРасчета,
			ПараметрыКорректировки,
			"ВТРезультатРаспределенияСредняяФИФО",
			"ВТСторноОстаткиПартийСебестоимостиСредняя",
			Истина,
			Истина);
			
		#КонецОбласти
		
	КонецЕсли;

	Если РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТОрганизацииСПереходом24_22") > 0 Тогда
	
		#Область ПодготовкаДанных
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Т.Организация,
		|	Т.РазделУчета,
		|	Т.АналитикаУчетаНоменклатуры,
		|	Т.ВидЗапасов,
		|	Т.ВидДеятельностиНДС,
		|	&РесурсыРегистра
		|ПОМЕСТИТЬ ВТОстаткиПартийСебестоимостиФИФО
		|ИЗ
		|	ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОрганизацииСПереходом24_22 КАК Отбор
		|		ПО Т.Организация = Отбор.Организация
		|ГДЕ
		|	Т.Партия <> НЕОПРЕДЕЛЕНО
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Организация,
		|	Т.РазделУчета,
		|	Т.АналитикаУчетаНоменклатуры,
		|	Т.ВидЗапасов,
		|	Т.ВидДеятельностиНДС,
		|	Т.Партия,
		|	Т.АналитикаУчетаПартий,
		|	Т.АналитикаФинансовогоУчета,
		|	&РесурсыРегистра
		|ПОМЕСТИТЬ ВТСторноОстаткиПартийСебестоимостиФИФО
		|ИЗ
		|	ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОрганизацииСПереходом24_22 КАК Отбор
		|		ПО Т.Организация = Отбор.Организация
		|ГДЕ
		|	Т.Партия <> НЕОПРЕДЕЛЕНО
		|";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&РесурсыРегистра",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра, "Т."));
		
		РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
		
		РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
			ПараметрыРасчета,
			ПараметрыКорректировки,
			"ВТОстаткиПартийСебестоимостиФИФО",
			"ВТСторноОстаткиПартийСебестоимостиФИФО",
			Истина,
			Истина);
		
		#КонецОбласти
		
	КонецЕсли;

	Если РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТОрганизацииСПереходомНаСреднескользящую") > 0 Тогда
	
		#Область ПодготовкаДанных
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	АВТОНОМЕРЗАПИСИ() КАК НомерЗаписи,
		|	Т.Организация,
		|	Т.РазделУчета,
		|	Т.АналитикаУчетаНоменклатуры,
		|	Т.ВидЗапасов,
		|	Т.ВидДеятельностиНДС,
		|	Т.Партия,
		|	Т.АналитикаУчетаПартий,
		|	Т.АналитикаФинансовогоУчета,
		|
		|	Т.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|	Т.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	ВЫБОР КОГДА Т.ВидДеятельностиНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
		|		ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ИсключитьНДС,
		|	ВЫБОР КОГДА Т.Организация В(&ОрганизацииСУчетомПартийНДСВерсии24ВПрошломПериоде)
		|		ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПереходСФИФОСкользящая,
        |
		|	Т.Количество,
		|	Т.Стоимость,
		|	Т.СтоимостьБезНДС,
		|	Т.СтоимостьРегл,
		|	Т.СтоимостьУпр,
		|	Т.СтоимостьЗабалансовая,
		|	Т.СтоимостьЗабалансоваяРегл,
		|	Т.ДопРасходы,
		|	Т.ДопРасходыБезНДС,
		|	Т.Трудозатраты,
		|	Т.ПостатейныеПостоянныеСНДС,
		|	Т.ПостатейныеПеременныеСНДС,
		|	Т.ПостатейныеПостоянныеБезНДС,
		|	Т.ПостатейныеПеременныеБезНДС,
		|	Т.ДопРасходыРегл,
		|	Т.ТрудозатратыРегл,
		|	Т.ПостатейныеПостоянныеРегл,
		|	Т.ПостатейныеПеременныеРегл,
		|	Т.ПостояннаяРазница,
		|	Т.ВременнаяРазница,
		|	Т.ДопРасходыУпр,
		|	Т.ТрудозатратыУпр,
		|	Т.ПостатейныеПостоянныеУпр,
		|	Т.ПостатейныеПеременныеУпр,
		|	Т.ПостатейныеПостоянныеУпр,
		|	Т.РезервПодОбесценениеРегл,
		|	Т.РезервПодОбесценениеУпр,
		|	Т.СтоимостьНДД
		|ПОМЕСТИТЬ ВТОстаткиПартийСебестоимостиДоСреднескользящей
		|ИЗ
		|	ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОрганизацииСПереходомНаСреднескользящую КАК Отбор
		|		ПО Т.Организация = Отбор.Организация
		|ГДЕ
		|	Т.Количество <> 0
		|	И (Т.Партия <> НЕОПРЕДЕЛЕНО
		|		ИЛИ Т.АналитикаУчетаПартий <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
		|		ИЛИ Т.АналитикаФинансовогоУчета <> НЕОПРЕДЕЛЕНО
		|		ИЛИ Т.ВидДеятельностиНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка))
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерЗаписи
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.НомерЗаписи,
		|	Т.Партия,
		|	Т.АналитикаУчетаПартий,
		|	Т.Организация,
		|	Т.ВидЗапасов,
		|	Т.Номенклатура,
		|	Т.Характеристика
		|ПОМЕСТИТЬ ВТОтборДетализацияФИФОПостоянные
		|ИЗ
		|	ВТОстаткиПартийСебестоимостиДоСреднескользящей КАК Т
		|ГДЕ
		|	Т.Партия <> НЕОПРЕДЕЛЕНО
		|	И Т.Организация В(&ОрганизацииСУчетомПартийНДСВерсии24ВПрошломПериоде)
		|ИНДЕКСИРОВАТЬ ПО
		|	Партия,
		|	Организация,
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.НомерЗаписи,
		|	Т.Организация,
		|	Т.РазделУчета,
		|	Т.АналитикаУчетаНоменклатуры,
		|	Т.ВидЗапасов,
		|	Т.ВидДеятельностиНДС,
		|	Т.Партия,
		|	Т.АналитикаУчетаПартий,
		|	Т.АналитикаФинансовогоУчета
		|ПОМЕСТИТЬ ВТОтборДетализацияФИФОПеременные
		|ИЗ
		|	ВТОстаткиПартийСебестоимостиДоСреднескользящей КАК Т
		|ГДЕ
		|	Т.Партия <> НЕОПРЕДЕЛЕНО
		|	И Т.Организация В(&ОрганизацииСУчетомПартийНДСВерсии24ВПрошломПериоде)
		|ИНДЕКСИРОВАТЬ ПО
		|	АналитикаУчетаНоменклатуры,
		|	Организация,
		|	Партия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.НомерЗаписи,
		|	Т.Организация,
		|	Т.РазделУчета,
		|	Т.АналитикаУчетаНоменклатуры,
		|	Т.ВидЗапасов,
		|	Т.ВидДеятельностиНДС,
		|	Т.АналитикаФинансовогоУчета
		|ПОМЕСТИТЬ ВТОтборДетализацияСредняя
		|ИЗ
		|	ВТОстаткиПартийСебестоимостиДоСреднескользящей КАК Т
		|ГДЕ
		|	Т.Партия = НЕОПРЕДЕЛЕНО
		|	И Т.Организация В(&ОрганизацииСУчетомПартийНДСВерсии22ВПрошломПериоде)
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	АналитикаУчетаНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.НомерЗаписи,
		|	СУММА(Т.НДСРегл) КАК НДСРегл,
		|	СУММА(Т.НДСУпр) КАК НДСУпр
		|ПОМЕСТИТЬ ВТЦеныПартийНДСДляСреднескользящей
		|ИЗ
		|(ВЫБРАТЬ
		|	Отбор.НомерЗаписи,
		|	Т.НДСРегл,
		|	Т.НДСУпр
		|ИЗ
		|	РегистрСведений.ДетализацияСебестоимостиПартииТоваров КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтборДетализацияФИФОПостоянные КАК Отбор
		|		ПО Т.Партия = Отбор.Партия
		|		И Т.АналитикаУчетаПартий = Отбор.АналитикаУчетаПартий
		|		И Т.Организация = Отбор.Организация
		|		И Т.Номенклатура = Отбор.Номенклатура
		|		И Т.Характеристика = Отбор.Характеристика
		|ГДЕ
		|	Т.ПериодРегистрации < &НачалоПериода
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Отбор.НомерЗаписи,
		|	Т.НДСРегл,
		|	Т.НДСУпр
		|ИЗ
		|	РегистрСведений.ДетализацияСебестоимостиПартииТоваровПостатейныеЗатраты КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтборДетализацияФИФОПостоянные КАК Отбор
		|		ПО Т.Партия = Отбор.Партия
		|		И Т.АналитикаУчетаПартий = Отбор.АналитикаУчетаПартий
		|		И Т.Организация = Отбор.Организация
		|		И Т.Номенклатура = Отбор.Номенклатура
		|		И Т.Характеристика = Отбор.Характеристика
		|ГДЕ
		|	Т.ПериодРегистрации < &НачалоПериода
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Отбор.НомерЗаписи,
		|	Т.НДСРегл,
		|	Т.НДСУпр
		|ИЗ
		|	РегистрСведений.ДетализацияСебестоимостиТоваровПостатейныеЗатраты КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтборДетализацияФИФОПеременные КАК Отбор
		|		ПО Т.Организация = Отбор.Организация
		|		И Т.РазделУчета = Отбор.РазделУчета
		|		И Т.АналитикаУчетаНоменклатуры = Отбор.АналитикаУчетаНоменклатуры
		|		И Т.ВидЗапасов = Отбор.ВидЗапасов
		|		И Т.Партия = Отбор.Партия
		|		И Т.АналитикаУчетаПартий = Отбор.АналитикаУчетаПартий
		|		И Т.ВидДеятельностиНДС = Отбор.ВидДеятельностиНДС
		|		И Т.АналитикаФинансовогоУчета = Отбор.АналитикаФинансовогоУчета
		|ГДЕ
		|	Т.Период = &НачалоПредыдущегоПериода
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		// При учете по средней в ресурсах хранятся суммы, а не цены, как при ФИФО
		|	Отбор.НомерЗаписи,
		|	Т.НДСОстаток КАК НДСРегл,
		|	Т.НДСУпрОстаток КАК НДСУпр
		|ИЗ
		|	РегистрНакопления.ДетализацияПартийТоваровДляНДСиУСН.Остатки(
		|		&ГраницаКонецПредыдущегоПериода,
		|		Организация В(&ОрганизацииСУчетомПартийНДСВерсии22ВПрошломПериоде)) КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтборДетализацияСредняя КАК Отбор
		|		ПО Т.Организация = Отбор.Организация
		|		И Т.РазделУчета = Отбор.РазделУчета
		|		И Т.АналитикаУчетаНоменклатуры = Отбор.АналитикаУчетаНоменклатуры
		|		И Т.ВидЗапасов = Отбор.ВидЗапасов
		|		И Т.ВидДеятельностиНДС = Отбор.ВидДеятельностиНДС
		|		И Т.АналитикаФинансовогоУчета = Отбор.АналитикаФинансовогоУчета
		|) КАК Т
		|СГРУППИРОВАТЬ ПО
		|	Т.НомерЗаписи
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерЗаписи
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Организация,
		|	Т.РазделУчета,
		|	Т.АналитикаУчетаНоменклатуры,
		|	Т.ВидЗапасов,
		|
		|	Т.Количество,
		|	Т.Стоимость,
		|	Т.СтоимостьБезНДС,
		|	Т.СтоимостьРегл,
		|	Т.СтоимостьУпр,
		|	Т.СтоимостьЗабалансовая,
		|	Т.СтоимостьЗабалансоваяРегл,
		|	Т.ДопРасходы,
		|	Т.ДопРасходыБезНДС,
		|	Т.Трудозатраты,
		|	Т.ПостатейныеПостоянныеСНДС,
		|	Т.ПостатейныеПеременныеСНДС,
		|	Т.ПостатейныеПостоянныеБезНДС,
		|	Т.ПостатейныеПеременныеБезНДС,
		|	Т.ДопРасходыРегл,
		|	Т.ТрудозатратыРегл,
		|	Т.ПостатейныеПостоянныеРегл,
		|	Т.ПостатейныеПеременныеРегл,
		|	Т.ПостояннаяРазница,
		|	Т.ВременнаяРазница,
		|	Т.ДопРасходыУпр,
		|	Т.ТрудозатратыУпр,
		|	Т.ПостатейныеПостоянныеУпр,
		|	Т.ПостатейныеПеременныеУпр,
		|	Т.ПостатейныеПостоянныеУпр,
		|	Т.РезервПодОбесценениеРегл,
		|	Т.РезервПодОбесценениеУпр,
		|	Т.СтоимостьНДД,
		|	ВЫРАЗИТЬ(ВЫБОР КОГДА Т.ПереходСФИФОСкользящая
		|		ТОГДА ЕСТЬNULL(Цены.НДСРегл, 0) * Т.Количество
		|		ИНАЧЕ ЕСТЬNULL(Цены.НДСРегл, 0)
		|	КОНЕЦ КАК ЧИСЛО (31,2)) КАК НДСРегл,
		|	ВЫРАЗИТЬ(ВЫБОР КОГДА Т.ПереходСФИФОСкользящая
		|		ТОГДА ЕСТЬNULL(Цены.НДСУпр, 0) * Т.Количество
		|		ИНАЧЕ ЕСТЬNULL(Цены.НДСУпр, 0)
		|	КОНЕЦ КАК ЧИСЛО (31,2)) КАК НДСУпр
		|ПОМЕСТИТЬ ВТПартииСебестоимостиСреднескользящей
		|ИЗ
		|	ВТОстаткиПартийСебестоимостиДоСреднескользящей КАК Т
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЦеныПартийНДСДляСреднескользящей КАК Цены
		|		ПО Т.НомерЗаписи = Цены.НомерЗаписи
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Организация,
		|	Т.РазделУчета,
		|	Т.АналитикаУчетаНоменклатуры,
		|	Т.ВидЗапасов,
		|	Т.ВидДеятельностиНДС,
		|	Т.Партия,
		|	Т.АналитикаУчетаПартий,
		|	Т.АналитикаФинансовогоУчета,
		|	&РесурсыРегистра
		|ПОМЕСТИТЬ ВТСторноОстаткиПартийСебестоимостиДоСреднескользящей
		|ИЗ
		|	ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОрганизацииСПереходомНаСреднескользящую КАК Отбор
		|		ПО Т.Организация = Отбор.Организация
		|ГДЕ
		|	Т.Партия <> НЕОПРЕДЕЛЕНО
		|	ИЛИ Т.АналитикаУчетаПартий <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
		|	ИЛИ Т.АналитикаФинансовогоУчета <> НЕОПРЕДЕЛЕНО
		|	ИЛИ Т.ВидДеятельностиНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&РесурсыРегистра",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра, "Т."));
		
		РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
		
		РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
			ПараметрыРасчета,
			ПараметрыКорректировки,
			"ВТПартииСебестоимостиСреднескользящей",
			"ВТСторноОстаткиПартийСебестоимостиДоСреднескользящей",
			Истина,
			Истина);
		
		#КонецОбласти
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДетализацииПартийНДС

// Выполняет все необходимые корректировки регистров ДетализацииПартийНДС...
//
Процедура КорректировкаНачальныхОстатковРегистровДетализацииПартийНДС(ПараметрыРасчета)
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "КорректировкаНачальныхОстатковРегистровДетализацииПартийНДС");
	
	ПараметрыКорректировкиНДССредняя = РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		Метаданные.РегистрыНакопления.ДетализацияПартийТоваровДляНДСиУСН.ПолноеИмя());
		
	ПараметрыКорректировкиФИФОСтарыйРегистр1 = РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		Метаданные.РегистрыНакопления.ДетализацияПартийТоваровДляНДСиУСН2_4.ПолноеИмя()); // используется для очистки прошлых корректировок
		
	ПараметрыКорректировкиФИФОСтарыйРегистр2 = РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		Метаданные.РегистрыСведений.ДетализацияСебестоимостиТоваров.ПолноеИмя()); // используется для очистки прошлых корректировок
	
	ПараметрыКорректировкиПартииТоваровФИФО = РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		Метаданные.РегистрыСведений.ДетализацияСебестоимостиПартииТоваров.ПолноеИмя());
	
	Если ПараметрыРасчета.ФормироватьНачальныеОстаткиПартий22 Тогда
		СформироватьНачальныеОстаткиПартийНДСиУСН(ПараметрыРасчета, ПараметрыКорректировкиНДССредняя, ПараметрыКорректировкиПартииТоваровФИФО);
	КонецЕсли;
	
	Если НЕ ПараметрыРасчета.ФормироватьНачальныеОстаткиПартий22 Тогда
		СформироватьКорректировочныеДвиженияПартийНДСПриИзмененииУчетнойПолитики(ПараметрыРасчета, ПараметрыКорректировкиПартииТоваровФИФО, ПараметрыКорректировкиНДССредняя);
		КорректировкаПереходНаНовыйРегистрДетализацияСебестоимостиТоваров(ПараметрыРасчета, ПараметрыКорректировкиПартииТоваровФИФО);
		// При переходе на ПУ 2.2 такая корректировка уже выполняется.
		КорректировкаВключитьУчетПоВидамЗапасовПартииНДСиУСН(ПараметрыРасчета, ПараметрыКорректировкиНДССредняя);
	КонецЕсли;
	
	Если РасчетСебестоимостиПовтИсп.СебестоимостьТоваровПоНазначениям(ПараметрыРасчета.РасчетныйПериод.НачалоПериода) Тогда
		Если РасчетСебестоимостиПовтИсп.ДатаВключенияОбособленногоУчетаСебестоимостиПоНазначениям() = ПараметрыРасчета.РасчетныйПериод.НачалоПериода Тогда
			КорректировкаВключитьУчетПоНазначениямПартииНДСиУСН(ПараметрыРасчета, ПараметрыКорректировкиНДССредняя);
		КонецЕсли;
	Иначе
		КорректировкаОтключитьУчетПоНазначениямПартииНДСиУСН(ПараметрыРасчета, ПараметрыКорректировкиНДССредняя);
	КонецЕсли;
		
	КорректировкаЗаменитьПартнераНаДоговорВАналитикеУчетаНоменклатурыПартииНДСиУСН(ПараметрыРасчета, ПараметрыКорректировкиНДССредняя);
	
	Если НЕ ПараметрыРасчета.ФормироватьНачальныеОстаткиПартий22 Тогда
		
		Если ПараметрыРасчета.УправленческийУчетОрганизаций Тогда
			Если РасчетСебестоимостиПовтИсп.ДатаНачалаВеденияУправленческогоУчетаОрганизаций() = ПараметрыРасчета.РасчетныйПериод.НачалоПериода Тогда
				КорректировкаЗаполнитьСуммыУпрУчетаПартииНДСиУСН(ПараметрыРасчета, ПараметрыКорректировкиНДССредняя, ПараметрыКорректировкиФИФОСтарыйРегистр2);
			КонецЕсли;
		Иначе
			КорректировкаОчиститьСуммыУпрУчетаПартииНДСиУСН(ПараметрыРасчета, ПараметрыКорректировкиНДССредняя, ПараметрыКорректировкиФИФОСтарыйРегистр2);
		КонецЕсли;
		
	КонецЕсли;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗавершитьКорректировкуОстатковРегистра(ПараметрыРасчета, ПараметрыКорректировкиФИФОСтарыйРегистр1);
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗавершитьКорректировкуОстатковРегистра(ПараметрыРасчета, ПараметрыКорректировкиФИФОСтарыйРегистр2);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗавершитьКорректировкуОстатковРегистра(ПараметрыРасчета, ПараметрыКорректировкиНДССредняя);
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗавершитьКорректировкуОстатковРегистра(ПараметрыРасчета, ПараметрыКорректировкиПартииТоваровФИФО);
	
КонецПроцедуры

// При расчете первого месяца в партионном учете версии 2.2 формирует начальные остатки партий НДС и УСН.
//
Процедура СформироватьНачальныеОстаткиПартийНДСиУСН(ПараметрыРасчета, ПараметрыКорректировкиНДССредняя, ПараметрыКорректировкиНДСФИФО)
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировкиНДССредняя,
		Перечисления.ТипыЗаписейПартий.СлужебноеПереходНаПартионныйУчет22);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировкиНДСФИФО,
		Перечисления.ТипыЗаписейПартий.СлужебноеПереходНаПартионныйУчет22);
	
	Если НЕ ПараметрыКорректировкиНДССредняя.ТребуетсяКорректировка И НЕ ПараметрыКорректировкиНДСФИФО.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	#Область ВыборкаОстатков
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.ДокументПоступления,
	|	Т.АналитикаУчетаПартий,
	|	Т.КоличествоОстаток КАК Количество,
	|	Т.СтоимостьРеглОстаток КАК СтоимостьБезНДС,
	|	Т.НДСРеглОстаток КАК НДС,
	|	ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций ТОГДА 0
	|		КОГДА &ВалютыУпрИРеглУчетаСовпадают ТОГДА Т.СтоимостьРеглОстаток
	|		ИНАЧЕ Т.СтоимостьБезНДСОстаток
	|	КОНЕЦ КАК СтоимостьБезНДСУпр,
	|	ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций ТОГДА 0
	|		КОГДА &ВалютыУпрИРеглУчетаСовпадают ТОГДА Т.НДСРеглОстаток
	|		ИНАЧЕ Т.СтоимостьОстаток - Т.СтоимостьБезНДСОстаток
	|	КОНЕЦ КАК НДСУпр
	|ПОМЕСТИТЬ ВТОстаткиПартииТоваровОрганизаций
	|ИЗ
	|	РегистрНакопления.ПартииТоваровОрганизаций.Остатки(&ГраницаКонецПредыдущегоПериода, Организация В (&МассивОрганизаций)
	|		И НЕ ВидЗапасов.ТипЗапасов В
	|			(ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар),
	|			 ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца),
	|			 ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца))) КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ДокументПоступления,
	|	МАКСИМУМ(Т.ВидЗапасов) КАК ВидЗапасов,
	|	МАКСИМУМ(Т.АналитикаУчетаПартий) КАК АналитикаУчетаПартий
	|ПОМЕСТИТЬ ВТАналитикиИзПартийТоваровОрганизаций
	|ИЗ
	|	ВТОстаткиПартииТоваровОрганизаций КАК Т
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ДокументПоступления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.ДокументПоступления,
	|	Т.АналитикаУчетаПартий,
	|	Т.КоличествоОстаток КАК Количество,
	|	Т.СтоимостьРеглОстаток КАК СтоимостьБезНДС,
	|	Т.НДСРеглОстаток КАК НДС,
	|	ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций ТОГДА 0
	|		КОГДА &ВалютыУпрИРеглУчетаСовпадают ТОГДА Т.СтоимостьРеглОстаток
	|		ИНАЧЕ Т.СтоимостьБезНДСОстаток
	|	КОНЕЦ КАК СтоимостьБезНДСУпр,
	|	ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций ТОГДА 0
	|		КОГДА &ВалютыУпрИРеглУчетаСовпадают ТОГДА Т.НДСРеглОстаток
	|		ИНАЧЕ Т.СтоимостьОстаток - Т.СтоимостьБезНДСОстаток
	|	КОНЕЦ КАК НДСУпр
	|ПОМЕСТИТЬ ВТОстаткиПартииТоваровПереданныеНаКомиссию
	|ИЗ
	|	РегистрНакопления.ПартииТоваровПереданныеНаКомиссию.Остатки(&ГраницаКонецПредыдущегоПериода, Организация В (&МассивОрганизаций)) КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.ДокументПоступления,
	|	Т.АналитикаУчетаПартий,
	|	Т.КоличествоОстаток КАК Количество,
	|	Т.СтоимостьРеглОстаток КАК СтоимостьБезНДС,
	|	Т.НДСРеглОстаток КАК НДС,
	|	ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций ТОГДА 0
	|		КОГДА &ВалютыУпрИРеглУчетаСовпадают ТОГДА Т.СтоимостьРеглОстаток
	|		ИНАЧЕ Т.СтоимостьБезНДСОстаток
	|	КОНЕЦ КАК СтоимостьБезНДСУпр,
	|	ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций ТОГДА 0
	|		КОГДА &ВалютыУпрИРеглУчетаСовпадают ТОГДА Т.НДСРеглОстаток
	|		ИНАЧЕ Т.СтоимостьОстаток - Т.СтоимостьБезНДСОстаток
	|	КОНЕЦ КАК НДСУпр
	|ПОМЕСТИТЬ ВТОстаткиПартииПроизводственныхЗатрат
	|ИЗ
	|	РегистрНакопления.ПартииПроизводственныхЗатрат.Остатки(&ГраницаКонецПредыдущегоПериода, Организация В (&МассивОрганизаций)) КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.ДокументПоступления,
	|	Т.ДокументПоступленияРасходов,
	|	Т.СтатьяРасходов,
	|	Т.АналитикаУчетаПартий,
	|	0 КАК Количество,
	|	Т.СтоимостьРеглОстаток КАК СтоимостьБезНДС,
	|	Т.НДСРеглОстаток КАК НДС,
	|	ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций ТОГДА 0
	|		КОГДА СтатьяРасходов.ВариантРаспределенияРасходовУпр <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров) ТОГДА 0
	|		КОГДА ЕСТЬNULL(РеестрПоступление.Организация, НЕОПРЕДЕЛЕНО) <> ЕСТЬNULL(РеестрРасход.Организация, НЕОПРЕДЕЛЕНО) ТОГДА 0
	|		КОГДА &ВалютыУпрИРеглУчетаСовпадают ТОГДА Т.СтоимостьРеглОстаток
	|		ИНАЧЕ Т.СтоимостьБезНДСОстаток
	|	КОНЕЦ КАК СтоимостьБезНДСУпр,
	|	ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций ТОГДА 0
	|		КОГДА &ВалютыУпрИРеглУчетаСовпадают ТОГДА Т.НДСРеглОстаток
	|		ИНАЧЕ Т.СтоимостьОстаток - Т.СтоимостьБезНДСОстаток
	|	КОНЕЦ КАК НДСУпр
	|ПОМЕСТИТЬ ВТОстаткиПартииРасходовНаСебестоимостьТоваров
	|ИЗ
	|	РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров.Остатки(&ГраницаКонецПредыдущегоПериода, Организация В (&МассивОрганизаций)) КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрПоступление
	|		ПО РеестрПоступление.Ссылка = Т.ДокументПоступления
	|		И НЕ РеестрПоступление.ДополнительнаяЗапись
	|		И РеестрПоступление.Ссылка <> НЕОПРЕДЕЛЕНО
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрРасход
	|		ПО РеестрРасход.Ссылка = Т.ДокументПоступленияРасходов
	|		И НЕ РеестрРасход.ДополнительнаяЗапись
	|		И РеестрРасход.Ссылка <> НЕОПРЕДЕЛЕНО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаПродукции,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ДокументВыпуска,
	|	Т.ДокументПоступления,
	|	Т.АналитикаУчетаПартий,
	|	0 КАК Количество,
	|	Т.СтоимостьРеглОстаток КАК СтоимостьБезНДС,
	|	Т.НДСРеглОстаток КАК НДС,
	|	ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций ТОГДА 0
	|		КОГДА ЕСТЬNULL(РеестрПоступление.Организация, НЕОПРЕДЕЛЕНО) <> ЕСТЬNULL(РеестрВыпуск.Организация, НЕОПРЕДЕЛЕНО) ТОГДА 0
	|		КОГДА &ВалютыУпрИРеглУчетаСовпадают ТОГДА Т.СтоимостьРеглОстаток
	|		ИНАЧЕ Т.СтоимостьБезНДСОстаток
	|	КОНЕЦ КАК СтоимостьБезНДСУпр,
	|	ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций ТОГДА 0
	|		КОГДА &ВалютыУпрИРеглУчетаСовпадают ТОГДА Т.НДСРеглОстаток
	|		ИНАЧЕ Т.СтоимостьОстаток - Т.СтоимостьБезНДСОстаток
	|	КОНЕЦ КАК НДСУпр
	|ПОМЕСТИТЬ ВТОстаткиПартииЗатратНаВыпуск
	|ИЗ
	|	РегистрНакопления.ПартииЗатратНаВыпуск.Остатки(&ГраницаКонецПредыдущегоПериода,	Организация В (&МассивОрганизаций)
	|		И АналитикаУчетаПродукции <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|		И ДокументПоступления <> НЕОПРЕДЕЛЕНО) КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрПоступление
	|		ПО РеестрПоступление.Ссылка = Т.ДокументПоступления
	|		И НЕ РеестрПоступление.ДополнительнаяЗапись
	|		И РеестрПоступление.Ссылка <> НЕОПРЕДЕЛЕНО
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрВыпуск
	|		ПО РеестрВыпуск.Ссылка = Т.ДокументВыпуска
	|		И НЕ РеестрВыпуск.ДополнительнаяЗапись
	|		И РеестрВыпуск.Ссылка <> НЕОПРЕДЕЛЕНО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.РазделУчета 				 		 КАК РазделУчета,
	|	Т.Организация 				 		 КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры 		 КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов 				 		 КАК ВидЗапасов,
	|	Т.Партия 		 			 		 КАК Партия,
	|	Т.ДокументПоступления 		 		 КАК ДокументПоступления,
	|	Т.АналитикаУчетаПартий 		 		 КАК АналитикаУчетаПартий,
	|	Т.АналитикаУчетаДокументаПоступления КАК АналитикаУчетаДокументаПоступления,
	|	Т.ВидЦенности 				 		 КАК ВидЦенности,
	|	Т.ВидЦенностиДокументаПоступления	 КАК ВидЦенностиДокументаПоступления,
	|	СУММА(Т.Количество) 		 		 КАК Количество,
	|	СУММА(Т.СтоимостьБезНДС) 	 		 КАК СтоимостьБезНДС,
	|	СУММА(Т.НДС) 				 		 КАК НДС,
	|	СУММА(Т.СтоимостьБезНДСУпр)	 		 КАК СтоимостьБезНДСУпр,
	|	СУММА(Т.НДСУпр) 				 	 КАК НДСУпр
	|ПОМЕСТИТЬ ВТОстаткиПартийПредварительная
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР КОГДА Т.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		КОНЕЦ КАК РазделУчета,
	|		Т.Организация КАК Организация,
	|		Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		Т.ВидЗапасов КАК ВидЗапасов,
	|		Т.ДокументПоступления КАК Партия,
	|		Т.ДокументПоступления КАК ДокументПоступления,
	|		Т.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|		Т.АналитикаУчетаПартий КАК АналитикаУчетаДокументаПоступления,
	|		ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары) КАК ВидЦенности,
	|		ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары) КАК ВидЦенностиДокументаПоступления,
	|		Т.Количество КАК Количество,
	|		Т.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|		Т.НДС КАК НДС,
	|		Т.СтоимостьБезНДСУпр КАК СтоимостьБезНДСУпр,
	|		Т.НДСУпр КАК НДСУпр
	|	ИЗ
	|		ВТОстаткиПартииТоваровОрганизаций КАК Т
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию),
	|		Т.Организация,
	|		Т.АналитикаУчетаНоменклатуры,
	|		Т.ВидЗапасов,
	|		Т.ДокументПоступления,
	|		Т.ДокументПоступления,
	|		Т.АналитикаУчетаПартий,
	|		Т.АналитикаУчетаПартий,
	|		ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары),
	|		Т.Количество,
	|		Т.СтоимостьБезНДС,
	|		Т.НДС,
	|		Т.СтоимостьБезНДСУпр КАК СтоимостьБезНДСУпр,
	|		Т.НДСУпр КАК НДСУпр
	|	ИЗ
	|		ВТОстаткиПартииТоваровПереданныеНаКомиссию КАК Т
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеПереработчику)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		КОНЕЦ,
	|		Т.Организация,
	|		Т.АналитикаУчетаНоменклатуры,
	|		Т.ВидЗапасов,
	|		Т.ДокументПоступления,
	|		Т.ДокументПоступления,
	|		Т.АналитикаУчетаПартий,
	|		Т.АналитикаУчетаПартий,
	|		ВЫБОР КОГДА Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры В (
	|							ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары) 
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги) 
	|		КОНЕЦ,
	|		ВЫБОР КОГДА Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры В (
	|							ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары) 
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги) 
	|		КОНЕЦ,
	|		Т.Количество,
	|		Т.СтоимостьБезНДС,
	|		Т.НДС,
	|		Т.СтоимостьБезНДСУпр КАК СтоимостьБезНДСУпр,
	|		Т.НДСУпр КАК НДСУпр
	|	ИЗ
	|		ВТОстаткиПартииПроизводственныхЗатрат КАК Т
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|			КОГДА Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ,
	|		Т.Организация,
	|		Т.АналитикаУчетаНоменклатуры,
	|		Т.ВидЗапасов,
	|		Т.ДокументПоступления,
	|		Т.ДокументПоступленияРасходов,
	|		ЕСТЬNULL(ПартииТоваров.АналитикаУчетаПартий, Т.АналитикаУчетаПартий),
	|		Т.АналитикаУчетаПартий,
	|		ВЫБОР 
	|			КОГДА НЕ СтатьиРасходов.Ссылка ЕСТЬ NULL 
	|				ТОГДА СтатьиРасходов.ВидЦенностиНДС
	|			КОГДА Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры В (
	|							ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|		КОНЕЦ,
	|		ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги),
	|		Т.Количество,
	|		Т.СтоимостьБезНДС,
	|		Т.НДС,
	|		Т.СтоимостьБезНДСУпр КАК СтоимостьБезНДСУпр,
	|		Т.НДСУпр КАК НДСУпр
	|	ИЗ
	|		ВТОстаткиПартииРасходовНаСебестоимостьТоваров КАК Т
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|			ПО Т.СтатьяРасходов = СтатьиРасходов.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТАналитикиИзПартийТоваровОрганизаций КАК ПартииТоваров
	|			ПО Т.Организация = ПартииТоваров.Организация
	|				И Т.АналитикаУчетаНоменклатуры = ПартииТоваров.АналитикаУчетаНоменклатуры
	|				И Т.ДокументПоступления = ПартииТоваров.ДокументПоступления
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА Т.АналитикаУчетаПродукции.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|			КОГДА Т.АналитикаУчетаПродукции.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ,
	|		Т.Организация,
	|		Т.АналитикаУчетаПродукции,
	|		ЕСТЬNULL(ПартииТоваров.ВидЗапасов, ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)),
	|		Т.ДокументВыпуска,
	|		Т.ДокументПоступления,
	|		ЕСТЬNULL(ПартииТоваров.АналитикаУчетаПартий, Т.АналитикаУчетаПартий),
	|		Т.АналитикаУчетаПартий,
	|		ВЫБОР КОГДА Т.АналитикаУчетаПродукции.Номенклатура.ТипНоменклатуры В (
	|							ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары) 
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги) 
	|		КОНЕЦ,
	|		ВЫБОР КОГДА Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры В (
	|							ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары) 
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги) 
	|		КОНЕЦ,
	|		Т.Количество,
	|		Т.СтоимостьБезНДС,
	|		Т.НДС,
	|		Т.СтоимостьБезНДСУпр КАК СтоимостьБезНДСУпр,
	|		Т.НДСУпр КАК НДСУпр
	|	ИЗ
	|		ВТОстаткиПартииЗатратНаВыпуск КАК Т
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТАналитикиИзПартийТоваровОрганизаций КАК ПартииТоваров
	|			ПО Т.Организация = ПартииТоваров.Организация
	|				И Т.АналитикаУчетаПродукции = ПартииТоваров.АналитикаУчетаНоменклатуры
	|				И Т.ДокументВыпуска = ПартииТоваров.ДокументПоступления
	|	
	|	) КАК Т
	|ГДЕ
	|	НЕ Т.Организация В (&ОрганизацииБезУчетаПартийНДС)
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.РазделУчета,
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.Партия,
	|	Т.ДокументПоступления,
	|	Т.АналитикаУчетаПартий,
	|	Т.АналитикаУчетаДокументаПоступления,
	|	Т.ВидЦенности,
	|	Т.ВидЦенностиДокументаПоступления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПартий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.АналитикаУчетаДокументаПоступления КАК АналитикаУчетаПартий,
	|	Т.ВидЦенностиДокументаПоступления	 КАК ВидЦенности
	|ПОМЕСТИТЬ ВТАналитикиДокументаПоступления
	|ИЗ
	|	ВТОстаткиПартийПредварительная КАК Т
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПартий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.РазделУчета,
	|	Т.ВидЗапасов,
	|	Т.Организация,
	|	Т.Партия,
	|	Т.ВидДеятельностиНДС
	|ПОМЕСТИТЬ ВТОстаткиСебестоимости
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров.Остатки(&ГраницаКонецПредыдущегоПериода, Организация В (&МассивОрганизаций)) КАК Т
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.РазделУчета,
	|	Т.ВидЗапасов,
	|	Т.Организация,
	|	Т.ВидДеятельностиНДС
	|ПОМЕСТИТЬ ВТОстаткиСебестоимостиСредняя
	|ИЗ
	|	ВТОстаткиСебестоимости КАК Т
	|ГДЕ
	|	Т.Организация В (&ОрганизацииСУчетомПартийНДСВерсии22)
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры
	|";
	
	#КонецОбласти
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос); // формируем ВТОстаткиПартийПредварительная
	
	СформироватьАналитикиПартийСВидомЦенности(ПараметрыРасчета); // формируем ВТАналитикиПартийСВидомЦенности
	СформироватьАналитикиПартийСВидомЦенности(ПараметрыРасчета, "ВТАналитикиДокументаПоступления", "ВТАналитикиДокументаПоступленияСВидомЦенности"); // формируем ВТАналитикиПартийСВидомЦенности
	
	#Область ФормированиеИтоговыхТаблицОстатков
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Т.РазделУчета <> НЕОПРЕДЕЛЕНО
	|			ТОГДА Т.РазделУчета
	|		КОГДА НЕ ОстаткиКомиссии.Организация ЕСТЬ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеПереработчику)
	|	КОНЕЦ КАК РазделУчета,
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов КАК ВидЗапасов,
	|	Т.Партия КАК Партия,
	|	Т.ДокументПоступления КАК ДокументПоступления,
	|	ЕСТЬNULL(АналитикиПартий.АналитикаУчетаПартийСВидомЦенности, Т.АналитикаУчетаПартий) КАК АналитикаУчетаПартий,
	|	ЕСТЬNULL(АналитикиДокументаПоступления.АналитикаУчетаПартийСВидомЦенности, Т.АналитикаУчетаДокументаПоступления) КАК АналитикаУчетаДокументаПоступления,
	|	ВЫБОР
	|		КОГДА Аналитика.ГруппаПродукции ЕСТЬ NULL 
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА Аналитика.ГруппаПродукции <> ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА Аналитика.ГруппаПродукции
	|		КОГДА Аналитика.УстарелоПредназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляСделки)
	|			ТОГДА Аналитика.УстарелоСделка
	|		КОГДА Аналитика.УстарелоПредназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляПодразделения)
	|			ТОГДА Аналитика.УстарелоПодразделение
	|		КОГДА Аналитика.УстарелоПредназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляМенеджера)
	|			ТОГДА Аналитика.УстарелоМенеджер
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА НЕ Т.АналитикаУчетаПартий.НалогообложениеНДС ЕСТЬ NULL И Т.АналитикаУчетаПартий.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА Т.АналитикаУчетаПартий.НалогообложениеНДС
	|		КОГДА НЕ Аналитика.НалогообложениеНДС ЕСТЬ NULL И Аналитика.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА Аналитика.НалогообложениеНДС
	|		КОГДА ЕСТЬNULL(ПримененияЕНВД.РозничнаяТорговляОблагаетсяЕНВД, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|		КОГДА УчетныеПолитикиПрошлогоПериода.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Упрощенная)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|	КОНЕЦ КАК ВидДеятельностиНДС,
	|	Т.Количество,
	|	Т.СтоимостьБезНДС,
	|	Т.НДС,
	|	Т.СтоимостьБезНДСУпр,
	|	Т.НДСУпр
	|ПОМЕСТИТЬ ВТОстаткиПартий
	|ИЗ
	|	ВТОстаткиПартийПредварительная КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиПартииТоваровПереданныеНаКомиссию КАК ОстаткиКомиссии
	|		ПО (Т.РазделУчета = НЕОПРЕДЕЛЕНО)
	|			И Т.Организация = ОстаткиКомиссии.Организация
	|			И Т.АналитикаУчетаНоменклатуры = ОстаткиКомиссии.АналитикаУчетаНоменклатуры
	|			И Т.ВидЗапасов = ОстаткиКомиссии.ВидЗапасов
	|			И Т.Партия = ОстаткиКомиссии.ДокументПоступления
	|			И Т.АналитикаУчетаПартий = ОстаткиКомиссии.АналитикаУчетаПартий
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК Аналитика
	|		ПО Т.ВидЗапасов = Аналитика.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТАналитикиПартийСВидомЦенности КАК АналитикиПартий
	|		ПО Т.АналитикаУчетаПартий = АналитикиПартий.АналитикаУчетаПартий
	|			И Т.ВидЦенности = АналитикиПартий.ВидЦенности
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТАналитикиДокументаПоступленияСВидомЦенности КАК АналитикиДокументаПоступления
	|		ПО Т.АналитикаУчетаДокументаПоступления = АналитикиДокументаПоступления.АналитикаУчетаПартий
	|			И Т.ВидЦенностиДокументаПоступления = АналитикиДокументаПоступления.ВидЦенности
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПримененияЕНВД.СрезПоследних(&КонецПредыдущегоПериода, Организация В (&МассивОрганизаций)) КАК ПримененияЕНВД
	|		ПО Т.Организация = ПримененияЕНВД.Организация
	|			И Т.АналитикаУчетаНоменклатуры.МестоХранения = ПримененияЕНВД.Склад
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУчетныеПолитикиПрошлогоПериода КАК УчетныеПолитикиПрошлогоПериода
	|		ПО Т.Организация = УчетныеПолитикиПрошлогоПериода.Организация
	|ГДЕ
	|	Т.Партия <> НЕОПРЕДЕЛЕНО
	|	И Т.ДокументПоступления <> НЕОПРЕДЕЛЕНО
	|	И ЕСТЬNULL(АналитикиПартий.АналитикаУчетаПартийСВидомЦенности, Т.АналитикаУчетаПартий) <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
	|	И (Т.Количество <> 0 ИЛИ Т.НДС <> 0 ИЛИ Т.НДСУпр <> 0)
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.РазделУчета КАК РазделУчета,
	|	Т.Организация КАК Организация,
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов КАК ВидЗапасов,
	|	Т.Партия КАК Партия,
	|	Т.ДокументПоступления КАК ДокументПоступления,
	|	Т.АналитикаУчетаДокументаПоступления КАК АналитикаУчетаПартий,
	|	Т.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|	Т.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	Т.Количество КАК Количество,
	|	Т.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|	Т.НДС КАК НДС,
	|	Т.НДСУпр КАК НДСУпр
	|ПОМЕСТИТЬ ВТОстаткиПартийНДССредняя
	|ИЗ
	|	ВТОстаткиПартий КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОстаткиСебестоимостиСредняя КАК Отбор
	|		ПО Т.АналитикаУчетаНоменклатуры = Отбор.АналитикаУчетаНоменклатуры
	|		И Т.ВидЗапасов = Отбор.ВидЗапасов
	|		И Т.РазделУчета = Отбор.РазделУчета
	|		И Т.Организация = Отбор.Организация
	|		И Т.ВидДеятельностиНДС = Отбор.ВидДеятельностиНДС
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|	И Т.Организация В (&ОрганизацииСУчетомПартийНДСВерсии22)
	|	И НЕ Т.Организация В (&ОрганизацииСФИФОСкользящаяВПрошломПериоде)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.РазделУчета,
	|	Т.Партия,
	|	Т.АналитикаУчетаПартий,
	|	Т.АналитикаФинансовогоУчета,
	|	Т.ВидДеятельностиНДС,
	|	СУММА(Т.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТОстаткиПартийПоКоличествуФИФО
	|ИЗ
	|	ВТОстаткиПартий КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОстаткиСебестоимости КАК Отбор
	|		ПО Т.АналитикаУчетаНоменклатуры = Отбор.АналитикаУчетаНоменклатуры
	|		И Т.ВидЗапасов = Отбор.ВидЗапасов
	|		И Т.Партия = Отбор.Партия
	|		И Т.РазделУчета = Отбор.РазделУчета
	|		И Т.Организация = Отбор.Организация
	|		И Т.ВидДеятельностиНДС = Отбор.ВидДеятельностиНДС
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|	И Т.Партия <> НЕОПРЕДЕЛЕНО
	|	И Т.Организация В (&ОрганизацииСУчетомПартийНДСВерсии24)
	|	И Т.Организация В (&ОрганизацииСФИФОСкользящаяВПрошломПериоде)
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.РазделУчета,
	|	Т.Партия,
	|	Т.АналитикаУчетаПартий,
	|	Т.АналитикаФинансовогоУчета,
	|	Т.ВидДеятельностиНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.РазделУчета,
	|	Т.Партия,
	|	Т.АналитикаУчетаПартий,
	|	Т.АналитикаФинансовогоУчета,
	|	Т.ВидДеятельностиНДС,
	|	Т.ДокументПоступления,
	|	Т.АналитикаУчетаДокументаПоступления КАК АналитикаУчетаПартийДокументаПоступления,
	|	ОстаткиПоКоличеству.Количество КАК КоличествоПартии,
	|	Т.СтоимостьБезНДС,
	|	Т.НДС,
	|	Т.СтоимостьБезНДСУпр,
	|	Т.НДСУпр
	|ПОМЕСТИТЬ ВТОстаткиПартийНДСФИФОПредварительная
	|ИЗ
	|	ВТОстаткиПартий КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОстаткиПартийПоКоличествуФИФО КАК ОстаткиПоКоличеству
	|		ПО Т.Организация = ОстаткиПоКоличеству.Организация
	|		И Т.АналитикаУчетаНоменклатуры = ОстаткиПоКоличеству.АналитикаУчетаНоменклатуры
	|		И Т.ВидЗапасов = ОстаткиПоКоличеству.ВидЗапасов
	|		И Т.РазделУчета = ОстаткиПоКоличеству.РазделУчета
	|		И Т.Партия = ОстаткиПоКоличеству.Партия
	|		И Т.АналитикаУчетаПартий = ОстаткиПоКоличеству.АналитикаУчетаПартий
	|		И Т.АналитикаФинансовогоУчета = ОстаткиПоКоличеству.АналитикаФинансовогоУчета
	|		И Т.ВидДеятельностиНДС = ОстаткиПоКоличеству.ВидДеятельностиНДС
	|ГДЕ
	|	ОстаткиПоКоличеству.Количество <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.Партия,
	|	Т.АналитикаУчетаПартий,
	|	Т.ВидЗапасов,
	|	ВЫРАЗИТЬ(Т.АналитикаУчетаНоменклатуры КАК Справочник.КлючиАналитикиУчетаНоменклатуры).Номенклатура КАК Номенклатура,
	|	ВЫРАЗИТЬ(Т.АналитикаУчетаНоменклатуры КАК Справочник.КлючиАналитикиУчетаНоменклатуры).Характеристика КАК Характеристика,
	|	Т.ДокументПоступления,
	|	Т.АналитикаУчетаПартийДокументаПоступления,
	|	ВЫРАЗИТЬ(СУММА(Т.СтоимостьБезНДСУпр + Т.НДСУпр) / СУММА(Т.КоличествоПартии) КАК ЧИСЛО (31,10)) 	КАК СтоимостьСНДС,
	|	ВЫРАЗИТЬ(СУММА(Т.СтоимостьБезНДСУпр) / СУММА(Т.КоличествоПартии) КАК ЧИСЛО (31,10)) 			КАК СтоимостьБезНДС,
	|	ВЫРАЗИТЬ(СУММА(Т.СтоимостьБезНДС) / СУММА(Т.КоличествоПартии) КАК ЧИСЛО (31,10)) 				КАК СтоимостьБезНДСРегл,
	|	ВЫРАЗИТЬ(СУММА(Т.НДС) / СУММА(Т.КоличествоПартии) КАК ЧИСЛО (31,10)) 							КАК НДСРегл,
	|	ВЫРАЗИТЬ(СУММА(Т.СтоимостьБезНДСУпр) / СУММА(Т.КоличествоПартии) КАК ЧИСЛО (31,10)) 			КАК СтоимостьБезНДСУпр,
	|	ВЫРАЗИТЬ(СУММА(Т.НДСУпр) / СУММА(Т.КоличествоПартии) КАК ЧИСЛО (31,10)) 						КАК НДСУпр,
	|	СУММА(Т.КоличествоПартии) 																		КАК КоличествоПартии
	|ПОМЕСТИТЬ ВТОстаткиПартийНДСФИФО
	|ИЗ
	|	ВТОстаткиПартийНДСФИФОПредварительная КАК Т
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация,
	|	Т.Партия,
	|	Т.АналитикаУчетаПартий,
	|	Т.ВидЗапасов,
	|	ВЫРАЗИТЬ(Т.АналитикаУчетаНоменклатуры КАК Справочник.КлючиАналитикиУчетаНоменклатуры).Номенклатура,
	|	ВЫРАЗИТЬ(Т.АналитикаУчетаНоменклатуры КАК Справочник.КлючиАналитикиУчетаНоменклатуры).Характеристика,
	|	Т.ДокументПоступления,
	|	Т.АналитикаУчетаПартийДокументаПоступления
	|";
	
	#КонецОбласти
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос); // формируем ВТОстаткиПартийНДССредняя, ВТОстаткиПартийНДСФИФО
	
	#Область ОшибкиВОстатках
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПартииТоваровОрганизаций"" КАК ИмяРегистра,
	|	Т.Организация КАК Организация
	|ИЗ
	|	ВТОстаткиПартииТоваровОрганизаций КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПартииТоваровПереданныеНаКомиссию"" КАК ИмяРегистра,
	|	Т.Организация КАК Организация
	|ИЗ
	|	ВТОстаткиПартииТоваровПереданныеНаКомиссию КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПартииПроизводственныхЗатрат"" КАК ИмяРегистра,
	|	Т.Организация КАК Организация
	|ИЗ
	|	ВТОстаткиПартииПроизводственныхЗатрат КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПартииРасходовНаСебестоимостьТоваров"" КАК ИмяРегистра,
	|	Т.Организация КАК Организация
	|ИЗ
	|	ВТОстаткиПартииРасходовНаСебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)";
	
	Выборка = РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	Пока Выборка.Следующий() Цикл
		
		ТекстДляПротокола = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='В регистре накопления ""%1"" по организации ""%2"" на начало периода %3 есть остатки по пустой аналитике учета номенклатуры.'", ОбщегоНазначения.КодОсновногоЯзыка()),
			Выборка.ИмяРегистра,
			РасчетСебестоимостиПротоколРасчета.ПредставлениеЗначения(Выборка.Организация),
			РасчетСебестоимостиПротоколРасчета.ПредставлениеПериодаРасчета(ПараметрыРасчета));
		
		РасчетСебестоимостиПротоколРасчета.ЗафиксироватьОшибкуРасчета(
			ПараметрыРасчета,
			Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаВНачальныхОстаткахПУ22,
			ТекстДляПротокола);
		
		РасчетСебестоимостиПрикладныеАлгоритмы.ЗарегистрироватьПроблемуВыполненияРасчета(
			ПараметрыРасчета,
			Выборка.Организация,
			НСтр("ru='При переходе на партионный учет версии 2.2 диагностированы ошибки'", ОбщегоНазначения.КодОсновногоЯзыка()),
			ТекстДляПротокола);
		
	КонецЦикла;
	
	#КонецОбласти
	
	// Добавляем данные к корректировочным движениям
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировкиНДССредняя,
		"ВТОстаткиПартийНДССредняя",
		,
		Истина);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировкиНДСФИФО,
		"ВТОстаткиПартийНДСФИФО",
		,
		Истина);
	
КонецПроцедуры

// При изменении учетной политики формирует корректировочные движения в регистрах партий НДС.
//
Процедура СформироватьКорректировочныеДвиженияПартийНДСПриИзмененииУчетнойПолитики(ПараметрыРасчета, ПараметрыКорректировкиПартииТоваровФИФО, ПараметрыКорректировкиНДССредняя)
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировкиПартииТоваровФИФО,
		Перечисления.ТипыЗаписейПартий.СлужебноеИзменениеМетодаОценкиСтоимости,
		Ложь);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировкиНДССредняя,
		Перечисления.ТипыЗаписейПартий.СлужебноеИзменениеМетодаОценкиСтоимости,
		Ложь);
	
	Если НЕ ПараметрыКорректировкиПартииТоваровФИФО.ТребуетсяКорректировка
	 И НЕ ПараметрыКорректировкиНДССредняя.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.Ссылка КАК Организация
	|ПОМЕСТИТЬ ВТОрганизацииСПереходом22_24
	|ИЗ
	|	Справочник.Организации КАК Т
	|ГДЕ
	|	Т.Ссылка В(&ОрганизацииСУчетомПартийНДСВерсии22ВПрошломПериоде)
	|	И Т.Ссылка В(&ОрганизацииСУчетомПартийНДСВерсии24)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Ссылка КАК Организация
	|ПОМЕСТИТЬ ВТОрганизацииСПереходом24_22
	|ИЗ
	|	Справочник.Организации КАК Т
	|ГДЕ
	|	Т.Ссылка В(&ОрганизацииСУчетомПартийНДСВерсии24ВПрошломПериоде)
	|	И Т.Ссылка В(&ОрганизацииСУчетомПартийНДСВерсии22)
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТОрганизацииСПереходом22_24") = 0
	 И РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТОрганизацииСПереходом24_22") = 0 Тогда
		Возврат; // нет данных для распределения
	КонецЕсли;
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТОрганизацииСПереходом22_24") > 0 Тогда
	
		#Область ПодготовкаДанных
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Т.Организация,
		|	Т.РазделУчета,
		|	ВЫРАЗИТЬ(Т.АналитикаУчетаНоменклатуры КАК Справочник.КлючиАналитикиУчетаНоменклатуры) КАК АналитикаУчетаНоменклатуры,
		|	Т.ВидЗапасов,
		|	Т.ВидДеятельностиНДС,
		|	Т.Партия,
		|	Т.АналитикаФинансовогоУчета,
		|	Т.ДокументПоступления,
		|	Т.АналитикаУчетаПартий КАК АналитикаУчетаПартийДокументаПоступления,
		|	Т.Количество,
		|	Т.СтоимостьБезНДС,
		|	Т.НДС,
		|	Т.НДСУпр
		|ПОМЕСТИТЬ ВТОстаткиПартийНДССредняяПредварительная
		|ИЗ
		|	ВТРасчетныеНачальныеОстаткиДетализацияПартийТоваровДляНДСиУСН КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОрганизацииСПереходом22_24 КАК Отбор
		|		ПО Т.Организация = Отбор.Организация
		|ГДЕ
		|	Т.Партия <> НЕОПРЕДЕЛЕНО
		|ИНДЕКСИРОВАТЬ ПО
		|	Партия,
		|	АналитикаУчетаНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Партия,
		|	Т.АналитикаУчетаНоменклатуры,
		|	Т.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|	Т.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	Т.АналитикаУчетаНоменклатуры.Серия КАК Серия
		|ПОМЕСТИТЬ ВТПартииНДССредняя
		|ИЗ
		|	ВТОстаткиПартийНДССредняяПредварительная КАК Т
		|ИНДЕКСИРОВАТЬ ПО
		|	Партия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Партия КАК Партия,
		|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	МАКСИМУМ(ИсходныеПартии.КорАналитикаУчетаПартий) КАК АналитикаУчетаПартий
		|ПОМЕСТИТЬ ВТАналитикиУчетаПартийНДССредняя
		|ИЗ
		|	ВТПартииНДССредняя КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК ИсходныеПартии
		|		ПО ИсходныеПартии.Регистратор = Т.Партия
		|		И ИсходныеПартии.АналитикаУчетаНоменклатуры.Номенклатура = Т.Номенклатура
		|		И ИсходныеПартии.АналитикаУчетаНоменклатуры.Характеристика = Т.Характеристика
		|		И ИсходныеПартии.АналитикаУчетаНоменклатуры.Серия = Т.Серия
		|СГРУППИРОВАТЬ ПО
		|	Т.Партия,
		|	Т.АналитикаУчетаНоменклатуры
		|ИНДЕКСИРОВАТЬ ПО
		|	Партия,
		|	АналитикаУчетаНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Организация,
		|	Т.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|	Т.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	Т.ВидЗапасов,
		|	Т.Партия КАК Партия,
		|	ЕСТЬNULL(Аналитики.АналитикаУчетаПартий, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)) КАК АналитикаУчетаПартий,
		|	Т.ДокументПоступления,
		|	Т.АналитикаУчетаПартийДокументаПоступления,
		|	СУММА(Т.Количество) КАК Количество,
		|	СУММА(Т.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(Т.НДС) КАК НДС,
		|	СУММА(Т.НДСУпр) КАК НДСУпр
		|ПОМЕСТИТЬ ВТОстаткиПартийНДССредняя
		|ИЗ
		|	ВТОстаткиПартийНДССредняяПредварительная КАК Т
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТАналитикиУчетаПартийНДССредняя КАК Аналитики
		|		ПО Т.Партия = Аналитики.Партия
		|		И Т.АналитикаУчетаНоменклатуры = Аналитики.АналитикаУчетаНоменклатуры
		|СГРУППИРОВАТЬ ПО
		|	Т.Организация,
		|	Т.АналитикаУчетаНоменклатуры.Номенклатура,
		|	Т.АналитикаУчетаНоменклатуры.Характеристика,
		|	Т.ВидЗапасов,
		|	Т.Партия,
		|	ЕСТЬNULL(Аналитики.АналитикаУчетаПартий, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)),
		|	Т.ДокументПоступления,
		|	Т.АналитикаУчетаПартийДокументаПоступления
		|ИНДЕКСИРОВАТЬ ПО
		|	Партия,
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Организация,
		|	Т.Номенклатура,
		|	Т.Характеристика,
		|	Т.ВидЗапасов,
		|	Т.Партия,
		|	Т.АналитикаУчетаПартий,
		|	СУММА(Т.Количество) КАК Количество
		|ПОМЕСТИТЬ ВТКоличествоПартийНДССредняя
		|ИЗ
		|	ВТОстаткиПартийНДССредняя КАК Т
		|СГРУППИРОВАТЬ ПО
		|	Т.Организация,
		|	Т.Номенклатура,
		|	Т.Характеристика,
		|	Т.ВидЗапасов,
		|	Т.Партия,
		|	Т.АналитикаУчетаПартий
		|ИМЕЮЩИЕ
		|	СУММА(Т.Количество) > 0
		|ИНДЕКСИРОВАТЬ ПО
		|	Партия,
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Организация КАК Организация,
		|	Т.Номенклатура КАК Номенклатура,
		|	Т.Характеристика,
		|	Т.ВидЗапасов,
		|	Т.Партия КАК Партия,
		|	Т.АналитикаУчетаПартий,
		|	Т.ДокументПоступления КАК ДокументПоступления,
		|	Т.АналитикаУчетаПартийДокументаПоступления,
		|	Т.Количество,
		|	ВЫРАЗИТЬ(Т.СтоимостьБезНДС/ВЫБОР КОГДА Т.Количество = 0 ТОГДА Знаменатели.Количество ИНАЧЕ Т.Количество КОНЕЦ КАК ЧИСЛО(31,10)) КАК СтоимостьБезНДСРегл,
		|	ВЫРАЗИТЬ(Т.НДС/ВЫБОР КОГДА Т.Количество = 0 ТОГДА Знаменатели.Количество ИНАЧЕ Т.Количество КОНЕЦ КАК ЧИСЛО(31,10)) КАК НДСРегл,
		|	ВЫРАЗИТЬ(Т.НДСУпр/ВЫБОР КОГДА Т.Количество = 0 ТОГДА Знаменатели.Количество ИНАЧЕ Т.Количество КОНЕЦ КАК ЧИСЛО(31,10)) КАК НДСУпр
		|ПОМЕСТИТЬ ВТОстаткиПартийНДСФИФОПредварительная
		|ИЗ
		|	ВТОстаткиПартийНДССредняя КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКоличествоПартийНДССредняя КАК Знаменатели
		|		ПО Т.Партия = Знаменатели.Партия
		|		И Т.Номенклатура = Знаменатели.Номенклатура
		|		И Т.Характеристика = Знаменатели.Характеристика
		|		И Т.Организация = Знаменатели.Организация
		|		И Т.ВидЗапасов = Знаменатели.ВидЗапасов
		|		И Т.АналитикаУчетаПартий = Знаменатели.АналитикаУчетаПартий
		|ГДЕ
		|	Т.СтоимостьБезНДС <> 0
		|	ИЛИ Т.НДС <> 0
		|	ИЛИ Т.НДСУпр <> 0
		|ИНДЕКСИРОВАТЬ ПО
		|	Партия,
		|	Организация,
		|	Номенклатура,
		|	ДокументПоступления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Организация,
		|	Т.Номенклатура,
		|	Т.Характеристика,
		|	Т.ВидЗапасов,
		|	Т.Партия,
		|	Т.АналитикаУчетаПартий,
		|	Т.ДокументПоступления,
		|	Т.АналитикаУчетаПартийДокументаПоступления,
		|	Т.Количество,
		|	ВЫРАЗИТЬ(Т.СтоимостьБезНДСРегл КАК ЧИСЛО(31,10)) КАК СтоимостьБезНДСРегл,
		|	ВЫРАЗИТЬ(Т.НДСРегл КАК ЧИСЛО(31,10)) КАК НДСРегл,
		|	ВЫРАЗИТЬ(Т.НДСУпр КАК ЧИСЛО(31,10)) КАК НДСУпр
		|ПОМЕСТИТЬ ВТОстаткиПартийНДСФИФО
		|ИЗ
		|	ВТОстаткиПартийНДСФИФОПредварительная КАК Т
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДетализацияСебестоимостиПартииТоваров КАК Детализация
		|		ПО Т.Партия = Детализация.Партия
		|		И Т.Организация = Детализация.Организация
		|		И Т.Номенклатура = Детализация.Номенклатура
		|		И Т.Характеристика = Детализация.Характеристика
		|		И Т.ВидЗапасов = Детализация.ВидЗапасов
		|		И Т.ДокументПоступления = Детализация.ДокументПоступления
		|		И Т.АналитикаУчетаПартийДокументаПоступления = Детализация.АналитикаУчетаПартийДокументаПоступления
		|		И Детализация.ПериодРегистрации < &НачалоПредыдущегоПериода
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДетализацияСебестоимостиПартииТоваров КАК ДетализацияПостатейные
		|		ПО Т.Партия = ДетализацияПостатейные.Партия
		|		И Т.Организация = ДетализацияПостатейные.Организация
		|		И Т.Номенклатура = ДетализацияПостатейные.Номенклатура
		|		И Т.Характеристика = ДетализацияПостатейные.Характеристика
		|		И Т.ВидЗапасов = ДетализацияПостатейные.ВидЗапасов
		|		И Т.ДокументПоступления = ДетализацияПостатейные.ДокументПоступления
		|		И Т.АналитикаУчетаПартийДокументаПоступления = ДетализацияПостатейные.АналитикаУчетаПартийДокументаПоступления
		|		И ДетализацияПостатейные.ПериодРегистрации < &НачалоПредыдущегоПериода
		|ГДЕ
		|	Детализация.Партия ЕСТЬ NULL
		|	И ДетализацияПостатейные.Партия ЕСТЬ NULL
		|";
		
		РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
		
		// Добавляем результаты расчета к корректировочным движениям
		РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
			ПараметрыРасчета,
			ПараметрыКорректировкиПартииТоваровФИФО,
			"ВТОстаткиПартийНДСФИФО",
			,
			Истина);
		
		#КонецОбласти
	
	КонецЕсли;

	Если РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТОрганизацииСПереходом24_22") > 0 Тогда
		
		#Область ПодготовкаДанных
		
		ПараметрыКорректировкиСебестоимость = РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьКорректировкуОстатковРегистра(
			ПараметрыРасчета,
			Метаданные.РегистрыНакопления.СебестоимостьТоваров.ПолноеИмя());
		
		РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
			ПараметрыРасчета,
			ПараметрыКорректировкиСебестоимость,
			Перечисления.ТипыЗаписейПартий.СлужебноеИзменениеМетодаОценкиСтоимости);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Т.Организация,
		|	Т.РазделУчета,
		|	Т.АналитикаУчетаНоменклатуры,
		|	Т.ВидЗапасов,
		|	Т.Партия,
		|	Т.АналитикаУчетаПартий,
		|	Т.ВидДеятельностиНДС,
		|	Т.АналитикаФинансовогоУчета,
		|	СУММА(Т.Количество) КАК Количество
		|ПОМЕСТИТЬ ВТОстаткиСебестоимостиДляПартийНДС
		|ИЗ
		|	ВТРасчетныеНачальныеОстаткиСебестоимостьТоваров КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОрганизацииСПереходом24_22 КАК Отбор
		|		ПО Т.Организация = Отбор.Организация
		|ГДЕ
		|	Т.Партия <> НЕОПРЕДЕЛЕНО
		|	И Т.ВидДеятельностиНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|	И Т.Количество > 0
		|СГРУППИРОВАТЬ ПО
		|	Т.Организация,
		|	Т.РазделУчета,
		|	Т.АналитикаУчетаНоменклатуры,
		|	Т.ВидЗапасов,
		|	Т.Партия,
		|	Т.АналитикаУчетаПартий,
		|	Т.ВидДеятельностиНДС,
		|	Т.АналитикаФинансовогоУчета
		|ИМЕЮЩИЕ
		|	СУММА(Т.Количество) <> 0
		|ИНДЕКСИРОВАТЬ ПО
		|	Партия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Партия
		|ПОМЕСТИТЬ ВТПартииИзСебестоимости
		|ИЗ
		|	ВТОстаткиСебестоимостиДляПартийНДС КАК Т
		|ИНДЕКСИРОВАТЬ ПО
		|	Партия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Организация КАК Организация,
		|	Т.Партия КАК Партия,
		|	Т.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
		|	Т.ВидЗапасов КАК ВидЗапасов,
		|	Т.Номенклатура КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|
		|	Т.ДокументПоступления КАК ДокументПоступления,
		|	Т.АналитикаУчетаПартийДокументаПоступления КАК АналитикаУчетаПартийДокументаПоступления,
		|	Т.СтоимостьБезНДСРегл,
		|	Т.НДСРегл,
		|	Т.СтоимостьБезНДСУпр,
		|	Т.НДСУпр
		|ПОМЕСТИТЬ ВТЦеныПартийНДСФИФО
		|ИЗ
		|	РегистрСведений.ДетализацияСебестоимостиПартииТоваров КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПартииИзСебестоимости КАК ОтборПартийСебестоимости
		|	ПО Т.Партия = ОтборПартийСебестоимости.Партия
		|ГДЕ
		|	Т.ПериодРегистрации < &НачалоПериода
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Т.Организация КАК Организация,
		|	Т.Партия КАК Партия,
		|	Т.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
		|	Т.ВидЗапасов КАК ВидЗапасов,
		|	Т.Номенклатура КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|
		|	Т.ДокументПоступления КАК ДокументПоступления,
		|	Т.АналитикаУчетаПартийДокументаПоступления КАК АналитикаУчетаПартийДокументаПоступления,
		|	Т.СтоимостьБезНДСРегл,
		|	Т.НДСРегл,
		|	Т.СтоимостьБезНДСУпр,
		|	Т.НДСУпр
		|ИЗ
		|	РегистрСведений.ДетализацияСебестоимостиПартииТоваровПостатейныеЗатраты КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПартииИзСебестоимости КАК ОтборПартийСебестоимости
		|	ПО Т.Партия = ОтборПартийСебестоимости.Партия
		|ГДЕ
		|	Т.ПериодРегистрации < &НачалоПериода
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Партия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Организация,
		|	Т.АналитикаУчетаНоменклатуры,
		|	Т.ВидЗапасов,
		|	Т.РазделУчета,
		|	Т.Партия,
		|	Т.АналитикаФинансовогоУчета,
		|	Т.ВидДеятельностиНДС,
		|	Т.ДокументПоступления,
		|	Т.АналитикаУчетаПартий,
		|	СУММА(Т.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(Т.НДС) КАК НДС,
		|	СУММА(Т.СтоимостьБезНДСУпр) КАК СтоимостьБезНДСУпр,
		|	СУММА(Т.НДСУпр) КАК НДСУпр,
		|	СУММА(Т.Количество) КАК Количество
		|ПОМЕСТИТЬ ВТЦеныПартийНДССредняя
		|ИЗ
		|(ВЫБРАТЬ
		|	Остатки.Организация КАК Организация,
		|	Остатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	Остатки.ВидЗапасов,
		|	Остатки.РазделУчета,
		|	Остатки.Партия КАК Партия,
		|	Остатки.АналитикаФинансовогоУчета,
		|	Остатки.ВидДеятельностиНДС,
		|	Т.ДокументПоступления,
		|	Т.АналитикаУчетаПартийДокументаПоступления КАК АналитикаУчетаПартий,
		|	ВЫРАЗИТЬ(Остатки.Количество * Т.СтоимостьБезНДСРегл КАК ЧИСЛО(31,2)) КАК СтоимостьБезНДС,
		|	ВЫРАЗИТЬ(Остатки.Количество * Т.НДСРегл КАК ЧИСЛО(31,2)) КАК НДС,
		|	ВЫРАЗИТЬ(Остатки.Количество * Т.СтоимостьБезНДСУпр КАК ЧИСЛО(31,2)) КАК СтоимостьБезНДСУпр,
		|	ВЫРАЗИТЬ(Остатки.Количество * Т.НДСУпр КАК ЧИСЛО(31,2)) КАК НДСУпр,
		|	0 КАК Количество
		|ИЗ
		|	ВТЦеныПартийНДСФИФО КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОстаткиСебестоимостиДляПартийНДС КАК Остатки
		|		ПО Т.Организация = Остатки.Организация
		|		И Т.Партия = Остатки.Партия
		|		И Т.АналитикаУчетаПартий = Остатки.АналитикаУчетаПартий
		|		И Т.ВидЗапасов = Остатки.ВидЗапасов
		|		И Т.Номенклатура = ВЫРАЗИТЬ(Остатки.АналитикаУчетаНоменклатуры КАК Справочник.КлючиАналитикиУчетаНоменклатуры).Номенклатура
		|		И Т.Характеристика = ВЫРАЗИТЬ(Остатки.АналитикаУчетаНоменклатуры КАК Справочник.КлючиАналитикиУчетаНоменклатуры).Характеристика
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Остатки.Организация КАК Организация,
		|	Остатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	Остатки.ВидЗапасов,
		|	Остатки.РазделУчета,
		|	Остатки.Партия КАК Партия,
		|	Остатки.АналитикаФинансовогоУчета,
		|	Остатки.ВидДеятельностиНДС,
		|	Т.ДокументПоступления,
		|	Т.АналитикаУчетаПартийДокументаПоступления КАК АналитикаУчетаПартий,
		|	ВЫРАЗИТЬ(Остатки.Количество * Т.СтоимостьБезНДСРегл КАК ЧИСЛО(31,2)) КАК СтоимостьБезНДС,
		|	ВЫРАЗИТЬ(Остатки.Количество * Т.НДСРегл КАК ЧИСЛО(31,2)) КАК НДС,
		|	ВЫРАЗИТЬ(Остатки.Количество * Т.СтоимостьБезНДСУпр КАК ЧИСЛО(31,2)) КАК СтоимостьБезНДСУпр,
		|	ВЫРАЗИТЬ(Остатки.Количество * Т.НДСУпр КАК ЧИСЛО(31,2)) КАК НДСУпр,
		|	0 КАК Количество
		|ИЗ
		|	РегистрСведений.ДетализацияСебестоимостиТоваровПостатейныеЗатраты КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОстаткиСебестоимостиДляПартийНДС КАК Остатки
		|		ПО Т.Организация = Остатки.Организация
		|		И Т.РазделУчета = Остатки.РазделУчета
		|		И Т.АналитикаУчетаНоменклатуры = Остатки.АналитикаУчетаНоменклатуры
		|		И Т.ВидЗапасов = Остатки.ВидЗапасов
		|		И Т.Партия = Остатки.Партия
		|		И Т.АналитикаУчетаПартий = Остатки.АналитикаУчетаПартий
		|		И Т.ВидДеятельностиНДС = Остатки.ВидДеятельностиНДС
		|		И Т.АналитикаФинансовогоУчета = Остатки.АналитикаФинансовогоУчета
		|ГДЕ
		|	Т.Период = &НачалоПредыдущегоПериода
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Остатки.Организация КАК Организация,
		|	Остатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	Остатки.ВидЗапасов,
		|	Остатки.РазделУчета,
		|	Остатки.Партия КАК Партия,
		|	Остатки.АналитикаФинансовогоУчета,
		|	Остатки.ВидДеятельностиНДС,
		|	Остатки.Партия КАК ДокументПоступления,
		|	Остатки.АналитикаУчетаПартий КАК АналитикаУчетаПартийДокументаПоступления,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК НДС,
		|	0 КАК СтоимостьБезНДСУпр,
		|	0 КАК НДСУпр,
		|	Остатки.Количество КАК Количество
		|ИЗ
		|	ВТОстаткиСебестоимостиДляПартийНДС КАК Остатки
		|) КАК Т
		|СГРУППИРОВАТЬ ПО
		|	Т.Организация,
		|	Т.АналитикаУчетаНоменклатуры,
		|	Т.ВидЗапасов,
		|	Т.РазделУчета,
		|	Т.Партия,
		|	Т.АналитикаФинансовогоУчета,
		|	Т.ВидДеятельностиНДС,
		|	Т.ДокументПоступления,
		|	Т.АналитикаУчетаПартий
		|";
		
		РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
		
		// Добавляем данные к корректировочным движениям
		РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
			ПараметрыРасчета,
			ПараметрыКорректировкиНДССредняя,
			"ВТЦеныПартийНДССредняя",
			"",
			Истина);
		
		#КонецОбласти
		
	КонецЕсли;
	
	#Область ПодготовкаДанныхСписаниеОстатков
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Организация КАК Организация
	|ПОМЕСТИТЬ ВТОрганизацииСПереходом22_24
	|ИЗ
	|	РегистрНакопления.ДетализацияПартийТоваровДляНДСиУСН КАК Т
	|ГДЕ
	|	Т.Организация В(&ОрганизацииСУчетомПартийНДСВерсии24ВПрошломПериоде)
	|	И Т.Организация В(&ОрганизацииСУчетомПартийНДСВерсии24)
	|	И Т.Период МЕЖДУ &НачалоПредыдущегоПериода И &КонецПредыдущегоПериода
	|	И Т.Активность
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Ссылка
	|ИЗ
	|	Справочник.Организации КАК Т
	|ГДЕ
	|	Т.Ссылка В(&ОрганизацииСУчетомПартийНДСВерсии22ВПрошломПериоде)
	|	И Т.Ссылка В(&ОрганизацииСУчетомПартийНДСВерсии24)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ИзмеренияРегистраНДССредняя,
	|	&РесурсыРегистраНДССредняя
	|ПОМЕСТИТЬ ВТСторноПартийНДССредняя
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиДетализацияПартийТоваровДляНДСиУСН КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОрганизацииСПереходом22_24 КАК ОтборОрганизации
	|		ПО Т.Организация = ОтборОрганизации.Организация
	|ГДЕ
	|	НЕ ОтборОрганизации.Организация ЕСТЬ NULL
	|	ИЛИ (НЕ Т.Организация В (&ОрганизацииСУчетомПартийНДСВерсии22ВПрошломПериоде)
	|		И НЕ Т.Организация В (&ОрганизацииСУчетомПартийНДСВерсии24ВПрошломПериоде))
	|	ИЛИ (НЕ Т.Организация В (&ОрганизацииСУчетомПартийНДСВерсии22)
	|		И НЕ Т.Организация В (&ОрганизацииСУчетомПартийНДСВерсии24))
	|";

	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИзмеренияРегистраНДССредняя",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировкиНДССредняя.ОписаниеРегистра.ИзмеренияРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&РесурсыРегистраНДССредняя",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировкиНДССредняя.ОписаниеРегистра.РесурсыРегистра, "Т."));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	// Добавляем данные к корректировочным движениям
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировкиНДССредняя,
		"",
		"ВТСторноПартийНДССредняя",
		,
		Истина);
	
	#КонецОбласти
	
КонецПроцедуры

// Формирует движения для перехода на новый регистр учета НДС ДетализацияСебестоимостиТоваров.
//
Процедура КорректировкаПереходНаНовыйРегистрДетализацияСебестоимостиТоваров(ПараметрыРасчета, ПараметрыКорректировки)
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		Перечисления.ТипыЗаписейПартий.СлужебноеПереходНаРегистрДетализацияСебестоимостиТоваров,
		Ложь);
	
	Если НЕ ПараметрыКорректировки.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	Запрос.УстановитьПараметр("ОрганизацииДляКорректировки", ПараметрыКорректировки.ОрганизацииДляКорректировки);
	
	#Область ОпределениеОрганизацийДляРасчета
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.Организация КАК Организация,
	|	МАКСИМУМ(Т.ПериодНовыйРегистр) КАК ПериодНовыйРегистр,
	|	МАКСИМУМ(Т.ПериодСтарыйРегистр1) КАК ПериодСтарыйРегистр1,
	|	МАКСИМУМ(Т.ПериодСтарыйРегистр2) КАК ПериодСтарыйРегистр2
	|ИЗ
	|	(ВЫБРАТЬ
	|		Т.Организация КАК Организация,
	|		МАКСИМУМ(Т.ПериодРегистрации) КАК ПериодНовыйРегистр,
	|		ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ПериодСтарыйРегистр1,
	|		ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ПериодСтарыйРегистр2
	|	ИЗ
	|		РегистрСведений.ДетализацияСебестоимостиПартииТоваров КАК Т
	|	ГДЕ
	|		Т.Организация В(&ОрганизацииДляКорректировки)
	|		И Т.ПериодРегистрации < &НачалоПериода
	|		И НЕ (Т.ПериодРегистрации = &НачалоПредыдущегоПериода
	|			И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СлужебноеПереходНаРегистрДетализацияСебестоимостиТоваров))
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Т.Организация
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Т.Организация,
	|		МАКСИМУМ(Т.ПериодРегистрации),
	|		ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0),
	|		ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	ИЗ
	|		РегистрСведений.ДетализацияСебестоимостиПартииТоваровПостатейныеЗатраты КАК Т
	|	ГДЕ
	|		Т.Организация В(&ОрганизацииДляКорректировки)
	|		И Т.ПериодРегистрации < &НачалоПериода
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Т.Организация
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Т.Организация,
	|		ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0),
	|		МАКСИМУМ(Т.Период),
	|		ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	ИЗ
	|		РегистрСведений.ДетализацияСебестоимостиТоваров КАК Т
	|	ГДЕ
	|		Т.Организация В(&ОрганизацииДляКорректировки)
	|		И Т.Период < &НачалоПериода
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Т.Организация
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Т.Организация,
	|		ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0),
	|		ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0),
	|		&НачалоПредыдущегоПериода
	|	ИЗ
	|		РегистрНакопления.ДетализацияПартийТоваровДляНДСиУСН2_4.Остатки(&ГраницаКонецПредыдущегоПериода, Организация В (&ОрганизацииДляКорректировки)) КАК Т
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Т.Организация) КАК Т
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация";
	
	Выборка = РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос,,,, НСтр("ru = 'Организации для корректировки'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	ОрганизацииДляКорректировки = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.ПериодНовыйРегистр) Тогда
			
			Если ЗначениеЗаполнено(Выборка.ПериодСтарыйРегистр1)
			 И Выборка.ПериодСтарыйРегистр1 >= Выборка.ПериодНовыйРегистр Тогда
			 
				ТекстДляПротокола = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'По организации ""%1"" в предыдущих периодах есть некорректные движения по регистрам партий НДС.'", ОбщегоНазначения.КодОсновногоЯзыка()),
					Выборка.Организация);
				
				РасчетСебестоимостиПротоколРасчета.ЗафиксироватьОшибкуРасчета(
					ПараметрыРасчета,
					Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаФормированияДвиженийПоРегистрам,
					ТекстДляПротокола);
				
		 	КонецЕсли;
			
			Продолжить;
			
		ИначеЕсли ЗначениеЗаполнено(Выборка.ПериодСтарыйРегистр1) Тогда
			Продолжить; // данные регистра ДетализацияСебестоимостиТоваров будут учтены при расчете партий НДС текущего периода
		Иначе
			ОрганизацииДляКорректировки.Добавить(Выборка.Организация);
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ ЗначениеЗаполнено(ОрганизацииДляКорректировки) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ОрганизацииДляКорректировки", ОрганизацииДляКорректировки);
	
	#КонецОбласти
	
	#Область ПодготовкаДанных
	
	// Выберем данные для распределения
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Т.РазделУчета КАК РазделУчета,
	|	Т.ВидЗапасов КАК ВидЗапасов,
	|	Т.Организация КАК Организация,
	|	Т.Партия КАК Партия,
	|	Т.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	Т.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|	Т.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	ЕСТЬNULL(Т.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	Т.СтоимостьРеглОстаток КАК СтоимостьРегл,
	|	Т.ДопРасходыРеглОстаток КАК ДопРасходыРегл,
	|	Т.КоличествоОстаток КАК КоличествоСебестоимость
	|ПОМЕСТИТЬ ВТОстаткиСебестоимостиПредварительная
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииДляКорректировки)
	|		И НЕ РазделУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку), 
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки))
	|	) КАК Т
	|ГДЕ
	|	Т.КоличествоОстаток <> 0
	|	И Т.СтоимостьРеглОстаток + Т.ДопРасходыРеглОстаток <> 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	Т.РазделУчета КАК РазделУчета,
	|	Т.ВидЗапасов КАК ВидЗапасов,
	|	Т.Организация КАК Организация,
	|	Т.Партия КАК Партия,
	|	Т.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	Т.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|	Т.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	Т.НаправлениеДеятельности,
	|	Т.СтоимостьРегл КАК Количество,
	|	Т.КоличествоСебестоимость
	|ПОМЕСТИТЬ ВТОстаткиСебестоимости
	|ИЗ
	|	ВТОстаткиСебестоимостиПредварительная КАК Т
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Т.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	Т.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	Т.РазделУчета КАК РазделУчета,
	|	Т.ВидЗапасов КАК ВидЗапасов,
	|	Т.Организация КАК Организация,
	|	Т.Партия КАК Партия,
	|	Т.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	Т.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|	Т.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	Т.НаправлениеДеятельности,
	|	СУММА(Т.ДопРасходыРегл) КАК Количество,
	|	СУММА(Т.КоличествоСебестоимость) КАК КоличествоСебестоимость
	|ИЗ
	|	ВТОстаткиСебестоимостиПредварительная КАК Т
	|СГРУППИРОВАТЬ ПО
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.АналитикаУчетаНоменклатуры.Номенклатура,
	|	Т.АналитикаУчетаНоменклатуры.Характеристика,
	|	Т.РазделУчета,
	|	Т.ВидЗапасов,
	|	Т.Организация,
	|	Т.Партия,
	|	Т.АналитикаУчетаПартий,
	|	Т.АналитикаФинансовогоУчета,
	|	Т.ВидДеятельностиНДС,
	|	Т.НаправлениеДеятельности
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.Партия,
	|	Т.ВидДеятельностиНДС,
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	Т.НаправлениеДеятельности,
	|	СУММА(Т.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТОстаткиСебестоимостиСвернуто
	|ИЗ
	|	ВТОстаткиСебестоимости КАК Т
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация,
	|	Т.Партия,
	|	Т.ВидДеятельностиНДС,
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	Т.НаправлениеДеятельности
	|ИМЕЮЩИЕ
	|	СУММА(Т.Количество) <> 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация КАК Организация,
	|	Т.Партия КАК Партия,
	|	Т.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	Т.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	Т.ДокументПоступления КАК ДокументПоступления,
	|	Т.АналитикаУчетаДокументаПоступления КАК АналитикаУчетаПартийДокументаПоступления,
	|	Т.Номенклатура КАК Номенклатура,
	|	Т.Характеристика КАК Характеристика,
	|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА Т.ВидДеятельностиНДС В (&ТипыНалогообложенияНДСНеУчитываетсяВСтоимости)
	|			ТОГДА Т.СтоимостьБезНДСОстаток
	|		ИНАЧЕ Т.СтоимостьБезНДСОстаток + Т.НДСОстаток
	|	КОНЕЦ КАК Количество,
	|	Т.СтоимостьБезНДСОстаток КАК СтоимостьБезНДС,
	|	Т.НДСОстаток КАК НДС,
	|	Т.СтоимостьБезНДСУпрОстаток КАК СтоимостьБезНДСУпр,
	|	Т.НДСУпрОстаток КАК НДСУпр
	|ПОМЕСТИТЬ ВТОстаткиДетализацииПартийПредварительная
	|ИЗ
	|	РегистрНакопления.ДетализацияПартийТоваровДляНДСиУСН2_4.Остатки(&ГраницаКонецПредыдущегоПериода, Организация В (&ОрганизацииДляКорректировки)) КАК Т
	|ГДЕ
	|	ВЫБОР
	|		КОГДА Т.ВидДеятельностиНДС В (&ТипыНалогообложенияНДСНеУчитываетсяВСтоимости)
	|			ТОГДА Т.СтоимостьБезНДСОстаток
	|		ИНАЧЕ Т.СтоимостьБезНДСОстаток + Т.НДСОстаток
	|	КОНЕЦ <> 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.Партия,
	|	Т.ВидДеятельностиНДС,
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	Т.НаправлениеДеятельности,
	|	СУММА(Т.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТОстаткиДетализацииПартийСвернуто
	|ИЗ
	|	ВТОстаткиДетализацииПартийПредварительная КАК Т
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация,
	|	Т.Партия,
	|	Т.ВидДеятельностиНДС,
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	Т.НаправлениеДеятельности
	|ИМЕЮЩИЕ
	|	СУММА(Т.Количество) <> 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.Партия КАК Партия,
	|	Т.ВидДеятельностиНДС,
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	Т.НаправлениеДеятельности,
	|	ВЫБОР КОГДА Т.Количество > База.Количество
	|		ТОГДА База.Количество / Т.Количество
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Коэффициент
	|ПОМЕСТИТЬ ВТКоэффициентыРаспределения
	|ИЗ
	|	ВТОстаткиДетализацииПартийСвернуто КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОстаткиСебестоимостиСвернуто КАК База
	|		ПО Т.Организация = База.Организация
	|		И Т.Партия = База.Партия
	|		И Т.ВидДеятельностиНДС = База.ВидДеятельностиНДС
	|		И Т.Номенклатура = База.Номенклатура
	|		И Т.Характеристика = База.Характеристика
	|		И Т.НаправлениеДеятельности = База.НаправлениеДеятельности
	|ИНДЕКСИРОВАТЬ ПО
	|	Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.Партия,
	|	Т.АналитикаУчетаПартий,
	|	Т.ВидДеятельностиНДС,
	|	Т.ДокументПоступления,
	|	Т.АналитикаУчетаПартийДокументаПоступления,
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	Т.НаправлениеДеятельности,
	|	ВЫРАЗИТЬ(Т.Количество * Коэффициенты.Коэффициент КАК ЧИСЛО (31,2)) КАК Количество,
	|	Т.СтоимостьБезНДС,
	|	Т.НДС,
	|	Т.СтоимостьБезНДСУпр,
	|	Т.НДСУпр
	|ПОМЕСТИТЬ ВТОстаткиДетализацииПартий
	|ИЗ
	|	ВТОстаткиДетализацииПартийПредварительная КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКоэффициентыРаспределения КАК Коэффициенты
	|		ПО Т.Организация = Коэффициенты.Организация
	|		И Т.Партия = Коэффициенты.Партия
	|		И Т.Номенклатура = Коэффициенты.Номенклатура
	|		И Т.Характеристика = Коэффициенты.Характеристика
	|		И Т.ВидДеятельностиНДС = Коэффициенты.ВидДеятельностиНДС
	|		И Т.НаправлениеДеятельности = Коэффициенты.НаправлениеДеятельности
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета,
		"ВТОстаткиСебестоимостиСвернуто, ВТОстаткиСебестоимостиПредварительная,
		|ВТОстаткиДетализацииПартийПредварительная, ВТОстаткиДетализацииПартийСвернуто, ВТКоэффициентыРаспределения");
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТОстаткиДетализацииПартий") = 0 Тогда
		Возврат;
	КонецЕсли;
	
	#КонецОбласти 
	
	#Область РаспределениеДанных
	
	// Инициализируем распределение
	ПараметрыРаспределения = РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПараметрыРаспределенияМетодомУменьшаемогоОстатка(
		"ВТОстаткиДетализацииПартий",
		"ВТОстаткиСебестоимости",
		"Организация, Партия, ВидДеятельностиНДС, Номенклатура, Характеристика, НаправлениеДеятельности");
	
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьЧисловыеПоляРаспределенияМетодомУменьшаемогоОстатка(
		ПараметрыРаспределения,
		"СтоимостьБезНДС, НДС, СтоимостьБезНДСУпр, НДСУпр");
		
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПрочиеПоляРаспределенияМетодомУменьшаемогоОстатка(
		ПараметрыРаспределения,
		"ДокументПоступления, АналитикаУчетаПартийДокументаПоступления",
		"АналитикаУчетаНоменклатуры, РазделУчета, ВидЗапасов, АналитикаФинансовогоУчета, АналитикаУчетаПартий, КоличествоСебестоимость");
		
	// Распределяем
	РасчетСебестоимостиПрикладныеАлгоритмы.РаспределитьМетодомУменьшаемогоОстаткаСЗамеромВремени(
		ПараметрыРасчета,
		ПараметрыРаспределения,
		"РаспределитьМетодомУменьшаемогоОстатка",
		"КорректировкаПереходНаНовыйРегистрДетализацияСебестоимостиТоваров");
	
	Если ПараметрыРаспределения.РезультатыРаспределения.РазмерРезультата = 0 Тогда
		Возврат; // нет распределенных данных
	КонецЕсли;
		
	#КонецОбласти
	
	#Область ФормированиеИтоговыхТаблицОстатков
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Т.РазделУчета КАК РазделУчета,
	|	Т.ВидЗапасов КАК ВидЗапасов,
	|	Т.Организация КАК Организация,
	|	Т.Партия КАК Партия,
	|	Т.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	Т.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|	Т.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	Т.ДокументПоступления КАК ДокументПоступления,
	|	Т.АналитикаУчетаПартийДокументаПоступления КАК АналитикаУчетаПартийДокументаПоступления,
	|	Т.КоличествоСебестоимость КАК КоличествоПартии,
	|	Т.СтоимостьБезНДС,
	|	Т.НДС,
	|	Т.СтоимостьБезНДСУпр,
	|	Т.НДСУпр
	|ПОМЕСТИТЬ ВТОстаткиДетализацииПартийПредварительная
	|ИЗ
	|	ВТРезультатРаспределения КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.Партия,
	|	Т.АналитикаУчетаПартий,
	|	Т.ВидЗапасов,
	|	ВЫРАЗИТЬ(Т.АналитикаУчетаНоменклатуры КАК Справочник.КлючиАналитикиУчетаНоменклатуры).Номенклатура КАК Номенклатура,
	|	ВЫРАЗИТЬ(Т.АналитикаУчетаНоменклатуры КАК Справочник.КлючиАналитикиУчетаНоменклатуры).Характеристика КАК Характеристика,
	|	Т.ДокументПоступления,
	|	Т.АналитикаУчетаПартийДокументаПоступления,
	|	ВЫРАЗИТЬ(СУММА(Т.СтоимостьБезНДСУпр + Т.НДСУпр) / СУММА(Т.КоличествоПартии) КАК ЧИСЛО (31,10)) 	КАК СтоимостьСНДС,
	|	ВЫРАЗИТЬ(СУММА(Т.СтоимостьБезНДСУпр) / СУММА(Т.КоличествоПартии) КАК ЧИСЛО (31,10)) 			КАК СтоимостьБезНДС,
	|	ВЫРАЗИТЬ(СУММА(Т.СтоимостьБезНДС) / СУММА(Т.КоличествоПартии) КАК ЧИСЛО (31,10)) 				КАК СтоимостьБезНДСРегл,
	|	ВЫРАЗИТЬ(СУММА(Т.НДС) / СУММА(Т.КоличествоПартии) КАК ЧИСЛО (31,10)) 							КАК НДСРегл,
	|	ВЫРАЗИТЬ(СУММА(Т.СтоимостьБезНДСУпр) / СУММА(Т.КоличествоПартии) КАК ЧИСЛО (31,10)) 			КАК СтоимостьБезНДСУпр,
	|	ВЫРАЗИТЬ(СУММА(Т.НДСУпр) / СУММА(Т.КоличествоПартии) КАК ЧИСЛО (31,10)) 						КАК НДСУпр,
	|	СУММА(Т.КоличествоПартии) 																		КАК КоличествоПартии
	|ПОМЕСТИТЬ ВТОстаткиДетализацииПартий
	|ИЗ
	|	ВТОстаткиДетализацииПартийПредварительная КАК Т
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация,
	|	Т.Партия,
	|	Т.АналитикаУчетаПартий,
	|	Т.ВидЗапасов,
	|	ВЫРАЗИТЬ(Т.АналитикаУчетаНоменклатуры КАК Справочник.КлючиАналитикиУчетаНоменклатуры).Номенклатура,
	|	ВЫРАЗИТЬ(Т.АналитикаУчетаНоменклатуры КАК Справочник.КлючиАналитикиУчетаНоменклатуры).Характеристика,
	|	Т.ДокументПоступления,
	|	Т.АналитикаУчетаПартийДокументаПоступления
	|ИМЕЮЩИЕ
	|	СУММА(Т.КоличествоПартии) <> 0
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос); // формируем ВТОстаткиДетализацииПартий, ВТОстаткиПартийНДСФИФО
	
	#КонецОбласти
	
	// Добавляем результаты распределения к корректировочным движениям
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		"ВТОстаткиДетализацииПартий",
		,
		Истина);
	
КонецПроцедуры

// Выполняет безусловное включение учета по видам запасов - заполняет виды запасов.
//
Процедура КорректировкаВключитьУчетПоВидамЗапасовПартииНДСиУСН(ПараметрыРасчета, ПараметрыКорректировки)
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		Перечисления.ТипыЗаписейПартий.СлужебноеВключитьУчетПоВидамЗапасов);
	
	Если НЕ ПараметрыКорректировки.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	#Область ПодготовкаДанных
	
	ОписаниеАналитики = СформироватьОписаниеКлючейАналитикУчетаНоменклатуры(, "Назначение");
	ОписаниеРегистраСебестоимость = ПараметрыРасчета.Движения[Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя];
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	Запрос.УстановитьПараметр("ОрганизацииДляКорректировки", ПараметрыКорректировки.ОрганизацииДляКорректировки);
	
	// Выберем данные для распределения
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	%ИзмеренияРегистраПартийНДС,
	|	%РесурсыРегистраПартийНДС,
	|	%ПоляАналитикиИменованные
	|ПОМЕСТИТЬ ВТОстаткиПартийНДСБезВидовЗапасов
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиДетализацияПартийТоваровДляНДСиУСН КАК Т
	|ГДЕ
	|	Т.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|	И Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И НЕ (Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		  И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения В
	|			(ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер),
	|			 ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Организация)))";

	РаспределяемыеИзмеренияПартийНДС = РасчетСебестоимостиУниверсальныеАлгоритмы.УдалитьЭлементыИзСтрокиШаблона(
		ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра,
		"%1ВидЗапасов");
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИзмеренияРегистраПартийНДС",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(РаспределяемыеИзмеренияПартийНДС, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%РесурсыРегистраПартийНДС",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляАналитикиИменованные",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеАналитики.ПоляИменованные, "Т."));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТОстаткиПартийНДСБезВидовЗапасов") = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	%ИзмеренияРегистраСебестоимости,
	|	Т.КоличествоОстаток КАК Количество,
	|	%ПоляАналитикиИменованные
	|ПОМЕСТИТЬ ВТОстаткиСебестоимостиСВидамиЗапасов
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииДляКорректировки)
	|	) КАК Т
	|ГДЕ
	|	Т.ВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|	И Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И НЕ (Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		  И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения В
	|			(ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер),
	|			 ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Организация)))";
	
	РаспределяемыеИзмеренияСебестоимость = РасчетСебестоимостиУниверсальныеАлгоритмы.УдалитьЭлементыИзСтрокиШаблона(
		ОписаниеРегистраСебестоимость.ИзмеренияРегистра,
		"%1Партия, %1АналитикаУчетаПартий");
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИзмеренияРегистраСебестоимости",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(РаспределяемыеИзмеренияСебестоимость, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляАналитикиИменованные",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеАналитики.ПоляИменованные, "Т."));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	#КонецОбласти 
	
	#Область РаспределениеДанных
	
	УчетПоНазначениям = РасчетСебестоимостиПовтИсп.СебестоимостьТоваровПоНазначениям(ПараметрыРасчета.РасчетныйПериод.НачалоПериода);
	
	ПоляСвязи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		РасчетСебестоимостиУниверсальныеАлгоритмы.УдалитьЭлементыИзСтрокиШаблона(
			ОписаниеРегистраСебестоимость.ИзмеренияРегистра,
			"%1ВидЗапасов, %1Партия, %1АналитикаУчетаПартий, %1АналитикаУчетаНоменклатуры"),
		"");
	
	// Инициализируем распределение
	ПараметрыРаспределения = РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПараметрыРаспределенияМетодомУменьшаемогоОстатка(
		"ВТОстаткиПартийНДСБезВидовЗапасов",
		"ВТОстаткиСебестоимостиСВидамиЗапасов",
		РасчетСебестоимостиУниверсальныеАлгоритмы.СоединитьСтроки(ПоляСвязи, ОписаниеАналитики.ПереченьПолей));
	
	РаспределяемыеРесурсы = РасчетСебестоимостиУниверсальныеАлгоритмы.УдалитьЭлементыИзСтрокиШаблона(
		ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра,
		"%1Количество");
	
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьЧисловыеПоляРаспределенияМетодомУменьшаемогоОстатка(
		ПараметрыРаспределения,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(РаспределяемыеРесурсы, ""));
		
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПрочиеПоляРаспределенияМетодомУменьшаемогоОстатка(
		ПараметрыРаспределения,
		"Партия, АналитикаУчетаПартий, ДокументПоступления, АналитикаУчетаНоменклатуры",
		"ВидЗапасов" + ?(УчетПоНазначениям, ", АналитикаУчетаНоменклатуры", ""));
		
	// Распределяем
	РасчетСебестоимостиПрикладныеАлгоритмы.РаспределитьМетодомУменьшаемогоОстаткаСЗамеромВремени(
		ПараметрыРасчета,
		ПараметрыРаспределения,
		"РаспределитьМетодомУменьшаемогоОстатка",
		"КорректировкаВключитьУчетПоВидамЗапасовПартииНДСиУСН");
	
	Если ПараметрыРаспределения.РезультатыРаспределения.РазмерРезультата = 0 Тогда
		Возврат; // нет распределенных данных
	КонецЕсли;
		
	Если НЕ УчетПоНазначениям Тогда
		ЕстьКорректировки = СформироватьАналитикиНоменклатурыБезНазначения(
			ПараметрыРасчета,
			"ВТРезультатРаспределения",
			"ВТРезультатРаспределенияСкорректированный");
	Иначе
		ЕстьКорректировки = Ложь;
	КонецЕсли;
	
	#КонецОбласти
	
	// Добавляем результаты распределения к корректировочным движениям
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		?(ЕстьКорректировки, "ВТРезультатРаспределенияСкорректированный", "ВТРезультатРаспределения") + ", ВТНераспределенныеДанныеИсточника",
		"ВТОстаткиПартийНДСБезВидовЗапасов",
		Истина,
		Истина);
	
КонецПроцедуры

// Выполняет включение учета по назначениям - заполняет назначения в аналитике номенклатуры.
//
Процедура КорректировкаВключитьУчетПоНазначениямПартииНДСиУСН(ПараметрыРасчета, ПараметрыКорректировки)
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		Перечисления.ТипыЗаписейПартий.СлужебноеВключитьУчетПоНазначениям);
	
	Если НЕ ПараметрыКорректировки.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	#Область ПодготовкаДанных
	
	ОписаниеАналитики = СформироватьОписаниеКлючейАналитикУчетаНоменклатуры(, "Назначение");
	ОписаниеРегистраСебестоимость = ПараметрыРасчета.Движения[Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя];
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	Запрос.УстановитьПараметр("ОрганизацииДляКорректировки", ПараметрыКорректировки.ОрганизацииДляКорректировки);
	
	// Выберем данные для распределения
	Запрос.Текст =
	"ВЫБРАТЬ
	|	%ИзмеренияРегистра,
	|	%РесурсыРегистра,
	|	%ПоляАналитикиИменованные
	|ПОМЕСТИТЬ ВТОстаткиПартийНДСБезНазначений
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиДетализацияПартийТоваровДляНДСиУСН КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИзмеренияРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%РесурсыРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляАналитикиИменованные",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеАналитики.ПоляИменованные, "Т."));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТОстаткиПартийНДСБезНазначений") = 0 Тогда
		Возврат; // нет распределенных данных
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	%ИзмеренияРегистра,
	|	%ПоляАналитикиИменованные,
	|	Т.КоличествоОстаток КАК Количество
	|ПОМЕСТИТЬ ВТОстаткиСНазначениями
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииДляКорректировки)
	|		И АналитикаУчетаНоменклатуры.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	) КАК Т
	|ГДЕ
	|	Т.КоличествоОстаток > 0";
	
	РаспределяемыеИзмеренияСебестоимость = РасчетСебестоимостиУниверсальныеАлгоритмы.УдалитьЭлементыИзСтрокиШаблона(
		ОписаниеРегистраСебестоимость.ИзмеренияРегистра,
		"%1Партия, %1АналитикаУчетаПартий");
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИзмеренияРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(РаспределяемыеИзмеренияСебестоимость, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляАналитикиИменованные",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеАналитики.ПоляИменованные, "Т."));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	#КонецОбласти 
	
	#Область РаспределениеДанных
	
	ПоляСвязи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		РасчетСебестоимостиУниверсальныеАлгоритмы.УдалитьЭлементыИзСтрокиШаблона(
			ОписаниеРегистраСебестоимость.ИзмеренияРегистра,
			"%1Партия, %1АналитикаУчетаПартий, %1АналитикаУчетаНоменклатуры"),
		"");
	
	// Инициализируем распределение
	ПараметрыРаспределения = РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПараметрыРаспределенияМетодомУменьшаемогоОстатка(
		"ВТОстаткиПартийНДСБезНазначений",
		"ВТОстаткиСНазначениями",
		РасчетСебестоимостиУниверсальныеАлгоритмы.СоединитьСтроки(ПоляСвязи, ОписаниеАналитики.ПереченьПолей));
		
	РаспределяемыеРесурсы = РасчетСебестоимостиУниверсальныеАлгоритмы.УдалитьЭлементыИзСтрокиШаблона(
		ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра,
		"%1Количество");
	
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьЧисловыеПоляРаспределенияМетодомУменьшаемогоОстатка(
		ПараметрыРаспределения,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(РаспределяемыеРесурсы, ""));
		
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПрочиеПоляРаспределенияМетодомУменьшаемогоОстатка(
		ПараметрыРаспределения,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра, ""),
		"АналитикаУчетаНоменклатуры");
	
	// Распределяем
	РасчетСебестоимостиПрикладныеАлгоритмы.РаспределитьМетодомУменьшаемогоОстаткаСЗамеромВремени(
		ПараметрыРасчета,
		ПараметрыРаспределения,
		"РаспределитьМетодомУменьшаемогоОстатка",
		"КорректировкаВключитьУчетПоНазначениямПартииНДСиУСН");
	
	#КонецОбласти
	
	// Добавляем результаты распределения к корректировочным движениям
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		"ВТРезультатРаспределения, ВТНераспределенныеДанныеИсточника",
		"ВТОстаткиПартийНДСБезНазначений",
		Истина,
		Истина);
		
КонецПроцедуры

// Выполняет отключение учета по назначениям - очищает назначения в аналитике номенклатуры.
//
Процедура КорректировкаОтключитьУчетПоНазначениямПартииНДСиУСН(ПараметрыРасчета, ПараметрыКорректировки)
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		Перечисления.ТипыЗаписейПартий.СлужебноеОтключитьУчетПоНазначениям);
	
	Если НЕ ПараметрыКорректировки.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	#Область ПодготовкаДанных
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	Запрос.УстановитьПараметр("ОрганизацииДляКорректировки", ПараметрыКорректировки.ОрганизацииДляКорректировки);
	
	// Выберем данные для корректировки
	Запрос.Текст =
	"ВЫБРАТЬ
	|	%ИзмеренияРегистра,
	|	%РесурсыРегистра
	|ПОМЕСТИТЬ ВТОстаткиПартийНДССНазначением
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиДетализацияПартийТоваровДляНДСиУСН КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИзмеренияРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%РесурсыРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра, "Т."));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	ЕстьКорректировки = СформироватьАналитикиНоменклатурыБезНазначения(
		ПараметрыРасчета,
		"ВТОстаткиПартийНДССНазначением",
		"ВТОстаткиПартийНДСБезНазначения");
	
	Если НЕ ЕстьКорректировки Тогда
		Возврат;
	КонецЕсли;
	
	#КонецОбласти 
	
	// Добавляем результаты распределения к корректировочным движениям
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		"ВТОстаткиПартийНДСБезНазначения",
		"ВТОстаткиПартийНДССНазначением",
		Истина,
		Истина);
	
КонецПроцедуры

// Выполняет безусловную корректировку аналитики номенклатуры - заменяет в аналитике партнера на его договор.
//
Процедура КорректировкаЗаменитьПартнераНаДоговорВАналитикеУчетаНоменклатурыПартииНДСиУСН(ПараметрыРасчета, ПараметрыКорректировки)
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		Перечисления.ТипыЗаписейПартий.СлужебноеИзменениеПартнераВАналитикеУчетаНоменклатуры);
	
	Если НЕ ПараметрыКорректировки.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	#Область ПодготовкаДанных
	
	ОписаниеАналитики = СформироватьОписаниеКлючейАналитикУчетаНоменклатуры(, "МестоХранения");
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	Запрос.УстановитьПараметр("ОрганизацииДляКорректировки", ПараметрыКорректировки.ОрганизацииДляКорректировки);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.АналитикаУчетаНоменклатуры.Партнер КАК Партнер,
	|	%ПоляАналитикиИменованные,
	|	%ИзмеренияРегистра,
	|	%РесурсыРегистра
	|ПОМЕСТИТЬ ВТОстаткиСАналитикойПартнер
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиДетализацияПартийТоваровДляНДСиУСН КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|	И Т.АналитикаУчетаНоменклатуры.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|	И Т.РазделУчета В
	|		(ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки),
	|		 ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути))";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИзмеренияРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%РесурсыРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляАналитикиИменованные",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеАналитики.ПоляИменованные, "Т."));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, "ВТОстаткиСАналитикойПартнер") = 0 Тогда
		Возврат; // нет данных для распределения
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.АналитикаУчетаНоменклатуры.Договор.Партнер КАК Партнер,
	|	%ПоляАналитикиИменованные,
	|	%ИзмеренияРегистра,
	|	Т.КоличествоОстаток 						 КАК Количество
	|ПОМЕСТИТЬ ВТОстаткиСАналитикойДоговор
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров.Остатки(&ГраницаКонецПредыдущегоПериода,
	|		Организация В (&ОрганизацииДляКорректировки)
	|		И АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.ДоговорКонтрагента)
	|		И АналитикаУчетаНоменклатуры.Договор <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|		И АналитикаУчетаНоменклатуры.Договор.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|	) КАК Т
	|ГДЕ
	|	Т.КоличествоОстаток > 0";
	
	ОписаниеРегистраСебестоимость = ПараметрыРасчета.Движения[Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя];
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИзмеренияРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеРегистраСебестоимость.ИзмеренияРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляАналитикиИменованные",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеАналитики.ПоляИменованные, "Т."));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	#КонецОбласти 
	
	#Область РаспределениеДанных
	
	ПоляСвязи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		РасчетСебестоимостиУниверсальныеАлгоритмы.УдалитьЭлементыИзСтрокиШаблона(
			ОписаниеРегистраСебестоимость.ИзмеренияРегистра,
			"%1Партия, %1АналитикаУчетаПартий, %1АналитикаУчетаНоменклатуры"),
		"");
	
	// Инициализируем распределение
	ПараметрыРаспределения = РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПараметрыРаспределенияМетодомУменьшаемогоОстатка(
		"ВТОстаткиСАналитикойПартнер",
		"ВТОстаткиСАналитикойДоговор",
		РасчетСебестоимостиУниверсальныеАлгоритмы.СоединитьСтроки("Партнер", ПоляСвязи, ОписаниеАналитики.ПереченьПолей));
	
	РаспределяемыеРесурсы = РасчетСебестоимостиУниверсальныеАлгоритмы.УдалитьЭлементыИзСтрокиШаблона(
		ПараметрыКорректировки.ОписаниеРегистра.РесурсыРегистра,
		"%1Количество");
	
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьЧисловыеПоляРаспределенияМетодомУменьшаемогоОстатка(
		ПараметрыРаспределения,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(РаспределяемыеРесурсы, ""));
		
	РасчетСебестоимостиУниверсальныеАлгоритмы.ИнициализироватьПрочиеПоляРаспределенияМетодомУменьшаемогоОстатка(
		ПараметрыРаспределения,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра, ""),
		"АналитикаУчетаНоменклатуры");
	
	// Распределяем
	РасчетСебестоимостиПрикладныеАлгоритмы.РаспределитьМетодомУменьшаемогоОстаткаСЗамеромВремени(
		ПараметрыРасчета,
		ПараметрыРаспределения,
		"РаспределитьМетодомУменьшаемогоОстатка",
		"КорректировкаЗаменитьПартнераНаДоговорВАналитикеУчетаНоменклатурыПартииНДСиУСН");
	
	Если ПараметрыРаспределения.РезультатыРаспределения.РазмерРезультата = 0 Тогда
		Возврат; // нет распределенных данных
	КонецЕсли;
		
	#КонецОбласти
	
	// Добавляем результаты распределения к корректировочным движениям
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		"ВТРезультатРаспределения, ВТНераспределенныеДанныеИсточника",
		"ВТОстаткиСАналитикойПартнер",
		Истина,
		Истина);
	
КонецПроцедуры

// Выполняет включение упр. учета - заполняет упр. ресурсы регистра.
//
Процедура КорректировкаЗаполнитьСуммыУпрУчетаПартииНДСиУСН(ПараметрыРасчета, ПараметрыКорректировкиНДССредняя, ПараметрыКорректировкиНДСФИФО)
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировкиНДССредняя,
		Перечисления.ТипыЗаписейПартий.СлужебноеВключитьУправленческийУчетПоПравиламМФУ);
		
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировкиНДСФИФО,
		Перечисления.ТипыЗаписейПартий.СлужебноеВключитьУправленческийУчетПоПравиламМФУ);
		
	Если НЕ ПараметрыКорректировкиНДССредняя.ТребуетсяКорректировка И НЕ ПараметрыКорректировкиНДСФИФО.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	#Область ПодготовкаДанных
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	Запрос.УстановитьПараметр("ОрганизацииДляКорректировки", ПараметрыКорректировкиНДССредняя.ОрганизацииДляКорректировки);
	
	// Выберем данные для формирования начальных остатков
	Запрос.Текст =
	"ВЫБРАТЬ
	|	%ИзмеренияРегистраНДССредняя,
	|	- Т.НДСУпр + Т.НДС КАК НДСУпр
	|ПОМЕСТИТЬ ВТОстаткиСуммыУпрНДССредняя
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиДетализацияПартийТоваровДляНДСиУСН КАК Т
	|ГДЕ
	|	&ВалютыУпрИРеглУчетаСовпадают
	|	И - Т.НДСУпр + Т.НДС <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	%ИзмеренияРегистраНДСФИФО,
	|	Т.СтоимостьБезНДС							КАК СтоимостьБезНДС,
	|	Т.НДС										КАК НДС,
	|	Т.КоличествоПартии							КАК КоличествоПартии,
	|	- Т.СтоимостьБезНДСУпр + Т.СтоимостьБезНДС  КАК СтоимостьБезНДСУпр,
	|	- Т.НДСУпр + Т.НДС							КАК НДСУпр
	|ПОМЕСТИТЬ ВТОстаткиСуммыУпрНДСФИФО
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиДетализацияСебестоимостиТоваров КАК Т
	|ГДЕ
	|	&ВалютыУпрИРеглУчетаСовпадают
	|	И Т.КоличествоПартии <> 0
	|	И (- Т.СтоимостьБезНДСУпр + Т.СтоимостьБезНДС <> 0
	|		ИЛИ - Т.НДСУпр + Т.НДС <> 0)";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИзмеренияРегистраНДССредняя",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировкиНДССредняя.ОписаниеРегистра.ИзмеренияРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИзмеренияРегистраНДСФИФО",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировкиНДСФИФО.ОписаниеРегистра.ИзмеренияРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "Т.ТипЗаписи,", "");
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	#КонецОбласти 
	
	// Добавляем данные к корректировочным движениям
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировкиНДССредняя,
		"ВТОстаткиСуммыУпрНДССредняя",
		,
		Истина);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировкиНДСФИФО,
		"ВТОстаткиСуммыУпрНДСФИФО",
		,
		Истина);
	
КонецПроцедуры

// Выполняет отключение упр. учета - очищает упр. ресурсы регистра.
//
Процедура КорректировкаОчиститьСуммыУпрУчетаПартииНДСиУСН(ПараметрыРасчета, ПараметрыКорректировкиНДССредняя, ПараметрыКорректировкиНДСФИФО)
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировкиНДССредняя,
		Перечисления.ТипыЗаписейПартий.СлужебноеОтключитьУправленческийУчетПоПравиламМФУ);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировкиНДСФИФО,
		Перечисления.ТипыЗаписейПартий.СлужебноеОтключитьУправленческийУчетПоПравиламМФУ);
	
	Если НЕ ПараметрыКорректировкиНДССредняя.ТребуетсяКорректировка И НЕ ПараметрыКорректировкиНДСФИФО.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	#Область ПодготовкаДанных
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	Запрос.УстановитьПараметр("ОрганизацииДляКорректировки", ПараметрыКорректировкиНДССредняя.ОрганизацииДляКорректировки);
	
	// Выберем данные для очистки остатков сумм упр. учета
	Запрос.Текст =
	"ВЫБРАТЬ
	|	%ИзмеренияРегистраНДССредняя,
	|	- Т.НДСУпр КАК НДСУпр
	|ПОМЕСТИТЬ ВТОстаткиОчисткаСуммУпрНДССредняя
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиДетализацияПартийТоваровДляНДСиУСН КАК Т
	|ГДЕ
	|	- Т.НДСУпр <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	%ИзмеренияРегистраНДСФИФО,
	|	Т.СтоимостьБезНДС		КАК СтоимостьБезНДС,
	|	Т.НДС					КАК НДС,
	|	Т.КоличествоПартии		КАК КоличествоПартии,
	|	- Т.СтоимостьБезНДСУпр	КАК СтоимостьБезНДСУпр,
	|	- Т.НДСУпр 			 	КАК НДСУпр
	|ПОМЕСТИТЬ ВТОстаткиОчисткаСуммУпрНДСФИФО
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиДетализацияСебестоимостиТоваров КАК Т
	|ГДЕ
	|	Т.КоличествоПартии <> 0
	|	И (- Т.СтоимостьБезНДСУпр <> 0 ИЛИ - Т.НДСУпр <> 0)";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИзмеренияРегистраНДССредняя",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировкиНДССредняя.ОписаниеРегистра.ИзмеренияРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИзмеренияРегистраНДСФИФО",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировкиНДСФИФО.ОписаниеРегистра.ИзмеренияРегистра, "Т."));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "Т.ТипЗаписи,", "");
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	#КонецОбласти 
	
	// Добавляем данные к корректировочным движениям
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировкиНДССредняя,
		"ВТОстаткиОчисткаСуммУпрНДССредняя",
		,
		Истина);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировкиНДСФИФО,
		"ВТОстаткиОчисткаСуммУпрНДСФИФО",
		,
		Истина);
	
КонецПроцедуры

#КонецОбласти

#Область НДСПредъявленный

// Выполняет все необходимые корректировки регистра НДСПредъявленный.
//
Процедура КорректировкаНачальныхОстатковРегистраНДСПредъявленный(ПараметрыРасчета)
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "КорректировкаНачальныхОстатковРегистраНДСПредъявленный");
	
	ПараметрыКорректировки = РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		Метаданные.РегистрыНакопления.НДСПредъявленный.ПолноеИмя());
	
	Если ПараметрыРасчета.ФормироватьНачальныеОстаткиПартий22 Тогда
		// При расчете первого месяца на партионном учете версии 2.2
		// надо сформировать остатки НДСУпр в регистре НДСПредъявленный.
		СформироватьНачальныеОстаткиНДСПредъявленный(ПараметрыРасчета, ПараметрыКорректировки);
	КонецЕсли;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗавершитьКорректировкуОстатковРегистра(ПараметрыРасчета, ПараметрыКорректировки);
	
КонецПроцедуры

// При расчете первого месяца в партионном учете версии 2.2 формирует начальные остатки НДСУпр в регистре НДСПредъявленный.
//
Процедура СформироватьНачальныеОстаткиНДСПредъявленный(ПараметрыРасчета, ПараметрыКорректировки)
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		Перечисления.ТипыЗаписейПартий.СлужебноеПереходНаПартионныйУчет22);
	
	Если НЕ ПараметрыКорректировки.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	#Область ПодготовкаДанных
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	Запрос.УстановитьПараметр("ОрганизацииДляКорректировки", ПараметрыКорректировки.ОрганизацииДляКорректировки);
	
	// Выберем данные для формирования начальных остатков
	Запрос.Текст =
	"ВЫБРАТЬ
	|	%ИзмеренияРегистра,
	|	ИСТИНА КАК РегламентнаяОперация,
	|	Т.НДС КАК НДСУпр
	|ПОМЕСТИТЬ ВТНачальныеОстаткиНДСУпр
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиНДСПредъявленный КАК Т
	|ГДЕ
	|	&ВалютыУпрИРеглУчетаСовпадают
	|	И &УправленческийУчетОрганизаций
	|	И Т.НДС <> 0";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИзмеренияРегистра",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыКорректировки.ОписаниеРегистра.ИзмеренияРегистра, "Т."));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	#КонецОбласти
	
	// Добавляем данные к корректировочным движениям
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		"ВТНачальныеОстаткиНДСУпр",
		,
		Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ПартииПрочихРасходов

// Выполняет все необходимые корректировки регистра ПартииПрочихРасходов.
//
Процедура КорректировкаНачальныхОстатковРегистраПартииПрочихРасходов(ПараметрыРасчета)
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "КорректировкаНачальныхОстатковРегистраПартииПрочихРасходов");
	
	ПараметрыКорректировки = РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		Метаданные.РегистрыНакопления.ПартииПрочихРасходов.ПолноеИмя());
	
	Если НЕ ПараметрыРасчета.ФормироватьНачальныеОстаткиПартий22 Тогда
		СкорректироватьНачальныеОстаткиПартииПрочихРасходов(ПараметрыРасчета, ПараметрыКорректировки);
	КонецЕсли;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗавершитьКорректировкуОстатковРегистра(ПараметрыРасчета, ПараметрыКорректировки);
	
КонецПроцедуры

// Списывает зависшие остатки регистра по аналитикам расходов прошлых периодов.
//
Процедура СкорректироватьНачальныеОстаткиПартииПрочихРасходов(ПараметрыРасчета, ПараметрыКорректировки)
	
	РасчетСебестоимостиПрикладныеАлгоритмы.НачатьСледующуюКорректировкуОстатковРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		Перечисления.ТипыЗаписейПартий.СлужебноеКорректировкаОстатковПартийПрочихРасходов);
	
	Если НЕ ПараметрыКорректировки.ТребуетсяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	#Область ПодготовкаДанных
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	Запрос.УстановитьПараметр("МассивОрганизаций", ПараметрыКорректировки.ОрганизацииДляКорректировки);
	Запрос.УстановитьПараметр("УчитыватьКорректировки", Истина);
	
	// Выберем данные для корректировки остатков
	Запрос.Текст = РасчетСебестоимостиНДС.ТекстЗапросаСоответствияПрочиеРасходыИПартийПрочихРасходов();
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ГраницаКонецПериода", "&НачалоПериода");
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.Организация КАК Организация,
	|	Т.Подразделение КАК Подразделение,
	|	Т.СтатьяРасходов КАК СтатьяРасходов,
	|	Т.АналитикаРасходов КАК АналитикаРасходов,
	|	Т.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	Т.ДокументПоступленияРасходов КАК ДокументПоступленияРасходов,
	|	Т.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|
	|	-Т.Стоимость КАК Стоимость,
	|	-Т.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|	-Т.СтоимостьРегл КАК СтоимостьРегл,
	|	-Т.НДСРегл КАК НДСРегл,
	|	-Т.ПостояннаяРазница КАК ПостояннаяРазница,
	|	-Т.ВременнаяРазница КАК ВременнаяРазница,
	|	-Т.НДСУпр КАК НДСУпр
	|ПОМЕСТИТЬ ВТКорректировкаОстатковПартийПрочихРасходов
	|ИЗ
	|	ВТРасчетныеНачальныеОстаткиПартииПрочихРасходов КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОстаткиПартийПрочихРасходов КАК Отбор
	|		ПО Т.Организация = Отбор.Организация
	|		И Т.Подразделение = Отбор.Подразделение
	|		И Т.НаправлениеДеятельности = Отбор.НаправлениеДеятельности
	|		И Т.СтатьяРасходов = Отбор.СтатьяРасходов
	|		И Т.АналитикаРасходов = Отбор.АналитикаРасходов
	|ГДЕ
	|	Т.СтатьяРасходов.ВариантРаспределенияРасходовРегл В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров))
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВыполнитьЗапросСЗамеромДляПротокола(ПараметрыРасчета, Запрос);
	
	#КонецОбласти
	
	// Добавляем данные к корректировочным движениям
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьКорректировочныеДвиженияРегистра(
		ПараметрыРасчета,
		ПараметрыКорректировки,
		"ВТКорректировкаОстатковПартийПрочихРасходов",
		,
		Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОперацииСКлючамиАналитик

// Заполняет вид ценности в ключах аналитики учета партий и создает временную таблицу ВТАналитикиПартийСВидомЦенности.
//
Процедура СформироватьАналитикиПартийСВидомЦенности(ПараметрыРасчета,
	ИмяИсходнойТаблицы = "ВТОстаткиПартийПредварительная", ИмяТаблицыРезультата = "ВТАналитикиПартийСВидомЦенности")
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.АналитикаУчетаПартий,
	|	ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка) КАК ГруппаФинансовогоУчета,
	|	Ключи.Поставщик,
	|	Ключи.Контрагент,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
	|	Ключи.СтавкаНДС,
	|	Т.ВидЦенности,
	|	0 КАК КодСтроки
	|ПОМЕСТИТЬ ВТРеквизитыНовыхКлючейАналитикиУчетаПартий
	|ИЗ
	|	ВТОстаткиПартийПредварительная КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК Ключи
	|		ПО Т.АналитикаУчетаПартий = Ключи.Ссылка
	|ГДЕ
	|	Ключи.ВидЦенности <> Т.ВидЦенности
	|	ИЛИ Ключи.ГруппаФинансовогоУчета <> ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)
	|	ИЛИ Ключи.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоПериода КАК Дата,
	|	Т.ГруппаФинансовогоУчета,
	|	Т.Поставщик,
	|	Т.Контрагент,
	|	Т.НалогообложениеНДС,
	|	Т.СтавкаНДС,
	|	Т.ВидЦенности,
	|	Т.КодСтроки
	|ИЗ
	|	ВТРеквизитыНовыхКлючейАналитикиУчетаПартий КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикаПартий
	|		ПО Т.ГруппаФинансовогоУчета = АналитикаПартий.ГруппаФинансовогоУчета
	|		 И Т.Поставщик 				= АналитикаПартий.Поставщик
	|		 И Т.Контрагент 			= АналитикаПартий.Контрагент
	|		 И Т.НалогообложениеНДС 	= АналитикаПартий.НалогообложениеНДС
	|		 И Т.СтавкаНДС 				= АналитикаПартий.СтавкаНДС
	|		 И Т.ВидЦенности 			= АналитикаПартий.ВидЦенности
	|		 И Т.КодСтроки 				= АналитикаПартий.КодСтроки
	|ГДЕ
	|	ЕСТЬNULL(АналитикаПартий.КлючАналитики, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка))
	|		= ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТОстаткиПартийПредварительная", ИмяИсходнойТаблицы);
	
	Выборка = Запрос.Выполнить().Выбрать(); // получим аналитики учета партий с некорректным видом ценности
	Пока Выборка.Следующий() Цикл
		
		// Вид ценности в ключе аналитики, по которому есть остатки "старых" партий (из партионных регистров),
		// по всей видимости будет не заполнен.
		// В то же время, при формировании остатков "новых" партий (в регистре себестоимости),
		// мы знаем к какому именно виду ценности относятся эти остатки.
		// Поэтому, если вид ценности в ключе аналитики "старых" остатков не совпадает с видом ценности "новых" остатков,
		// то ключ аналитики "новых" остатков нельзя брать из "старых" остатков - его надо заменить.
		// Аналогичная ситуация и с реквизитами "ГруппаФинансовогоУчета" и "НалогообложениеНДС" - они должны быть пустыми,
		// но в "старых" ключах они скорее всего будут заполнены.
		// При этом, подходящий ключ для "новых" остатков уже может существовать в ИБ:
		// - до выполнения этого кода (при закрытии месяца) в ИБ добавлялись/изменялись документы
		// - при проведении этих документов формировались ключи аналитики с заполненным видом ценности
		// - эти ключи могут оказаться подходящими для "новых" остатков
		// Создадим только ключи, отсутствующие в ИБ на текущий момент.
		НовыйКлючАналитики = Справочники.КлючиАналитикиУчетаПартий.ПолучитьКлючАналитики(Выборка, Ложь);
		
	КонецЦикла;
	
	// Теперь в регистре (и в справочнике) ключей есть все необходимые ключи для "новых" остатков.
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.АналитикаУчетаПартий,
	|	Т.ВидЦенности,
	|	АналитикаПартий.КлючАналитики КАК АналитикаУчетаПартийСВидомЦенности
	|ПОМЕСТИТЬ ВТАналитикиПартийСВидомЦенности
	|ИЗ
	|	ВТРеквизитыНовыхКлючейАналитикиУчетаПартий КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикаПартий
	|		ПО Т.ГруппаФинансовогоУчета = АналитикаПартий.ГруппаФинансовогоУчета
	|		 И Т.Поставщик 				= АналитикаПартий.Поставщик
	|		 И Т.Контрагент 			= АналитикаПартий.Контрагент
	|		 И Т.НалогообложениеНДС 	= АналитикаПартий.НалогообложениеНДС
	|		 И Т.СтавкаНДС 				= АналитикаПартий.СтавкаНДС
	|		 И Т.ВидЦенности 			= АналитикаПартий.ВидЦенности
	|		 И Т.КодСтроки 				= АналитикаПартий.КодСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТРеквизитыНовыхКлючейАналитикиУчетаПартий
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТАналитикиПартийСВидомЦенности", ИмяТаблицыРезультата);
	
	Запрос.Выполнить(); // формируем ВТАналитикиПартийСВидомЦенности
	
КонецПроцедуры

// Формирует описание полей ключа аналитики для подстановки в тексты запросов.
//
Функция СформироватьОписаниеКлючейАналитикУчетаНоменклатуры(ИмяРеквизитаАналитики = "АналитикаУчетаНоменклатуры",
			ИсключатьПоля = "", ИсключатьПоляДетализацииМестаХранения = Истина)
	
	ОписаниеКлючей = Новый Структура("ПоляИменованные, ПоляНеИменованные, ПереченьПолей");
	СтруктураИсключаемыхПолей = Новый Структура(
		РасчетСебестоимостиУниверсальныеАлгоритмы.СоединитьСтроки(
			ИсключатьПоля,
			?(ИсключатьПоляДетализацииМестаХранения, "ТипМестаХранения, СкладскаяТерритория, Подразделение, Партнер, Контрагент, Договор, Организация", "")));
	
	Для Каждого МетаРеквизиты Из Метаданные.Справочники.КлючиАналитикиУчетаНоменклатуры.Реквизиты Цикл
		
		Если СтруктураИсключаемыхПолей.Свойство(МетаРеквизиты.Имя) Тогда
			Продолжить;
		КонецЕсли;
		
		РасчетСебестоимостиУниверсальныеАлгоритмы.ДополнитьСтроку(
			ОписаниеКлючей.ПоляИменованные,
			"%1" + ИмяРеквизитаАналитики + "." + МетаРеквизиты.Имя + " КАК " + ИмяРеквизитаАналитики + "_" + МетаРеквизиты.Имя,,,,
			"," + Символы.ПС + "	");
		
		РасчетСебестоимостиУниверсальныеАлгоритмы.ДополнитьСтроку(
			ОписаниеКлючей.ПоляНеИменованные,
			"%1" + ИмяРеквизитаАналитики + "." + МетаРеквизиты.Имя,,,,
			"," + Символы.ПС + "	");
		
		РасчетСебестоимостиУниверсальныеАлгоритмы.ДополнитьСтроку(
			ОписаниеКлючей.ПереченьПолей,
			ИмяРеквизитаАналитики + "_" + МетаРеквизиты.Имя);
		
	КонецЦикла;
	
	Возврат ОписаниеКлючей;
	
КонецФункции

// Подбирает или создает ключи аналитики учета номенклатуры без назначения и создает временную таблицу с именем <ИмяТаблицыПриемника>.
//
Функция СформироватьАналитикиНоменклатурыБезНазначения(ПараметрыРасчета,
			ИмяИсходнойТаблицы, ИмяТаблицыПриемника, ИмяПоляАналитики = "АналитикаУчетаНоменклатуры")
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.%ИмяПоляАналитики КАК АналитикаУчетаНоменклатуры
	|ПОМЕСТИТЬ ВТКлючиАналитикиНоменклатурыСНазначениями
	|ИЗ
	|	%ИмяИсходнойТаблицы КАК Т
	|ГДЕ
	|	Т.%ИмяПоляАналитики <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|	И Т.%ИмяПоляАналитики.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Т.АналитикаУчетаНоменклатуры
	|ИЗ
	|	ВТКлючиАналитикиНоменклатурыСНазначениями КАК Т
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИмяИсходнойТаблицы", ИмяИсходнойТаблицы);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИмяПоляАналитики",   ИмяПоляАналитики);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета, "ВТКлючиАналитикиНоменклатурыСНазначениями");
		Возврат Ложь; // корректировка аналитик не требуется
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Т.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	Т.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	Т.АналитикаУчетаНоменклатуры.Серия КАК Серия,
	|	Т.АналитикаУчетаНоменклатуры.МестоХранения КАК МестоХранения,
	|	Т.АналитикаУчетаНоменклатуры.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ЕСТЬNULL(Ключи.Ссылка, НЕОПРЕДЕЛЕНО) КАК АналитикаУчетаНоменклатурыБезНазначения
	|ПОМЕСТИТЬ ВТКлючиАналитикиНоменклатурыБезНазначения
	|ИЗ
	|	ВТКлючиАналитикиНоменклатурыСНазначениями КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
	|		ПО Т.АналитикаУчетаНоменклатуры.Номенклатура = Ключи.Номенклатура
	|			И Т.АналитикаУчетаНоменклатуры.Характеристика = Ключи.Характеристика
	|			И Т.АналитикаУчетаНоменклатуры.Серия = Ключи.Серия
	|			И Т.АналитикаУчетаНоменклатуры.МестоХранения = Ключи.МестоХранения
	|			И Т.АналитикаУчетаНоменклатуры.СтатьяКалькуляции = Ключи.СтатьяКалькуляции
	|			И (Ключи.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
	|			И (Ключи.ПометкаУдаления = ЛОЖЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Т.Номенклатура КАК Номенклатура,
	|	Т.Характеристика КАК Характеристика,
	|	Т.Серия КАК Серия,
	|	Т.МестоХранения КАК МестоХранения,
	|	Т.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение
	|ИЗ
	|	ВТКлючиАналитикиНоменклатурыБезНазначения КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатурыБезНазначения = НЕОПРЕДЕЛЕНО";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ТаблицаАналитик = Новый ТаблицаЗначений;
	ТаблицаАналитик.Колонки.Добавить("АналитикаУчетаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаНоменклатуры"));
	ТаблицаАналитик.Колонки.Добавить("АналитикаУчетаНоменклатурыБезНазначения", Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаНоменклатуры"));
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаАналитик.Добавить();
		НоваяСтрока.АналитикаУчетаНоменклатуры = Выборка.АналитикаУчетаНоменклатуры;
		
		Попытка
			НоваяСтрока.АналитикаУчетаНоменклатурыБезНазначения = РегистрыСведений.АналитикаУчетаНоменклатуры.СоздатьКлючАналитики(Выборка);
		Исключение
			
			НоваяСтрока.АналитикаУчетаНоменклатурыБезНазначения = НоваяСтрока.АналитикаУчетаНоменклатуры;
			
			ТекстДляПротокола = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не удалось создать ключ аналитики без назначения для аналитики учета номенклатуры ""%1"":
					|%2'", ОбщегоНазначения.КодОсновногоЯзыка()),
				НоваяСтрока.АналитикаУчетаНоменклатуры,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			РасчетСебестоимостиПротоколРасчета.ЗафиксироватьОшибкуРасчета(
				ПараметрыРасчета,
				Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаРаспределенияПартий,
				ТекстДляПротокола);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ТаблицаАналитик", ТаблицаАналитик);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Т.АналитикаУчетаНоменклатурыБезНазначения КАК АналитикаУчетаНоменклатурыБезНазначения
	|ПОМЕСТИТЬ ВТНовыеКлючиАналитикиНоменклатурыБезНазначения
	|ИЗ
	|	&ТаблицаАналитик КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА Т.АналитикаУчетаНоменклатурыБезНазначения = НЕОПРЕДЕЛЕНО
	|			ТОГДА НовыеКлючи.АналитикаУчетаНоменклатурыБезНазначения
	|		ИНАЧЕ Т.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ КАК АналитикаУчетаНоменклатурыБезНазначения
	|ПОМЕСТИТЬ ВТКлючиДляЗамены
	|ИЗ
	|	ВТКлючиАналитикиНоменклатурыБезНазначения КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНовыеКлючиАналитикиНоменклатурыБезНазначения КАК НовыеКлючи
	|		ПО Т.АналитикаУчетаНоменклатуры = НовыеКлючи.АналитикаУчетаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	%ПоляИсходнойТаблицы
	|ПОМЕСТИТЬ %ИмяТаблицыПриемника
	|ИЗ
	|	%ИмяИсходнойТаблицы КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКлючиДляЗамены КАК НовыеКлючи
	|		ПО Т.%ИмяПоляАналитики = НовыеКлючи.АналитикаУчетаНоменклатуры
	|";
	
	ПоляИсходнойТаблицы    = "";
	КолонкиИсходнойТаблицы = РасчетСебестоимостиПрикладныеАлгоритмы.ВыгрузитьВременнуюТаблицу(
		ПараметрыРасчета,
		ИмяИсходнойТаблицы,
		0).Колонки;
	
	Для Каждого ТекущаяКолонка Из КолонкиИсходнойТаблицы Цикл
		
		Если НРег(ТекущаяКолонка.Имя) = НРег(ИмяПоляАналитики) Тогда
			РасчетСебестоимостиУниверсальныеАлгоритмы.ДополнитьСтроку(
				ПоляИсходнойТаблицы,
				"ЕСТЬNULL(НовыеКлючи.АналитикаУчетаНоменклатурыБезНазначения, Т." + ИмяПоляАналитики + ") КАК " + ИмяПоляАналитики,,,,
				"," + Символы.ПС + "	");
		Иначе
			РасчетСебестоимостиУниверсальныеАлгоритмы.ДополнитьСтроку(
				ПоляИсходнойТаблицы,
				"Т." + ТекущаяКолонка.Имя,,,,
				"," + Символы.ПС + "	");
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИмяИсходнойТаблицы",  ИмяИсходнойТаблицы);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИмяТаблицыПриемника", ИмяТаблицыПриемника);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИмяПоляАналитики",    ИмяПоляАналитики);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляИсходнойТаблицы", ПоляИсходнойТаблицы);
	
	Запрос.Выполнить();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета,
		"ВТКлючиАналитикиНоменклатурыСНазначениями, ВТКлючиАналитикиНоменклатурыБезНазначения,
		|ВТНовыеКлючиАналитикиНоменклатурыБезНазначения, ВТКлючиДляЗамены");
	
	Возврат Истина;
	
КонецФункции

// Подбирает или создает ключи аналитики учета номенклатуры по значениям полей ключа и создает временную таблицу с именем <ИмяТаблицыПриемника>.
//
Функция СформироватьАналитикиНоменклатурыПоЗначениямПолей(ПараметрыРасчета,
			ИмяИсходнойТаблицы, ИмяТаблицыПриемника, ИмяПоляАналитики = "АналитикаУчетаНоменклатуры")
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.АналитикаУчетаНоменклатуры_Номенклатура КАК Номенклатура,
	|	Т.АналитикаУчетаНоменклатуры_Характеристика КАК Характеристика,
	|	Т.АналитикаУчетаНоменклатуры_Серия КАК Серия,
	|	Т.АналитикаУчетаНоменклатуры_МестоХранения КАК МестоХранения,
	|	Т.АналитикаУчетаНоменклатуры_Назначение КАК Назначение,
	|	Т.АналитикаУчетаНоменклатуры_СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ЕСТЬNULL(Ключи.Ссылка, НЕОПРЕДЕЛЕНО) КАК АналитикаУчетаНоменклатуры
	|ПОМЕСТИТЬ ВТПоляКлючейАналитикиНоменклатуры
	|ИЗ
	|	%ИмяИсходнойТаблицы КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
	|		ПО Т.АналитикаУчетаНоменклатуры_Номенклатура = Ключи.Номенклатура
	|			И Т.АналитикаУчетаНоменклатуры_Характеристика = Ключи.Характеристика
	|			И Т.АналитикаУчетаНоменклатуры_Серия = Ключи.Серия
	|			И Т.АналитикаУчетаНоменклатуры_МестоХранения = Ключи.МестоХранения
	|			И Т.АналитикаУчетаНоменклатуры_Назначение = Ключи.Назначение
	|			И Т.АналитикаУчетаНоменклатуры_СтатьяКалькуляции = Ключи.СтатьяКалькуляции
	|			И (Ключи.ПометкаУдаления = ЛОЖЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	Т.Серия,
	|	Т.МестоХранения,
	|	Т.Назначение,
	|	Т.СтатьяКалькуляции
	|ИЗ
	|	ВТПоляКлючейАналитикиНоменклатуры КАК Т
	|ГДЕ
	|	Т.АналитикаУчетаНоменклатуры = НЕОПРЕДЕЛЕНО";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИмяИсходнойТаблицы", ИмяИсходнойТаблицы);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	МетаРеквизиты = Метаданные.Справочники.КлючиАналитикиУчетаНоменклатуры.Реквизиты;
	
	ТаблицаАналитик = Новый ТаблицаЗначений;
	ТаблицаАналитик.Колонки.Добавить("Номенклатура", 	  МетаРеквизиты.Номенклатура.Тип);
	ТаблицаАналитик.Колонки.Добавить("Характеристика", 	  МетаРеквизиты.Характеристика.Тип);
	ТаблицаАналитик.Колонки.Добавить("Серия", 			  МетаРеквизиты.Серия.Тип);
	ТаблицаАналитик.Колонки.Добавить("МестоХранения", 	  МетаРеквизиты.МестоХранения.Тип);
	ТаблицаАналитик.Колонки.Добавить("Назначение", 		  МетаРеквизиты.Назначение.Тип);
	ТаблицаАналитик.Колонки.Добавить("СтатьяКалькуляции", МетаРеквизиты.СтатьяКалькуляции.Тип);
	ТаблицаАналитик.Колонки.Добавить("АналитикаУчетаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаНоменклатуры"));
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаАналитик.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		Попытка
			НоваяСтрока.АналитикаУчетаНоменклатуры = РегистрыСведений.АналитикаУчетаНоменклатуры.СоздатьКлючАналитики(Выборка);
		Исключение
			
			ТекстДляПротокола = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не удалось создать ключ аналитики для номенклатуры ""%1"":
					|%2'", ОбщегоНазначения.КодОсновногоЯзыка()),
				Выборка.Номенклатура,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			РасчетСебестоимостиПротоколРасчета.ЗафиксироватьОшибкуРасчета(
				ПараметрыРасчета,
				Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаРаспределенияПартий,
				ТекстДляПротокола);
			
			ВызватьИсключение РасчетСебестоимостиПрикладныеАлгоритмы.ТекстИсключениеДляРегистрацииПроблемы(ТекстДляПротокола);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ТаблицаАналитик", ТаблицаАналитик);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	Т.Серия,
	|	Т.МестоХранения,
	|	Т.Назначение,
	|	Т.СтатьяКалькуляции
	|ПОМЕСТИТЬ ВТНовыеКлючиАналитикиНоменклатуры
	|ИЗ
	|	&ТаблицаАналитик КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	%ПоляИсходнойТаблицы
	|ПОМЕСТИТЬ %ИмяТаблицыПриемника
	|ИЗ
	|	%ИмяИсходнойТаблицы КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНовыеКлючиАналитикиНоменклатуры КАК НовыеКлючи
	|		ПО Т.АналитикаУчетаНоменклатуры_Номенклатура = НовыеКлючи.Номенклатура
	|			И Т.АналитикаУчетаНоменклатуры_Характеристика = НовыеКлючи.Характеристика
	|			И Т.АналитикаУчетаНоменклатуры_Серия = НовыеКлючи.Серия
	|			И Т.АналитикаУчетаНоменклатуры_МестоХранения = НовыеКлючи.МестоХранения
	|			И Т.АналитикаУчетаНоменклатуры_Назначение = НовыеКлючи.Назначение
	|			И Т.АналитикаУчетаНоменклатуры_СтатьяКалькуляции = НовыеКлючи.СтатьяКалькуляции
	|";
	
	ПоляИсходнойТаблицы    = "";
	КолонкиИсходнойТаблицы = РасчетСебестоимостиПрикладныеАлгоритмы.ВыгрузитьВременнуюТаблицу(
		ПараметрыРасчета,
		ИмяИсходнойТаблицы,
		0).Колонки;
	
	РасчетСебестоимостиУниверсальныеАлгоритмы.ДополнитьСтроку(
		ПоляИсходнойТаблицы,
		"ЕСТЬNULL(НовыеКлючи.АналитикаУчетаНоменклатуры, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)) КАК " + ИмяПоляАналитики,,,,
		"," + Символы.ПС + "	");
	
	Для Каждого ТекущаяКолонка Из КолонкиИсходнойТаблицы Цикл
		
		РасчетСебестоимостиУниверсальныеАлгоритмы.ДополнитьСтроку(
				ПоляИсходнойТаблицы,
				"Т." + ТекущаяКолонка.Имя,,,,
				"," + Символы.ПС + "	");
		
	КонецЦикла;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИмяИсходнойТаблицы",  ИмяИсходнойТаблицы);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИмяТаблицыПриемника", ИмяТаблицыПриемника);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИмяПоляАналитики",    ИмяПоляАналитики);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляИсходнойТаблицы", ПоляИсходнойТаблицы);
	
	Запрос.Выполнить();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета,
		"ВТПоляКлючейАналитикиНоменклатуры, ВТНовыеКлючиАналитикиНоменклатуры");
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область ПроцедурыПроверкиПартионногоУчетаИСебестоимости

// Процедура-обработчик проверки состояния системы "КорректностьПереходаНаПУ22".
//
Процедура ПроверкаКорректностиПереходаНаПартионныйУчетВерсии22(ПараметрыПроверки) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Т.Организация,
	|	Т.Регистратор
	|ПОМЕСТИТЬ НекорректныйПереносДанных
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|ГДЕ
	|	&ПартионныйУчетВерсии22
	|	И Т.Организация В(&МассивОрганизаций)
	|	И Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Период <> ДОБАВИТЬКДАТЕ(&ДатаПереходаНаПартионныйУчетВерсии22, СЕКУНДА, -1)
	|	И Т.Активность
	|	И НЕ Т.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|	И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СлужебноеПереходНаПартионныйУчет22)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ДетализацияПартийТоваровДляНДСиУСН"" КАК ИмяРегистра,
	|	Т.Организация,
	|	Т.Регистратор
	|ИЗ
	|	РегистрНакопления.ДетализацияПартийТоваровДляНДСиУСН КАК Т
	|ГДЕ
	|	&ПартионныйУчетВерсии22
	|	И Т.Организация В(&МассивОрганизаций)
	|	И Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Период <> ДОБАВИТЬКДАТЕ(&ДатаПереходаНаПартионныйУчетВерсии22, СЕКУНДА, -1)
	|	И Т.Активность
	|	И НЕ Т.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|	И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СлужебноеПереходНаПартионныйУчет22)";
	
	СписокПолей = Новый СписокЗначений;
	СписокПолей.Добавить("Организация",		НСтр("ru='Организация'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("ИмяРегистра",		НСтр("ru='Регистр'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("Регистратор",		НСтр("ru='Документ'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	ПараметрыРегистрации = ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыРегистрацииПроблемПроверки(
		"НекорректныйПереносДанных",
		НСтр("ru='Обнаружены некорректные движения ""Перенос данных"" по организации ""%1"" в периоде %2'", ОбщегоНазначения.КодОсновногоЯзыка()),
		СписокПолей,
		"Регистратор");
	
	ЗакрытиеМесяцаСервер.ЗарегистрироватьПроблемыВыполненияПроверки(
		ПараметрыПроверки,
		ПараметрыРегистрации,
		ТекстЗапроса);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
//-- Локализация
