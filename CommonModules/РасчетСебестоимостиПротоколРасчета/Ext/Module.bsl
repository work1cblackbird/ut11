///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Процедуры протоколирования расчета, выполняемого механизмами
//	- партионного учета версии 2.2
//	- расчета себестоимости.
//
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

#Область ПротоколированиеРасчета


// Создает структуру описания протокола расчета.
// 
// Возвращаемое значение:
// 	Структура - Описание:
// * Этапы - см. РасчетСебестоимостиПротоколРасчета.ИнициализироватьТаблицуОписанияЭтапаРасчета
// * СформированныеДвижения - Структура -
// * ЗаписьДвижений - Структура -
// * КоличествоВызововЗаписиДвижений - Число -
// * ОшибкиЗаписи - Массив -
// * ОшибкиДвижений - Массив -
// * ОшибкиРасчета - Соответствие -
// * Длительность - Неопределено -
// * ВремяОкончания - Неопределено -
// * ВремяНачала - Число -
// * ДатаНачала - Дата -
// * Идентификатор - Строка -
// * ВыполнениеФрагментаКода - Массив -
//
Функция ИнициализироватьПротоколРасчета() Экспорт
	
	ПротоколРасчета = Новый Структура;
	
	// Общая информация о расчете.
	ПротоколРасчета.Вставить("Идентификатор",  СокрЛП(Новый УникальныйИдентификатор));;
	ПротоколРасчета.Вставить("ДатаНачала",     ТекущаяДатаСеанса());
	ПротоколРасчета.Вставить("ВремяНачала",    ТекущаяУниверсальнаяДатаВМиллисекундах());
	ПротоколРасчета.Вставить("ВремяОкончания", Неопределено);
	ПротоколРасчета.Вставить("Длительность",   Неопределено);
	ПротоколРасчета.Вставить("РасчетЗавершен", Ложь);
	
	// Информация об ошибках.
	ПротоколРасчета.Вставить("ОшибкиРасчета",  Новый Соответствие); // виды возникших при расчете ошибок
	ПротоколРасчета.Вставить("ОшибкиДвижений", Новый Массив); // тексты ошибок о некорректных регистраторах
	ПротоколРасчета.Вставить("ОшибкиЗаписи",   Новый Массив); // тексты ошибок, возникших при записи движений
	
	// Информация о записанных в ИБ движениях.
	ПротоколРасчета.Вставить("ЗаписьДвижений", Новый Структура);
	ПротоколРасчета.Вставить("КоличествоВызововЗаписиДвижений", 0);
	
	// Для подсчета общего количества сформированных в кэше движений (нарастающим итогом).
	ПротоколРасчета.Вставить("СформированныеДвижения", Новый Структура);
	
	ПротоколРасчета.Вставить("Этапы", ИнициализироватьТаблицуОписанияЭтапаРасчета());
	
	Возврат ПротоколРасчета;
	 
КонецФункции


// Создает таблицу описания этапа расчета.
// 
// Возвращаемое значение:
// 	ТаблицаЗначений - Описание:
// * ПредставлениеЭтапа - Строка - 
// * УровеньЭтапа - Число - 
// * ДатаНачала - Дата - 
// * ВремяНачала - Число - 
// * ВремяОкончания - Число -
// * СформированныеДвижения - Структура -
// * СформированныеСтрокиВТ - Структура - 
// * ВременныеТаблицы - Структура -
// * ЗамерыВыполненияКода - Структура - 
// * ИнформацияОбОшибках - Соответствие - 
// * КоличествоДанных - Число -
// * ДополнительнаяИнформация - Массив -
// 
Функция ИнициализироватьТаблицуОписанияЭтапаРасчета() Экспорт
	
	Этапы = Новый ТаблицаЗначений;
	Этапы.Колонки.Добавить("ПредставлениеЭтапа");
	Этапы.Колонки.Добавить("УровеньЭтапа");
	Этапы.Колонки.Добавить("ДатаНачала");
	Этапы.Колонки.Добавить("ВремяНачала");
	Этапы.Колонки.Добавить("ВремяОкончания");
	Этапы.Колонки.Добавить("СформированныеДвижения"); // по конкретному этапу, только по измененным регистрам
	Этапы.Колонки.Добавить("СформированныеСтрокиВТ"); // по конкретному этапу, только по измененным временным таблицам
	Этапы.Колонки.Добавить("ВременныеТаблицы"); // Структура(имя временной таблицы - Массив(Структура(количество записей, время формирования)))
	Этапы.Колонки.Добавить("ЗамерыВыполненияКода"); // Структура(идентификатор кода - Массив(Структура(пояснение, время выполнения)))
	Этапы.Колонки.Добавить("ИнформацияОбОшибках"); // массив строк, сгруппированных по типам ошибок
	Этапы.Колонки.Добавить("КоличествоДанных"); // количество данных для замера производительности
	Этапы.Колонки.Добавить("ДополнительнаяИнформация"); // массив произвольных строк
	
	Возврат Этапы;
	
КонецФункции

// Инициализирует вспомогательные данные для ведения протокола расчета.
// Можно не вызывать явно - вызывается при начале первого этапа.
//
Процедура НачалоРасчета(ПараметрыРасчета) Экспорт
	
	// Сделаем запись о начале этапа в ЖР.
	ОтметкаВЖурналеРегистрации(ПараметрыРасчета, "ИнициализироватьПараметрыРасчета");
	
	// Добавим протокол в общие параметры расчета.
	ПараметрыРасчета.Вставить("ПротоколРасчета", ИнициализироватьПротоколРасчета());
	
	// Добавим параметры замера производительности в общие параметры расчета.
	НачатьЗамерВремени(ПараметрыРасчета);
	
КонецПроцедуры

// Завершает ведение протокола и формирует текстовый протокол расчета.
// 
// Параметры:
//  ПараметрыРасчета - Структура - Параметры расчета
Процедура ОкончаниеРасчета(ПараметрыРасчета) Экспорт
	
	ПротоколРасчета = ПараметрыРасчета.ПротоколРасчета; // см. РасчетСебестоимостиПротоколРасчета.ИнициализироватьПротоколРасчета 
	
	// Если не было явного объявления конца предыдущего этапа расчета, то закроем его в связи с окончание расчета.
	ОкончаниеЭтапаРасчета(ПараметрыРасчета);
	
	// Определим наличие ошибок при расчете.
	БылиОшибкиДвижений = (ПротоколРасчета.ОшибкиДвижений.Количество() > 0); // проблемы с Регистратором
	БылиОшибкиЗаписи   = (ПротоколРасчета.ОшибкиЗаписи.Количество() > 0);   // проблемы записи движений
	
	ПротоколРасчета.РасчетЗавершен = Истина;
	
	СформироватьПротоколРасчета(ПараметрыРасчета);
	
#Область СохранениеОшибокРасчета
	
	// В регистр ошибок расчета записывается информация только о тех вариантах расчета,
	// которые влияют на определение состояния закрытия месяца в части расчета партий/себестоимости.
	
	ВариантРасчета = Неопределено;
	
	Если ПараметрыРасчета.ПартионныйУчетВерсии22 Тогда // партионный учет версии 2.2
		Если ПараметрыРасчета.ВариантРасчета = Перечисления.ВариантыРасчетаПартийИСебестоимости.ПартииИСебестоимость
		 ИЛИ ПараметрыРасчета.ВариантРасчета = Перечисления.ВариантыРасчетаПартийИСебестоимости.ФактическаяСебестоимость Тогда
			ВариантРасчета = ПараметрыРасчета.ВариантРасчета;
		КонецЕсли;
	Иначе // партионный учет версии 2.1 (или вообще не используется)
		Если ПараметрыРасчета.ВариантРасчета = Перечисления.ВариантыРасчетаПартийИСебестоимости.ФактическаяСебестоимость Тогда
			ВариантРасчета = ПараметрыРасчета.ВариантРасчета;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВариантРасчета) Тогда		
		
		// Создадим записи в регистр ошибок по каждой организации/ошибке.
		// При этом необходимо очистить все предыдущие ошибки по данному периоду и организации.
		
		Для Каждого ТекущаяОрганизация Из ПараметрыРасчета.МассивОрганизаций Цикл
			
			ЗаписиОшибок = РегистрыСведений.ОшибкиРасчетаПартийИСебестоимости.СоздатьНаборЗаписей();
			
			ЗаписиОшибок.Отбор.Период.Установить(ПараметрыРасчета.РасчетныйПериод.НачалоПериода);
			ЗаписиОшибок.Отбор.Организация.Установить(ТекущаяОрганизация);
			
			Для Каждого ТекущаяОшибка Из ПротоколРасчета.ОшибкиРасчета Цикл
				
				ЗаписьОшибки = ЗаписиОшибок.Добавить();
				
				ЗаписьОшибки.Период 	 			= ПараметрыРасчета.РасчетныйПериод.НачалоПериода;
				ЗаписьОшибки.Организация 			= ТекущаяОрганизация;
				ЗаписьОшибки.ВариантРасчета	 		= ВариантРасчета;
				ЗаписьОшибки.ТипОшибки				= ТекущаяОшибка.Ключ;
				ЗаписьОшибки.ИдентификаторПротокола	= ПротоколРасчета.Идентификатор;
				
			КонецЦикла;
			
			ЗаписиОшибок.Записать(Истина);
			
		КонецЦикла;
		
	КонецЕсли;
	
#КонецОбласти
	
	ЗакончитьЗамерВремени(ПараметрыРасчета);
	
	ПараметрыРасчета.Удалить("ПротоколРасчета");
	
	Если БылиОшибкиДвижений ИЛИ БылиОшибкиЗаписи Тогда
		
		ТекстОшибки =
			РасчетСебестоимостиПрикладныеАлгоритмы.СлужебныйСимвол_ПроблемаУжеЗарегистрирована()
		 	+ НСтр("ru='Расчет себестоимости завершился аварийно.
				|Подробнее см. в Журнале регистрации'", ОбщегоНазначения.КодОсновногоЯзыка());
		
		ВызватьИсключение ТекстОшибки; // Движения сформированы/записаны некорректно - расчет не выполнен
		
	КонецЕсли;
	
КонецПроцедуры

// Формирует протокол расчета.
//
Процедура СформироватьПротоколРасчета(ПараметрыРасчета)
	
	ПротоколРасчета = ПараметрыРасчета.ПротоколРасчета; // см. РасчетСебестоимостиПротоколРасчета.ИнициализироватьПротоколРасчета 
	
	// Если не было явного объявления конца предыдущего этапа расчета, то закроем его в связи с окончание расчета.
	ОкончаниеЭтапаРасчета(ПараметрыРасчета);
	
	ПротоколРасчета.ВремяОкончания = ТекущаяУниверсальнаяДатаВМиллисекундах();
	ПротоколРасчета.Длительность   = ПредставлениеВремениВыполнения(ПротоколРасчета);
	
	// Определим наличие ошибок при расчете.
	БылиОшибки 		   = (ПротоколРасчета.ОшибкиРасчета.Количество() > 0);
	БылиОшибкиДвижений = (ПротоколРасчета.ОшибкиДвижений.Количество() > 0); // проблемы с Регистратором
	БылиОшибкиЗаписи   = (ПротоколРасчета.ОшибкиЗаписи.Количество() > 0);   // проблемы записи движений
	
	// Сформируем текст протокола.
	Протокол = Новый ТекстовыйДокумент;
	
#Область ЗаголовокПротокола
	
	Если НЕ ПротоколРасчета.РасчетЗавершен ИЛИ БылиОшибки Тогда
		Протокол.ДобавитьСтроку(НСтр("ru = 'ВНИМАНИЕ !'", ОбщегоНазначения.КодОсновногоЯзыка()));
	КонецЕсли;
	
	Если НЕ ПротоколРасчета.РасчетЗавершен Тогда
		Протокол.ДобавитьСтроку(НСтр("ru = 'Это промежуточный протокол расчета - расчет еще не завершен!'", ОбщегоНазначения.КодОсновногоЯзыка()));
		Протокол.ДобавитьСтроку("");
	КонецЕсли;

	Если БылиОшибки Тогда
		
		Протокол.ДобавитьСтроку(ПредставлениеОшибокРасчета(
			ПротоколРасчета,
			НСтр("ru='В ходе расчета были диагностированы ошибки:'", ОбщегоНазначения.КодОсновногоЯзыка()),
			Символы.ПС + "	- "));
		Протокол.ДобавитьСтроку(НСтр("ru='Подробное описание ошибок можно найти (Ctrl-F) в тексте протокола по символу ""!"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
		Если БылиОшибкиДвижений ИЛИ БылиОшибкиЗаписи Тогда
			Протокол.ДобавитьСтроку(НСтр("ru='Расчет завершился аварийно.'", ОбщегоНазначения.КодОсновногоЯзыка()));
		КонецЕсли;
		
		Протокол.ДобавитьСтроку("");
		
	КонецЕсли;
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	
	Протокол.ДобавитьСтроку(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Протокол расчета (идентификатор %1)'", ОбщегоНазначения.КодОсновногоЯзыка()),
		ПротоколРасчета.Идентификатор));
	Протокол.ДобавитьСтроку(НСтр("ru='Платформа:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " " + СистемнаяИнформация.ВерсияПриложения);
	Протокол.ДобавитьСтроку(НСтр("ru='Конфигурация:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " " + ПредставлениеЗначения(Метаданные.Имя)
		+ ", " + НСтр("ru='версия:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " " + ПредставлениеЗначения(Метаданные.Версия));
	Протокол.ДобавитьСтроку(НСтр("ru='Информационная база:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " " 
		+ ПредставлениеЗначения(ПараметрыРасчета.СтрокаСоединенияИБ));
	Протокол.ДобавитьСтроку(НСтр("ru='Начало расчета:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " " + ПредставлениеЗначения(ПротоколРасчета.ДатаНачала)
		+ ", " + НСтр("ru='длительность:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " " + ПредставлениеЗначения(ПротоколРасчета.Длительность));
	Протокол.ДобавитьСтроку(НСтр("ru='Место вызова расчета:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " " 
		+ ПредставлениеЗначения(ПараметрыРасчета.МестоВызоваРасчета));
	
	Протокол.ДобавитьСтроку("");
	Протокол.ДобавитьСтроку(НСтр("ru='Вариант расчета:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " " 
		+ ПредставлениеЗначения(ПараметрыРасчета.ВариантРасчета));
	Протокол.ДобавитьСтроку(НСтр("ru='Режим закрытия месяца:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " " 
		+ ПредставлениеЗначения(ПараметрыРасчета.РежимЗакрытияМесяца));
	Протокол.ДобавитьСтроку(НСтр("ru='Период расчета:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " "
		+ ПредставлениеПериодаРасчета(ПараметрыРасчета));
	
	Если НЕ ПротоколРасчета.РасчетЗавершен И ПараметрыРасчета.Свойство("КопияМассивОрганизаций") Тогда
		ОрганизацииДляПротокола = ПараметрыРасчета.КопияМассивОрганизаций;
	ИначеЕсли ПараметрыРасчета.ВариантРасчета = Перечисления.ВариантыРасчетаПартийИСебестоимости.ПартииИСебестоимость Тогда
		ОрганизацииДляПротокола = ПараметрыРасчета.МассивОрганизаций;
	Иначе		
		ОрганизацииДляПротокола = ПараметрыРасчета.ОрганизацииСДвижениямиПоСебестоимости;
	КонецЕсли;
	
	Протокол.ДобавитьСтроку(НСтр("ru='Организации для расчета:'", ОбщегоНазначения.КодОсновногоЯзыка()));
	Для Каждого ТекущаяОрганизация Из ОрганизацииДляПротокола Цикл
		Протокол.ДобавитьСтроку("	- "	+ ПредставлениеЗначения(ТекущаяОрганизация)
			+ ", " + НСтр("ru='метод оценки стоимости:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " " 
			+ ?(ПараметрыРасчета.Свойство("УчетныеПолитики"), ПредставлениеЗначения(ПараметрыРасчета.УчетныеПолитики.МетодыОценкиСтоимости[ТекущаяОрганизация]), ""));
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ПараметрыРасчета.ИзмененоДокументов) Тогда
		Протокол.ДобавитьСтроку(НСтр("ru='Изменено документов в ИБ после прошлого расчета:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " "
			+ ПредставлениеЗначения(ПараметрыРасчета.ИзмененоДокументов));
	КонецЕсли;
	
	Протокол.ДобавитьСтроку("");
	ИменаРасширений = "";
	Попытка
		Для Каждого ТекущееРасширение Из РасширенияКонфигурации.Получить() Цикл
			
			Если ТекущееРасширение.Активно Тогда
				ИменаРасширений = ИменаРасширений + ?(ИменаРасширений = "", "", Символы.ПС) + "	- "
					+ ТекущееРасширение.Имя + " (" + ТекущееРасширение.УникальныйИдентификатор + ")";
			КонецЕсли;
			
		КонецЦикла;
	Исключение
		ИменаРасширений = "";
	КонецПопытки;
	
	ИменаРасширений = НСтр("ru='Активные расширения конфигурации:'")
		+ ?(ЗначениеЗаполнено(ИменаРасширений), Символы.ПС + ИменаРасширений, " " + НСтр("ru='отсутствуют'"));
	Протокол.ДобавитьСтроку(ИменаРасширений);
	
	Протокол.ДобавитьСтроку("");
	Протокол.ДобавитьСтроку(РазделительЧастейПротокола());
	Протокол.ДобавитьСтроку(НСтр("ru='Функциональные опции, влияющие на результат расчета:'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Для Каждого ТекущаяФО Из ПараметрыРасчета.ФО Цикл
		ОписаниеФО = РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеКонстантыИлиФункциональнойОпции(ТекущаяФО.Ключ);
		Протокол.ДобавитьСтроку("	- " + ОписаниеФО.Метаданные.Представление() + ": "	+ ПредставлениеЗначения(ТекущаяФО.Значение, Истина));
	КонецЦикла;
	
	// Выведем технологические параметры расчета.
	Протокол.ДобавитьСтроку("");
	Протокол.ДобавитьСтроку(РазделительЧастейПротокола());
	Протокол.ДобавитьСтроку("Технологические параметры расчета:");
	Протокол.ДобавитьСтроку("");
	
	// Выведем информацию об общих параметрах закрытия месяца.
	ОписаниеПараметров 		= ЗакрытиеМесяцаСервер.ОписаниеТехнологическихПараметровЗакрытияМесяца();
	ОписаниеГруппПараметров = ЗакрытиеМесяцаСервер.ОписаниеГруппПараметровОперацииЗакрытияМесяца();
	ЗначенияПараметров 		= ЗакрытиеМесяцаСервер.ОбщиеПараметрыЗакрытияМесяца();
	
	Для Каждого ТекущаяГруппа Из ОписаниеГруппПараметров Цикл
		
		Протокол.ДобавитьСтроку("	" + ТекущаяГруппа.Наименование + ":");
		
		ПараметрыГруппы = ОписаниеПараметров.НайтиСтроки(Новый Структура("Родитель", ТекущаяГруппа.Имя)); // см. РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеПараметровОперацииЗакрытияМесяца
		
		Для Каждого ТекущийПараметр Из ПараметрыГруппы Цикл
			
			ТекущееЗначениеПараметра = ЗначенияПараметров[ТекущийПараметр.Имя];
			
			Если ТекущееЗначениеПараметра <> ТекущийПараметр.ЗначениеПоУмолчанию Тогда
				ДополнениеКОписанию = "; (" + НСтр("ru='значение по умолчанию'", ОбщегоНазначения.КодОсновногоЯзыка()) + ": "
					+ ПредставлениеЗначения(ТекущийПараметр.ЗначениеПоУмолчанию) + ")";
			Иначе
				ДополнениеКОписанию = "";
			КонецЕсли;
			
			Протокол.ДобавитьСтроку("		- " + ТекущийПараметр.Наименование + ": "
				+ ПредставлениеЗначения(ТекущееЗначениеПараметра) + ДополнениеКОписанию);
			
		КонецЦикла;
		
	КонецЦикла;
	
	// Выведем информацию о параметрах распределения затрат и расчета себестоимости.
	ОписаниеПараметров 		= РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеПараметровОперацииЗакрытияМесяца();
	ОписаниеГруппПараметров = РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеГруппПараметровОперацииЗакрытияМесяца(); //ТаблицаЗначений
	ЗначенияПараметров 		= РасчетСебестоимостиПовтИсп.ЗначенияТехнологическихПараметров();
	
	Для Каждого ТекущаяГруппа Из ОписаниеГруппПараметров Цикл
		
		Протокол.ДобавитьСтроку("	" + ТекущаяГруппа.Наименование + ":");
		
		ПараметрыГруппы = ОписаниеПараметров.НайтиСтроки(Новый Структура("Родитель", ТекущаяГруппа.Имя)); // см. РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеПараметровОперацииЗакрытияМесяца
		
		Для Каждого ТекущийПараметр Из ПараметрыГруппы Цикл
			
			ТекущееЗначениеПараметра = ЗначенияПараметров[ТекущийПараметр.Имя];
			
			Если ТекущееЗначениеПараметра <> ТекущийПараметр.ЗначениеПоУмолчанию Тогда
				ДополнениеКОписанию = "; (" + НСтр("ru='значение по умолчанию'", ОбщегоНазначения.КодОсновногоЯзыка()) + ": "
					+ ПредставлениеЗначения(ТекущийПараметр.ЗначениеПоУмолчанию) + ")";
			Иначе
				ДополнениеКОписанию = "";
			КонецЕсли;
			
			Протокол.ДобавитьСтроку("		- " + ТекущийПараметр.Наименование + ": "
				+ ПредставлениеЗначения(ТекущееЗначениеПараметра) + ДополнениеКОписанию);
			
		КонецЦикла;
		
	КонецЦикла;
	
	// Выведем информацию об обслуживаемых регистрах.
	Если ПараметрыРасчета.Свойство("Движения") Тогда
		
		Протокол.ДобавитьСтроку("");
		Протокол.ДобавитьСтроку(РазделительЧастейПротокола());
		Протокол.ДобавитьСтроку(НСтр("ru='Обслуживаемые регистры:'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
		Для Каждого ОписаниеРегистра Из ПараметрыРасчета.Движения Цикл
			Протокол.ДобавитьСтроку("	- " + ОписаниеРегистра.Значение.ПолноеИмяРегистра
				+ ?(ПараметрыРасчета.РегистрыСРасчетнымиОборотами.Свойство(ОписаниеРегистра.Ключ),
					" (" + НСтр("ru='кэш оборотов'", ОбщегоНазначения.КодОсновногоЯзыка()) + ")", "")
				+ ?(ПараметрыРасчета.РегистрыСРасчетнымиОстатками.Свойство(ОписаниеРегистра.Ключ),
					" (" + НСтр("ru='кэш остатков'", ОбщегоНазначения.КодОсновногоЯзыка()) + ")", ""));
		КонецЦикла;
		
	КонецЕсли;
	
#КонецОбласти

#Область ЭтапыРасчета
	
	Протокол.ДобавитьСтроку("");
	Протокол.ДобавитьСтроку(РазделительЧастейПротокола());
	Протокол.ДобавитьСтроку(НСтр("ru='Протокол выполнения этапов расчета:'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Счетчик = 0;
	
	Для Каждого ОписаниеЭтапа Из ПротоколРасчета.Этапы Цикл
		
		Счетчик = Счетчик + 1;
		
		Протокол.ДобавитьСтроку("");
		Протокол.ДобавитьСтроку(СокрЛП(Счетчик) + ". " + ОписаниеЭтапа.ПредставлениеЭтапа); // УровеньЭтапа пока не поддерживаем
		Протокол.ДобавитьСтроку(НСтр("ru='Начало этапа:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " " + ПредставлениеЗначения(ОписаниеЭтапа.ДатаНачала)
			+ ", " + НСтр("ru='длительность:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " " + ПредставлениеВремениВыполнения(ОписаниеЭтапа, ПротоколРасчета));
		Протокол.ДобавитьСтроку(НСтр("ru='Время от начала расчета до начала этапа:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " " + ПредставлениеВремени(ОписаниеЭтапа.ДатаНачала - ПротоколРасчета.ДатаНачала));
		
		Если ЗначениеЗаполнено(ОписаниеЭтапа.СформированныеДвижения) Тогда
			Протокол.ДобавитьСтроку(НСтр("ru='Сформированы движения по регистрам (всего / уникальных):'", ОбщегоНазначения.КодОсновногоЯзыка()));
			Для Каждого ОписаниеДвижений Из ОписаниеЭтапа.СформированныеДвижения Цикл
				Протокол.ДобавитьСтроку("	- " + ОписаниеДвижений.Ключ + ": "
					+ ПредставлениеЗначения(ОписаниеДвижений.Значение.Всего) + " / "
					+ ПредставлениеЗначения(ОписаниеДвижений.Значение.Уникальных));
			КонецЦикла;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОписаниеЭтапа.СформированныеСтрокиВТ) Тогда
			Протокол.ДобавитьСтроку(НСтр("ru='Сформированы строки вспомогательных временных таблиц (всего / уникальных):'", ОбщегоНазначения.КодОсновногоЯзыка()));
			Для Каждого ОписаниеДвижений Из ОписаниеЭтапа.СформированныеСтрокиВТ Цикл
				Протокол.ДобавитьСтроку("	- " + ОписаниеДвижений.Ключ + ": "
					+ ПредставлениеЗначения(ОписаниеДвижений.Значение.Всего) + " / "
					+ ПредставлениеЗначения(ОписаниеДвижений.Значение.Уникальных));
			КонецЦикла;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОписаниеЭтапа.ВременныеТаблицы) Тогда
			
			Протокол.ДобавитьСтроку(НСтр("ru='Сформированы временные таблицы (размер / время (% времени этапа) / уточнение / исходные таблицы):'", ОбщегоНазначения.КодОсновногоЯзыка()));
			ВремяФормированияВсехТаблиц = Новый Структура("ОбщееВремя", 0);
			
			Для Каждого ОписаниеТаблицы Из ОписаниеЭтапа.ВременныеТаблицы Цикл
				
				Протокол.ДобавитьСтроку("	- " + ОписаниеТаблицы.Ключ + ": ");
				
				Для Каждого ОписаниеДействия Из ОписаниеТаблицы.Значение Цикл
					
					Если ЗначениеЗаполнено(ОписаниеДействия.ВремяНачала) И ЗначениеЗаполнено(ОписаниеДействия.ВремяОкончания) Тогда
						ВремяФормированияВсехТаблиц.ОбщееВремя = ВремяФормированияВсехТаблиц.ОбщееВремя + (ОписаниеДействия.ВремяОкончания - ОписаниеДействия.ВремяНачала);
					КонецЕсли;
					
					Протокол.ДобавитьСтроку("		- "
						+ ПредставлениеЗначения(ОписаниеДействия.КоличествоСтрок)
						+ " / " + ПредставлениеВремениВыполнения(ОписаниеДействия, ОписаниеЭтапа)
						+ ?(НЕ ЗначениеЗаполнено(ОписаниеДействия.Пояснение), "",
							" / " + ПредставлениеЗначения(ОписаниеДействия.Пояснение))
						+ ?(НЕ ЗначениеЗаполнено(ОписаниеДействия.ИсходныеДанные), "",
							" / " + ПредставлениеЗначения(ОписаниеДействия.ИсходныеДанные)));
					
				КонецЦикла;
				
			КонецЦикла;
			
			Если ЗначениеЗаполнено(ВремяФормированияВсехТаблиц.ОбщееВремя) Тогда
				Протокол.ДобавитьСтроку(НСтр("ru='Общее время формирования таблиц:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " "
					+ ПредставлениеВремениВыполнения(ВремяФормированияВсехТаблиц, ОписаниеЭтапа));
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОписаниеЭтапа.ЗамерыВыполненияКода) Тогда
			
			Протокол.ДобавитьСтроку(НСтр("ru='Выполнены замеры фрагментов кода (время (% времени этапа) / уточнение):'", ОбщегоНазначения.КодОсновногоЯзыка()));
			ВремяВсехЗамеровКода = Новый Структура("ОбщееВремя", 0);
			
			Для Каждого ОписаниеЗамераКода Из ОписаниеЭтапа.ЗамерыВыполненияКода Цикл
				
				Протокол.ДобавитьСтроку("	- " + ОписаниеЗамераКода.Ключ + ": ");
				
				Для Каждого ОписаниеДействия Из ОписаниеЗамераКода.Значение Цикл
					
					ВремяВсехЗамеровКода.ОбщееВремя = ВремяВсехЗамеровКода.ОбщееВремя + (ОписаниеДействия.ВремяОкончания - ОписаниеДействия.ВремяНачала);
					
					Протокол.ДобавитьСтроку("		- "
						+ ПредставлениеВремениВыполнения(ОписаниеДействия, ОписаниеЭтапа)
						+ ?(НЕ ЗначениеЗаполнено(ОписаниеДействия.Пояснение), "",
							" / " + ПредставлениеЗначения(ОписаниеДействия.Пояснение)));
					
				КонецЦикла;
				
			КонецЦикла;
			
			Протокол.ДобавитьСтроку(НСтр("ru='Общее время замеров выполнения кода:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " "
				+ ПредставлениеВремениВыполнения(ВремяВсехЗамеровКода, ОписаниеЭтапа));
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОписаниеЭтапа.ДополнительнаяИнформация) Тогда
			
			Протокол.ДобавитьСтроку(" ");
			Протокол.ДобавитьСтроку(НСтр("ru='Дополнительная информация об этапе:'", ОбщегоНазначения.КодОсновногоЯзыка()));
			
			Для Каждого ОписаниеСведений Из ОписаниеЭтапа.ДополнительнаяИнформация Цикл
				Протокол.ДобавитьСтроку(ПредставлениеМногострочногоТекста(ОписаниеСведений, "	- "));
			КонецЦикла;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОписаниеЭтапа.ИнформацияОбОшибках) Тогда
			ВывестиИнформациюОбОшибках(
				ПараметрыРасчета,
				Протокол,
				ОписаниеЭтапа.ИнформацияОбОшибках,
				НСтр("ru='При выполнении этапа расчета обнаружены ошибки'", ОбщегоНазначения.КодОсновногоЯзыка()),
				ОписаниеЭтапа.ПредставлениеЭтапа);
		КонецЕсли;
		
	КонецЦикла;
	
#КонецОбласти
	
#Область ИтоговыеПоказатели
	
	// Длительность этапов
	Протокол.ДобавитьСтроку("");
	Протокол.ДобавитьСтроку(РазделительЧастейПротокола());
	Протокол.ДобавитьСтроку(НСтр("ru='Итоговая информация о длительности этапов (абсолютная (относительная)):'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	ТаблицаДлительностиЭтапов = Новый ТаблицаЗначений;
	ТаблицаДлительностиЭтапов.Колонки.Добавить("Этап");
	ТаблицаДлительностиЭтапов.Колонки.Добавить("Длительность");
	
	Счетчик = 0;
	
	Для Каждого ОписаниеЭтапа Из ПротоколРасчета.Этапы Цикл
		
		Счетчик = Счетчик + 1;
		
		Протокол.ДобавитьСтроку("	" + СокрЛП(Счетчик) + ". " + ОписаниеЭтапа.ПредставлениеЭтапа + ": "
			+ ПредставлениеВремениВыполнения(ОписаниеЭтапа, ПротоколРасчета));
		
		НоваяСтрока = ТаблицаДлительностиЭтапов.Добавить();
		НоваяСтрока.Этап = НСтр("ru='Этап'", ОбщегоНазначения.КодОсновногоЯзыка()) + " №" + СокрЛП(Счетчик) + ". " + ОписаниеЭтапа.ПредставлениеЭтапа;
		НоваяСтрока.Длительность = (ОписаниеЭтапа.ВремяОкончания - ОписаниеЭтапа.ВремяНачала + 1) / 1000;
		
	КонецЦикла;
	
	ТаблицаДлительностиЭтапов.Сортировать("Длительность Убыв");
	
	Протокол.ДобавитьСтроку("");
	Протокол.ДобавитьСтроку(НСтр("ru='ТОП-10 самых длительных этапов:'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Счетчик = 0;
	
	Пока Счетчик < 10 И Счетчик < ТаблицаДлительностиЭтапов.Количество() Цикл
		
		Счетчик = Счетчик + 1;
		
		Протокол.ДобавитьСтроку("	" + СокрЛП(Счетчик) + ". " + ТаблицаДлительностиЭтапов[Счетчик - 1].Этап + ": "
			+ ПредставлениеВремени(ТаблицаДлительностиЭтапов[Счетчик - 1].Длительность));
		
	КонецЦикла;
	
	// Сформированные движения
	Протокол.ДобавитьСтроку("");
	Протокол.ДобавитьСтроку(РазделительЧастейПротокола());
	Протокол.ДобавитьСтроку(НСтр("ru='Итоговая информация о сформированных движениях по регистрам (всего / уникальных):'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Если ЗначениеЗаполнено(ПротоколРасчета.СформированныеДвижения) Тогда
		Для Каждого ОписаниеДвижений Из ПротоколРасчета.СформированныеДвижения Цикл
			
			Протокол.ДобавитьСтроку("	- " + ОписаниеДвижений.Ключ + ": "
				+ ПредставлениеЗначения(ОписаниеДвижений.Значение.Всего) + " / "
				+ ПредставлениеЗначения(ОписаниеДвижений.Значение.Уникальных));
			
			Если ОписаниеДвижений.Значение.Ошибочных > 0 Тогда
				
				Протокол.ДобавитьСтроку("		- " + НСтр("ru='в т.ч. ошибочных:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " "
					+ ПредставлениеЗначения(ОписаниеДвижений.Значение.Ошибочных) + " !");
				
				// Выведем описание ошибок сформированных движений в разрезе регистраторов
				Для Каждого ОписаниеОшибки Из ОписаниеДвижений.Значение.ОшибкиРегистраторов Цикл
					
					Протокол.ДобавитьСтроку("		" + ПредставлениеЗначения(ОписаниеОшибки.Ключ) + ":");
					
					Для Каждого ТекущийРегистратор Из ОписаниеОшибки.Значение Цикл
						Протокол.ДобавитьСтроку("			- "	+ ПредставлениеЗначения(ТекущийРегистратор)
							+ " (" + ПолучитьНавигационнуюСсылку(ТекущийРегистратор) + ")");
					КонецЦикла;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
	Иначе
		Протокол.ДобавитьСтроку(НСтр("ru='Нет сформированных движений.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	КонецЕсли;
	
	// Выведем информацию об ошибках регистраторов
	Если БылиОшибкиДвижений Тогда
		ВывестиИнформациюОбОшибках(
			ПараметрыРасчета,
			Протокол,
			ПротоколРасчета.ОшибкиДвижений,
			НСтр("ru='Формирование движений выполнено с ошибками'", ОбщегоНазначения.КодОсновногоЯзыка()));
	КонецЕсли;
	
	// Записанные движения
	Протокол.ДобавитьСтроку("");
	Протокол.ДобавитьСтроку(РазделительЧастейПротокола());
	
	КоличествоДокументов 	= 0;
	КоличествоЗаписей		= 0;
	ВремяЗаписи				= 0;
	
	Если ПараметрыРасчета.ОбщееКоличествоФЗЗаписи > 0 Тогда
		
		Протокол.ДобавитьСтроку(НСтр("ru='Всего запущено фоновых заданий записи движений:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " "
			+ ПредставлениеЗначения(ПараметрыРасчета.ОбщееКоличествоФЗЗаписи));
		
		НомерЗадания = 0;
		
		Для Каждого ТекущаяСтрока Из ПараметрыРасчета.ТаблицаЗаданийЗаписи Цикл
			
			НомерЗадания = НомерЗадания + 1;
			
			Протокол.ДобавитьСтроку(
				СокрЛП(НомерЗадания) + ". "
				+ ТекущаяСтрока.ИмяРегистра + ": "
				+ ТекущаяСтрока.ТекстСтатистика);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Протокол.ДобавитьСтроку("");
	Протокол.ДобавитьСтроку(НСтр("ru='Итоговая информация о записанных движениях по регистрам (документов / записей / время записи):'", ОбщегоНазначения.КодОсновногоЯзыка())); // количество записей - с учетом "первичных"
	
	Если ЗначениеЗаполнено(ПротоколРасчета.СформированныеДвижения)
	 И ПротоколРасчета.ЗаписьДвижений.Свойство("СформированныеДвижения") Тогда
		
		Для Каждого ОписаниеДвижений Из ПротоколРасчета.ЗаписьДвижений.СформированныеДвижения Цикл
			
			Если ОписаниеДвижений.Значение.КоличествоЗаписей > 0 ИЛИ ОписаниеДвижений.Значение.КоличествоДокументов > 0 Тогда
				
				КоличествоДокументов = КоличествоДокументов + ОписаниеДвижений.Значение.КоличествоДокументов;
				КоличествоЗаписей    = КоличествоЗаписей + ОписаниеДвижений.Значение.КоличествоЗаписей;
				ВремяЗаписи			 = ВремяЗаписи + ОписаниеДвижений.Значение.ВремяЗаписи;
				
				Протокол.ДобавитьСтроку("	- " + ОписаниеДвижений.Ключ + ": "
					+ ПредставлениеЗначения(ОписаниеДвижений.Значение.КоличествоДокументов) + " / "
					+ ПредставлениеЗначения(ОписаниеДвижений.Значение.КоличествоЗаписей) + " / "
					+ ПредставлениеВремени(ОписаниеДвижений.Значение.ВремяЗаписи));
				
				КонецЕсли;
				
		КонецЦикла;
		
	КонецЕсли;
		
	Если НЕ БылиОшибкиЗаписи Тогда
		
		Если КоличествоЗаписей > 0 ИЛИ КоличествоДокументов > 0 Тогда
			Протокол.ДобавитьСтроку(НСтр("ru='Всего записано движений по регистрам:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " "
				+ ПредставлениеЗначения(КоличествоДокументов) + " / "
				+ ПредставлениеЗначения(КоличествоЗаписей) + " / "
				+ ПредставлениеВремени(ВремяЗаписи));
		ИначеЕсли КоличествоЗаписей = 0 И КоличествоДокументов = 0 Тогда
			Протокол.ДобавитьСтроку(НСтр("ru='Запись движений не выполнялась - нет измененных движений.'", ОбщегоНазначения.КодОсновногоЯзыка()));
		КонецЕсли;
		
	Иначе
		
		Протокол.ДобавитьСтроку("");
		
		ВывестиИнформациюОбОшибках(
			ПараметрыРасчета,
			Протокол,
			ПротоколРасчета.ОшибкиЗаписи,
			НСтр("ru='Запись движений выполнена с ошибками'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПротоколРасчета.ЗаписьДвижений) Тогда
		Протокол.ДобавитьСтроку("");
		Протокол.ДобавитьСтроку(НСтр("ru='Длительность операций записи:'", ОбщегоНазначения.КодОсновногоЯзыка()) + " "
			+ ПредставлениеВремениВыполнения(ПротоколРасчета.ЗаписьДвижений, ПротоколРасчета));
	КонецЕсли;
	
	Если ПараметрыРасчета.Свойство("ЛогЗаписиДвижений") И ЗначениеЗаполнено(ПараметрыРасчета.ЛогЗаписиДвижений) Тогда
		Протокол.ДобавитьСтроку("");
		Протокол.ДобавитьСтроку(НСтр("ru = 'Лог работы механизма записи движений:'", ОбщегоНазначения.КодОсновногоЯзыка()));
		Протокол.ДобавитьСтроку(СокрЛП(ПараметрыРасчета.ЛогЗаписиДвижений));
	КонецЕсли;
	
	Протокол.ДобавитьСтроку("");
	Протокол.ДобавитьСтроку(РазделительЧастейПротокола());
	
#КонецОбласти

#Область СохранениеПротоколаРасчета

	ТекстПротокола = Протокол.ПолучитьТекст();
	
	ТекстПротокола = СтрЗаменить(ТекстПротокола, СокрЛП(РасчетСебестоимостиПрикладныеАлгоритмы.СлужебныйСимвол_ТекстДляРегистрацииПроблемы()), "");
	ТекстПротокола = СтрЗаменить(ТекстПротокола, СокрЛП(РасчетСебестоимостиПрикладныеАлгоритмы.СлужебныйСимвол_ПроблемаУжеЗарегистрирована()), "");
	
	Протокол.УстановитьТекст(ТекстПротокола);
	
	// Запишем протокол в журнал регистрации
	Если ПротоколРасчета.РасчетЗавершен Тогда
		ЗаписьЖурналаРегистрации(
			РасчетСебестоимостиПрикладныеАлгоритмы.ИмяСобытияПротоколДляЖурналаРегистрации(ПараметрыРасчета),
			УровеньЖурналаРегистрации.Информация,
			,
			,
			ТекстПротокола);
	КонецЕсли;
	
	// Удалим предыдущий промежуточный протокол расчета.
	НаборЗаписей = РегистрыСведений.ПротоколыРасчетаПартийИСебестоимости.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Идентификатор.Установить(ПротоколРасчета.Идентификатор);
	НаборЗаписей.Записать(Истина);
	
	// Запишем протокол в регистр сведений
	ЗаписьПротокола = РегистрыСведений.ПротоколыРасчетаПартийИСебестоимости.СоздатьМенеджерЗаписи();
	
	ЗаписьПротокола.Период 					 = ТекущаяДатаСеанса();
	ЗаписьПротокола.Идентификатор 			 = ПротоколРасчета.Идентификатор;
	
	ЗаписьПротокола.Протокол 				 = Новый ХранилищеЗначения(Протокол, Новый СжатиеДанных(9));
	
	ЗаписьПротокола.ДлительностьРасчета 	 = ПротоколРасчета.Длительность;
	ЗаписьПротокола.ВариантРасчета 			 = ПараметрыРасчета.ВариантРасчета;
	ЗаписьПротокола.РежимЗакрытияМесяца		 = ПараметрыРасчета.РежимЗакрытияМесяца;
	ЗаписьПротокола.ПериодРасчета 			 = ПараметрыРасчета.РасчетныйПериод.НачалоПериода;
	ЗаписьПротокола.БылиОшибки 				 = НЕ ПротоколРасчета.РасчетЗавершен ИЛИ БылиОшибки;
	ЗаписьПротокола.ОписаниеОшибок 			 = ?(ПротоколРасчета.РасчетЗавершен, ПредставлениеОшибокРасчета(ПротоколРасчета), НСтр("ru = 'Расчет не завершен'", ОбщегоНазначения.КодОсновногоЯзыка()));
	ЗаписьПротокола.ПредставлениеОрганизаций =
		РасчетСебестоимостиПрикладныеАлгоритмы.ПредставлениеОрганизаций(ОрганизацииДляПротокола, ", ", Истина);
	ЗаписьПротокола.РасчетНеЗавершен		 = НЕ ПротоколРасчета.РасчетЗавершен;
	
	
	ЗаписьПротокола.ПредставлениеОрганизаций =
		РасчетСебестоимостиПрикладныеАлгоритмы.ПредставлениеОрганизаций(ПараметрыРасчета.МассивОрганизаций, ", ", Истина);
	
	ЗаписьПротокола.Записать(Истина);
	
#КонецОбласти

КонецПроцедуры

	
// Определяет, была ли выполнена инициализация протокола.
//
// Возвращаемое значение:
//	Булево - 
//
Функция ВедетсяПротоколРасчета(ПараметрыРасчета) Экспорт
	
	Возврат ТипЗнч(ПараметрыРасчета) = Тип("Структура") И ПараметрыРасчета.Свойство("ПротоколРасчета");
	
КонецФункции

// Возвращает идентификатор протокола в регистре сведений ПротоколыРасчетаПартийИСебестоимости.
//
// Возвращаемое значение:
//	УникальныйИдентификатор -
//
Функция ИдентификаторПротокола(ПараметрыРасчета) Экспорт
	ПротоколРасчета = ПараметрыРасчета.ПротоколРасчета; // см. РасчетСебестоимостиПротоколРасчета.ИнициализироватьПротоколРасчета 
	Возврат ПротоколРасчета.Идентификатор;
КонецФункции

#КонецОбласти

#Область ПротоколированиеЭтаповРасчета

// Протоколирует начало этапа расчета.
//
Процедура НачалоЭтапаРасчета(ПараметрыРасчета, ПредставлениеЭтапа, УровеньЭтапа = 1) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ПараметрыРасчета) Тогда
		Возврат; // параметры расчета еще не инициализированы
	КонецЕсли;
	
	ПараметрыРасчета.Вставить("ПредставлениеЭтапа", ПредставлениеЭтапа);
	
	Если НЕ ВедетсяПротоколРасчета(ПараметрыРасчета) Тогда
		НачалоРасчета(ПараметрыРасчета);
	Иначе
		РасчетСебестоимостиПрикладныеАлгоритмы.ЗапуститьСледующееЗаданиеЗаписиИзСтека(ПараметрыРасчета);
	КонецЕсли;
	
	ПротоколРасчета = ПараметрыРасчета.ПротоколРасчета; // см. РасчетСебестоимостиПротоколРасчета.ИнициализироватьПротоколРасчета 
	
	// Если не было явного объявления конца предыдущего этапа расчета, то закроем его в связи с началом нового этапа.
	ОкончаниеЭтапаРасчета(ПараметрыРасчета);
	
	Если ПараметрыРасчета.Свойство("Производительность") Тогда
		ПараметрыРасчета.Производительность.НаименованиеЭтапа = ПредставлениеЭтапа;
	КонецЕсли;
	
	ПолноеПредставлениеЭтапа =
		?(ПараметрыРасчета.ИдетРасчетПартий, НСтр("ru='Партионный учет'", ОбщегоНазначения.КодОсновногоЯзыка()), НСтр("ru='Расчет себестоимости'", ОбщегоНазначения.КодОсновногоЯзыка()))
		+ ": " + ПредставлениеЭтапа;
	
	Если УровеньЭтапа = 1 Тогда
		// Сделаем запись о начале этапа в ЖР
		ОтметкаВЖурналеРегистрации(ПараметрыРасчета, ПолноеПредставлениеЭтапа);
	КонецЕсли;
	
	ОписаниеЭтапа = ПротоколРасчета.Этапы.Добавить();
	
	ОписаниеЭтапа.ПредставлениеЭтапа 	 	= ПолноеПредставлениеЭтапа;
	ОписаниеЭтапа.УровеньЭтапа 			 	= УровеньЭтапа;
	ОписаниеЭтапа.ДатаНачала 	 		 	= ТекущаяДатаСеанса();
	ОписаниеЭтапа.ВремяНачала 	 		 	= ТекущаяУниверсальнаяДатаВМиллисекундах();
	ОписаниеЭтапа.СформированныеДвижения 	= Новый Структура;
	ОписаниеЭтапа.СформированныеСтрокиВТ 	= Новый Структура;
	ОписаниеЭтапа.ВременныеТаблицы 		 	= Новый Структура;
	ОписаниеЭтапа.ЗамерыВыполненияКода	 	= Новый Структура;
	ОписаниеЭтапа.ИнформацияОбОшибках 		= Новый Соответствие;
	ОписаниеЭтапа.КоличествоДанных			= 0;
	ОписаниеЭтапа.ДополнительнаяИнформация 	= Новый Массив;
	
КонецПроцедуры

// Протоколирует окончание этапа расчета.
// Можно не вызывать явно - вызывается при начале следующего этапа.
//
Процедура ОкончаниеЭтапаРасчета(ПараметрыРасчета, ОкончаниеРасчета = Ложь) Экспорт
	
	ПротоколРасчета = ПараметрыРасчета.ПротоколРасчета; // см. РасчетСебестоимостиПротоколРасчета.ИнициализироватьПротоколРасчета 
	ОписаниеЭтапа 	= ОписаниеТекущегоЭтапа(ПараметрыРасчета);
	
	Если НЕ ЗначениеЗаполнено(ОписаниеЭтапа) // еще не начали протоколирование расчета
	 ИЛИ ЗначениеЗаполнено(ОписаниеЭтапа.ВремяОкончания) Тогда // протокол этапа уже сформирован
		Возврат;
	КонецЕсли;
	
	ОписаниеЭтапа.ВремяОкончания = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	Для Каждого ОписаниеДвижения Из ОписаниеЭтапа.СформированныеДвижения Цикл
		
		Если НЕ ПротоколРасчета.СформированныеДвижения.Свойство(ОписаниеДвижения.Ключ) Тогда
			ОчисткаСформированныхДвижений(ПараметрыРасчета, ОписаниеДвижения.Ключ); // инициализирует счетчики движений
		КонецЕсли;
		
		// Подсчитаем количество уникальных движений, сформированных на текущем этапе
		ВсегоДвижений = РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, ПараметрыРасчета.Движения[ОписаниеДвижения.Ключ].ИмяТаблицыКэшаРегистра);
		ОписаниеЭтапа.СформированныеДвижения[ОписаниеДвижения.Ключ].Уникальных =
			ВсегоДвижений - ПротоколРасчета.СформированныеДвижения[ОписаниеДвижения.Ключ].Уникальных;
		
		// Пересчитаем общее количество сформированных движений
		ПротоколРасчета.СформированныеДвижения[ОписаниеДвижения.Ключ].Всего = 
			ПротоколРасчета.СформированныеДвижения[ОписаниеДвижения.Ключ].Всего + ОписаниеДвижения.Значение.Всего;
		ПротоколРасчета.СформированныеДвижения[ОписаниеДвижения.Ключ].Уникальных = ВсегоДвижений;
		
	КонецЦикла;
	
	Для Каждого ОписаниеДвижения Из ОписаниеЭтапа.СформированныеСтрокиВТ Цикл
		
		// Подсчитаем количество уникальных движений, сформированных на текущем этапе
		ВсегоДвижений = РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, ОписаниеДвижения.Ключ);
		ОписаниеЭтапа.СформированныеСтрокиВТ[ОписаниеДвижения.Ключ].Уникальных = ВсегоДвижений;
		
	КонецЦикла;
	
	// Зафиксируем данные замера производительности.
	ЗафиксироватьЗамерВремени(ПараметрыРасчета);
	
	Если НЕ ОкончаниеРасчета И РасчетСебестоимостиПовтИсп.ЗначенияТехнологическихПараметров().ФормироватьПромежуточныеПротоколыРасчета Тогда
		СформироватьПротоколРасчета(ПараметрыРасчета);
	КонецЕсли;
	
	Если ПараметрыРасчета.ПараметрыОтладки.Свойство("ИмяРегистраПоискаДвижений")
	 И ПараметрыРасчета.ПараметрыОтладки.Свойство("УсловиеПоискаДвижений") Тогда
		РасчетСебестоимостиПрикладныеАлгоритмы.ПроверитьУсловиеПоискаДвиженийПриРасчете(ПараметрыРасчета);
	КонецЕсли;
		
КонецПроцедуры

// Возвращает описание текущего этапа.
//
Функция ОписаниеТекущегоЭтапа(ПараметрыРасчета)
	
	ПротоколРасчета = ПараметрыРасчета.ПротоколРасчета; // см. РасчетСебестоимостиПротоколРасчета.ИнициализироватьПротоколРасчета 
	
	Если ПротоколРасчета.Этапы.Количество() = 0 Тогда
		ОписаниеТекущегоЭтапа = Неопределено;
	Иначе
		ОписаниеТекущегоЭтапа = ПротоколРасчета.Этапы.Получить(ПротоколРасчета.Этапы.Количество()-1);
	КонецЕсли;
	
	Возврат ОписаниеТекущегоЭтапа;
	
КонецФункции

// Возвращает текстовое описание текущего этапа.
//
// Возвращаемое значение:
//	Строка - 
//
Функция ПредставлениеТекущегоЭтапа(ПараметрыРасчета) Экспорт
	
	Описание = ОписаниеТекущегоЭтапа(ПараметрыРасчета);
	
	Если НЕ ЗначениеЗаполнено(Описание) Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат Описание.ПредставлениеЭтапа;
	
КонецФункции

#КонецОбласти

#Область ПротоколированиеВременныхТаблиц

// Протоколирует начало формирования временной таблицы (внутри этапа расчета).
//
Процедура НачалоФормированиеВременнойТаблицы(ПараметрыРасчета, ИмяВременнойТаблицы, Пояснение = "", ИменаИсходныхТаблиц = "", ВремяНачала = Неопределено) Экспорт
	
	Если НЕ ВедетсяПротоколРасчета(ПараметрыРасчета) Тогда
		Возврат;
	КонецЕсли;
	
	ПротоколРасчета = ПараметрыРасчета.ПротоколРасчета; // см. РасчетСебестоимостиПротоколРасчета.ИнициализироватьПротоколРасчета 
	
	ОписаниеЗамера = Новый Структура;
	ОписаниеЗамера.Вставить("ИмяВременнойТаблицы", ИмяВременнойТаблицы);
	ОписаниеЗамера.Вставить("ВремяНачала", 		   ?(ЗначениеЗаполнено(ВремяНачала), ВремяНачала, ТекущаяУниверсальнаяДатаВМиллисекундах()));
	ОписаниеЗамера.Вставить("Пояснение", 		   Пояснение);
	ОписаниеЗамера.Вставить("ИсходныеДанные", 	   "");
	
	Если ЗначениеЗаполнено(ИменаИсходныхТаблиц) Тогда
		
		ТекстИсходныеДанные = "";
		
		Для Каждого КлючИЗначение Из Новый Структура(ИменаИсходныхТаблиц) Цикл
			ТекстИсходныеДанные =
				ТекстИсходныеДанные + ?(ТекстИсходныеДанные = "", "", ", ")
				+ КлючИЗначение.Ключ + " ("
				+ ПредставлениеЗначения(
					РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, КлючИЗначение.Ключ)) + ")";
		КонецЦикла;
		
		ОписаниеЗамера.ИсходныеДанные = ТекстИсходныеДанные;
		
	КонецЕсли;
	
	Если НЕ ПротоколРасчета.Свойство("ФормированиеВременнойТаблицы") Тогда
		ПротоколРасчета.Вставить("ФормированиеВременнойТаблицы", Новый Массив);
	КонецЕсли;
	
	ПротоколРасчета.ФормированиеВременнойТаблицы.Добавить(ОписаниеЗамера);
	
КонецПроцедуры

// Протоколирует окончание формирования временной таблицы (внутри этапа расчета).
//
Процедура ОкончаниеФормированиеВременнойТаблицы(ПараметрыРасчета, УвеличиватьСчетчикЗамераПроизводительности = Ложь, КоличествоДанных = Неопределено, ВремяОкончания = Неопределено) Экспорт
	
	Если НЕ ВедетсяПротоколРасчета(ПараметрыРасчета) Тогда
		Возврат;
	КонецЕсли;
	
	ПротоколРасчета 	= ПараметрыРасчета.ПротоколРасчета; // см. РасчетСебестоимостиПротоколРасчета.ИнициализироватьПротоколРасчета 
	ОписаниеЭтапа 		= ОписаниеТекущегоЭтапа(ПараметрыРасчета);
	ОписаниеЗамера		= ПротоколРасчета.ФормированиеВременнойТаблицы[ПротоколРасчета.ФормированиеВременнойТаблицы.Количество() - 1];
	
	Если НЕ ОписаниеЭтапа.ВременныеТаблицы.Свойство(ОписаниеЗамера.ИмяВременнойТаблицы) Тогда
		ТекущаяВременнаяТаблица = Новый Массив;
	Иначе
		ТекущаяВременнаяТаблица = ОписаниеЭтапа.ВременныеТаблицы[ОписаниеЗамера.ИмяВременнойТаблицы]; // Массив
	КонецЕсли;
	
	Если КоличествоДанных <> Неопределено Тогда
		КоличествоСтрок = КоличествоДанных;
	ИначеЕсли РасчетСебестоимостиПрикладныеАлгоритмы.ВременнаяТаблицаСуществует(ПараметрыРасчета, ОписаниеЗамера.ИмяВременнойТаблицы) Тогда
		КоличествоСтрок = РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, ОписаниеЗамера.ИмяВременнойТаблицы);
		КоличествоДанных = КоличествоСтрок;
	Иначе
		КоличествоСтрок = 0;
	КонецЕсли;
	
	ОписаниеВременнойТаблицы = Новый Структура;
	ОписаниеВременнойТаблицы.Вставить("КоличествоСтрок", КоличествоСтрок);
	ОписаниеВременнойТаблицы.Вставить("ВремяНачала", 	 ОписаниеЗамера.ВремяНачала);
	ОписаниеВременнойТаблицы.Вставить("ВремяОкончания",  ?(ЗначениеЗаполнено(ВремяОкончания), ВремяОкончания, ТекущаяУниверсальнаяДатаВМиллисекундах()));
	ОписаниеВременнойТаблицы.Вставить("Пояснение", 		 ОписаниеЗамера.Пояснение);
	ОписаниеВременнойТаблицы.Вставить("ИсходныеДанные",  ОписаниеЗамера.ИсходныеДанные);
	
	ТекущаяВременнаяТаблица.Добавить(ОписаниеВременнойТаблицы);
	ОписаниеЭтапа.ВременныеТаблицы.Вставить(ОписаниеЗамера.ИмяВременнойТаблицы, ТекущаяВременнаяТаблица);
	
	ПротоколРасчета.ФормированиеВременнойТаблицы.Удалить(ПротоколРасчета.ФормированиеВременнойТаблицы.Количество() - 1);
	
	Если УвеличиватьСчетчикЗамераПроизводительности Тогда
		УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыРасчета, КоличествоСтрок);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПротоколированиеФрагментовКода


// Инициализирует структуру описания замера фрагмента кода.
// 
// Возвращаемое значение:
// 	Структура - Описание:
// * Пояснение - Строка -
// * Идентификатор - Строка -
// * ВремяНачала - Число -
//
Функция ИнициализироватьОписаниеВыполненияФрагментаКода() Экспорт
	
	ОписаниеЗамера = Новый Структура;
	ОписаниеЗамера.Вставить("ВремяНачала", 		0);
	ОписаниеЗамера.Вставить("Идентификатор",    "");
	ОписаниеЗамера.Вставить("Пояснение", 		"");
	
	Возврат ОписаниеЗамера;
	
КонецФункции
	
// Протоколирует начало выполнения фрагмента кода (внутри этапа расчета).
// Вложенность замеров фрагментов кода не допускается.
//
Процедура НачалоВыполненияФрагментаКода(ПараметрыРасчета, Идентификатор, Пояснение) Экспорт
	
	Если НЕ ВедетсяПротоколРасчета(ПараметрыРасчета) Тогда
		Возврат;
	КонецЕсли;
	
	ПротоколРасчета = ПараметрыРасчета.ПротоколРасчета; // см. РасчетСебестоимостиПротоколРасчета.ИнициализироватьПротоколРасчета 
	
	ОписаниеЗамера = ИнициализироватьОписаниеВыполненияФрагментаКода();
	ОписаниеЗамера.ВремяНачала = ТекущаяУниверсальнаяДатаВМиллисекундах();
	ОписаниеЗамера.Идентификатор = Идентификатор;
	ОписаниеЗамера.Пояснение = Пояснение;
	
	Если НЕ ПротоколРасчета.Свойство("ВыполнениеФрагментаКода") Тогда
		ПротоколРасчета.Вставить("ВыполнениеФрагментаКода", Новый Массив);
	КонецЕсли;
	
	ПротоколРасчета.ВыполнениеФрагментаКода.Добавить(ОписаниеЗамера);
	
КонецПроцедуры

// Протоколирует окончание выполнения фрагмента кода (внутри этапа расчета).
//
Процедура ОкончаниеВыполненияФрагментаКода(ПараметрыРасчета, КоличествоДанныхДляЗамераПроизводительности = 0) Экспорт
	
	Если НЕ ВедетсяПротоколРасчета(ПараметрыРасчета) Тогда
		Возврат;
	КонецЕсли;
	
	ПротоколРасчета 	= ПараметрыРасчета.ПротоколРасчета; // см. РасчетСебестоимостиПротоколРасчета.ИнициализироватьПротоколРасчета 
	ОписаниеЭтапа 		= ОписаниеТекущегоЭтапа(ПараметрыРасчета);
	ОписаниеЗамера 		= РасчетСебестоимостиПротоколРасчета.ИнициализироватьОписаниеВыполненияФрагментаКода();
	ЗаполнитьЗначенияСвойств(ОписаниеЗамера, ПротоколРасчета.ВыполнениеФрагментаКода.Получить(ПротоколРасчета.ВыполнениеФрагментаКода.Количество() - 1));
	ЗамерыВыполненияКода = ОписаниеЭтапа.ЗамерыВыполненияКода;
	
	Если НЕ ЗамерыВыполненияКода.Свойство(ОписаниеЗамера.Идентификатор) Тогда
		ТекущийЗамер = Новый Массив; 
	Иначе
		ТекущийЗамер = ЗамерыВыполненияКода[ОписаниеЗамера.Идентификатор]; // Массив 
	КонецЕсли;
	
	ОписаниеЗамера.Вставить("КоличествоДанных", КоличествоДанныхДляЗамераПроизводительности);
	ОписаниеЗамера.Вставить("ВремяОкончания",   ТекущаяУниверсальнаяДатаВМиллисекундах());
	
	ТекущийЗамер.Добавить(ОписаниеЗамера);
	ЗамерыВыполненияКода.Вставить(ОписаниеЗамера.Идентификатор, ТекущийЗамер);
	
	ПротоколРасчета.ВыполнениеФрагментаКода.Удалить(ПротоколРасчета.ВыполнениеФрагментаКода.Количество() - 1);
	
	Если КоличествоДанныхДляЗамераПроизводительности > 0 Тогда
		УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыРасчета, КоличествоДанныхДляЗамераПроизводительности);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПротоколированиеДвижений

// Протоколирует начало записи сформированных движений.
//
Процедура НачалоЗаписиДвижений(ПараметрыРасчета, ЗаписатьРегистры) Экспорт
	
	ПротоколРасчета = ПараметрыРасчета.ПротоколРасчета; // см. РасчетСебестоимостиПротоколРасчета.ИнициализироватьПротоколРасчета 
	ЗаписьДвижений	= ПротоколРасчета.ЗаписьДвижений;
	
	// Если не было явного объявления конца предыдущего этапа расчета, то закроем его в связи с началом нового этапа.
	ОкончаниеЭтапаРасчета(ПараметрыРасчета);
	
	Если ПараметрыРасчета.Отладка.ВестиЛогРаботыМеханизмаЗаписиДвижений
	 И НЕ ПараметрыРасчета.Свойство("ЛогЗаписиДвижений") Тогда
		ПараметрыРасчета.Вставить("ЛогЗаписиДвижений", "");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗаписатьРегистры) Тогда
		ПротоколРасчета.КоличествоВызововЗаписиДвижений = ПротоколРасчета.КоличествоВызововЗаписиДвижений + 1;
		СуффиксИмениЭтапа = "ГотовыеДвижения" + Формат(ПротоколРасчета.КоличествоВызововЗаписиДвижений, "ЧГ=");
	Иначе
		Если ПротоколРасчета.КоличествоВызововЗаписиДвижений = 0 Тогда
			СуффиксИмениЭтапа = ""; // запись вызывается всего 1 раз за весь расчет
		Иначе
			СуффиксИмениЭтапа = "ОставшиесяДвижения"; // последний вызов записи движений
		КонецЕсли;
	КонецЕсли;
		
	НачалоЭтапаРасчета(ПараметрыРасчета, "ЗаписатьСформированныеДвижения" + ?(СуффиксИмениЭтапа = "", "", "_") + СуффиксИмениЭтапа);
	
	ЗаписьДвижений.Вставить("ВремяНачала", ТекущаяУниверсальнаяДатаВМиллисекундах());
	
	Если НЕ ЗаписьДвижений.Свойство("СформированныеДвижения") Тогда
		ЗаписьДвижений.Вставить("СформированныеДвижения", Новый Структура);
	КонецЕсли;
	
КонецПроцедуры

// Протоколирует окончание записи сформированных движений.
//
Процедура ОкончаниеЗаписиДвижений(ПараметрыРасчета) Экспорт
	
	ЗаписьДвижений = ПараметрыРасчета.ПротоколРасчета.ЗаписьДвижений;
	
	ЗаписьДвижений.Вставить("ВремяОкончания", ТекущаяУниверсальнаяДатаВМиллисекундах());
	
	ОбщееВремя = ?(ЗаписьДвижений.Свойство("ОбщееВремя"), ЗаписьДвижений.ОбщееВремя, 0);
	ЗаписьДвижений.Вставить("ОбщееВремя", ОбщееВремя + (ЗаписьДвижений.ВремяОкончания - ЗаписьДвижений.ВремяНачала));
	
КонецПроцедуры

// Добавляет записи в лог работы механизма записи движений.
//
Процедура ДобавитьИнформациюОЗаписиДвижений(ПараметрыРасчета, ТекстДляПротокола) Экспорт
	
	Если ПараметрыРасчета.Свойство("ЛогЗаписиДвижений") Тогда
		ПараметрыРасчета.ЛогЗаписиДвижений = ПараметрыРасчета.ЛогЗаписиДвижений + Символы.ПС
			+ Формат(ТекущаяДатаСеанса(), "ДЛФ=DT") + " " + ТекстДляПротокола;
	КонецЕсли;
	
КонецПроцедуры

// Протоколирует количественные показатели кэширования сформированных движений.
//
Процедура КэшированиеСформированныхДвижений(ПараметрыРасчета, ИмяРегистра, КоличествоСтрокТаблицыЗначений) Экспорт
	
	ОписаниеЭтапа = ОписаниеТекущегоЭтапа(ПараметрыРасчета);
	
	Если НЕ ОписаниеЭтапа.СформированныеДвижения.Свойство(ИмяРегистра) Тогда
		ОписаниеЭтапа.СформированныеДвижения.Вставить(
			ИмяРегистра,
			Новый Структура("Всего, Уникальных", КоличествоСтрокТаблицыЗначений, 0)); //в таблице значений и во временной таблице (свернутые записи)
	Иначе
		ОписаниеЭтапа.СформированныеДвижения[ИмяРегистра].Всего =
			ОписаниеЭтапа.СформированныеДвижения[ИмяРегистра].Всего + КоличествоСтрокТаблицыЗначений;
	КонецЕсли;
	
КонецПроцедуры

// Протоколирует количественные показатели кэширования сформированных записей вспомогательных временных таблиц.
//
Процедура КэшированиеСформированныхЗаписейВТ(ПараметрыРасчета, ИмяТаблицы, КоличествоСтрокТаблицыЗначений) Экспорт
	
	ОписаниеЭтапа = ОписаниеТекущегоЭтапа(ПараметрыРасчета);
	
	Если НЕ ОписаниеЭтапа.СформированныеСтрокиВТ.Свойство(ИмяТаблицы) Тогда
		ОписаниеЭтапа.СформированныеСтрокиВТ.Вставить(
			ИмяТаблицы,
			Новый Структура("Всего, Уникальных", КоличествоСтрокТаблицыЗначений, 0)); //в таблице значений и во временной таблице (свернутые записи)
	Иначе
		ОписаниеЭтапа.СформированныеСтрокиВТ[ИмяТаблицы].Всего =
			ОписаниеЭтапа.СформированныеСтрокиВТ[ИмяТаблицы].Всего + КоличествоСтрокТаблицыЗначений;
	КонецЕсли;
	
КонецПроцедуры

// Протоколирует количественные показатели записи сформированных движений.
//
Процедура ЗаписьСформированныхДвижений(ПараметрыРасчета, ИмяРегистра, КоличествоДокументов = 0, КоличествоЗаписей = 0, ВремяЗаписи = 0) Экспорт
	
	Если КоличествоДокументов = 0 И КоличествоЗаписей = 0 И ВремяЗаписи = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьДвижений	= ПараметрыРасчета.ПротоколРасчета.ЗаписьДвижений;
	
	Если НЕ ЗаписьДвижений.СформированныеДвижения.Свойство(ИмяРегистра) Тогда
		ЗаписьДвижений.СформированныеДвижения.Вставить(
			ИмяРегистра,
			Новый Структура("КоличествоДокументов, КоличествоЗаписей, ВремяЗаписи", КоличествоДокументов, КоличествоЗаписей, ВремяЗаписи));
	Иначе
		ЗаписьДвижений.СформированныеДвижения[ИмяРегистра].КоличествоДокументов =
			ЗаписьДвижений.СформированныеДвижения[ИмяРегистра].КоличествоДокументов + КоличествоДокументов;
		ЗаписьДвижений.СформированныеДвижения[ИмяРегистра].КоличествоЗаписей =
			ЗаписьДвижений.СформированныеДвижения[ИмяРегистра].КоличествоЗаписей + КоличествоЗаписей;
		ЗаписьДвижений.СформированныеДвижения[ИмяРегистра].ВремяЗаписи =
			ЗаписьДвижений.СформированныеДвижения[ИмяРегистра].ВремяЗаписи + ВремяЗаписи;
	КонецЕсли;
	
КонецПроцедуры

// Фиксирует в протоколе расчета наличие ошибок в сформированных движениях.
//
Процедура ЗафиксироватьОшибкиВСформированныхДвижениях(ПараметрыРасчета, ОписаниеРегистра, ИмяТаблицы, ОписаниеОшибки) Экспорт
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(ПараметрыРасчета, ИмяТаблицы) = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РезультатЗапроса = РасчетСебестоимостиПрикладныеАлгоритмы.ВыбратьМоментыВремениРегистраторов(ПараметрыРасчета, ИмяТаблицы);
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат; // нет некорректных движений
	КонецЕсли;
	
	ПротоколРасчета = ПараметрыРасчета.ПротоколРасчета; // см. РасчетСебестоимостиПротоколРасчета.ИнициализироватьПротоколРасчета 
	Если Не ПротоколРасчета.СформированныеДвижения.Свойство(ОписаниеРегистра.ИмяРегистра) Тогда
		Возврат; // отсутствуют рассчитанные движения
	КонецЕсли;
	
	ОписаниеДвижений = ПротоколРасчета.СформированныеДвижения[ОписаниеРегистра.ИмяРегистра];
	
	ТаблицаРегистраторы = РезультатЗапроса.Выгрузить();
	
	Регистраторы = ТаблицаРегистраторы.Скопировать(, "Регистратор");
	Регистраторы.Свернуть("Регистратор", "");
	Регистраторы = Регистраторы.ВыгрузитьКолонку("Регистратор");
	
	ОписаниеДвижений.ОшибкиРегистраторов.Вставить(ОписаниеОшибки, Регистраторы);
	ОписаниеДвижений.Ошибочных = ОписаниеДвижений.Ошибочных + Регистраторы.Количество();
	
	ЗафиксироватьОшибкуРасчета(
		ПараметрыРасчета,
		Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаФормированияДвиженийПоРегистрам);
		
	Для Каждого ТекСтр Из ТаблицаРегистраторы Цикл
		
		РасчетСебестоимостиПрикладныеАлгоритмы.ЗарегистрироватьПроблемуВыполненияРасчета(
			ПараметрыРасчета,
			ТекСтр.Организация,
			НСтр("ru='При формировании движений диагностированы ошибки'", ОбщегоНазначения.КодОсновногоЯзыка()),
			ОписаниеОшибки + ": " + СокрЛП(ТекСтр.Регистратор),
			ТекСтр.Регистратор);
		
	КонецЦикла;
	
КонецПроцедуры

// Очищает счетчики сформированных в случае, когда кэш регистра очищается и движения формируются заново.
//
Процедура ОчисткаСформированныхДвижений(ПараметрыРасчета, ИмяРегистра) Экспорт
	
	ПротоколРасчета = ПараметрыРасчета.ПротоколРасчета; // см. РасчетСебестоимостиПротоколРасчета.ИнициализироватьПротоколРасчета 
	
	ОписаниеДвижений = Новый Структура;
	ОписаниеДвижений.Вставить("Всего", 				 0);
	ОписаниеДвижений.Вставить("Уникальных", 		 0);
	ОписаниеДвижений.Вставить("Ошибочных", 			 0);
	ОписаниеДвижений.Вставить("ОшибкиРегистраторов", Новый Соответствие);
	
	ПротоколРасчета.СформированныеДвижения.Вставить(ИмяРегистра, ОписаниеДвижений);
	
КонецПроцедуры

#КонецОбласти

#Область ПротоколированиеДополнительнойИнформации

// Добавляет в протокол произвольный текст (внутри этапа расчета).
//
Процедура ДополнительнаяИнформация(ПараметрыРасчета, ТекстовоеОписание, ТекстДляЖурналаРегистрации = Неопределено) Экспорт
	
	ОписаниеЭтапа	= ОписаниеТекущегоЭтапа(ПараметрыРасчета);
	
	ОписаниеЭтапа.ДополнительнаяИнформация.Добавить(ТекстовоеОписание);
	
	// Запишем дополнительную информацию расчета в журнал регистрации.
	Если ЗначениеЗаполнено(ТекстДляЖурналаРегистрации) Тогда
		
		ЗаписьЖурналаРегистрации(
			РасчетСебестоимостиПрикладныеАлгоритмы.ИмяСобытияДопИнформацииДляЖурналаРегистрации(ПараметрыРасчета),
			УровеньЖурналаРегистрации.Информация,
			,
			,
			ТекстДляЖурналаРегистрации);
		
	КонецЕсли;		
	
КонецПроцедуры

#КонецОбласти

#Область ПротоколированиеОшибокРасчета

// Добавляет в протокол информацию об ошибке расчета.
//
Процедура ЗафиксироватьОшибкуРасчета(ПараметрыРасчета, ТипОшибки,
			ТекстДляПротокола = Неопределено, ТекстДляЖурналаРегистрации = Неопределено,
			МассивОрганизаций = Неопределено) Экспорт
	
	Если ВедетсяПротоколРасчета(ПараметрыРасчета) Тогда
		
		Если ПараметрыРасчета.Свойство("НеФиксироватьОшибкуРасчета") Тогда
			ПараметрыРасчета.Удалить("НеФиксироватьОшибкуРасчета");
			Возврат;
		КонецЕсли;
		
		ПротоколРасчета = ПараметрыРасчета.ПротоколРасчета; // см. РасчетСебестоимостиПротоколРасчета.ИнициализироватьПротоколРасчета 
		
		ТекстДляПротоколаФорматированный = ПредставлениеТекстаОшибки(ТекстДляПротокола);
		
		КоличествоОшибок = ПротоколРасчета.ОшибкиРасчета[ТипОшибки];
		ПротоколРасчета.ОшибкиРасчета.Вставить(
			ТипОшибки,
			?(НЕ ЗначениеЗаполнено(КоличествоОшибок), 0, КоличествоОшибок) + 1);
		
		// Запишем описание ошибки в протокол.
		Если ТипОшибки = Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаФормированияДвиженийПоРегистрам
		 И ЗначениеЗаполнено(ТекстДляПротоколаФорматированный) Тогда
			
			// Т.к. ошибки этого типа выводятся не в этапе, а в конце протокола, то в тексте ошибки надо уточнить имя проблемного этапа.
			Если ПротоколРасчета.Этапы.Количество() > 0 Тогда
				ИмяЭтапа = ПротоколРасчета.Этапы[ПротоколРасчета.Этапы.Количество()-1].ПредставлениеЭтапа;
				ТекстДляПротоколаФорматированный = ТекстДляПротоколаФорматированный + " (" + ИмяЭтапа + ")";
			КонецЕсли;
			
			ПротоколРасчета.ОшибкиДвижений.Добавить(ТекстДляПротоколаФорматированный);
			
		ИначеЕсли ТипОшибки = Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаЗаписиДвиженийПоРегистрам
		 И ЗначениеЗаполнено(ТекстДляПротоколаФорматированный) Тогда
			
			ПротоколРасчета.ОшибкиЗаписи.Добавить(ТекстДляПротоколаФорматированный);
			
		ИначеЕсли ЗначениеЗаполнено(ТекстДляПротоколаФорматированный) Тогда
			
			ОписаниеЭтапа = ОписаниеТекущегоЭтапа(ПараметрыРасчета);
			
			Если ОписаниеЭтапа <> Неопределено
			 И ОписаниеЭтапа.ИнформацияОбОшибках[ТипОшибки] = Неопределено Тогда
				ОписаниеЭтапа.ИнформацияОбОшибках.Вставить(ТипОшибки, Новый Массив);
			КонецЕсли;
			
			Если НЕ (СтрНачинаетсяС(ТекстДляПротоколаФорматированный, СлужебныйСимвол_БезНумерации())
					И ОписаниеЭтапа <> Неопределено
			 		И ЗначениеЗаполнено(ОписаниеЭтапа.ИнформацияОбОшибках[ТипОшибки])
			 		И ОписаниеЭтапа.ИнформацияОбОшибках[ТипОшибки][0] = ТекстДляПротоколаФорматированный) Тогда
				ОписаниеЭтапа.ИнформацияОбОшибках[ТипОшибки].Добавить(ТекстДляПротоколаФорматированный);
			КонецЕсли;
			
		КонецЕсли;		
		
	КонецЕсли;
	
	// Запишем описание ошибки в журнал регистрации.
	Если ЗначениеЗаполнено(ТекстДляЖурналаРегистрации) Тогда
		
		ЗаписьЖурналаРегистрации(
			РасчетСебестоимостиПрикладныеАлгоритмы.ИмяСобытияОшибкиДляЖурналаРегистрации(ПараметрыРасчета),
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ПредставлениеТекстаОшибки(ТекстДляЖурналаРегистрации));
		
	КонецЕсли;		
	
КонецПроцедуры

#КонецОбласти


#Область ЖурналРегистрации

// Делает запись в журнал регистрации о начале выполнения этапа.
//
Процедура ОтметкаВЖурналеРегистрации(ПараметрыРасчета, ПредставлениеЭтапа, Уровень = Неопределено, Комментарий = "")
	
	Если НЕ ЗначениеЗаполнено(Уровень) Тогда
		Уровень = УровеньЖурналаРегистрации.Информация;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Комментарий) Тогда
		Комментарий = НСтр("ru = 'Начало выполнения этапа.'", ОбщегоНазначения.КодОсновногоЯзыка());
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(
		РасчетСебестоимостиПрикладныеАлгоритмы.ИмяСобытияЖурналаРегистрации(
			ПараметрыРасчета, НСтр("ru='Этап'", ОбщегоНазначения.КодОсновногоЯзыка()) + "." + ПредставлениеЭтапа),
		Уровень,
		,
		,
		Комментарий);
	
КонецПроцедуры

#КонецОбласти

#Область ПредставлениеИВыводДанных

// Возвращает представление интервала времени (ВремяНачала, ВремяОкончания) и его % от общего времени расчета.
//
Функция ПредставлениеВремениВыполнения(ИсточникДанныхОВремени, ИсточникДанныхОбОбщемВремени = Неопределено)
	
	СтруктураДанныхОВремени = Новый Структура("ВремяНачала, ВремяОкончания, ОбщееВремя");
	ЗаполнитьЗначенияСвойств(СтруктураДанныхОВремени, ИсточникДанныхОВремени);
	
	Если ЗначениеЗаполнено(СтруктураДанныхОВремени.ОбщееВремя) Тогда
		Секунды = СтруктураДанныхОВремени.ОбщееВремя / 1000;
	ИначеЕсли ЗначениеЗаполнено(СтруктураДанныхОВремени.ВремяНачала) И ЗначениеЗаполнено(СтруктураДанныхОВремени.ВремяОкончания) Тогда
		Секунды = (СтруктураДанныхОВремени.ВремяОкончания - СтруктураДанныхОВремени.ВремяНачала) / 1000;
	Иначе
		Секунды = 0;
	КонецЕсли;
	
	ПредставлениеВремени = ПредставлениеВремени(Секунды);
	
	Если ИсточникДанныхОбОбщемВремени <> Неопределено Тогда
		
		СтруктураДанныхОВремени = Новый Структура("ВремяНачала, ВремяОкончания");
		ЗаполнитьЗначенияСвойств(СтруктураДанныхОВремени, ИсточникДанныхОбОбщемВремени);
		
		Если ЗначениеЗаполнено(СтруктураДанныхОВремени.ВремяНачала) И ЗначениеЗаполнено(СтруктураДанныхОВремени.ВремяОкончания) Тогда
			
			ОбщееВремя 			 = СтруктураДанныхОВремени.ВремяОкончания - СтруктураДанныхОВремени.ВремяНачала;
			ПроцентВремени 		 = Окр(100 * Секунды / (?(ОбщееВремя = 0, 1, ОбщееВремя) / 1000), 2);
			ПредставлениеВремени = ПредставлениеВремени + " (" + ПредставлениеЗначения(ПроцентВремени) + "%)";
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СокрЛП(ПредставлениеВремени);
	
КонецФункции

// Возвращает представление месяца расчета.
//
// Возвращаемое значение:
//	Строка - 
//
Функция ПредставлениеПериодаРасчета(ПараметрыРасчетаИлиПериод) Экспорт
	
	Если ТипЗнч(ПараметрыРасчетаИлиПериод) = Тип("Структура") Тогда
		Дата = ПараметрыРасчетаИлиПериод.РасчетныйПериод.КонецПериода;
		РежимЗакрытияМесяца = ПараметрыРасчетаИлиПериод.РежимЗакрытияМесяца;
	Иначе
		Дата = ПараметрыРасчетаИлиПериод;
		РежимЗакрытияМесяца = Неопределено;
	КонецЕсли;
	
	Если РежимЗакрытияМесяца = Перечисления.РежимыЗакрытияМесяца.ПредварительноеЗакрытие Тогда
		ПредставлениеПериода = НСтр("ru='с %1 по %2'");
		ПредставлениеПериода = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ПредставлениеПериода,
			Формат(НачалоМесяца(Дата), "ДЛФ=D"),
			Формат(Дата, "ДЛФ=D"));
	Иначе
		ПредставлениеПериода = ЗакрытиеМесяцаКлиентСервер.ПредставлениеПериодаМесяцГод(Дата);
	КонецЕсли;
	
	Возврат ПредставлениеПериода;
	
КонецФункции

// Возвращает представление произвольного значения.
// При использовании этой функции все значения одинакового типа будут одинаковое форматирование.
//
// Возвращаемое значение:
//	Строка - 
//
Функция ПредставлениеЗначения(Значение, ПредставлениеДатыКакПериодРасчета = Ложь) Экспорт
	
	Если ТипЗнч(Значение) = Тип("Булево") Тогда
		
		Возврат Формат(Значение, НСтр("ru='БЛ=Нет; БИ=Да'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	ИначеЕсли ТипЗнч(Значение) = Тип("Число") Тогда 
		
		Если Значение <> 0 И Значение > -0.001 И Значение < 0.001 Тогда
			
			// Представление малых чисел
			
			Степень 	  = 0;
			НовоеЗначение = Значение;
			
			Пока НовоеЗначение > -1 И НовоеЗначение < 1 Цикл
				Степень = Степень + 1;
				НовоеЗначение = НовоеЗначение * 10;
			КонецЦикла;
			
			Возврат СокрЛП(НовоеЗначение) + " * 10^-" + СокрЛП(Степень);
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Значение) = Тип("Дата") И ПредставлениеДатыКакПериодРасчета Тогда 
		
		Возврат ПредставлениеПериодаРасчета(Значение);
		
	ИначеЕсли ТипЗнч(Значение) = Тип("ПеречислениеСсылка.МетодыОценкиСтоимостиТоваров") Тогда
		
		Если НЕ ЗначениеЗаполнено(Значение) Тогда
			Возврат "<" + НСтр("ru='Не используется'", ОбщегоНазначения.КодОсновногоЯзыка()) + ">";
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Значение) = Тип("ПеречислениеСсылка.ТипыЗаписейПартий") Тогда
		
		Если НЕ ЗначениеЗаполнено(Значение) Тогда
			Возврат "<" + НСтр("ru='Пустой тип записи партии'", ОбщегоНазначения.КодОсновногоЯзыка()) + ">";
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СокрЛП(Значение);
	
КонецФункции

// Возвращает представление времени в часах/минутах/секундах.
//
// Возвращаемое значение:
//	Строка - 
//
Функция ПредставлениеВремени(Значение, СДолямиСекунд = Истина) Экспорт
	
	Секунды = Значение;
	
	Часы = Цел(Секунды / 3600);
	Секунды = Секунды - Часы*3600;
	
	Минуты  = Цел(Секунды / 60);
	Секунды = Окр(Секунды - Минуты*60, ?(СДолямиСекунд, 3, 0));
	
	ПредставлениеВремени = ""
		+ ?(Часы = 0, "", ПредставлениеЗначения(Часы) + " " + НСтр("ru='ч.'", ОбщегоНазначения.КодОсновногоЯзыка()) + " ")
		+ ?(Минуты = 0, "", ПредставлениеЗначения(Минуты) + " " + НСтр("ru='мин.'", ОбщегоНазначения.КодОсновногоЯзыка()) + " ")
		+ ПредставлениеЗначения(Секунды) + " " + НСтр("ru='сек.'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	Возврат СокрЛП(ПредставлениеВремени);
	
КонецФункции

// Возвращает представление многострочного текста - добавляет отступы для каждой строки.
//
// Возвращаемое значение:
//	Строка - 
//
Функция ПредставлениеМногострочногоТекста(Значение, Знач ТекстФорматаАбзаца = "",
			НеВыводитьПустыеСтроки = Истина, УдалятьСтароеФорматирование = Истина) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Значение) Тогда
		Возврат "";
	КонецЕсли;
	
	ПредставлениеТекста = "";
	ТекстФорматаАбзаца  = СтрЗаменить(ТекстФорматаАбзаца, "	", "    "); // заменим табуляцию на пробелы
	ТекстОтступ			= Символы.ПС;
	
	Для НС = 1 По СтрДлина(ТекстФорматаАбзаца) Цикл
		ТекстОтступ = ТекстОтступ + " "; // отступ для 2й и следующих строк
	КонецЦикла;
	
	Для НС = 1 По СтрЧислоСтрок(Значение) Цикл
		
		Если УдалятьСтароеФорматирование Тогда
			ТекущаяСтрока = СокрЛП(СтрПолучитьСтроку(Значение, НС));
		Иначе
			ТекущаяСтрока = СтрПолучитьСтроку(Значение, НС);
		КонецЕсли;
		
		Если НеВыводитьПустыеСтроки И НЕ ЗначениеЗаполнено(ТекущаяСтрока) Тогда
			Продолжить;
		КонецЕсли;
		
		ПредставлениеТекста = ПредставлениеТекста
			+ ?(НЕ ЗначениеЗаполнено(ПредставлениеТекста), ТекстФорматаАбзаца, ТекстОтступ) + ТекущаяСтрока;
		
	КонецЦикла;
	
	Возврат ПредставлениеТекста;
	
КонецФункции

// Возвращает представление текста сообщения об ошибке.
//
// Возвращаемое значение:
//	Строка - 
//
Функция ПредставлениеТекстаОшибки(Значение) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Значение) Тогда
		Возврат "";
	КонецЕсли;
	
	ПредставлениеТекста = "";
	ТекстОтступ 		= Символы.ПС;
	ТекстРазделитель 	= "}:";
	
	Для НС = 1 По СтрЧислоСтрок(Значение) Цикл
		
		ТекущаяСтрока = СокрЛП(СтрПолучитьСтроку(Значение, НС));
		
		Если НЕ ЗначениеЗаполнено(ТекущаяСтрока) Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрНайти(НРег(ТекущаяСтрока), НРег("ВызватьИсключение")) = 1
		 ИЛИ СтрНайти(НРег(ТекущаяСтрока), НРег("Raise")) = 1 Тогда
			Продолжить; // текущая строка лишняя, неинформативная - мы сами вызвали ошибку
		КонецЕсли;
		
		Если Лев(ТекущаяСтрока, СтрДлина(СлужебныйСимвол_МаркированныйСписок())) = СлужебныйСимвол_МаркированныйСписок() Тогда
			ТекстОтступ = Символы.ПС; // описание ошибки содержит список - очищаем форматирование для вывода очередного элемента списка
			ТекущаяСтрока = "- " + Сред(ТекущаяСтрока, 2);
		КонецЕсли;
		
		Пока ЗначениеЗаполнено(ТекущаяСтрока) И СтрНайти(ТекущаяСтрока, ТекстРазделитель) > 0 Цикл
			
			ТекущаяПодстрока = Лев(ТекущаяСтрока, СтрНайти(ТекущаяСтрока, ТекстРазделитель) + СтрДлина(ТекстРазделитель) - 1); // место ошибки в коде
			ТекущаяСтрока 	 = СокрЛП(Сред(ТекущаяСтрока, СтрДлина(ТекущаяПодстрока) + 1)); // остаток строки
			
			ПредставлениеТекста = ПредставлениеТекста
				+ ?(НЕ ЗначениеЗаполнено(ПредставлениеТекста), "", ТекстОтступ) + ТекущаяПодстрока; // левая часть текущей строки
			
			ТекстОтступ = ТекстОтступ + "    "; // увеличим отступ остального текста ошибки
			
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ТекущаяСтрока) Тогда
			ПредставлениеТекста = ПредставлениеТекста
				+ ?(НЕ ЗначениеЗаполнено(ПредставлениеТекста), "", ТекстОтступ) + ТекущаяСтрока; // остаток текущей строки
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПредставлениеТекста;
	
КонецФункции

// Возвращает представление списка ошибок, зафиксированных при расчете.
//
Функция ПредставлениеОшибокРасчета(ПротоколРасчета, Заголовок = Неопределено, Разделитель = Неопределено)
	
	Если Заголовок = Неопределено Тогда
		Заголовок = НСтр("ru='Обнаружены ошибки'", ОбщегоНазначения.КодОсновногоЯзыка());
	КонецЕсли;
	
	Если Разделитель = Неопределено Тогда
		Разделитель = ", ";
	КонецЕсли;
	
	МногострочныйРазделитель = (СтрНайти(Разделитель, Символы.ПС) > 0);
	
	ПредставлениеОшибок = "";
	
	ТипыОшибок = Новый Соответствие;
    ТипыОшибок.Вставить(Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаВИсходныхДанныхДляРасчета,
        НСтр("ru='в исходных данных для расчета'", ОбщегоНазначения.КодОсновногоЯзыка()));
    ТипыОшибок.Вставить(Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаИнициализацииПараметровРасчета,
        НСтр("ru='при инициализации параметров расчета'", ОбщегоНазначения.КодОсновногоЯзыка()));
    ТипыОшибок.Вставить(Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаВУчетныхПолитикахОрганизаций,
        НСтр("ru='в учетных политиках организаций'", ОбщегоНазначения.КодОсновногоЯзыка()));
    ТипыОшибок.Вставить(Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаВосстановленияДвиженийДокументов,
        НСтр("ru='при восстановлении движений документов'", ОбщегоНазначения.КодОсновногоЯзыка()));
    ТипыОшибок.Вставить(Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаИнициализацииДокументовРасчетаСебестоимости,
        НСтр("ru='при инициализации документов расчета себестоимости'", ОбщегоНазначения.КодОсновногоЯзыка()));
    ТипыОшибок.Вставить(Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаРаспределенияПартий,
        НСтр("ru='при распределении партий'", ОбщегоНазначения.КодОсновногоЯзыка()));
    ТипыОшибок.Вставить(Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаРаспределенияНДС,
        НСтр("ru='при распределении НДС'", ОбщегоНазначения.КодОсновногоЯзыка()));
    ТипыОшибок.Вставить(Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаВыполненияРегламентныхОпераций,
        НСтр("ru='при выполнении связанных регламентных операций закрытия месяца'", ОбщегоНазначения.КодОсновногоЯзыка()));
    ТипыОшибок.Вставить(Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаФормированияДвиженийПоРегистрам,
        НСтр("ru='при формировании движений по регистрам'", ОбщегоНазначения.КодОсновногоЯзыка()));
    ТипыОшибок.Вставить(Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаЗаписиДвиженийПоРегистрам,
        НСтр("ru='при записи движений по регистрам'", ОбщегоНазначения.КодОсновногоЯзыка()));
    ТипыОшибок.Вставить(Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаВОстаткахПослеРасчета,
        НСтр("ru='в остатках после расчета'", ОбщегоНазначения.КодОсновногоЯзыка()));
    ТипыОшибок.Вставить(Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаВремениВыполнения,
        НСтр("ru='при выполнения кода (run-time error)'", ОбщегоНазначения.КодОсновногоЯзыка()));
    ТипыОшибок.Вставить(Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаВНачальныхОстаткахПУ22,
        НСтр("ru='в начальных остатках при переходе на партионный учет версии 2.2'", ОбщегоНазначения.КодОсновногоЯзыка()));	
	
	// Выведем типы ошибок в том же порядке, в котором они присутствуют в метаданных.
	Для Каждого МетаЗначение Из Метаданные.Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ЗначенияПеречисления Цикл
		
		Если ЗначениеЗаполнено(ПротоколРасчета.ОшибкиРасчета[Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости[МетаЗначение.Имя]]) Тогда 
			
			ТекущаяОшибка = ТипыОшибок.Получить(Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости[МетаЗначение.Имя]);
			
			Если НЕ ЗначениеЗаполнено(ТекущаяОшибка) Тогда
				ТекущаяОшибка = МетаЗначение.Синоним;
			КонецЕсли;
			
			ПредставлениеОшибок = ПредставлениеОшибок + ?(ПредставлениеОшибок = "", "", Разделитель) + СокрЛП(ТекущаяОшибка);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ПредставлениеОшибок) Тогда
		ПредставлениеОшибок =
			Заголовок + ?(МногострочныйРазделитель, Разделитель, " ")
			+ ПредставлениеОшибок + ?(МногострочныйРазделитель, "", ".");
	КонецЕсли;
	
	Возврат ПредставлениеОшибок;
	
КонецФункции

// Возвращает служебный символ, который позволяет пропустить нумерацию для "заголовка" списка ошибок протокола.
//
// Возвращаемое значение:
//	Строка - 
//
Функция СлужебныйСимвол_БезНумерации() Экспорт
	Возврат "#";
КонецФункции

// Возвращает служебный символ, который позволяет сформировать маркированный список ошибок.
//
// Возвращаемое значение:
//	Строка - 
//
Функция СлужебныйСимвол_МаркированныйСписок() Экспорт
	Возврат "~";
КонецФункции

// Возвращает строку, разделяющую части протокола расчета.
//
Функция РазделительЧастейПротокола()
	Возврат "=================================================================================";
КонецФункции

// Возвращает заголовок протокола для вывода его на экран.
//
// Возвращаемое значение:
//	Строка - 
//
Функция ЗаголовокПротоколаПриВыводе(ПериодРасчета) Экспорт
	
	ТекстЗаголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Протокол расчета периода %1'", ОбщегоНазначения.КодОсновногоЯзыка()),
		ПредставлениеПериодаРасчета(ПериодРасчета));
	
	Возврат ТекстЗаголовок;
	
КонецФункции

	
// Выводит в протокол и дублирует в журнал регистрации информацию об ошибках.
//
Процедура ВывестиИнформациюОбОшибках(ПараметрыРасчета, Протокол, ДанныеОшибок, Заголовок, ПредставлениеЭтапа = Неопределено)
	
	Если НЕ ЗначениеЗаполнено(ДанныеОшибок) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеОшибок) = Тип("Массив") Тогда
		СоответствиеОшибок = Новый Соответствие;
		СоответствиеОшибок.Вставить(Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ПустаяСсылка(), ДанныеОшибок);
	Иначе
		СоответствиеОшибок = ДанныеОшибок;
	КонецЕсли;
	
	ОписаниеОшибок   = "";
	ОписаниеОшибокЖР = "";
	ЕстьОшибкиДляЖР  = Ложь;
	НомерОшибки      = 0;
	
	Для Каждого КлючИЗначение Из СоответствиеОшибок Цикл
		
		Если СоответствиеОшибок.Количество() > 1 Тогда
			// Если есть несколько типов ошибок, то выведем текст-группировку по типу.
			ОписаниеОшибок = ОписаниеОшибок + ?(ОписаниеОшибок = "", "", "
				|") + НСтр("ru = 'Тип ошибки'", ОбщегоНазначения.КодОсновногоЯзыка()) + ": """ + ПредставлениеЗначения(КлючИЗначение.Ключ) + """.";
			
			Если КлючИЗначение.Ключ <> Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаВОстаткахПослеРасчета
			 И КлючИЗначение.Ключ <> Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаВИсходныхДанныхДляРасчета Тогда
				// Ошибки указанных выше типов будут записаны в журнал регистрации механизмом закрытия месяца
				ОписаниеОшибокЖР = ОписаниеОшибокЖР + ?(ОписаниеОшибокЖР = "", "", "
					|") + НСтр("ru = 'Тип ошибки'", ОбщегоНазначения.КодОсновногоЯзыка()) + ": """ + ПредставлениеЗначения(КлючИЗначение.Ключ) + """.";
			КонецЕсли;
			
		КонецЕсли;
		
		Для Каждого ТекстОшибки Из КлючИЗначение.Значение Цикл
			
			ТребуетсяНумерация = (Лев(ТекстОшибки, СтрДлина(СлужебныйСимвол_БезНумерации())) <> СлужебныйСимвол_БезНумерации());
			
			Если ТребуетсяНумерация Тогда 
				НомерОшибки = НомерОшибки + 1;
				ПрефиксНумерации = "	" + ПредставлениеЗначения(НомерОшибки) + ". ";
			Иначе
				// В начале строк-группировок можно указать специальный символ #, и такие строки не будут нумероваться при выводе.
				ТекстОшибки = Сред(ТекстОшибки, 2);
				ПрефиксНумерации = "";
			КонецЕсли;
			
			ОписаниеОшибок = ОписаниеОшибок + ?(ОписаниеОшибок = "", "", "
				|") + ПредставлениеМногострочногоТекста(ТекстОшибки, ?(СоответствиеОшибок.Количество() > 1, "	", "") + ПрефиксНумерации, Ложь, Ложь);
			
			Если КлючИЗначение.Ключ <> Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаВОстаткахПослеРасчета
			 И КлючИЗначение.Ключ <> Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаВИсходныхДанныхДляРасчета Тогда
				// Ошибки указанных выше типов будут записаны в журнал регистрации механизмом закрытия месяца
				ЕстьОшибкиДляЖР = Истина;
				ОписаниеОшибокЖР = ОписаниеОшибокЖР + ?(ОписаниеОшибокЖР = "", "", "
					|") + ПредставлениеМногострочногоТекста(ТекстОшибки, ?(СоответствиеОшибок.Количество() > 1, "	", "") + ПрефиксНумерации, Ложь, Ложь);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ОписаниеОшибок =
		Заголовок
		+ ?(НомерОшибки > 0,  " (" + ПредставлениеЗначения(НомерОшибки) + " " + НСтр("ru = 'шт.'", ОбщегоНазначения.КодОсновногоЯзыка()) + ")", "")
		+ " !" + Символы.ПС + ОписаниеОшибок;
	
	Протокол.ДобавитьСтроку(ОписаниеОшибок);
	
	Если ЕстьОшибкиДляЖР Тогда
		
		ОписаниеОшибокЖР =
			Заголовок
			+ ?(НомерОшибки > 0,  " (" + ПредставлениеЗначения(НомерОшибки) + " " + НСтр("ru = 'шт.'", ОбщегоНазначения.КодОсновногоЯзыка()) + ")", "")
			+ " !" + Символы.ПС + ОписаниеОшибокЖР;
		
		Если ЗначениеЗаполнено(ПредставлениеЭтапа) Тогда
			ОписаниеОшибокЖР = ПредставлениеЭтапа + Символы.ПС + ОписаниеОшибокЖР;
		КонецЕсли;
		
		Если ПараметрыРасчета.ПротоколРасчета.РасчетЗавершен Тогда
			ЗаписьЖурналаРегистрации(
				РасчетСебестоимостиПрикладныеАлгоритмы.ИмяСобытияОшибкиДляЖурналаРегистрации(ПараметрыРасчета), 
				УровеньЖурналаРегистрации.Ошибка,,,
				ОписаниеОшибокЖР);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Производительность

Процедура НачатьЗамерВремени(ПараметрыРасчета) Экспорт
	
	Если ПараметрыРасчета.Свойство("ОписаниеЗамера") Тогда
		// Штатный вызов из механизма партионного учета версии 2.1.
		ОписаниеЗамераПроизводительности = ПараметрыРасчета.ОписаниеЗамера;
	ИначеЕсли ПараметрыРасчета.ВариантРасчета = Перечисления.ВариантыРасчетаПартийИСебестоимости.ПартииИСебестоимость Тогда
		ОписаниеЗамераПроизводительности = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
			"ЗакрытиеМесяца.РасчетПартийИСебестоимости22ПлатформенноеСЛУ");
	Иначе
		Возврат;
	КонецЕсли;
	
	ПараметрыРасчета.Вставить("Производительность", Новый Структура);
	
	ПараметрыРасчета.Производительность.Вставить("ОписаниеЗамераПроизводительности", ОписаниеЗамераПроизводительности);
	ПараметрыРасчета.Производительность.Вставить("КоличествоДанных", 0);
	ПараметрыРасчета.Производительность.Вставить("НаименованиеЭтапа", ПараметрыРасчета.ПредставлениеЭтапа);
	
КонецПроцедуры

Процедура ЗакончитьЗамерВремени(ПараметрыРасчета) Экспорт
	
	Если НЕ ПараметрыРасчета.Свойство("Производительность") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыРасчета.Свойство("ОписаниеЗамера") Тогда
		// Замер запущен "извне" - там же должен закончиться.
		ЗафиксироватьЗамерВремени(ПараметрыРасчета);
	Иначе
		ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(
			ПараметрыРасчета.Производительность.ОписаниеЗамераПроизводительности,
			ПараметрыРасчета.Производительность.КоличествоДанных);
	КонецЕсли;
	
	ПараметрыРасчета.Удалить("Производительность");
	ПараметрыРасчета.Удалить("ОписаниеЗамера");
	
КонецПроцедуры

Процедура ЗафиксироватьЗамерВремени(ПараметрыРасчета) Экспорт
	
	Если НЕ ПараметрыРасчета.Свойство("Производительность") Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеЭтапа = ОписаниеТекущегоЭтапа(ПараметрыРасчета);
	
	Если НЕ ЗначениеЗаполнено(ОписаниеЭтапа) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыРасчета.Производительность.КоличествоДанных = 0 Тогда
		ПараметрыРасчета.Производительность.КоличествоДанных = 1;
	КонецЕсли;
	
	ОписаниеЭтапа.КоличествоДанных = ОписаниеЭтапа.КоличествоДанных + ПараметрыРасчета.Производительность.КоличествоДанных;
	
	ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(
		ПараметрыРасчета.Производительность.ОписаниеЗамераПроизводительности,
		ПараметрыРасчета.Производительность.КоличествоДанных,
		ПараметрыРасчета.Производительность.НаименованиеЭтапа);
	
	ПараметрыРасчета.Производительность.КоличествоДанных = 0;
	
КонецПроцедуры

Процедура УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыРасчета, КоличествоДанных) Экспорт
	
	Если НЕ ПараметрыРасчета.Свойство("Производительность") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыРасчета.Производительность.КоличествоДанных =
		ПараметрыРасчета.Производительность.КоличествоДанных + ?(КоличествоДанных <= 0, 1, КоличествоДанных);
	
КонецПроцедуры

#КонецОбласти

#Область ПредставлениеИВыводДанных

Функция ВыполнитьЗапросСЗамерами(ПараметрыРасчета, ЗапросДляЗамера) Экспорт
	
	ОписаниеЭтапа 	= ОписаниеТекущегоЭтапа(ПараметрыРасчета);
	
	ТаблицаЗамеров = Новый ТаблицаЗначений;
	ТаблицаЗамеров.Колонки.Добавить("ИмяВременнойТаблицы", Новый ОписаниеТипов("Строка"));
	ТаблицаЗамеров.Колонки.Добавить("УничтожениеТаблицы",  Новый ОписаниеТипов("Булево"));
	ТаблицаЗамеров.Колонки.Добавить("ВремяНачала", 		   Новый ОписаниеТипов("Дата"));
	ТаблицаЗамеров.Колонки.Добавить("Длительность", 	   Новый ОписаниеТипов("Число"));
	ТаблицаЗамеров.Колонки.Добавить("РазмерРезультата",    Новый ОписаниеТипов("Число"));
	
	РезультатЗапроса   = Новый Массив;
	СтарыйТекстЗапроса = ЗапросДляЗамера.Текст;
	
	Если ЗапросДляЗамера.МенеджерВременныхТаблиц = Неопределено Тогда
		ВременныйМенеджерВТ = Новый МенеджерВременныхТаблиц;
		ЗапросДляЗамера.МенеджерВременныхТаблиц = ВременныйМенеджерВТ;
	Иначе
		ВременныйМенеджерВТ = Неопределено;
	КонецЕсли;
	
	СхемаЗапросаДляЗамера = Новый СхемаЗапроса;
	СхемаЗапросаДляЗамера.УстановитьТекстЗапроса(СтарыйТекстЗапроса);
	
	Для Каждого ТекущийЗапрос Из СхемаЗапросаДляЗамера.ПакетЗапросов Цикл
		
		ИмяВременнойТаблицы = "";
		
		НоваяСтрока = ТаблицаЗамеров.Добавить();
		НоваяСтрока.УничтожениеТаблицы = (ТипЗнч(ТекущийЗапрос) = Тип("ЗапросУничтоженияТаблицыСхемыЗапроса"));
		
		Если НоваяСтрока.УничтожениеТаблицы Тогда
			НоваяСтрока.ИмяВременнойТаблицы = ТекущийЗапрос.ИмяТаблицы;
			ЗапросДляЗамера.Текст = "УНИЧТОЖИТЬ " + ТекущийЗапрос.ИмяТаблицы;
		Иначе
			НоваяСтрока.ИмяВременнойТаблицы = ТекущийЗапрос.ТаблицаДляПомещения;
			ЗапросДляЗамера.Текст = ТекущийЗапрос.ПолучитьТекстЗапроса();
		КонецЕсли;
		
		НоваяСтрока.ВремяНачала = ТекущаяДатаСеанса();
		ВремяНачала = ТекущаяУниверсальнаяДатаВМиллисекундах();
		
		РезультатТекущегоЗапроса = ЗапросДляЗамера.Выполнить();
		
		ВремяОкончания = ТекущаяУниверсальнаяДатаВМиллисекундах();
		НоваяСтрока.Длительность = ВремяОкончания - ВремяНачала;
		
		Если НоваяСтрока.УничтожениеТаблицы Тогда
			
			ИмяВременнойТаблицы = "_УНИЧТОЖИТЬ_" + ТекущийЗапрос.ИмяТаблицы;
			РезультатЗапроса.Добавить(Неопределено);
			
		ИначеЕсли ЗначениеЗаполнено(НоваяСтрока.ИмяВременнойТаблицы) Тогда
			
			ИмяВременнойТаблицы = НоваяСтрока.ИмяВременнойТаблицы;
			
			РезультатЗапроса.Добавить(Неопределено);
			
			НоваяСтрока.РазмерРезультата = РезультатТекущегоЗапроса.Выгрузить()[0][0];
			
		Иначе
			
			ИмяВременнойТаблицы = "_ВЫБРАТЬ_";
			
			ВыборкаТекущегоЗапроса = РезультатТекущегоЗапроса.Выбрать();
			РезультатЗапроса.Добавить(ВыборкаТекущегоЗапроса);
			
			НоваяСтрока.РазмерРезультата = ВыборкаТекущегоЗапроса.Количество();
			
		КонецЕсли;
		
		Если НЕ ОписаниеЭтапа.ВременныеТаблицы.Свойство(ИмяВременнойТаблицы) Тогда
			ТекущаяВременнаяТаблица = Новый Массив;
		Иначе
			ТекущаяВременнаяТаблица = ОписаниеЭтапа.ВременныеТаблицы[ИмяВременнойТаблицы]; // Массив
		КонецЕсли;
	
		ОписаниеВременнойТаблицы = Новый Структура;
		ОписаниеВременнойТаблицы.Вставить("КоличествоСтрок", НоваяСтрока.РазмерРезультата);
		ОписаниеВременнойТаблицы.Вставить("ВремяНачала", 	 ВремяНачала);
		ОписаниеВременнойТаблицы.Вставить("ВремяОкончания",  ВремяОкончания);
		ОписаниеВременнойТаблицы.Вставить("Пояснение", 		 "");
		ОписаниеВременнойТаблицы.Вставить("ИсходныеДанные",  "");
		
		ТекущаяВременнаяТаблица.Добавить(ОписаниеВременнойТаблицы);
		ОписаниеЭтапа.ВременныеТаблицы.Вставить(ИмяВременнойТаблицы, ТекущаяВременнаяТаблица);
		
	КонецЦикла;
	
	Если ВременныйМенеджерВТ <> Неопределено Тогда
		ЗапросДляЗамера.МенеджерВременныхТаблиц = Неопределено;
		ВременныйМенеджерВТ.Закрыть();
	КонецЕсли;
	
	ЗапросДляЗамера.Текст = СтарыйТекстЗапроса;
	
	Если РезультатЗапроса.Количество() = 0 Тогда
		Возврат Неопределено;
	ИначеЕсли РезультатЗапроса.Количество() = 1 Тогда
		Возврат РезультатЗапроса[0];
	КонецЕсли;
	
	Возврат РезультатЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти
