////////////////////////////////////////////////////////////////
// Модуль "НазначенияКлиентСервер" содержит процедуры и функции для 
// работы с назначениями на клиенте и на сервере.
//
////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область Заполнение

// Возвращает параметры заполнения по типу назначения
// 
// Возвращаемое значение:
//  Структура - с ключами:
// * ИмяЗаполняемогоПоля - Строка                            - имя заполняемого поля
// * ИмяПоляТЧ           - Строка, Неопределено              - источник значение заполнения, приоритетнее чем ЗначениеПоля
// * ЗначениеПоля        - Произвольный                      - значение заполнения
// * ТипНазначения       - ПеречислениеСсылка.ТипыНазначений - тип назначения, по которому заполняется указанное поле указанным значением
//
Функция ПараметрыЗаполнения() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("ИмяЗаполняемогоПоля");
	Параметры.Вставить("ИмяПоляТЧ");
	Параметры.Вставить("ЗначениеПоля");
	Параметры.Вставить("ТипНазначения");
	
	Возврат Параметры;
	
КонецФункции

// Предназначена для использования в формах документах в обработчиках событий, которые приводят к изменению назначения в
// строке табличной части документа. Добавляет в структуру действий действие, которое отрабатывается механизмами
// обработки табличных частей, см. ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ Выполнение такого
// действия актуализирует назначение в строке табличной части документа, в соответствии с изменившимся направлением деятельности.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма в которой необходимо выполнить действие по обработки строки табличной части.
//  СтруктураДействий - Структура - Структура действий, используемая механизмами обработки табличных частей (см.
//                                  процедуру ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ).
//  Устарело - Булево - Для совместимости с НаправленияДеятельностиКлиентСервер.СтруктураДействийВставитьПриДобавленииСтроки
Процедура СтруктураДействийВставитьПриДобавленииСтроки(Форма, СтруктураДействий, Устарело = Ложь) Экспорт
	
	Если Устарело Тогда
		ПорядокОбработкиДокумента = Форма.МетаданныеФормы;
		Кэш = Форма.НаправленияДеятельностиКэшированныеЗначения;
	Иначе
		ПорядокОбработкиДокумента = Форма.НазначенияКэшированныеЗначения.ПорядокОбработкиДокумента;
		Кэш = Форма.НазначенияКэшированныеЗначения;
	КонецЕсли;
	
	ИмяРеквизитаПоЗаказу = ПорядокОбработкиДокумента.ИмяРеквизитаПоЗаказу;
	ИмяРеквизитаОбособленно = ПорядокОбработкиДокумента.Служебные.ИмяРеквизитаОбособленно;

	НазначениеПоУмолчанию = Неопределено;
	Если ИмяРеквизитаПоЗаказу = Неопределено
		Или Не ЗначениеЗаполнено(Форма.Объект[ИмяРеквизитаПоЗаказу])
		Или Не Форма.Объект[ИмяРеквизитаПоЗаказу] Тогда
			НазначениеПоУмолчанию = Кэш.НазначениеПоУмолчанию;
	КонецЕсли;
		
	СтруктураДействий.Вставить("ПроверитьЗаполнитьНазначение", НазначениеПоУмолчанию);
	
	Если ИмяРеквизитаОбособленно <> Неопределено И ЗначениеЗаполнено(НазначениеПоУмолчанию) Тогда
		СтруктураДействий.Вставить("ЗаполнитьФлагОбособленноПоНазначению", ИмяРеквизитаОбособленно);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет невидимое поле Назначение в документах исходя из заполненного поля Обособленно.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма в которой необходимо выполнить действие по обработки строки табличной части.
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка в которой нужно заполнить назначение исходя из флага Обособленно.
//  Устарело - Булево - Для совместимости с НаправленияДеятельностиКлиентСервер.ЗаполнитьНазначениеПоФлагуОбособленно
	Процедура ЗаполнитьНазначениеПоФлагуОбособленно(Форма, ТекущаяСтрока, Устарело = Ложь) Экспорт
	
	Если Устарело Тогда
		НазначениеПоУмолчанию = Форма.НаправленияДеятельностиКэшированныеЗначения.НазначениеПоУмолчанию;
	Иначе
		НазначениеПоУмолчанию = Форма.НазначенияКэшированныеЗначения.НазначениеПоУмолчанию;
	КонецЕсли;
	Если ТекущаяСтрока.Обособленно Тогда
		
		ТекущаяСтрока.Назначение = НазначениеПоУмолчанию;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти