#Область СлужебныйПрограммныйИнтерфейс

#Область ОткрытиеФорм

// Открывает форму сравнения или просмотра версии
// 
// Параметры:
// 	ПараметрыСравненияИПросмотра - Структура - см. НовыйПараметрыОткрытияФормы
Процедура ОткрытьФормуСравненияИПросмотраВерсий(ПараметрыСравненияИПросмотра) Экспорт
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("ПредставлениеДокумента" , ПараметрыСравненияИПросмотра.ПредставлениеДокумента);
	ПараметрыОткрытияФормы.Вставить("РежимПросмотра"         , ПараметрыСравненияИПросмотра.РежимПросмотра);
	ПараметрыОткрытияФормы.Вставить("ТипДокумента"           , ПараметрыСравненияИПросмотра.ТипДокумента);
	ПараметрыОткрытияФормы.Вставить("Документ"               , ПараметрыСравненияИПросмотра.Документ);
	ПараметрыОткрытияФормы.Вставить("ДокументСсылка"         , ПараметрыСравненияИПросмотра.ДокументСсылка);
	ПараметрыОткрытияФормы.Вставить("Версия"                 , ПараметрыСравненияИПросмотра.Версия);
	ПараметрыОткрытияФормы.Вставить("ВерсияДляСравнения"     , ПараметрыСравненияИПросмотра.ВерсияДляСравнения);
	ПараметрыОткрытияФормы.Вставить("Организация"            , ПараметрыСравненияИПросмотра.Организация);
	
	ОткрытьФорму("Обработка.РаботаСВерсиямиEDI.Форма.СравнениеИПросмотрВерсий", ПараметрыОткрытияФормы, 
		ПараметрыСравненияИПросмотра.ФормаВладелец, ПараметрыСравненияИПросмотра.Документ, , ,
		ПараметрыСравненияИПросмотра.ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Открывает форму выбора версии документа сервиса
// 
// Параметры:
// 	ПараметрыВыбораВерсииДокумента - Структура - см. РаботаСВерсиямиEDIКлиент.НовыйПараметрыВыбораВерсииДокумента()
Процедура ОткрытьФормуВыбораВерсии(ПараметрыВыбораВерсииДокумента) Экспорт
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("ТипДокумента"       ,               ПараметрыВыбораВерсииДокумента.ТипДокумента);
	ПараметрыОткрытияФормы.Вставить("Документ"           ,               ПараметрыВыбораВерсииДокумента.Документ);
	ПараметрыОткрытияФормы.Вставить("Заголовок"          ,               ПараметрыВыбораВерсииДокумента.Заголовок);
	ПараметрыОткрытияФормы.Вставить("Версия"             ,               ПараметрыВыбораВерсииДокумента.Версия);
	ПараметрыОткрытияФормы.Вставить("ВерсияДляСравнения" ,               ПараметрыВыбораВерсииДокумента.ВерсияДляСравнения);
	ПараметрыОткрытияФормы.Вставить("АктуальнаяВерсия" ,                 ПараметрыВыбораВерсииДокумента.АктуальнаяВерсия);
	ПараметрыОткрытияФормы.Вставить("ПрисланнаяНаСогласованиеВерсия" ,   ПараметрыВыбораВерсииДокумента.ПрисланнаяНаСогласованиеВерсия);
	ПараметрыОткрытияФормы.Вставить("СостояниеРедактированияДокумента" , ПараметрыВыбораВерсииДокумента.СостояниеРедактированияДокумента);
	ПараметрыОткрытияФормы.Вставить("Организация" ,                      ПараметрыВыбораВерсииДокумента.Организация);
	
	ОткрытьФорму("Обработка.РаботаСВерсиямиEDI.Форма.ВыборВерсииДокумента", ПараметрыОткрытияФормы, 
		ПараметрыВыбораВерсииДокумента.ФормаВладелец, ПараметрыВыбораВерсииДокумента.Документ, , ,
		ПараметрыВыбораВерсииДокумента.ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область ДействияИзОтчетаПросмотрСравнениеВерсий

// Выполняет обработчик команды формы сравнения версий
// 
// Параметры:
// 	Параметры - Структура - см. РаботаСВерсиямиEDIКлиент.НовыйПараметрыКомандыДоступногоДействия
Процедура ВыполнитьКомандуОтчетаПросмотраСравнения(Параметры) Экспорт
	
	РаботаСВерсиямиИнтеграцияEDIКлиент.ВыполнитьКомандуОтчетаПросмотраСравнения(Параметры);
	
КонецПроцедуры

#КонецОбласти

#Область ПараметрыОткрытияФорм

// Конструктор параметров для открытия формы просмотра версии
// 
// Параметры:
// 	ФормаВладелец - ФормаКлиентскогоПриложения - форма документа, просмотр версии которого требуется запустить
// Возвращаемое значение:
// 	Структура - см. НовыйПараметрыОткрытияФормы
//
Функция НовыйПараметрыПросмотра(ФормаВладелец = Неопределено) Экспорт
	
	ПараметрыПросмотра = НовыйПараметрыОткрытияФормы(ФормаВладелец);
	ПараметрыПросмотра.РежимПросмотра = РаботаСВерсиямиEDIКлиентСервер.РежимПросмотраПросмотр();
	
	Возврат ПараметрыПросмотра;
	
КонецФункции

// Конструктор параметров для открытия формы сравнения версий
// 
// Параметры:
// 	ФормаВладелец - ФормаКлиентскогоПриложения - форма документа, сравнение версий которого требуется запустить
// Возвращаемое значение:
// 	Структура - см. НовыйПараметрыОткрытияФормы
//
Функция НовыйПараметрыСравнения(ФормаВладелец = Неопределено) Экспорт
	
	ПараметрыПросмотра = НовыйПараметрыОткрытияФормы(ФормаВладелец);
	ПараметрыПросмотра.РежимПросмотра = РаботаСВерсиямиEDIКлиентСервер.РежимПросмотраСравнение();
	
	Возврат ПараметрыПросмотра;
	
КонецФункции

// Конструктор параметров для открытия формы выбора версии документа
// 
// Параметры:
// 	ФормаВладелец - ФормаКлиентскогоПриложения - форма документа, для которого открывается выбор версии
// Возвращаемое значение:
// 	Структура - Описание:
// * Организация                      - ОпределяемыйТип.ОрганизацияEDI - организация документа
// * СостояниеРедактированияДокумента - Строка                         - идентификатор состояния редактирования документа:
//     см. РаботаСВерсиямиEDIКлиентСервер.СостояниеДокументаНеРедактируется()
//     см. РаботаСВерсиямиEDIКлиентСервер.СостояниеДокументаРедактируется()
//     см. РаботаСВерсиямиEDIКлиентСервер.СостояниеДокументаРассмотрениеПрисланнойВерсии()
// * Версия                           - Строка - идентификатор версии сервиса
// * ВерсияДляСравнения               - Строка - идентификатор версии сервиса для сравнения
// * ПрисланнаяНаСогласованиеВерсия   - Строка - идентификатор, присланной на согласование, версии сервиса
// * АктуальнаяВерсия                 - Строка - идентификатор актуальной версии сервиса
// * ДокументСсылка                   - ОпределяемыйТип.ПрикладнойДокументОбъектEDI - документ информационной базы
// * Документ                         - Строка - идентификатор документа сервиса
// * ТипДокумента                     - ПеречислениеСсылка.ТипыДокументовEDI - тип документа сервиса
// * Заголовок                        - Строка - текст заголовка формы сравнения
// * ОписаниеОповещения               - ОписаниеОповещения, Неопределено - обработчик оповещения при закрытии формы
Функция НовыйПараметрыВыбораВерсииДокумента(ФормаВладелец) Экспорт 
	
	ПараметрыВыбора = Новый Структура;
	
	ПараметрыВыбора.Вставить("ФормаВладелец"                    , ФормаВладелец);
	ПараметрыВыбора.Вставить("ОписаниеОповещения"               , Неопределено);
	ПараметрыВыбора.Вставить("Заголовок"                        , "");
	ПараметрыВыбора.Вставить("ТипДокумента"                     , "");
	ПараметрыВыбора.Вставить("Документ"                         , "");
	ПараметрыВыбора.Вставить("ДокументСсылка"                   , Неопределено);
	ПараметрыВыбора.Вставить("АктуальнаяВерсия"                 , "");
	ПараметрыВыбора.Вставить("ПрисланнаяНаСогласованиеВерсия"   , "");
	ПараметрыВыбора.Вставить("Версия"                           , "");
	ПараметрыВыбора.Вставить("ВерсияДляСравнения"               , "");
	ПараметрыВыбора.Вставить("СостояниеРедактированияДокумента" , "");
	ПараметрыВыбора.Вставить("Организация"                      , Неопределено);
	
	Возврат ПараметрыВыбора;
	
КонецФункции

// Конструктор параметров для выполнения действия сравнения версий
// 
// Возвращаемое значение:
// 	Структура - Описание:
// * ФормаОтчета                    - ФормаКлиентскогоПриложения - форма сравнения версий.
// * ПрисланнаяНаСогласованиеВерсия - Строка - идентификатор, присланной на согласование, версии сервиса
// * АктуальнаяВерсия               - Строка - идентификатор актуальной версии сервиса.
// * Версия                         - Строка - идентификатор версии сервиса.
// * ВерсияДляСравнения             - Строка - идентификатор версии сервиса для сравнения.
// * ТипДокумента                   - ПеречислениеСсылка.ТипыДокументовEDI - тип документа сервиса.
// * Документ                       - Строка - идентификатор документа сервиса.
// * ВыполняемаяКоманда             - ПеречислениеСсылка.ДоступныеКомандыОтчетаСравненияВерсийEDI - ссылка на команду
//                                                                                                  сервиса.
//
Функция НовыйПараметрыКомандыДоступногоДействия() Экспорт
	
	ПараметрыДоступногоДействия = Новый Структура;
	ПараметрыДоступногоДействия.Вставить("ВыполняемаяКоманда",             ПредопределенноеЗначение("Перечисление.ДоступныеКомандыОтчетаСравненияВерсийEDI.ПустаяСсылка"));
	ПараметрыДоступногоДействия.Вставить("Документ",                       "");
	ПараметрыДоступногоДействия.Вставить("ТипДокумента",                   Неопределено);
	ПараметрыДоступногоДействия.Вставить("Версия",                         "");
	ПараметрыДоступногоДействия.Вставить("ВерсияДляСравнения",             "");
	ПараметрыДоступногоДействия.Вставить("АктуальнаяВерсия",               "");
	ПараметрыДоступногоДействия.Вставить("ПрисланнаяНаСогласованиеВерсия", "");
	ПараметрыДоступногоДействия.Вставить("ФормаОтчета",                    Неопределено);
	
	Возврат ПараметрыДоступногоДействия;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Конструктор параметров для открытия формы сравнения и просмотра версий
// 
// Параметры:
// 	ФормаВладелец - ФормаКлиентскогоПриложения - форма документа, сравнение версий которого требуется запустить
// Возвращаемое значение:
// 	Структура - Описание:
// * ПредставлениеДокумента - Строка - пользовательское представление документа информационной базы
// * Версия                           - Строка - идентификатор версии сервиса
// * ВерсияДляСравнения               - Строка - идентификатор версии сервиса для сравнения
// * ДокументСсылка                   - ОпределяемыйТип.ПрикладнойДокументОбъектEDI - документ информационной базы
// * Документ                         - Строка - идентификатор документа сервиса
// * ТипДокумента                     - ПеречислениеСсылка.ТипыДокументовEDI - тип документа сервиса
// * РежимПросмотра                   - Строка - идентификатор режима просмотра, один из:
//                                               - см. РаботаСВерсиямиEDIКлиентСервер.РежимПросмотраПросмотр
//                                               - см. РаботаСВерсиямиEDIКлиентСервер.РежимПросмотраСравнение
// * ОписаниеОповещения     - ОписаниеОповещения - обработчик оповещения закрытия формы сравнения и просмотра
//
Функция НовыйПараметрыОткрытияФормы(ФормаВладелец)
	
	ПараметрыОткрытияФормы = Новый Структура;
	
	ПараметрыОткрытияФормы.Вставить("ФормаВладелец"          , ФормаВладелец);
	ПараметрыОткрытияФормы.Вставить("ОписаниеОповещения"     , Неопределено);
	ПараметрыОткрытияФормы.Вставить("РежимПросмотра"         , "");
	ПараметрыОткрытияФормы.Вставить("ТипДокумента"           , "");
	ПараметрыОткрытияФормы.Вставить("Документ"               , Неопределено);
	ПараметрыОткрытияФормы.Вставить("ДокументСсылка"         , Неопределено);
	ПараметрыОткрытияФормы.Вставить("Версия"                 , "");
	ПараметрыОткрытияФормы.Вставить("ВерсияДляСравнения"     , "");
	ПараметрыОткрытияФормы.Вставить("ПредставлениеДокумента" , "");
	ПараметрыОткрытияФормы.Вставить("Организация"            , Неопределено);
	
	Возврат ПараметрыОткрытияФормы;
	
КонецФункции

#КонецОбласти