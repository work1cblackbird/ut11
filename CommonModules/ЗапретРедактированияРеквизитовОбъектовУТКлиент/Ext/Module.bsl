
#Область ПрограммныйИнтерфейс

Процедура ПроверитьИспользованиеОбъекта(Форма, ПараметрыОбработчикаОжидания, ИспользоватьЛокальнуюФункциюПроверки = Ложь) Экспорт
	
	ЕстьСсылки = Неопределено;
	Результат = ЗапретРедактированияРеквизитовОбъектовУТВызовСервера.ПроверитьИспользованиеОбъекта(
		Форма.Параметры.Объект,
		Форма.УникальныйИдентификатор,
		Форма.АдресХранилища,
		ЕстьСсылки,
		ИспользоватьЛокальнуюФункциюПроверки);
	
	Если НЕ Результат.ЗаданиеВыполнено Тогда
		Форма.ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		Форма.АдресХранилища       = Результат.АдресХранилища;
		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПараметрыОбработчикаОжидания.КоэффициентУвеличенияИнтервала = 1.2; // Уменьшим шаг увеличения времени опроса выполнения задания
		Форма.ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(Форма, Форма.ИдентификаторЗадания);
		ПараметрыОбработчикаОжидания.Вставить("ФормаДлительнойОперации", ФормаДлительнойОперации);
	Иначе
		ЗапретРедактированияРеквизитовОбъектовУТКлиентСервер.ПолучитьРезультатПроверки(Форма, ЕстьСсылки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьВыполнениеЗадания(Форма, ФормаДлительнойОперации, ПараметрыОбработчикаОжидания) Экспорт
	
	Попытка
		ФормаДлительнойОперации = ПараметрыОбработчикаОжидания.ФормаДлительнойОперации;
		Если ФормаДлительнойОперации.Открыта()
			И ФормаДлительнойОперации.ИдентификаторЗадания = Форма.ИдентификаторЗадания Тогда
			Если ЗапретРедактированияРеквизитовОбъектовУТВызовСервера.ЗаданиеВыполнено(Форма.ИдентификаторЗадания) Тогда
				ЗапретРедактированияРеквизитовОбъектовУТКлиентСервер.ПолучитьРезультатПроверки(Форма);
				ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
			Иначе
				ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
				Форма.ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
			КонецЕсли;
		КонецЕсли;
	Исключение
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти