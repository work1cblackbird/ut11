#Область ПрограммныйИнтерфейс

// Получает контрагента по умолчанию
//
// Параметры:
//  Партнер - СправочникСсылка.Партнеры - партнер для которого необходимо получить контрагента.
//
// Возвращаемое значение:
//   СправочникСсылка.Контрагенты   - контрагент партнера по умолчанию.
//
Функция ПолучитьКонтрагентаПартнераПоУмолчанию(Партнер) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
		
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	Контрагенты.Ссылка КАК Контрагент
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.Партнер = &Партнер";
		
	Иначе
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИерархияПартнеров.Родитель КАК Партнер,
		|	Контрагенты.Ссылка КАК Контрагент,
		|	ИерархияПартнеров.Уровень
		|ПОМЕСТИТЬ ДоступныеКонтрагентыПартнера
		|ИЗ
		|	РегистрСведений.ИерархияПартнеров КАК ИерархияПартнеров
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО ИерархияПартнеров.Родитель = Контрагенты.Партнер
		|ГДЕ
		|	ИерархияПартнеров.Партнер = &Партнер
		|	И (НЕ Контрагенты.ПометкаУдаления)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДоступныеКонтрагентыПартнера.Уровень КАК Уровень,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДоступныеКонтрагентыПартнера.Контрагент) КАК КоличествоНаУровне
		|ПОМЕСТИТЬ БлижнийУровень
		|ИЗ
		|	ДоступныеКонтрагентыПартнера КАК ДоступныеКонтрагентыПартнера
		|
		|СГРУППИРОВАТЬ ПО
		|	ДоступныеКонтрагентыПартнера.Уровень
		|
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДоступныеКонтрагентыПартнера.Контрагент) = 1
		|
		|УПОРЯДОЧИТЬ ПО
		|	Уровень УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДоступныеКонтрагентыПартнера.Уровень КАК Уровень,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДоступныеКонтрагентыПартнера.Контрагент) КАК КоличествоНаУровне
		|ПОМЕСТИТЬ УровеньСЧисломКонтрагентовБольшеОдного
		|ИЗ
		|	ДоступныеКонтрагентыПартнера КАК ДоступныеКонтрагентыПартнера
		|
		|СГРУППИРОВАТЬ ПО
		|	ДоступныеКонтрагентыПартнера.Уровень
		|
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДоступныеКонтрагентыПартнера.Контрагент) > 1
		|
		|УПОРЯДОЧИТЬ ПО
		|	Уровень УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(БлижнийУровень.Уровень, 0) < ЕСТЬNULL(УровеньСЧисломКонтрагентовБольшеОдного.Уровень, 0)
		|			ТОГДА NULL
		|		ИНАЧЕ БлижнийУровень.Уровень
		|	КОНЕЦ КАК Уровень
		|ПОМЕСТИТЬ ИскомыйУровень
		|ИЗ
		|	БлижнийУровень КАК БлижнийУровень
		|		ПОЛНОЕ СОЕДИНЕНИЕ УровеньСЧисломКонтрагентовБольшеОдного КАК УровеньСЧисломКонтрагентовБольшеОдного
		|		ПО (ИСТИНА)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДоступныеКонтрагентыПартнера.Контрагент
		|ИЗ
		|	ДоступныеКонтрагентыПартнера КАК ДоступныеКонтрагентыПартнера
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИскомыйУровень КАК ИскомыйУровень
		|		ПО ДоступныеКонтрагентыПартнера.Уровень = ИскомыйУровень.Уровень";
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Партнер", Партнер);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Результат = Выборка.Контрагент;
		
	Иначе
		
		Результат = Справочники.Контрагенты.ПустаяСсылка();
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Заполняет контрагента по партнеру.
//
// Параметры:
//   Партнер                  - СправочникСсылка.Партнеры - Партнер, контрагента которого нужно заполнить.
//   Контрагент               - СправочникСсылка.Контрагенты -Контрагент которого нужно заполнить.
//   ПерезаполнятьКонтрагента - Булево -Признак необходимости перезаполнять контрагента.
//
Процедура ЗаполнитьКонтрагентаПартнераПоУмолчанию(Знач Партнер, Контрагент, ПерезаполнятьКонтрагента = Ложь) Экспорт
	
	Если ЗначениеЗаполнено(Партнер)
		И (ПерезаполнятьКонтрагента Или Не ЗначениеЗаполнено (Контрагент)) Тогда
		
		Если ЗначениеЗаполнено(Контрагент) Тогда
			Если КонтрагентДоступенДляВыбораДляПартнера(Партнер, Контрагент) Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		КонтрагентПоУмолчанию = ПолучитьКонтрагентаПартнераПоУмолчанию(Партнер);
		
		Если КонтрагентПоУмолчанию <> Неопределено Тогда
			Контрагент = КонтрагентПоУмолчанию;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Проверяет, доступен ли контрагент для выбора в документы партнера.
//
// Параметры:
//   Партнер                  - СправочникСсылка.Партнеры - Партнер, контрагента которого нужно проверить.
//   Контрагент               - СправочникСсылка.Контрагенты -проверяемый контрагент.
//
// Возвращаемое значение:
//   Булево - Истина, если доступен, Ложь в обратном случае.
//
Функция КонтрагентДоступенДляВыбораДляПартнера(Партнер, Контрагент) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
	|	Контрагенты.Ссылка КАК Контрагент
	|ИЗ
	|	РегистрСведений.ИерархияПартнеров КАК ИерархияПартнеров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО ИерархияПартнеров.Родитель = Контрагенты.Партнер
	|ГДЕ
	|	ИерархияПартнеров.Партнер = &Партнер
	|	И Контрагенты.Ссылка = &Контрагент";
	
	Запрос.УстановитьПараметр("Партнер", Партнер);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

// Проверяет, является ли партнер, родителем для других партнеров.
//
// Параметры:
//  Партнер - СправочникСсылка.Партнеры - партнер, для которого выполняется проверка.
//
// Возвращаемое значение:
//   Булево - Истина, если является, Ложь в обратном случае.
//
Функция ЕстьПодчиненныеПартнеры(Партнер) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Партнеры.Ссылка
	|ИЗ
	|	Справочник.Партнеры КАК Партнеры
	|ГДЕ
	|	Партнеры.Родитель = &Партнер
	|");
	Запрос.УстановитьПараметр("Партнер",Партнер);
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

// Обновляет индекс полнотекстового поиска
Процедура ОбновитьИндексПолнотекстовогоПоиска() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ПолнотекстовыйПоиск.ОбновитьИндекс();
	
КонецПроцедуры

// Получает массив доступных для выбора партнером контрагентов.
//
// Параметры:
//  Партнер - СправочникСсылка.Партнеры - партнер для которого необходимо получить контрагентов.
//
// Возвращаемое значение:
//  Массив -  массив содержащий всех доступных для партнера контрагентов.
// 
Функция ПолучитьВсехКонтрагентовПартнера(Партнер) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Контрагенты.Ссылка КАК Контрагент
	|ИЗ
	|	РегистрСведений.ИерархияПартнеров КАК ИерархияПартнеров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО ИерархияПартнеров.Родитель = Контрагенты.Партнер
	|ГДЕ
	|	ИерархияПартнеров.Партнер = &Партнер";
	
	Запрос.УстановитьПараметр("Партнер", Партнер);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Новый Массив;
	Иначе
		Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Контрагент");
	КонецЕсли;
	
КонецФункции

// Получает массив нижестоящих партнеров.
//
// Параметры:
//  Партнер  -  СправочникСсылка.Партнеры - партнер, для которого необходимо получить контрагентов.
//
// Возвращаемое значение:
//  Массив -  содержит всех нижестоящих партнеров и самого партнера.
//
Функция ПолучитьНижестоящихПартнеров(Партнер) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ИерархияПартнеров.Партнер
	|ИЗ
	|	РегистрСведений.ИерархияПартнеров КАК ИерархияПартнеров
	|ГДЕ
	|	ИерархияПартнеров.Родитель = &Партнер";
	
	Запрос.УстановитьПараметр("Партнер",Партнер);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Новый Массив;
	Иначе
		Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Партнер");
	КонецЕсли;
	
КонецФункции

// Получает список родителей партнера снизу вверх по иерархии.
//
// Параметры:
//  Партнер  - СправочникСсылка.Партнеры - партнер, для которого формируется список.
//  СписокПартнераСРодителями  - СписокЗначений - заполняемый список родителей партнера.
//
Процедура ЗаполнитьСписокПартнераСРодителями(Партнер,СписокПартнераСРодителями) Экспорт
	
	СписокПартнераСРодителями.Очистить();
	
	Если Не ЗначениеЗаполнено(Партнер) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Получить родителей партнера
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Партнеры.Ссылка КАК Ссылка,
	|	Партнеры.Наименование
	|ИЗ
	|	Справочник.Партнеры КАК Партнеры
	|ГДЕ
	|	Партнеры.Ссылка = &Партнер
	|ИТОГИ ПО
	|	Ссылка ИЕРАРХИЯ
	|");
	Запрос.УстановитьПараметр("Партнер",Партнер);
	
	ДеревоИерархии = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	ЗаполнитьСписокПартнеровИзДерева(ДеревоИерархии.Строки, СписокПартнераСРодителями,Партнер);
	
КонецПроцедуры

// Получает список всех родительских и дочерних элементов партнера в иерархии.
//
// Параметры:
//  Партнер                   - СправочникСсылка.Партнеры - партнер, для которого формируется список.
//  СписокПартнераСРодителями - СписокЗначений - список значений, который включает самого партнера и все родителей и дочерние элементы в иерархии
//
Процедура ЗаполнитьСписокПартнераСоВсехИерархией(Партнер, СписокПартнераСРодителями) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР
	|		КОГДА ИерархияПартнеров.Партнер = &Партнер
	|			ТОГДА ИерархияПартнеров.Родитель
	|		ИНАЧЕ ИерархияПартнеров.Партнер
	|	КОНЕЦ КАК Партнер,
	|	ВЫБОР
	|		КОГДА ИерархияПартнеров.Партнер = &Партнер
	|			ТОГДА ПРЕДСТАВЛЕНИЕ(ИерархияПартнеров.Родитель)
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ИерархияПартнеров.Партнер)
	|	КОНЕЦ КАК Представление
	|ИЗ
	|	РегистрСведений.ИерархияПартнеров КАК ИерархияПартнеров
	|ГДЕ
	|	(ИерархияПартнеров.Партнер = &Партнер
	|			ИЛИ ИерархияПартнеров.Родитель = &Партнер)";
	
	Запрос.УстановитьПараметр("Партнер", Партнер);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокПартнераСРодителями.Добавить(Выборка.Партнер, Выборка.Представление);
	КонецЦикла;
	
	Возврат;
	
КонецПроцедуры

// По переданному значению перечисления ЮрФизЛицо определяет, является ли оно признаком ЮрЛица.
//
// Параметры:
//  ЮрФизЛицо  - ПеречислениеСсылка.ЮрФизЛицо - переданное значение.
//
// Возвращаемое значение:
//  Булево, Неопределено - Истина, если юридическое лицо, Ложь если нет, Неопределено, если значение ЮрФизЛицо не задано.
//
Функция ЭтоЮрЛицо(ЮрФизЛицо) Экспорт
	
	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо ИЛИ ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент Тогда
		Возврат Истина;
	ИначеЕсли ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо ИЛИ ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель Тогда
		Возврат Ложь;
	ИначеЕсли ТипЗнч(ЮрФизЛицо) = Тип("ПеречислениеСсылка.ЮридическоеФизическоеЛицо") Тогда
	 	Возврат ЮрФизЛицо <> Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Определяет доступен ли для пользователя упрощенный ввод партнеров и контрагентов.
//
// Возвращаемое значение:
//   Булево   - Истина, если используется, и ложь в обратном случае.
//
Функция УпрощенныйВводДоступен() Экспорт
	
	Возврат ПравоДоступа("Добавление", Метаданные.Справочники.Партнеры) И ПраваПользователяПовтИсп.ВводИнформацииПоПартнеруБезКонтроля();
	
КонецФункции

// Определяет, есть ли в базе контрагент с таким же набором РегистрационныйНомер/СтранаРегистрации.
//
// Параметры:
//  РегистрационныйНомер   - Строка - Регистрационный номер контрагента для поиска в базе.
//  СтранаРегистрации      - СправочникСсылка.СтраныМира - страна регистрации контрагента для поиска в базе.
//  ИсключаяСсылку         - СправочникСсылка.Контрагенты - контрагент, который исключается при поиске.
//
// Возвращаемое значение:
//   Булево   - Истина, если контрагент с таким регистрационным номером и страной регистрации уже существует в базе.
//
Функция СтранаРегистрационныйНомерУжеИспользуетсяВИнформационнойБазе(РегистрационныйНомер, СтранаРегистрации, ИсключаяСсылку = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	Контрагенты.Ссылка,
	|	Партнеры.Ссылка КАК Партнер,
	|	Партнеры.Код КАК Код,
	|	ПРЕДСТАВЛЕНИЕ(Партнеры.ОсновнойМенеджер) КАК ОсновнойМенеджер
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
	|		ПО Контрагенты.Партнер = Партнеры.Ссылка
	|ГДЕ
	|	Контрагенты.РегистрационныйНомер = &РегистрационныйНомер
	|	И Контрагенты.СтранаРегистрации = &СтранаРегистрации
	|	И Контрагенты.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("РегистрационныйНомер",РегистрационныйНомер);
	Запрос.УстановитьПараметр("СтранаРегистрации",СтранаРегистрации);
	Запрос.УстановитьПараметр("Ссылка",ИсключаяСсылку);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Удаляет из настроек и пользовательских настроек элемент отбора.
//
// Параметры:
//  КомпоновщикНастроек   - КомпоновщикНастроекКомпоновкиДанных - 
//  ПредставлениеЭлемента - Строка - представление элемента, который будет удален.
//
Процедура УдалитьЭлементИзНастроекОтборовОтчета(КомпоновщикНастроек, ПредставлениеЭлемента) Экспорт

	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
		
		ЭлементыОтбора = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(КомпоновщикНастроек.Настройки.Отбор, ПредставлениеЭлемента);
		Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл
			ИдентификаторПользовательскойНастройки = ЭлементОтбора.ИдентификаторПользовательскойНастройки;
			КомпоновщикНастроек.Настройки.Отбор.Элементы.Удалить(ЭлементОтбора);
			
			ЭлементПользовательскихНастроек = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ЭлементОтбора.ИдентификаторПользовательскойНастройки);
			Если ЭлементПользовательскихНастроек <> Неопределено Тогда
				КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Удалить(ИдентификаторПользовательскойНастройки);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

// Заполняет контактное лицо по партнеру
//
// Параметры:
// Партнер - СправочникСсылка.Партнеры - Партнер, контактное лицо которого нужно заполнить
// КонтактноеЛицо - СправочникСсылка.КонтактныеЛицаПартнеров -КонтактноеЛицо которое нужно заполнить.
//
Процедура ЗаполнитьКонтактноеЛицоПартнераПоУмолчанию(Знач Партнер, КонтактноеЛицо) Экспорт
	
	Если Не ЗначениеЗаполнено(Партнер) Тогда
		КонтактноеЛицо = Справочники.КонтактныеЛицаПартнеров.ПустаяСсылка();
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КонтактноеЛицо) И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КонтактноеЛицо, "Владелец") <> Партнер Тогда
		КонтактноеЛицо = Справочники.КонтактныеЛицаПартнеров.ПустаяСсылка();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КонтактноеЛицо) Тогда
		
		КонтактноеЛицоПоУмолчанию = ПолучитьКонтактноеЛицоПартнераПоУмолчанию(Партнер);
		
		Если КонтактноеЛицоПоУмолчанию <> Неопределено Тогда
			КонтактноеЛицо = КонтактноеЛицоПоУмолчанию;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Получает контактное лицо партнера контрагента по умолчанию.
//
// Параметры:
//  Контрагент  - СправочникСсылка.Контрагенты - контрагент, для партнера которого необходимо получить контактное лицо.
//
// Возвращаемое значение:
//   СправочникСсылка.КонтактныеЛицаПартнеров  - контактное лицо партнера контрагента по умолчанию.
//
Функция ПолучитьКонтактноеЛицоПартнераКонтрагентаПоУмолчанию(Контрагент) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Контрагенты.Партнер КАК Владелец
	|ПОМЕСТИТЬ ВладелецКонтактногоЛица
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Ссылка = &Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА ВложенныйЗапрос.Количество = 1
	|			ТОГДА КонтактныеЛицаПартнеров.Ссылка
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КонтактныеЛицаПартнеров.ПустаяСсылка)
	|	КОНЕЦ КАК КонтактноеЛицо
	|ИЗ
	|	(ВЫБРАТЬ
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ КонтактныеЛицаПартнеров.Ссылка) КАК Количество
	|	ИЗ
	|		Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
	|	ГДЕ
	|		КонтактныеЛицаПартнеров.Владелец В
	|				(ВЫБРАТЬ
	|					ВладелецКонтактногоЛица.Владелец КАК Владелец
	|				ИЗ
	|					ВладелецКонтактногоЛица)
	|		И НЕ КонтактныеЛицаПартнеров.ПометкаУдаления) КАК ВложенныйЗапрос,
	|	Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
	|ГДЕ
	|	КонтактныеЛицаПартнеров.Владелец В
	|			(ВЫБРАТЬ
	|				ВладелецКонтактногоЛица.Владелец КАК Владелец
	|			ИЗ
	|				ВладелецКонтактногоЛица)";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Справочники.КонтактныеЛицаПартнеров.ПустаяСсылка();
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.КонтактноеЛицо;
	
КонецФункции

// Получает контактное лицо партнера по умолчанию.
//
// Параметры:
//  Партнер - СправочникСсылка.Партнеры - партнер для которого необходимо получить контактное лицо.
//
// Возвращаемое значение:
//   СправочникСсылка.КонтактныеЛицаПартнеров - контактное лицо партнера по умолчанию.
//
Функция ПолучитьКонтактноеЛицоПартнераПоУмолчанию(Партнер) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА ВложенныйЗапрос.Количество = 1
	|			ТОГДА КонтактныеЛицаПартнеров.Ссылка
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КонтактныеЛицаПартнеров.ПустаяСсылка)
	|	КОНЕЦ КАК КонтактноеЛицо
	|ИЗ
	|	(ВЫБРАТЬ
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ КонтактныеЛицаПартнеров.Ссылка) КАК Количество
	|	ИЗ
	|		Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
	|	ГДЕ
	|		КонтактныеЛицаПартнеров.Владелец = &Партнер
	|		И НЕ КонтактныеЛицаПартнеров.ПометкаУдаления
	|		И (КонтактныеЛицаПартнеров.ДатаПрекращенияСвязи > &ТекущаяДата
	|			ИЛИ КонтактныеЛицаПартнеров.ДатаПрекращенияСвязи = &ПустаяДата)) КАК ВложенныйЗапрос,
	|	Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
	|ГДЕ
	|	КонтактныеЛицаПартнеров.Владелец = &Партнер
	|	И НЕ КонтактныеЛицаПартнеров.ПометкаУдаления
	|	И (КонтактныеЛицаПартнеров.ДатаПрекращенияСвязи > &ТекущаяДата
	|		ИЛИ КонтактныеЛицаПартнеров.ДатаПрекращенияСвязи = &ПустаяДата)";
	
	Запрос.УстановитьПараметр("Партнер", Партнер);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ПустаяДата", Дата(1,1,1));
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Справочники.КонтактныеЛицаПартнеров.ПустаяСсылка();
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.КонтактноеЛицо;
	
КонецФункции

Процедура УстановитьПодсказкуРасширенныйПоиск(ПояснениеРасширенныйПоиск) Экспорт

	Макет = Справочники.Партнеры.ПолучитьМакет("РасширенныйПоиск");
	
	Попытка
		
		ОбъектДокументHTML = Макет.ПолучитьДокументHTML();
		
		ТекстПояснения = ТекстHTMLИзОбъектаДокументHTML(ОбъектДокументHTML);
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
			ТекстПояснения = СтрЗаменить(ТекстПояснения, НСтр("ru = 'Партнеры'"), НСтр("ru = 'Контрагенты'"));
		КонецЕсли;
		
	Исключение
		
		ТекстПояснения = "";
	
	КонецПопытки;
	
	ПояснениеРасширенныйПоиск = ТекстПояснения;
	
	
КонецПроцедуры

#Область ОбособленныеПодразделения

// Для выбранного контрагента получает его обособленные подразделения.
//
// Параметры:
//  ГоловнойКонтрагент - СправочникСсылка.Контрагенты - контрагент, для которого нужно получить обособленные подразделения.
//
// Возвращаемое значение:
//   Массив - массив подчиненных контрагентов.
//
Функция ОбособленныеПодразделенияКонтрагента(ГоловнойКонтрагент) Экспорт
	
	МассивКонтрагентов = Новый Массив;
	
	Если НЕ ЗначениеЗаполнено(ГоловнойКонтрагент) Тогда
		Возврат МассивКонтрагентов;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГоловнойКонтрагент", ГоловнойКонтрагент);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Контрагенты.Ссылка КАК Ссылка,
	|	Контрагенты.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Ссылка <> &ГоловнойКонтрагент
	|	И Контрагенты.ГоловнойКонтрагент = &ГоловнойКонтрагент
	|	И Контрагенты.ОбособленноеПодразделение
	|	И НЕ Контрагенты.ПометкаУдаления";
	
	Результат = Запрос.Выполнить();
	МассивКонтрагентов = Результат.Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат МассивКонтрагентов;
	
КонецФункции

// Устанавливает условное оформление поля ГоловнойКонтрагент для таблицы формы.
// Если контрагент является обособленным подразделением и головной не заполнен, то элемент выделяется.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма для которой устанавливается условное оформление, содержит:
//  * Элементы - ВсеЭлементыФормы - содержит:
//    ** ГоловнойКонтрагент - ПолеФормы -
//
Процедура УстановитьОформлениеГоловногоКонтрагентаВСписке(Форма) Экспорт

	УсловноеОформление = Форма.УсловноеОформление;
	Элементы = Форма.Элементы; //

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ГоловнойКонтрагент.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ОбособленноеПодразделение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ГоловнойКонтрагент");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.ПолеСОшибкойФон);
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ГоловнойКонтрагент.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ОбособленноеПодразделение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ГоловнойКонтрагент");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДоступноДобавлениеПартнеров");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", "Заполнить");
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ГиперссылкаЦвет);
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(, , , , Истина));

КонецПроцедуры

// Преобразует представление контактной информации во внутренний формат JSON.
//
// Для адресов, введенных в свободной форме, корректное преобразование не гарантируется.
//
//  Параметры:
//      Представление - Строка  - Строковое представление контактной информации, выводимое пользователю.
//      ЗначенияПолей - Строка - Контактная информация в виде JSON.
//      ВидКонтактнойИнформации - СправочникСсылка.ВидыКонтактнойИнформации, ПеречислениеСсылка.ТипыКонтактнойИнформации, Структура - Вид или тип контактной информации.
//
Процедура ЗначенияПолейКонтактнойИнформации(Знач Представление, ЗначенияПолей, Знач ВидКонтактнойИнформации) Экспорт
	
	// Создаем новый экземпляр по представлению
	ЗначенияПолей = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(Представление, ВидКонтактнойИнформации);
	
КонецПроцедуры

#КонецОбласти

#Область ИерархияПартнеров

// Выполняет запись в РС "Иерархия партнеров" для массива переданных партнеров или для всего справочника,
// если массив не передан в процедуру.
//
// Параметры:
//   МассивПартнеров - Массив из СправочникСсылка.Партнеры - содержит партнеров, для которых необходимо выполнить запись в РС "Иерархия партнеров".
//
Процедура ВыполнитьЗаписьИерархияПартнеров(МассивПартнеров = Неопределено) Экспорт
	
	Если МассивПартнеров = Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	Партнеры.Ссылка
		|ИЗ
		|	Справочник.Партнеры КАК Партнеры
		|ГДЕ
		|	Партнеры.Родитель = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ЗаписатьИерархиюПартнера(Выборка.Ссылка);
			
		КонецЦикла;
		
	Иначе
		
		Для каждого ЭлементМассива Из МассивПартнеров Цикл
			
			ЗаписатьИерархиюПартнера(ЭлементМассива);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Очищает РС "Иерархия партнеров"
Процедура ОчиститьРегистрСведенийИерархияПартнеров() Экспорт
	
	НаборЗаписей = РегистрыСведений.ИерархияПартнеров.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Выполняет запись в РС "Иерархия партнеров" для переданного в процедуру партнера.
//
// Параметры:
//  Ссылка - СправочникСсылка.Партнеры - ссылка на партнера для которого необходимо выполнить запись в РС "Иерархия партнеров".
//
Процедура ЗаписатьИерархиюПартнера(Ссылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Партнеры.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Партнеры КАК Партнеры
	|ГДЕ
	|	Партнеры.Ссылка В ИЕРАРХИИ(&Ссылка)
	|ИТОГИ ПО
	|	Ссылка ИЕРАРХИЯ";
	
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ДеревоИерархии = Результат.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	ЭлементТекущегоСправочника = ДеревоИерархии.Строки.Найти(Ссылка,"Ссылка",Истина);
	
	Если ЭлементТекущегоСправочника <> Неопределено Тогда
		
		ВыполнитьЗаписьВРегистрПоПартнеру(ЭлементТекущегоСправочника);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииРаботыСФормами

// Определяет доступность полнотекстового поиска в формах списка и выбора справочника партнеры.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, для которой определяется доступность, содержит:
//  * Элементы - ВсеЭлементыФормы - содержит:
//   ** Найти - ПолеФормы -
//
Процедура ДоступностьПолнотекстовыйПоискСписокПартнеры(Форма) Экспорт
	
	// Настроить видимость элементов
	Форма.РасширенныйПоиск = Ложь;
	
	ОбщегоНазначенияУТ.ИнициализироватьРеквизитыФормыДляПолнотекстовогоПоиска(Форма, "ИспользоватьПолнотекстовыйПоиск"); 
	
	Если Форма.ИспользоватьПолнотекстовыйПоиск Тогда
		
		СпискиВыбораКлиентСервер.Загрузить("ИсторияПоискаПартнеров", Форма.Элементы.СтрокаПоиска.СписокВыбора);
		
	Иначе
		
		Форма.Элементы.СтрокаПоиска.Видимость = Ложь;
		Форма.Элементы.Найти.Видимость        = Ложь;

	КонецЕсли;
	
КонецПроцедуры

// Изменяет запрос динамического списка партнеров и устанавливает отбор по типу отношений.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, для которой устанавливаются отборы, содержит:
//   * Параметры - Структура - параметры переданные в форму при создании, содержат:
//     ** Отбор - Структура - 
//     ** Б - Структура - 
//
Процедура УстановитьОтборыДинамическийСписокПартнеры(Форма) Экспорт
	
	СформироватьСписокУстановленныхОтборовДинамическийСписокПартнеры(Форма);
	
	Если Форма.СписокОтборПоТипуПартнера.Количество() > 0 Тогда
		
		УстанавливатьОтборПоТипуПартнераКакИЛИ = Ложь;
		
		Если Форма.Параметры.Свойство("УстанавливатьОтборПоТипуПартнераКакИЛИ") И Форма.Параметры.УстанавливатьОтборПоТипуПартнераКакИЛИ Тогда
			УстанавливатьОтборПоТипуПартнераКакИЛИ = Истина;
		КонецЕсли;
		
		Для каждого ЭлементСписка Из Форма.СписокОтборПоТипуПартнера Цикл
			Форма.Параметры.Отбор.Удалить(ЭлементСписка.Значение);
		КонецЦикла;
		
		Форма.Список.ТекстЗапроса =  Форма.Список.ТекстЗапроса + ДополнениеЗапросаДинамическогоСпискаПоПартнерам(Форма.СписокОтборПоТипуПартнера, УстанавливатьОтборПоТипуПартнераКакИЛИ); 
		
		МассивУсловийОтбора = Новый Массив;
		Для каждого ЭлементСписка Из Форма.СписокОтборПоТипуПартнера Цикл
			Если ЭлементСписка.Значение = "НашеПредприятие" Тогда
				МассивУсловийОтбора.Добавить(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Справочники.Партнеры.НашеПредприятие, "Наименование"));
			Иначе
				МассивУсловийОтбора.Добавить(ПредставлениеОтбораПоТипуПартнера(ЭлементСписка.Значение));
			КонецЕсли;
		КонецЦикла;
		Разделитель = " "  + ?(УстанавливатьОтборПоТипуПартнераКакИЛИ, НСтр("ru='или'"), НСтр("ru='и'")) + " ";
		УсловияОтбора = СтрСоединить(МассивУсловийОтбора, Разделитель);
		
		Форма.АвтоЗаголовок = Ложь;
		ШаблонЗаголовка = ?(Форма.ИспользоватьПартнеровКакКонтрагентов, НСтр("ru='Контрагенты (%1)'"), НСтр("ru='Партнеры (%1)'"));
		Форма.Заголовок = СтрШаблон(ШаблонЗаголовка, УсловияОтбора);
		
	ИначеЕсли Форма.ИспользоватьПартнеровКакКонтрагентов Тогда
		
		Форма.АвтоЗаголовок = Ложь;
		Форма.Заголовок =  НСтр("ru='Контрагенты'");
		
	КонецЕсли;
	
	Если Форма.ИспользоватьПартнеровКакКонтрагентов Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Форма.Список,
			?(Форма.ИмяФормы = "Справочник.Контрагенты.Форма.ФормаВыбораИспользуютсяТолькоПартнеры"
			   Или Форма.ИмяФормы = "Справочник.Контрагенты.Форма.ФормаВыбораИспользуютсяТолькоПартнерыБезПолнотекстовогоПоиска", 
			   "Партнер", "Ссылка"),
			Справочники.Партнеры.НеизвестныйПартнер,
			ВидСравненияКомпоновкиДанных.НеРавно);
		
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Менеджеры") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Форма.Менеджеры,
			"Недействителен", 
			Ложь, 
			ВидСравненияКомпоновкиДанных.Равно, 
			НСтр("ru = 'Только действительные пользователи'"), 
			Истина, 
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Форма.Менеджеры,
			"Служебный", 
			Ложь, 
			ВидСравненияКомпоновкиДанных.Равно, 
			НСтр("ru = 'Без служебных пользователей'"), 
			Истина, 
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПредставлениеОтбораПоТипуПартнера(ЗначениеОтбора)
	
	Если ЗначениеОтбора = "Клиент" Тогда
		Возврат НСтр("ru = 'Клиенты'");
	ИначеЕсли ЗначениеОтбора = "Поставщик" Тогда
		Возврат НСтр("ru = 'Поставщики'");
	ИначеЕсли ЗначениеОтбора = "Конкурент" Тогда
		Возврат НСтр("ru = 'Конкуренты'");
	ИначеЕсли ЗначениеОтбора = "ПрочиеОтношения" Тогда
		Возврат НСтр("ru = 'Прочие отношения'");
	ИначеЕсли ЗначениеОтбора = "ОбслуживаетсяТорговымиПредставителями" Тогда
		Возврат НСтр("ru = 'Обслуживается торговыми представителями'");
	ИначеЕсли ЗначениеОтбора = "Перевозчик" Тогда
		Возврат НСтр("ru = 'Перевозчики'");
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

// Выполняет полнотекстовый поиск партнеров в форме
//
// Параметры:
//  Форма   - ФормаКлиентскогоПриложения - форма, в которой выполняется поиск.
//
// Возвращаемое значение:
//   Строка   - Текст сообщения о ошибки, Неопределено, если поиск выполнен успешно.
//
Функция НайтиПартнеровПолнотекстовыйПоиск(Форма) Экспорт
	
	ТаблицаОснований = Форма.РеквизитФормыВЗначение("Основания");
	
	// получить результаты поиска
	ТекстОшибки =
	НайтиПартнеров(Форма.СтрокаПоиска, ТаблицаОснований);
	Если ТекстОшибки = Неопределено Тогда
		
		СпискиВыбораКлиентСервер.ОбновитьСписокВыбора(Форма.Элементы.СтрокаПоиска.СписокВыбора, Форма.СтрокаПоиска, 100);
		
		СпискиВыбораКлиентСервер.Сохранить("ИсторияПоискаПартнеров", Форма.Элементы.СтрокаПоиска.СписокВыбора);
		
		// вернуть таблицу оснований
		Форма.ЗначениеВРеквизитФормы(ТаблицаОснований, "Основания");
		
		Если НЕ Форма.РасширенныйПоиск Тогда
			Форма.РежимОтображенияДоПримененияПолнотекстовогоПоиска = Строка(Форма.Элементы.Список.Отображение);
		КонецЕсли;
		Форма.Элементы.Список.Отображение = ОтображениеТаблицы.Список;
		
		// установить отбор по списку найденных партнеров
		#Если НЕ ВнешнееСоединение Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.Список,
		                                                                   "ОтборПоПолнотекстовомуПоискуУстановлен",
		                                                                   Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.Список,
		                                                                   "ОтборПоПолнотекстовомуПоиску",
		                                                                   ТаблицаОснований.ВыгрузитьКолонку("Партнер"));
		#КонецЕсли
		Форма.Элементы.СтрокаПоиска.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
		Возврат Неопределено;
		
	КонецЕсли;
	
	#Если НЕ ВнешнееСоединение Тогда
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.Список,
		                                                               "ОтборПоПолнотекстовомуПоискуУстановлен",
		                                                               Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.Список,
		                                                               "ОтборПоПолнотекстовомуПоиску",
		                                                               Неопределено);
	#КонецЕсли
	
	Форма.Элементы.СтрокаПоиска.ЦветФона = ЦветаСтиля.ПолеСОшибкойФон;
	Возврат ТекстОшибки;
	
КонецФункции

// Устанавливает заголовок элемента формы "Партнер" в зависимости от хозяйственной операции.
//
// Параметры:
//  Форма  - ФормаКлиентскогоПриложения - Форма, в которой находится элемент, для которого устанавливается заголовок.
//  ИмяЭлементаФормыПартнер             - Строка - Имя элемента формы, для которого устанавливается заголовок.
//  ХозяйственнаяОперация               - ПеречислениеСсылка.ХозяйственныеОперации - хозяйственная операция, в зависимости от которой 
//                 устанавливается заголовок.
//
Процедура ЗаголовокЭлементаПартнерВЗависимостиОтХозяйственнойОперации(Форма, ИмяЭлементаФормыПартнер, ХозяйственнаяОперация) Экспорт
	
	ЗаголовокПартнер = ЗаголовокРеквизитаПартнерВЗависимостиОтХозяйственнойОперации(ХозяйственнаяОперация);
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ИмяЭлементаФормыПартнер, "Заголовок", ЗаголовокПартнер);
	
КонецПроцедуры

// Устанавливает заголовок реквизита формы партнер в зависимости от хозяйственной операции.
//
// Параметры:
//  ХозяйственнаяОперация  - ПеречислениеСсылка.ХозяйственныеОперации - операция, которая влияет на текст заголовка.
//
// Возвращаемое значение:
//   Строка   - сформированный заголовок.
//
Функция ЗаголовокРеквизитаПартнерВЗависимостиОтХозяйственнойОперации( ХозяйственнаяОперация) Экспорт
	
	ОперацииЗакупки = ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика);
	ОперацииИмпорта = ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту);
	ОперацииВСтранахЕАЭС = ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС);
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиенту Тогда
		ЗаголовокПартнер = НСтр("ru = 'Покупатель'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию Тогда
		ЗаголовокПартнер = НСтр("ru = 'Комиссионер'");
	ИначеЕсли ОперацииЗакупки.Найти(ХозяйственнаяОперация) <> Неопределено
		Или ОперацииИмпорта.Найти(ХозяйственнаяОперация) <> Неопределено
		Или ОперацииВСтранахЕАЭС.Найти(ХозяйственнаяОперация) <> Неопределено Тогда
		ЗаголовокПартнер = НСтр("ru = 'Поставщик'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПриемНаКомиссию Тогда
		ЗаголовокПартнер = НСтр("ru = 'Комитент'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоставщику Тогда
		ЗаголовокПартнер = НСтр("ru = 'Поставщик'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту Тогда
		ЗаголовокПартнер = НСтр("ru = 'Покупатель'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет Тогда
		ЗаголовокПартнер = НСтр("ru = 'Налоговый орган'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеТаможне Тогда
		ЗаголовокПартнер = НСтр("ru = 'Таможня'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты Тогда
		ЗаголовокПартнер = НСтр("ru = 'Банк'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоКредитам Тогда
		ЗаголовокПартнер = НСтр("ru = 'Кредитор'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеНаДепозиты Тогда
		ЗаголовокПартнер = НСтр("ru = 'Дебитор'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаЗаймов Тогда
		ЗаголовокПартнер = НСтр("ru = 'Дебитор'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.НачисленияПоКредитам Тогда
		ЗаголовокПартнер = НСтр("ru = 'Кредитор'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.НачисленияПоДепозитам Тогда
		ЗаголовокПартнер = НСтр("ru = 'Дебитор'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.НачисленияПоЗаймамВыданным Тогда
		ЗаголовокПартнер = НСтр("ru = 'Дебитор'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента Тогда
		ЗаголовокПартнер = НСтр("ru = 'Покупатель'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика Тогда
		ЗаголовокПартнер = НСтр("ru = 'Поставщик'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам Тогда
		ЗаголовокПартнер = НСтр("ru = 'Кредитор'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам Тогда
		ЗаголовокПартнер = НСтр("ru = 'Дебитор'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным Тогда
		ЗаголовокПартнер = НСтр("ru = 'Дебитор'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи Тогда
		ЗаголовокПартнер = НСтр("ru = 'Поклажедатель'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаХранениеСПравомПродажи Тогда
		ЗаголовокПартнер = НСтр("ru = 'Хранитель'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоставкаПодПринципала Тогда
		ЗаголовокПартнер = НСтр("ru = 'Клиент'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.УдалитьОплатаСамозанятому Тогда
		ЗаголовокПартнер = НСтр("ru = 'Поставщик'");
	Иначе
		Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
			ЗаголовокПартнер = НСтр("ru = 'Контрагент'");
		Иначе
			ЗаголовокПартнер = НСтр("ru = 'Партнер'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЗаголовокПартнер;
	
КонецФункции

// Устанавливает заголовок элемента формы "Партнер" в зависимости от характера договора кредита или депозита.
//
// Параметры:
//  Форма  - ФормаКлиентскогоПриложения - Форма, в которой находится элемент, для которого устанавливается заголовок.
//  ИмяЭлементаФормыПартнер  - Строка - Имя элемента формы, для которого устанавливается заголовок.
//  ХарактерДоговора  - ПеречислениеСсылка.ХарактерыДоговоровФинансовыхИнструментов - характер договора, в зависимости от которого 
//                 устанавливается заголовок.
//
Процедура ЗаголовокЭлементаПартнерВЗависимостиОтХарактераДоговораКредитаИДепозита(Форма, ИмяЭлементаФормыПартнер, ХарактерДоговора) Экспорт

	Если ХарактерДоговора = Перечисления.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм Тогда
		
		ЗаголовокПартнер = НСтр("ru = 'Кредитор'");
		
	Иначе
		
		ЗаголовокПартнер = НСтр("ru = 'Дебитор'");
		
	КонецЕсли;
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ИмяЭлементаФормыПартнер, "Заголовок", ЗаголовокПартнер);


КонецПроцедуры

// Устанавливает заголовок элемента формы "Счет контрагента" в зависимости от хозяйственной операции.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма, в которой находится элемент, для которого устанавливается заголовок
//  ИмяЭлементаФормы - Строка - Имя элемента формы, для которого устанавливается заголовок
//  ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - хозяйственная операция, в зависимости от которой 
//                 устанавливается заголовок.
//
Процедура ЗаголовокЭлементаСчетКонтрагентаВЗависимостиОтХозяйственнойОперации(Форма, ИмяЭлементаФормы, ХозяйственнаяОперация) Экспорт
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиенту
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаХранениеСПравомПродажи Тогда
		Заголовок = НСтр("ru = 'Счет покупателя'");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПриемНаКомиссию
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи Тогда
		Заголовок = НСтр("ru = 'Счет продавца'");
	Иначе
		Заголовок = НСтр("ru = 'Счет контрагента'");
	КонецЕсли;
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ИмяЭлементаФормы, "Заголовок", Заголовок);

КонецПроцедуры 

// Устанавливает заголовок реквизита в зависимости от значения ФО "Использовать партнеров как контрагентов".
//
// Параметры:
//  Форма  - ФормаКлиентскогоПриложения - Форма, в которой находится элемент, для которого устанавливается заголовок.
//  ИмяЭлемента  - Строка - Имя элемента формы, для которого устанавливается заголовок.
//  НовыйЗаголовок  - Строка - заголовок, который будет установлен, если значение ФО "Использовать партнеров как
//                             контрагентов" - ИСТИНА.
//  ИспользоватьПартнеровКакКонтрагентов  - Булево - значение ФО "Использовать партнеров как контрагентов".
//  НоваяРасширеннаяПодсказка  - Строка - подсказка.
//
Процедура ЗаголовокРеквизитаВЗависимостиОтФОИспользоватьПартнеровКакКонтрагентов(Форма, ИмяЭлемента, НовыйЗаголовок, ИспользоватьПартнеровКакКонтрагентов = Неопределено, НоваяРасширеннаяПодсказка = "") Экспорт
	
	Если ИспользоватьПартнеровКакКонтрагентов = Неопределено Тогда
		ИспользоватьПартнеровКакКонтрагентов = ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов");
	КонецЕсли;
	
	Если ИспользоватьПартнеровКакКонтрагентов Тогда
		Если ПустаяСтрока(ИмяЭлемента) Тогда
			Форма.Автозаголовок = Ложь;
			Форма.Заголовок = НовыйЗаголовок;
		Иначе
			ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ИмяЭлемента, "Заголовок", НовыйЗаголовок);
			Если Не ПустаяСтрока(НоваяРасширеннаяПодсказка) Тогда
				ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ИмяЭлемента, "РасширеннаяПодсказка", НоваяРасширеннаяПодсказка);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает заголовок поля СКД в зависимости от значения ФО "Использовать партнеров как контрагентов".
//
// Параметры:
//  НаборДанных  - НаборДанныхЗапросМакетаКомпоновкиДанных - Набор, в котором находится элемент, для которого
//                                                           устанавливается заголовок.
//  ИмяЭлемента  - Строка - Имя элемента, для которого устанавливается заголовок.
//  НовыйЗаголовок  - Строка - заголовок, который будет установлен, если значение ФО "Использовать партнеров как
//                             контрагентов" - ИСТИНА.
//
Процедура ЗаголовокПоляСКДВЗависимостиОтФОИспользоватьПартнеровКакКонтрагентов(НаборДанных, ИмяЭлемента, НовыйЗаголовок) Экспорт

	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
		Поле = НаборДанных.Поля.Найти(ИмяЭлемента); // ПолеНабораДанныхСхемыКомпоновкиДанных 
		Если Поле <> Неопределено Тогда
			Поле.Заголовок = НовыйЗаголовок;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Получает признак ЮрФизлицо по данным формы помощника нового.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.ЮрФизЛицо, Неопределено - 
//
Функция ЮрФизЛицоПоДаннымФормыПомощникаНового(Форма) Экспорт
	
	Если Форма.ЭтоКомпания = 1 Тогда
		Возврат Перечисления.ЮрФизЛицо.ФизЛицо;
	Иначе
		Если Форма.ВидКомпании = 0 
			Или Форма.ВидКомпании = 3 Тогда
			 Возврат Перечисления.ЮрФизЛицо.ЮрЛицо;
			
		 ИначеЕсли Форма.ВидКомпании = 1 Тогда
			 
			Возврат Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент;
			
		ИначеЕсли Форма.ВидКомпании = 2 Тогда
			
			Возврат Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область ОбщиеПроцедурыФормЭлементаСправочникаПартнеры

// Обработчик события ПриСозданииНаСервере форм элемента справочника Партнеры.
//
// Параметры:
//  Форма                - ФормаКлиентскогоПриложения - форма, для которой выполняется обработчик, содержит:
//    * Объект - СправочникОбъект.Партнеры -
//  Отказ                - Булево - признак отказа от создания формы.
//  СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события.
//
Процедура ПартнерФормаЭлементаПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Форма.ИспользоватьЗапретОтгрузки = ПолучитьФункциональнуюОпцию("ИспользоватьЗапретОтгрузки");
	УпрощенныйВводДоступен           = УпрощенныйВводДоступен();
	ДоступноДобавлениеПартнеров      = ПравоДоступа("Добавление", Метаданные.Справочники.Партнеры);
	
	// определить канал и источник первичного интереса
	Если ПолучитьФункциональнуюОпцию("ФиксироватьПервичныйИнтерес") Тогда
		Интерес = ПервичныйИнтерес(Форма.Объект.Ссылка,Истина);
		Форма.КаналПервичногоИнтереса = Интерес.КаналПервичногоИнтереса;
		Форма.ИсточникПервичногоИнтереса = Интерес.ИсточникПервичногоИнтереса;
		Форма.Элементы.ИсточникПервичногоИнтереса.ТолькоПросмотр = Не ЗначениеЗаполнено(Форма.КаналПервичногоИнтереса);
	Иначе
		Форма.Элементы.ГруппаПервичныйИнтерес.Видимость = Ложь;
	КонецЕсли;
	
	Если Форма.Параметры.Свойство("СписокОтборПоТипуПартнера") Тогда
		
		Для каждого ЭлементСписка Из Форма.Параметры.СписокОтборПоТипуПартнера Цикл
			Форма.Объект[ЭлементСписка.Значение] = Истина;
		КонецЦикла;
		
	КонецЕсли;
	
	Если Форма.Объект.Перевозчик Тогда
		Форма.Объект.Поставщик = Истина;
	КонецЕсли;
	
	Если Форма.Объект.ОбслуживаетсяТорговымиПредставителями Тогда
		Форма.Объект.Клиент = Истина;
	КонецЕсли;
	
	Если Форма.Объект.Ссылка.Пустая() Тогда
		ПартнерФормаЭлементаПриСозданииЧтенииНаСервере(Форма);
	КонецЕсли;
	
	Если СтрНайти(Форма.ИмяФормы, "ФормаЭлементаРеквизитыКонтрагента") > 0 Тогда
		
		ПартнерыИКонтрагентыЛокализация.ПриСозданииНаСервереФормаЭлементаРеквизитыКонтрагента(Форма, Отказ, СтандартнаяОбработка);
		
	Иначе
		
		ПартнерыИКонтрагентыЛокализация.ПриСозданииНаСервереФормаЭлементаПартнер(Форма, Отказ, СтандартнаяОбработка);
		
	КонецЕсли;
	
	Если ДоступноДобавлениеПартнеров И Не УпрощенныйВводДоступен Тогда
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
		                                                               "ФормаСкопировать",
		                                                               "Видимость",
		                                                               Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДанныеКонтрагентаПартнера(Приемник, Партнер, СИсторическимиДанными = Истина) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Контрагенты.Ссылка КАК КонтрагентПартнера,
	|	Контрагенты.НаименованиеМеждународное КАК НаименованиеМеждународное,
	|	Контрагенты.НаименованиеВТранскрипции КАК НаименованиеВТранскрипции,
	|	Контрагенты.ЮрФизЛицо                 КАК ЮрФизЛицо,
	|	Контрагенты.ЮридическоеФизическоеЛицо КАК ЮридическоеФизическоеЛицо,
	|	Контрагенты.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	Контрагенты.ГоловнойКонтрагент        КАК ГоловнойКонтрагент,
	|	Контрагенты.СтранаРегистрации         КАК СтранаРегистрации,
	|	Контрагенты.РегистрационныйНомер      КАК РегистрационныйНомер,
	|	Контрагенты.ИНН                       КАК ИНН,
	|	Контрагенты.НалоговыйНомер            КАК НалоговыйНомер
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Партнер = &Партнер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КонтрагентыИсторияНаименований.Период                  КАК Период,
	|	КонтрагентыИсторияНаименований.СокращенноеНаименование КАК СокращенноеНаименование
	|ИЗ
	|	Справочник.Контрагенты.ИсторияНаименований КАК КонтрагентыИсторияНаименований
	|ГДЕ
	|	КонтрагентыИсторияНаименований.Ссылка.Партнер = &Партнер";
	
	Запрос.УстановитьПараметр("Партнер",Партнер);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = РезультатЗапроса[0].Выбрать();

	Если Выборка.Следующий() Тогда
	
		ЗаполнитьЗначенияСвойств(Приемник, Выборка);
		Если СИсторическимиДанными Тогда
			Приемник.ИсторияНаименований.Загрузить(РезультатЗапроса[1].Выгрузить());
		КонецЕсли;
	
	КонецЕсли;
	ПартнерыИКонтрагентыЛокализация.ДополнитьДанныеКонтрагентаПартнера(Приемник, Партнер, СИсторическимиДанными);
КонецПроцедуры

// Заполняет контактную информацию контрагента по контактной информации партнера.
// 
// Параметры:
// 	КонтрагентОбъект             - СправочникОбъект.Контрагенты - 
// 	Форма                        - ФормаКлиентскогоПриложения - 
// 	КонтактнаяИнформацияПартнера - ТаблицаЗначений -
// 	
Процедура ЗаполнитьКонтактнуюИнформациюКонтрагентаПартнера(КонтрагентОбъект, Форма, КонтактнаяИнформацияПартнера) Экспорт
	
	УдалитьСтрокуСВидомКИ(КонтрагентОбъект.КонтактнаяИнформация, Справочники.ВидыКонтактнойИнформации.EmailКонтрагента);
	УдалитьСтрокуСВидомКИ(КонтрагентОбъект.КонтактнаяИнформация, Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента);
	
	Для каждого СтрокаКИПартнера Из КонтактнаяИнформацияПартнера Цикл
		
		Если СтрокаКИПартнера.Вид = Справочники.ВидыКонтактнойИнформации.EmailПартнера Тогда
			ВидКиКонтрагента = Справочники.ВидыКонтактнойИнформации.EmailКонтрагента;
		ИначеЕсли СтрокаКИПартнера.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонПартнера Тогда
			ВидКиКонтрагента = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента;
		Иначе
			Продолжить;
		КонецЕсли;
		
		СтрокаКиКонтрагента = КонтрагентОбъект.КонтактнаяИнформация.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаКиКонтрагента, СтрокаКИПартнера);
		СтрокаКиКонтрагента.Вид = ВидКиКонтрагента;
		
	КонецЦикла;
	
КонецПроцедуры

// Обработчик события ПриЧтенииНаСервере форм элемента справочника "Партнеры"
//
// Параметры:
//  Форма          - ФормаКлиентскогоПриложения - форма, для которой выполняется обработчик.
//  ТекущийОбъект  - СправочникОбъект.Партнеры - Объект, который будет прочитан.
//
Процедура ПартнерФормаЭлементаПриЧтенииНаСервере(Форма, ТекущийОбъект) Экспорт
	
	ПартнерФормаЭлементаПриСозданииЧтенииНаСервере(Форма);
	
КонецПроцедуры

// Управляет доступностью элементов формы элемента справочника "Партнеры".
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, настраивается доступность элементов, содержит:
//    * Элементы - ВсеЭлементыФормы - содержит:
//      ** ДатаРождения                    - ПолеФормы - 
//      ** Пол                             - ПолеФормы - 
//      ** ВариантОтправкиЭлектронногоЧека - ПолеФормы - .
//
Процедура ПартнерФормаЭлементаУправлениеДоступностью(Форма) Экспорт
	
	Если Форма.Объект.Предопределенный Тогда
		Форма.Элементы.Клиент.ТолькоПросмотр = Истина;
		Форма.Элементы.Поставщик.ТолькоПросмотр = Истина;
		Форма.Элементы.Конкурент.ТолькоПросмотр = Истина;
		Форма.Элементы.ПрочиеОтношения.ТолькоПросмотр = Истина;
		Форма.Элементы.ОбслуживаетсяТорговымиПредставителями.ТолькоПросмотр = Истина;
		Форма.Элементы.Перевозчик.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Форма.Элементы.ОбслуживаетсяТорговымиПредставителями.Доступность = Форма.Объект.Клиент;
	Если НЕ Форма.Объект.Клиент Тогда
		Форма.Объект.ОбслуживаетсяТорговымиПредставителями = Ложь;
	КонецЕсли;
	
	Форма.Элементы.Перевозчик.Доступность = Форма.Объект.Поставщик;
	Если НЕ Форма.Объект.Поставщик Тогда
		Форма.Объект.Перевозчик = Ложь;
	КонецЕсли;
	
	Форма.Элементы.Пол.Доступность                             = (Форма.Объект.ЮрФизЛицо = Перечисления.КомпанияЧастноеЛицо.ЧастноеЛицо);
	Форма.Элементы.ДатаРождения.Доступность                    = (Форма.Объект.ЮрФизЛицо = Перечисления.КомпанияЧастноеЛицо.ЧастноеЛицо);
	Форма.Элементы.ВариантОтправкиЭлектронногоЧека.Доступность = (Форма.Объект.ЮрФизЛицо = Перечисления.КомпанияЧастноеЛицо.ЧастноеЛицо);
	
	Форма.Элементы.ГруппаСтраницыСписокЗапретовОтгрузки.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьЗапретОтгрузки") И Форма.Объект.Клиент;
	
КонецПроцедуры

// Обработчик события ПослеЗаписиНаСервере форм элемента справочника Партнеры
//
// Параметры:
//  Форма            - ФормаКлиентскогоПриложения - форма, для которой выполняется обработчик.
//  ТекущийОбъект    - СправочникОбъект.Партнеры - Записанный объект.
//  ПараметрыЗаписи  - Структура - содержит параметры записи.
//
Процедура ПартнерФормаЭлементаПослеЗаписиНаСервере(Форма, ТекущийОбъект, ПараметрыЗаписи) Экспорт

	ПартнерФормаЭлементаНастроитьПанельНавигации(Форма);
	ЗафиксироватьПервичныйИнтерес(Форма);
	Форма.Элементы.ГруппаДополнительнаяИнформация.Картинка = ОбщегоНазначенияКлиентСервер.КартинкаКомментария(Форма.Объект.ДополнительнаяИнформация);

КонецПроцедуры

#КонецОбласти

#Область ОбщиеПроцедурыИФункцииФормСпискаИВыбораСправочникаПартнеры

Процедура ПартнерыФормаВыбораСпискаПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Форма.УпрощенныйВводДоступен               = УпрощенныйВводДоступен();
	Форма.ТекущийПользователь                  = Пользователи.АвторизованныйПользователь();
	Форма.ДоступноДобавлениеПартнеров          = ПравоДоступа("Добавление", Метаданные.Справочники.Партнеры);
	Форма.ИспользоватьБизнесРегионы            = ПолучитьФункциональнуюОпцию("ИспользоватьБизнесРегионы");
	Форма.ИспользоватьПартнеровКакКонтрагентов = ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов");
	ЗапросСИнформациейПоКонтрагенту = Ложь;
	
	Если Не СтрНайти(Форма.ИмяФормы, "Контрагенты") > 0 Тогда
		
		ЗапросСИнформациейПоКонтрагенту = Ложь;
		
		СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
		СвойстваСписка.ТекстЗапроса = ТекстЗапросаДинамическогоСпискаПартнеры(ЗапросСИнформациейПоКонтрагенту);
		СвойстваСписка.ОсновнаяТаблица = "Справочник.Партнеры";
		СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;
	
		ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Форма.Элементы.Список, СвойстваСписка);
		
		Форма.Элементы.ГруппаКонтрагентыПартнера.Видимость      = НЕ Форма.ИспользоватьПартнеровКакКонтрагентов;

		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
		                                                               "СписокКонтекстноеМенюПеренестиЭлемент",
		                                                               "Видимость",
		                                                               НЕ Форма.ИспользоватьПартнеровКакКонтрагентов);
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
		                                                               "ФормаПеренестиЭлемент",
		                                                               "Видимость",
		                                                               НЕ Форма.ИспользоватьПартнеровКакКонтрагентов);
	КонецЕсли;
	
	Если Форма.ДоступноДобавлениеПартнеров И Не Форма.УпрощенныйВводДоступен Тогда
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
		                                                               "ФормаСкопировать",
		                                                               "Видимость",
		                                                               Ложь);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.Список,
	                                                                   "ОтборПоПолнотекстовомуПоискуУстановлен",
	                                                                   Ложь);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.Список,
	                                                                   "ОтборПоПолнотекстовомуПоиску",
	                                                                   Неопределено);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.Список,
	                                                                   "ОтборПоСегментуУстановлен",
	                                                                   Ложь);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.Список,
	                                                                   "ОтборПоСегменту",
	                                                                   Неопределено);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.Список,
	                                                                   "ПредставлениеОбособленногоПодразделения",
	                                                                   НСтр("ru='Обособленное подразделение'"));
	ДоступностьПолнотекстовыйПоискСписокПартнеры(Форма);
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "СоздатьНового", "Видимость", Форма.УпрощенныйВводДоступен);
	
	ЗаполнитьСписокВыбораТипФильтраСписокПартнеров(Форма, Форма.Элементы.ТипФильтра.СписокВыбора);
	Форма.ТипФильтра = Форма.Элементы.ТипФильтра.СписокВыбора[0].Значение;
	УстановитьВидимостьКомандОтчетов(Форма);
	УстановитьОтборыДинамическийСписокПартнеры(Форма);
	СоздатьРеквизитыДляОтображенияКИ(Форма);
	ПартнерыИКонтрагентыЛокализация.ПриСозданииНаСервереФормаВыбораСпискаПартнеры(Форма, Отказ, СтандартнаяОбработка, ЗапросСИнформациейПоКонтрагенту);
	ОпределитьРежимИспользованияПоискаВСписке(Форма);
	
КонецПроцедуры

// Устанавливает условное оформление в форме списка партнеров.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма для которой устанавливается условное оформление, содержит:
//   * Элементы - ВсеЭлементыФормы - содержит:
//    ** СтрокаПоиска  - ПолеФормы - 
//    ** БизнесРегионы           - ТаблицаФормы -
//    ** ГруппыДоступаПартнеров  - ТаблицаФормы -
//    ** Свойства                - ТаблицаФормы - 
//    ** Менеджеры               - ТаблицаФормы - 
//    ** Категории               - ТаблицаФормы - 
//
Процедура ПартнерыФормаВыбораСпискаУсловноеОформление(Форма) Экспорт

	Элементы = Форма.Элементы;
	УсловноеОформление = Форма.УсловноеОформление;

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтрокаПоиска.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("РасширенныйПоиск");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтрокаПоиска");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.FieldBackColor);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.БизнесРегионы.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ГруппыДоступаПартнеров.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Менеджеры.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Свойства.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Категории.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИспользоватьФильтр");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.FormBackColor);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);

КонецПроцедуры

// Создает в форме списка или выбора справочника "Партнеры" реквизиты для отображения КИ партнера в панели информации.
//
// Параметры:
//  Форма  - ФормаКлиентскогоПриложения - форма, для которой создаются реквизиты.
//
Процедура СоздатьРеквизитыДляОтображенияКИ(Форма) Экспорт
	
	ДобавляемыеРеквизиты = Новый Массив;
	ОписаниеТипаСтроки500 = Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(500));
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ВидыКонтактнойИнформации.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(ВидыКонтактнойИнформацииПредставления.Наименование, ВидыКонтактнойИнформации.Наименование) КАК Наименование,
	|	ВЫБОР
	|		КОГДА ВидыКонтактнойИнформации.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЗначениеУпорядочивания
	|ИЗ
	|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыКонтактнойИнформации.Представления КАК ВидыКонтактнойИнформацииПредставления
	|		ПО (ВидыКонтактнойИнформацииПредставления.Ссылка = ВидыКонтактнойИнформации.Ссылка
	|				И ВидыКонтактнойИнформацииПредставления.КодЯзыка = &КодЯзыка)
	|ГДЕ
	|	ВидыКонтактнойИнформации.Родитель = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.СправочникПартнеры)
	|	И НЕ ВидыКонтактнойИнформации.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗначениеУпорядочивания,
	|	Наименование";
	
	Запрос.УстановитьПараметр("КодЯзыка", ТекущийЯзык().КодЯзыка);
	ТаблицаВидовКИ = Запрос.Выполнить().Выгрузить();
	
	МаксимальнаяДлинаВидаКИ = 0;
	Для каждого СтрокаКИ Из ТаблицаВидовКИ Цикл
		Если СтрДлина(СтрокаКИ["Наименование"]) > МаксимальнаяДлинаВидаКИ Тогда
			МаксимальнаяДлинаВидаКИ = СтрДлина(СтрокаКИ["Наименование"] + 3);
		КонецЕсли;
	КонецЦикла;
	
	ОписаниеТипаСтрокиВидКИ = Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(МаксимальнаяДлинаВидаКИ + 3));
	Форма.Элементы.ГруппаВидыКИ.Ширина = МаксимальнаяДлинаВидаКИ + 3;
	Форма.КоличествоВидовКИ = ТаблицаВидовКИ.Количество();
	
	Для инд = 1 По Форма.КоличествоВидовКИ Цикл
		
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ЗаголовокВидКИ_" + Строка(инд), ОписаниеТипаСтрокиВидКИ,));
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ВидКИ_" + Строка(инд), ОписаниеТипаСтроки500,));
		
	КонецЦикла;
	
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Для инд = 1 По Форма.КоличествоВидовКИ Цикл
		
		Элемент = Форма.Элементы.Добавить("ЗаголовокВидКИ_" + Строка(инд), Тип("ПолеФормы"), Форма.Элементы.ГруппаВидыКИ);
		Элемент.Вид                      = ВидПоляФормы.ПолеНадписи;
		Элемент.ПоложениеЗаголовка       = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элемент.ВертикальноеПоложение    = ВертикальноеПоложениеЭлемента.Верх;
		Элемент.ПутьКДанным              = "ЗаголовокВидКИ_" + Строка(инд);
		Элемент.АвтоВысотаЯчейки         = Истина;
		Элемент.РастягиватьПоВертикали   = Ложь;
		Элемент.РастягиватьПоГоризонтали = Истина;
		Форма["ЗаголовокВидКИ_" + Строка(инд)] = ТаблицаВидовКИ[инд - 1]["Наименование"] + " :";
		
		Элемент = Форма.Элементы.Добавить("ВидКИ_" + Строка(инд), Тип("ПолеФормы"), Форма.Элементы.ГруппаЗначениеКИ); 
		Элемент.Вид                      = ВидПоляФормы.ПолеНадписи;
		Элемент.ПоложениеЗаголовка       = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элемент.ВертикальноеПоложение    = ВертикальноеПоложениеЭлемента.Верх;
		Элемент.ПутьКДанным              = "ВидКИ_" + Строка(инд);
		Элемент.ЦветТекста               = ЦветаСтиля.ИндивидуальнаяЦена;
		Элемент.АвтоВысотаЯчейки         = Истина;
		Элемент.РастягиватьПоВертикали   = Ложь;
		Элемент.РастягиватьПоГоризонтали = Истина;
		
	КонецЦикла;
	
	Для инд = 1 По Форма.КоличествоВидовКИ Цикл
		
		ЭлементУсловногоОформления = Форма.УсловноеОформление.Элементы.Добавить();
		
		ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ВидКИ_" + Строка(инд));
		ЭлементОтбораДанных.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбораДанных.ПравоеЗначение = НСтр("ru = '<Не указан>'");
		ЭлементОтбораДанных.Использование  = Истина;
		
		ЭлементЦветаОформления               = ЭлементУсловногоОформления.Оформление.Элементы.Найти("TextColor");
		ЭлементЦветаОформления.Значение      = Метаданные.ЭлементыСтиля.ЦветТекстаОтмененнойСтрокиДокумента.Значение;
		ЭлементЦветаОформления.Использование = Истина;
		
		НовыйЭлемент = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		НовыйЭлемент.Поле = Новый ПолеКомпоновкиДанных("ВидКИ_" + Строка(инд));
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет в форме списка или выбора справочника "Партнеры" реквизиты для отображения КИ партнера в панели информации.
//
// Параметры:
//  ДанныеПартнера - Структура                  - данные, по которым заполняется форма.
//  Форма          - ФормаКлиентскогоПриложения - форма, для которой заполняются реквизиты.
//
Процедура ЗаполнитьДанныеКИПартнера(ДанныеПартнера, Форма) Экспорт
	
	Если ДанныеПартнера = Неопределено Тогда
		
		Для инд = 1 По Форма.КоличествоВидовКИ Цикл
			
			Форма["ВидКИ_" + Строка(инд)] = НСтр("ru = '<Не указан>'");
			
		КонецЦикла;
		
		Возврат;
		
	КонецЕсли;
	
	Если ДанныеПартнера.КонтактнаяИнформацияТаблица.Количество() > Форма.КоличествоВидовКИ Тогда
		Возврат;
	КонецЕсли;
	
	Для инд = 1 По Форма.КоличествоВидовКИ Цикл
		
		Если ПустаяСтрока(ДанныеПартнера.КонтактнаяИнформацияТаблица[инд - 1].ПредставлениеКИ) Тогда
			Форма["ВидКИ_" + Строка(инд)] = НСтр("ru = '<Не указан>'");
		Иначе
			Форма["ВидКИ_" + Строка(инд)] = ДанныеПартнера.КонтактнаяИнформацияТаблица[инд - 1].ПредставлениеКИ;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Изменяет отбор в форме списка или выбора справочника "Партнеры" при изменении типа панели навигации или
// перепозиционировании в панели навигации.
//
// Параметры:
//  Форма                              - ФормаКлиентскогоПриложения - форма, для которой выполняется действие, содержит:
//   * ТипФильтра - Структура - содержит:
//     ** Наименование - Строка - 
//   * Элементы  - ВсеЭлементыФормы - содержит:
//     ** ТолькоЗначимые - ПолеФормы - 
//  ПереформированиеПанелиНавигации    - Булево           - признак того, что надо обновить панель навигации.
//  ТребуетсяЗаполнениеСтраницыСвойств - Булево           - признак того, что надо перезаполнить страницу свойств.
//
Процедура ИзменитьОтборСписок(Форма, ПереформированиеПанелиНавигации, ТребуетсяЗаполнениеСтраницыСвойств) Экспорт
	
	Если ТребуетсяЗаполнениеСтраницыСвойств Тогда
		
		Если Форма.ТипФильтра = "Категории" Тогда
			ЗаполнитьДеревоКатегорий(Форма);
			Форма.Элементы.СтраницыТипФильтра.ТекущаяСтраница = Форма.Элементы.СтраницаКатегории;
			
			НайденныеСтроки = Форма.НастройкиПанелейНавигации.НайтиСтроки(Новый Структура("ВидПанели","Категории"));
			Если НайденныеСтроки.Количество() > 0 Тогда
				НайденныеСтрокиПанели = Форма.Категории.НайтиСтроки(Новый Структура("Значение", НайденныеСтроки[0].ТекущееЗначение));
				Если НайденныеСтрокиПанели.Количество() > 0 Тогда
					Форма.Элементы.Категории.ТекущаяСтрока = НайденныеСтрокиПанели[0].ПолучитьИдентификатор();
				КонецЕсли;
			КонецЕсли;
			
			Форма.НеОтрабатыватьАктивизациюПанелиНавигации = Истина;
			
		Иначе
			
			ЗаполнитьДеревоСвойств(Форма);
			Форма.Элементы.СтраницыТипФильтра.ТекущаяСтраница = Форма.Элементы.СтраницаСвойства;
			
			НайденныеСтроки = Форма.НастройкиПанелейНавигации.НайтиСтроки(Новый Структура("ВидПанели", Форма.ТипФильтра));
			Если НайденныеСтроки.Количество() > 0 Тогда
				Форма.Элементы.Свойства.ТекущаяСтрока = НайтиСтрокуВДанныхФормыДерево(Форма.Свойства, НайденныеСтроки[0].ТекущееЗначение, "Значение", Истина)
			КонецЕсли;
			
			Форма.НеОтрабатыватьАктивизациюПанелиНавигации = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	УстанавливатьОтбор = Истина;
	Если НЕ Форма.ИспользоватьФильтр Тогда
		Форма.ТекущееЗначениеФильтра = Неопределено;
		УстанавливатьОтбор = Ложь;
	КонецЕсли;
	
	Если УстанавливатьОтбор ИЛИ Форма.ТолькоМои Тогда
		ГруппаОтбора = СоздатьГруппуОтбораПоФильтру(Форма);
	Иначе
		ГруппаОтбора = ОбщегоНазначенияКлиентСервер.НайтиЭлементОтбораПоПредставлению(
			ОбщегоНазначенияУТКлиентСервер.ПолучитьОтборДинамическогоСписка(Форма.Список).Элементы, "ОтборПоФильтру");
		Если ГруппаОтбора <> Неопределено Тогда
			ОбщегоНазначенияУТКлиентСервер.ПолучитьОтборДинамическогоСписка(Форма.Список).Элементы.Удалить(ГруппаОтбора);
		КонецЕсли;
	КонецЕсли;
	
	Если Форма.Элементы.СтраницыТипФильтра.ТекущаяСтраница = Форма.Элементы.СтраницаСвойства 
		ИЛИ Форма.Элементы.СтраницыТипФильтра.ТекущаяСтраница = Форма.Элементы.СтраницаКатегории Тогда
		
		Форма.ТолькоЗначимые = Истина;
		Форма.Элементы.ТолькоЗначимые.Доступность = Ложь;
		ИмяРеквизитаОтбора = ?(Форма.ИмяФормы = "Справочник.Контрагенты.Форма.ФормаВыбораИспользуютсяТолькоПартнеры" 
		                      ИЛИ Форма.ИмяФормы = "Справочник.Контрагенты.Форма.ФормаВыбораИспользуютсяТолькоПартнерыБезПолнотекстовогоПоиска",
		                      "Партнер", "Ссылка");
		
	Иначе
		
		Форма.Элементы.ТолькоЗначимые.Доступность = Истина;
		
		Если ПереформированиеПанелиНавигации Тогда
			
			Если Форма.Элементы.СтраницыТипФильтра.ТекущаяСтраница = Форма.Элементы.СтраницаБизнесРегионы Тогда
				НайденныеСтроки = Форма.НастройкиПанелейНавигации.НайтиСтроки(Новый Структура("ВидПанели", "БизнесРегионы"));
				Если НайденныеСтроки.Количество() > 0 Тогда
					Форма.Элементы.БизнесРегионы.ТекущаяСтрока = НайденныеСтроки[0].ТекущееЗначение;
				КонецЕсли;
				ИмяСписка = "БизнесРегионы";
			ИначеЕсли Форма.Элементы.СтраницыТипФильтра.ТекущаяСтраница = Форма.Элементы.СтраницаГруппыДоступа Тогда
				НайденныеСтроки = Форма.НастройкиПанелейНавигации.НайтиСтроки(Новый Структура("ВидПанели", "ГруппыДоступа"));
				Если НайденныеСтроки.Количество() > 0 Тогда
					Форма.Элементы.ГруппыДоступаПартнеров.ТекущаяСтрока = НайденныеСтроки[0].ТекущееЗначение;
				КонецЕсли;
				ИмяСписка = "ГруппыДоступаПартнеров";
			ИначеЕсли Форма.Элементы.СтраницыТипФильтра.ТекущаяСтраница = Форма.Элементы.СтраницаМенеджеры Тогда
				НайденныеСтроки = Форма.НастройкиПанелейНавигации.НайтиСтроки(Новый Структура("ВидПанели", "Менеджер"));
				Если НайденныеСтроки.Количество() > 0 Тогда
					Форма.Элементы.Менеджеры.ТекущаяСтрока = НайденныеСтроки[0].ТекущееЗначение;
				КонецЕсли;
				ИмяСписка = "Менеджеры";
			КонецЕсли;
			
			ОтборСпискаДляИзменения = ОбщегоНазначенияУТКлиентСервер.ПолучитьОтборДинамическогоСписка(Форма[ИмяСписка]);
			СписокЗначимые = СписокЗначимыхЗначенийПанелиНавигации(ИмяСписка);
			
			Если ПереформированиеПанелиНавигации И НЕ Форма.ТолькоЗначимые Тогда
				ГруппаОтбораЗначимые = ОбщегоНазначенияКлиентСервер.НайтиЭлементОтбораПоПредставлению(ОтборСпискаДляИзменения.Элементы, "ОтборПоЗначимым");
				Если ГруппаОтбораЗначимые <> Неопределено Тогда
					ОтборСпискаДляИзменения.Элементы.Удалить(ГруппаОтбораЗначимые);
				КонецЕсли;
			Иначе
				ГруппаОтбораЗначимые = СоздатьГруппуОтбораЗначимые(ОтборСпискаДляИзменения);
				ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораЗначимые,
				"Ссылка",
				ВидСравненияКомпоновкиДанных.ВСписке,
				СписокЗначимые);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Форма.Элементы.СтраницыТипФильтра.ТекущаяСтраница = Форма.Элементы.СтраницаБизнесРегионы И ПозиционированиеКорректно("БизнесРегионы", Форма) Тогда
		
		Если УстанавливатьОтбор Тогда
			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
		                                                       "БизнесРегион",
		                                                       ВидСравненияКомпоновкиДанных.Равно,
		                                                       Форма.Элементы.БизнесРегионы.ТекущаяСтрока);
		КонецЕсли;
		
		Форма.ТекущееЗначениеФильтра = Форма.Элементы.БизнесРегионы.ТекущаяСтрока;
		
	ИначеЕсли Форма.Элементы.СтраницыТипФильтра.ТекущаяСтраница = Форма.Элементы.СтраницаГруппыДоступа И  ПозиционированиеКорректно("ГруппыДоступаПартнеров", Форма) Тогда
		
		Если УстанавливатьОтбор Тогда
			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
			                                                       "ГруппаДоступа",
			                                                       ВидСравненияКомпоновкиДанных.Равно,
			                                                       Форма.Элементы.ГруппыДоступаПартнеров.ТекущаяСтрока);
		КонецЕсли;
		
		Форма.ТекущееЗначениеФильтра = Форма.Элементы.ГруппыДоступаПартнеров.ТекущаяСтрока;
		
	ИначеЕсли Форма.Элементы.СтраницыТипФильтра.ТекущаяСтраница = Форма.Элементы.СтраницаМенеджеры И ПозиционированиеКорректно("Менеджеры", Форма) Тогда
		
		Если УстанавливатьОтбор Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
		                                                       "ОсновнойМенеджер",
		                                                       ВидСравненияКомпоновкиДанных.Равно,
		                                                       Форма.Элементы.Менеджеры.ТекущаяСтрока);
		КонецЕсли;
		
		Форма.ТекущееЗначениеФильтра = Форма.Элементы.Менеджеры.ТекущаяСтрока;
		
	ИначеЕсли Форма.Элементы.СтраницыТипФильтра.ТекущаяСтраница = Форма.Элементы.СтраницаСвойства Тогда
		
		Если УстанавливатьОтбор Тогда
			
			Если Форма.Элементы.Свойства.ТекущаяСтрока <> Неопределено И ТипЗнч(Форма.Элементы.Свойства.ТекущаяСтрока) = Тип("Число") Тогда 
				ТекущиеДанные = Форма.Свойства.НайтиПоИдентификатору(Форма.Элементы.Свойства.ТекущаяСтрока);
				Если ТекущиеДанные <> Неопределено Тогда
					ЗначениеОтбора =  ТекущиеДанные.Значение;
				Иначе
					Возврат;
				КонецЕсли;
			Иначе
				Возврат
			КонецЕсли;
			
			Если ЗначениеОтбора = НСтр("ru = 'Не указан'") Тогда
				
				ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
				                                                       ИмяРеквизитаОтбора + ".[" + Строка(Форма.ТипФильтра) + "]",
				                                                       ВидСравненияКомпоновкиДанных.НеЗаполнено)
				
			ИначеЕсли ЗначениеОтбора = НСтр("ru = 'Все'") Тогда
				
				ОбщегоНазначенияУТКлиентСервер.ПолучитьОтборДинамическогоСписка(Форма.Список).Элементы.Удалить(ГруппаОтбора);
				Возврат;
				
			Иначе
				
				ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
				                                                       ИмяРеквизитаОтбора + ".[" + Строка(Форма.ТипФильтра) + "]",
				                                                       ВидСравненияКомпоновкиДанных.Равно,
				                                                       ЗначениеОтбора);
				
			КонецЕсли;
		КонецЕсли;
		
		Форма.ТекущееЗначениеФильтра = ЗначениеОтбора;
		
	ИначеЕсли Форма.Элементы.СтраницыТипФильтра.ТекущаяСтраница = Форма.Элементы.СтраницаКатегории Тогда
		
		Если УстанавливатьОтбор Тогда
			
			Если Форма.Элементы.Категории.ТекущаяСтрока <> Неопределено И ТипЗнч(Форма.Элементы.Категории.ТекущаяСтрока) = Тип("Число") Тогда 
				ТекущиеДанные = Форма.Категории.НайтиПоИдентификатору(Форма.Элементы.Категории.ТекущаяСтрока);
				Если ТекущиеДанные <> Неопределено Тогда
					ЗначениеОтбора      = ТекущиеДанные.Значение; // СправочникСсылка - 
					ПредставлениеОтбора = ТекущиеДанные.Представление; // Строка - 
				Иначе
					Возврат;
				КонецЕсли;
			Иначе
				Возврат
			КонецЕсли;
				
			Если ЗначениеОтбора <> НСтр("ru = 'Все'") Тогда
				
				ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
				                                                       ИмяРеквизитаОтбора + ".[" + ПредставлениеОтбора + "]",
				                                                       ВидСравненияКомпоновкиДанных.Равно,
				                                                       Истина);
				
			КонецЕсли;
			
			Форма.ТекущееЗначениеФильтра = ЗначениеОтбора;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Форма.ТолькоМои 
		И (НЕ Форма.Элементы.СтраницыТипФильтра.ТекущаяСтраница = Форма.Элементы.СтраницаМенеджеры
		   Или Не УстанавливатьОтбор) Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
		                                                       "ОсновнойМенеджер",
		                                                       ВидСравненияКомпоновкиДанных.Равно,
		                                                       Форма.ТекущийПользователь);
	КонецЕсли;
	
КонецПроцедуры

// Создает группу отбора для отображения только значимых элементов в динамическом списке,
//  который выступает панелью навигации для списка партнеров.
// Параметры:
//  ОтборДляИзменения  - ОтборКомпоновкиДанных - отбор, для которого добавляется группа.
//
// Возвращаемое значение:
//   ЭлементОтбора   - созданная группа отбора.
//
Функция СоздатьГруппуОтбораЗначимые(ОтборДляИзменения) Экспорт

	Возврат ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ОтборДляИзменения.Элементы,"ОтборПоЗначимым",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);

КонецФункции

// Создает группу отбора в динамическом списке партнеров, в которой будет размещены элементы отбора
// согласно позиционированию в панели навигации.
// Параметры:
//  Форма  - ФормаКлиентскогоПриложения - форма, в которой находится динамический список партнеров.
//
// Возвращаемое значение:
//   ЭлементОтбора   - созданная группа отбора.
//
Функция СоздатьГруппуОтбораПоФильтру(Форма) Экспорт

	Возврат ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ОбщегоНазначенияУТКлиентСервер.ПолучитьОтборДинамическогоСписка(Форма.Список).Элементы, "ОтборПоФильтру",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);

КонецФункции

// Проверяет, корректно ли выполнено позиционирование в динамическом списке.
// 
// Параметры:
//  ИмяСписка - Строка - наименование элемента формы, для которого выполняется проверка.
//  Форма     - ФормаКлиентскогоПриложения - форма, в которой находится динамический список партнеров.
//
// Возвращаемое значение:
//   ЭлементОтбора   - созданная группа отбора.
//
Функция ПозиционированиеКорректно(ИмяСписка,Форма) Экспорт

	ТипГруппировка = Тип("СтрокаГруппировкиДинамическогоСписка");
	
	Список = Форма.Элементы[ИмяСписка]; // ТаблицаФормы - 
		
	Если ТипЗнч(Список.ТекущаяСтрока) <> ТипГруппировка И ЗначениеЗаполнено(Список.ТекущаяСтрока) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

// Заполняет панель информации в форме списка по данным партнера.
// 
// Параметры:
//  Форма     - ФормаКлиентскогоПриложения - форма, в которой находится динамический список партнеров.
//  Партнер   - СправочникСсылка.Партнеры - партнер, данными которого заполняется панель информации.
//
Процедура ЗаполнитьПанельИнформацииПоДаннымПартнера(Форма, Партнер) Экспорт

	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(
		Форма.Элементы,
		"ОтчетДосьеПартнера, КонтактныеЛицаПартнера",
		"Доступность",
		Партнер <> Неопределено);
	
	Если НЕ Форма.ИспользоватьПартнеровКакКонтрагентов Тогда
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(
			Форма.Элементы,
			"КонтрагентыПартнера",
			"Доступность",
			Партнер <> Неопределено);
	КонецЕсли;
	
	Если Партнер = Неопределено Тогда
		
		ДанныеПартнера = Неопределено;
		
		Если Форма.ИспользоватьПартнеровКакКонтрагентов Тогда
			Форма.НаименованиеТекущегоПартнера                = НСтр("ru = 'Контрагент не выбран'");
		Иначе
			Форма.НаименованиеТекущегоПартнера                = НСтр("ru = 'Партнер не выбран'");
			Форма.КонтрагентыПартнера                         = НСтр("ru = 'Контрагенты'");
		КонецЕсли;
		
		Форма.ТекущийБизнесРегион    = "";
		Форма.КонтактныеЛицаПартнера = НСтр("ru = 'Контактные лица'");
		
	Иначе
		
		ДанныеПартнера = ПартнерыИКонтрагенты.ДанныеПартнераДляПанелиИнформации(Партнер);
		
		Форма.НаименованиеТекущегоПартнера = ДанныеПартнера.Наименование;
		
		Форма.ТекущийБизнесРегион 	 = ?(Форма.ИспользоватьБизнесРегионы, ДанныеПартнера.БизнесРегион, " ");
		Форма.КонтактныеЛицаПартнера = НСтр("ru = 'Контактные лица'") + " (" + Строка(ДанныеПартнера.КоличествоКонтактныхЛиц) + ")";
		
	КонецЕсли;
	
	Если Форма.ИспользоватьПартнеровКакКонтрагентов Тогда
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ГруппаКонтрагентыПартнера", "Видимость", Ложь, Истина);
	КонецЕсли;
	ПартнерыИКонтрагенты.ЗаполнитьДанныеКИПартнера(ДанныеПартнера, Форма);
	ПартнерыИКонтрагентыЛокализация.ЗаполнитьПанельИнформацииПоДаннымПартнера(Форма, Партнер, ДанныеПартнера);
	Форма.ТекущийАктивныйПартнер = Партнер;

КонецПроцедуры

// Обработчик события "Перед записью".
// 
// Параметры:
//  Форма     - ФормаКлиентскогоПриложения - форма, в которой находится динамический список партнеров, содержит:
//   * Элементы - ВсеЭлементыФормы - содержит:
//     ** Свойства               - ТаблицаФормы - 
//     ** БизнесРегионы          - ТаблицаФормы -
//     ** Категории              - ТаблицаФормы - 
//     ** Менеджеры              - ТаблицаФормы - 
//     ** ГруппыДоступаПартнеров - ТаблицаФормы -
//  Настройки - Соответствие - содержит:
//    * ТипФильтра             - Строка - тип устанавливаемого фильтра формы
//    * ТекущееЗначениеФильтра - Произвольный - текущее значение фильтра
//    * ИспользоватьФильтр     - Булево - признак того, что фильтр используется
//    * ИсторияВыбораСегментов - СписокЗначений из СправочникСсылка.СегментыПартнеров - история ранее выбираемых сегментов
//    * ТолькоЗначимые         - Булево - признак того, что пользователю будут предложены только те значения фильтра, которые гарантируют наличие элементов в динамическом списке
//    * Сегмент                - СправочникСсылка.СегментыПартнеров - сегмент, по которому установлен отбор
//    * ИспользоватьФильтр     - Булево - признак того, что фильтр используется
//    * ТолькоЗначимые         - Булево - признак установленного отбора по партнерам, у которых текущий пользователь является основным менеджером
//
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Форма, Настройки) Экспорт
	
	ТипФильтраНастройки                  = Настройки.Получить("ТипФильтра");
	ТекущееЗначениеФильтраНастройки      = Настройки.Получить("ТекущееЗначениеФильтра");
	ИспользоватьФильтрНастройки          = Настройки.Получить("ИспользоватьФильтр");
	ИсторияВыбораСегментовНастройки      = Настройки.Получить("ИсторияВыбораСегментов");
	ТолькоЗначимыеНастройки              = Настройки.Получить("ТолькоЗначимые");
	Сегмент                              = Настройки.Получить("Сегмент");
	ТолькоМои                            = Настройки.Получить("ТолькоМои");
	
	Если ИсторияВыбораСегментовНастройки <> Неопределено Тогда
		Форма.Элементы.Сегмент.СписокВыбора.ЗагрузитьЗначения(ИсторияВыбораСегментовНастройки.ВыгрузитьЗначения());
	КонецЕсли;

	Если ТолькоМои <> Неопределено Тогда
		Форма.ТолькоМои = ТолькоМои;
		Настройки.Удалить("ТолькоМои");
		Если Форма.ИспользоватьФильтр И Форма.ТипФильтра = "Менеджер" Тогда
			ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ТолькоМои","Доступность", Ложь);
			Форма.ТолькоМои = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если Сегмент <> Неопределено И Сегмент <> Справочники.СегментыПартнеров.ПустаяСсылка() Тогда
		
		Форма.Элементы.Список.Отображение = ОтображениеТаблицы.Список;
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.Список,
		                                                                   "ОтборПоСегментуУстановлен",
		                                                                   Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.Список,
		                                                                   "ОтборПоСегменту",
		                                                                   СегментыВызовСервера.СписокЗначений(Сегмент).ВыгрузитьЗначения());
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.Список,
		                                                                   "ОтборПоСегментуУстановлен",
		                                                                   Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Форма.Список,
		                                                                   "ОтборПоСегменту",
		                                                                   Неопределено);
		
	КонецЕсли;
	
	Если ТипФильтраНастройки = Неопределено  Тогда
		Возврат;
	ИначеЕсли Форма.Элементы.ТипФильтра.СписокВыбора.НайтиПоЗначению(ТипФильтраНастройки) <> Неопределено Тогда
		
		Форма.ТипФильтра = ТипФильтраНастройки;
		
		Если ТипЗнч(ТипФильтраНастройки) = Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения")Тогда
			Форма.ТолькоЗначимые = Истина;
			Форма.Элементы.ТолькоЗначимые.Доступность = Ложь;
			Форма.Элементы.СтраницыТипФильтра.ТекущаяСтраница = Форма.Элементы.СтраницаСвойства;
			ЗаполнитьДеревоСвойств(Форма);
			Форма.Элементы.Свойства.ТекущаяСтрока = НайтиСтрокуВДанныхФормыДерево(Форма.Свойства, ТекущееЗначениеФильтраНастройки, "Значение", Истина);
		ИначеЕсли Форма.ТипФильтра  = "Категории" Тогда
			Форма.ТолькоЗначимые = Истина;
			Форма.Элементы.ТолькоЗначимые.Доступность = Ложь;
			Форма.Элементы.СтраницыТипФильтра.ТекущаяСтраница = Форма.Элементы.СтраницаКатегории;
			ЗаполнитьДеревоКатегорий(Форма);
			НайденныеСтроки = Форма.Категории.НайтиСтроки(Новый Структура("Значение", ТекущееЗначениеФильтраНастройки));
			Если НайденныеСтроки.Количество() > 0 Тогда
				Форма.Элементы.Категории.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
			КонецЕсли;
		ИначеЕсли Форма.ТипФильтра  = "БизнесРегионы" Тогда
			Форма.Элементы.СтраницыТипФильтра.ТекущаяСтраница = Форма.Элементы.СтраницаБизнесРегионы;
			Форма.Элементы.БизнесРегионы.ТекущаяСтрока = ТекущееЗначениеФильтраНастройки;
		ИначеЕсли Форма.ТипФильтра = "ГруппыДоступа" Тогда
			Форма.Элементы.СтраницыТипФильтра.ТекущаяСтраница = Форма.Элементы.СтраницаГруппыДоступа;
			Форма.Элементы.ГруппыДоступаПартнеров.ТекущаяСтрока = ТекущееЗначениеФильтраНастройки;
		ИначеЕсли Форма.ТипФильтра = "Менеджер" Тогда
			Форма.Элементы.СтраницыТипФильтра.ТекущаяСтраница = Форма.Элементы.СтраницаМенеджеры;
			Форма.Элементы.Менеджеры.ТекущаяСтрока = ТекущееЗначениеФильтраНастройки;
		КонецЕсли;
		
		Если ИспользоватьФильтрНастройки <> Неопределено Тогда
			Настройки.Удалить("ИспользоватьФильтр");
			Если Форма.ИмяФормы = "Справочник.Контрагенты.Форма.ФормаВыбораИспользуютсяТолькоПартнеры" И Форма.ПоПартнеру Тогда
				Форма.ИспользоватьФильтр = Ложь;
			Иначе
				Форма.ИспользоватьФильтр =  ИспользоватьФильтрНастройки;
			КонецЕсли;
		КонецЕсли;
		
		Если ТолькоЗначимыеНастройки <> Неопределено Тогда
			Форма.ТолькоЗначимые = ТолькоЗначимыеНастройки;
		КонецЕсли;
		
		Форма.НеОтрабатыватьАктивизациюПанелиНавигации = Истина;
		ИзменитьОтборСписок(Форма,Истина,Ложь);
		
	КонецЕсли;
	
	Настройки.Удалить("ТипФильтра");
	Если ТекущееЗначениеФильтраНастройки <> Неопределено Тогда
		Настройки.Удалить("ТекущееЗначениеФильтра");
	КонецЕсли;
	
КонецПроцедуры

// Проверяет наличие в настройках компоновки динамического списка поиска строке .
// 
// Параметры:
//  Список - ДинамическийСписок - список, в котором выполняется проверка.
//
// Возвращаемое значение:
//   Булево - .
//
Функция ЕстьТекстПлатформенногоПоиска(Список) Экспорт
	
	ОтборКомпоновкиДанных = Список.ПолучитьИсполняемыеНастройкиКомпоновкиДанных().Отбор; // ОтборКомпоновкиДанных
	ЭлементыОтбора = ОтборКомпоновкиДанных.Элементы;

	АнализируемоеПолеКомпоновкиДанных = Новый ПолеКомпоновкиДанных("Ссылка");
	АнализируемыйВидСравнения         = ВидСравненияКомпоновкиДанных.ВСписке;
	
	Результат = Ложь;
	ПроверитьНаличиеЭлементаОтбора(ЭлементыОтбора, АнализируемоеПолеКомпоновкиДанных, АнализируемыйВидСравнения, Результат);
	
	Возврат Результат
	
КонецФункции

// Выполняет настройку элементов формы при изменении режима полнотекстового поиска .
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой выполняется переключение режима.
//  СохранятьНастройки - Булево - определяет, требуется ли сохранение текущих настроек при выполнении процедуры.
//
Процедура ПриИзмененииРежимаИспользованияПолнотекстовогоПоиска(Форма, СохранятьНастройки = Ложь) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если Не Форма.ИспользоватьПолнотекстовыйПоиск Тогда
		
		Элементы.ГруппаГорячиеКлавиши.Видимость = Ложь;
		Элементы.СписокПереключитьРежимПоиска.Видимость = Ложь;
		Возврат;
		
	КонецЕсли;
	
	Элементы.СписокПереключитьРежимПоиска.Пометка    = Форма.ИспользоватьРасширенныйПоиск;
	Элементы.СписокУправлениеПоиском.Видимость       = Ложь;
	Если Элементы.Найти("СписокПоискПоТекущемуЗначению") <> Неопределено Тогда
		Элементы.СписокПоискПоТекущемуЗначению.Видимость = Ложь;
	КонецЕсли;
	
	Если Форма.ИспользоватьРасширенныйПоиск Тогда
		
		Элементы.Список.ПоложениеСтрокиПоиска            = ПоложениеСтрокиПоиска.Нет;
		Элементы.СписокСтрокаПоиска.Видимость            = Ложь; 
		Элементы.СписокУправлениеПоиском.Видимость       = Ложь;
		Элементы.ГруппаФильтры.Видимость                 = Истина; 
		Элементы.ГруппаГорячиеКлавиши.Видимость          = Истина;
	
	Иначе
		
		Элементы.СписокСтрокаПоиска.Видимость            = Истина;
		Элементы.Список.ПоложениеСтрокиПоиска            = ПоложениеСтрокиПоиска.Верх;
		Элементы.ГруппаФильтры.Видимость                 = Ложь;
		Элементы.ГруппаГорячиеКлавиши.Видимость          = Ложь;
		Элементы.ОснованиеВыбора.Видимость               = Ложь;
	
	КонецЕсли;
	
	Если СохранятьНастройки Тогда
	
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ИспользованиеПолнотекстовогоПоискаВСпискеПартнеров",
		                                                 "НастройкиПользователяИспользованиеПартнеров",
		                                                 Форма.ИспользоватьРасширенныйПоиск);
	
	КонецЕсли;

КонецПроцедуры

// Находит первую строку в заданной колонке, с заданным значением в коллекции ДанныеФормыДерево.
// 
// Параметры:
//  ГдеИскать            - ДанныеФормыДерево - дерево, в котором выполняется поиск.
//  Значение             - Произвольный      - значение, которое ищем.
//  Колонка              - Строка            - имя колонки, в которой ищем.
//  ИскатьВПодчиненных   - Булево            - признак необходимости поиска в подчиненных строках.
//
// Возвращаемое значение:
//   Число - идентификатор найденной строки.
//
Функция НайтиСтрокуВДанныхФормыДерево(ГдеИскать, Значение, Колонка, ИскатьВПодчиненных) Экспорт
	
	ЭлементыДерева = ГдеИскать.ПолучитьЭлементы();
	
	Для каждого ЭлементДерева Из ЭлементыДерева Цикл
		Если ЭлементДерева[Колонка] = Значение Тогда
			Возврат ЭлементДерева.ПолучитьИдентификатор();
		ИначеЕсли  ИскатьВПодчиненных Тогда
			НайденныйИдентификаторСтроки =  НайтиСтрокуВДанныхФормыДерево(ЭлементДерева, Значение, Колонка, ИскатьВПодчиненных);
			Если НайденныйИдентификаторСтроки >=0 Тогда
				Возврат НайденныйИдентификаторСтроки;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат -1;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыПроверкиКорректностиЗаполнения

// Проверяет корректность заполнения контрагента. Вызывается из процедуры "ОбработкаПроверкиЗаполнения".
//
// Параметры:
//  Объект     - СправочникОбъект.Контрагенты, ФормаКлиентскогоПриложения - объект, содержащий данные контрагента.
//  Контрагент - СправочникСсылка.Контрагенты - ссылка на проверяемого контрагент.
//  Отказ      - Булево - флаг отказа в процедуре проверки заполнения.
//
Процедура ПроверитьКорректностьЗаполненияКонтрагента(Объект, Контрагент, Отказ) Экспорт
	
	КлючДанных = ?(ТипЗнч(Объект) = Тип("ФормаКлиентскогоПриложения"), Неопределено, Объект);
	
	Если Объект.ОбособленноеПодразделение И ЗначениеЗаполнено(Объект.ГоловнойКонтрагент) Тогда
		
		Если Объект.ГоловнойКонтрагент = Контрагент Тогда
			
			ТекстОшибки = НСтр("ru = 'Контрагент не может являться своим обособленным подразделением'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, КлючДанных, "ГоловнойКонтрагент", , Отказ);
			
		Иначе
			
			СвойстваГоловногоКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				Объект.ГоловнойКонтрагент, "ЮрФизЛицо, Наименование, ОбособленноеПодразделение");
			
			Если СвойстваГоловногоКонтрагента.ОбособленноеПодразделение Тогда
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Контрагент %1 не может быть выбран головным, т.к. сам является обособленным подразделением'"),
					СвойстваГоловногоКонтрагента.Наименование);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, КлючДанных, "ГоловнойКонтрагент", , Отказ);
				
			ИначеЕсли СвойстваГоловногоКонтрагента.ЮрФизЛицо <> Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Контрагент %1 не может быть выбран головным, т.к. не является юридическим лицом'"),
					СвойстваГоловногоКонтрагента.Наименование);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, КлючДанных, "ГоловнойКонтрагент", , Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Объект.ОбособленноеПодразделение Или Объект.ЮрФизЛицо <> Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
		
		МассивОбособленныхПодразделений = ОбособленныеПодразделенияКонтрагента(Контрагент);
		Если МассивОбособленныхПодразделений.Количество() > 0 Тогда
			
			Если Объект.ОбособленноеПодразделение Тогда
				ТекстОшибки =
					НСтр("ru = 'Контрагент не может быть обособленным подразделением, т.к. сам является головным для других контрагентов'");
			Иначе
				ТекстОшибки =
					НСтр("ru = 'Контрагент должен быть юридическим лицом, т.к. является головным подразделением для других контрагентов'");
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , , "ВидКонтрагента", Отказ);
			
		КонецЕсли
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.РегистрационныйНомер) И Объект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент Тогда
		
		ВыборкаКонтрагент = СтранаРегистрационныйНомерУжеИспользуетсяВИнформационнойБазе(Объект.РегистрационныйНомер, Объект.СтранаРегистрации, Контрагент);
		
		Если ВыборкаКонтрагент <> Неопределено Тогда
			ТекстСообщения = НСтр("ru='Данные регистрационного номера в этой же стране регистрации уже указаны для контрагента с кодом %1, ответственный - %2.'");
			ТекстСообщения =  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ВыборкаКонтрагент.Код, ВыборкаКонтрагент.ОсновнойМенеджер);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, КлючДанных, "РегистрационныйНомер", , Отказ);			
		КонецЕсли;
		
	КонецЕсли;
	
	ПартнерыИКонтрагентыЛокализация.ПроверитьКорректностьЗаполненияКонтрагента(Объект, Контрагент, Отказ);
	
КонецПроцедуры

#КонецОбласти

// Вызывается для включения ведения истории для вида контактной информации.
// Параметры:
//  ИмяВидаКонтактнойИнформации - Строка - имя вида контактной информации.
//
Процедура ВключитьХранениеИсторииИзменений(ИмяВидаКонтактнойИнформации) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Справочники.ВидыКонтактнойИнформации[ИмяВидаКонтактнойИнформации], "ХранитьИсториюИзменений") Тогда
		
		ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации[ИмяВидаКонтактнойИнформации].ПолучитьОбъект();
		ВидКонтактнойИнформации.ХранитьИсториюИзменений = Истина;
		ВидКонтактнойИнформации.ВидРедактирования = "Диалог";
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ВидКонтактнойИнформации);
		
	КонецЕсли;
	
КонецПроцедуры

// Функция получает значения реквизитов выбранного контрагента.
//
// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты - Ссылка на контрагента.
//  ДатаСведений - Дата - дата получения сведений.
//
// Возвращаемое значение:
//  Структура - реквизиты выбранного контрагента:
//  	* Представление - Строка - 
//  	* Наименование - Строка -
//  	* НаименованиеМеждународное - Строка - 
//  	* ЮрФизЛицо - ПеречислениеСсылка.ЮрФизЛицо -
Функция РеквизитыКонтрагента(Контрагент, ДатаСведений = Неопределено) Экспорт
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Представление", "");
	СтруктураРеквизитов.Вставить("Наименование",  "");
	СтруктураРеквизитов.Вставить("ЮрФизЛицо",     Перечисления.ЮрФизЛицо.ПустаяСсылка());
	СтруктураРеквизитов.Вставить("ИНН",           "");
	СтруктураРеквизитов.Вставить("НаименованиеМеждународное", "");
	
	Запрос = Новый Запрос;
	
	Если ДатаСведений <> Неопределено Тогда
		
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ИсторияНаименованийКонтрагентов.СокращенноеНаименование КАК СокращенноеНаименование
		|ПОМЕСТИТЬ ИсторическоеЗначениеНаименования
		|ИЗ
		|	Справочник.Контрагенты.ИсторияНаименований КАК ИсторияНаименованийКонтрагентов
		|ГДЕ
		|	ИсторияНаименованийКонтрагентов.Ссылка = &Контрагент
		|	И ИсторияНаименованийКонтрагентов.Период <= &ДатаСведений
		|	И ИсторияНаименованийКонтрагентов.СокращенноеНаименование <> &ПустаяСтрока
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИсторияНаименованийКонтрагентов.Период УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Контрагенты.ИНН                                                                                    КАК ИНН,
		|	ЕСТЬNULL(ИсторическоеЗначениеНаименования.СокращенноеНаименование, Контрагенты.НаименованиеПолное) КАК НаименованиеПолное,
		|	Контрагенты.ЮрФизЛицо                                                                              КАК ЮрФизЛицо,
		|	Контрагенты.НаименованиеМеждународное КАК НаименованиеМеждународное
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|	ЛЕВОЕ СОЕДИНЕНИЕ ИсторическоеЗначениеНаименования КАК ИсторическоеЗначениеНаименования
		|		ПО (ИСТИНА)
		|ГДЕ
		|	Контрагенты.Ссылка = &Контрагент
		|";
		
		Запрос.УстановитьПараметр("ДатаСведений", ДатаСведений);
		Запрос.УстановитьПараметр("ПустаяСтрока", "");
		
	Иначе
		
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Контрагенты.ИНН                       КАК ИНН,
		|	Контрагенты.НаименованиеПолное        КАК НаименованиеПолное,
		|	Контрагенты.ЮрФизЛицо                 КАК ЮрФизЛицо,
		|	Контрагенты.НаименованиеМеждународное КАК НаименованиеМеждународное
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.Ссылка = &Контрагент";
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СтруктураРеквизитов.Представление = Выборка.НаименованиеПолное;
		СтруктураРеквизитов.Наименование  = Выборка.НаименованиеПолное;
		СтруктураРеквизитов.ЮрФизЛицо     = Выборка.ЮрФизЛицо;
		СтруктураРеквизитов.ИНН           = Выборка.ИНН;
		СтруктураРеквизитов.НаименованиеМеждународное = Выборка.НаименованиеМеждународное;
	КонецЕсли;
	ПартнерыИКонтрагентыЛокализация.ДополнитьРеквизитыКонтрагента(Контрагент, СтруктураРеквизитов, ДатаСведений);
	Возврат СтруктураРеквизитов;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Дополняет запрос динамического списка партнера условием по типу отношений сохраняя вышестоящие элементы иерархии.
//
// Параметры:
//  ТипОтношений - Строка - тип отношений для которого наложено условие.
//
// Возвращаемое значение:
//  Строка  - дополнение текста запроса.
//
Функция ДополнениеЗапросаДинамическогоСпискаПоПартнерам(СписокОтборПоТипуПартнера, УстанавливатьОтборПоТипуПартнераКакИЛИ) 
	
	СтрокаОтбора = "";
	СтрокаУсловиеИли = " ИЛИ ";
	СтрокаУсловиеИ   = " И ";
	СтрокаУсловия = ?(УстанавливатьОтборПоТипуПартнераКакИЛИ, СтрокаУсловиеИли, СтрокаУсловиеИ);
	
	Для каждого ЭлементСписка Из СписокОтборПоТипуПартнера Цикл
		
		Если ЭлементСписка.Значение = "НашеПредприятие" Тогда
			Условие = "Партнеры.Ссылка = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)"; 
		Иначе
			Условие = "Партнеры." + ЭлементСписка; 
		КонецЕсли; 
		СтрокаОтбора = СтрокаОтбора + СтрокаУсловия + Условие;
		
	КонецЦикла;
	
	СтрокаОтбора = Прав(СтрокаОтбора,
	               СтрДлина(СтрокаОтбора) - ?(УстанавливатьОтборПоТипуПартнераКакИЛИ, СтрДлина(СтрокаУсловиеИли) - 1, СтрДлина(СтрокаУсловиеИ) - 1));
	
	Возврат "
	|И
	|	СправочникПартнеры.Ссылка В
	|		(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ИерархияПартнеров.Родитель КАК Ссылка
	|		ИЗ
	|			Справочник.Партнеры КАК Партнеры ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИерархияПартнеров КАК ИерархияПартнеров
	|				ПО
	|					ИерархияПартнеров.Партнер = Партнеры.Ссылка
	|		ГДЕ
	|			" + СтрокаОтбора + ")";
	
КонецФункции

Функция ЕстьРеквизитФормы(Источник, ИмяРеквизита) Экспорт
	
	ИскомыеРеквизиты = Новый Структура(ИмяРеквизита, NULL);
	ЗаполнитьЗначенияСвойств(ИскомыеРеквизиты, Источник);

	РеквизитСуществует = Ложь;
	Если ИскомыеРеквизиты[ИмяРеквизита] <> NULL Тогда
		РеквизитСуществует = Истина;
	КонецЕсли;
	
	Возврат РеквизитСуществует;

КонецФункции

#Область ИерархияПартнеров

Процедура ВыполнитьЗаписьПоПодчиненнымПартнерам(СтрокиДерева, ТекущийЭлементОбработки)
	
	Для каждого СтрокаДерева Из СтрокиДерева Цикл
		
		Если СтрокаДерева["Ссылка"] <> ТекущийЭлементОбработки Тогда
			ВыполнитьЗаписьВРегистрПоПартнеру(СтрокаДерева);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры


// Выполняет запись в РС "Иерархия Партнеров".
// 
// Параметры:
// 	СтрокаДерева - Неопределено, СтрокаДереваЗначений - Описание:
//   * Ссылка - СправочникСсылка.Партнеры - партнер, для которого определен родитель в иерархии.
//
Процедура ВыполнитьЗаписьВРегистрПоПартнеру(СтрокаДерева);
	
	НаборЗаписей = РегистрыСведений.ИерархияПартнеров.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Партнер.Установить(СтрокаДерева.Ссылка);
	
	РодительСтрокиДерева = СтрокаДерева;
	Пока РодительСтрокиДерева <> Неопределено Цикл
		ЗаписьНабора = НаборЗаписей.Добавить();
		ЗаписьНабора.Партнер  = СтрокаДерева.Ссылка;
		ЗаписьНабора.Родитель = РодительСтрокиДерева.Ссылка;
		ЗаписьНабора.Уровень  = РодительСтрокиДерева.Уровень();
		РодительСтрокиДерева  = РодительСтрокиДерева.Родитель;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
	ВыполнитьЗаписьПоПодчиненнымПартнерам(СтрокаДерева.Строки, СтрокаДерева.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииРаботыСФормами

// Формирует список установленных отборов по партнеру в форме списка.
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - содержит:
// 	* СписокОтборПоТипуПартнера - СписокЗначений - 
// 	
Процедура СформироватьСписокУстановленныхОтборовДинамическийСписокПартнеры(Форма)
	
	Если Форма.Параметры.Отбор.Свойство("Клиент") Тогда
		Форма.СписокОтборПоТипуПартнера.Добавить("Клиент");
	КонецЕсли;
	Если Форма.Параметры.Отбор.Свойство("Поставщик") Тогда
		Форма.СписокОтборПоТипуПартнера.Добавить("Поставщик");
	КонецЕсли;
	Если Форма.Параметры.Отбор.Свойство("Перевозчик") Тогда
		Форма.СписокОтборПоТипуПартнера.Добавить("Перевозчик");
	КонецЕсли;
	Если Форма.Параметры.Отбор.Свойство("Конкурент") Тогда
		Форма.СписокОтборПоТипуПартнера.Добавить("Конкурент");
	КонецЕсли;
	Если Форма.Параметры.Отбор.Свойство("ПрочиеОтношения") Тогда
		Форма.СписокОтборПоТипуПартнера.Добавить("ПрочиеОтношения");
	КонецЕсли;
	Если Форма.Параметры.Отбор.Свойство("ОбслуживаетсяТорговымиПредставителями") Тогда
		Форма.СписокОтборПоТипуПартнера.Добавить("ОбслуживаетсяТорговымиПредставителями");
	КонецЕсли;
	Если Форма.Параметры.Отбор.Свойство("НашеПредприятие") Тогда
		Форма.СписокОтборПоТипуПартнера.Добавить("НашеПредприятие");
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьВидимостьКомандОтчетов(Форма)
	ИмяФормы = Форма.ИмяФормы;
	
	Если СтрНайти(ИмяФормы, "ФормаСписка") Тогда
		ОтборыФормы = Форма.Параметры.Отбор;
		ЭтоФормаПоставщиков = ОтборыФормы.Свойство("Поставщик") И ОтборыФормы.Поставщик;
		ЭтоФормаКлиентов = ОтборыФормы.Свойство("Клиент") И ОтборыФормы.Клиент;
		
		Если Не Форма.Элементы.Найти("СписокОтчетСостояниеРасчетовСКлиентом") = Неопределено Тогда
			Форма.Элементы.СписокОтчетСостояниеРасчетовСКлиентом.Видимость = ЭтоФормаКлиентов;
		КонецЕсли;
		Если Не Форма.Элементы.Найти("СписокОтчетКарточкаРасчетовСКлиентом") = Неопределено Тогда
			Форма.Элементы.СписокОтчетКарточкаРасчетовСКлиентом.Видимость = ЭтоФормаКлиентов;
		КонецЕсли;
		Если Не Форма.Элементы.Найти("СписокОтчетВыручкаИСебестоимостьПродажПродажи") = Неопределено Тогда
			Форма.Элементы.СписокОтчетВыручкаИСебестоимостьПродажПродажи.Видимость = ЭтоФормаКлиентов;
		КонецЕсли;
		Если Не Форма.Элементы.Найти("СписокОтчетУсловияСоглашенийСКлиентомПоПартнеру") = Неопределено Тогда
			Форма.Элементы.СписокОтчетУсловияСоглашенийСКлиентомПоПартнеру.Видимость = ЭтоФормаКлиентов;
		КонецЕсли;
		Если Не Форма.Элементы.Найти("СписокОтчетСостояниеРасчетовСПоставщиком") = Неопределено Тогда
			Форма.Элементы.СписокОтчетСостояниеРасчетовСПоставщиком.Видимость = ЭтоФормаПоставщиков;
		КонецЕсли;
		Если Не Форма.Элементы.Найти("СписокОтчетКарточкаРасчетовСПоставщиком") = Неопределено Тогда
			Форма.Элементы.СписокОтчетКарточкаРасчетовСПоставщиком.Видимость = ЭтоФормаПоставщиков;
		КонецЕсли;
		Если Не Форма.Элементы.Найти("СписокОтчетУсловияСоглашенийСПоставщикомПоПартнеру") = Неопределено Тогда
			Форма.Элементы.СписокОтчетУсловияСоглашенийСПоставщикомПоПартнеру.Видимость = ЭтоФормаПоставщиков;
		КонецЕсли;
	КонецЕсли;
	
	ПартнерыИКонтрагенты.ЗаголовокРеквизитаВЗависимостиОтФОИспользоватьПартнеровКакКонтрагентов(
	                   Форма,"ОтчетДосьеПартнера", НСтр("ru = 'Досье'"));
	
КонецПроцедуры

#КонецОбласти

#Область ОбщиеПроцедурыФормЭлементаСправочникаПартнеры

Процедура ПартнерФормаЭлементаПриСозданииЧтенииНаСервере(Форма)

	ПартнерФормаЭлементаУправлениеДоступностью(Форма);
	Форма.ТекущийБизнесРегион = Форма.Объект.БизнесРегион;
	ПартнерФормаЭлементаНастроитьПанельНавигации(Форма);
	Форма.Элементы.ГруппаДополнительнаяИнформация.Картинка = ОбщегоНазначенияКлиентСервер.КартинкаКомментария(Форма.Объект.ДополнительнаяИнформация);
	
КонецПроцедуры

Процедура ПартнерФормаЭлементаНастроитьПанельНавигации(Форма)

	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("ИспользоватьДанныеПартнераКлиента", Форма.Объект.Клиент);
	СтруктураНастроек.Вставить("ИспользоватьДанныеПартнераПоставщика", Форма.Объект.Поставщик);
	СтруктураНастроек.Вставить("ИспользоватьДанныеТорговогоПредставителя", Форма.Объект.ОбслуживаетсяТорговымиПредставителями);
	СтруктураНастроек.Вставить("ИспользоватьДанныеПоставщикаИлиКонкурента", Форма.Объект.Поставщик ИЛИ Форма.Объект.Конкурент);
	СтруктураНастроек.Вставить("ИспользоватьДанныеКонтрагентаФизическогоЛица", ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов")
	                                                                           И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ЮрФизЛицо")
	                                                                           И (Форма.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Или Форма.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель));
	СтруктураНастроек.Вставить("ИспользоватьДанныеПартнераСоглашенияСКлиентами", Форма.Объект.Клиент 
	                                                                             И ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами"));
	СтруктураНастроек.Вставить("ИспользоватьДанныеПартнераСделкиСКлиентами",   Форма.Объект.Клиент 
	                                                                             И ПолучитьФункциональнуюОпцию("ИспользоватьСделкиСКлиентами"));
	СтруктураНастроек.Вставить("ИспользоватьДоговорыКредитовИДепозитов", Форма.Объект.ПрочиеОтношения И ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыКредитовИДепозитов"));
	
	ОбщегоНазначенияУТ.НастроитьФормуПоПараметрам(Форма, СтруктураНастроек);

КонецПроцедуры


// Получает первичный интерес по партнеру.
// 
// Параметры:
// 	Партнер       - СправочникСсылка.Партнеры - для которого получается первичный интерес.
// 	ТолькоПартнер - Булево - признак того, что не нужно учитывать интерес по сделкам.
// Возвращаемое значение:
// 	Структура - Описание:
// * КаналПервичногоИнтереса     - ПланВидовХарактеристикСсылка.КаналыРекламныхВоздействий -
// * ИсточникПервичногоИнтереса  - СправочникСсылка - 
// * Сделка                      - СправочникСсылка.СделкиСКлиентами - 
//
Функция ПервичныйИнтерес(Партнер, ТолькоПартнер = Ложь)

	Интерес = Новый Структура("ИсточникПервичногоИнтереса, Сделка");
	Интерес.Вставить(
		"КаналПервичногоИнтереса",
		ПланыВидовХарактеристик.КаналыРекламныхВоздействий.ПустаяСсылка());

	Запрос = Новый Запрос;
	
	ТекстЗапроса = "ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИсточникиПервичногоИнтереса.КаналПервичногоИнтереса,
		|	ИсточникиПервичногоИнтереса.ИсточникПервичногоИнтереса,
		|	ИсточникиПервичногоИнтереса.Сделка
		|ИЗ
		|	РегистрСведений.ИсточникиПервичногоИнтереса КАК ИсточникиПервичногоИнтереса
		|ГДЕ
		|	ИсточникиПервичногоИнтереса.Партнер = &Партнер";
		
	Если ТолькоПартнер Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	И ИсточникиПервичногоИнтереса.Сделка = ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)";
	КонецЕсли;	

	ТекстЗапроса = ТекстЗапроса + "
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИсточникиПервичногоИнтереса.Сделка.ДатаНачала";
		
	Запрос.Текст = ТекстЗапроса;

	Запрос.УстановитьПараметр("Партнер", Партнер);

	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Следующий() Тогда
		Интерес.КаналПервичногоИнтереса		= Выборка.КаналПервичногоИнтереса;
		Интерес.ИсточникПервичногоИнтереса	= Выборка.ИсточникПервичногоИнтереса;
		Интерес.Сделка						= Выборка.Сделка;
	КонецЕсли;

	Возврат Интерес;

КонецФункции

// Фиксирует первичный интерес при записи из формы.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - содержит:
//   * Объект - СправочникОбъект.Партнеры -
//            - СправочникОбъект.СделкиСКлиентами -
Процедура ЗафиксироватьПервичныйИнтерес(Форма)

	Интерес = ПервичныйИнтерес(Форма.Объект.Ссылка, Истина);
	Если Форма.КаналПервичногоИнтереса <> Интерес.КаналПервичногоИнтереса
	 Или Форма.ИсточникПервичногоИнтереса <> Интерес.ИсточникПервичногоИнтереса Тогда

		УстановитьПривилегированныйРежим(Истина);
		Запись = РегистрыСведений.ИсточникиПервичногоИнтереса.СоздатьМенеджерЗаписи();
		Запись.Партнер = Форма.Объект.Ссылка;

		Если Не ЗначениеЗаполнено(Форма.КаналПервичногоИнтереса)
		   И Не ЗначениеЗаполнено(Форма.ИсточникПервичногоИнтереса) Тогда
			Запись.Удалить();
		Иначе
			Запись.КаналПервичногоИнтереса    = Форма.КаналПервичногоИнтереса;
			Запись.ИсточникПервичногоИнтереса = Форма.ИсточникПервичногоИнтереса;
			Запись.Записать();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункции

Процедура ЗаполнитьСписокПартнеровИзДерева(СтрокиДерева, СписокПартнеров, Партнер)
	
	Для каждого СтрокаДерева Из СтрокиДерева Цикл
		СписокПартнеров.Добавить(СтрокаДерева.Ссылка, СтрокаДерева.Наименование);
		Если СтрокаДерева.Ссылка <> Партнер Тогда
			ЗаполнитьСписокПартнеровИзДерева(СтрокаДерева.Строки, СписокПартнеров,Партнер);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Осуществляет расширенный поиск партнеров
// Параметры:
//   СтрокаПоиска    - Строка - по ней выполняется поиск.
//   СписокПартнеров - ТаблицаЗначений - заполняется результатами поиска.
// Возвращаемое значение:
//    Строка, Неопределено - текстовое предствление результата выполненного поиска.
//
Функция НайтиПартнеров(СтрокаПоиска, СписокПартнеров)

	// настроить параметры поиска
	мОбластьПоиска = Новый Массив;
	РазмерПорции = 200;
	СписокПоиска = ПолнотекстовыйПоиск.СоздатьСписок(СтрокаПоиска, РазмерПорции);
	мОбластьПоиска.Добавить(Метаданные.Справочники.Партнеры);
	мОбластьПоиска.Добавить(Метаданные.Справочники.Контрагенты);
	мОбластьПоиска.Добавить(Метаданные.Справочники.КонтактныеЛицаПартнеров);
	мОбластьПоиска.Добавить(Метаданные.Справочники.ДоговорыКонтрагентов);
	СписокПоиска.ОбластьПоиска = мОбластьПоиска;
	
	Попытка
		СписокПоиска.ПерваяЧасть();
	Исключение
		Возврат НСтр("ru = 'При выполнении поиска произошла ошибка, попробуйте изменить выражение поиска.'");
	КонецПопытки;
	
	// возврат, если поиск не результативен
	Если СписокПоиска.ПолноеКоличество() = 0 Тогда
		Возврат НСтр("ru = 'Ничего не найдено'");
	КонецЕсли;
	
	Если СписокПоиска.СлишкомМногоРезультатов() Тогда
		Возврат НСтр("ru = 'Слишком много результатов, уточните запрос.'");
	КонецЕсли;
	
	КоличествоЭлементов = СписокПоиска.ПолноеКоличество();
	
	// сформировать список найденных партнеров
	СписокПартнеров.Очистить();
	НачальнаяПозиция = 0;
	КонечнаяПозиция = ?(КоличествоЭлементов > РазмерПорции, РазмерПорции, КоличествоЭлементов)-1;
	ЕстьСледующаяПорция = Истина;

	// обработать по порциям результаты ППД
	Пока ЕстьСледующаяПорция Цикл
		Для СчетчикЭлементов = 0 По КонечнаяПозиция Цикл
			
			// сформировать элемент результата
			Элемент = СписокПоиска.Получить(СчетчикЭлементов);
			ЭлементСсылка = Элемент.Значение.Ссылка;
			Основание = Элемент.Метаданные.ПредставлениеОбъекта + " """
			            + Элемент.Представление + """ - " + Элемент.Описание;
			Если Элемент.Метаданные = Метаданные.Справочники.Партнеры Тогда
				Партнер = Элемент.Значение;
				Основание = НСтр("ru = 'Найдено в реквизите партнера'") + " - " + Элемент.Описание;
			ИначеЕсли Элемент.Метаданные = Метаданные.Справочники.КонтактныеЛицаПартнеров Тогда
				Партнер = Элемент.Значение.Владелец;
				ШаблонОснования =  НСтр("ru = 'Найдено в реквизите контактного лица партнера ""%1"" - %2'");
				Основание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОснования, Элемент.Значение, Элемент.Описание); 
			ИначеЕсли Элемент.Метаданные = Метаданные.Справочники.ДоговорыКонтрагентов Тогда
				Партнер = Элемент.Значение.Партнер;
				ШаблонОснования =  НСтр("ru = 'Найдено в договоре партнера ""%1"" - %2'");
				Основание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОснования, Элемент.Значение, Элемент.Описание);
			ИначеЕсли Элемент.Метаданные = Метаданные.Справочники.Контрагенты Тогда
				Партнер = Элемент.Значение.Партнер;
				Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
					Основание = НСтр("ru = 'Найдено в реквизите партнера'") + " - "+ Элемент.Описание;
					ЭлементСсылка = Партнер;
				Иначе
					ШаблонОснования =  НСтр("ru = 'Найдено в контрагенте партнера ""%1"" - %2'");
					Основание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОснования, Элемент.Значение, Элемент.Описание);
				КонецЕсли;
				
			ИначеЕсли Не ЗначениеЗаполнено(Элемент.Значение.Партнер) Тогда
				Продолжить;
			КонецЕсли;
			
			Если НЕ ДобавитьПартнераВСписокНайденныхПолнотекстовымПоиском(СписокПартнеров, Партнер, Основание, ЭлементСсылка) Тогда
				Возврат НСтр("ru = 'Слишком много результатов, уточните запрос.'");
			КонецЕсли;
			
		КонецЦикла;
		НачальнаяПозиция = НачальнаяПозиция + РазмерПорции;
		ЕстьСледующаяПорция = (НачальнаяПозиция < КоличествоЭлементов - 1);
		Если ЕстьСледующаяПорция Тогда
			КонечнаяПозиция = 
			?(КоличествоЭлементов > НачальнаяПозиция + РазмерПорции, РазмерПорции,
			КоличествоЭлементов - НачальнаяПозиция) - 1;
			СписокПоиска.СледующаяЧасть();
		КонецЕсли;
	КонецЦикла;
	
	Если СписокПартнеров.Количество() = 0 Тогда
		Возврат НСтр("ru = 'Ничего не найдено.'");
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

Функция ДобавитьПартнераВСписокНайденныхПолнотекстовымПоиском(СписокПартнеров, Партнер, Основание, ЭлементСсылка)
	
	// добавить элемент, если партнера еще нет в списке найденных
	НайденнаяСтрока = СписокПартнеров.Найти(Партнер,"Партнер"); 
	Если НайденнаяСтрока = Неопределено Тогда
		// ограничить количество возвращаемых партнеров
		Если СписокПартнеров.Количество() > 100 Тогда
			Возврат Ложь; 
		Иначе 
			Запись = СписокПартнеров.Добавить(); 
			Запись.Партнер = Партнер;
			Запись.Основание = Основание;
			Запись["Ссылка"] = ЭлементСсылка;
		КонецЕсли;
		
	Иначе
		
		Если (ТипЗнч(НайденнаяСтрока["Ссылка"]) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров")
			ИЛИ ТипЗнч(НайденнаяСтрока["Ссылка"]) = Тип("СправочникСсылка.Контрагенты")
			ИЛИ ТипЗнч(НайденнаяСтрока["Ссылка"]) = Тип("СправочникСсылка.ДоговорыКонтрагентов"))
			И ТипЗнч(ЭлементСсылка) = Тип("СправочникСсылка.Партнеры") Тогда
		
			НайденнаяСтрока.Партнер = Партнер;
			НайденнаяСтрока.Основание = Основание;
			НайденнаяСтрока["Ссылка"] = ЭлементСсылка;
		
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура ДополнитьСписокВыбораТипФильтраСписокПартнеров(Форма, СписокВыбора)
	
	ОписаниеТиповБулево = Новый ОписаниеТипов("Булево");
	Если Форма.СписокОтборПоТипуПартнера.Количество() > 0 Тогда
		ТекстОтбораДопРеквизиты = "";
		
		Для каждого ЭлементСписка Из Форма.СписокОтборПоТипуПартнера Цикл
		
			ТекстОтбораДопРеквизиты = ТекстОтбораДопРеквизиты + "
			| ИЛИ НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка = &Набор_Справочник_Партнеры_" 
			+ ИмяНабораСведений(ЭлементСписка.Значение);
		
		КонецЦикла;
		
	Иначе
		
		ТекстОтбораДопРеквизиты = "
		| ИЛИ НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка = &Набор_Справочник_Партнеры_Конкуренты
		| ИЛИ НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка = &Набор_Справочник_Партнеры_Клиенты
		| ИЛИ НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка = &Набор_Справочник_Партнеры_Поставщики
		| ИЛИ НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка = &Набор_Справочник_Партнеры_Прочие";
		
	КонецЕсли;
	
	ТекстОтбораСвойства = СтрЗаменить(ТекстОтбораДопРеквизиты,
	                                 "НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты",
	                                 "НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство,
	|	ИСТИНА КАК ЭтоРеквизит
	|ПОМЕСТИТЬ ДопРеквизитыИСведения
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты
	|ГДЕ
	|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка = &Набор_Справочник_Партнеры_Общие " + ТекстОтбораДопРеквизиты + "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения.Свойство,
	|	ЛОЖЬ КАК ЭтоРеквизит
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеСведения КАК НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения
	|ГДЕ
	|	НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения.Ссылка = &Набор_Справочник_Партнеры_Общие " + ТекстОтбораСвойства + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДополнительныеРеквизитыИСведения.Ссылка КАК ДопРеквизитСведение,
	|	ДопРеквизитыИСведения.ЭтоРеквизит,
	|	ДополнительныеРеквизитыИСведения.Представление,
	|	ДополнительныеРеквизитыИСведения.ТипЗначения
	|ИЗ
	|	ДопРеквизитыИСведения КАК ДопРеквизитыИСведения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|		ПО ДопРеквизитыИСведения.Свойство = ДополнительныеРеквизитыИСведения.Ссылка";
	
	Запрос.УстановитьПараметр("Набор_Справочник_Партнеры_Общие", УправлениеСвойствами.НаборСвойствПоИмени("Справочник_Партнеры_Общие"));
	Запрос.УстановитьПараметр("Набор_Справочник_Партнеры_Прочие", УправлениеСвойствами.НаборСвойствПоИмени("Справочник_Партнеры_Прочие"));
	Запрос.УстановитьПараметр("Набор_Справочник_Партнеры_Поставщики", УправлениеСвойствами.НаборСвойствПоИмени("Справочник_Партнеры_Поставщики"));
	Запрос.УстановитьПараметр("Набор_Справочник_Партнеры_Клиенты", УправлениеСвойствами.НаборСвойствПоИмени("Справочник_Партнеры_Клиенты"));
	Запрос.УстановитьПараметр("Набор_Справочник_Партнеры_Конкуренты", УправлениеСвойствами.НаборСвойствПоИмени("Справочник_Партнеры_Конкуренты"));
	
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ТипЗначения = ОписаниеТиповБулево Тогда
			НоваяСтрока = Форма.ТаблицаДопРеквизитовСвойствТипаБулево.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка);
		Иначе
			СписокВыбора.Добавить(Выборка.ДопРеквизитСведение, НРег(Лев(Выборка.Представление,1)) + Прав(Выборка.Представление,СтрДлина(Выборка.Представление) - 1));
		КонецЕсли;
		
	КонецЦикла;
	
	Если Форма.ТаблицаДопРеквизитовСвойствТипаБулево.Количество() > 0 Тогда
		
		СписокВыбора.Добавить("Категории", НСтр("ru = 'категориям'"));
		
	КонецЕсли;
	
КонецПроцедуры

Функция ИмяНабораСведений(ИмяОтбора)
	
	Если ИмяОтбора = "Клиент" Тогда
		Возврат "Клиенты";
	ИначеЕсли ИмяОтбора = "Поставщик" Тогда
		Возврат "Поставщики";
	ИначеЕсли ИмяОтбора = "Конкурент" Тогда
		Возврат "Конкуренты";
	Иначе
		Возврат "Прочие";
	КонецЕсли;
	
КонецФункции

// Формирует строку, в которой содержатся через запятую все возможные типы отношений партнера.
//
// Параметры:
//  ДанныеПартнера  - ВыборкаИзРезультатаЗапроса, Структура - содержит данные о типах отношений с партнером.
//
// Возвращаемое значение:
//   Строка  - сформированная строковая информация о типах установленных отношений с партнером.
//
Функция СформироватьСтрокуТекущихТиповОтношений(ДанныеПартнера) Экспорт

	СтрокаТипОтношений = ?(ДанныеПартнера.Клиент, НСтр("ru = 'Клиент'"),"") + ?(ДанныеПартнера.Поставщик, НСтр("ru = ', Поставщик'"),"")
	                     + ?(ДанныеПартнера.Конкурент, НСтр("ru = ', Конкурент'"),"") + ?(ДанныеПартнера.ПрочиеОтношения, НСтр("ru = ', Прочие отношения'"),"");
	Если Лев(СтрокаТипОтношений, 2) = ", " Тогда
		СтрокаТипОтношений = Прав(СтрокаТипОтношений,СтрДлина(СтрокаТипОтношений) - 2);
	КонецЕсли;
	
	Возврат СтрокаТипОтношений;

КонецФункции

// Получает необходимые данные о партнере для вывода в панель информации форм списка 
// и выбора справочника "Партнеры".
//
// Параметры:
//  Партнер  - СправочникСсылка.Партнеры - партнер для которого необходимо получить информацию.
//
// Возвращаемое значение:
//   Структура   - содержит информацию о партнере.
//
Функция ДанныеПартнераДляПанелиИнформации(Партнер) Экспорт
	
	ДанныеПартнера = Новый Структура;
	ДанныеПартнера.Вставить("Наименование", "");
	ДанныеПартнера.Вставить("БизнесРегион", "");
	ДанныеПартнера.Вставить("КонтактнаяИнформация", "");
	ДанныеПартнера.Вставить("КоличествоКонтактныхЛиц", 0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Партнеры.Наименование КАК Наименование,
	|	ПРЕДСТАВЛЕНИЕ(Партнеры.БизнесРегион) КАК БизнесРегион
	|ИЗ
	|	Справочник.Партнеры КАК Партнеры
	|ГДЕ
	|	Партнеры.Ссылка = &Партнер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВидыКонтактнойИнформацииПредставления.Наименование, ВидыКонтактнойИнформации.Наименование) КАК ВидКИ,
	|	ЕСТЬNULL(ПартнерыКонтактнаяИнформация.Представление, """") КАК ПредставлениеКИ,
	|	ВЫБОР
	|		КОГДА ВидыКонтактнойИнформации.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЗначениеУпорядочивания,
	|	ВидыКонтактнойИнформации.Ссылка КАК СсылкаВидКИ
	|ИЗ
	|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
	|		ПО (ПартнерыКонтактнаяИнформация.Вид = ВидыКонтактнойИнформации.Ссылка)
	|			И (ПартнерыКонтактнаяИнформация.Ссылка = &Партнер)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыКонтактнойИнформации.Представления КАК ВидыКонтактнойИнформацииПредставления
	|		ПО (ВидыКонтактнойИнформацииПредставления.Ссылка = ВидыКонтактнойИнформации.Ссылка
	|				И ВидыКонтактнойИнформацииПредставления.КодЯзыка = &КодЯзыка)
	|ГДЕ
	|	ВидыКонтактнойИнформации.Родитель = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.СправочникПартнеры)
	|	И НЕ ВидыКонтактнойИнформации.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗначениеУпорядочивания,
	|	ВидКИ
	|ИТОГИ ПО
	|	СсылкаВидКИ
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ КонтактныеЛицаПартнеров.Ссылка) КАК КоличествоКЛ
	|ИЗ
	|	Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
	|ГДЕ
	|	КонтактныеЛицаПартнеров.Владелец = &Партнер
	|	И НЕ КонтактныеЛицаПартнеров.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////";
	
	Запрос.УстановитьПараметр("Партнер", Партнер);
	Запрос.УстановитьПараметр("КодЯзыка", ТекущийЯзык().КодЯзыка);
	
	Результат = Запрос.ВыполнитьПакет();
	
	РезультатДанныеПартнера = Результат[0]; // РезультатЗапроса - 
	ВыборкаДанныеПартнера = РезультатДанныеПартнера.Выбрать();
	Если ВыборкаДанныеПартнера.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ДанныеПартнера, ВыборкаДанныеПартнера);
	КонецЕсли;
	
	КонтактнаяИнформацияТаблица = Новый ТаблицаЗначений;
	КонтактнаяИнформацияТаблица.Колонки.Добавить("ВидКИ");
	КонтактнаяИнформацияТаблица.Колонки.Добавить("ПредставлениеКИ");
	
	РезультатЗапросаВидыКИ = Результат[1]; // РезультатЗапроса 
	ВыборкаВидыКИ = РезультатЗапросаВидыКИ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаВидыКИ.Следующий() Цикл
		ВыборкаДетали = ВыборкаВидыКИ.Выбрать();
		ПредставлениеКИ = "";
		Пока ВыборкаДетали.Следующий() Цикл
			ПредставлениеКИ = ПредставлениеКИ + ?(ПустаяСтрока(ПредставлениеКИ),"" ,"; ") + ВыборкаДетали.ПредставлениеКИ; 
		КонецЦикла;
		НоваяСтрока = КонтактнаяИнформацияТаблица.Добавить();
		НоваяСтрока.ВидКИ = ВыборкаДетали.ВидКИ;
		НоваяСтрока.ПредставлениеКИ = ПредставлениеКИ;
	КонецЦикла;
	ДанныеПартнера.Вставить("КонтактнаяИнформацияТаблица",КонтактнаяИнформацияТаблица);
	
	РезультатЗапросаКонактныеЛица = Результат[2]; // РезультатЗапроса 
	ВыборкаКоличествоКЛ = РезультатЗапросаКонактныеЛица.Выбрать();
	Если ВыборкаКоличествоКЛ.Следующий() Тогда
		
		ДанныеПартнера.КоличествоКонтактныхЛиц = ВыборкаКоличествоКЛ.КоличествоКЛ;
		
	КонецЕсли;

	ПартнерыИКонтрагентыЛокализация.ДополнитьДанныеПартнераДляПанелиИнформации(ДанныеПартнера, Партнер);
	
	Возврат ДанныеПартнера;
	
КонецФункции

// Формирует список выбора для выбора типа фильтра.
//
// Параметры:
//  Форма        - ФормаКлиентскогоПриложения - форма, для которой формируется список выбора.
//  СписокВыбора - СписокЗначений   - сформированный список выбора.
//
Процедура ЗаполнитьСписокВыбораТипФильтраСписокПартнеров(Форма, СписокВыбора)
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьБизнесРегионы") 
		И ПравоДоступа("Чтение", Метаданные.Справочники.БизнесРегионы) Тогда
		СписокВыбора.Добавить("БизнесРегионы", НСтр("ru = 'бизнес-региону'"));
	КонецЕсли;
	
	Если ПравоДоступа("Чтение", Метаданные.Справочники.Пользователи) Тогда
		СписокВыбора.Добавить("Менеджер", НСтр("ru = 'основному менеджеру'"));
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГруппыДоступаПартнеров")
		И ПравоДоступа("Чтение", Метаданные.Справочники.ГруппыДоступаПартнеров) Тогда
		СписокВыбора.Добавить("ГруппыДоступа", НСтр("ru = 'группе доступа'"));
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеРеквизитыИСведения") Тогда
		ДополнитьСписокВыбораТипФильтраСписокПартнеров(Форма, СписокВыбора);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьЭлементКонтактнойИнформации(Форма, ВидКонтактнойИнформации, СтруктураДанных) Экспорт
	
	Если СтруктураДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки = ДанныеСтрокиКонтактнойИнформацииПоВиду(Форма, ВидКонтактнойИнформации);
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ДанныеСтроки.Представление = СтруктураДанных.Представление;
	ДанныеСтроки.Значение = СтруктураДанных.КонтактнаяИнформация;
	Форма[ДанныеСтроки.ИмяРеквизита] = СтруктураДанных.Представление;
	
КонецПроцедуры

Функция ДанныеСтрокиКонтактнойИнформацииПоВиду(Форма, ВидКонтактнойИнформации) Экспорт
	
	Отбор  = Новый Структура("Вид", ВидКонтактнойИнформации);
	Строки = Форма.КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(Отбор);
	Возврат ?(Строки.Количество() = 0, Неопределено, Строки[0]);
	
КонецФункции

Процедура УдалитьСтрокуСВидомКИ(КонтактнаяИнформация, ВидКИ)
	
	НайденныеСтроки = КонтактнаяИнформация.НайтиСтроки(Новый Структура("Вид", ВидКИ));
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		КонтактнаяИнформация.Удалить(НайденнаяСтрока);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбщиеПроцедурыИФункцииФормСпискаИВыбораСправочникаПартнеры

Процедура ЗаполнитьДеревоКатегорий(Форма)
	
	Форма.Категории.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СвойстваТипаБулево.ДопРеквизитСведение КАК Свойство,
	|	СвойстваТипаБулево.ЭтоРеквизит
	|ПОМЕСТИТЬ СвойстваТипаБулево
	|ИЗ
	|	&СвойстваТипаБулево КАК СвойстваТипаБулево
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ПартнерыДополнительныеРеквизиты.Свойство КАК Значение,
	|	ПРЕДСТАВЛЕНИЕ(ПартнерыДополнительныеРеквизиты.Свойство) КАК Представление
	|ИЗ
	|	Справочник.Партнеры.ДополнительныеРеквизиты КАК ПартнерыДополнительныеРеквизиты
	|ГДЕ
	|	ПартнерыДополнительныеРеквизиты.Свойство В
	|			(ВЫБРАТЬ
	|				СвойстваТипаБулево.Свойство
	|			ИЗ
	|				СвойстваТипаБулево КАК СвойстваТипаБулево
	|			ГДЕ
	|				СвойстваТипаБулево.ЭтоРеквизит)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ДополнительныеСведения.Свойство,
	|	ПРЕДСТАВЛЕНИЕ(ДополнительныеСведения.Свойство)
	|ИЗ
	|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|ГДЕ
	|	ДополнительныеСведения.Свойство В
	|			(ВЫБРАТЬ
	|				СвойстваТипаБулево.Свойство
	|			ИЗ
	|				СвойстваТипаБулево КАК СвойстваТипаБулево
	|			ГДЕ
	|				НЕ СвойстваТипаБулево.ЭтоРеквизит)";
	
	Запрос.УстановитьПараметр("СвойстваТипаБулево", Форма.ТаблицаДопРеквизитовСвойствТипаБулево.Выгрузить());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтрокаТаблицы = Форма.Категории.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);
		СтрокаТаблицы.НомерКартинки = 0;
		
	КонецЦикла;
	
	ДобавитьСтрокуВПанельНавигации(Форма.Категории,Нстр("ru = 'Все'"),1);
	
	Форма.ТекущееСвойствоПанелиНавигации = Форма.ТипФильтра;
	
КонецПроцедуры

Процедура ПроверитьНаличиеЭлементаОтбора(ЭлементыОтбора, ПолеКомпоновкиДанных, ВидСравнения, Результат)
	
	Для каждого ЭлементОтбора Из ЭлементыОтбора Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			
			ПроверитьНаличиеЭлементаОтбора(ЭлементОтбора.Элементы, ПолеКомпоновкиДанных, ВидСравнения, Результат)
			
		Иначе
			
			Если ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновкиДанных
				И ЭлементОтбора.ВидСравнения = ВидСравнения Тогда
				Результат = Истина;
				Прервать;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Результат Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОпределитьРежимИспользованияПоискаВСписке(Форма)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ИспользоватьРасширенныйПоиск") Тогда
	
		ИспользоватьРасширенныйПоискНастройки = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ИспользованиеПолнотекстовогоПоискаВСпискеПартнеров",
		                                                                                         "НастройкиПользователяИспользованиеПартнеров",
		                                                                                          Неопределено);
		
		Форма.ИспользоватьРасширенныйПоиск = ?(ИспользоватьРасширенныйПоискНастройки = Неопределено, Истина, Форма.ИспользоватьРасширенныйПоиск);
		
		ПриИзмененииРежимаИспользованияПолнотекстовогоПоиска(Форма, Ложь);
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДеревоСвойств(Форма)
	
	Форма.Свойства.ПолучитьЭлементы().Очистить();
	
	Запрос = Новый Запрос;
	
	Если Форма.ТипФильтра.ЭтоДополнительноеСведение Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ДополнительныеСведения.Значение КАК Значение,
		|	ПРЕДСТАВЛЕНИЕ(ДополнительныеСведения.Значение) КАК Представление
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Свойство = &Свойство
		|	И ДополнительныеСведения.Объект ССЫЛКА Справочник.Партнеры
		|
		|УПОРЯДОЧИТЬ ПО
		|	Значение
		|ИТОГИ ПО
		|	Значение ИЕРАРХИЯ";
		
	Иначе
	
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ПартнерыДополнительныеРеквизиты.Значение КАК Значение,
		|	ПРЕДСТАВЛЕНИЕ(ПартнерыДополнительныеРеквизиты.Значение) КАК Представление
		|ИЗ
		|	Справочник.Партнеры.ДополнительныеРеквизиты КАК ПартнерыДополнительныеРеквизиты
		|ГДЕ
		|	ПартнерыДополнительныеРеквизиты.Свойство = &Свойство
		|
		|УПОРЯДОЧИТЬ ПО
		|	Значение
		|ИТОГИ ПО
		|	Значение ИЕРАРХИЯ";
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Свойство",Форма.ТипФильтра);
	
	Результат = Запрос.Выполнить();
	Дерево = Результат.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ДобавитьСтрокуВПанельНавигации(Форма.Свойства,Нстр("ru = 'Не указан'"));
	
	СтрокиПервыйУровень = Форма.Свойства.ПолучитьЭлементы(); // КоллекцияСтрокДереваЗначений 
	
	Для Каждого Строка Из Дерево.Строки Цикл
		СтрокаСвойства = СтрокиПервыйУровень.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСвойства, Строка);
		СтрокаСвойства.НомерКартинки = 1;
		ДобавитьСтрокиВДеревоНавигации(Строка, СтрокаСвойства,Истина);
	КонецЦикла;
	
	ДобавитьСтрокуВПанельНавигации(Форма.Свойства,Нстр("ru = 'Все'"),2);

	Форма.ТекущееСвойствоПанелиНавигации = Форма.ТипФильтра;
	
КонецПроцедуры

Функция ДобавитьСтрокуВПанельНавигации(ДанныеФормыКоллекция, ИмяСтроки, НомерКартинки = 0)
	
	Если ТипЗнч(ДанныеФормыКоллекция) = Тип("ДанныеФормыДерево") Тогда
		НоваяСтрока = ДанныеФормыКоллекция.ПолучитьЭлементы().Добавить();
	Иначе
		НоваяСтрока = ДанныеФормыКоллекция.Добавить();
	КонецЕсли;
	
		НоваяСтрока.Значение = ИмяСтроки;
		НоваяСтрока.Представление = ИмяСтроки;
		НоваяСтрока.НомерКартинки = НомерКартинки;
	
	Возврат НоваяСтрока;
	
КонецФункции

Процедура ДобавитьСтрокиВДеревоНавигации(РодительскаяСтрока, СтрокаРодитель, ВыполнятьПроверку = Ложь)
	
	Для Каждого Строка Из РодительскаяСтрока.Строки Цикл
		
		Если Строка.Значение = РодительскаяСтрока.Значение Или Строка.Значение = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокиДерева = СтрокаРодитель.ПолучитьЭлементы(); // КоллекцияСтрокДереваЗначений - 
		
		НоваяСтрока = СтрокиДерева.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.НомерКартинки = 1;
		
		ДобавитьСтрокиВДеревоНавигации(Строка, НоваяСтрока);
		
	КонецЦикла;
	
КонецПроцедуры

Функция СписокЗначимыхЗначенийПанелиНавигации(ИмяСписка)
	
	Если ИмяСписка = "БизнесРегионы" Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Партнеры.БизнесРегион КАК Ссылка
		|ИЗ
		|	Справочник.Партнеры КАК Партнеры";
		
	ИначеЕсли ИмяСписка = "ГруппыДоступаПартнеров" Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Партнеры.ГруппаДоступа КАК Ссылка
		|ИЗ
		|	Справочник.Партнеры КАК Партнеры";
		
	ИначеЕсли ИмяСписка = "Менеджеры" Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Партнеры.ОсновнойМенеджер КАК Ссылка
		|ИЗ
		|	Справочник.Партнеры КАК Партнеры";
		
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Функция ТекстЗапросаДинамическогоСпискаПартнеры(ЗапросСИнформациейПоКонтрагенту)

	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Контрагенты.Ссылка) КАК Ссылка,
		|	Контрагенты.Партнер КАК Партнер
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|
		|СГРУППИРОВАТЬ ПО
		|	Контрагенты.Партнер
		|
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Контрагенты.Ссылка) > 1";
		
		УстановитьПривилегированныйРежим(Истина);
		
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			ЗапросСИнформациейПоКонтрагенту = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗапросСИнформациейПоКонтрагенту Тогда
		ТекстЗапроса = ПартнерыИКонтрагентыЛокализация.ТекстЗапросаДинамическогоСпискаПартнерыСИнформациейПоКонтрагенту();
		Если ПустаяСтрока(ТекстЗапроса) Тогда
			ТекстЗапроса = "
			|ВЫБРАТЬ
			|	СправочникПартнеры.Ссылка,
			|	СправочникПартнеры.ПометкаУдаления,
			|	СправочникПартнеры.Предопределенный,
			|	СправочникПартнеры.Родитель,
			|	СправочникПартнеры.Код,
			|	СправочникПартнеры.Наименование,
			|	СправочникПартнеры.БизнесРегион,
			|	СправочникПартнеры.ГруппаДоступа,
			|	СправочникПартнеры.ДатаРегистрации,
			|	СправочникПартнеры.Клиент,
			|	СправочникПартнеры.Комментарий,
			|	СправочникПартнеры.Конкурент,
			|	СправочникПартнеры.НаименованиеПолное,
			|	СправочникПартнеры.ОсновнойМенеджер,
			|	СправочникПартнеры.Поставщик,
			|	СправочникПартнеры.Перевозчик,
			|	СправочникПартнеры.ПрочиеОтношения,
			|	СправочникПартнеры.ДополнительныеРеквизиты.(
			|		Ссылка,
			|		НомерСтроки,
			|		Свойство,
			|		Значение,
			|		ТекстоваяСтрока
			|	),
			|	СправочникПартнеры.КонтактнаяИнформация.(
			|		Ссылка,
			|		НомерСтроки,
			|		Тип,
			|		Вид,
			|		Представление,
			|		ЗначенияПолей,
			|		Страна,
			|		Регион,
			|		Город,
			|		АдресЭП,
			|		ДоменноеИмяСервера,
			|		НомерТелефона,
			|		НомерТелефонаБезКодов
			|	),
			|	ЕСТЬNULL(Контрагенты.Ссылка, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК Контрагент,
			|	ЕСТЬNULL(Контрагенты.ЮрФизЛицо, ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ПустаяСсылка)) КАК ЮрФизЛицо,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(Контрагенты.ОбособленноеПодразделение, ЛОЖЬ)
			|			ТОГДА Контрагенты.ГоловнойКонтрагент
			|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
			|	КОНЕЦ КАК ГоловнойКонтрагент,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(Контрагенты.ОбособленноеПодразделение, ЛОЖЬ)
			|			ТОГДА &ПредставлениеОбособленногоПодразделения
			|		ИНАЧЕ ЕСТЬNULL(ПРЕДСТАВЛЕНИЕ(Контрагенты.ЮрФизЛицо), """")
			|	КОНЕЦ КАК ВидКонтрагента,
			|	ЕСТЬNULL(Контрагенты.ОбособленноеПодразделение, ЛОЖЬ) КАК ОбособленноеПодразделение
			|ИЗ
			|	Справочник.Партнеры КАК СправочникПартнеры
			|		{ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
			|		ПО (Контрагенты.Партнер = СправочникПартнеры.Ссылка)}
			|ГДЕ
			|	ВЫБОР
			|		КОГДА &ОтборПоСегментуУстановлен <> ИСТИНА
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ СправочникПартнеры.Ссылка В (&ОтборПоСегменту)
			|		КОНЕЦ
			|	И ВЫБОР
			|			КОГДА НЕ &ОтборПоПолнотекстовомуПоискуУстановлен
			|				ТОГДА ИСТИНА
			|			ИНАЧЕ СправочникПартнеры.Ссылка В (&ОтборПоПолнотекстовомуПоиску)
			|		КОНЕЦ";
		КонецЕсли;
		
		Возврат ТекстЗапроса
	Иначе
		
		Возврат "
		|ВЫБРАТЬ
		|	СправочникПартнеры.Ссылка,
		|	СправочникПартнеры.ПометкаУдаления,
		|	СправочникПартнеры.Предопределенный,
		|	СправочникПартнеры.Родитель,
		|	СправочникПартнеры.Код,
		|	СправочникПартнеры.Наименование,
		|	СправочникПартнеры.БизнесРегион,
		|	СправочникПартнеры.ГруппаДоступа,
		|	СправочникПартнеры.ДатаРегистрации,
		|	СправочникПартнеры.Клиент,
		|	СправочникПартнеры.Комментарий,
		|	СправочникПартнеры.Конкурент,
		|	СправочникПартнеры.НаименованиеПолное,
		|	СправочникПартнеры.ОсновнойМенеджер,
		|	СправочникПартнеры.Поставщик,
		|	СправочникПартнеры.Перевозчик,
		|	СправочникПартнеры.ПрочиеОтношения,
		|	СправочникПартнеры.ОбслуживаетсяТорговымиПредставителями,
		|	СправочникПартнеры.ДополнительныеРеквизиты.(
		|		Ссылка КАК Ссылка1,
		|		НомерСтроки,
		|		Свойство,
		|		Значение
		|	),
		|	СправочникПартнеры.КонтактнаяИнформация.(
		|		Ссылка КАК Ссылка2,
		|		НомерСтроки КАК НомерСтроки1,
		|		Тип,
		|		Вид,
		|		Представление,
		|		ЗначенияПолей,
		|		Страна,
		|		Регион,
		|		Город,
		|		АдресЭП,
		|		ДоменноеИмяСервера,
		|		НомерТелефона,
		|		НомерТелефонаБезКодов
		|	)
		|ИЗ
		|	Справочник.Партнеры КАК СправочникПартнеры
		|ГДЕ
		|	ВЫБОР
		|		КОГДА &ОтборПоСегментуУстановлен <> ИСТИНА
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ СправочникПартнеры.Ссылка В (&ОтборПоСегменту)
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА НЕ &ОтборПоПолнотекстовомуПоискуУстановлен
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ СправочникПартнеры.Ссылка В (&ОтборПоПолнотекстовомуПоиску)
		|		КОНЕЦ";
		
	КонецЕсли;
	

КонецФункции

// Скрывает команды формы, при отсуствии прав на изменение
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - форма, для которой выполняются действия.
//
Процедура СкрытьКомандыПриОтсутствииПравНаИзменение(Форма) Экспорт

	ПравоИзменения = ПравоДоступа("Изменение", Метаданные.Справочники.Партнеры);
	
	Если Не ПравоИзменения Тогда
		
		МассивЭлементов = Новый Массив;
		МассивЭлементов.Добавить("СписокКонтекстноеМенюПеренестиЭлемент");
		МассивЭлементов.Добавить("СписокКонтекстноеМенюУровеньВверх");
		МассивЭлементов.Добавить("СписокКонтекстноеМенюУровеньВниз");
		
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Форма.Элементы, МассивЭлементов, "Видимость", Ложь);
		
	КонецЕсли;
	
	Если Не Форма.УпрощенныйВводДоступен Тогда
		
		МассивЭлементов = Новый Массив;
		МассивЭлементов.Добавить("СписокКонтекстноеМенюСкопировать");
		
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Форма.Элементы, МассивЭлементов, "Видимость", Ложь);
		
	КонецЕсли;

КонецПроцедуры

Функция ТекстHTMLИзОбъектаДокументHTML(ДокументHTML)
	
	Попытка
		ЗаписьDOM = Новый ЗаписьDOM;
		ЗаписьHTML = Новый ЗаписьHTML;
		ЗаписьHTML.УстановитьСтроку();
		ЗаписьDOM.Записать(ДокументHTML,ЗаписьHTML);
		Возврат ЗаписьHTML.Закрыть();
	Исключение
		Возврат "";
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#Область РассылкаОтчетов

// См. РассылкаОтчетовПереопределяемый.ПереопределитьТаблицуТиповПолучателей.
Процедура ПриПереопределенииТаблицыТиповПолучателейРассылокОтчетов(ТаблицаТипов, ДоступныеТипы) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РассылкаОтчетов") Тогда
		
		// Параметры справочника "Контрагенты".
		НастройкиТипа = Новый Структура;
		НастройкиТипа.Вставить("ГруппаКИ",          Справочники.ВидыКонтактнойИнформации.СправочникКонтрагенты);
		НастройкиТипа.Вставить("ОсновнойВидКИ",     Справочники.ВидыКонтактнойИнформации.EmailКонтрагента);
		НастройкиТипа.Вставить("ОсновнойТип",       Тип("СправочникСсылка.Контрагенты"));
		МодульРассылкаОтчетов = ОбщегоНазначения.ОбщийМодуль("РассылкаОтчетов");
		МодульРассылкаОтчетов.ДобавитьЭлементВТаблицуТиповПолучателей(ТаблицаТипов, ДоступныеТипы, НастройкиТипа);
		
		// Параметры справочника "Партнеры".
		НастройкиТипа = Новый Структура;
		НастройкиТипа.Вставить("ГруппаКИ",          Справочники.ВидыКонтактнойИнформации.СправочникПартнеры);
		НастройкиТипа.Вставить("ОсновнойВидКИ",     Справочники.ВидыКонтактнойИнформации.EmailПартнера);
		НастройкиТипа.Вставить("ОсновнойТип",       Тип("СправочникСсылка.Партнеры"));
		МодульРассылкаОтчетов = ОбщегоНазначения.ОбщийМодуль("РассылкаОтчетов");
		МодульРассылкаОтчетов.ДобавитьЭлементВТаблицуТиповПолучателей(ТаблицаТипов, ДоступныеТипы, НастройкиТипа);

		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
