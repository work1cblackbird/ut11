// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

// При создании на сервере.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура ПриСозданииНаСервере(Форма) Экспорт
	
	Если Не ВстроенаГиперссылкаОформлениеПередачиМеждуОрганизациями(Форма.ИмяФормы) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ТекстПерейтиКОбработкеПередачиТоваровМеждуОрганизациями") Тогда
		МодификацияФормы(Форма);
	КонецЕсли;
	ОформлениеПередачиТоваровМеждуОрганизациямиКлиентСервер.ЗаполнениеРеквизитовФормыДокументаОснования(Форма);
	
КонецПроцедуры

// При чтении на сервере.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура ПриЧтенииНаСервере(Форма) Экспорт
	
	Если Не ВстроенаГиперссылкаОформлениеПередачиМеждуОрганизациями(Форма.ИмяФормы) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ТекстПерейтиКОбработкеПередачиТоваровМеждуОрганизациями") Тогда
		МодификацияФормы(Форма);
	КонецЕсли;
	ОформлениеПередачиТоваровМеждуОрганизациямиКлиентСервер.ЗаполнениеРеквизитовФормыДокументаОснования(Форма);
	
КонецПроцедуры

// После записи на сервере.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура ПослеЗаписиНаСервере(Форма) Экспорт
	
	Если Не ВстроенаГиперссылкаОформлениеПередачиМеждуОрганизациями(Форма.ИмяФормы) Тогда
		Возврат;
	КонецЕсли;
	
	ОформлениеПередачиТоваровМеждуОрганизациямиКлиентСервер.ЗаполнениеРеквизитовФормыДокументаОснования(Форма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РаботаСГиперссылкойПередачиМеждуОрганизациями

#Область ГиперссылкиВСвязанныхФормахОбъектов

// Модифицирует прикладные формы: добавляет необходимые реквизиты, элементы, команды ГосИС
//   Требуется выполнить до прочей работы с ГосИС в прикладных формах.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - модифицируемая форма
//
Процедура МодификацияФормы(Форма)
	
	ДобавляемыеРеквизиты = Новый Массив; //Массив Из РеквизитФормы
	
	МодификацияРеквизитовФормы(Форма, ДобавляемыеРеквизиты);
	
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	МодификацияЭлементовФормы(Форма);
	
КонецПроцедуры

Процедура МодификацияРеквизитовФормы(Форма, ДобавляемыеРеквизиты)
	
	Реквизит = Новый РеквизитФормы(
		"ТекстПерейтиКОбработкеПередачиТоваровМеждуОрганизациями",
		Новый ОписаниеТипов("ФорматированнаяСтрока"));
	ДобавляемыеРеквизиты.Добавить(Реквизит);
	
КонецПроцедуры

Процедура МодификацияЭлементовФормы(Форма)
	
	ПараметрыНадписи = СобытияФормИС.ПараметрыИнтеграцииДляДокументаОснования();
	ПараметрыНадписи.Ключ              = "ОбработкаПередачиТоваровМеждуОрганизациями";
	ПараметрыНадписи.ИмяЭлементаФормы  = "ТекстПерейтиКОбработкеПередачиТоваровМеждуОрганизациями";
	ПараметрыНадписи.ИмяРеквизитаФормы = "ТекстПерейтиКОбработкеПередачиТоваровМеждуОрганизациями";
	
	ГруппаИнтеграции = СобытияФормИС.ДобавитьПолучитьГруппуИнтеграцииНаФормуДокументаОснования(Форма, ПараметрыНадписи);
	СобытияФормИС.ДобавитьПолеИнтеграцииНаФормуДокументаОснования(Форма, ПараметрыНадписи, ГруппаИнтеграции);
	
	Элемент = Форма.Элементы.Найти("СоответствиеТребованиямГИСМТГосИС");
	ГруппаКоманды = Форма.Элементы.Найти("ГруппаСоответствиеТребованиямГИСМТГосИС");
	Если Элемент <> Неопределено И ГруппаКоманды = Неопределено Тогда
		ГруппаКоманды = Форма.Элементы.Добавить(
			"ГруппаСоответствиеТребованиямГИСМТГосИС",
			Тип("ГруппаФормы"),
			Элемент.Родитель);
		Форма.Элементы.Переместить(ГруппаКоманды, Элемент.Родитель, Элемент);
		Форма.Элементы.Переместить(Элемент, ГруппаКоманды);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

Функция ИнтеграцияГосИСАктивна()
	
	Если ПолучитьФункциональнуюОпцию("ВестиСведенияДляДекларацийПоАлкогольнойПродукции") Тогда
		Возврат Истина;
	ИначеЕсли ПолучитьФункциональнуюОпцию("ВестиУчетПодконтрольныхТоваровВЕТИС") Тогда
		Возврат Истина;
	ИначеЕсли ПолучитьФункциональнуюОпцию("ВестиУчетЗернаИПродуктовПереработкиЗЕРНО") Тогда
		Возврат Истина;
	ИначеЕсли ПолучитьФункциональнуюОпцию("ВестиУчетПестицидовАгрохимикатовТукосмесейСАТУРН") Тогда
		Возврат Истина;
	ИначеЕсли ПолучитьФункциональнуюОпцию("ВестиУчетМаркируемойПродукцииИСМП")
		И ОбщегоНазначенияИСМПКлиентСерверПовтИсп.УчитываемыеВидыМаркируемойПродукции().Количество() Тогда
		Возврат Истина;
	КонецЕсли;
	Возврат Ложь;
	
КонецФункции

Функция ВстроенаГиперссылкаОформлениеПередачиМеждуОрганизациями(ИмяФормы)
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями") Тогда
		Возврат Ложь;
	ИначеЕсли Не ИнтеграцияГосИСАктивна() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ЭтоФормаСГиперссылкойПередачиМеждуОрганизациями(ИмяФормы);
	
КонецФункции

Функция ЭтоФормаСГиперссылкойПередачиМеждуОрганизациями(ИмяФормы) Экспорт
	
	Если ИмяФормы = "Документ.ВнутреннееПотребление.Форма.ФормаДокумента"
		Или ИмяФормы = "Документ.ВозвратТоваровПоставщику.Форма.ФормаДокумента"
		Или ИмяФормы = "Документ.КорректировкаРеализации.Форма.ФормаДокумента"
		Или ИмяФормы = "Документ.ОтчетОРозничныхПродажах.Форма.ФормаДокумента"
		Или ИмяФормы = "Документ.ПередачаТоваровХранителю.Форма.ФормаДокумента"
		Или ИмяФормы = "Документ.ПеремещениеТоваров.Форма.ФормаДокумента"
		Или ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента"
		Или ИмяФормы = "Документ.СборкаТоваров.Форма.ФормаДокумента"

	Тогда
		Возврат Истина;
	КонецЕсли;
	Возврат Ложь;
	
КонецФункции

#КонецОбласти
