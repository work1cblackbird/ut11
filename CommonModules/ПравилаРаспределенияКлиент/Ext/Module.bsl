
#Область ПрограммныйИнтерфейс

// Открывает форму настройки отборов для правил распределения статей расходов.
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - форма владелец из которой открывается настройка отбора.
//	ПараметрыОткрытия - см. ПараметрыОткрытияФормыНастройкиОтбора
Процедура ОткрытьФормуНастройкиОтборов(Форма, ПараметрыОткрытия) Экспорт

	ДопПараметры = ПравилаРаспределенияВызовСервера.ДополнительныеПараметрыНастройкиОтборов(ПараметрыОткрытия);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НеПомещатьНастройкиВСхемуКомпоновкиДанных", Истина);
	ПараметрыФормы.Вставить("НеЗагружатьСхемуКомпоновкиДанныхИзФайла", Истина);
	ПараметрыФормы.Вставить("НеНастраиватьПараметры", ДопПараметры.НеНастраиватьПараметры);
	ПараметрыФормы.Вставить("НеРедактироватьСхемуКомпоновкиДанных", Истина);
	ПараметрыФормы.Вставить("НеНастраиватьУсловноеОформление", Истина);
	ПараметрыФормы.Вставить("НеНастраиватьВыбор", Истина);
	ПараметрыФормы.Вставить("НеНастраиватьПорядок", Истина);
	ПараметрыФормы.Вставить("УникальныйИдентификатор", ПараметрыОткрытия.УникальныйИдентификатор);
	ПараметрыФормы.Вставить("АдресСхемыКомпоновкиДанных", ДопПараметры.АдресСхемыКомпоновкиДанных);
	ПараметрыФормы.Вставить("АдресНастроекКомпоновкиДанных", ДопПараметры.АдресНастроекКомпоновкиДанных);
	ПараметрыФормы.Вставить("Заголовок", ПараметрыОткрытия.ЗаголовокФормы);
	
	ОткрытьФорму("ОбщаяФорма.УпрощеннаяНастройкаСхемыКомпоновкиДанных",
		ПараметрыФормы,,,,,
		Новый ОписаниеОповещения("РедактироватьСхемуКомпоновкиДанныхЗавершение",
			Форма,
			ПараметрыОткрытия),
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры

// Конструктор параметров открытия формы настройки отборов постатейных расходов.
// Возвращаемое значение:
//	Структура - параметры открытия.
Функция ПараметрыОткрытияФормыНастройкиОтбора() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("БазаРаспределения", Неопределено);
	Параметры.Вставить("ИмяНастроекКомпоновщика", "");
	Параметры.Вставить("КомпоновщикНастроек", Неопределено);
	Параметры.Вставить("ЗаголовокФормы", "");
	Параметры.Вставить("УникальныйИдентификатор", "");
	Параметры.Вставить("НедоступныеПоляОтбора", Новый Массив);
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти
