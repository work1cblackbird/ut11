///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Определяет состав назначений и общие реквизиты в шаблонах сообщений 
//
// Параметры:
//  Настройки - Структура:
//    * ПредметыШаблонов - ТаблицаЗначений - содержит варианты предметов для шаблонов. Колонки:
//         ** Имя           - Строка - уникальное имя назначения.
//         ** Представление - Строка - представление варианта.
//         ** Макет         - Строка - имя макета СКД, если состав реквизитов определяется посредством СКД.
//         ** ЗначенияПараметровСКД - Структура - значения параметров СКД для текущего предмета шаблона сообщения.
//    * ОбщиеРеквизиты - ДеревоЗначений - содержит описание общих реквизитов доступных во всех шаблонах. Колонки:
//         ** Имя            - Строка - уникальное имя общего реквизита.
//         ** Представление  - Строка - представление общего реквизита.
//         ** Тип            - Тип    - тип общего реквизита. По умолчанию строка.
//    * ИспользоватьПроизвольныеПараметры  - Булево - указывает, можно ли использовать произвольные пользовательские
//                                                    параметры в шаблонах сообщений.
//    * ЗначенияПараметровСКД - Структура - общие значения параметров СКД, для всех макетов, где состав реквизитов
//                                          определяется средствами СКД.
//    * РасширенныйСписокПолучателей - Булево - если Истина, то для получателей письма можно указывать вариант отправки
//                                              и контакт в исходящих писем подсистемы Взаимодействия.
//
Процедура ПриОпределенииНастроек(Настройки) Экспорт
	
	//++ НЕ ГОСИС
	Настройки.ИспользоватьПроизвольныеПараметры = Истина;
	Настройки.ЗначенияПараметровСКД.Вставить("ОбращениеМужскойРод",       НСтр("ru='Уважаемый'"));
	Настройки.ЗначенияПараметровСКД.Вставить("ОбращениеЖенскийРод",       НСтр("ru='Уважаемая'"));
	Настройки.ЗначенияПараметровСКД.Вставить("ОбращениеПолНеопределен",   НСтр("ru='Уважаемый(ая)'"));
	Настройки.ЗначенияПараметровСКД.Вставить("СтрокаБезОграничений",      НСтр("ru='Без ограничений'"));
	Настройки.ЗначенияПараметровСКД.Вставить("МожетВыкупатьсяЧастично",   НСтр("ru='Может выкупаться частично'"));
	Настройки.ЗначенияПараметровСКД.Вставить("НеМожетВыкупатьсяЧастично", НСтр("ru='Не может выкупаться частично'"));
	
	Для Каждого ЗначениеПеречисления ИЗ Перечисления.ТипыСобытийОповещений.ИзменениеСостоянияЗаказа.Метаданные().ЗначенияПеречисления Цикл
	
		Если НЕ Перечисления.ТипыСобытийОповещений.ДанныеДляОбработкиОчередиОповещений(Перечисления.ТипыСобытийОповещений[ЗначениеПеречисления.Имя]).ДоступноДляИспользования Тогда
			Продолжить;
		КонецЕсли;
		
		Предмет = Настройки.ПредметыШаблонов.Добавить();
		Предмет.Имя = ЗначениеПеречисления.Имя;
		Предмет.Представление = НСтр("ru = 'Оповещение клиента'") + " """ + ЗначениеПеречисления.Синоним +"""";
		Предмет.Макет = ЗначениеПеречисления.Имя;
	
	КонецЦикла;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
		
		Предмет = Настройки.ПредметыШаблонов.Найти("Справочник.Партнеры", "Имя");
		
		Если Предмет <> Неопределено Тогда
			Предмет.Макет = "ДанныеШаблонаСообщенийИспользоватьПартнеровКакКонтрагентов";	
		КонецЕсли;
		
	КонецЕсли;
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Вызывается при подготовке шаблонов сообщений и позволяет переопределить список реквизитов и вложений.
//
// Параметры:
//  Реквизиты - КоллекцияСтрокДереваЗначений - список реквизитов шаблона:
//    * Имя            - Строка - уникальное имя реквизита.
//    * Представление  - Строка - представление реквизита.
//    * Тип            - Тип    - тип реквизита.
//    * Подсказка      - Строка - расширенная информация о реквизите.
//    * Формат         - Строка - формат вывода значения для чисел, дат, строк и булевых значений. 
//                                Например, "ДЛФ=D" для даты.
//  Вложения - ТаблицаЗначений - печатные формы и вложения, где:
//    * Имя           - Строка - уникальное имя вложения.
//    * Идентификатор - Строка - идентификатор вложения.
//    * Представление - Строка - представление варианта.
//    * Подсказка     - Строка - расширенная информация о вложении.
//    * ТипФайла      - Строка - тип вложения, который соответствует расширению файла: "pdf", "png", "jpg", mxl" и др.
//  НазначениеШаблона       - Строка  - назначение шаблона сообщения, например, "ОповещениеКлиентаИзменениеЗаказа".
//  ДополнительныеПараметры - Структура - дополнительные сведения о шаблоне сообщения.
//
Процедура ПриПодготовкеШаблонаСообщения(Реквизиты, Вложения, НазначениеШаблона, ДополнительныеПараметры) Экспорт
	
	//++ НЕ ГОСИС
	МассивНазначенийШаблона = Новый Массив;
	Для Каждого ЗначениеПеречисления ИЗ Перечисления.ТипыСобытийОповещений.ИзменениеСостоянияЗаказа.Метаданные().ЗначенияПеречисления Цикл
		Если НЕ Перечисления.ТипыСобытийОповещений.ДанныеДляОбработкиОчередиОповещений(Перечисления.ТипыСобытийОповещений[ЗначениеПеречисления.Имя]).ДоступноДляИспользования Тогда
			Продолжить;
		КонецЕсли;
		МассивНазначенийШаблона.Добавить(ЗначениеПеречисления.Имя);
	КонецЦикла;
	
	НазначениеШаблонаЭтоОповещение = Ложь;
	Если МассивНазначенийШаблона.Найти(НазначениеШаблона) <> Неопределено Тогда
		НазначениеШаблонаЭтоОповещение = Истина;
		ИмяМакета = СтрЗаменить(НазначениеШаблона, "Перечисление.ТипыСобытийОповещений.", "");
	КонецЕсли;
	
	Если НазначениеШаблонаЭтоОповещение Тогда
		ШаблоныСообщений.СформироватьСписокРеквизитовПоСКД(Реквизиты, Перечисления.ТипыСобытийОповещений.ПолучитьМакет(ИмяМакета));
	КонецЕсли;
	
	КнопкаОплатитьЧерезЯндексКассуПредставление = НСтр("ru = 'Кнопка ""Оплатить через ЮKassa""'");
	КлючПоиска = СтрШаблон("%1.%2",Метаданные.Документы["ЗаказКлиента"].Синоним, КнопкаОплатитьЧерезЯндексКассуПредставление);
	ДополнительныеПараметры.Текст = СтрЗаменить(ДополнительныеПараметры.Текст,
		КлючПоиска,
		"ЗаказКлиента.ПредставлениеСсылки");
		
	КлючПоиска = СтрШаблон("%1.%2",Метаданные.Документы["СчетНаОплатуКлиенту"].Синоним, КнопкаОплатитьЧерезЯндексКассуПредставление);
	ДополнительныеПараметры.Текст = СтрЗаменить(ДополнительныеПараметры.Текст,
		КлючПоиска,
		"СчетНаОплатуКлиенту.ПредставлениеСсылки");
		
	ДополнительныеПараметры.Текст = СтрЗаменить(ДополнительныеПараметры.Текст,
		"КнопкаОплатитьЧерезЯндексКассу",
		"ПредставлениеСсылки");
		
	// ИнтернетПоддержкаПользователей
	ИнтернетПоддержкаПользователей.ПриПодготовкеШаблонаСообщения(
		Реквизиты,
		Вложения,
		НазначениеШаблона,
		ДополнительныеПараметры);
	// Конец ИнтернетПоддержкаПользователей
		
	ШаблоныСообщенийЛокализация.ПриПодготовкеШаблонаСообщения(Реквизиты, Вложения, НазначениеШаблона, ДополнительныеПараметры);
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Вызывается в момент создания сообщений по шаблону для заполнения значений реквизитов и вложений.
//
// Параметры:
//  Сообщение - Структура:
//    * ЗначенияРеквизитов - Соответствие из КлючИЗначение - список используемых в шаблоне реквизитов:
//      ** Ключ     - Строка - имя реквизита в шаблоне;
//      ** Значение - Строка - значение заполнения в шаблоне.
//    * ЗначенияОбщихРеквизитов - Соответствие из КлючИЗначение - список используемых в шаблоне общих реквизитов:
//      ** Ключ     - Строка - имя реквизита в шаблоне;
//      ** Значение - Строка - значение заполнения в шаблоне.
//    * Вложения - Соответствие из КлючИЗначение:
//      ** Ключ     - Строка - имя вложения в шаблоне;
//      ** Значение - ДвоичныеДанные
//                  - Строка - двоичные данные или адрес во временном хранилище вложения.
//    * ДополнительныеПараметры - Структура - дополнительные параметры сообщения. 
//  НазначениеШаблона - Строка -  полное имя назначения шаблон сообщения.
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//  ПараметрыШаблона - Структура -  дополнительная информация о шаблоне сообщения.
//
Процедура ПриФормированииСообщения(Сообщение, НазначениеШаблона, ПредметСообщения, ПараметрыШаблона) Экспорт
	
	//++ НЕ ГОСИС
	МассивНазначенийШаблона = Новый Массив;
	Для Каждого ЗначениеПеречисления ИЗ Перечисления.ТипыСобытийОповещений.ИзменениеСостоянияЗаказа.Метаданные().ЗначенияПеречисления Цикл
		Если НЕ Перечисления.ТипыСобытийОповещений.ДанныеДляОбработкиОчередиОповещений(Перечисления.ТипыСобытийОповещений[ЗначениеПеречисления.Имя]).ДоступноДляИспользования Тогда
			Продолжить;
		КонецЕсли;
		МассивНазначенийШаблона.Добавить(ЗначениеПеречисления.Имя);
	КонецЦикла;
	
	Если МассивНазначенийШаблона.Найти(НазначениеШаблона) <> Неопределено Тогда
		ШаблоныСообщений.ЗаполнитьРеквизитыПоСКД(Сообщение.ЗначенияРеквизитов, ПредметСообщения, ПараметрыШаблона);
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей
	ИнтернетПоддержкаПользователей.ПриФормированииСообщения(
		Сообщение,
		НазначениеШаблона,
		ПредметСообщения,
		ПараметрыШаблона);
	// Конец ИнтернетПоддержкаПользователей
	
	ШаблоныСообщенийЛокализация.ПриФормированииСообщения(Сообщение, НазначениеШаблона, ПредметСообщения, ПараметрыШаблона);
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Заполняет список получателей SMS при отправке сообщения сформированного по шаблону.
//
// Параметры:
//   ПолучателиSMS - ТаблицаЗначений:
//     * НомерТелефона - Строка - номер телефона, куда будет отправлено сообщение SMS;
//     * Представление - Строка - представление получателя сообщения SMS;
//     * Контакт       - Произвольный - контакт, которому принадлежит номер телефона.
//  НазначениеШаблона - Строка - идентификатор назначения шаблона
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект, являющийся источником данных.
//                   - Структура  - структура описывающая параметры шаблона:
//    * Предмет               - ЛюбаяСсылка - ссылка на объект, являющийся источником данных;
//    * ВидСообщения - Строка - вид формируемого сообщения: "ЭлектроннаяПочта" или "СообщениеSMS";
//    * ПроизвольныеПараметры - Соответствие - заполненный список произвольных параметров;
//    * ОтправитьСразу - Булево - признак мгновенной отправки;
//    * ПараметрыСообщения - Структура - дополнительные параметры сообщения.
//
Процедура ПриЗаполненииТелефоновПолучателейВСообщении(ПолучателиSMS, НазначениеШаблона, ПредметСообщения) Экспорт
	
	//++ НЕ ГОСИС
	ФормированиеПечатныхФорм.ЗаполнитьПолучателейСообщенияПоШаблону(ПолучателиSMS, НазначениеШаблона, ПредметСообщения, Истина);
	
	ШаблоныСообщенийЛокализация.ПриЗаполненииТелефоновПолучателейВСообщении(ПолучателиSMS, НазначениеШаблона, ПредметСообщения);
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Заполняет список получателей почты при отправке сообщения сформированного по шаблону.
//
// Параметры:
//   ПолучателиПисьма - ТаблицаЗначений - список получается письма:
//     * ВариантОтправки - Строка - вариант отправки для получателя письма: Кому, Копия, СкрытаяКопия, ОбратныйАдрес;
//     * Адрес           - Строка - адрес электронной почты получателя;
//     * Представление   - Строка - представление получателя письма;
//     * Контакт         - Произвольный - контакт, которому принадлежит адрес электронной почты.
//  НазначениеШаблона - Строка - идентификатор назначения шаблона.
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект, являющийся источником данных.
//                   - Структура  - структура описывающая параметры шаблона:
//    * Предмет               - ЛюбаяСсылка - ссылка на объект, являющийся источником данных;
//    * ВидСообщения - Строка - вид формируемого сообщения: "ЭлектроннаяПочта" или "СообщениеSMS";
//    * ПроизвольныеПараметры - Соответствие - заполненный список произвольных параметров;
//    * ОтправитьСразу - Булево - признак мгновенной отправки письма;
//    * ПараметрыСообщения - Структура - дополнительные параметры сообщения;
//    * ПреобразовыватьHTMLДляФорматированногоДокумента - Булево - признак преобразование HTML текста
//             сообщения содержащего картинки в тексте письма из-за особенностей вывода изображений
//             в форматированном документе;
//    * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты - учетная запись для отправки письма.
//
Процедура ПриЗаполненииПочтыПолучателейВСообщении(ПолучателиПисьма, НазначениеШаблона, ПредметСообщения) Экспорт
	
	//++ НЕ ГОСИС
	ФормированиеПечатныхФорм.ЗаполнитьПолучателейСообщенияПоШаблону(ПолучателиПисьма, НазначениеШаблона, ПредметСообщения, Ложь);
	
	ШаблоныСообщенийЛокализация.ПриЗаполненииПочтыПолучателейВСообщении(ПолучателиПисьма, НазначениеШаблона, ПредметСообщения);
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Начальное заполнение предопределенных шаблонов сообщений

// Смотри также ОбновлениеИнформационнойБазыПереопределяемый.ПриНастройкеНачальногоЗаполненияЭлементов
// 
// Параметры:
//  Настройки - см. ОбновлениеИнформационнойБазыПереопределяемый.ПриНастройкеНачальногоЗаполненияЭлементов.Настройки
//
Процедура ПриНастройкеНачальногоЗаполненияЭлементов(Настройки) Экспорт
	
КонецПроцедуры

// Смотри также ОбновлениеИнформационнойБазыПереопределяемый.ПриНачальномЗаполненииЭлементов
//
// Параметры:
//  КодыЯзыков - см. ОбновлениеИнформационнойБазыПереопределяемый.ПриНачальномЗаполненииЭлементов.КодыЯзыков
//  Элементы   - см. ОбновлениеИнформационнойБазыПереопределяемый.ПриНачальномЗаполненииЭлементов.Элементы
//  ТабличныеЧасти - см. ОбновлениеИнформационнойБазыПереопределяемый.ПриНачальномЗаполненииЭлементов.ТабличныеЧасти
//
Процедура ПриНачальномЗаполненииЭлементов(КодыЯзыков, Элементы, ТабличныеЧасти) Экспорт
	
	
	
КонецПроцедуры

// Смотри также ОбновлениеИнформационнойБазыПереопределяемый.ПриНастройкеНачальногоЗаполненияЭлементов
//
// Параметры:
//  Объект                  - СправочникОбъект.РолиИсполнителей - заполняемый объект.
//  Данные                  - СтрокаТаблицыЗначений - данные заполнения объекта.
//  ДополнительныеПараметры - Структура:
//   * ПредопределенныеДанные - ТаблицаЗначений - данные заполненные в процедуре ПриНачальномЗаполненииЭлементов.
//
Процедура ПриНачальномЗаполненииЭлемента(Объект, Данные, ДополнительныеПараметры) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти

