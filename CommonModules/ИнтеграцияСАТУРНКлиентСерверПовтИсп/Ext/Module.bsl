#Область СлужебныеПроцедурыИФункции

// Возвращает список особенностей учета продукци для ФГИС "Сатурн".
// 
// Возвращаемое значение:
//  ФиксированныйМассив Из ПеречислениеСсылка.ВидыПродукцииИС - Учитываемые виды продукции
Функция УчитываемыеВидыПродукции() Экспорт
	
	ВидыПродукции = Новый Массив;
	
	ВидыПродукции.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПодконтрольнаяПродукцияСАТУРН"));
	
	Возврат Новый ФиксированныйМассив(ВидыПродукции);
	
КонецФункции

#Область УпаковкиЕдиницыИзмерения

// Возвращает единицу измерения, указанную в константе ЕдиницаИзмеренияКилограммИС.
//
// Возвращаемое значение:
// 	ОпределяемыйТип.Упаковка
// 
Функция ЕдиницаИзмеренияКилограмм() Экспорт
	
	ЕдиницаИзмерения = ИнтеграцияИСВызовСервера.ЕдиницаИзмеренияКилограмм();
	Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
		ВызватьИсключение НСтр("ru = 'Не указана единица измерения для учета продукции, подконтрольной ФГИС ""Сатурн"".
			|Перейдите в панель администрирования и укажите единицу измерения ""Килограмм"".'");
	КонецЕсли;
	
	Возврат ЕдиницаИзмерения;
	
КонецФункции

// Возвращает единицу измерения, указанную в константе ЕдиницаИзмеренияЛитрИС.
//
// Возвращаемое значение:
// 	ОпределяемыйТип.Упаковка
// 
Функция ЕдиницаИзмеренияЛитр() Экспорт
	
	ЕдиницаИзмерения = ИнтеграцияИСВызовСервера.ЕдиницаИзмеренияЛитр();
	Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
		ВызватьИсключение НСтр("ru = 'Не указана единица измерения для учета продукции, подконтрольной ФГИС ""Сатурн"".
			|Перейдите в панель администрирования и укажите единицу измерения ""Литр"".'");
	КонецЕсли;
	
	Возврат ЕдиницаИзмерения;
	
КонецФункции

// Возвращает единицы измерения веса и объема.
//
// Возвращаемое значение:
// 	Массив из ОпределяемыйТип.Упаковка
// 
Функция ЕдиницыИзмеренияВесаИОбъема() Экспорт
	
	ЕдиницыИзмерения = ИнтеграцияИСВызовСервера.ЕдиницыИзмеренияВесаИОбъема();
	
	Если Не ЗначениеЗаполнено(ЕдиницыИзмерения.ЕдиницаИзмеренияВеса) Тогда
		ТекстОшибки =  НСтр("ru = '""Килограмм""'");
	ИначеЕсли Не ЗначениеЗаполнено(ЕдиницыИзмерения.ЕдиницаИзмеренияОбъема) Тогда
		ТекстОшибки =  НСтр("ru = '""Литр""'");
	Иначе
		ТекстОшибки = "";
	КонецЕсли;
	
	Если ТекстОшибки <> "" Тогда
		Если Не ЗначениеЗаполнено(ЕдиницыИзмерения.ЕдиницаИзмеренияВеса)
			И Не ЗначениеЗаполнено(ЕдиницыИзмерения.ЕдиницаИзмеренияОбъема) Тогда
			ВызватьИсключение НСтр("ru = 'Не указаны единицы измерения для учета продукции, подконтрольной ФГИС ""Сатурн"".
				|Перейдите в панель администрирования и укажите единицы измерения ""Килограмм"" и ""Литр"".'");
		Иначе
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Не указана единица измерения для учета продукции, подконтрольной ФГИС ""Сатурн"".
				|Перейдите в панель администрирования и укажите единицу измерения %1.'"),
				ТекстОшибки);
		КонецЕсли;
	КонецЕсли;
	
	Результат = Новый Массив;
	Результат.Добавить(ЕдиницыИзмерения.ЕдиницаИзмеренияВеса);
	Результат.Добавить(ЕдиницыИзмерения.ЕдиницаИзмеренияОбъема);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти
