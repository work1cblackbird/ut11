////////////////////////////////////////////////////////////////////////////////
// Клиентские процедуры работы с механизмами обеспечения
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Добавляет действие "Передан ранее" в варианты выбора обеспечения в заказе.
// Параметры:
//  Элемент - ПолеВвода - элемент формы связанный с реквизитом табличной части ВариантОбеспечения
//  ТекущиеДанные - ДанныеФормыЭлементКоллекции - текущие данные табличной части.
Процедура ДобавитьВПараметрыВыбораВариантаОбеспеченияДействиеПереданРанее(Элемент, ТекущиеДанные) Экспорт
	
	НовыйМассив = Новый Массив();
	
	Если ТекущиеДанные.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар")
			Или ТекущиеДанные.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.МногооборотнаяТара") Тогда
		
		МассивДействий = ОбеспечениеВДокументахКлиентСервер.ДоступныеДействияДляВыбораОбеспеченияВСтрокеПоУмолчанию();
		МассивДействий.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.ПереданРанее"));
		
		ДопустимыеДействия = Новый ФиксированныйМассив(МассивДействий);
		НовыйПараметр = Новый ПараметрВыбора("ДопустимыеДействия", ДопустимыеДействия);
		НовыйМассив.Добавить(НовыйПараметр);
		
	КонецЕсли;
	
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассив);
	
КонецПроцедуры

// Предназначена для проверки заполнения полей перед выполнением команды по заполнению обеспечения в документах.
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - основной реквизит формы документа, связанный с редактируемым объектом-документом в
//                                  которым нужно выполнить проверки заполнения полей.
//  ТаблицаТовары - ДанныеФормыКоллекция - список в котором необходимо выполнить проверки заполнения полей
//  Идентификатор - Число - идентификатор строки.
//  ПараметрыПроверки - Структура - структура параметров проверки, формируемая функцией ОбеспечениеКлиентСервер.ИнициализироватьПараметрыПроверкиЗаполнения
//  Склад - СправочникСсылка.Склады - значение поля склад (склад - отгрузки) из шапки документа.
//  Режим - Строка  - режим выполнения команды.
//
// Возвращаемое значение:
//  Булево - истина, если проверка заполнения выполнена успешно, ложь - в противном случае.
//
Функция ПроверитьЗаполнение(Объект, ТаблицаТовары, Идентификатор, ПараметрыПроверки, Склад, Режим) Экспорт
	
	ВыдаватьСообщения = Режим <> ОбеспечениеВДокументахКлиентСервер.РежимПодборСкладов();
	Пути = "НомерСтроки,
	       |Отменено,
	       |Номенклатура, Характеристика, ВариантОбеспечения,
	       |ТипНоменклатуры, ХарактеристикиИспользуются,
	       |Количество, КоличествоУпаковок";

	ДанныеСтроки = Новый Структура(Пути);
	Ошибки = Новый Массив();

	Хранилище = Новый Структура(Пути);

	ЗаполнитьЗначенияСвойств(Хранилище, Объект);

	ЕстьОшибкиСклад         = Ложь;
	
	СтрокаТовары = ТаблицаТовары.НайтиПоИдентификатору(Идентификатор);
	ЗаполнитьЗначенияСвойств(Хранилище, СтрокаТовары);
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, Хранилище);
	
	Если Режим = ОбеспечениеВДокументахКлиентСервер.РежимПросмотрДоступности()
			И ДанныеСтроки.ВариантОбеспечения <> ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.РезервироватьПоМереПоступления")
			И ДанныеСтроки.ВариантОбеспечения <> ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.КОбеспечению")
			И ДанныеСтроки.ВариантОбеспечения <> ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.НеТребуется") Тогда
			Возврат Ложь;
	КонецЕсли;

	Если ДанныеСтроки.ТипНоменклатуры <> ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар")
			И ДанныеСтроки.ТипНоменклатуры <> ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.МногооборотнаяТара") Тогда
			
		Возврат Ложь;
		
	КонецЕсли;
	
	Если ДанныеСтроки.Отменено = Истина Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Для отмененной строки обеспечение и отгрузка невозможны.'"));
		Возврат Ложь;
		
	КонецЕсли;
	
	Если ДанныеСтроки.Количество = 0 И ДанныеСтроки.КоличествоУпаковок = 0 Тогда
		
		Ошибка = Новый Структура("Поле, НомерСтроки", "Количество", ДанныеСтроки.НомерСтроки);
		Ошибки.Добавить(Ошибка);
		
	КонецЕсли;
	
	Если ДанныеСтроки.Количество = 0 И ДанныеСтроки.КоличествоУпаковок <> 0 Тогда
		
		Ошибка = Новый Структура("Поле, НомерСтроки", "КоличествоУпаковок", ДанныеСтроки.НомерСтроки);
		Ошибки.Добавить(Ошибка);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеСтроки.Номенклатура) Тогда
		
		Ошибка = Новый Структура("Поле, НомерСтроки", "Номенклатура", ДанныеСтроки.НомерСтроки);
		Ошибки.Добавить(Ошибка);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеСтроки.Характеристика) И ДанныеСтроки.ХарактеристикиИспользуются = Истина Тогда
		
		Ошибка = Новый Структура("Поле, НомерСтроки", "Характеристика", ДанныеСтроки.НомерСтроки);
		Ошибки.Добавить(Ошибка);
		
	КонецЕсли;

	Если ПараметрыПроверки.Поля.Свойство("Склад") И Не ЗначениеЗаполнено(Склад) Тогда
		
		Если Не ЕстьОшибкиСклад Тогда
			Ошибка = Новый Структура("Поле, НомерСтроки", "Склад", ДанныеСтроки.НомерСтроки);
			Ошибки.Добавить(Ошибка);
			ЕстьОшибкиСклад = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ОшибкиПользователю = Неопределено;
	
	Для Каждого Ошибка Из Ошибки Цикл
		
		Поле  = СтрЗаменить(ПараметрыПроверки.Поля[Ошибка.Поле], "%1", Ошибка.НомерСтроки - 1);
		Текст = СтрЗаменить(ПараметрыПроверки.Тексты[Ошибка.Поле], "%1", Ошибка.НомерСтроки);
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(ОшибкиПользователю, Поле, Текст, "");
		
	КонецЦикла;
	
	Если ВыдаватьСообщения Тогда
		ОчиститьСообщения();
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(ОшибкиПользователю);
	КонецЕсли;
	
	Возврат Ошибки.Количество() = 0;
	
КонецФункции

// Возвращает структуру параметров, используемую при открытии формы обработки "ОбеспечениеПотребностей".
//  Возвращаемое значение:
//   Структура - 
//    ОтборПоТипуОбеспечения - ПеречислениеСсылка.ТипыОбеспечения, Неопределено - отбор по типу обеспечения.
//    ОтборПоПодразделению - СправочникСсылка.СтруктураПредприятия, Неопределено - отбор по подразделению.
//    ЕстьШагЗапасы - Булево - если Ложь, то шаг поддержания запасов игнорируется.
//
Функция ПараметрыОткрытияФормыОбработкиОбеспечениеПотребностей() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("ОтборПоТипуОбеспечения", Неопределено);
	Результат.Вставить("ОтборПоИсточникуОбеспечения", Неопределено);
	Результат.Вставить("ОтборПоПодразделению", Неопределено);
	Результат.Вставить("ЕстьШагЗапасы", Истина);
	Результат.Вставить("ОтборПоЦеховымКладовым", Ложь);
	
	Возврат Результат;
	
КонецФункции

// Сообщает о том, что в заказе нет ни одного товара с действием "Обеспечивать обособленно" по собственному назначению.
//
Процедура СообщитьОбОтсутствииТовараКОбособленномуОбеспечению() Экспорт
	
	ТекстОшибки = НСтр("ru = 'В заказе нет товаров для обособленного обеспечения.'");
	
	ПоказатьПредупреждение(, ТекстОшибки);
	
КонецПроцедуры

// Сообщает о том, что по назначению заказа нет резервов на складе.
Процедура СообщитьОбОтсутствииТовараКСнятиюРезерва() Экспорт
	
	ТекстОшибки = НСтр("ru = 'По заказу нет обособленных остатков. Выполнение команды не предусмотрено.'");
	
	ПоказатьПредупреждение(, ТекстОшибки);
	
КонецПроцедуры

// Сообщает о том, что все присутствующие в документе товары с действием "Обеспечивать обособленно" - по стороннему назначению.
//
// Параметры:
//  ЭтоРезервирование	 - Булево - Это резервирование или снятие резерва.
//
Процедура СообщитьОбОтсутствииТовараКОбособленномуОбеспечениюПоТекущемуЗаказу(ЭтоРезервирование) Экспорт
	
	Если ЭтоРезервирование Тогда
		
		ТекстОшибки = НСтр("ru = 'Потребности зафиксированы по назначению, не связанному с данным заказом. Резервирование для такого назначения доступно из заказа, указанного в назначении, или с помощью документа ""Корректировка назначения товаров"", создаваемого вручную из списка документов.'");
		
	Иначе
		
		ТекстОшибки = НСтр("ru = 'По назначению связанному с данным заказом на складе нет остатков для снятия. Снятие резерва для назначения, не связанного с данным заказом, доступно из заказа, указанного в назначении или с помощью документа ""Корректировка назначения товаров"", создаваемого вручную из списка документов.'");
		
	КонецЕсли;
	
	ПоказатьПредупреждение(, ТекстОшибки);
	
КонецПроцедуры

// Сообщает о невозможности запуска обработки резервирования/снятия резерва из документа с 
// неподходящим статусов.
//
// Параметры:
//  ЭтоРезервирование	 - Булево - Это резервирование или снятие резерва
//  МинимальныйСтатус	 - Строка - Минимальный статус, начиная с которого доступен запуск обработки резервирования/снятия резерва.
//
Процедура СообщитьОНеобходимомМинимальномСтатусеДокумента(ЭтоРезервирование, МинимальныйСтатус) Экспорт
	
	ТекстОшибки = ?(ЭтоРезервирование,
		НСтр("ru = 'Минимальный статус документа для резервирования под назначение - ""%1"".'"),
		НСтр("ru = 'Минимальный статус документа для снятия резерва под назначение - ""%1"".'"));
	
	ТекстОшибки = СтрШаблон(ТекстОшибки, МинимальныйСтатус);
	
	ПоказатьПредупреждение(, ТекстОшибки);
	
КонецПроцедуры

// Конструктор параметров формы заполнения корректировки назначения.
// Возвращаемое значение:
//  Структура - структура с полями:
//   * Мастер - Булево - Истина, при закрытии формы заполнения будет создан документ,
//              Ложь - в результате товары для заполнения будут помещены во временное хранилище.
//   * ВидОперации - ПеречислениеСсылка.ВидыОперацийКорректировкиНазначения - вид операции.
//   * Назначение - СправочникСсылка.Назначения - назначение для которого необходимо заполнить документ.
//   * Заказ - ДокументСсылка - заказ в контексте которого вызывают заполнение.
//   * Организация - СправочникСсылка.СтруктураПредприятия - используется для отбора остатков под назначения заказов заданной организации.
//   * АдресТоваров - Строка - адрес хранилища с таблицей отбора товаров.
//   * КорректировкаНазначения - ДокументСсылка.КорректировкаНазначенияТоваров - ссылка на перезаполняемый документ, который ранее мог быть уже проведен.
//   * Количество - Число - используется в операции встречная корректировка.
Функция ПараметрыФормыЗаполненияКорректировкиНазначенияТоваров() Экспорт
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Мастер", Ложь);
	ПараметрыФормы.Вставить("ВидОперации", Неопределено);
	ПараметрыФормы.Вставить("Назначение", Неопределено);
	ПараметрыФормы.Вставить("Заказ", Неопределено);
	ПараметрыФормы.Вставить("Организация", Неопределено);
	ПараметрыФормы.Вставить("АдресТоваров", Неопределено);
	ПараметрыФормы.Вставить("КорректировкаНазначения", Неопределено);
	ПараметрыФормы.Вставить("КоличествоПередатьПодНазначение", Неопределено);
	ПараметрыФормы.Вставить("Назначения", Неопределено);
	ПараметрыФормы.Вставить("НесколькоНазначений", Ложь);

	Возврат ПараметрыФормы;
		
КонецФункции

#КонецОбласти
