
#Область СлужебныйПрограммныйИнтерфейс

Процедура УстановитьДоступностьЭлементовФормыУзла(ФормаУзла, ОбъектУзла) Экспорт
	
	ФормаУзла.Элементы.ГруппаДоступаПартнеров.Доступность = ФормаУзла.ИспользуютсяГруппыДоступаПартнеров 
	                                                        И (ФормаУзла.ИспользоватьПартнеровКакКонтрагентов
	                                                          ИЛИ ФормаУзла.СоздаватьПартнеровДляНовыхКонтрагентов);
	
КонецПроцедуры

Процедура УстановитьДоступностьНастроекОнлайнОплаты(ФормаУзла, ОбъектУзла) Экспорт
	
	ФормаУзла.Элементы.СоответствиеОнлайнОплат.Доступность = ФормаУзла.ОнлайнОплаты;
	
КонецПроцедуры

Процедура УстановитьДоступностьНастроекФормыОплаты(ФормаУзла, ОбъектУзла) Экспорт
	
	ФормаУзла.Элементы.СоответствиеФормОплат.Доступность = ФормаУзла.ЗагружатьФормыОплаты;
	
КонецПроцедуры

Процедура УстановитьПодсказкуОнлайнОплаты(ФормаУзла) Экспорт
	
	ЭлементОнлайнОплаты = ФормаУзла.Элементы.ОнлайнОплаты;
	ТекстЗаголовка = 
		НСтр("ru = 'Позволяет загружать с сайта он-лайн оплаты и пробивать по ним чеки оплат автоматически.
					|
					|Для загрузки он-лайн оплат необходимо:
					|	- установить признак ""Загружать он-лайн оплаты"";
					|	- настроить соответствие видов оплат.
					|
					|Для автоматического пробития чеков оплат необходимо
					|1. Включить опции в разделе ""НСИ и Администрирование"":
					|	- ""Продажи"" - ""Розничные продажи""
					|	- ""РМК и Оборудование"" - ""Использовать подключаемое оборудование""
					|2. Включить систему ""Обсуждения"": ""Все функции"" - ""Стандартные"" - ""Управление системой взаимодействия"".
					|3. В узле обмена с сайтом:
					|	- установить признак ""Загружать он-лайн оплаты"";
					|	- настроить соответствие видов оплат;
					|	- установить признак по каким видам оплаты нужна фискализация.
					|4. Подключить и настроить ККТ. 
					|5. Установить признак у оборудования ""Автоматическая фискализация чеков из очереди чеков"".'");
	ЭлементОнлайнОплаты.РасширеннаяПодсказка.Заголовок = ТекстЗаголовка;
	ЭлементОнлайнОплаты.РасширеннаяПодсказка.АвтоМаксимальнаяШирина = Ложь;
	ЭлементОнлайнОплаты.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	
КонецПроцедуры

Процедура ПроверитьДублированиеСтатусов(Форма, Объект, ОтменаРедактирования, Отказ) Экспорт
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	СоответствиеСтатусовЗаказов = Форма.СоответствиеСтатусовЗаказов;
	ТекущиеДанные = Форма.Элементы.СоответствиеСтатусовЗаказов.ТекущиеДанные;
	
	ИмяКолонки = "";
	Если Не ЕстьДублированиеСтатусов(ТекущиеДанные, СоответствиеСтатусовЗаказов, ИмяКолонки) Тогда
		
		ОчиститьСообщения();
		
		Статус = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			"СоответствиеСтатусовЗаказов",
			СоответствиеСтатусовЗаказов.Индекс(ТекущиеДанные) + 1,
			ИмяКолонки);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Такой статус заказа на сайте уже указан в другой строке таблицы!'"),
			Объект.Ссылка, Статус);
			
		Отказ = Истина;
		
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеПроцедурыИФункции

Функция ЕстьДублированиеСтатусов(ТекущиеДанные, СоответствиеСтатусовЗаказов, ИмяКолонки)
	
	
	СтатусЗаказаНаСайте       = ТекущиеДанные.СтатусЗаказаНаСайте;
	СостояниеЗаказаПокупателя = ТекущиеДанные.СтатусЗаказаВБазе;
	
	Если НЕ ПустаяСтрока(СтатусЗаказаНаСайте) Тогда
 		Найдено = СоответствиеСтатусовЗаказов.НайтиСтроки(Новый Структура("СтатусЗаказаНаСайте",
			СтатусЗаказаНаСайте));
		Если Найдено.Количество() > 1 Тогда
			ИмяКолонки = "СтатусЗаказаНаСайте";
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции


#КонецОбласти