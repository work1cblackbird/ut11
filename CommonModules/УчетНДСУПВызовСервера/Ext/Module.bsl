#Область СлужебныйПрограммныйИнтерфейс

// Возвращает список типов налогообложения операций продажи и/или закупки, доступных в конфигурации.
//
// Параметры:
// 	Продажи - Булево - Признак необходимости вернуть типы налогообложения продажи
// 	Закупки - Булево - Признак необходимости вернуть типы налогообложения закупки
// 	РаздельныйУчет - Булево - Признак необходимости дополнить видами деятельности НДС
//
// Возвращаемое значение:
// 	СписокЗначений из ПеречислениеСсылка.ТипыНалогообложенияНДС
//
Функция ПрименяемыеТипыНалогообложенияНДС(Продажи, Закупки, РаздельныйУчет) Экспорт
	
	Возврат УчетНДСУПСлужебный.ПрименяемыеТипыНалогообложенияНДС(Продажи, Закупки, РаздельныйУчет)
	
КонецФункции

// Возвращает значение ставки НДС по умолчанию.
//
// Параметры:
// 	Организация - СправочникСсылка.Организации - Организация, для которой необходимо получить ставку НДС.
//  Дата - Дата - Дата, на которую необходимо получить ставку НДС по умолчанию.
//  РасчетнаяСтавка - Булево - указывает необходимость получения расчетной ставки НДС X/(100 + X).
//
// Возвращаемое значение:
//  СправочникСсылка.СтавкиНДС - значение ставки НДС.
//
Функция СтавкаНДСПоУмолчанию(Организация, Дата, РасчетнаяСтавка = Ложь) Экспорт
	
	Возврат УчетНДСУП.СтавкаНДСПоУмолчанию(Организация, Дата, РасчетнаяСтавка);
	
КонецФункции

// Возвращает список незначащих ставок НДС (процент = 0)
//
// Возвращаемое значение:
//  СписокЗначений из СправочникСсылка.СтавкиНДС - Список ставок НДС с процентной ставкой 0
//
Функция СписокНезначащихСтавок() Экспорт
	
	СписокЗначений = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтавкиНДС.Ссылка КАК Ссылка,
		|	СтавкиНДС.Представление КАК Представление
		|ИЗ
		|	Справочник.СтавкиНДС КАК СтавкиНДС
		|ГДЕ
		|	СтавкиНДС.Ставка = 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СписокЗначений.Добавить(ВыборкаДетальныеЗаписи.Ссылка, ВыборкаДетальныеЗаписи.Представление);
	КонецЦикла;
	
	Возврат СписокЗначений;
	
КонецФункции

// Функция возвращает процент НДС.
//
// Параметры:
//	СтавкаНДС - СправочникСсылка.СтавкиНДС - Значение ставки НДС.
//
// Возвращаемое значение:
//	Число - Процент НДС.
//
Функция ЗначениеСтавкиНДС(СтавкаНДС) Экспорт
	
	Если Не ЗначениеЗаполнено(СтавкаНДС) Тогда
		Возврат 0;
	КонецЕсли;

	Если ТипЗнч(СтавкаНДС) = Тип("СправочникСсылка.СтавкиНДС") Тогда
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтавкаНДС, "Ставка");
		
	//++ Локализация
	
	ИначеЕсли ТипЗнч(СтавкаНДС) = Тип("ПеречислениеСсылка.СтавкиНДС") Тогда
		Если СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10")
			Или СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10_110") Тогда
			Возврат 10;
		ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18")
			Или СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18_118") Тогда
			Возврат 18;
		ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20")
			Или СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20_120") Тогда
			Возврат 20;
		КонецЕсли;
		
	//-- Локализация
		
	КонецЕсли;
	
КонецФункции

// Получает ставку НДС по типу налогообложения из табличной части справочника СтавкиНДС
// 
// Параметры:
//  ТипНалогообложенияНДС - ПеречислениеСсылка.ТипыНалогообложенияНДС - Данные для поиска ставки НДС.
//  Организация - СправочникСсылка.Организации - Организация, для которой необходимо получить ставку НДС
//  Дата -  Дата - Дата, на которую необходимо получить ставку НДС.
// 
// Возвращаемое значение:
//  СправочникСсылка.СтавкиНДС -
//
Функция СтавкаНДСПоТипуНалогообложения(ТипНалогообложенияНДС, Организация, Дата) Экспорт
	
	Если ТипНалогообложенияНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС Тогда
		Возврат Справочники.СтавкиНДС.БезНДС;
	КонецЕсли;
	
	Если ТипНалогообложенияНДС = Перечисления.ТипыНалогообложенияНДС.РеверсивноеОбложениеНДС Тогда
		Возврат Справочники.СтавкиНДС.БезНДС;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОсновныеСтавкиНДС.СтавкаНДС
	|ИЗ
	|	РегистрСведений.ОсновныеСтавкиНДС.СрезПоследних(&Дата, Страна = &Страна) КАК ОсновныеСтавкиНДС
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтавкиНДСТипыНалогообложенияНДС.Ссылка КАК СтавкаНДС,
	|	1 КАК Приоритет
	|ИЗ
	|	Справочник.СтавкиНДС.ТипыНалогообложенияНДС КАК СтавкиНДСТипыНалогообложенияНДС
	|ГДЕ
	|	СтавкиНДСТипыНалогообложенияНДС.ТипНалогообложенияНДС = &ТипНалогообложенияНДС
	|	И СтавкиНДСТипыНалогообложенияНДС.Ссылка.Страна = &Страна
	|	И (СтавкиНДСТипыНалогообложенияНДС.Ссылка.НачалоПериода <= &Дата
	|			ИЛИ СтавкиНДСТипыНалогообложенияНДС.Ссылка.НачалоПериода = ДАТАВРЕМЯ(1, 1, 1))
	|	И (КОНЕЦПЕРИОДА(СтавкиНДСТипыНалогообложенияНДС.Ссылка.КонецПериода, ДЕНЬ) >= &Дата
	|			ИЛИ СтавкиНДСТипыНалогообложенияНДС.Ссылка.КонецПериода = ДАТАВРЕМЯ(1, 1, 1))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтавкиНДСТипыНалогообложенияНДС.Ссылка,
	|	2
	|ИЗ
	|	Справочник.СтавкиНДС.ТипыНалогообложенияНДС КАК СтавкиНДСТипыНалогообложенияНДС
	|ГДЕ
	|	СтавкиНДСТипыНалогообложенияНДС.ТипНалогообложенияНДС = &ТипНалогообложенияНДС
	|	И СтавкиНДСТипыНалогообложенияНДС.Ссылка.Страна = &ПустаяСтрана
	|	И (СтавкиНДСТипыНалогообложенияНДС.Ссылка.НачалоПериода <= &Дата
	|			ИЛИ СтавкиНДСТипыНалогообложенияНДС.Ссылка.НачалоПериода = ДАТАВРЕМЯ(1, 1, 1))
	|	И (КОНЕЦПЕРИОДА(СтавкиНДСТипыНалогообложенияНДС.Ссылка.КонецПериода, ДЕНЬ) >= &Дата
	|			ИЛИ СтавкиНДСТипыНалогообложенияНДС.Ссылка.КонецПериода = ДАТАВРЕМЯ(1, 1, 1))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет";
	
	Запрос.УстановитьПараметр("ТипНалогообложенияНДС", ТипНалогообложенияНДС);
	Запрос.УстановитьПараметр("Дата", ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("Страна", ЗначениеНастроекКлиентСерверПовтИсп.СтранаРегистрацииОрганизации(Организация));
	Запрос.УстановитьПараметр("ПустаяСтрана", Справочники.СтраныМира.ПустаяСсылка());
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	Если ТипНалогообложенияНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС Тогда
		ВыборкаОсновнойСтавкиНДС = РезультатыЗапроса[0].Выбрать();
		Если ВыборкаОсновнойСтавкиНДС.Следующий() Тогда
			Если ЗначениеЗаполнено(ВыборкаОсновнойСтавкиНДС.СтавкаНДС) Тогда
				Возврат ВыборкаОсновнойСтавкиНДС.СтавкаНДС;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатыЗапроса[1].Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Количество() = 1 Тогда
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Возврат ВыборкаДетальныеЗаписи.СтавкаНДС;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Справочники.СтавкиНДС.ПустаяСсылка();
	
КонецФункции

// Возвращает настройки учета НДС.
//
// Возвращаемое значение:
// 	 Структура - Параметры учета НДС:
// 	   * ИспользоватьУчетНДС - Булево, Произвольный - Значение опции использования учета НДС
// 	   * ОсновноеНалогообложениеНДСПродажи - ПеречислениеСсылка.ТипыНалогообложенияНДС - Основное налогообложение НДС организации.
// 	   * ОсновнойВидДеятельностиНДСЗакупки - ПеречислениеСсылка.ТипыНалогообложенияНДС - Основной вид деятельности НДС закупки товаров.
// 	   * РаздельныйУчетНДС - Булево, Произвольный - Признак наличия в ИБ организаций, использующих раздельный учет НДС по налогообложению.
// 	   * ИспользоватьЗаполнениеРаздела7ДекларацииПоНДС - Булево - Признак использования Раздела 7 в декларации.
// 	   * ПродажиНаЭкспорт - Булево, Произвольный - Применения экспортных операций.
// 	   * ЭкспортНесырьевыхТоваров - Булево - Признак того, что осуществляется экспорт несырьевых товаров.
// 	   * ЭкспортСырьевыхТоваровУслуг - Булево - Признак того, что осуществляется экспорт сырьевых товаров, работ или услуг. 
// 	   * ДатаРазделенияЭкспорта - Дата - Дата начала применения 150-ФЗ, разделение экспорта на сырьевой и несырьевой.
// 	   * ПродажаТоваровОблагаемыхНДСУПокупателя - Булево - Признак того, что осуществляется реализация товаров с обратным обложением НДС. 
// 	                                                       При реализации таких товаров НДС начисляет не продавец, а покупатель (налоговый агент).
// 	   * ПокупкаТоваровОблагаемыхНДСУПокупателя - Булево - Признак того, что осуществляется приобретение товаров с обратным обложением НДС. 
// 	   * ДатаНачалаУчетаНДСПоНаправлениямДеятельности - Дата - Дата, начиная с которой ведется учет входящего НДС в разрезе направлений деятельности.
// 	   * ДатаОтраженияВозвратовКорректировочнымиСФ - Дата - Дата, начиная с которой возвраты отражаются КСФ.
// 	   * РеверсивноеОбложениеНДС - Булево, Произвольный - примение операций с реверсивным обложением НДС
//
Функция НастройкиУчета() Экспорт
	
	НастройкиУчета = Новый Структура();
	НастройкиУчета.Вставить("ИспользоватьУчетНДС", ПолучитьФункциональнуюОпцию("ИспользоватьУчетНДС"));
	НастройкиУчета.Вставить("ОсновноеНалогообложениеНДСПродажи",
		?(ПолучитьФункциональнуюОпцию("ИспользоватьУчетНДС"),
			Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС,
			Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС));
	НастройкиУчета.Вставить("ОсновнойВидДеятельностиНДСЗакупки",
		?(ПолучитьФункциональнуюОпцию("ИспользоватьУчетНДС"),
			Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС,
			Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС));
	НастройкиУчета.Вставить("РаздельныйУчетНДС",                             ПолучитьФункциональнуюОпцию("ИспользоватьРаздельныйУчетПоНалогообложению"));
	НастройкиУчета.Вставить("ИспользоватьЗаполнениеРаздела7ДекларацииПоНДС", Ложь);
	НастройкиУчета.Вставить("ПродажиНаЭкспорт",                              ПолучитьФункциональнуюОпцию("ИспользоватьПродажиНаЭкспорт"));
	НастройкиУчета.Вставить("ЭкспортНесырьевыхТоваров",                      Ложь);
	НастройкиУчета.Вставить("ЭкспортСырьевыхТоваровУслуг",                   Ложь);
	НастройкиУчета.Вставить("ДатаРазделенияЭкспорта",                        Дата(1,1,1));
	НастройкиУчета.Вставить("ПродажаТоваровОблагаемыхНДСУПокупателя",        Ложь);
	НастройкиУчета.Вставить("ПокупкаТоваровОблагаемыхНДСУПокупателя",        Ложь);
	НастройкиУчета.Вставить("ДатаНачалаУчетаНДСПоНаправлениямДеятельности",  Дата(1,1,1));
	НастройкиУчета.Вставить("ДатаОтраженияВозвратовКорректировочнымиСФ",     Дата(1,1,1));
	НастройкиУчета.Вставить("РеверсивноеОбложениеНДС", ПолучитьФункциональнуюОпцию("ИспользоватьРеверсивноеОбложениеНДС"));
	
	УчетНДСЛокализация.ДополнитьНастройкиУчета(НастройкиУчета);
	
	Возврат НастройкиУчета;
	
КонецФункции

#КонецОбласти
