
#Область ПрограммныйИнтерфейс

// Подключает или отключает оборудования в зависимости от переданного статуса подключения
// 
// Параметры:
// 	СтатусПодключения - Булево - Статус подключения оборудования: Истина - подключить, Ложь - отключить
// 	Форма - ФормаКлиентскогоПриложения - Форма документа
// 	ИспользуемоеОборудование - Массив Из СправочникСсылка.ПодключаемоеОборудование - Доступное текущему пользователю
// 	 оборудование, в том числе эквайринговые терминалы.
// 	ТаблицаОборудования - ТаблицаЗначений - Список доступного оборудования печати чеков
// 	ОповещениеЗавершения - ОписаниеОповещения - Оповедение о завершении выполнения действия
Процедура УстановитьСтатусПодключенияОборудования(СтатусПодключения, Форма, ИспользуемоеОборудование, ТаблицаОборудования, ОповещениеЗавершения = Неопределено) Экспорт
	
	Для Каждого Оборудование Из ИспользуемоеОборудование Цикл
		Контекст = Новый Структура;
		Контекст.Вставить("Оборудование", Оборудование);
		Контекст.Вставить("ТаблицаОборудования", ТаблицаОборудования);
		Контекст.Вставить("УникальныйИдентификатор", Форма.УникальныйИдентификатор);
		Контекст.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
		
		Если СтатусПодключения Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("НачатьПодключениеОборудованиеПоИдентификаторуЗавершение", ЭтотОбъект, Контекст);
			МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПоИдентификатору(ОписаниеОповещения, Форма.УникальныйИдентификатор, Оборудование);
		Иначе
			ОписаниеОповещения = Новый ОписаниеОповещения("НачатьОтключениеОборудованиеПоИдентификаторуЗавершение", ЭтотОбъект, Контекст);
			МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПоИдентификатору(ОписаниеОповещения, Форма.УникальныйИдентификатор, Оборудование);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Открыть форму предварительного просмотра чека ККМ.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма.
//  ПараметрыОперации - Структура - Структура со свойствами:
//   * ДокументСсылка - ДокументСсылка -;
//   * Организация - СправочникСсылка.Организации -;
//   * ТорговыйОбъект - СправочникСсылка -;
//   * ПодключенноеОборудование - СправочникСсылка.ПодключаемоеОборудование - ;
//   * Сумма - Число -;
//   * ВыемкаДенежныхСредствИзКассыККМ - ДокументСсылка.ВыемкаДенежныхСредствИзКассыККМ - ;
//   * ВнесениеДенежныхСредствВКассуККМ - ДокументСсылка.ВнесениеДенежныхСредствВКассуККМ - ;
//  РежимЗаписи - РежимЗаписиДокумента - Режим записи.
//  ОповещениеПриЗавершении - ОписаниеОповещения - Описание оповещения.
//
Процедура ПробитьЧек(Форма, ПараметрыОперации, РежимЗаписи, ОповещениеПриЗавершении = Неопределено) Экспорт
	
	ОчиститьСообщения();
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма",                   Форма);
	ДополнительныеПараметры.Вставить("ПараметрыОперации",       ПараметрыОперации);
	ДополнительныеПараметры.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	ДополнительныеПараметры.Вставить("РежимЗаписиДокумента",    РежимЗаписи);
	ДополнительныеПараметры.Вставить("ИмяПроцедуры",            "ПробитиеЧекаПослеЗаписиДокумента");
	
	ОбработатьРежимЗаписи(Истина, ДополнительныеПараметры);
	
КонецПроцедуры

// Пробить чек выемки денежных средств.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма.
//  ПараметрыОперации - Структура - Структура со свойствами:
//   * ДокументСсылка - ДокументСсылка -;
//   * Организация - СправочникСсылка.Организации -;
//   * ТорговыйОбъект - СправочникСсылка -;
//   * ПодключенноеОборудование - СправочникСсылка.ПодключаемоеОборудование - ;
//   * Сумма - Число -;
//   * ВыемкаДенежныхСредствИзКассыККМ - ДокументСсылка.ВыемкаДенежныхСредствИзКассыККМ - ;
//   * ВнесениеДенежныхСредствВКассуККМ - ДокументСсылка.ВнесениеДенежныхСредствВКассуККМ - ;
//  РежимЗаписи - РежимЗаписиДокумента - Режим записи.
//  ОповещениеПриЗавершении - ОписаниеОповещения - Описание оповещения.
//
Процедура ПробитьЧекВыемкаДенежныхСредств(Форма, ПараметрыОперации, РежимЗаписи, ОповещениеПриЗавершении) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма",                   Форма);
	ДополнительныеПараметры.Вставить("ПараметрыОперации",       ПараметрыОперации);
	ДополнительныеПараметры.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	ДополнительныеПараметры.Вставить("РежимЗаписиДокумента",    РежимЗаписи);
	ДополнительныеПараметры.Вставить("ИмяПроцедуры",            "ПробитиеЧекаВыемкаДенежныхСредствПослеЗаписиДокумента");
	
	РозничныеПродажиКлиент.ОбработатьСостояниеСмены(
		Форма,
		Новый ОписаниеОповещения("ОбработатьРежимЗаписи", ЭтотОбъект, ДополнительныеПараметры));
	
КонецПроцедуры

// Пробить чек внесения денежных средств.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма.
//  ПараметрыОперации - Структура - Структура со свойствами:
//   * ДокументСсылка - ДокументСсылка -;
//   * Организация - СправочникСсылка.Организации -;
//   * ТорговыйОбъект - СправочникСсылка -;
//   * ПодключенноеОборудование - СправочникСсылка.ПодключаемоеОборудование - ;
//   * Сумма - Число -;
//   * ВыемкаДенежныхСредствИзКассыККМ - ДокументСсылка.ВыемкаДенежныхСредствИзКассыККМ - ;
//   * ВнесениеДенежныхСредствВКассуККМ - ДокументСсылка.ВнесениеДенежныхСредствВКассуККМ - ;
//  РежимЗаписи - РежимЗаписиДокумента - Режим записи.
//  ОповещениеПриЗавершении - ОписаниеОповещения - Описание оповещения.
//
Процедура ПробитьЧекВнесениеДенежныхСредств(Форма, ПараметрыОперации, РежимЗаписи, ОповещениеПриЗавершении) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма",                   Форма);
	ДополнительныеПараметры.Вставить("ПараметрыОперации",       ПараметрыОперации);
	ДополнительныеПараметры.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	ДополнительныеПараметры.Вставить("РежимЗаписиДокумента",    РежимЗаписи);
	ДополнительныеПараметры.Вставить("ИмяПроцедуры",            "ПробитиеЧекаВнесениеДенежныхСредствПослеЗаписиДокумента");
	
	РозничныеПродажиКлиент.ОбработатьСостояниеСмены(
		Форма,
		Новый ОписаниеОповещения("ОбработатьРежимЗаписи", ЭтотОбъект, ДополнительныеПараметры));
	
КонецПроцедуры

// Процедура формирует очередь чеков для последующей фискализации
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма.
//  ПараметрыОперации - Структура - Структура со свойствами:
//   * ДокументСсылка - ДокументСсылка -;
//   * Организация - СправочникСсылка.Организации -;
//   * ТорговыйОбъект - СправочникСсылка -;
//   * ПодключенноеОборудование - СправочникСсылка.ПодключаемоеОборудование - ;
//   * Сумма - Число -;
//   * ВыемкаДенежныхСредствИзКассыККМ - ДокументСсылка.ВыемкаДенежныхСредствИзКассыККМ - ;
//   * ВнесениеДенежныхСредствВКассуККМ - ДокументСсылка.ВнесениеДенежныхСредствВКассуККМ - ;
//  РежимЗаписи - РежимЗаписиДокумента - Режим записи.
//  ОповещениеПриЗавершении - ОписаниеОповещения - Описание оповещения.
//
Процедура СформироватьОчередьЧеков(Форма, ПараметрыОперации, РежимЗаписи, ОповещениеПриЗавершении = Неопределено) Экспорт
	
	ОчиститьСообщения();
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма",                   Форма);
	ДополнительныеПараметры.Вставить("ПараметрыОперации",       ПараметрыОперации);
	ДополнительныеПараметры.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	ДополнительныеПараметры.Вставить("РежимЗаписиДокумента",    РежимЗаписи);
	ДополнительныеПараметры.Вставить("ИмяПроцедуры",            "СформироватьОчередьЧековПослеЗаписиДокумента");
	
	ОбработатьРежимЗаписи(Истина, ДополнительныеПараметры);
	
КонецПроцедуры

// Открыть форму записи фискальной операции. Объединение с БПО
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма.
//  ДокументОснование - ДокументСсылка - Ссылка на документ-основание
//  ИдентификаторЗаписи - Строка, Неопределено - Идентификатор записи;
//  ТипРасчета - ПеречислениеСсылка.ТипыРасчетаДенежнымиСредствами - Тип расчета
//
Процедура ОткрытьЗаписьФискальнойОперации(Форма, Знач ДокументОснование, ИдентификаторЗаписи = Неопределено, ТипРасчета = Неопределено) Экспорт
	
	Если ИдентификаторЗаписи = Неопределено Тогда
		ДанныеФискальнойОперации = ФормированиеФискальныхЧековВызовСервера.ДанныеПробитогоФискальногоЧекаПоДокументу(ДокументОснование, ТипРасчета);
		Если ДанныеФискальнойОперации = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ДокументОснование = ДанныеФискальнойОперации.ДокументОснование;
		ИдентификаторЗаписи = ДанныеФискальнойОперации.ИдентификаторЗаписи;
	КонецЕсли;
	
	ПараметрыЗаписи = Новый Структура();
	ПараметрыЗаписи.Вставить("ДокументОснование",   ДокументОснование);
	ПараметрыЗаписи.Вставить("ИдентификаторЗаписи", ИдентификаторЗаписи);
	
	ЗначениеКлюча = Новый Массив;
	ЗначениеКлюча.Добавить(ПараметрыЗаписи);
	
	КлючЗаписи = Новый("РегистрСведенийКлючЗаписи.ФискальныеОперации", ЗначениеКлюча);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", КлючЗаписи);
	
	ОткрытьФорму("РегистрСведений.ФискальныеОперации.Форма.ФормаЗаписи", ПараметрыФормы, Форма);
	
КонецПроцедуры

// Проверяет, подключается ли кассовое оборудование к документу или форме предпросмотра чека.
// 
// Параметры:
//  ДокументСсылка - ДокументСсылка - Документ, по которому нужно проверить статус
// 
// Возвращаемое значение:
//  Булево - Истина, если кассовое оборудование подключается к документу
Функция КассовоеОборудованиеПодключаетсяКДокументу(ДокументСсылка) Экспорт
	
	КассовоеОборудованиеПодключаетсяКДокументу = Ложь;
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") Тогда
		КассовоеОборудованиеПодключаетсяКДокументу = Истина;
	КонецЕсли;
	
	Возврат КассовоеОборудованиеПодключаетсяКДокументу;
	
КонецФункции

// Получить структуру реквизитов операции инкассации кассового узла.
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * Дата - Дата - Дата операции.
//   * ДокументОснование - ДокументСсылка - Ссылка на документ-основание.
//   * Организация - ОпределяемыйТип.ОрганизацияБПО - Организация.
//   * ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектБПО - Торговый объект.
//   * Устройство - СправочникСсылка.ПодключаемоеОборудование - Устройство.
//   * ТипОперации - ПеречислениеСсылка.ТипыОперацииКассовогоУзла - Тип операции.
//   * Сумма - Число - Сумма.
//   * ДополнительныеПараметры - Структура - Дополнительные параметры.
//
Функция СтруктураРеквизитыОперацииИнкассацииКассовогоУзла() Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	
	Реквизиты = Новый Структура;
	
	Реквизиты.Вставить("Дата");
	Реквизиты.Вставить("ДокументОснование");
	Реквизиты.Вставить("Организация");
	Реквизиты.Вставить("ТорговыйОбъект");
	Реквизиты.Вставить("Устройство");
	
	Реквизиты.Вставить("ТипОперации");
	Реквизиты.Вставить("Сумма");
	
	Реквизиты.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
	
	Возврат Реквизиты;
	
КонецФункции

// Записать объект.
//
// Параметры:
//  Форма - РасширениеУправляемойФормыДляСправочника - Форма.
//  РежимЗаписиДокумента - РежимЗаписиДокумента - Режим записи.
//  ОписаниеОповещения - ОписаниеОповещения - Описание оповещения.
//
Процедура ЗаписатьОбъект(Форма, РежимЗаписиДокумента, ОписаниеОповещения) Экспорт
	
	ОтветЧерезОписаниеОповещения = ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(Форма, "НеВыполнятьПроверкуПередЗаписью");
	
	Проведен = Ложь;
	Попытка
		
		Если ОтветЧерезОписаниеОповещения Тогда
			
			Форма.Записать(
				Новый Структура(
					"РежимЗаписи, ДействиеПослеЗаписи",
					РежимЗаписиДокумента,
					ОписаниеОповещения));
			
		Иначе
			
			Проведен = Форма.Записать(
				Новый Структура(
					"РежимЗаписи",
					РежимЗаписиДокумента));
			
		КонецЕсли;
		
	Исключение
		
		Проведен = Ложь;
		
	КонецПопытки;
	
	Если Не ОтветЧерезОписаниеОповещения Тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Проведен);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПослеОтветаНаВопросОЗаписиДокумента(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		
		Если ДополнительныеПараметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПриЗавершении, Ложь);
		КонецЕсли;
		
	Иначе
		
		ЗаписатьОбъект(
			ДополнительныеПараметры.Форма,
			ДополнительныеПараметры.РежимЗаписиДокумента,
			Новый ОписаниеОповещения(ДополнительныеПараметры.ИмяПроцедуры, ЭтотОбъект, ДополнительныеПараметры));
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
// 	Результат - Булево, Неопределено -;
// 	ДополнительныеПараметры - Структура:
// 		* Форма - ФормаКлиентскогоПриложения:
// 			** Объект - ДанныеФормыСтруктура:
// 				*** Ссылка - ДокументСсылка
//
Процедура ПробитиеЧекаПослеЗаписиДокумента(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено
		Или Не Результат Тогда
		
		Если ДополнительныеПараметры.РежимЗаписиДокумента = РежимЗаписиДокумента.Запись Тогда
			ПоказатьПредупреждение(
				Неопределено,
				НСтр("ru = 'Не удалось записать документ'"));
		Иначе
			ПоказатьПредупреждение(
				Неопределено,
				НСтр("ru = 'Не удалось провести документ'"));
		КонецЕсли;
		
		Если ДополнительныеПараметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПриЗавершении, Ложь);
		КонецЕсли;
		
	Иначе
		
		Если ДополнительныеПараметры.ПараметрыОперации.ДокументСсылка <> Неопределено
			И Не ЗначениеЗаполнено(ДополнительныеПараметры.ПараметрыОперации.ДокументСсылка) Тогда
			ДополнительныеПараметры.ПараметрыОперации.ДокументСсылка = ДополнительныеПараметры.Форма.Объект.Ссылка;
		КонецЕсли;
		
		ОткрытьФорму(
			"Обработка.ПредпросмотрЧека.Форма",
			ДополнительныеПараметры.ПараметрыОперации,
			ДополнительныеПараметры.Форма,,,,
			ДополнительныеПараметры.ОповещениеПриЗавершении);
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
// Результат - Булево, Неопределено - ;
// 	ДополнительныеПараметры - Структура:
// 		* Форма - ФормаКлиентскогоПриложения:
// 			** Объект - ДанныеФормыСтруктура:
// 				*** Ссылка - ДокументСсылка
//
Процедура ПробитиеЧекаВыемкаДенежныхСредствПослеЗаписиДокумента(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено
		Или Не Результат Тогда
		
		Если ДополнительныеПараметры.РежимЗаписиДокумента = РежимЗаписиДокумента.Запись Тогда
			ПоказатьПредупреждение(
				Неопределено,
				НСтр("ru = 'Не удалось записать документ'"));
		Иначе
			ПоказатьПредупреждение(
				Неопределено,
				НСтр("ru = 'Не удалось провести документ'"));
		КонецЕсли;
		
		Если ДополнительныеПараметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПриЗавершении, Ложь);
		КонецЕсли;
		
	Иначе
		
		Если ДополнительныеПараметры.ПараметрыОперации.ДокументСсылка <> Неопределено
			И Не ЗначениеЗаполнено(ДополнительныеПараметры.ПараметрыОперации.ДокументСсылка) Тогда
			ДополнительныеПараметры.ПараметрыОперации.ДокументСсылка = ДополнительныеПараметры.Форма.Объект.Ссылка;
		КонецЕсли;
		
		ОповещениеПриЗавершении = Новый ОписаниеОповещения("ЗаписатьВЖурналВыемкуДенежныхСредствВКассуККМ", ЭтотОбъект, ДополнительныеПараметры);
		
		ПараметрыОперации = Новый Структура;
		ПараметрыОперации.Вставить("ТипИнкассации", 0);
		ПараметрыОперации.Вставить("Сумма",         ДополнительныеПараметры.ПараметрыОперации.СуммаДокумента);
		ПараметрыОперации.Вставить("ДокументОснование", ДополнительныеПараметры.ПараметрыОперации.ДокументСсылка);
		
		ПараметрыОперацииНовый = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыИнкассации();
		ЗаполнитьЗначенияСвойств(ПараметрыОперацииНовый, ПараметрыОперации);
		
		ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьИнкассациюНаФискальномУстройстве(
			ОповещениеПриЗавершении,
			ДополнительныеПараметры.Форма.УникальныйИдентификатор,
			ДополнительныеПараметры.ПараметрыОперации.ПодключенноеОборудование,
			ПараметрыОперацииНовый);
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
// 	Результат - Булево, Строка - ;
// 	ДополнительныеПараметры - Структура:
// 		* Форма - ФормаКлиентскогоПриложения:
// 			** Объект - ДанныеФормыСтруктура:
// 				*** Ссылка - ДокументСсылка
//
Процедура ПробитиеЧекаВнесениеДенежныхСредствПослеЗаписиДокумента(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено
		Или Не Результат Тогда
		
		Если ДополнительныеПараметры.РежимЗаписиДокумента = РежимЗаписиДокумента.Запись Тогда
			ПоказатьПредупреждение(
				Неопределено,
				НСтр("ru = 'Не удалось записать документ'"));
		Иначе
			ПоказатьПредупреждение(
				Неопределено,
				НСтр("ru = 'Не удалось провести документ'"));
		КонецЕсли;
		
		Если ДополнительныеПараметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПриЗавершении, Ложь);
		КонецЕсли;
		
	Иначе
		
		Если ДополнительныеПараметры.ПараметрыОперации.ДокументСсылка <> Неопределено
			И Не ЗначениеЗаполнено(ДополнительныеПараметры.ПараметрыОперации.ДокументСсылка) Тогда
			ДополнительныеПараметры.ПараметрыОперации.ДокументСсылка = ДополнительныеПараметры.Форма.Объект.Ссылка;
		КонецЕсли;
		
		ОповещениеПриЗавершении = Новый ОписаниеОповещения("ЗаписатьВЖурналВнесениеДенежныхСредствВКассуККМ", ЭтотОбъект, ДополнительныеПараметры);
		
		ПараметрыОперации = Новый Структура;
		ПараметрыОперации.Вставить("ТипИнкассации", 1);
		ПараметрыОперации.Вставить("Сумма",         ДополнительныеПараметры.ПараметрыОперации.СуммаДокумента);
		ПараметрыОперации.Вставить("ДокументОснование", ДополнительныеПараметры.ПараметрыОперации.ДокументСсылка);
		
		ПараметрыОперацииНовый = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыИнкассации();
		ЗаполнитьЗначенияСвойств(ПараметрыОперацииНовый, ПараметрыОперации);
		
		ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьИнкассациюНаФискальномУстройстве(
			ОповещениеПриЗавершении,
			ДополнительныеПараметры.Форма.УникальныйИдентификатор,
			ДополнительныеПараметры.ПараметрыОперации.ПодключенноеОборудование,
			ПараметрыОперацииНовый);
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
// 	Результат - Булево, Неопределено - Результат выполнения
// 	ДополнительныеПараметры - Структура:
// 		* Форма - ФормаКлиентскогоПриложения:
// 			** Объект - ДанныеФормыСтруктура:
// 				*** Ссылка - ДокументСсылка
//
Процедура СформироватьОчередьЧековПослеЗаписиДокумента(Результат, ДополнительныеПараметры) Экспорт
	
	СформироватьОчередьЧековПослеЗаписиДокументаЛокализация(Результат, ДополнительныеПараметры);
	
КонецПроцедуры

// Параметры:
// 	РезультатВыполнения - Структура
// 	ДополнительныеПараметры - Структура:
//		* ОповещениеПриЗавершении - ОписаниеОповещения
//
Процедура ЗаписатьВЖурналВнесениеДенежныхСредствВКассуККМ(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	Если ДополнительныеПараметры.ОповещениеПриЗавершении <> Неопределено Тогда
		ДополнительныеПараметры.ОповещениеПриЗавершении.ДополнительныеПараметры.Вставить("РезультатВыполненияФискальнойОперации", РезультатВыполнения);
	КонецЕсли;
	
	Если РезультатВыполнения.Результат Тогда
		
		Форма = ДополнительныеПараметры.Форма;
		
		ПараметрыФискализации = Неопределено;
		// Если данные в регистр ФискальныеОперации не были записаны - произошла ошибка при записи.
		Если РезультатВыполнения.Свойство("ВыходныеПараметры") 
			 И РезультатВыполнения.ВыходныеПараметры.Количество() > 8 Тогда
			ПараметрыФискализации = РезультатВыполнения.ВыходныеПараметры[8];
		КонецЕсли;
		
		ПараметрыЗавершения = Новый Структура;
		ПараметрыЗавершения.Вставить("ЗакрытьФорму",                Ложь);
		ПараметрыЗавершения.Вставить("Форма",                       Форма);
		ПараметрыЗавершения.Вставить("ОповещениеПриЗавершении",     ДополнительныеПараметры.ОповещениеПриЗавершении);
		ПараметрыЗавершения.Вставить("ФискальнаяОперацияРеквизиты", ПараметрыФискализации);
		
		ВыполнитьДействиеПослеЗаписиФискальнойОперацииВЖурнал(
			Форма,
			Новый ОписаниеОповещения("ФискальнаяОперацияЗаписанаВЖурнал", ЭтотОбъект, ПараметрыЗавершения),
			НСтр("ru = 'Не удалось записать данные в журнал фискальных операций.'"));
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатВыполнения.ОписаниеОшибки);
		
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПриЗавершении, Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
// 	РезультатВыполнения - Структура
// 	ДополнительныеПараметры - Структура:
//		* ОповещениеПриЗавершении - ОписаниеОповещения
//
Процедура ЗаписатьВЖурналВыемкуДенежныхСредствВКассуККМ(РезультатВыполнения, ДополнительныеПараметры) Экспорт

	Если ДополнительныеПараметры.ОповещениеПриЗавершении <> Неопределено Тогда
		ДополнительныеПараметры.ОповещениеПриЗавершении.ДополнительныеПараметры.Вставить("РезультатВыполненияФискальнойОперации", РезультатВыполнения);
	КонецЕсли;
	
	Если РезультатВыполнения.Результат Тогда
		
		Форма = ДополнительныеПараметры.Форма;
		
		ПараметрыФискализации = Неопределено;
		// Если данные в регистр ФискальныеОперации не были записаны - произошла ошибка при записи.
		Если РезультатВыполнения.Свойство("ВыходныеПараметры") 
			 И РезультатВыполнения.ВыходныеПараметры.Количество() > 8 Тогда
			
			ПараметрыФискализации = РезультатВыполнения.ВыходныеПараметры[8];
		КонецЕсли;
		
		ПараметрыЗавершения = Новый Структура;
		ПараметрыЗавершения.Вставить("ЗакрытьФорму",                Ложь);
		ПараметрыЗавершения.Вставить("Форма",                       Форма);
		ПараметрыЗавершения.Вставить("ОповещениеПриЗавершении",     ДополнительныеПараметры.ОповещениеПриЗавершении);
		ПараметрыЗавершения.Вставить("ФискальнаяОперацияРеквизиты", ПараметрыФискализации);
		
		ВыполнитьДействиеПослеЗаписиФискальнойОперацииВЖурнал(
			Форма,
			Новый ОписаниеОповещения("ФискальнаяОперацияЗаписанаВЖурнал", ЭтотОбъект, ПараметрыЗавершения),
			НСтр("ru = 'Не удалось записать данные в журнал фискальных операций.'"));
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатВыполнения.ОписаниеОшибки);
		
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПриЗавершении, Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ФискальнаяОперацияЗаписанаВЖурнал(Результат, ДополнительныеПараметры) Экспорт
	
	ФискальнаяОперацияРеквизиты = ДополнительныеПараметры.ФискальнаяОперацияРеквизиты;
	
	Если Результат.ВыполненаОперацияНаУстройстве
		И Результат.ИзмененныеДанныеЗаписаны Тогда
		
		Если ДополнительныеПараметры.ЗакрытьФорму Тогда
			ДополнительныеПараметры.Форма.Закрыть(ФискальнаяОперацияРеквизиты);
		Иначе
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПриЗавершении, Истина);
		КонецЕсли;
		
	Иначе
		
		Если Результат.ВыполненаОперацияНаУстройстве И Не Результат.ИзмененныеДанныеЗаписаны Тогда
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Данные", ФискальнаяОперацияРеквизиты.ДокументОснование);
			ПараметрыФормы.Вставить("ДанныеДляЖурналаРегистрации", ФискальнаяОперацияРеквизиты);
			ПараметрыФормы.Вставить("ТекстСообщения",
				НСтр("ru = 'ВНИМАНИЕ! Произошла исключительная ситуация:
				           |Чек ККМ пробит, но не зафиксирован в системе.'"));
			
			ОткрытьФорму("Документ.ЧекККМ.Форма.ОшибкаЗаписи", ПараметрыФормы, ЭтотОбъект);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьРежимЗаписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено
		Или Не Результат Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПриЗавершении, Ложь);
	КонецЕсли;
	
	Если ДополнительныеПараметры.РежимЗаписиДокумента = РежимЗаписиДокумента.Запись
		И (Не ЗначениеЗаполнено(ДополнительныеПараметры.Форма.Объект.Ссылка) Или ДополнительныеПараметры.Форма.Модифицированность) Тогда
		
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ПослеОтветаНаВопросОЗаписиДокумента", ЭтотОбъект, ДополнительныеПараметры),
			НСтр("ru = 'Операция возможна только после записи документа, записать документ?'"),
			РежимДиалогаВопрос.ДаНет);
		
	ИначеЕсли ДополнительныеПараметры.РежимЗаписиДокумента = РежимЗаписиДокумента.Проведение
		И (Не ДополнительныеПараметры.Форма.Объект.Проведен Или ДополнительныеПараметры.Форма.Модифицированность) Тогда
		
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ПослеОтветаНаВопросОЗаписиДокумента", ЭтотОбъект, ДополнительныеПараметры),
			НСтр("ru = 'Операция возможна только после проведения документа, провести документ?'"),
			РежимДиалогаВопрос.ДаНет);
		
	Иначе
		ОписаниеОповещения = Новый ОписаниеОповещения(ДополнительныеПараметры.ИмяПроцедуры, ЭтотОбъект, ДополнительныеПараметры);
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОшибкаПриПроведенииЧекаВопросЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Отмена Тогда
		
		ТребуетсяПовторнаяПопыткаЗаписи = Ложь;
		Если ДополнительныеПараметры.ИмяПроцедуры = "ЗаписатьФискальнуюОперациюВТранзакции" Тогда
			РезультатОперации = ПодключаемоеОборудованиеУТВызовСервера.ЗаписатьФискальнуюОперациюВТранзакции(
				ТребуетсяПовторнаяПопыткаЗаписи,
				ДополнительныеПараметры.ОписаниеОповещения.ДополнительныеПараметры.ФискальнаяОперацияРеквизиты);
		КонецЕсли;
		
		Если (ТипЗнч(РезультатОперации) = Тип("Булево") И РезультатОперации) Тогда
			
			ВыполнитьОбработкуОповещения(
				ДополнительныеПараметры.ОписаниеОповещения,
				?(ДополнительныеПараметры.ВозвращатьРезультатФункции, РезультатОперации, ДополнительныеПараметры.РезультатПриУспешномПроведении));
			
		Иначе
			
			ПоказатьВопрос(
				Новый ОписаниеОповещения("ОшибкаПриПроведенииЧекаВопросЗавершение", ЭтотОбъект, ДополнительныеПараметры),
				ДополнительныеПараметры.ТекстСообщения,
				РежимДиалогаВопрос.ПовторитьОтмена, 10, КодВозвратаДиалога.Повторить,,КодВозвратаДиалога.Повторить);
			
		КонецЕсли;
		
	Иначе
		
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОписаниеОповещения,
			?(ДополнительныеПараметры.ВозвращатьРезультатФункции, ДополнительныеПараметры.РезультатОперации, ДополнительныеПараметры.РезультатПриОтмене));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьДействиеПослеЗаписиФискальнойОперацииВЖурнал(Форма, ОповещениеПриЗавершении, ТекстСообщения) Экспорт
	
	ТребуетсяПовторнаяПопыткаЗаписи = Ложь;
	ИзмененныеДанныеЗаписаны = ПодключаемоеОборудованиеУТВызовСервера.ЗаписатьФискальнуюОперациюВТранзакции(
		ТребуетсяПовторнаяПопыткаЗаписи,
		ОповещениеПриЗавершении.ДополнительныеПараметры.ФискальнаяОперацияРеквизиты);
	
	Если Не ИзмененныеДанныеЗаписаны Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Форма", Форма);
		ДополнительныеПараметры.Вставить("ОписаниеОповещения", ОповещениеПриЗавершении);
		ДополнительныеПараметры.Вставить("ТекстСообщения", ТекстСообщения);
		ДополнительныеПараметры.Вставить("ВозвращатьРезультатФункции", Ложь);
		ДополнительныеПараметры.Вставить("РезультатПриУспешномПроведении", Новый Структура("ВыполненаОперацияНаУстройстве, ИзмененныеДанныеЗаписаны", Истина, Истина));
		ДополнительныеПараметры.Вставить("РезультатПриОтмене", Новый Структура("ВыполненаОперацияНаУстройстве, ИзмененныеДанныеЗаписаны", Истина, Ложь));
		ДополнительныеПараметры.Вставить("ИмяПроцедуры", "ЗаписатьФискальнуюОперациюВТранзакции");
		ДополнительныеПараметры.Вставить("РезультатОперации", ИзмененныеДанныеЗаписаны);
		
		Если Не ТребуетсяПовторнаяПопыткаЗаписи Тогда
			ОшибкаПриПроведенииЧекаВопросЗавершение(КодВозвратаДиалога.Отмена, ДополнительныеПараметры);
			Возврат;
		КонецЕсли;
		
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ОшибкаПриПроведенииЧекаВопросЗавершение", ЭтотОбъект, ДополнительныеПараметры),
			ДополнительныеПараметры.ТекстСообщения,
			РежимДиалогаВопрос.ПовторитьОтмена, 10, КодВозвратаДиалога.Повторить,,КодВозвратаДиалога.Повторить);
		Возврат;
		
	Иначе
		
		ВыполнитьОбработкуОповещения(
			ОповещениеПриЗавершении,
			Новый Структура("ВыполненаОперацияНаУстройстве, ИзмененныеДанныеЗаписаны",
				Истина, Истина));
		
	КонецЕсли;
	
КонецПроцедуры

#Область РаботаСПодключаемымОборудованием

Процедура НачатьПодключениеОборудованиеПоИдентификаторуЗавершение(Данные, ДополнительныеПараметры) Экспорт
	
	Оборудование            = ДополнительныеПараметры.Оборудование;
	ТаблицаОборудования     = ДополнительныеПараметры.ТаблицаОборудования;
	УникальныйИдентификатор = ДополнительныеПараметры.УникальныйИдентификатор;
	
	Если Данные.Результат Тогда
		
		НайденныеСтроки = ТаблицаОборудования.НайтиСтроки(Новый Структура("Оборудование", Оборудование));
		
		Для Каждого СтрокаТЧ Из НайденныеСтроки Цикл
			
			Если Не СтрокаТЧ.Подключено Тогда
				
				СтрокаТЧ.Подключено = Истина;
				
				Контекст = Новый Структура;
				Контекст.Вставить("Оборудование",         СтрокаТЧ.Оборудование);
				Контекст.Вставить("ВерсияФФД",            СтрокаТЧ.ВерсияФФД);
				Контекст.Вставить("ОповещениеЗавершения", ДополнительныеПараметры.ОповещениеЗавершения);
				
				ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьПолучениеПараметровФискальногоУстройства(
					Новый ОписаниеОповещения("ПослеПолученияПараметровУстройства", ЭтотОбъект, Контекст),
					УникальныйИдентификатор,
					Оборудование);
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		ТекстСообщения = НСтр("ru = 'При подключении устройства %1 произошла ошибка:
									|""%2"".'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(ТекстСообщения, Оборудование, Данные.ОписаниеОшибки));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПолученияПараметровУстройства(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		
		ПодключаемоеОборудование = Новый Структура;
		ПодключаемоеОборудование.Вставить("Оборудование", ДополнительныеПараметры.Оборудование);
		ПодключаемоеОборудование.Вставить("ВерсияФФД",    ДополнительныеПараметры.ВерсияФФД);
		
		Если Не ЗначениеЗаполнено(ДополнительныеПараметры.ВерсияФФД)
			И ЗначениеЗаполнено(РезультатВыполнения.ПараметрыККТ.ВерсияФФДККТ) Тогда
			
			ПодключаемоеОборудование.ВерсияФФД = РезультатВыполнения.ПараметрыККТ.ВерсияФФДККТ;
			
		ИначеЕсли Не ЗначениеЗаполнено(ДополнительныеПараметры.ВерсияФФД) Тогда
			
			ПодключаемоеОборудование.ВерсияФФД = ФормированиеФискальныхЧековВызовСервера.МаксимальнаяВерсияФФД();
			
		КонецЕсли;
		
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеЗавершения, ПодключаемоеОборудование);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьОтключениеОборудованиеПоИдентификаторуЗавершение(Данные, Оборудование) Экспорт
	
	Если Не Данные.Результат Тогда
		
		ТекстСообщения = НСтр("ru = 'При отключении устройства %1 произошла ошибка:
									|""%2"".'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(ТекстСообщения, Оборудование, Данные.ОписаниеОшибки));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСоСканеромШтрихкода

Функция ПреобразоватьДанныеСоСканераВМассив(Параметр) Экспорт
	
	МенеджерОборудованияУТКлиент.ОбработатьСобытие();
	
	Данные = Новый Массив;
	Данные.Добавить(ПреобразоватьДанныеСоСканераВСтруктуру(Параметр));
	
	Возврат Данные;
	
КонецФункции

Функция ПреобразоватьДанныеСоСканераВСтруктуру(Параметр) Экспорт
	
	МенеджерОборудованияУТКлиент.ОбработатьСобытие();
	
	Если Параметр[1] = Неопределено Тогда
		Данные = Новый Структура("Штрихкод, Количество", Параметр[0], 1); 	 // Достаем штрихкод из основных данных
	Иначе
		Данные = Новый Структура("Штрихкод, Количество", Параметр[1][1], 1); // Достаем штрихкод из дополнительных данных
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

#КонецОбласти

#Область Локализация

Процедура СформироватьОчередьЧековПослеЗаписиДокументаЛокализация(Результат, ДополнительныеПараметры)
	
	РезультатЗаписиДокумента = Ложь;
	Если Результат = Неопределено
		Или Не Результат Тогда
		
		Если ДополнительныеПараметры.РежимЗаписиДокумента = РежимЗаписиДокумента.Запись Тогда
			ПоказатьПредупреждение(
				Неопределено,
				НСтр("ru = 'Не удалось записать документ'"));
		Иначе
			ПоказатьПредупреждение(
				Неопределено,
				НСтр("ru = 'Не удалось провести документ'"));
		КонецЕсли;
		
	Иначе
		
		РезультатЗаписиДокумента = Истина;
		Если Не ДополнительныеПараметры.ПараметрыОперации.Свойство("ДокументОснование")
			Или Не ЗначениеЗаполнено(ДополнительныеПараметры.ПараметрыОперации.ДокументОснование) Тогда
			ДополнительныеПараметры.ПараметрыОперации.Вставить("ДокументОснование", ДополнительныеПараметры.Форма.Объект.Ссылка);
		КонецЕсли;
		
//++ Локализация
		ПодключаемоеОборудованиеУТВызовСервера.СформироватьОчередьЧеков(ДополнительныеПараметры.ПараметрыОперации.ДокументОснование);
		
		ОткрытьФорму("РегистрСведений.ОчередьЧековККТ.ФормаСписка",
			Новый Структура("Отбор", ДополнительныеПараметры.ПараметрыОперации),
			ДополнительныеПараметры.Форма,,,,
			ДополнительныеПараметры.ОповещениеПриЗавершении,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		Возврат;
//-- Локализация
		
	КонецЕсли;
	
	Если ДополнительныеПараметры.ОповещениеПриЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПриЗавершении, РезультатЗаписиДокумента);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
