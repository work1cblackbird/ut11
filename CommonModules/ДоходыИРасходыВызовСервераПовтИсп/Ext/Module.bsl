
#Область СлужебныйПрограммныйИнтерфейс

#Область ВыборСтатейИАналитик

Функция ЗначенияРеквизитовСтатьи(Статья) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ТипЗначения", Новый ОписаниеТипов("Неопределено"));
	Результат.Вставить("КонтролироватьЗаполнениеАналитики", Ложь);
	Результат.Вставить("АналитикаРасходовЗаказРеализация",  Ложь);
	Результат.Вставить("БезАналитики", Ложь);
	
	Если Не ЗначениеЗаполнено(Статья) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ЗначенияРеквизитовСтатьи = Новый Структура();
	Если ТипЗнч(Статья) =  Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов") Тогда
		ЗначенияРеквизитовСтатьи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Статья, 
						"ТипЗначения,
						|КонтролироватьЗаполнениеАналитики,
						|АналитикаРасходовЗаказРеализация");
	ИначеЕсли ТипЗнч(Статья) = Тип("ПланВидовХарактеристикСсылка.СтатьиДоходов") Тогда
		ЗначенияРеквизитовСтатьи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Статья, 
						"ТипЗначения,
						|КонтролироватьЗаполнениеАналитики");
	ИначеЕсли ТипЗнч(Статья) = Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов") Тогда
		ЗначенияРеквизитовСтатьи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Статья, 
						"ТипЗначения,
						|БезАналитики");
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(Результат, ЗначенияРеквизитовСтатьи);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти