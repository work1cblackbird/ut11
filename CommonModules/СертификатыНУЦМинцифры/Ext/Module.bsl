///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает массив доверенных сертификатов
//
// Возвращаемое значение:
//   Массив из Структура:
//    * Наименование - Строка
//    * Хранилище - Строка - Имя хранилища
//    * ДействителенДо - Дата
//    * Сертификат - Строка - сертификат в виде Base64
//
Функция СписокДоверенныхСертификатов() Экспорт
	
	Если Не ОбщегоНазначенияБПО.ИспользуетсяБСП() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат СертификатыНУЦМинцифрыПовтИсп.СписокДоверенныхСертификатовИзМакета();
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает Истина если интегрирована минимально требуемая версия БСП
//
// Возвращаемое значение:
//  Булево - 
//
Функция ВерсияБСПДопустимаДляУстановкиКорневыхСертификатов() Экспорт
	
	Если Не ОбщегоНазначенияБПО.ИспользуетсяБСП() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Вызов БСП
	МодульОбщегоНазначенияКлиентСервер = ОбщегоНазначенияБПО.ОбщийМодуль("ОбщегоНазначенияКлиентСервер");
	ТекущаяВерсияБСП = ОбщегоНазначенияБПО.ВерсияБСП();
	ТекущаяВерсияБСПБезНомераСборки = 
		МодульОбщегоНазначенияКлиентСервер.ВерсияКонфигурацииБезНомераСборки(ТекущаяВерсияБСП);
	Если МодульОбщегоНазначенияКлиентСервер.СравнитьВерсииБезНомераСборки(ТекущаяВерсияБСПБезНомераСборки, "3.1.7")=0 Тогда
		МинимальнаяВерсияБСП = "3.1.7.305";
		Если МодульОбщегоНазначенияКлиентСервер.СравнитьВерсии(ТекущаяВерсияБСП, МинимальнаяВерсияБСП)>=0 Тогда
			Возврат Истина;
		КонецЕсли;
	ИначеЕсли МодульОбщегоНазначенияКлиентСервер.СравнитьВерсииБезНомераСборки(ТекущаяВерсияБСПБезНомераСборки, "3.1.8")=0 Тогда
		МинимальнаяВерсияБСП = "3.1.8.208";
		Если МодульОбщегоНазначенияКлиентСервер.СравнитьВерсии(ТекущаяВерсияБСП, МинимальнаяВерсияБСП)>=0 Тогда
			Возврат Истина;
		КонецЕсли;
	ИначеЕсли МодульОбщегоНазначенияКлиентСервер.СравнитьВерсииБезНомераСборки(ТекущаяВерсияБСПБезНомераСборки, "3.1.8")>0 Тогда
		Возврат Истина;
	КонецЕсли;
	// Конец Вызов БСП
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

