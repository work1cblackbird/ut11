////////////////////////////////////////////////////////////////////////////////
// Модуль содержит процедуры и функции для работы пользователя
// с журналами документов.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Поиск данных текущей строки списка.
// 
// Параметры:
//  СсылкаНаДокумент - ДокументСсылка - Ссылка на документ
//  СтруктураКлюча - Структура Из Строка - Структура ключа
//  КлючиСписка - Соответствие Из Строка -Ключи списка
Процедура НайтиДанныеТекущейСтрокиСписка(СсылкаНаДокумент, СтруктураКлюча, КлючиСписка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	РеестрДокументов.ТипСсылки КАК ТипСсылки,
	|	РеестрДокументов.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	РеестрДокументов.ДатаДокументаИБ КАК ДатаДокументаИБ
	|ИЗ
	|	РегистрСведений.РеестрДокументов КАК РеестрДокументов
	|ГДЕ
	|	РеестрДокументов.Ссылка = &Ссылка
	|	И РеестрДокументов.ИдентификаторЗаписи = &ИдентификаторЗаписи";
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаДокумент);
	Запрос.УстановитьПараметр("ИдентификаторЗаписи", СтруктураКлюча.ИдентификаторЗаписи);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Выборка.Следующий() Тогда
		СтруктураКлюча.Вставить(КлючиСписка.Получить("Дата"), Выборка.ДатаДокументаИБ);
		СтруктураКлюча.Вставить(КлючиСписка.Получить("ТипСсылки"), Выборка.ТипСсылки);
		СтруктураКлюча.Вставить(КлючиСписка.Получить("ХозяйственнаяОперация"), Выборка.ХозяйственнаяОперация);
	КонецЕсли;
	
КонецПроцедуры

// Сохранение настройки производительного режима обновления списка.
// 
// Параметры:
//  ПроизводительныйРежимОбновления - Булево -Включен режим ускорения списков
Процедура СохранитьНастройкуПроизводительныйРежимОбновленияСписка(ПроизводительныйРежимОбновления) Экспорт

	КлючиСохраняемыхНастроек = РаботаСЖурналамиДокументов.КлючиСохраняемыхНастроекПроизводительныйРежимОбновленияСписка();
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(КлючиСохраняемыхНастроек.КлючОбъекта,
		КлючиСохраняемыхНастроек.КлючНастроек, ПроизводительныйРежимОбновления);

КонецПроцедуры

#КонецОбласти