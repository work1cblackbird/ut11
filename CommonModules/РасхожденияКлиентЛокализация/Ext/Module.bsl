
#Область СлужебныйПрограммныйИнтерфейс

// Выполняет действие при нажатии гиперссылки в отчете "Оформляемые документы" по документу расхождений.
//
// Параметры:
//  Форма               - ФормаКлиентскогоПриложения - форма, из которой происходит вызов 
//  ОписаниеКоманды     - Строка - описание выполняемого действия, "Изменить" или "Оформить"
//  АктОРасхождениях    - ДокументСсылка.АктОРасхожденияхПослеОтгрузки - отрабатываемый акт о расхождениях.
//  ОснованиеАкта       - ДокументСсылка.РеализацияТоваровУслуг,
//                      - ДокументСсылка.ВозвратТоваровПоставщику      - основание акта о расхождениях, по которому
//                                                                 выполняются действия
//  ОформляемыйДокумент - Структура, ДокументСсылка - тут лежит пока либо пустая ссылка оформляемого типа, либо структура из такой же ссылки и хоз. операции.
//
Процедура ОбработкаКомандыОтчетаОформлениеДокументов(Форма, ОписаниеКоманды, АктОРасхождениях, ОснованиеАкта, ОформляемыйДокумент) Экспорт
	//++ Локализация
	Если ОписаниеКоманды = "Подтвердить" Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("УчетныйДокумент", ОснованиеАкта);
		
		ОписаниеОповещенияОПодтвержденииЭД = Новый ОписаниеОповещения("ПодтверждениеЭДПослеОкончания", ЭтотОбъект, ДополнительныеПараметры);
		ОбменСКонтрагентамиКлиент.ВыполнитьКомплекснуюОбработкуАктуальногоЭлектронногоДокумента(ОснованиеАкта, ОписаниеОповещенияОПодтвержденииЭД);
		
	ИначеЕсли ОписаниеКоманды = "Отклонить" Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("УчетныйДокумент", ОснованиеАкта);
		
		ОписаниеОповещенияОтклоненииЭД = Новый ОписаниеОповещения("ОтклонениеЭДПослеОкончания", ЭтотОбъект, ДополнительныеПараметры);
		ОбменСКонтрагентамиКлиент.ОтклонитьАктуальныйЭлектронныйДокумент(ОснованиеАкта, ОписаниеОповещенияОтклоненииЭД);
		
	КонецЕсли;
	//-- Локализация
КонецПроцедуры

//++ Локализация
Процедура ПодтверждениеЭДПослеОкончания(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	Оповестить("ЭлектронныйДокумент_Подтверждение", ДополнительныеПараметры.УчетныйДокумент);
	
КонецПроцедуры

Процедура ОтклонениеЭДПослеОкончания(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	Оповестить("ЭлектронныйДокумент_Отклонение", ДополнительныеПараметры.УчетныйДокумент);
	
КонецПроцедуры
//-- Локализация

#КонецОбласти