///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Расписание = Новый РасписаниеРегламентногоЗадания;
	Расписание.ПериодПовтораДней = ?(Значение, 1, 0);
	Расписание.ПериодПовтораВТечениеДня = ?(Значение, 43200, 0);
	
	Отбор = Новый Структура("Метаданные", Метаданные.РегламентныеЗадания.СерверЛояльностиОбработкаСостоянияБонусовПодарочныхСертификатов);
	ТекущиеЗадания	= РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	
	Если ЗначениеЗаполнено(ТекущиеЗадания) Тогда
		
		ПараметрыЗадания = Новый Структура;
		ПараметрыЗадания.Вставить("Использование", Значение);
		ПараметрыЗадания.Вставить("Расписание", Расписание);
		
		Для Каждого ТекущееЗадание Из ТекущиеЗадания Цикл
			
			РегламентныеЗаданияСервер.ИзменитьЗадание(ТекущееЗадание, ПараметрыЗадания);
			
		КонецЦикла;
		
	Иначе
		
		ПараметрыЗадания = Новый Структура;
		ПараметрыЗадания.Вставить("Использование", Значение);
		ПараметрыЗадания.Вставить("Метаданные", Метаданные.РегламентныеЗадания.СерверЛояльностиОбработкаСостоянияБонусовПодарочныхСертификатов);
		ПараметрыЗадания.Вставить("Ключ", Строка(Новый УникальныйИдентификатор));
		ПараметрыЗадания.Вставить("Расписание", Расписание);
		
		РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли