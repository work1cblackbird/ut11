//@strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ЗначениеИзменено; // Булево

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		
		ТекстСообщения = НСтр("ru = 'Запрещено изменение функционала ретро-бонусов в подчиненном узле обмена'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		
	КонецЕсли;
	
	ЗначениеИзменено = (Значение <> Константы.ИспользоватьРетроБонусыКлиентов.Получить());
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеИзменено Тогда
		
		ПараметрыНабора = УправлениеСвойствами.СтруктураПараметровНабораСвойств();
		ПараметрыНабора.Используется = ПолучитьФункциональнуюОпцию("ИспользоватьРетроБонусыКлиентов");
		
		УправлениеСвойствами.УстановитьПараметрыНабораСвойств(
			"Справочник_ВидыРетроБонусовКлиентов", ПараметрыНабора);
		УправлениеСвойствами.УстановитьПараметрыНабораСвойств(
			"Документ_УсловияРетроБонусовКлиентов", ПараметрыНабора);
		УправлениеСвойствами.УстановитьПараметрыНабораСвойств(
			"Документ_СписаниеРетроБонусовКлиента", ПараметрыНабора);
		УправлениеСвойствами.УстановитьПараметрыНабораСвойств(
			"Документ_НачислениеРетроБонусовКлиента", ПараметрыНабора);
			
		Если ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюС1СДокументооборот3")
		 ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюС1СДокументооборот") Тогда
			ОбновитьПовторноИспользуемыеЗначения();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли