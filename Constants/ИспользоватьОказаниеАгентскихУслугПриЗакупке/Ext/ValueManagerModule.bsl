#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыНабора = УправлениеСвойствами.СтруктураПараметровНабораСвойств();
	ПараметрыНабора.Используется = Константы.ИспользоватьОказаниеАгентскихУслугПриЗакупке.Получить();	
	УправлениеСвойствами.УстановитьПараметрыНабораСвойств("Документ_ОтчетКомитентуОЗакупках", ПараметрыНабора);
	
	Если ЭтотОбъект.Значение = Истина Тогда
		
		ИспользоватьЗаказыКлиентов = Константы.ИспользоватьЗаказыКлиентов.Получить();
		РежимИсполненияЗаказов     = Число(Константы.ИспользоватьРасширенныеВозможностиЗаказаКлиента.Получить())
			+ Число(Константы.ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента.Получить());
		
		МожноИспользоватьОбособлениеПоЗаказам = ИспользоватьЗаказыКлиентов
			И Константы.ИспользоватьОбособленноеОбеспечениеЗаказов.Получить()
			И РежимИсполненияЗаказов = 2;

		Если Не МожноИспользоватьОбособлениеПоЗаказам Тогда
			Константы.ВариантОбособленияТоваровВПродажах.Установить(Перечисления.ВариантыОбособленияТоваровВПродажах.Договор);
		КонецЕсли;
		
	Иначе
		
		ТекущийВариантОбособления                    = Константы.ВариантОбособленияТоваровВПродажах.Получить();
		ИспользоватьЗаказыКлиентов                   = Константы.ИспользоватьЗаказыКлиентов.Получить();
		ИспользоватьЗаявкиНаВозвратТоваровОтКлиентов = Константы.ИспользоватьЗаявкиНаВозвратТоваровОтКлиентов.Получить();
		
		Если ТекущийВариантОбособления = Перечисления.ВариантыОбособленияТоваровВПродажах.Договор Тогда
			
			Если ИспользоватьЗаказыКлиентов 
				Или ИспользоватьЗаявкиНаВозвратТоваровОтКлиентов Тогда
				Константы.ВариантОбособленияТоваровВПродажах.Установить(Перечисления.ВариантыОбособленияТоваровВПродажах.ЗаказКлиента);
			Иначе
				Константы.ВариантОбособленияТоваровВПродажах.Установить(Перечисления.ВариантыОбособленияТоваровВПродажах.ПустаяСсылка());
			КонецЕсли;
			
		ИначеЕсли Не (ИспользоватьЗаказыКлиентов Или ИспользоватьЗаявкиНаВозвратТоваровОтКлиентов)
					И Не ТекущийВариантОбособления = Перечисления.ВариантыОбособленияТоваровВПродажах.ПустаяСсылка() Тогда
			Константы.ВариантОбособленияТоваровВПродажах.Установить(Перечисления.ВариантыОбособленияТоваровВПродажах.ПустаяСсылка());
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
