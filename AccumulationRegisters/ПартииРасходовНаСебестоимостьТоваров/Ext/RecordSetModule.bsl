#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ РасчетСебестоимостиПрикладныеАлгоритмы.ФормироватьДвиженияРегистровУчетаСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства)
	 ИЛИ ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ПартииРасходов.Период                       КАК Период,
	|	ПартииРасходов.Регистратор                  КАК Регистратор,
	|	ПартииРасходов.Организация                  КАК Организация,
	|	ПартииРасходов.АналитикаУчетаНоменклатуры   КАК АналитикаУчетаНоменклатуры,
	|	ПартииРасходов.ДокументПоступления          КАК ДокументПоступления,
	|	ПартииРасходов.ВидЗапасов                   КАК ВидЗапасов,
	|	ПартииРасходов.АналитикаУчетаПартий         КАК АналитикаУчетаПартий,
	|	ПартииРасходов.ДокументПоступленияРасходов  КАК ДокументПоступленияРасходов,
	|	ПартииРасходов.СтатьяРасходов               КАК СтатьяРасходов,
	|	ПартииРасходов.Стоимость                    КАК Стоимость,
	|	ПартииРасходов.СтоимостьБезНДС              КАК СтоимостьБезНДС,
	|	ПартииРасходов.СтоимостьРегл                КАК СтоимостьРегл,
	|	ПартииРасходов.НДСРегл                      КАК НДСРегл,
	|	ПартииРасходов.ПостояннаяРазница            КАК ПостояннаяРазница,
	|	ПартииРасходов.ВременнаяРазница             КАК ВременнаяРазница
	|
	|ПОМЕСТИТЬ ПартииРасходовНаСебестоимостьТоваровПередЗаписью
	|ИЗ
	|	РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров КАК ПартииРасходов
	|ГДЕ
	|	ПартииРасходов.Регистратор = &Регистратор
	|	И (ПартииРасходов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ИЛИ &ЭтоОтменаПроведения)
	|");
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	ЭтоОтменаПроведения = ДополнительныеСвойства.СвойстваДокумента.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения;
	Запрос.УстановитьПараметр("ЭтоОтменаПроведения", ЭтоОтменаПроведения);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
	 ИЛИ Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства)
	 ИЛИ ПланыОбмена.ГлавныйУзел() <> Неопределено
	 ИЛИ РасчетСебестоимостиПрикладныеАлгоритмы.ДвиженияЗаписываютсяРасчетомПартийИСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ РасчетСебестоимостиПрикладныеАлгоритмы.ФормироватьДвиженияРегистровУчетаСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	МассивТекстовЗапроса = Новый Массив;
	
	ТекстТаблицыИзменений = 
	"ВЫБРАТЬ
	|	Таблица.Период                           КАК Период,
	|	Таблица.Регистратор                      КАК Регистратор,
	|	Таблица.Организация                      КАК Организация,
	|	Таблица.АналитикаУчетаНоменклатуры       КАК АналитикаУчетаНоменклатуры,
	|	Таблица.ДокументПоступления              КАК ДокументПоступления,
	|	Таблица.ВидЗапасов                       КАК ВидЗапасов,
	|	Таблица.АналитикаУчетаПартий             КАК АналитикаУчетаПартий,
	|	Таблица.ДокументПоступленияРасходов      КАК ДокументПоступленияРасходов,
	|	Таблица.СтатьяРасходов                   КАК СтатьяРасходов,
	|	СУММА(Таблица.Стоимость)                 КАК Стоимость,
	|	СУММА(Таблица.СтоимостьБезНДС)           КАК СтоимостьБезНДС,
	|	СУММА(Таблица.СтоимостьРегл)             КАК СтоимостьРегл,
	|	СУММА(Таблица.НДСРегл)                   КАК НДСРегл,
	|	СУММА(Таблица.ПостояннаяРазница)         КАК ПостояннаяРазница,
	|	СУММА(Таблица.ВременнаяРазница)          КАК ВременнаяРазница
	|ПОМЕСТИТЬ ТаблицаИзмененийПартииРасходовНаСебестоимостьТоваров
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Период                       КАК Период,
	|		Таблица.Регистратор                  КАК Регистратор,
	|		Таблица.Организация                  КАК Организация,
	|		Таблица.АналитикаУчетаНоменклатуры   КАК АналитикаУчетаНоменклатуры,
	|		Таблица.ДокументПоступления          КАК ДокументПоступления,
	|		Таблица.ВидЗапасов                   КАК ВидЗапасов,
	|		Таблица.АналитикаУчетаПартий         КАК АналитикаУчетаПартий,
	|		Таблица.ДокументПоступленияРасходов  КАК ДокументПоступленияРасходов,
	|		Таблица.СтатьяРасходов               КАК СтатьяРасходов,
	|		Таблица.Стоимость                    КАК Стоимость,
	|		Таблица.СтоимостьБезНДС              КАК СтоимостьБезНДС,
	|		Таблица.СтоимостьРегл                КАК СтоимостьРегл,
	|		Таблица.НДСРегл                      КАК НДСРегл,
	|		Таблица.ПостояннаяРазница            КАК ПостояннаяРазница,
	|		Таблица.ВременнаяРазница             КАК ВременнаяРазница
	|	ИЗ
	|		ПартииРасходовНаСебестоимостьТоваровПередЗаписью КАК Таблица
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПартииРасходов.Период                      КАК Период,
	|		ПартииРасходов.Регистратор                 КАК Регистратор,
	|		ПартииРасходов.Организация                 КАК Организация,
	|		ПартииРасходов.АналитикаУчетаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
	|		ПартииРасходов.ДокументПоступления         КАК ДокументПоступления,
	|		ПартииРасходов.ВидЗапасов                  КАК ВидЗапасов,
	|		ПартииРасходов.АналитикаУчетаПартий        КАК АналитикаУчетаПартий,
	|		ПартииРасходов.ДокументПоступленияРасходов КАК ДокументПоступленияРасходов,
	|		ПартииРасходов.СтатьяРасходов              КАК СтатьяРасходов,
	|		-ПартииРасходов.Стоимость                  КАК Стоимость,
	|		-ПартииРасходов.СтоимостьБезНДС            КАК СтоимостьБезНДС,
	|		-ПартииРасходов.СтоимостьРегл              КАК СтоимостьРегл,
	|		-ПартииРасходов.НДСРегл                    КАК НДСРегл,
	|		-ПартииРасходов.ПостояннаяРазница          КАК ПостояннаяРазница,
	|		-ПартииРасходов.ВременнаяРазница           КАК ВременнаяРазница
	|	ИЗ
	|		РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров КАК ПартииРасходов
	|	ГДЕ
	|		ПартииРасходов.Регистратор = &Регистратор
	|		И ПартииРасходов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Период,
	|	Таблица.Регистратор,
	|	Таблица.Организация,
	|	Таблица.АналитикаУчетаНоменклатуры,
	|	Таблица.ДокументПоступления,
	|	Таблица.ВидЗапасов,
	|	Таблица.АналитикаУчетаПартий,
	|	Таблица.ДокументПоступленияРасходов,
	|	Таблица.СтатьяРасходов
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Стоимость) <> 0
	|	ИЛИ СУММА(Таблица.СтоимостьБезНДС) <> 0
	|	ИЛИ СУММА(Таблица.СтоимостьРегл) <> 0
	|	ИЛИ СУММА(Таблица.НДСРегл) <> 0
	|	ИЛИ СУММА(Таблица.ПостояннаяРазница) <> 0
	|	ИЛИ СУММА(Таблица.ВременнаяРазница) <> 0
	|";
	МассивТекстовЗапроса.Добавить(ТекстТаблицыИзменений);
	МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ ПартииРасходовНаСебестоимостьТоваровПередЗаписью");
	
	Запрос = Новый Запрос();
	Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
