#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ РасчетСебестоимостиПрикладныеАлгоритмы.ФормироватьДвиженияРегистровУчетаСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства)
		Или ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Партии.Период,
	|	Партии.Регистратор,
	|	Партии.Организация,
	|	Партии.АналитикаУчетаНоменклатуры,
	|	Партии.ДокументПоступления,
	|	Партии.ВидЗапасов,
	|	Партии.АналитикаУчетаПартий,
	|
	|	Партии.Количество,
	|	Партии.Стоимость,
	|	Партии.СтоимостьБезНДС,
	|	Партии.СтоимостьРегл,
	|	Партии.НДСРегл,
	|	Партии.ПостояннаяРазница,
	|	Партии.ВременнаяРазница,
	|
	|	Партии.СтатьяКалькуляции,
	|	Партии.НалогообложениеНДС,
	|	Партии.Спецификация,
	|	Партии.ЗаказНаПроизводство,
	|	Партии.КодСтрокиПродукция,
	|	Партии.Этап
	|ПОМЕСТИТЬ ПартииПроизводственныхЗатратПередЗаписью
	|ИЗ
	|	РегистрНакопления.ПартииПроизводственныхЗатрат КАК Партии
	|ГДЕ
	|	Партии.Регистратор = &Регистратор
	|");
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства)
		Или ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ РасчетСебестоимостиПрикладныеАлгоритмы.ФормироватьДвиженияРегистровУчетаСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	Таблица.Период КАК Период,
	|	Таблица.Регистратор КАК Регистратор,
	|	Таблица.Организация КАК Организация,
	|	Таблица.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Таблица.ДокументПоступления КАК ДокументПоступления,
	|	Таблица.ВидЗапасов КАК ВидЗапасов,
	|	Таблица.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	Таблица.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	Таблица.НалогообложениеНДС КАК НалогообложениеНДС,
	|	Таблица.Спецификация КАК Спецификация,
	|	Таблица.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	Таблица.КодСтрокиПродукция КАК КодСтрокиПродукция,
	|	Таблица.Этап КАК Этап,
	|	СУММА(Таблица.Количество) КАК КоличествоИзменение,
	|	СУММА(Таблица.Стоимость) КАК СтоимостьИзменение,
	|	СУММА(Таблица.СтоимостьБезНДС) КАК СтоимостьБезНДСИзменение,
	|	СУММА(Таблица.СтоимостьРегл) КАК СтоимостьРеглИзменение,
	|	СУММА(Таблица.НДСРегл) КАК НДСРеглИзменение,
	|	СУММА(Таблица.ПостояннаяРазница) КАК ПостояннаяРазницаИзменение,
	|	СУММА(Таблица.ВременнаяРазница) КАК ВременнаяРазницаИзменение
	|ПОМЕСТИТЬ ТаблицаИзмененийПартииПроизводственныхЗатрат
	|ИЗ
	|	(ВЫБРАТЬ
	|		Партии.Период,
	|		Партии.Регистратор,
	|		Партии.Организация,
	|		Партии.АналитикаУчетаНоменклатуры,
	|		Партии.ДокументПоступления,
	|		Партии.ВидЗапасов,
	|		Партии.АналитикаУчетаПартий,
	|		Партии.Количество,
	|		Партии.Стоимость,
	|		Партии.СтоимостьБезНДС,
	|		Партии.СтоимостьРегл,
	|		Партии.НДСРегл,
	|		Партии.ПостояннаяРазница,
	|		Партии.ВременнаяРазница,
	|		Партии.СтатьяКалькуляции,
	|		Партии.НалогообложениеНДС,
	|		Партии.Спецификация,
	|		Партии.ЗаказНаПроизводство,
	|		Партии.КодСтрокиПродукция,
	|		Партии.Этап
	|	ИЗ
	|		ПартииПроизводственныхЗатратПередЗаписью КАК Партии
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Партии.Период,
	|		Партии.Регистратор,
	|		Партии.Организация,
	|		Партии.АналитикаУчетаНоменклатуры,
	|		Партии.ДокументПоступления,
	|		Партии.ВидЗапасов,
	|		Партии.АналитикаУчетаПартий,
	|		-Партии.Количество,
	|		-Партии.Стоимость,
	|		-Партии.СтоимостьБезНДС,
	|		-Партии.СтоимостьРегл,
	|		-Партии.НДСРегл,
	|		-Партии.ПостояннаяРазница,
	|		-Партии.ВременнаяРазница,
	|		Партии.СтатьяКалькуляции,
	|		Партии.НалогообложениеНДС,
	|		Партии.Спецификация,
	|		Партии.ЗаказНаПроизводство,
	|		Партии.КодСтрокиПродукция,
	|		Партии.Этап
	|	ИЗ
	|		РегистрНакопления.ПартииПроизводственныхЗатрат КАК Партии
	|	ГДЕ
	|		Партии.Регистратор = &Регистратор) КАК Таблица
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Период,
	|	Таблица.Регистратор,
	|	Таблица.Организация,
	|	Таблица.АналитикаУчетаНоменклатуры,
	|	Таблица.ДокументПоступления,
	|	Таблица.ВидЗапасов,
	|	Таблица.АналитикаУчетаПартий,
	|	Таблица.СтатьяКалькуляции,
	|	Таблица.НалогообложениеНДС,
	|	Таблица.Спецификация,
	|	Таблица.ЗаказНаПроизводство,
	|	Таблица.КодСтрокиПродукция,
	|	Таблица.Этап
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Количество) <> 0
	|	ИЛИ СУММА(Таблица.Стоимость) <> 0
	|	ИЛИ СУММА(Таблица.СтоимостьБезНДС) <> 0
	|	ИЛИ СУММА(Таблица.СтоимостьРегл) <> 0
	|	ИЛИ СУММА(Таблица.НДСРегл) <> 0
	|	ИЛИ СУММА(Таблица.ПостояннаяРазница) <> 0
	|	ИЛИ СУММА(Таблица.ВременнаяРазница) <> 0
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПартииПроизводственныхЗатратПередЗаписью");
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
