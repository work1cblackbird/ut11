#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
	
		БлокироватьДляИзменения = Истина;
		
		// Текущее состояние набора помещается во временную таблицу "ДвиженияТоварыКОтгрузкеПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно текущего.
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый);
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Таблица.Распоряжение   КАК Распоряжение,
		|	Таблица.Номенклатура   КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Серия          КАК Серия,
		|	Таблица.КодСтроки      КАК КодСтроки,
		|	Таблица.Склад          КАК Склад,
		|	Таблица.Заказано       КАК ЗаказаноПередЗаписью,
		|	Таблица.КОформлению    КАК КОформлениюПередЗаписью,
		|	Таблица.КПередаче      КАК КПередачеПередЗаписью,
		|	Таблица.Сумма          КАК СуммаПередЗаписью
		|ПОМЕСТИТЬ ДвиженияРаспоряженияНаОтгрузкуПередЗаписью
		|ИЗ
		|	РегистрНакопления.РаспоряженияНаОтгрузку КАК Таблица
		|ГДЕ
		|	Таблица.Регистратор = &Регистратор
		|	И НЕ &ЭтоНовый
		|";
		Запрос.Выполнить();
	КонецЕсли;

	Если Количество() = 0 Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РаспоряженияНаОтгрузку.Распоряжение КАК Распоряжение
		|ИЗ
		|	РегистрНакопления.РаспоряженияНаОтгрузку КАК РаспоряженияНаОтгрузку
		|ГДЕ
		|	РаспоряженияНаОтгрузку.Регистратор = &Регистратор";
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		РаспоряженияДляРасчетаСостояния = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Распоряжение");
		
		ДополнительныеСвойства.Вставить("РаспоряженияДляРасчетаСостояния", РаспоряженияДляРасчетаСостояния);
	КонецЕсли;

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		
		// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
		// и помещается во временную таблицу.
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаИзменений.Распоряжение                КАК Распоряжение,
		|	ТаблицаИзменений.Номенклатура                КАК Номенклатура,
		|	ТаблицаИзменений.Характеристика              КАК Характеристика,
		|	ТаблицаИзменений.Серия                       КАК Серия,
		|	ТаблицаИзменений.КодСтроки                   КАК КодСтроки,
		|	ТаблицаИзменений.Склад                       КАК Склад,
		|	СУММА(ТаблицаИзменений.ЗаказаноИзменение)    КАК ЗаказаноИзменение,
		|	СУММА(ТаблицаИзменений.КОформлениюИзменение) КАК КОформлениюИзменение,
		|	СУММА(ТаблицаИзменений.КПередачеИзменение)   КАК КПередачеИзменение,
		|	СУММА(ТаблицаИзменений.СуммаИзменение)       КАК СуммаИзменение
		|ПОМЕСТИТЬ ДвиженияРаспоряженияНаОтгрузкуИзменение
		|ИЗ
		|	(ВЫБРАТЬ
		|		Таблица.Распоряжение            КАК Распоряжение,
		|		Таблица.Номенклатура            КАК Номенклатура,
		|		Таблица.Характеристика          КАК Характеристика,
		|		Таблица.Серия                   КАК Серия,
		|		Таблица.КодСтроки               КАК КодСтроки,
		|		Таблица.Склад                   КАК Склад,
		|		Таблица.ЗаказаноПередЗаписью    КАК ЗаказаноИзменение,
		|		Таблица.КОформлениюПередЗаписью КАК КОформлениюИзменение,
		|		Таблица.КПередачеПередЗаписью   КАК КПередачеИзменение,
		|		Таблица.СуммаПередЗаписью       КАК СуммаИзменение
		|	ИЗ
		|		ДвиженияРаспоряженияНаОтгрузкуПередЗаписью КАК Таблица
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Таблица.Распоряжение           КАК Распоряжение,
		|		Таблица.Номенклатура           КАК Номенклатура,
		|		Таблица.Характеристика         КАК Характеристика,
		|		Таблица.Серия                  КАК Серия,
		|		Таблица.КодСтроки              КАК КодСтроки,
		|		Таблица.Склад                  КАК Склад,
		|		-Таблица.Заказано              КАК ЗаказаноПередЗаписью,
		|		-Таблица.КОформлению           КАК КОформлениюИзменение,
		|		-Таблица.КПередаче             КАК КПередачеИзменение,
		|		-Таблица.Сумма                 КАК СуммаПередЗаписью
		|	ИЗ
		|		РегистрНакопления.РаспоряженияНаОтгрузку КАК Таблица
		|	ГДЕ
		|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИзменений.Распоряжение,
		|	ТаблицаИзменений.Номенклатура,
		|	ТаблицаИзменений.Характеристика,
		|	ТаблицаИзменений.Серия,
		|	ТаблицаИзменений.КодСтроки,
		|	ТаблицаИзменений.Склад
		|
		|ИМЕЮЩИЕ
		|	СУММА(ТаблицаИзменений.КОформлениюИзменение) <> 0
		|	ИЛИ СУММА(ТаблицаИзменений.КПередачеИзменение) <> 0
		|	ИЛИ СУММА(ТаблицаИзменений.ЗаказаноИзменение) <> 0
		|	ИЛИ СУММА(ТаблицаИзменений.СуммаИзменение) <> 0
		|;
		|УНИЧТОЖИТЬ ДвиженияРаспоряженияНаОтгрузкуПередЗаписью
		|";
		
		МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
		РезультатЗапроса = МассивРезультатовЗапроса[0]; // РезультатЗапроса
		Выборка = РезультатЗапроса.Выбрать();
		ЕстьИзменения = Выборка.Следующий() И Выборка.Количество > 0;
		ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
				"ДвиженияРаспоряженияНаОтгрузкуИзменение", ЕстьИзменения);
		
		Если ЕстьИзменения
			И ПолучитьФункциональнуюОпцию("ИспользоватьДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров") Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров",
					Константы.ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров.Получить());
			Запрос.УстановитьПараметр("МерныеТипыЕдиницИзмерений",
					Справочники.УпаковкиЕдиницыИзмерения.МерныеТипыЕдиницИзмерений());
			Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
			
			ТекстЗапроса = "
			|ВЫБРАТЬ
			|	Таблица.Распоряжение,
			|	Таблица.Номенклатура,
			|	Таблица.Характеристика,
			|	Таблица.Серия,
			|	Таблица.Склад
			|ПОМЕСТИТЬ ДвиженияРаспоряженияНаОтгрузкуИзменениеМерныеТовары
			|ИЗ
			|	ДвиженияРаспоряженияНаОтгрузкуИзменение КАК Таблица
			|ГДЕ 
			|	Таблица.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины В (&МерныеТипыЕдиницИзмерений)
			|СГРУППИРОВАТЬ ПО
			|	Таблица.Распоряжение,
			|	Таблица.Номенклатура,
			|	Таблица.Характеристика,
			|	Таблица.Серия,
			|	Таблица.Склад
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|";
			
			// Основная таблица
			ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	РаспоряженияНаОтгрузку.ВидДвиженияРегистра    КАК ВидДвиженияРегистра,
			|	РаспоряженияНаОтгрузку.Распоряжение   КАК Распоряжение,
			|	РаспоряженияНаОтгрузку.Номенклатура   КАК Номенклатура,
			|	РаспоряженияНаОтгрузку.Характеристика КАК Характеристика,
			|	РаспоряженияНаОтгрузку.Склад          КАК Склад,
			|	РаспоряженияНаОтгрузку.Серия          КАК Серия,
			|	РаспоряженияНаОтгрузку.КОформлению    КАК КОформлению,
			|	РаспоряженияНаОтгрузку.КПередаче      КАК КПередаче
			|ПОМЕСТИТЬ ВТРаспоряженияНаОтгрузку
			|ИЗ
			|	РегистрНакопления.РаспоряженияНаОтгрузку КАК РаспоряженияНаОтгрузку
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|		ДвиженияРаспоряженияНаОтгрузкуИзменение КАК Изменения
			|		ПО РаспоряженияНаОтгрузку.Распоряжение      = Изменения.Распоряжение
			|			И РаспоряженияНаОтгрузку.Номенклатура   = Изменения.Номенклатура
			|			И РаспоряженияНаОтгрузку.Характеристика = Изменения.Характеристика
			|			И РаспоряженияНаОтгрузку.Склад          = Изменения.Склад
			|			И РаспоряженияНаОтгрузку.Серия          = Изменения.Серия
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|";
			
			// Допустимые отклонения
			ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	РаспоряженияНаОтгрузку.Распоряжение   КАК Распоряжение,
			|	РаспоряженияНаОтгрузку.Номенклатура   КАК Номенклатура,
			|	РаспоряженияНаОтгрузку.Характеристика КАК Характеристика,
			|	РаспоряженияНаОтгрузку.Склад          КАК Склад,
			|	РаспоряженияНаОтгрузку.Серия          КАК Серия,
			|	СУММА(РаспоряженияНаОтгрузку.КОформлению
			|		* (&ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров / 100)) КАК ДопустимоеОтклонение,
			|	СУММА(РаспоряженияНаОтгрузку.КПередаче
			|		* (&ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров / 100)) КАК ДопустимоеОтклонениеКПередаче
			|ПОМЕСТИТЬ ВТДопустимыеОтклоненияРаспоряженияНаОтгрузку
			|ИЗ
			|	ВТРаспоряженияНаОтгрузку КАК РаспоряженияНаОтгрузку
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияРаспоряженияНаОтгрузкуИзменениеМерныеТовары КАК Изменения
			|		ПО РаспоряженияНаОтгрузку.Распоряжение      = Изменения.Распоряжение
			|			И РаспоряженияНаОтгрузку.Номенклатура   = Изменения.Номенклатура
			|			И РаспоряженияНаОтгрузку.Характеристика = Изменения.Характеристика
			|			И РаспоряженияНаОтгрузку.Склад          = Изменения.Склад
			|			И РаспоряженияНаОтгрузку.Серия          = Изменения.Серия
			|ГДЕ
			|	РаспоряженияНаОтгрузку.ВидДвиженияРегистра = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженияНакопления.Приход)
			|СГРУППИРОВАТЬ ПО
			|	РаспоряженияНаОтгрузку.Распоряжение,
			|	РаспоряженияНаОтгрузку.Номенклатура,
			|	РаспоряженияНаОтгрузку.Характеристика,
			|	РаспоряженияНаОтгрузку.Склад,
			|	РаспоряженияНаОтгрузку.Серия
			|ИНДЕКСИРОВАТЬ ПО
			|	Распоряжение,
			|	Номенклатура,
			|	Характеристика,
			|	Склад,
			|	Серия
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|";
			
			//Остатки
			ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	РаспоряженияНаОтгрузку.Распоряжение       КАК Распоряжение,
			|	РаспоряженияНаОтгрузку.Номенклатура       КАК Номенклатура,
			|	РаспоряженияНаОтгрузку.Характеристика     КАК Характеристика,
			|	РаспоряженияНаОтгрузку.Склад              КАК Склад,
			|	РаспоряженияНаОтгрузку.Серия              КАК Серия,
			|	СУММА(РаспоряженияНаОтгрузку.КОформлению) КАК КОформлениюОстаток,
			|	СУММА(РаспоряженияНаОтгрузку.КПередаче)   КАК КПередачеОстаток
			|ПОМЕСТИТЬ ВТРаспоряженияНаОтгрузкуОстатки
			|ИЗ
			|	ВТРаспоряженияНаОтгрузку КАК РаспоряженияНаОтгрузку
			|СГРУППИРОВАТЬ ПО
			|	РаспоряженияНаОтгрузку.Распоряжение,
			|	РаспоряженияНаОтгрузку.Номенклатура,
			|	РаспоряженияНаОтгрузку.Характеристика,
			|	РаспоряженияНаОтгрузку.Склад,
			|	РаспоряженияНаОтгрузку.Серия
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|";
			
			ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РаспряженияОстатки.Распоряжение КАК Распоряжение
			|ИЗ
			|	(ВЫБРАТЬ
			|		РаспряженияОстатки.Распоряжение КАК Распоряжение
			|	ИЗ
			|		ВТРаспоряженияНаОтгрузкуОстатки КАК РаспряженияОстатки
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|			ВТДопустимыеОтклоненияРаспоряженияНаОтгрузку КАК ДопустимыеОтклонения
			|			ПО
			|				РаспряженияОстатки.Распоряжение     = ДопустимыеОтклонения.Распоряжение
			|				И РаспряженияОстатки.Номенклатура   = ДопустимыеОтклонения.Номенклатура
			|				И РаспряженияОстатки.Характеристика = ДопустимыеОтклонения.Характеристика
			|				И РаспряженияОстатки.Склад          = ДопустимыеОтклонения.Склад
			|				И РаспряженияОстатки.Серия          = ДопустимыеОтклонения.Серия
			|	ГДЕ
			|		РаспряженияОстатки.КОформлениюОстаток <= ДопустимыеОтклонения.ДопустимоеОтклонение
			|		И РаспряженияОстатки.КОформлениюОстаток >= -ДопустимыеОтклонения.ДопустимоеОтклонение
			|		И РаспряженияОстатки.КОформлениюОстаток <> 0
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		РаспряженияОстатки.Распоряжение КАК Распоряжение
			|	ИЗ
			|		ВТРаспоряженияНаОтгрузкуОстатки КАК РаспряженияОстатки
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|			ВТДопустимыеОтклоненияРаспоряженияНаОтгрузку КАК ДопустимыеОтклонения
			|			ПО
			|				РаспряженияОстатки.Распоряжение     = ДопустимыеОтклонения.Распоряжение
			|				И РаспряженияОстатки.Номенклатура   = ДопустимыеОтклонения.Номенклатура
			|				И РаспряженияОстатки.Характеристика = ДопустимыеОтклонения.Характеристика
			|				И РаспряженияОстатки.Склад          = ДопустимыеОтклонения.Склад
			|				И РаспряженияОстатки.Серия          = ДопустимыеОтклонения.Серия
			|	ГДЕ
			|		РаспряженияОстатки.КПередачеОстаток <= ДопустимыеОтклонения.ДопустимоеОтклонениеКПередаче
			|		И РаспряженияОстатки.КПередачеОстаток >= -ДопустимыеОтклонения.ДопустимоеОтклонениеКПередаче
			|		И РаспряженияОстатки.КПередачеОстаток <> 0
			|	) КАК РаспряженияОстатки
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|";
			
			Запрос.Текст = ТекстЗапроса;
			
			ВыборкаРаспоряжение = Запрос.Выполнить().Выбрать();
			
			Пока ВыборкаРаспоряжение.Следующий() Цикл
				
				РегистрыСведений.ОчередьЗаказовККорректировкеСтрокМерныхТоваров.ДобавитьЗаказВОчередь(ВыборкаРаспоряжение.Распоряжение);
				
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
	Распоряжения = Новый Массив;
	ТипыРаспоряжений = Новый Массив;
	Если Количество() Тогда
		Для Каждого Запись Из ЭтотОбъект Цикл
			Если Распоряжения.Найти(Запись.Распоряжение) = Неопределено Тогда
				Распоряжения.Добавить(Запись.Распоряжение);
			КонецЕсли;
		КонецЦикла;
	Иначе 
		Если ДополнительныеСвойства.Свойство("РаспоряженияДляРасчетаСостояния") Тогда
			Распоряжения = ДополнительныеСвойства.РаспоряженияДляРасчетаСостояния;
		КонецЕсли;
	КонецЕсли;    
	
	Если Распоряжения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Распоряжение Из Распоряжения Цикл
		Если ТипыРаспоряжений.Найти(ТипЗнч(Распоряжение)) = Неопределено Тогда
			ТипыРаспоряжений.Добавить(ТипЗнч(Распоряжение));
		КонецЕсли;
	КонецЦикла;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РаспоряженияНаОтгрузкуКВыполнению");
	ЭлементБлокировки.ИсточникДанных = Выгрузить();
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Распоряжение", "Распоряжение");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	Попытка
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РаспоряженияНаОтгрузкуОбороты.Распоряжение КАК Распоряжение,
		|	РаспоряженияНаОтгрузкуОбороты.Склад КАК Склад,
		|	РаспоряженияНаОтгрузкуОбороты.Номенклатура КАК Номенклатура,
		|	РаспоряженияНаОтгрузкуОбороты.Характеристика КАК Характеристика,
		|	РаспоряженияНаОтгрузкуОбороты.Серия КАК Серия,
		|	СУММА(ВЫБОР
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.ВидДвиженияРегистра = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженияНакопления.Приход)
		|			ТОГДА РаспоряженияНаОтгрузкуОбороты.КОформлениюОборот
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК КОформлениюПриход,
		|	СУММА(ВЫБОР
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.ВидДвиженияРегистра = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженияНакопления.Расход)
		|			ТОГДА -РаспоряженияНаОтгрузкуОбороты.КОформлениюОборот
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК КОформлениюРасход,
		|	СУММА(ВЫБОР
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.ВидДвиженияРегистра = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженияНакопления.Приход)
		|			ТОГДА РаспоряженияНаОтгрузкуОбороты.КПередачеОборот
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК КПередачеПриход,
		|	СУММА(ВЫБОР
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.ВидДвиженияРегистра = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженияНакопления.Расход)
		|			ТОГДА -РаспоряженияНаОтгрузкуОбороты.КПередачеОборот
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК КПередачеРасход,
		|	СУММА(ВЫБОР
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.ВидДвиженияРегистра = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженияНакопления.Приход)
		|			ТОГДА РаспоряженияНаОтгрузкуОбороты.ЗаказаноОборот
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК ЗаказаноПриход,
		|	СУММА(ВЫБОР
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.ВидДвиженияРегистра = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженияНакопления.Расход)
		|			ТОГДА -РаспоряженияНаОтгрузкуОбороты.ЗаказаноОборот
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК ЗаказаноРасход
		|ПОМЕСТИТЬ ВТРаспоряженияНаОтгрузкуОбороты
		|ИЗ
		|	РегистрНакопления.РаспоряженияНаОтгрузку.Обороты(,,, Распоряжение В (&Распоряжения)) КАК
		|		РаспоряженияНаОтгрузкуОбороты
		|ГДЕ
		|	(РаспоряженияНаОтгрузкуОбороты.КОформлениюОборот <> 0
		|	ИЛИ РаспоряженияНаОтгрузкуОбороты.КПередачеОборот <> 0
		|	ИЛИ РаспоряженияНаОтгрузкуОбороты.ЗаказаноОборот <> 0)
		|СГРУППИРОВАТЬ ПО
		|	РаспоряженияНаОтгрузкуОбороты.Распоряжение,
		|	РаспоряженияНаОтгрузкуОбороты.Склад,
		|	РаспоряженияНаОтгрузкуОбороты.Номенклатура,
		|	РаспоряженияНаОтгрузкуОбороты.Характеристика,
		|	РаспоряженияНаОтгрузкуОбороты.КодСтроки,
		|	РаспоряженияНаОтгрузкуОбороты.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РаспоряженияНаОтгрузкуОбороты.Распоряжение КАК Распоряжение,
		|	РаспоряженияНаОтгрузкуОбороты.Склад КАК Склад,
		|	РаспоряженияНаОтгрузкуОбороты.Номенклатура КАК Номенклатура,
		|	РаспоряженияНаОтгрузкуОбороты.Характеристика КАК Характеристика,
		|	РаспоряженияНаОтгрузкуОбороты.Серия КАК Серия,
		|	ВЫБОР
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.КОформлениюПриход = РаспоряженияНаОтгрузкуОбороты.КОформлениюРасход
		|		И РаспоряженияНаОтгрузкуОбороты.КОформлениюПриход > 0
		|			ТОГДА 1
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.КОформлениюРасход = 0
		|			ТОГДА 2
		|		ИНАЧЕ 3
		|	КОНЕЦ КАК СостояниеКОформлению,
		|	ВЫБОР
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.КПередачеПриход = РаспоряженияНаОтгрузкуОбороты.КПередачеРасход
		|			ТОГДА 1
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.КПередачеРасход = 0
		|		И РаспоряженияНаОтгрузкуОбороты.КПередачеПриход > 0
		|			ТОГДА 2
		|		ИНАЧЕ 3
		|	КОНЕЦ КАК СостояниеКПередаче,
		|	ВЫБОР
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.ЗаказаноПриход = РаспоряженияНаОтгрузкуОбороты.ЗаказаноРасход
		|			ТОГДА 1
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.ЗаказаноРасход = 0
		|		И РаспоряженияНаОтгрузкуОбороты.ЗаказаноПриход > 0
		|			ТОГДА 2
		|		ИНАЧЕ 3
		|	КОНЕЦ КАК СостояниеЗаказано
		|ПОМЕСТИТЬ ВТСостоянияРаспоряжений
		|ИЗ
		|	ВТРаспоряженияНаОтгрузкуОбороты КАК РаспоряженияНаОтгрузкуОбороты
		|ГДЕ
		|	НЕ РаспоряженияНаОтгрузкуОбороты.Распоряжение ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СостоянияРаспоряжений.Распоряжение КАК Распоряжение,
		|	СостоянияРаспоряжений.Склад КАК Склад,
		|	&Организация КАК Организация,
		|	&Номер КАК Номер,
		|	&Дата КАК Дата,
		|	&СуммаДокумента КАК СуммаДокумента,
		|	&Валюта КАК Валюта,
		|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	&Партнер КАК Партнер,
		|	&Контрагент КАК Контрагент,
		|	&Менеджер КАК Менеджер,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеКОформлению) = 1
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.Оформлены)
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеКОформлению) = 2
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.НеОформлены)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.ЧастичноОформлены)
		|	КОНЕЦ КАК СостояниеКОформлению,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеКПередаче) = 1
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.Оформлены)
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеКПередаче) = 2
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.НеОформлены)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.ЧастичноОформлены)
		|	КОНЕЦ КАК СостояниеКПередаче,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеЗаказано) = 1
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.Оформлены)
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеЗаказано) = 2
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.НеОформлены)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.ЧастичноОформлены)
		|	КОНЕЦ КАК СостояниеЗаказано
		|ИЗ
		|	ВТСостоянияРаспоряжений КАК СостоянияРаспоряжений
		|СГРУППИРОВАТЬ ПО
		|	СостоянияРаспоряжений.Распоряжение,
		|	СостоянияРаспоряжений.Склад
		|ИМЕЮЩИЕ
		|	(МАКСИМУМ(СостоянияРаспоряжений.СостояниеКОформлению) > 1
		|	ИЛИ МАКСИМУМ(СостоянияРаспоряжений.СостояниеКПередаче) > 1
		|	ИЛИ МАКСИМУМ(СостоянияРаспоряжений.СостояниеЗаказано) > 1)
		|ИТОГИ
		|ПО
		|	Распоряжение"; 
		
		Запрос.УстановитьПараметр("Распоряжения", Распоряжения);  
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Организация",
				ТекстЗапросаЗначениеРеквизитаИзСоставногоТипа("СостоянияРаспоряжений.Распоряжение","Организация", ТипыРаспоряжений));
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Номер",
				ТекстЗапросаЗначениеРеквизитаИзСоставногоТипа("СостоянияРаспоряжений.Распоряжение","Номер", ТипыРаспоряжений));
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Дата",
				ТекстЗапросаЗначениеРеквизитаИзСоставногоТипа("СостоянияРаспоряжений.Распоряжение","Дата", ТипыРаспоряжений));
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СуммаДокумента",
				ТекстЗапросаЗначениеРеквизитаИзСоставногоТипа("СостоянияРаспоряжений.Распоряжение","СуммаДокумента", ТипыРаспоряжений));
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Валюта",
				ТекстЗапросаЗначениеРеквизитаИзСоставногоТипа("СостоянияРаспоряжений.Распоряжение","Валюта", ТипыРаспоряжений));
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ХозяйственнаяОперация",
				ТекстЗапросаЗначениеРеквизитаИзСоставногоТипа("СостоянияРаспоряжений.Распоряжение","ХозяйственнаяОперация", ТипыРаспоряжений));
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Партнер",
				ТекстЗапросаЗначениеРеквизитаИзСоставногоТипа("СостоянияРаспоряжений.Распоряжение","Партнер", ТипыРаспоряжений));
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Контрагент",
				ТекстЗапросаЗначениеРеквизитаИзСоставногоТипа("СостоянияРаспоряжений.Распоряжение","Контрагент", ТипыРаспоряжений));
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Менеджер",
				ТекстЗапросаЗначениеРеквизитаИзСоставногоТипа("СостоянияРаспоряжений.Распоряжение","Менеджер", ТипыРаспоряжений));
		
		РезультатЗапроса = Запрос.Выполнить(); 
		Если РезультатЗапроса.Пустой() Тогда
			Для Каждого Распоряжение Из Распоряжения Цикл
				НаборРаспоряженияНаОтгрузкуКВыполнению = РегистрыСведений.РаспоряженияНаОтгрузкуКВыполнению.СоздатьНаборЗаписей();
				НаборРаспоряженияНаОтгрузкуКВыполнению.Отбор.Распоряжение.Установить(Распоряжение);
				НаборРаспоряженияНаОтгрузкуКВыполнению.Записать();
			КонецЦикла;
		Иначе
			ВыборкаРаспоряжение = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаРаспоряжение.Следующий() Цикл
				НаборРаспоряженияНаОтгрузкуКВыполнению = РегистрыСведений.РаспоряженияНаОтгрузкуКВыполнению.СоздатьНаборЗаписей();
				НаборРаспоряженияНаОтгрузкуКВыполнению.Отбор.Распоряжение.Установить(ВыборкаРаспоряжение.Распоряжение);
				Выборка = ВыборкаРаспоряжение.Выбрать();
				Пока Выборка.Следующий() Цикл
					Запись = НаборРаспоряженияНаОтгрузкуКВыполнению.Добавить();
					Запись.Распоряжение = Выборка.Распоряжение;
					Запись.Склад = Выборка.Склад;
					Запись.СостояниеКОформлению = Выборка.СостояниеКОформлению;
					Запись.СостояниеКПередаче = Выборка.СостояниеКПередаче;
					Запись.СостояниеЗаказано = Выборка.СостояниеЗаказано;
					Запись.Организация = Выборка.Организация;
					Запись.Номер = Выборка.Номер;
					Запись.Дата = Выборка.Дата;
					Запись.СуммаДокумента = Выборка.СуммаДокумента;
					Запись.Валюта = Выборка.Валюта;
					Запись.ХозяйственнаяОперация = Выборка.ХозяйственнаяОперация;
					Запись.Партнер = Выборка.Партнер;
					Запись.Контрагент = Выборка.Контрагент;
					Запись.Менеджер = Выборка.Менеджер;
				КонецЦикла;
				НаборРаспоряженияНаОтгрузкуКВыполнению.Записать();
			КонецЦикла;
		КонецЕсли;
	Исключение
		ВызватьИсключение(ОписаниеОшибки());
	КонецПопытки;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Текст запроса значение реквизита из составного типа.
// 
// Параметры:
//  ИмяТаблицы - Строка - Имя таблицы
//  ИмяРеквизита - Строка - Имя реквизита
//  ТипыРеквизита - Массив Из Тип - Массив типов реквизита
// 
// Возвращаемое значение:
//  Строка - Текст запроса значение реквизита из составного типа
Функция ТекстЗапросаЗначениеРеквизитаИзСоставногоТипа(ИмяТаблицы, ИмяРеквизита, ТипыРеквизита) Экспорт
	
	ТекстВыражения = "";
	ШаблонТекстаЗапроса ="ВЫБРАТЬ ВЫБОР КОГДА &ПолеСсылка ССЫЛКА Документ.ЗаказКлиента ТОГДА ВЫРАЗИТЬ(&ПолеСсылка КАК Документ.ЗаказКлиента).Ссылка"; //@Query-part
	ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "ВЫБРАТЬ ВЫБОР",""); //@Query-part
	СтандартныеРеквизиты = Новый Массив;
	СтандартныеРеквизиты.Добавить("Номер");
	СтандартныеРеквизиты.Добавить("Дата");

	Для Каждого Тип Из ТипыРеквизита Цикл
		МетаданныеРеквизита = Метаданные.НайтиПоТипу(Тип);
		
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта(ИмяРеквизита, МетаданныеРеквизита) 
			Или СтандартныеРеквизиты.Найти(ИмяРеквизита) <> Неопределено Тогда
				ПолноеИмя = СтрШаблон("Документ.%1", МетаданныеРеквизита.Имя); 
				ЧастьЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, ".Ссылка", СтрШаблон(".%1",ИмяРеквизита));
				ЧастьЗапроса = СтрЗаменить(ЧастьЗапроса, "&ПолеСсылка", ИмяТаблицы);
				ЧастьЗапроса = СтрЗаменить(ЧастьЗапроса, "Документ.ЗаказКлиента", ПолноеИмя);
				ТекстВыражения = ТекстВыражения + ЧастьЗапроса;
		КонецЕсли;
	КонецЦикла;
	ТекстВыражения = "ВЫБОР
	|	" + ТекстВыражения + "
	|
	|	КОНЕЦ"; //@Query-part
	
	Возврат ТекстВыражения;

КонецФункции
#КонецОбласти

#КонецЕсли