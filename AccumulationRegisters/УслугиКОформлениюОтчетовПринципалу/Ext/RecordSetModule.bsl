#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	БлокироватьДляИзменения = Истина;
	
	// Текущее состояние набора помещается во временную таблицу "ДвиженияУслугиКОформлениюОтчетовПринципалуПередЗаписью",
	// чтобы при записи получить изменение нового набора относительно текущего.
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Период						КАК Период,
	|	Таблица.Организация					КАК Организация,
	|	Таблица.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	Таблица.Соглашение  				КАК Соглашение,
	|	Таблица.Валюта   					КАК Валюта,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			Таблица.Количество
	|		ИНАЧЕ
	|			-Таблица.Количество
	|	КОНЕЦ                  КАК КоличествоПередЗаписью,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			Таблица.СуммаВыручки
	|		ИНАЧЕ
	|			-Таблица.СуммаВыручки
	|	КОНЕЦ                  КАК СуммаВыручкиПередЗаписью,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			Таблица.СуммаНДС
	|		ИНАЧЕ
	|			-Таблица.СуммаНДС
	|	КОНЕЦ                  КАК СуммаНДСПередЗаписью
	|ПОМЕСТИТЬ ДвиженияУслугиКОформлениюОтчетовПринципалуПередЗаписью
	|ИЗ
	|	РегистрНакопления.УслугиКОформлениюОтчетовПринципалу КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый
	|";
	Запрос.Выполнить();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ТаблицаИзменений.Период, МЕСЯЦ) КАК Период,
	|	ТаблицаИзменений.Организация КАК Организация,
	|	ТаблицаИзменений.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаИзменений.Соглашение КАК Соглашение,
	|	ТаблицаИзменений.Валюта КАК Валюта,
	|	СУММА(ТаблицаИзменений.Количество) КАК КоличествоИзменение,
	|	СУММА(ТаблицаИзменений.СуммаВыручки) КАК СуммаВыручкиИзменение,
	|	СУММА(ТаблицаИзменений.СуммаНДС) КАК СуммаНДСИзменение
	|ПОМЕСТИТЬ ДвиженияУслугиКОформлениюОтчетовПринципалуИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Организация КАК Организация,
	|		Таблица.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		Таблица.Соглашение КАК Соглашение,
	|		Таблица.Валюта КАК Валюта,
	|		Таблица.КоличествоПередЗаписью КАК Количество,
	|		Таблица.СуммаВыручкиПередЗаписью КАК СуммаВыручки,
	|		Таблица.СуммаНДСПередЗаписью КАК СуммаНДС
	|	ИЗ
	|		ДвиженияУслугиКОформлениюОтчетовПринципалуПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Период,
	|		Таблица.Организация,
	|		Таблица.АналитикаУчетаНоменклатуры,
	|		Таблица.Соглашение,
	|		Таблица.Валюта,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.Количество
	|			ИНАЧЕ Таблица.Количество
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.СуммаВыручки
	|			ИНАЧЕ Таблица.СуммаВыручки
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.СуммаНДС
	|			ИНАЧЕ Таблица.СуммаНДС
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.УслугиКОформлениюОтчетовПринципалу КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ТаблицаИзменений.Период, МЕСЯЦ),
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.АналитикаУчетаНоменклатуры,
	|	ТаблицаИзменений.Соглашение,
	|	ТаблицаИзменений.Валюта
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ТаблицаИзменений.Количество) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.СуммаВыручки) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.СуммаНДС) <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияУслугиКОформлениюОтчетовПринципалуПередЗаписью";
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
		
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ДвиженияУслугиКОформлениюОтчетовПринципалуИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
