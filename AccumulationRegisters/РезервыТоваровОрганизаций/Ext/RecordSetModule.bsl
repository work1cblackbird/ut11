#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	БлокироватьДляИзменения = Истина;
	
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
	КонецЕсли;
	
	ЗапасыСервер.СохранитьИсходныеДвиженияТоваровИРезервов(Отбор.Регистратор.Значение,
		ДополнительныеСвойства.МенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаПриЗаписи.Период КАК Период,
	|	ТаблицаПриЗаписи.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаПриЗаписи.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаПриЗаписи.АналитикаУчетаНоменклатуры.МестоХранения КАК Склад,
	|	ТаблицаПриЗаписи.Организация КАК Организация,
	|	ТаблицаПриЗаписи.КорОрганизация,
	|	ТаблицаПриЗаписи.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаПриЗаписи.КорВидЗапасов КАК КорВидЗапасов,
	|	ТаблицаПриЗаписи.ВидЗапасов.ТипЗапасов КАК ТипЗапасов,
	|	ТаблицаПриЗаписи.КорВидЗапасов.ТипЗапасов КАК ТипЗапасовКорВидаЗапасов,
	|	ТаблицаПриЗаписи.ВидЗапасов.Организация КАК ОрганизацияВидаЗапасов,
	|	ТаблицаПриЗаписи.НомерГТД КАК НомерГТД,
	|	ТаблицаПриЗаписи.Количество,
	|	ТаблицаПриЗаписи.ВидДвижения КАК ВидДвижения,
	|	ИСТИНА КАК ЭтоНовоеДвижение
	|ПОМЕСТИТЬ АналитикаРезервыТоваровОрганизаций
	|ИЗ
	|	РегистрНакопления.РезервыТоваровОрганизаций КАК ТаблицаПриЗаписи
	|ГДЕ
	|	ТаблицаПриЗаписи.Регистратор = &Регистратор";
	
	Если ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаПередЗаписью.Период,
		|	ТаблицаПередЗаписью.АналитикаУчетаНоменклатуры,
		|	ТаблицаПередЗаписью.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры,
		|	ТаблицаПередЗаписью.АналитикаУчетаНоменклатуры.МестоХранения,
		|	ТаблицаПередЗаписью.Организация,
		|	ТаблицаПередЗаписью.КорОрганизация,
		|	ТаблицаПередЗаписью.ВидЗапасов,
		|	ТаблицаПередЗаписью.КорВидЗапасов,
		|	ТаблицаПередЗаписью.ВидЗапасов.ТипЗапасов,
		|	ТаблицаПередЗаписью.КорВидЗапасов.ТипЗапасов,
		|	ТаблицаПередЗаписью.ВидЗапасов.Организация,
		|	ТаблицаПередЗаписью.НомерГТД,
		|	ТаблицаПередЗаписью.Количество,
		|	ТаблицаПередЗаписью.ВидДвижения,
		|	ЛОЖЬ
		|ИЗ
		|	ДвиженияРезервыТоварыОрганизацийПередЗаписью КАК ТаблицаПередЗаписью
		|";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + 
	"
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры,
	|	Организация,
	|	ВидЗапасов,
	|	НомерГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	МАКСИМУМ(ВложенныйЗапрос.Период) КАК Период,
	|	NULL КАК ВидДвижения,
	|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.ВидЗапасов КАК ВидЗапасов,
	|	ВложенныйЗапрос.НомерГТД КАК НомерГТД,
	|	""ОшибкаБаланса"" КАК ТипОшибки
	|ИЗ
	|	(ВЫБРАТЬ
	|		РезервыТоваровОрганизаций.Период КАК Период,
	|		РезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		РезервыТоваровОрганизаций.Организация КАК Организация,
	|		РезервыТоваровОрганизаций.ВидЗапасов КАК ВидЗапасов,
	|		РезервыТоваровОрганизаций.НомерГТД КАК НомерГТД,
	|		РезервыТоваровОрганизаций.Количество КАК Количество
	|	ИЗ
	|		АналитикаРезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|	ГДЕ
	|		РезервыТоваровОрганизаций.ЭтоНовоеДвижение
	|		И РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыТоваровОрганизаций.Период,
	|		РезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры,
	|		РезервыТоваровОрганизаций.КорОрганизация,
	|		РезервыТоваровОрганизаций.КорВидЗапасов,
	|		РезервыТоваровОрганизаций.НомерГТД,
	|		-РезервыТоваровОрганизаций.Количество
	|	ИЗ
	|		АналитикаРезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|	ГДЕ
	|		РезервыТоваровОрганизаций.ЭтоНовоеДвижение
	|		И РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыТоваровОрганизаций.Период,
	|		РезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры,
	|		РезервыТоваровОрганизаций.Организация,
	|		РезервыТоваровОрганизаций.ВидЗапасов,
	|		РезервыТоваровОрганизаций.НомерГТД,
	|		РезервыТоваровОрганизаций.Количество
	|	ИЗ
	|		АналитикаРезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|	ГДЕ
	|		РезервыТоваровОрганизаций.ЭтоНовоеДвижение
	|		И РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыТоваровОрганизаций.Период,
	|		РезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры,
	|		РезервыТоваровОрганизаций.КорОрганизация,
	|		РезервыТоваровОрганизаций.КорВидЗапасов,
	|		РезервыТоваровОрганизаций.НомерГТД,
	|		-РезервыТоваровОрганизаций.Количество
	|	ИЗ
	|		АналитикаРезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|	ГДЕ
	|		РезервыТоваровОрганизаций.ЭтоНовоеДвижение
	|		И РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.НомерГТД,
	|	ВложенныйЗапрос.ВидЗапасов,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Количество) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(РезервыТоваровОрганизаций.Период, МЕСЯЦ),
	|	РезервыТоваровОрганизаций.ВидДвижения,
	|	РезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры,
	|	РезервыТоваровОрганизаций.Организация,
	|	РезервыТоваровОрганизаций.ВидЗапасов,
	|	РезервыТоваровОрганизаций.НомерГТД,
	|	""ОшибкаНулевоеКоличество""
	|ИЗ
	|	АналитикаРезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|ГДЕ
	|	РезервыТоваровОрганизаций.ЭтоНовоеДвижение
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(РезервыТоваровОрганизаций.Период, МЕСЯЦ),
	|	РезервыТоваровОрганизаций.НомерГТД,
	|	РезервыТоваровОрганизаций.ВидЗапасов,
	|	РезервыТоваровОрганизаций.Организация,
	|	РезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры,
	|	РезервыТоваровОрганизаций.КорОрганизация,
	|	РезервыТоваровОрганизаций.КорВидЗапасов,
	|	РезервыТоваровОрганизаций.ВидДвижения
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|			КОГДА РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА РезервыТоваровОрганизаций.Количество
	|			ИНАЧЕ -РезервыТоваровОрганизаций.Количество
	|		КОНЕЦ) = 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РезервыТоваровОрганизаций.Период,
	|	NULL,
	|	РезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры,
	|	РезервыТоваровОрганизаций.Организация,
	|	РезервыТоваровОрганизаций.ВидЗапасов,
	|	РезервыТоваровОрганизаций.НомерГТД,
	|	""ОшибкаПринадлежностиВидовЗапасов""
	|ИЗ
	|	АналитикаРезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|ГДЕ
	|	РезервыТоваровОрганизаций.ЭтоНовоеДвижение
	|	И РезервыТоваровОрганизаций.Организация <> РезервыТоваровОрганизаций.ОрганизацияВидаЗапасов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РезервыТоваровОрганизаций.Период,
	|	NULL,
	|	РезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры,
	|	РезервыТоваровОрганизаций.Организация,
	|	РезервыТоваровОрганизаций.ВидЗапасов,
	|	РезервыТоваровОрганизаций.НомерГТД,
	|	""ОшибкаПодобранногоВидаЗапасов""
	|ИЗ
	|	АналитикаРезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|ГДЕ
	|	РезервыТоваровОрганизаций.ЭтоНовоеДвижение
	|	И РезервыТоваровОрганизаций.Организация = РезервыТоваровОрганизаций.КорОрганизация
	|	И РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И НЕ РезервыТоваровОрганизаций.ТипЗапасовКорВидаЗапасов В (
	|									ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РезервыТоваровОрганизаций.Период,
	|	NULL,
	|	РезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры,
	|	РезервыТоваровОрганизаций.Организация,
	|	РезервыТоваровОрганизаций.ВидЗапасов,
	|	РезервыТоваровОрганизаций.НомерГТД,
	|	""ОшибкаПриходРезерваПоТоварНаХраненииСПравомПродажи""
	|ИЗ
	|	АналитикаРезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|ГДЕ
	|	РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И РезервыТоваровОрганизаций.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РезервыТоваровОрганизаций.Период,
	|	NULL,
	|	РезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры,
	|	РезервыТоваровОрганизаций.Организация,
	|	РезервыТоваровОрганизаций.ВидЗапасов,
	|	РезервыТоваровОрганизаций.НомерГТД,
	|	""ОшибкаТипаНоменклатуры""
	|ИЗ
	|	АналитикаРезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|ГДЕ
	|	РезервыТоваровОрганизаций.ЭтоНовоеДвижение
	|	И РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И НЕ РезервыТоваровОрганизаций.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(АналитикаРезервыТоваровОрганизаций.Период, МЕСЯЦ) КАК Период,
	|	АналитикаРезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	АналитикаРезервыТоваровОрганизаций.Организация КАК Организация,
	|	АналитикаРезервыТоваровОрганизаций.ВидЗапасов КАК ВидЗапасов,
	|	АналитикаРезервыТоваровОрганизаций.НомерГТД КАК НомерГТД
	|ПОМЕСТИТЬ ДвиженияРезервыТоваровОрганизацийИзменение
	|ИЗ
	|	АналитикаРезервыТоваровОрганизаций КАК АналитикаРезервыТоваровОрганизаций
	|ГДЕ
	|	НЕ АналитикаРезервыТоваровОрганизаций.Склад ССЫЛКА Справочник.СтруктураПредприятия
	|	И НЕ &НеПроверятьЛишнееСторно
	|
	|СГРУППИРОВАТЬ ПО
	|	КОНЕЦПЕРИОДА(АналитикаРезервыТоваровОрганизаций.Период, МЕСЯЦ),
	|	АналитикаРезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры,
	|	АналитикаРезервыТоваровОрганизаций.Организация,
	|	АналитикаРезервыТоваровОрганизаций.ВидЗапасов,
	|	АналитикаРезервыТоваровОрганизаций.НомерГТД
	|
	|ИМЕЮЩИЕ
	// Проверяем только уменьшение остатка
	|	СУММА(ВЫБОР
	|			КОГДА АналитикаРезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|						И АналитикаРезервыТоваровОрганизаций.ЭтоНовоеДвижение
	|					ИЛИ АналитикаРезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|						И НЕ АналитикаРезервыТоваровОрганизаций.ЭтоНовоеДвижение
	|				ТОГДА -АналитикаРезервыТоваровОрганизаций.Количество
	|			ИНАЧЕ АналитикаРезервыТоваровОрганизаций.Количество
	|		КОНЕЦ) < 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаРезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры,
	|	АналитикаРезервыТоваровОрганизаций.Организация,
	|	АналитикаРезервыТоваровОрганизаций.ВидЗапасов,
	|	АналитикаРезервыТоваровОрганизаций.НомерГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ДвиженияРезервыТоваровОрганизацийИзменение.Период) КАК Период
	|ИЗ
	|	ДвиженияРезервыТоваровОрганизацийИзменение КАК ДвиженияРезервыТоваровОрганизацийИзменение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезервыТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	РезервыТоваров.Организация КАК Организация,
	|	РезервыТоваров.ВидЗапасов КАК ВидЗапасов,
	|	РезервыТоваров.НомерГТД КАК НомерГТД,
	|	КОНЕЦПЕРИОДА(РезервыТоваров.Период, МЕСЯЦ) КАК Период,
	|	СУММА(РезервыТоваров.Количество) КАК Количество
	|ИЗ
	|	РегистрНакопления.РезервыТоваровОрганизаций КАК РезервыТоваров
	|ГДЕ
	|	(РезервыТоваров.АналитикаУчетаНоменклатуры, РезервыТоваров.Организация, РезервыТоваров.ВидЗапасов, РезервыТоваров.НомерГТД) В
	|			(ВЫБРАТЬ
	|				ТаблицаАналитик.АналитикаУчетаНоменклатуры,
	|				ТаблицаАналитик.Организация,
	|				ТаблицаАналитик.ВидЗапасов,
	|				ТаблицаАналитик.НомерГТД
	|			ИЗ
	|				ДвиженияРезервыТоваровОрганизацийИзменение КАК ТаблицаАналитик)
	|
	|СГРУППИРОВАТЬ ПО
	|	КОНЕЦПЕРИОДА(РезервыТоваров.Период, МЕСЯЦ),
	|	РезервыТоваров.ВидДвижения,
	|	РезервыТоваров.НомерГТД,
	|	РезервыТоваров.ВидЗапасов,
	|	РезервыТоваров.Организация,
	|	РезервыТоваров.АналитикаУчетаНоменклатуры
	|
	|ИМЕЮЩИЕ
	|	СУММА(РезервыТоваров.Количество) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	АналитикаУчетаНоменклатуры,
	|	Организация,
	|	ВидЗапасов,
	|	НомерГТД,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ АналитикаРезервыТоваровОрганизаций";
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("КонтролироватьОстатки", Константы.КонтролироватьОстаткиТоваровОрганизаций.Получить());
	Запрос.УстановитьПараметр("РассчитыватьИзменения", ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства));
	Запрос.УстановитьПараметр("НеПроверятьЛишнееСторно", ДополнительныеСвойства.Свойство("НеПроверятьЛишнееСторно"));
	
	Результат = Запрос.ВыполнитьПакет();  
	ВыборкаОшибок = Результат[1].Выбрать();
	
	МассивТекстовИсключений = Новый Массив;
	
	Пока ВыборкаОшибок.Следующий() Цикл
		
		Если ВыборкаОшибок.ТипОшибки = "ОшибкаБаланса" Тогда
			ТекстИсключения = НСтр("ru = 'Ошибка в алгоритмах при записи %Регистратор% в регистр %Регистр%: нарушен баланс по измерениям %АналитикаНоменклатуры%-%Организация%-%ВидЗапасов%-%НомерГТД%. Обратитесь к разработчикам программы.'");
		ИначеЕсли ВыборкаОшибок.ТипОшибки = "ОшибкаНулевоеКоличество" Тогда
			ТекстИсключения = НСтр("ru = 'Ошибка в алгоритмах при записи %Регистратор% в регистр %Регистр%: в месяце %Месяц% записывается нулевое количество по измерениям: %АналитикаНоменклатуры%-%Организация%-%ВидЗапасов%-%НомерГТД%. Обратитесь к разработчикам программы.'");
		ИначеЕсли ВыборкаОшибок.ТипОшибки = "ОшибкаПринадлежностиВидовЗапасов" Тогда
			ТекстИсключения = НСтр("ru = 'Ошибка в алгоритмах при записи %Регистратор% в регистр %Регистр%: по измерениям записывается вид запасов, не принадлежащий организации %АналитикаНоменклатуры%-%Организация%-%ВидЗапасов%-%НомерГТД%. Обратитесь к разработчикам программы.'");
		ИначеЕсли ВыборкаОшибок.ТипОшибки = "ОшибкаПодобранногоВидаЗапасов" Тогда
			ТекстИсключения = НСтр("ru = 'Ошибка в алгоритмах при записи %Регистратор% в регистр %Регистр%: организация и кор. организация совпадают, но при этом тип запасов вида запасов не %ТипЗапасов% %АналитикаНоменклатуры%-%Организация%-%ВидЗапасов%-%НомерГТД%. Обратитесь к разработчикам программы.'");
		ИначеЕсли ВыборкаОшибок.ТипОшибки = "ОшибкаПриходРезерваПоТоварНаХраненииСПравомПродажи" Тогда
			ТекстИсключения = НСтр("ru = 'Ошибка в алгоритмах при записи %Регистратор% в регистр %Регистр%: приход резерва по типу запасов %ТипЗапасов% %АналитикаНоменклатуры%-%Организация%-%ВидЗапасов%-%НомерГТД%. Обратитесь к разработчикам программы.'");
		ИначеЕсли ВыборкаОшибок.ТипОшибки = "ОшибкаТипаНоменклатуры" Тогда
			ТекстИсключения = НСтр("ru = 'Ошибка в алгоритмах при записи %Регистратор% в регистр %Регистр%: пишется резерв не по товару или многооборотной таре по измерениям %АналитикаНоменклатуры%-%Организация%-%ВидЗапасов%-%НомерГТД%. Обратитесь к разработчикам программы.'");
		Иначе
			ТекстИсключения = НСтр("ru = 'Ошибка в алгоритмах записи %Регистратор% в регистр %Регистр%: не классифицированная ошибка по измерениям %АналитикаНоменклатуры%-%Организация%-%ВидЗапасов%-%НомерГТД%'");
		КонецЕсли;

		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%Регистр%", "РезервыТоваровОрганизаций");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ТипЗапасов%", "ТоварНаХраненииСПравомПродажи");
		
		ЗапасыСервер.ЗаполнитьОбщиеПараметрыТекстеСообщенияОбОшибкахПроведенияПоТоварамОрганизацийИРезервам(
																				ТекстИсключения,
																				Отбор.Регистратор.Значение,
																				ВыборкаОшибок);
		
		МассивТекстовИсключений.Добавить(ТекстИсключения);
		
	КонецЦикла;
	
	ВыборкаПоРезервам = Результат[4].Выбрать();
	
	ТекущийРезерв = Новый Структура("АналитикаУчетаНоменклатуры, Организация, ВидЗапасов, НомерГТД, Период");
	НераспределенноеСторно = 0;
	
	Пока ВыборкаПоРезервам.Следующий() Цикл
		
		Если Не ОбщегоНазначенияУТКлиентСервер.СтруктурыРавны(
			ВыборкаПоРезервам, 
			ТекущийРезерв, 
			"АналитикаУчетаНоменклатуры,Организация,ВидЗапасов,НомерГТД") Тогда
			
			Если НераспределенноеСторно <> 0 Тогда
				ТекстИсключения = НСтр("ru = 'Ошибка в алгоритмах при записи %Регистратор% в регистр %Регистр%: образуется сторно, которое не нужно для покрытия резервов по изменениям: %Организация% - %АналитикаНоменклатуры% - %ВидЗапасов% - %НомерГТД%, лишнее сторно %НераспределенноеСторно%. Обратитесь к разработчикам программы.'");
				ТекстИсключения = СтрЗаменить(ТекстИсключения, "%Регистр%", "РезервыТоваровОрганизаций");
				
				ЗапасыСервер.ЗаполнитьОбщиеПараметрыТекстеСообщенияОбОшибкахПроведенияПоТоварамОрганизацийИРезервам(
					ТекстИсключения,
					Отбор.Регистратор.Значение,
					ТекущийРезерв);
				
				ТекстИсключения = СтрЗаменить(ТекстИсключения, "%НераспределенноеСторно%", НераспределенноеСторно);	
				
				МассивТекстовИсключений.Добавить(ТекстИсключения);
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(ТекущийРезерв, ВыборкаПоРезервам);
			НераспределенноеСторно = 0;
			
		КонецЕсли;
		
		Если (НераспределенноеСторно = 0
			И ВыборкаПоРезервам.Количество < 0)
			Или НераспределенноеСторно < 0 Тогда
		
			НераспределенноеСторно = НераспределенноеСторно + ВыборкаПоРезервам.Количество;
			
			Если НераспределенноеСторно > 0 Тогда
				НераспределенноеСторно = 0;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если МассивТекстовИсключений.Количество() > 0 Тогда
		ВызватьИсключение СтрСоединить(МассивТекстовИсключений,Символы.ПС);
	КонецЕсли;
	
	Если ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		МинимальныйПериод = Результат[3].Выбрать();
		Если МинимальныйПериод.Следующий() Тогда
			МинимальныйПериод = МинимальныйПериод.Период;
		Иначе
			МинимальныйПериод = Дата('00010101');
		КонецЕсли;
		
		Выборка = Результат[2].Выбрать();
		ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(
			ДополнительныеСвойства, "ДвиженияРезервыТоваровОрганизацийИзменение",
			Выборка.Следующий() И Выборка.Количество > 0, Новый Структура("МинимальныйПериод", МинимальныйПериод));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
