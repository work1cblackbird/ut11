#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	ОбщегоНазначенияУТ.СвернутьНаборЗаписей(ЭтотОбъект, Истина);
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
		
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	СоотвествиеОпераций = СкладыКлиентСервер.СоответствиеХозяйственныхОперацийСкладскихОперацийПриемки();
	
	Для Каждого СтрНабора Из ЭтотОбъект Цикл
		Операция = СоотвествиеОпераций.Получить(СтрНабора.ХозяйственнаяОперация);
		Если Операция = Неопределено Тогда
			ТекстСообщения = НСтр("ru = 'Отражение хозяйственной операции ""%ХозяйственнаяОперация%"" в регистр %ТоварыКПоступлению% не поддерживается. Обратитесь к разработчикам.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТоварыКПоступлению%", "ТоварыКПоступлению");
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%ХозяйственнаяОперация%", СтрНабора.ХозяйственнаяОперация);  
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		ИначеЕсли Операция <> Перечисления.СкладскиеОперации.ПриемкаПоПрочемуОприходованию
			И Не ЗначениеЗаполнено(СтрНабора.Отправитель) Тогда
			ТекстСообщения = НСтр("ru = 'Запись пустого отправителя в регистр %ТоварыКПоступлению% поддерживается только для складской операции %СкладскаяОперация%. Обратитесь к разработчикам.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТоварыКПоступлению%", "ТоварыКПоступлению");
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%СкладскаяОперация%", Перечисления.СкладскиеОперации.ПриемкаПоПрочемуОприходованию);  
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		КонецЕсли
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.ДокументПоступления КАК ДокументПоступления,
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	Таблица.Назначение КАК Назначение,
	|	Таблица.Серия КАК Серия,
	|	Таблица.Склад КАК Склад,
	|	Таблица.Отправитель КАК Отправитель,
	|	Таблица.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Таблица.КОформлениюНакладныхПоРаспоряжению
	|		ИНАЧЕ -Таблица.КОформлениюНакладныхПоРаспоряжению
	|	КОНЕЦ КАК КОформлениюНакладныхПоРаспоряжениюПередЗаписью,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Таблица.КОформлениюПоступленийПоРаспоряжению
	|		ИНАЧЕ -Таблица.КОформлениюПоступленийПоРаспоряжению
	|	КОНЕЦ КАК КОформлениюПоступленийПоРаспоряжениюПередЗаписью,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Таблица.КОформлениюПоступленийПоНакладным
	|		ИНАЧЕ -Таблица.КОформлениюПоступленийПоНакладным
	|	КОНЕЦ КАК КОформлениюПоступленийПоНакладнымПередЗаписью,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Таблица.КОформлениюПоступленийПоОрдерам
	|		ИНАЧЕ -Таблица.КОформлениюПоступленийПоОрдерам
	|	КОНЕЦ КАК КОформлениюПоступленийПоОрдерамПередЗаписью,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Таблица.КОформлениюОрдеров
	|		ИНАЧЕ -Таблица.КОформлениюОрдеров
	|	КОНЕЦ КАК КОформлениюОрдеровПередЗаписью,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Таблица.Принимается
	|		ИНАЧЕ -Таблица.Принимается
	|	КОНЕЦ КАК ПринимаетсяПередЗаписью
	|ПОМЕСТИТЬ ТоварыКПоступлениюПередЗаписью
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка 
		Или Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.ДокументПоступления КАК ДокументПоступления,
	|	ТаблицаИзменений.Номенклатура КАК Номенклатура,
	|	ТаблицаИзменений.Характеристика КАК Характеристика,
	|	ТаблицаИзменений.Назначение КАК Назначение,
	|	ТаблицаИзменений.Серия КАК Серия,
	|	ТаблицаИзменений.Склад КАК Склад,
	|	ТаблицаИзменений.Отправитель КАК Отправитель,
	|	ТаблицаИзменений.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	СУММА(ТаблицаИзменений.КОформлениюНакладныхПоРаспоряжениюИзменение) КАК КОформлениюНакладныхПоРаспоряжениюИзменение,
	|	СУММА(ТаблицаИзменений.КОформлениюПоступленийПоРаспоряжениюИзменение) КАК КОформлениюПоступленийПоРаспоряжениюИзменение,
	|	СУММА(ТаблицаИзменений.КОформлениюПоступленийПоНакладнымИзменение) КАК КОформлениюПоступленийПоНакладнымИзменение,
	|	СУММА(ТаблицаИзменений.КОформлениюПоступленийПоОрдерамИзменение) КАК КОформлениюПоступленийПоОрдерамИзменение,
	|	СУММА(ТаблицаИзменений.КОформлениюОрдеровИзменение) КАК КОформлениюОрдеровИзменение,
	|	СУММА(ТаблицаИзменений.ПринимаетсяИзменение) КАК ПринимаетсяИзменение
	|ПОМЕСТИТЬ ТоварыКПоступлениюИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.ДокументПоступления КАК ДокументПоступления,
	|		Таблица.Номенклатура КАК Номенклатура,
	|		Таблица.Характеристика КАК Характеристика,
	|		Таблица.Назначение КАК Назначение,
	|		Таблица.Серия КАК Серия,
	|		Таблица.Склад КАК Склад,
	|		Таблица.Отправитель КАК Отправитель,
	|		Таблица.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|		Таблица.КОформлениюНакладныхПоРаспоряжениюПередЗаписью КАК КОформлениюНакладныхПоРаспоряжениюИзменение,
	|		Таблица.КОформлениюПоступленийПоРаспоряжениюПередЗаписью КАК КОформлениюПоступленийПоРаспоряжениюИзменение,
	|		Таблица.КОформлениюПоступленийПоНакладнымПередЗаписью КАК КОформлениюПоступленийПоНакладнымИзменение,
	|		Таблица.КОформлениюПоступленийПоОрдерамПередЗаписью КАК КОформлениюПоступленийПоОрдерамИзменение,
	|		Таблица.КОформлениюОрдеровПередЗаписью КАК КОформлениюОрдеровИзменение,
	|		Таблица.ПринимаетсяПередЗаписью КАК ПринимаетсяИзменение
	|	ИЗ
	|		ТоварыКПоступлениюПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.ДокументПоступления КАК ДокументПоступления,
	|		Таблица.Номенклатура КАК Номенклатура,
	|		Таблица.Характеристика КАК Характеристика,
	|		Таблица.Назначение КАК Назначение,
	|		Таблица.Серия КАК Серия,
	|		Таблица.Склад КАК Склад,
	|		Таблица.Отправитель КАК Отправитель,
	|		Таблица.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.КОформлениюНакладныхПоРаспоряжению
	|			ИНАЧЕ Таблица.КОформлениюНакладныхПоРаспоряжению
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.КОформлениюПоступленийПоРаспоряжению
	|			ИНАЧЕ Таблица.КОформлениюПоступленийПоРаспоряжению
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.КОформлениюПоступленийПоНакладным
	|			ИНАЧЕ Таблица.КОформлениюПоступленийПоНакладным
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.КОформлениюПоступленийПоОрдерам
	|			ИНАЧЕ Таблица.КОформлениюПоступленийПоОрдерам
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.КОформлениюОрдеров
	|			ИНАЧЕ Таблица.КОформлениюОрдеров
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.Принимается
	|			ИНАЧЕ Таблица.Принимается
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ТоварыКПоступлению КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.ДокументПоступления,
	|	ТаблицаИзменений.Номенклатура,
	|	ТаблицаИзменений.Характеристика,
	|	ТаблицаИзменений.Назначение,
	|	ТаблицаИзменений.Серия,
	|	ТаблицаИзменений.Склад,
	|	ТаблицаИзменений.Отправитель,
	|	ТаблицаИзменений.ХозяйственнаяОперация
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ТаблицаИзменений.КОформлениюНакладныхПоРаспоряжениюИзменение) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.КОформлениюПоступленийПоРаспоряжениюИзменение) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.КОформлениюПоступленийПоНакладнымИзменение) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.КОформлениюПоступленийПоОрдерамИзменение) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.КОформлениюОрдеровИзменение) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.ПринимаетсяИзменение) <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТоварыКПоступлениюПередЗаписью";
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли