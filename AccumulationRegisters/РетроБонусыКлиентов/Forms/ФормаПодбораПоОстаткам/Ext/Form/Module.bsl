//@strict-types

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Параметры.Организация)
	 ИЛИ НЕ ЗначениеЗаполнено(Параметры.Контрагент)  Тогда
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
	Организация = Параметры.Организация;
	Контрагент = Параметры.Контрагент;
	ДатаАктуальности = Параметры.ДатаАктуальности;
	Если ЗначениеЗаполнено(Параметры.Валюта) Тогда
		
		ВалютаДокумента = Параметры.Валюта;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.АдресТаблицыПодобранныхСтрок) Тогда
		
		АдресВХранилище = Параметры.АдресТаблицыПодобранныхСтрок;
		ПодобранныеСтроки = ПолучитьИзВременногоХранилища(АдресВХранилище); // ТаблицаЗначений
		УдалитьИзВременногоХранилища(АдресВХранилище);
		ОстаткиРетроБонусов.Загрузить(ПодобранныеСтроки);
		
	КонецЕсли;
	
	ДанныеОстатковВТаблицуЗначений();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	Для Каждого НомерСтроки Из Элементы.ОстаткиРетроБонусов.ВыделенныеСтроки Цикл // Число 
		
		СтрокаОстатков = ОстаткиРетроБонусов.НайтиПоИдентификатору(НомерСтроки);
		
		Если НЕ СтрокаОстатков.ДокументВыбран Тогда
			
			СтрокаОстатков.ДокументВыбран = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметки(Команда)
	
	Для Каждого НомерСтроки Из Элементы.ОстаткиРетроБонусов.ВыделенныеСтроки Цикл // Число
		
		СтрокаОстатков = ОстаткиРетроБонусов.НайтиПоИдентификатору(НомерСтроки);
		
		Если СтрокаОстатков.ДокументВыбран Тогда
			
			СтрокаОстатков.ДокументВыбран = Ложь;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаОстатковРБВыполнитьВыборЗначения(Команда)
	
	ОчиститьСообщения();
	
	Отбор = Новый Структура;
	Отбор.Вставить("ДокументВыбран", Истина);
	НайденныеСтроки = ОстаткиРетроБонусов.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() > 0 Тогда
		
		Отказ = Ложь;
		Для Каждого СтрокаТаблицы Из НайденныеСтроки Цикл
			
			Если НЕ ЗначениеЗаполнено(ВалютаДокумента) Тогда
				ВалютаДокумента = СтрокаТаблицы.Валюта;
			КонецЕсли;
			
			Если СтрокаТаблицы.Валюта <> ВалютаДокумента Тогда
				
				ВалютаДокумента = ПредопределенноеЗначение("Справочник.Валюты.ПустаяСсылка");
				ТекстСообщения = НСтр("ru = 'Выбраны строки с разной валютой, перенос в документ невозможен'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,,, Отказ);
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;	
		
		Если НЕ Отказ Тогда
			
			АдресВХранилище = АдресРезультатаВХранилище();
			Закрыть(АдресВХранилище);
			
		КонецЕсли;
		
	Иначе
		
		ВыполнитьВыборЗначенияСВопросом();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОстаткиРетроБонусов.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОстаткиРетроБонусов.ДокументВыбран");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Gray);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВыполнитьВыборЗначенияСВопросом()
	
	ТекстВопроса = НСтр("ru = 'Не выбрано ни одной строки. Продолжить?'");
	Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет); // КодВозвратаДиалога
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		АдресВХранилище = АдресРезультатаВХранилище();
		Закрыть(АдресВХранилище);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДанныеОстатковВТаблицуЗначений()
	
	Граница =  Новый Граница(КонецДня(ДатаАктуальности), ВидГраницы.Включая);
	
	КоличествоСтрок = ОстаткиРетроБонусов.Количество();
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ОстаткиВыбранные.Валюта КАК Валюта,
	|	ОстаткиВыбранные.НомерСтроки КАК НомерСтроки,
	|	ОстаткиВыбранные.ДокументВыбран КАК ДокументВыбран,
	|	ОстаткиВыбранные.ДокументУсловий КАК ДокументУсловий,
	|	ОстаткиВыбранные.НачалоПериода КАК НачалоПериода,
	|	ОстаткиВыбранные.ОкончаниеПериода КАК ОкончаниеПериода,
	|	ОстаткиВыбранные.Партнер КАК Партнер
	|ПОМЕСТИТЬ ВТ_ОстаткиВыбранные
	|ИЗ
	|	&ОстаткиРетроБонусов КАК ОстаткиВыбранные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РетроБонусыКлиентов.Партнер КАК Партнер,
	|	РетроБонусыКлиентов.НачалоПериода КАК НачалоПериода,
	|	РетроБонусыКлиентов.ОкончаниеПериода КАК ОкончаниеПериода,
	|	РетроБонусыКлиентов.ДокументУсловий КАК ДокументУсловий,
	|	РетроБонусыКлиентов.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА РетроБонусыКлиентов.СуммаБонусНачальныйОстаток - РетроБонусыКлиентов.КАктированиюНачальныйОстаток - РетроБонусыКлиентов.КПодписаниюНачальныйОстаток - РетроБонусыКлиентов.КСписаниюНачальныйОстаток > РетроБонусыКлиентов.СуммаБонусКонечныйОстаток - РетроБонусыКлиентов.КАктированиюКонечныйОстаток - РетроБонусыКлиентов.КПодписаниюКонечныйОстаток - РетроБонусыКлиентов.КСписаниюКонечныйОстаток
	|			ТОГДА РетроБонусыКлиентов.СуммаБонусКонечныйОстаток - РетроБонусыКлиентов.КАктированиюКонечныйОстаток - РетроБонусыКлиентов.КПодписаниюКонечныйОстаток - РетроБонусыКлиентов.КСписаниюКонечныйОстаток
	|		ИНАЧЕ РетроБонусыКлиентов.СуммаБонусНачальныйОстаток - РетроБонусыКлиентов.КАктированиюНачальныйОстаток - РетроБонусыКлиентов.КПодписаниюНачальныйОстаток - РетроБонусыКлиентов.КСписаниюНачальныйОстаток
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ ВТ_ОстаткиБонусов
	|ИЗ
	|	РегистрНакопления.РетроБонусыКлиентов.ОстаткиИОбороты(
	|			&Период,
	|			,
	|			,
	|			,
	|			Организация = &Организация
	|				И Контрагент = &Контрагент
	|				И &УсловиеВалюта) КАК РетроБонусыКлиентов
	|ГДЕ
	|	РетроБонусыКлиентов.СуммаБонусНачальныйОстаток - РетроБонусыКлиентов.КАктированиюНачальныйОстаток - РетроБонусыКлиентов.КПодписаниюНачальныйОстаток - РетроБонусыКлиентов.КСписаниюНачальныйОстаток > 0
	|	И РетроБонусыКлиентов.СуммаБонусКонечныйОстаток - РетроБонусыКлиентов.КАктированиюКонечныйОстаток - РетроБонусыКлиентов.КПодписаниюКонечныйОстаток - РетроБонусыКлиентов.КСписаниюКонечныйОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВТ_ОстаткиВыбранные.ДокументВыбран, ЛОЖЬ) КАК ДокументВыбран,
	|	ЕСТЬNULL(ВТ_ОстаткиВыбранные.НомерСтроки, &КоличествоСтрок) КАК НомерСтроки,
	|	ОстаткиБонусов.Партнер КАК Партнер,
	|	ОстаткиБонусов.ДокументУсловий КАК ДокументУсловий,
	|	ОстаткиБонусов.НачалоПериода КАК НачалоПериода,
	|	ОстаткиБонусов.ОкончаниеПериода КАК ОкончаниеПериода,
	|	ОстаткиБонусов.Валюта КАК Валюта,
	|	ЕСТЬNULL(РетроБонусыКлиентовУсловия.Вид, ЗНАЧЕНИЕ(Справочник.ВидыРетроБонусовКлиентов.ПустаяСсылка)) КАК ВидРетроБонуса,
	|	ЕСТЬNULL(РетроБонусыКлиентовУсловия.Описание, """") КАК Описание,
	|	ОстаткиБонусов.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТ_Подготовка
	|ИЗ
	|	ВТ_ОстаткиБонусов КАК ОстаткиБонусов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОстаткиВыбранные КАК ВТ_ОстаткиВыбранные
	|		ПО ОстаткиБонусов.Партнер = ВТ_ОстаткиВыбранные.Партнер
	|			И ОстаткиБонусов.Валюта = ВТ_ОстаткиВыбранные.Валюта
	|			И ОстаткиБонусов.ДокументУсловий = ВТ_ОстаткиВыбранные.ДокументУсловий
	|			И ОстаткиБонусов.НачалоПериода = ВТ_ОстаткиВыбранные.НачалоПериода
	|			И ОстаткиБонусов.ОкончаниеПериода = ВТ_ОстаткиВыбранные.ОкончаниеПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РетроБонусыКлиентовУсловия КАК РетроБонусыКлиентовУсловия
	|		ПО ОстаткиБонусов.ДокументУсловий = РетроБонусыКлиентовУсловия.ДокументУсловий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТ_Подготовка.ДокументВыбран) КАК ДокументВыбран,
	|	МИНИМУМ(ВТ_Подготовка.НомерСтроки) КАК НомерСтроки,
	|	ВТ_Подготовка.Партнер КАК Партнер,
	|	ВТ_Подготовка.ДокументУсловий КАК ДокументУсловий,
	|	ВТ_Подготовка.НачалоПериода КАК НачалоПериода,
	|	ВТ_Подготовка.ОкончаниеПериода КАК ОкончаниеПериода,
	|	ВТ_Подготовка.Валюта КАК Валюта,
	|	ВТ_Подготовка.ВидРетроБонуса КАК ВидРетроБонуса,
	|	ВТ_Подготовка.Описание КАК Описание,
	|	ВТ_Подготовка.Сумма КАК Сумма
	|ИЗ
	|	ВТ_Подготовка КАК ВТ_Подготовка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Подготовка.Партнер,
	|	ВТ_Подготовка.ДокументУсловий,
	|	ВТ_Подготовка.НачалоПериода,
	|	ВТ_Подготовка.ОкончаниеПериода,
	|	ВТ_Подготовка.Валюта,
	|	ВТ_Подготовка.ВидРетроБонуса,
	|	ВТ_Подготовка.Описание,
	|	ВТ_Подготовка.Сумма
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументВыбран УБЫВ,
	|	НомерСтроки,
	|	ДокументУсловий,
	|	Партнер";
	
	Если ЗначениеЗаполнено(ВалютаДокумента) Тогда
		ТекстУсловияВалюта = "Валюта = &Валюта";
	Иначе
		ТекстУсловияВалюта = "ИСТИНА";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеВалюта", ТекстУсловияВалюта);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОстаткиРетроБонусов", ОстаткиРетроБонусов.Выгрузить());
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Период", Граница);
	Запрос.УстановитьПараметр("Валюта", ВалютаДокумента);
	Запрос.УстановитьПараметр("КоличествоСтрок", КоличествоСтрок);
	Запрос.Текст = ТекстЗапроса;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ДанныеОбОстатках = РезультатЗапроса.Выгрузить();
		ОстаткиРетроБонусов.Загрузить(ДанныеОбОстатках);
		
	Иначе
		ОстаткиРетроБонусов.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция АдресРезультатаВХранилище()
	
	Отбор = Новый Структура;
	Отбор.Вставить("ДокументВыбран", Истина);
	
	Возврат ПоместитьВоВременноеХранилище(ОстаткиРетроБонусов.Выгрузить(Отбор));
	
КонецФункции

#КонецОбласти	