#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	БлокироватьДляИзменения = Истина;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТоварныеМестаКОтражениюИзлишковНедостач.Склад КАК Склад,
	|	ТоварныеМестаКОтражениюИзлишковНедостач.Помещение КАК Помещение,
	|	ТоварныеМестаКОтражениюИзлишковНедостач.Номенклатура КАК Номенклатура,
	|	ТоварныеМестаКОтражениюИзлишковНедостач.Характеристика КАК Характеристика,
	|	ТоварныеМестаКОтражениюИзлишковНедостач.Назначение КАК Назначение,
	|	ТоварныеМестаКОтражениюИзлишковНедостач.Серия КАК Серия,
	|	ТоварныеМестаКОтражениюИзлишковНедостач.ТоварноеМесто КАК ТоварноеМесто,
	|	ВЫБОР
	|		КОГДА ТоварныеМестаКОтражениюИзлишковНедостач.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ТоварныеМестаКОтражениюИзлишковНедостач.Количество
	|		ИНАЧЕ -ТоварныеМестаКОтражениюИзлишковНедостач.Количество
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ ДвиженияТоварныеМестаКОтражениюИзлишковНедостачПередЗаписью
	|ИЗ
	|	РегистрНакопления.ТоварныеМестаКОтражениюИзлишковНедостач КАК ТоварныеМестаКОтражениюИзлишковНедостач
	|ГДЕ
	|	НЕ &ЭтоНовый
	|	И ТоварныеМестаКОтражениюИзлишковНедостач.Регистратор = &Регистратор";	
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Склад,
	|	ВложенныйЗапрос.Помещение,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Назначение,
	|	ВложенныйЗапрос.Серия
	|ПОМЕСТИТЬ ВТТаблицаИзменений
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаИзменений.Склад КАК Склад,
	|		ТаблицаИзменений.Помещение КАК Помещение,
	|		ТаблицаИзменений.Номенклатура КАК Номенклатура,
	|		ТаблицаИзменений.Характеристика КАК Характеристика,
	|		ТаблицаИзменений.Назначение КАК Назначение,
	|		ТаблицаИзменений.Серия КАК Серия,
	|		ТаблицаИзменений.ТоварноеМесто КАК ТоварноеМесто,
	|		СУММА(ТаблицаИзменений.Количество) КАК Количество
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ПередЗаписью.Склад КАК Склад,
	|			ПередЗаписью.Помещение КАК Помещение,
	|			ПередЗаписью.Номенклатура КАК Номенклатура,
	|			ПередЗаписью.Характеристика КАК Характеристика,
	|			ПередЗаписью.Назначение КАК Назначение,
	|			ПередЗаписью.Серия КАК Серия,
	|			ПередЗаписью.ТоварноеМесто КАК ТоварноеМесто,
	|			ПередЗаписью.Количество КАК Количество
	|		ИЗ
	|			ДвиженияТоварныеМестаКОтражениюИзлишковНедостачПередЗаписью КАК ПередЗаписью
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТоварныеМестаКОтражениюИзлишковНедостач.Склад,
	|			ТоварныеМестаКОтражениюИзлишковНедостач.Помещение,
	|			ТоварныеМестаКОтражениюИзлишковНедостач.Номенклатура,
	|			ТоварныеМестаКОтражениюИзлишковНедостач.Характеристика,
	|			ТоварныеМестаКОтражениюИзлишковНедостач.Назначение,
	|			ТоварныеМестаКОтражениюИзлишковНедостач.Серия,
	|			ТоварныеМестаКОтражениюИзлишковНедостач.ТоварноеМесто,
	|			ВЫБОР
	|				КОГДА ТоварныеМестаКОтражениюИзлишковНедостач.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|					ТОГДА -ТоварныеМестаКОтражениюИзлишковНедостач.Количество
	|				ИНАЧЕ ТоварныеМестаКОтражениюИзлишковНедостач.Количество
	|			КОНЕЦ
	|		ИЗ
	|			РегистрНакопления.ТоварныеМестаКОтражениюИзлишковНедостач КАК ТоварныеМестаКОтражениюИзлишковНедостач
	|		ГДЕ
	|			ТоварныеМестаКОтражениюИзлишковНедостач.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаИзменений.Характеристика,
	|		ТаблицаИзменений.Помещение,
	|		ТаблицаИзменений.Серия,
	|		ТаблицаИзменений.Номенклатура,
	|		ТаблицаИзменений.Склад,
	|		ТаблицаИзменений.Назначение,
	|		ТаблицаИзменений.ТоварноеМесто
	|	
	|	ИМЕЮЩИЕ
	|		СУММА(ТаблицаИзменений.Количество) <> 0) КАК ВложенныйЗапрос";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТТаблицаИзменений");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТоварныеМестаОстатки.Склад,
	|	ТоварныеМестаОстатки.Помещение,
	|	ТоварныеМестаОстатки.Номенклатура,
	|	ТоварныеМестаОстатки.Характеристика,
	|	ТоварныеМестаОстатки.Назначение,
	|	ТоварныеМестаОстатки.Серия,
	|	ТоварныеМестаОстатки.ТоварноеМесто,
	|	ТоварныеМестаОстатки.КоличествоОстаток
	|ПОМЕСТИТЬ ВТТоварныеМестаОстатки
	|ИЗ
	|	РегистрНакопления.ТоварныеМестаКОтражениюИзлишковНедостач.Остатки(
	|			,
	|			(Склад, Помещение, Номенклатура, Характеристика, Назначение, Серия) В
	|				(ВЫБРАТЬ
	|					ТаблицаИзменений.Склад КАК Склад,
	|					ТаблицаИзменений.Помещение КАК Помещение,
	|					ТаблицаИзменений.Номенклатура КАК Номенклатура,
	|					ТаблицаИзменений.Характеристика КАК Характеристика,
	|					ТаблицаИзменений.Назначение КАК Назначение,
	|					ТаблицаИзменений.Серия КАК Серия
	|				ИЗ
	|					ВТТаблицаИзменений КАК ТаблицаИзменений)) КАК ТоварныеМестаОстатки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТТоварныеМестаОстатки");
	
	СкладыСервер.ТекстЗапросаВТКомплектыПоТоварнымМестаКОтражениюИзлишковНедостач(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТКомплектыПоТоварнымМестаКОтражениюИзлишковНедостач.Склад,
	|	ВТКомплектыПоТоварнымМестаКОтражениюИзлишковНедостач.Помещение
	|ИЗ
	|	ВТКомплектыПоТоварнымМестаКОтражениюИзлишковНедостач КАК ВТКомплектыПоТоварнымМестаКОтражениюИзлишковНедостач";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "СкладыПомещенияКОформлениюИзлишковНедостач");
	
	ТекстЗапроса = "
	|УНИЧТОЖИТЬ ДвиженияТоварныеМестаКОтражениюИзлишковНедостачПередЗаписью";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	СкладыПомещенияКОформлениюИзлишковНедостач = ОбщегоНазначенияУТ.ВыгрузитьРезультатыЗапроса(Запрос, ТекстыЗапроса,, Истина,Истина).СкладыПомещенияКОформлениюИзлишковНедостач;
	
	Для Каждого СкладПомещение Из СкладыПомещенияКОформлениюИзлишковНедостач Цикл
		
		СкладыСервер.ДобавитьВОчередьФормированияКорректировкаИзлишковНедостачПоТоварнымМестам(СкладПомещение.Склад, 
																								СкладПомещение.Помещение, 
																								Отбор.Регистратор.Значение);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли