#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	РежимВыбора = Параметры.РежимВыбора;
	УпаковкиИспользуются = ИнтеграцияИС.УпаковкиИспользуются();
	
	Если Параметры.РежимВыбора Тогда
		НастроитьСписокДляВыбора();
	КонецЕсли;
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ИнтеграцияИСКлиент.ОбработкаОповещенияВФормеСпискаДокументовИС(
		ЭтотОбъект,
		ИнтеграцияЕГАИСКлиентСервер.ИмяПодсистемы(),
		ИмяСобытия,
		Параметр,
		Источник);
		
	Если ИмяСобытия = "Запись_КлассификаторОрганизацийЕГАИС" Тогда
		
		Элементы.Список.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.Список.РежимВыбора
		Или ТипЗнч(ВыбраннаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеСтроки = Элементы.Список.ДанныеСтроки(ВыбраннаяСтрока);
	
	Если Поле.Имя = Элементы.СписокОрганизацияЕГАИС.Имя Тогда
		
		Если ЗначениеЗаполнено(ДанныеСтроки.ОрганизацияЕГАИС) Тогда
			
			ПоказатьЗначение(, ДанныеСтроки.ОрганизацияЕГАИС);
			
		КонецЕсли;
		
	ИначеЕсли Поле.Имя = Элементы.СписокСправка2.Имя Тогда
		
		Если ЗначениеЗаполнено(ДанныеСтроки.Справка2) Тогда
			
			ПоказатьЗначение(, ДанныеСтроки.Справка2);
			
		КонецЕсли;
		
	ИначеЕсли Поле.Имя = Элементы.СписокАлкогольнаяПродукция.Имя Тогда
		
		Если ЗначениеЗаполнено(ДанныеСтроки.АлкогольнаяПродукция) Тогда
			
			ПоказатьЗначение(, ДанныеСтроки.АлкогольнаяПродукция);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ИнтеграцияИСКлиент.ВыборСтрокиСпискаКорректен(Элементы.Список, Истина) Тогда
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		ОповеститьОВыборе(ИнтеграцияЕГАИСКлиентСервер.РезультатВыбораСправки2ЕГАИС(ТекущиеДанные));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьСписокДляВыбора()
	
	Элементы.Список.РежимВыбора = Истина;
	Элементы.Список.РежимВыделения = РежимВыделенияТаблицы.Одиночный;
	
	Элементы.ГруппаОтбор.Видимость = Ложь;
	Элементы.СписокОрганизацияЕГАИС.Видимость   = Не ЗначениеЗаполнено(Параметры.ОрганизацияЕГАИС);
	Элементы.СписокАлкогольнаяПродукция.Видимость   = Не ЗначениеЗаполнено(Параметры.АлкогольнаяПродукция);
	
	НастроитьОтборСпискаДляВыбора();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОтборСпискаДляВыбора()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
		"ОрганизацияЕГАИС", Параметры.ОрганизацияЕГАИС,,, ЗначениеЗаполнено(Параметры.ОрганизацияЕГАИС));
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
		"ДоступныйОстаток", 0, ВидСравненияКомпоновкиДанных.Больше,, Истина);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Номенклатура", Параметры.Номенклатура, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Характеристика", Параметры.Характеристика, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Серия", Параметры.Серия, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "АлкогольнаяПродукция", Параметры.АлкогольнаяПродукция, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ЗаполненаНоменклатура", ЗначениеЗаполнено(Параметры.Номенклатура), Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ЗаполненаАлкогольнаяПродукция", ЗначениеЗаполнено(Параметры.АлкогольнаяПродукция), Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ЗаполненаСерия", ЗначениеЗаполнено(Параметры.Серия), Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ПустаяСерия", ИнтеграцияИС.НезаполненныеЗначенияОпределяемогоТипа("СерияНоменклатурыВЕТИС"), Истина);
	
КонецПроцедуры

#КонецОбласти