///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Параметры.ОснованиеПлатежа)
		Или Не ЗначениеЗаполнено(Параметры.НастройкиПодключения) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	НастройкиШаблонов = ПереводыСБПc2bСлужебный.НастройкиШаблоновСообщений();
	ОснованиеПлатежа = Параметры.ОснованиеПлатежа;
	Если Параметры.НастройкиПодключения.Количество() = 1 Тогда
		НастройкаПодключения = Параметры.НастройкиПодключения[0];
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСПочтовымиСообщениями") Тогда
		ВариантОтправки = "ЭлектроннаяПочта";
	ИначеЕсли ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ОтправкаSMS") Тогда
		ВариантОтправки = "Телефон";
	КонецЕсли;
	
	НастройкиПодключенияПрограммы        = СистемаБыстрыхПлатежейСлужебный.НастройкиПодключенияПрограммы();
	ИспользоватьЧастичныеОплаты          = НастройкиПодключенияПрограммы.c2b.ИспользоватьЧастичныеОплаты;
	НастройкаИспользованияЧастичныхОплат = ОбщегоНазначения.ХранилищеНастроекДанныхФормЗагрузить(
		"ФормаОтправкиПлатежнойСсылки",
		"ИспользоватьЧастичныеОплаты",
		Ложь);
	
	Если ИспользоватьЧастичныеОплаты Тогда
		МаксимальнаяСуммаОплатыСБП = Параметры.МаксимальнаяСуммаОплаты;
		ЗаполнитьДаннымиЧастичныхОплат();
	КонецЕсли;
	
	ОписаниеЧастичнойОплаты = Новый Структура;
	
	УправлениеЭлементамиФормыПоДанным();
	УправлениеЭлементамиФормыПоПодсистемам();
	УстановитьУсловноеОформление();
	
	ВосстановитьШаблоныПоУмолчанию();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ЗначениеЗаполнено(НастройкаПодключения) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не обнаружена настройка подключения к Системе быстрых платежей.'"));
		Отказ = Истина;
		Возврат;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОсновная Тогда
		НачатьПолучениеПлатежнойСсылки();
	КонецЕсли;
	
	ИнтернетПоддержкаПользователейКлиент.УстановитьОтображениеКнопкиКопироватьВБуфер(Элементы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если СтатусОперацииИзменен Тогда
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("ОснованиеПлатежа", ОснованиеПлатежа);
		ПараметрыОповещения.Вставить("НастройкаПодключения", НастройкаПодключения);
		Оповестить("ИзмененСтатусСБП", ПараметрыОповещения);
	КонецЕсли;
	
	Если НастройкиШаблонов.Используется 
		И ЗначениеЗаполнено(ОснованиеПлатежа) Тогда
		
		СохранитьШаблоныПоУмолчанию(
			ОснованиеПлатежа,
			ШаблонСообщенияЭлектроннаяПочта,
			ШаблонСообщенияТелефон);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ШаблонСообщенияЭлектроннаяПочтаПриИзменении(Элемент)
	
	ВариантОтправки = "ЭлектроннаяПочта";
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблонСообщенияТелефонПриИзменении(Элемент)
	
	ВариантОтправки = "Телефон";
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНастройкиПодключения

&НаКлиенте
Процедура НастройкиПодключенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьНастройкуПодключения(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЧастичныеОплаты

&НаКлиенте
Процедура ЧастичныеОплатыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ЧастичнаяОплата = ЧастичныеОплаты.НайтиПоИдентификатору(Элементы.ЧастичныеОплаты.ТекущаяСтрока);
		
	Если ЧастичнаяОплата = Неопределено Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не выбрана частичная оплата.'"),
			,
			,
			"ЧастичныеОплаты");
		Возврат;
		
	КонецЕсли;
	
	Если ЧастичнаяОплата.ЭтоПлатежнаяСсылкаСБПc2b Тогда
		ПлатежнаяСсылкаСБПc2b   = ЧастичнаяОплата.Ссылка;
		ОписаниеЧастичнойОплаты = ЗаполнитьОписаниеЧастичнойОплаты(ПлатежнаяСсылкаСБПc2b);
	КонецЕсли;
	
	Элементы.ОтправитьСсылку.КнопкаПоУмолчанию = Истина;
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОсновная;
	НачатьПолучениеПлатежнойСсылки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьНастройкуПодключения(Команда)
	
	СтрокаНастройкиПодключения = НастройкиНастройкаПодключения.НайтиПоИдентификатору(
		Элементы.НастройкиПодключенияВыбор.ТекущаяСтрока);
	Если СтрокаНастройкиПодключения = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не выбрана настройка подключения для формирования платежной ссылки в Системе быстрых платежей.'"),
			,
			,
			"НастройкиНастройкаПодключения");
		Возврат;
	КонецЕсли;
	
	НастройкаПодключения = СтрокаНастройкиПодключения.НастройкаПодключения;
	
	ЗаполнитьДаннымиЧастичныхОплат();
	УстановитьВидимостьДоступностьЧастичныхОплат();
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОсновная Тогда
		НачатьПолучениеПлатежнойСсылки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСсылку(Команда)
	
	Закрыть();
	
	Если ЗначениеЗаполнено(ПлатежнаяСсылка) Тогда
		ОтправитьПлатежнуюСсылку();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьQR(Команда)
	
	Если ЗначениеЗаполнено(ПлатежнаяСсылка) Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму(
			"ОбщаяФорма.ОтображениеQRКодаСБПc2b",
			Новый Структура(
				"ПлатежнаяСсылка, ОснованиеПлатежа",
				ПлатежнаяСсылка,
				ОснованиеПлатежа),
			ЭтотОбъект);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Платежная ссылка не сформирована.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСсылку(Команда)
	
	НачатьПолучениеПлатежнойСсылки(ТребуетсяОбновлениеПлатежнойСсылки);
	
КонецПроцедуры

&НаКлиенте
Процедура КопироватьВБуфер(Команда)
	
	// Копирование происходит с предварительной очисткой через обработчик, для обхода поведения платформы
	// при повторном копировании - при определенных условиях копирование не происходит.
	ЭтотОбъект.БуферОбмена = "";
	ПодключитьОбработчикОжидания("КопироватьСсылкуВБуфер", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОплату(Команда)
	
	Если СуммаКОплате > (МаксимальнаяСуммаОплатыСБП - СуммаВыполненныхОплатСБП) Тогда
		
		Оповещение = Новый ОписаниеОповещения(
			"ПослеОтветаНаВопросОПревышенииСуммы",
			ЭтотОбъект);
			
		ПоказатьВопрос(
			Оповещение,
			НСтр("ru = 'Превышена сумма оплаты по документу. Продолжить?'"),
			РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		НачатьСозданиеЧастичнойОплаты();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСтатусы(Команда)
	
	ОперацииДляЗагрузкиСтатуса = Новый Массив;
	
	Для Каждого ЧастичнаяОплата Из ЧастичныеОплаты Цикл
		Если ЧастичнаяОплата.КодСтатусаОперации = 2 Тогда
			ОперацииДляЗагрузкиСтатуса.Добавить(ЧастичнаяОплата.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Если ОперацииДляЗагрузкиСтатуса.Количество() > 0 Тогда
		НачатьЗагрузкуСтатусовЧастичныхОплат(ОперацииДляЗагрузкиСтатуса);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьЧастичныеОплаты(Команда)
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеОтветаНаВопросОбОтключенииЧастичныхОплат",
		ЭтотОбъект);
		
	ЕстьВыполненныеЧастичныеОплаты = ПроверитьВыполнениеЧастичныхОплат();
		
	Если ЕстьВыполненныеЧастичныеОплаты Тогда
		ПоказатьВопрос(
			Оповещение,
			НСтр("ru = 'Найдены выполненные частичные оплаты, для данного документа необходимо закончить оплату в текущем режиме.
			|Новые документы будут использовать режим полных оплат. Продолжить?'"),
			РежимДиалогаВопрос.ДаНетОтмена);
	Иначе
		ПоказатьВопрос(
			Оповещение,
			НСтр("ru = 'Включить режим использования полных оплат?'"),
			РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ВключитьЧастичныеОплаты(Команда)
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеОтветаНаВопросОВключенииЧастичныхОплат",
		ЭтотОбъект);
		
	ПоказатьВопрос(
		Оповещение,
		НСтр("ru = 'Будет включена возможность формирования частичных оплат (разбиения одного счета на несколько платежей)
			|для текущего пользователя. Включить режим использования частичных оплат?'"),
		РежимДиалогаВопрос.ДаНетОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКСпискуЧастичныхОплат(Команда)
	
	ЗаполнитьДаннымиЧастичныхОплат();
	УстановитьВидимостьДоступностьЧастичныхОплат();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ФормированиеПлатежнойСсылки

&НаСервере
Функция ПолучитьПлатежнуюСсылкуНаСервере(
		Знач ОснованиеПлатежа,
		Знач УникальныйИдентификатор,
		Знач СоздатьНовую)
	
	Элементы.ОбновитьСсылку.Видимость = Ложь;
	Элементы.ПерейтиКСпискуЧастичныхОплат.Доступность = Ложь;
	НовыйИдентификаторОплаты = СоздатьНовую;
	ТекущийСтатусОперации = Неопределено;
	ТребуетсяУточнениеСтатуса = Ложь;
	
	ДанныеДляФормированияПлатежнойСсылки =
		РегистрыСведений.ИдентификаторыОперацийСБПc2b.ДанныеДляФормированияПлатежнойСсылкиОперацииСБП(ОснованиеПлатежа);
	
	ИдентификаторМерчанта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		НастройкаПодключения,
		"ИдентификаторМерчанта");
	
	Если ДанныеДляФормированияПлатежнойСсылки <> Неопределено
		И ДанныеДляФормированияПлатежнойСсылки.НастройкаПодключения <> НастройкаПодключения
		И Не СоздатьНовую Тогда
		
		РезультатВыполнения = Новый Структура;
		РезультатВыполнения.Вставить("Статус", "НеобходимоАктуализировать");
		
	ИначеЕсли ДанныеДляФормированияПлатежнойСсылки = Неопределено
		Или СоздатьНовую
		Или Не ЗначениеЗаполнено(ДанныеДляФормированияПлатежнойСсылки.СтатусОперации)Тогда
		
		НовыйИдентификаторОплаты = Истина;
		
		Если ЗначениеЗаполнено(ОписаниеЧастичнойОплаты) Тогда
			
			ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
			ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Формирование идентификатора оплаты.'");
			
			РезультатВыполнения = ДлительныеОперации.ВыполнитьФункцию(
				ПараметрыВыполнения,
				"ПереводыСБПc2bСлужебный.СлужебныйДинамическаяСсылкаЧастичнойОплаты",
				НастройкаПодключения,
				ОписаниеЧастичнойОплаты);
			
		Иначе
			
			ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
			ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Формирование идентификатора оплаты.'");
			
			РезультатВыполнения = ДлительныеОперации.ВыполнитьФункцию(
				ПараметрыВыполнения,
				"ПереводыСБПc2bСлужебный.СлужебныйДинамическаяСсылка",
				ОснованиеПлатежа,
				НастройкаПодключения);
				
		КонецЕсли;
		
	ИначеЕсли ДанныеДляФормированияПлатежнойСсылки.СтатусОперации = СистемаБыстрыхПлатежейСлужебный.ИдентификаторСтатусаВПроцессе() Тогда
		
		ТекущийСтатусОперации = СистемаБыстрыхПлатежейКлиентСервер.СтатусОперацииВыполняется();
		
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Актуализация статуса идентификатора оплаты.'");
		
		РезультатВыполнения = ДлительныеОперации.ВыполнитьФункцию(
			ПараметрыВыполнения,
			"ПереводыСБПc2bСлужебный.АктуализироватьСтатусОплаты",
			ОснованиеПлатежа,
			НастройкаПодключения,
			ДанныеДляФормированияПлатежнойСсылки,
			ОписаниеЧастичнойОплаты);
		
	ИначеЕсли ДанныеДляФормированияПлатежнойСсылки.СтатусОперации = СистемаБыстрыхПлатежейСлужебный.ИдентификаторСтатусаВыполнена() Тогда
		
		РезультатВыполнения = Новый Структура;
		РезультатВыполнения.Вставить("Статус", "СтатусПолучен");
		РезультатВыполнения.Вставить(
			"СтатусОперации",
			СистемаБыстрыхПлатежейКлиентСервер.СтатусОперацииВыполнена());
		РезультатВыполнения.Вставить(
			"ПлатежнаяСсылка",
			ДанныеДляФормированияПлатежнойСсылки.ПлатежнаяСсылка);
		РезультатВыполнения.Вставить(
			"СуммаОплаты",
			ДанныеДляФормированияПлатежнойСсылки.СуммаОперации);
		
	ИначеЕсли ДанныеДляФормированияПлатежнойСсылки.СтатусОперации = СистемаБыстрыхПлатежейСлужебный.ИдентификаторСтатусаОтменена()
		Или ДанныеДляФормированияПлатежнойСсылки.СтатусОперации = СистемаБыстрыхПлатежейСлужебный.ИдентификаторСтатусаОтклонена() Тогда
		
		Если ДанныеДляФормированияПлатежнойСсылки.СтатусОперации = СистемаБыстрыхПлатежейСлужебный.ИдентификаторСтатусаОтменена() Тогда
			ТекущийСтатусОперации = СистемаБыстрыхПлатежейКлиентСервер.СтатусОперацииОтменена();
		ИначеЕсли ДанныеДляФормированияПлатежнойСсылки.СтатусОперации = СистемаБыстрыхПлатежейСлужебный.ИдентификаторСтатусаОтклонена() Тогда
			ТекущийСтатусОперации = СистемаБыстрыхПлатежейКлиентСервер.СтатусОперацииОтклонена();
		КонецЕсли;
		
		ТребуетсяУточнениеСтатуса = Истина;
		
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Формирование платежной ссылки.'");
		
		РезультатВыполнения = ДлительныеОперации.ВыполнитьФункцию(
			ПараметрыВыполнения,
			"ПереводыСБПc2bСлужебный.СлужебныйДинамическаяСсылка",
			ОснованиеПлатежа,
			НастройкаПодключения);
		
	Иначе
		РезультатВыполнения = Неопределено;
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не удалось определить статус операции, обработка прервана.'"));
	КонецЕсли;
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Процедура ПолучитьПлатежнуюСсылкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ТребуетсяОбновлениеПлатежнойСсылки = Ложь;
	ОписаниеОшибки = "";
	КодОшибки = "";
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если НовыйИдентификаторОплаты Тогда
			СтатусОперацииИзменен = Истина;
			РезультатОперации.Вставить(
				"СтатусОперации",
				СистемаБыстрыхПлатежейКлиентСервер.СтатусОперацииВыполняется());
			Если ЗначениеЗаполнено(РезультатОперации.ПлатежнаяСсылкаСБПc2b) Тогда
				ПлатежнаяСсылкаСБПc2b = РезультатОперации.ПлатежнаяСсылкаСБПc2b;
			КонецЕсли;
		КонецЕсли;
			
		Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			СтатусОперации = "Ошибка";
			ОписаниеОшибки = РезультатОперации.СообщениеОбОшибке;
			КодОшибки = РезультатОперации.КодОшибки;
			УправлениеЭлементамиФормыПоСтатусуОперации(СтатусОперации, ОписаниеОшибки, КодОшибки);
			Возврат;
		КонецЕсли;
		
		Если ТребуетсяУточнениеСтатуса Тогда
			ДанныеСтатусаПоОснованиюПлатежа = ПолучитьДанныеСтатусаПоОснованиюПлатежа(
				?(ЗначениеЗаполнено(ПлатежнаяСсылкаСБПc2b), ПлатежнаяСсылкаСБПc2b, ОснованиеПлатежа));
			ЗаполнитьЗначенияСвойств(РезультатОперации, ДанныеСтатусаПоОснованиюПлатежа, "КодОшибки, СообщениеОбОшибке");
			СтатусОперации = ДанныеСтатусаПоОснованиюПлатежа.СтатусОперации;
			РезультатОперации.Вставить("СтатусОперации", СтатусОперации);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			СтатусОперации = "Ошибка";
			ОписаниеОшибки = РезультатОперации.СообщениеОбОшибке;
			КодОшибки = РезультатОперации.КодОшибки;
		ИначеЕсли РезультатОперации.СтатусОперации = СистемаБыстрыхПлатежейКлиентСервер.СтатусОперацииОтменена()
			Или РезультатОперации.СтатусОперации = СистемаБыстрыхПлатежейКлиентСервер.СтатусОперацииОтклонена() Тогда
			СтатусОперации = РезультатОперации.СтатусОперации;
		Иначе
			ПлатежнаяСсылка = РезультатОперации.ПлатежнаяСсылка;
			СтатусОперации  = РезультатОперации.СтатусОперации;
			СуммаОплаты     = РезультатОперации.СуммаОплаты;
			
			ДанныеАктуальности = Новый Структура;
			ДанныеАктуальности.Вставить("НеобходимоАктуализировать", Ложь);
			ЗаполнитьЗначенияСвойств(ДанныеАктуальности, РезультатОперации);
			Если ДанныеАктуальности.НеобходимоАктуализировать Тогда
				ТребуетсяОбновлениеПлатежнойСсылки = РезультатОперации.НеобходимоАктуализировать;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Результат.Статус = "СтатусПолучен" Тогда
		СтатусОперации = Результат.СтатусОперации;
		СуммаОплаты    = Результат.СуммаОплаты;
		Если СтатусОперации = СистемаБыстрыхПлатежейКлиентСервер.СтатусОперацииВыполнена() Тогда
			ПлатежнаяСсылка = Результат.ПлатежнаяСсылка;
		КонецЕсли;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ОписаниеОшибки = НСтр("ru = 'Ошибка во время выполнения запроса.'");
		СтатусОперации = СистемаБыстрыхПлатежейКлиентСервер.СтатусОперацииОшибка();
	ИначеЕсли Результат.Статус = "НеобходимоАктуализировать" Тогда
		ПлатежнаяСсылка = "";
		ТребуетсяОбновлениеПлатежнойСсылки = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущийСтатусОперации)
		И ТекущийСтатусОперации <> СтатусОперации Тогда
		СтатусОперацииИзменен = Истина;
	КонецЕсли;
	
	УправлениеЭлементамиФормыПоСтатусуОперации(
		СтатусОперации,
		ОписаниеОшибки, КодОшибки);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПолучениеПлатежнойСсылки(СоздатьНовую = Ложь)
	
	ПлатежнаяСсылка = "";
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.Интервал = 1;
	
	РезультатВыполнения = ПолучитьПлатежнуюСсылкуНаСервере(
		?(ЗначениеЗаполнено(ПлатежнаяСсылкаСБПc2b), ПлатежнаяСсылкаСБПc2b, ОснованиеПлатежа),
		ЭтотОбъект.УникальныйИдентификатор,
		СоздатьНовую);
		
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ПолучитьПлатежнуюСсылкуЗавершение",
		ЭтотОбъект);
	
	Если РезультатВыполнения.Статус <> "Выполняется" Тогда
		ПолучитьПлатежнуюСсылкуЗавершение(РезультатВыполнения, Неопределено);
		Возврат;
	КонецЕсли;
	
	Элементы.ДекорацияПояснениеКФорме.ЦветТекста = Новый Цвет;
	Элементы.ДекорацияПояснениеКФорме.Заголовок = НСтр("ru = 'Ожидание ответа сервиса о текущем состоянии оплаты.'");
	Элементы.ДекорацияДлительнаяОперация.Видимость = Истина;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
		
КонецПроцедуры

&НаКлиенте
Процедура НачатьСозданиеЧастичнойОплаты()
	
	Элементы.ОтправитьСсылку.КнопкаПоУмолчанию = Истина;
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОсновная;
	
	ОписаниеЧастичнойОплаты = НовыйДанныеЧастичнойОплаты();
	ОписаниеЧастичнойОплаты.СуммаОплаты      = СуммаКОплате;
	ОписаниеЧастичнойОплаты.ОснованиеПлатежа = ОснованиеПлатежа;
	
	НачатьПолучениеПлатежнойСсылки(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОтправкаПлатежнойСсылки

&НаКлиенте
Процедура ОтправитьПлатежнуюСсылку()
	
	ШаблоныИспользуются = НастройкиШаблонов.Используется;
	
	Если ВариантОтправки = "ЭлектроннаяПочта" Тогда
		
		Если ШаблоныИспользуются И ЗначениеЗаполнено(ШаблонСообщенияЭлектроннаяПочта) Тогда
			СформироватьСообщениеДляОтправки(КонструкторПараметровОтправки(ШаблонСообщенияЭлектроннаяПочта, "Письмо"));
		Иначе
			Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСПочтовымиСообщениями") Тогда
				МодульРаботаСПочтовымиСообщениямиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСПочтовымиСообщениямиКлиент");
				МодульРаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(КонструкторПараметровОтправкиБезШаблона());
			КонецЕсли;
		КонецЕсли;
	
	ИначеЕсли ВариантОтправки = "Телефон" Тогда
		
		Если ШаблоныИспользуются И ЗначениеЗаполнено(ШаблонСообщенияТелефон) Тогда
			СформироватьСообщениеДляОтправки(КонструкторПараметровОтправки(ШаблонСообщенияТелефон, "СообщениеSMS"));
		Иначе
			ПоказатьФормуСообщения(
				Новый Структура("Текст, Получатель, ДополнительныеПараметры", ПлатежнаяСсылка, СписокПолучателей())
				,
				"СообщениеSMS",
				ОснованиеПлатежа);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьФормуСообщения(Сообщение, ВидСообщения, Предмет)
	
	Если ВидСообщения = "СообщениеSMS" Тогда
		Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ОтправкаSMS") Тогда
			
			ДополнительныеПараметры = Новый Структура("ИсточникКонтактнойИнформации, Предмет, ПеревестиВТранслит");
			
			Если Сообщение.ДополнительныеПараметры <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(ДополнительныеПараметры, Сообщение.ДополнительныеПараметры);
			КонецЕсли;
			
			ДополнительныеПараметры.Предмет = Предмет;
			
			Получатели = Новый Массив;
			
			ЗаполнитьПолучателейИзСообщения(Получатели, Сообщение);
			
			МодульОтправкаSMSКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОтправкаSMSКлиент");
			МодульОтправкаSMSКлиент.ОтправитьSMS(Получатели, Сообщение.Текст, ДополнительныеПараметры);
			
		Иначе
			КопироватьСсылкуВБуфер();
		КонецЕсли;
	Иначе
		Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСПочтовымиСообщениями") Тогда
			МодульРаботаСПочтовымиСообщениямиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСПочтовымиСообщениямиКлиент");
			МодульРаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(Сообщение);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура СформироватьСообщениеДляОтправки(ПараметрыОтправки)
	
	Результат = СформироватьСообщениеНаСервере(ПараметрыОтправки);
	
	ПоказатьФормуСообщения(
		Результат,
		ПараметрыОтправки.ДополнительныеПараметры.ВидСообщения,
		ПараметрыОтправки.Предмет);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьСообщениеНаСервере(Знач ПараметрыОтправки)
	
	МодульШаблоныСообщений = ОбщегоНазначения.ОбщийМодуль("ШаблоныСообщений");
	
	Результат = МодульШаблоныСообщений.СформироватьСообщение(
		ПараметрыОтправки.Шаблон,
		ПараметрыОтправки.Предмет,
		ПараметрыОтправки.УникальныйИдентификатор,
		ПараметрыОтправки.ДополнительныеПараметры);
	
	Вложения = Новый Массив;
	
	Для каждого ЭлементКоллекции Из Результат.Вложения Цикл
		
		ТекущееВложение = Новый Структура;
		
		ТекущееВложение.Вставить("Представление");
		ТекущееВложение.Вставить("АдресВоВременномХранилище");
		ТекущееВложение.Вставить("Кодировка");
		ТекущееВложение.Вставить("Идентификатор");
		
		ЗаполнитьЗначенияСвойств(ТекущееВложение, ЭлементКоллекции);
		
		Вложения.Добавить(ТекущееВложение);
		
	КонецЦикла;
	
	Результат.Вставить("Предмет", ПараметрыОтправки.Предмет);
	Результат.Вложения = Вложения;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция КонструкторПараметровОтправки(Шаблон, ВидСообщения)
	
	ПараметрыОтправки = Новый Структура();
	ПараметрыОтправки.Вставить("Шаблон", Шаблон);
	ПараметрыОтправки.Вставить("Предмет", ОснованиеПлатежа);
	ПараметрыОтправки.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	ПараметрыОтправки.Вставить("ДополнительныеПараметры", Новый Структура);
	ПараметрыОтправки.ДополнительныеПараметры.Вставить("ПреобразовыватьHTMLДляФорматированногоДокумента", Истина);
	ПараметрыОтправки.ДополнительныеПараметры.Вставить("ВидСообщения",          ВидСообщения);
	ПараметрыОтправки.ДополнительныеПараметры.Вставить("ПроизвольныеПараметры", Новый Соответствие);
	ПараметрыОтправки.ДополнительныеПараметры.Вставить("ОтправитьСразу",        Ложь);
	ПараметрыОтправки.ДополнительныеПараметры.Вставить("ПлатежнаяСсылка",       ПлатежнаяСсылка);
	ПараметрыОтправки.ДополнительныеПараметры.Вставить("СуммаПлатежнойСсылки",  СуммаОплаты);
	ПараметрыОтправки.ДополнительныеПараметры.Вставить("КонтактныеДанныеЧека",  "");
	
	Возврат ПараметрыОтправки;
	
КонецФункции

&НаКлиенте
Функция КонструкторПараметровОтправкиБезШаблона()
	
	ПараметрыСообщения = Новый Структура;
	
	ПараметрыСообщения.Вставить("Получатель", СписокПолучателей());
	ПараметрыСообщения.Вставить("Предмет", ОснованиеПлатежа);
	ПараметрыСообщения.Вставить("Тема", НСтр("ru = 'Ссылка для оплаты'"));
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("НастройкаПодключения", НастройкаПодключения);
	ПараметрыОперации.Вставить("ПлатежнаяСсылка", ПлатежнаяСсылка);
	ПараметрыОперации.Вставить("СуммаСсылкиСБП", СуммаОплаты);
	
	Если ИнтернетПоддержкаПользователейВызовСервера.ОтправлятьПисьмаВФорматеHTML() Тогда
		ПараметрыСообщения.Вставить(
			"Текст",
			Новый Структура(
				"ТекстHTML, СтруктураВложений",
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ТекстПисьмаБезШаблонаHTML(),
					ПлатежнаяСсылка,
					СуммаОплаты),
				Новый Структура()));
	Иначе
		ПараметрыСообщения.Вставить(
			"Текст",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстПисьмаБезШаблонаТекст(),
				ПлатежнаяСсылка,
				СуммаОплаты));
	КонецЕсли;
	
	ИнтеграцияПодсистемБИПКлиент.ПриЗаполненииПараметровСообщенияБезШаблонаСБП(
		ПараметрыСообщения,
		ПараметрыОперации);
	ПереводыСБПc2bКлиентПереопределяемый.ПриЗаполненииПараметровСообщенияБезШаблонаСБП(
		ПараметрыСообщения,
		ПараметрыОперации);
	
	Возврат ПараметрыСообщения;
	
КонецФункции

&НаКлиенте
Функция ТекстПисьмаБезШаблонаHTML()
	
	HTMLСтрока =
	"<html>
	|<head>
	|<meta http-equiv=""Content-Type"" content=""text/html; charset=utf-8"" />
	|<meta http-equiv=""X-UA-Compatible"" content=""IE=Edge"" />
	|<meta name=""format-detection"" content=""telephone=no"" />
	|</head>
	|<body>
	|<p>Ссылка для оплаты: <a href=""%1"">%1</a>, сумма %2 руб.</p>
	|
	|</body>
	|</html>";
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1'"), HTMLСтрока);
	
КонецФункции

&НаКлиенте
Функция ТекстПисьмаБезШаблонаТекст()
	
	Возврат НСтр("ru = 'Ссылка для оплаты: %1, сумма %2 руб.'");
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПолучателейИзСообщения(Знач Получатели, Знач Сообщение)
	
	Если ТипЗнч(Сообщение.Получатель) = Тип("СписокЗначений") Тогда
		
		Для каждого ЭлементКоллекции Из Сообщение.Получатель Цикл
			
			КонтактныеДанные = Новый Структура;
			
			КонтактныеДанные.Вставить("Телефон",                      ЭлементКоллекции.Значение);
			КонтактныеДанные.Вставить("Представление",                ЭлементКоллекции.Представление);
			КонтактныеДанные.Вставить("ИсточникКонтактнойИнформации", Неопределено);
			
			Получатели.Добавить(КонтактныеДанные);
			
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Сообщение.Получатель) = Тип("Массив") Тогда
		
		Для каждого ЭлементКоллекции Из Сообщение.Получатель Цикл
			
			КонтактныеДанные = Новый Структура;
			
			КонтактныеДанные.Вставить("Телефон",                      ЭлементКоллекции.НомерТелефона);
			КонтактныеДанные.Вставить("Представление",                ЭлементКоллекции.Представление);
			КонтактныеДанные.Вставить("ИсточникКонтактнойИнформации", ЭлементКоллекции.ИсточникКонтактнойИнформации);
			
			Получатели.Добавить(КонтактныеДанные);
		
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СписокПолучателей()
	
	СписокПолучателей = Новый СписокЗначений;
	
	ИнтеграцияПодсистемБИП.ПриФормированииСпискаПолучателейСообщенияСБП(
		ОснованиеПлатежа,
		ВариантОтправки,
		СписокПолучателей);
	ПереводыСБПc2bПереопределяемый.ПриФормированииСпискаПолучателейСообщенияСБП(
		ОснованиеПлатежа,
		ВариантОтправки,
		СписокПолучателей);
	
	Возврат СписокПолучателей;
	
КонецФункции

#КонецОбласти

#Область ШаблоныПоУмолчанию

&НаСервереБезКонтекста
Процедура СохранитьШаблоныПоУмолчанию(
	Знач ОснованиеПлатежа,
	Знач ШаблонСообщенияЭлектроннаяПочта,
	Знач ШаблонСообщенияТелефон)
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ОснованиеПлатежа)) Тогда
		ПредставлениеОснования = ОснованиеПлатежа.Метаданные().ПолноеИмя();
	Иначе
		ПредставлениеОснования = ОснованиеПлатежа;
	КонецЕсли;
	
	// Шаблоны электронной почты
	КлючНастроек = "ШаблоныСообщенийЭлектроннойПочты";
	
	Настройки = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить(
		"ФормаФункциональнойСсылкиСБП",
		КлючНастроек);
	
	Если Настройки = Неопределено Тогда
		ШаблоныПоУмолчанию = Новый Соответствие();
	Иначе
		ШаблоныПоУмолчанию = Настройки;
	КонецЕсли;
	
	ШаблоныПоУмолчанию.Вставить(ПредставлениеОснования, ШаблонСообщенияЭлектроннаяПочта);
	
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить(
		"ФормаПодготовкиПлатежнойСсылкиСБП",
		КлючНастроек,
		ШаблоныПоУмолчанию);
	
	// Шаблоны SMS сообщений
	КлючНастроек = "ШаблоныСообщенийSMS";
	
	Настройки = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить(
		"ФормаФункциональнойСсылкиСБП",
		КлючНастроек);
	
	Если Настройки = Неопределено Тогда
		ШаблоныПоУмолчанию = Новый Соответствие();
	Иначе
		ШаблоныПоУмолчанию = Настройки;
	КонецЕсли;
	
	ШаблоныПоУмолчанию.Вставить(ПредставлениеОснования, ШаблонСообщенияТелефон);
	
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить(
		"ФормаПодготовкиПлатежнойСсылкиСБП",
		КлючНастроек,
		ШаблоныПоУмолчанию);
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьШаблоныПоУмолчанию()
	
	Если НастройкиШаблонов.Используется
		И ЗначениеЗаполнено(ОснованиеПлатежа) Тогда
		
		Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ОснованиеПлатежа)) Тогда
			ПредставлениеОснования = ОснованиеПлатежа.Метаданные().ПолноеИмя();
		Иначе
			ПредставлениеОснования = ОснованиеПлатежа;
		КонецЕсли;
		
		// Шаблоны электронной почты
		Настройки = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить(
			"ФормаПодготовкиПлатежнойСсылкиСБП",
			"ШаблоныСообщенийЭлектроннойПочты");
		
		Если Настройки <> Неопределено
			И ТипЗнч(Настройки) = Тип("Соответствие")
			И Настройки[ПредставлениеОснования] <> Неопределено Тогда
			
			ШаблонСообщенияЭлектроннаяПочта = Настройки[ПредставлениеОснования];
			
		КонецЕсли;
		
		// Шаблоны SMS сообщений
		Настройки = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить(
			"ФормаПодготовкиПлатежнойСсылкиСБП",
			"ШаблоныСообщенийSMS");
		
		Если Настройки <> Неопределено
			И ТипЗнч(Настройки) = Тип("Соответствие")
			И Настройки[ПредставлениеОснования] <> Неопределено Тогда
			
			ШаблонСообщенияТелефон = Настройки[ПредставлениеОснования];
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЧастичныеОплаты

&НаСервере
Функция НовыйДанныеЧастичнойОплаты()
	
	ДанныеЧастичнойОплаты = Новый Структура;
	ДанныеЧастичнойОплаты.Вставить("СуммаОплаты",           0);
	ДанныеЧастичнойОплаты.Вставить("ДатаОплаты",            Дата(1,1,1));
	ДанныеЧастичнойОплаты.Вставить("ОснованиеПлатежа",      Неопределено);
	ДанныеЧастичнойОплаты.Вставить("ПлатежнаяСсылкаСБПc2b", Документы.ПлатежнаяСсылкаСБПc2b.ПустаяСсылка());
	
	Возврат ДанныеЧастичнойОплаты;
	
КонецФункции

&НаСервере
Функция ЗаполнитьОписаниеЧастичнойОплаты(Знач ПлатежнаяСсылкаСБПc2b)

	ДанныеПлатежнойСсылки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ПлатежнаяСсылкаСБПc2b,
		"Дата, Сумма, ОснованиеПлатежа");
	
	ДанныеЧастичнойОплаты = НовыйДанныеЧастичнойОплаты();
	ДанныеЧастичнойОплаты.СуммаОплаты           = ДанныеПлатежнойСсылки.Сумма;
	ДанныеЧастичнойОплаты.ОснованиеПлатежа      = ДанныеПлатежнойСсылки.ОснованиеПлатежа;
	ДанныеЧастичнойОплаты.ДатаОплаты            = ДанныеПлатежнойСсылки.Дата;
	ДанныеЧастичнойОплаты.ПлатежнаяСсылкаСБПc2b = ПлатежнаяСсылкаСБПc2b;
	
	Возврат ДанныеЧастичнойОплаты;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДаннымиЧастичныхОплат()
	
	ЧастичныеОплаты.Очистить();
	СуммаВыполненныхОплатСБП = 0;
	
	ДанныеЧастичныхОплат = Документы.ПлатежнаяСсылкаСБПc2b.ПолучитьДанныеЧастичныхОплат(ОснованиеПлатежа);
	
	Для Каждого ДанныеЧастичнойОплаты Из ДанныеЧастичныхОплат Цикл
		
		Если ДанныеЧастичнойОплаты.СтатусОперации = СистемаБыстрыхПлатежейСлужебный.ИдентификаторСтатусаВыполнена() Тогда
			СуммаВыполненныхОплатСБП = СуммаВыполненныхОплатСБП + ДанныеЧастичнойОплаты.Сумма;
		КонецЕсли;
		
		Если ДанныеЧастичнойОплаты.НастройкаПодключения <> НастройкаПодключения Тогда
			Продолжить;
		КонецЕсли;
		
		ЧастичнаяОплата = ЧастичныеОплаты.Добавить();
		ЗаполнитьЗначенияСвойств(ЧастичнаяОплата, ДанныеЧастичнойОплаты);
		ЧастичнаяОплата.КодСтатусаОперации = ПолучитьКодПоСтатусуОперации(ДанныеЧастичнойОплаты.СтатусОперации);
		
		Если ЧастичнаяОплата.КодСтатусаОперации = 3 Тогда
			ЕстьВыполненныеЧастичныеОплаты = Истина;
		КонецЕсли;
		
		ЧастичнаяОплата.ЭтоПлатежнаяСсылкаСБПc2b = Истина;
		
	КонецЦикла;
	
	Если ДанныеЧастичныхОплат.Количество() > 0 Тогда
		ДокументыЧастичныхОплатСозданы = Истина;
	КонецЕсли;
	
	ДанныеПлатежнойСсылкиОснованияПлатежа =
		РегистрыСведений.ИдентификаторыОперацийСБПc2b.ДанныеДляФормированияПлатежнойСсылкиОперацииСБП(ОснованиеПлатежа);
		
	Если ДанныеПлатежнойСсылкиОснованияПлатежа <> Неопределено 
		И НастройкаПодключения = ДанныеПлатежнойСсылкиОснованияПлатежа.НастройкаПодключения Тогда
		
		Если ДанныеПлатежнойСсылкиОснованияПлатежа.СтатусОперации = СистемаБыстрыхПлатежейСлужебный.ИдентификаторСтатусаВыполнена() Тогда
			СуммаВыполненныхОплатСБП = СуммаВыполненныхОплатСБП + ДанныеПлатежнойСсылкиОснованияПлатежа.СуммаОперации;
		КонецЕсли;
		
		Если ДанныеПлатежнойСсылкиОснованияПлатежа.НастройкаПодключения = НастройкаПодключения Тогда
			ЧастичнаяОплата = ЧастичныеОплаты.Добавить();
			ЧастичнаяОплата.Ссылка                = ОснованиеПлатежа;
			ЧастичнаяОплата.КодСтатусаОперации    = ПолучитьКодПоСтатусуОперации(
			ДанныеПлатежнойСсылкиОснованияПлатежа.СтатусОперации);
			ЧастичнаяОплата.ПредставлениеОперации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Оплата от %1 на сумму %2 руб.'"),
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОснованиеПлатежа, "Дата"),
				ДанныеПлатежнойСсылкиОснованияПлатежа.СуммаОперации);
		КонецЕсли;
		
	КонецЕсли;
	
	СуммаКОплате = Макс(МаксимальнаяСуммаОплатыСБП - СуммаВыполненныхОплатСБП, 0);
	
	Если СуммаКОплате = 0 Тогда
		Элементы.ДобавитьОплату.Доступность = Ложь;
		Элементы.СуммаКОплате.Доступность = Ложь;
		Элементы.ДекорацияПояснениеЧастичныеОплаты.ЦветТекста = Новый Цвет;
		Элементы.ДекорацияПояснениеЧастичныеОплаты.Заголовок  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Платежные ссылки были сформированы и отправлены покупателю. Оплачена сумма %1 руб.'"),
			СуммаВыполненныхОплатСБП);
	Иначе
		Элементы.ДобавитьОплату.Доступность = Истина;
		Элементы.СуммаКОплате.Доступность = Истина;
		Элементы.ДекорацияПояснениеЧастичныеОплаты.ЦветТекста = Новый Цвет;
		Элементы.ДекорацияПояснениеЧастичныеОплаты.Заголовок  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Введите сумму и сформируйте платежную ссылку. Осталось оплатить %1 руб.'"),
			СуммаКОплате);
	КонецЕсли;
	
	
	Элементы.ДекорацияДлительнаяОперацияЧастичныеОплаты.Видимость = Ложь;
	
КонецПроцедуры

// Определяет код статуса операции по ее идентфикатору
//
// Параметры:
//  СтатусОперации - Строка - идентификатор статуса операции;
//
// Возвращаемое значение:
//  Число,Неопределено - Числовой код статуса операции, Неопределено для случаев, когда статус операции неизвестен.
//
&НаСервере
Функция ПолучитьКодПоСтатусуОперации(СтатусОперации)
	
	Если ПустаяСтрока(СтатусОперации)
		Или СтатусОперации = СистемаБыстрыхПлатежейСлужебный.ИдентификаторСтатусаВПроцессе() Тогда
		Возврат 2;
	ИначеЕсли СтатусОперации = СистемаБыстрыхПлатежейСлужебный.ИдентификаторСтатусаВыполнена() Тогда
		Возврат 3;
	ИначеЕсли СтатусОперации = СистемаБыстрыхПлатежейСлужебный.ИдентификаторСтатусаОтклонена()
		Или СтатусОперации = СистемаБыстрыхПлатежейСлужебный.ИдентификаторСтатусаОтменена() Тогда
		Возврат 0;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура УстановитьРежимИспользованияЧастичныхОплат(Знач Использование)
	
	НастройкаИспользованияЧастичныхОплат = Использование;
	
	ОбщегоНазначения.ХранилищеНастроекДанныхФормСохранить(
		"ФормаОтправкиПлатежнойСсылки",
		"ИспользоватьЧастичныеОплаты",
		НастройкаИспользованияЧастичныхОплат);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступностьЧастичныхОплат()
	
	Если Не ИспользоватьЧастичныеОплаты Тогда
		Элементы.ОтправитьСсылку.КнопкаПоУмолчанию       = Истина;
		Элементы.Страницы.ТекущаяСтраница                = Элементы.СтраницаОсновная;
		ПлатежнаяСсылкаСБПc2b                            = Неопределено;
		Элементы.ГруппаВключитьЧастичныеОплаты.Видимость = Ложь;
		Элементы.ПерейтиКСпискуЧастичныхОплат.Видимость  = Ложь;
		Возврат;
	КонецЕсли;
	
	Если ДокументыЧастичныхОплатСозданы
		Или НастройкаИспользованияЧастичныхОплат Тогда
		Элементы.ДобавитьОплату.КнопкаПоУмолчанию = Истина;
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаЧастичныеОплаты;
	Иначе
		Элементы.ОтправитьСсылку.КнопкаПоУмолчанию = Истина;
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОсновная;
		ПлатежнаяСсылкаСБПc2b = Неопределено;
		ЭтотОбъект.ТекущийЭлемент = Элементы.СтраницаОсновная;
	КонецЕсли;
	
	Если ИспользоватьЧастичныеОплаты И Не НастройкаИспользованияЧастичныхОплат Тогда
		Элементы.ГруппаВключитьЧастичныеОплаты.Видимость = Истина;
		Элементы.ПерейтиКСпискуЧастичныхОплат.Видимость  = Ложь;
	Иначе
		Элементы.ГруппаВключитьЧастичныеОплаты.Видимость = Ложь;
		Элементы.ПерейтиКСпискуЧастичныхОплат.Видимость  = Истина;
	КонецЕсли;
	
	Если ИспользоватьЧастичныеОплаты И НастройкаИспользованияЧастичныхОплат Тогда
		Элементы.ГруппаОтключитьЧастичныеОплаты.Видимость = Истина;
	Иначе
		Элементы.ГруппаОтключитьЧастичныеОплаты.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьВыполнениеЧастичныхОплат()
	
	Для Каждого ЧастичнаяОплата Из ЧастичныеОплаты Цикл
		Если ЧастичнаяОплата.КодСтатусаОперации = 3 Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура ПослеОтветаНаВопросОВключенииЧастичныхОплат(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		УстановитьРежимИспользованияЧастичныхОплат(Истина);
		ЗаполнитьДаннымиЧастичныхОплат();
		УстановитьВидимостьДоступностьЧастичныхОплат();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопросОбОтключенииЧастичныхОплат(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		УстановитьРежимИспользованияЧастичныхОплат(Ложь);
		УстановитьВидимостьДоступностьЧастичныхОплат();
		Если Не ДокументыЧастичныхОплатСозданы Тогда
			НачатьПолучениеПлатежнойСсылки();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопросОПревышенииСуммы(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		НачатьСозданиеЧастичнойОплаты();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗагрузкуСтатусовЧастичныхОплат(ОперацииДляЗагрузкиСтатуса)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.Интервал = 1;
	
	РезультатВыполнения = ЗагрузитьСтатусыЧастичныхОплатНаСервере(
		ОперацииДляЗагрузкиСтатуса);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ЗагрузкаСтатусовЧастичныхОплатЗавершение",
		ЭтотОбъект);
	
	Если РезультатВыполнения.Статус <> "Выполняется" Тогда
		ЗагрузкаСтатусовЧастичныхОплатЗавершение(
			РезультатВыполнения,
			Неопределено);
		Возврат;
	КонецЕсли;
	
	Элементы.ДекорацияПояснениеКФорме.ЦветТекста = Новый Цвет;
	Элементы.ДекорацияПояснениеЧастичныеОплаты.Заголовок
		= НСтр("ru = 'Получение статусов частичных оплат, ожидание ответа сервиса.'");
	Элементы.ДекорацияДлительнаяОперацияЧастичныеОплаты.Видимость = Истина;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаСтатусовЧастичныхОплатЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаполнитьДаннымиЧастичныхОплат();
	Элементы.ДекорацияДлительнаяОперацияЧастичныеОплаты.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьСтатусыЧастичныхОплатНаСервере(Знач ОперацииДляЗагрузкиСтатуса)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Загрузка статусов частичных оплат.'");
	ОписаниеОпераций = Новый Массив;
	
	Для Каждого ОперацияДляЗагрузкиСтатуса Из ОперацииДляЗагрузкиСтатуса Цикл
		
		ОписаниеОперации = Новый Структура;
		ОписаниеОперации.Вставить("ОснованиеПлатежа", ОперацияДляЗагрузкиСтатуса);
		ОписаниеОперации.Вставить(
			"ПараметрыОперации",
			РегистрыСведений.ИдентификаторыОперацийСБПc2b.ДанныеДляФормированияПлатежнойСсылкиОперацииСБП(
				ОперацияДляЗагрузкиСтатуса));
		Если ТипЗнч(ОперацияДляЗагрузкиСтатуса) = Тип("ДокументСсылка.ПлатежнаяСсылкаСБПc2b") Тогда
			
			ОписаниеОперации.Вставить(
				"ДанныеЧастичнойОплаты",
				ЗаполнитьОписаниеЧастичнойОплаты(ОперацияДляЗагрузкиСтатуса));
			
		Иначе
			
			ОписаниеОперации.Вставить("ДанныеЧастичнойОплаты", Неопределено);
			
		КонецЕсли;
		
		ОписаниеОпераций.Добавить(ОписаниеОперации);
		
	КонецЦикла;
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"ПереводыСБПc2bСлужебный.АктуализироватьСтатусыОплат",
		НастройкаПодключения,
		ОписаниеОпераций);
		
	Возврат РезультатВыполнения;
	
КонецФункции

#КонецОбласти

&НаСервере
Процедура УправлениеЭлементамиФормыПоДанным()
	
	Если Параметры.НастройкиПодключения.Количество() <> 1 Тогда
		
		НастройкиПодключенияДокумента = ПереводыСБПc2bСлужебный.НастройкиПодключенияПоДокументуОперации(
			ОснованиеПлатежа);
		
		Для Каждого НастройкаПодключения Из Параметры.НастройкиПодключения Цикл
			НоваяНастройкаПодключения = НастройкиНастройкаПодключения.Добавить();
			НоваяНастройкаПодключения.НастройкаПодключения = НастройкаПодключения;
			Если НастройкиПодключенияДокумента.Найти(НастройкаПодключения) <> Неопределено Тогда
				НоваяНастройкаПодключения.ИдентификаторСформирован = Истина;
			КонецЕсли;
		КонецЦикла;
		
		НастройкиНастройкаПодключения.Сортировать("ИдентификаторСформирован Убыв",);
		
	Иначе
		УстановитьВидимостьДоступностьЧастичныхОплат();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭлементамиФормыПоПодсистемам()
	
	ЕстьЭлектроннаяПочта = ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСПочтовымиСообщениями");
	ЕстьОтправкаSMS = ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ОтправкаSMS");
	ИспользуютсяШаблоны = НастройкиШаблонов.Используется;
	
	ЕстьВыборВариантаОтправки = ЕстьЭлектроннаяПочта И ЕстьОтправкаSMS;

	Элементы.ГруппаВариантОтправкиЭлектроннаяПочта.Видимость = ЕстьЭлектроннаяПочта;
	Элементы.НадписьВариантОтправкиЭлектроннаяПочта.Видимость = Не ЕстьВыборВариантаОтправки;
	Элементы.ВариантОтправкиЭлектроннаяПочта.Видимость = ЕстьВыборВариантаОтправки;
	
	Элементы.ГруппаВариантОтправкиТелефон.Видимость = ЕстьОтправкаSMS;
	Элементы.НадписьВариантОтправкиТелефон.Видимость = Не ЕстьВыборВариантаОтправки;
	Элементы.ВариантОтправкиТелефон.Видимость = ЕстьВыборВариантаОтправки;
	
	Если Не ЕстьЭлектроннаяПочта И Не ЕстьОтправкаSMS Тогда
		Элементы.ГруппаВариантОтправки.Видимость = Ложь;
		Элементы.ОтправитьСсылку.Видимость = Ложь;
	КонецЕсли;
	
	Если Не (ЕстьВыборВариантаОтправки ИЛИ ИспользуютсяШаблоны) Тогда
		
		Элементы.ГруппаВариантОтправки.Видимость = Ложь;
		
	КонецЕсли;
	
	Если Не ИспользуютсяШаблоны Тогда
		Элементы.ГруппаВариантОтправки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		Элементы.ГруппаВариантОтправкиПереключатели.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		Элементы.ШаблонСообщенияЭлектроннаяПочта.Видимость = Ложь;
		Элементы.ШаблонСообщенияТелефон.Видимость = Ложь;
		Элементы.ДекорацияКонвертОтправка.Видимость = Ложь;
		Элементы.ДекорацияСообщенияОтправка.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭлементамиФормыПоСтатусуОперации(
	Знач СтатусОперации,
	Знач ОписаниеОшибки,
	Знач КодОшибки)
	
	Элементы.ДекорацияДлительнаяОперация.Видимость = Ложь;
	
	Если ТребуетсяОбновлениеПлатежнойСсылки Тогда
		
		Элементы.ДекорацияПояснениеКФорме.Заголовок = 
			НСтр("ru = 'Внимание. Данные оплаты были изменены, требуется обновление ссылки.'");
		
		Элементы.ДекорацияПояснениеКФорме.ЦветТекста = ЦветаСтиля.ЦветОсобогоТекста;
		
		Элементы.Страницы.ТолькоПросмотр                  = Истина;
		Элементы.ОбновитьСсылку.Видимость                 = Истина;
		Элементы.КопироватьВБуфер.Доступность             = Ложь;
		Элементы.ОтправитьСсылку.Доступность              = Ложь;
		Элементы.ПерейтиКСпискуЧастичныхОплат.Доступность = Истина;
		Элементы.ПоказатьQR.Доступность                   = Ложь;
		ЭтотОбъект.ТекущийЭлемент                         = Элементы.ОбновитьСсылку;
		
	ИначеЕсли СтатусОперации = СистемаБыстрыхПлатежейКлиентСервер.СтатусОперацииВыполняется() Тогда
		
		Элементы.ДекорацияПояснениеКФорме.Заголовок = 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ссылка для оплаты сформирована и готова к отправке покупателю. Сумма к оплате %1 руб.'"),
				СуммаОплаты);
			
		Элементы.ДекорацияПояснениеКФорме.ЦветФона = ЦветаСтиля.ЦветФонаФормы;
		Элементы.ДекорацияПояснениеКФорме.ЦветТекста = Новый Цвет;
		
		Элементы.Страницы.ТолькоПросмотр                  = Ложь;
		Элементы.ГруппаВариантОтправки.ТолькоПросмотр     = Ложь;
		Элементы.ГруппаПолеВводаСсылки.ТолькоПросмотр     = Ложь;
		Элементы.ОбновитьСсылку.Видимость                 = Ложь;
		Элементы.КопироватьВБуфер.Доступность             = Истина;
		Элементы.ОтправитьСсылку.Доступность              = Истина;
		Элементы.ПерейтиКСпискуЧастичныхОплат.Доступность = Истина;
		Элементы.ПоказатьQR.Доступность                   = Истина;
		
	ИначеЕсли СтатусОперации = СистемаБыстрыхПлатежейКлиентСервер.СтатусОперацииВыполнена() Тогда
		
		Элементы.ДекорацияПояснениеКФорме.Заголовок = 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Платежная ссылка была сформирована и отправлена покупателю. Оплачена сумма %1 руб.'"),
				СуммаОплаты);
		
		Элементы.ДекорацияПояснениеКФорме.ЦветФона = ЦветаСтиля.ЦветФонаПодсказки;
		
		Элементы.Страницы.ТолькоПросмотр                  = Истина;
		Элементы.ОбновитьСсылку.Видимость                 = Ложь;
		Элементы.КопироватьВБуфер.Доступность             = Ложь;
		Элементы.ОтправитьСсылку.Доступность              = Ложь;
		Элементы.ПоказатьQR.Доступность                   = Ложь;
		Элементы.ПерейтиКСпискуЧастичныхОплат.Доступность = Истина;
		
	ИначеЕсли СтатусОперации = СистемаБыстрыхПлатежейКлиентСервер.СтатусОперацииОшибка() Тогда
		
		Элементы.ДекорацияПояснениеКФорме.Заголовок = ОписаниеОшибки;
		
		Если КодОшибки = СистемаБыстрыхПлатежейСлужебный.КодОшибкиУжеОплачен() Тогда
			Элементы.ДекорацияПояснениеКФорме.ЦветФона = ЦветаСтиля.ЦветФонаПодсказки;
			Элементы.ОбновитьСсылку.Видимость = Ложь;
		Иначе
			Элементы.ДекорацияПояснениеКФорме.ЦветФона = ЦветаСтиля.ЦветФонаФормы;
			Элементы.ОбновитьСсылку.Видимость = Истина;
			ЭтотОбъект.ТекущийЭлемент         = Элементы.ОбновитьСсылку;
		КонецЕсли;
			
		Элементы.ГруппаВариантОтправки.ТолькоПросмотр     = Истина;
		Элементы.ГруппаПолеВводаСсылки.ТолькоПросмотр     = Истина;
		Элементы.КопироватьВБуфер.Доступность             = Ложь;
		Элементы.ОтправитьСсылку.Доступность              = Ложь;
		Элементы.ПерейтиКСпискуЧастичныхОплат.Доступность = Истина;
		Элементы.ПоказатьQR.Доступность                   = Ложь;
		
	ИначеЕсли СтатусОперации = СистемаБыстрыхПлатежейКлиентСервер.СтатусОперацииОтклонена()
		ИЛИ СтатусОперации = СистемаБыстрыхПлатежейКлиентСервер.СтатусОперацииОтменена() Тогда
		
		Элементы.ДекорацияПояснениеКФорме.Заголовок = 
			НСтр("ru = 'Операция отменена или истек срок жизни платежной ссылки, для обновления необходимо изменить данные оплаты.'");
			
		Элементы.ДекорацияПояснениеКФорме.ЦветТекста = ЦветаСтиля.ЦветОсобогоТекста; 
	
		Элементы.Страницы.ТолькоПросмотр                  = Истина;
		Элементы.ОбновитьСсылку.Видимость                 = Ложь;
		Элементы.КопироватьВБуфер.Доступность             = Ложь;
		Элементы.ОтправитьСсылку.Доступность              = Ложь;
		Элементы.ПерейтиКСпискуЧастичныхОплат.Доступность = Истина;
		Элементы.ПоказатьQR.Доступность                   = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	// Оформляемые поля
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиПодключенияВыбор.Имя);
	
	// Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("НастройкиНастройкаПодключения.ИдентификаторСформирован");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", Метаданные.ЭлементыСтиля.ВажнаяНадписьШрифт.Значение);
	
	Элемент.Использование = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КопироватьСсылкуВБуфер()
	
	ЭтотОбъект.БуферОбмена = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"<!DOCTYPE html>
		|<html>
		|	<body onload='copy()'>
		|		<input id='input' type='text'/>
		|		<script>
		|			function copy() {
		|				var text = '%1';
		|				var ua = navigator.userAgent;
		|				if (ua.search(/MSIE/) > 0 || ua.search(/Trident/) > 0) {
		|					window.clipboardData.setData('Text', text);
		|				} else {
		|					var copyText = document.getElementById('input');
		|					copyText.value = text;
		|					copyText.select();
		|					document.execCommand('copy');
		|				}
		|			}
		|		</script>
		|	</body>
		|</html>",
		ПлатежнаяСсылка);
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Ссылка получена'"),
		,
		НСтр("ru = 'Ссылка для оплаты через СБП скопирована в буфер обмена'"));
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеСтатусаПоОснованиюПлатежа(Знач ОснованиеПлатежа)
	
	ДанныеОснованияПлатежа =
		РегистрыСведений.ИдентификаторыОперацийСБПc2b.ДанныеДляФормированияПлатежнойСсылкиОперацииСБП(ОснованиеПлатежа);
	
	Возврат ПереводыСБПc2bСлужебный.ПолучитьСтатусОперацииПоИдентификатору(ДанныеОснованияПлатежа.СтатусОперации);
	
КонецФункции

#КонецОбласти
