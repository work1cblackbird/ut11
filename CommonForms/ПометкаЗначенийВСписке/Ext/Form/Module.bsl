#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = Параметры.Заголовок;
	Для Каждого Элемент Из Параметры.Список Цикл
		Список.Добавить(Элемент.Значение, Элемент.Представление, Элемент.Пометка, Элемент.Картинка);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВозвращаемоеЗначение = Новый СписокЗначений();
	
	ЕстьПометка = Ложь;
	Для Каждого Элемент Из Список Цикл
		ЕстьПометка = ЕстьПометка Или Элемент.Пометка;
		ВозвращаемоеЗначение.Добавить(Элемент.Значение, Элемент.Представление, Элемент.Пометка, Элемент.Картинка);
	КонецЦикла;
	
	Если Не ЕстьПометка Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Необходимо установить пометку хотя бы у одного элемента'"));
	Иначе
		ОповеститьОВыборе(ВозвращаемоеЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	Для Каждого Элемент Из Список Цикл
		Элемент.Пометка = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсеОтметки(Команда)
	Для Каждого Элемент Из Список Цикл
		Элемент.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти