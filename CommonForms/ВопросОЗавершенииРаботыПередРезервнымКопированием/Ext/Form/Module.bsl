///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Размещение текста.
	ТекстСообщения = Параметры.ТекстСообщения;
	
	МинимальнаяШиринаПоля = 40;
	ПримернаяВысотаПоля   = ЧислоСтрок(Параметры.ТекстСообщения, МинимальнаяШиринаПоля);
	Элементы.ТекстСообщения.Ширина = МинимальнаяШиринаПоля;
	Элементы.ТекстСообщения.Высота = Мин(ПримернаяВысотаПоля, 10);
	
	// Обновление заголовок команд.
	Элементы.Отмена.Заголовок = Параметры.ПредставлениеКнопкиОтмена;
	
	// Установка таймера обратного отсчета.
	ОжиданиеСчетчик = Параметры.Таймаут;
	
	// Сброс размеров и положения окна этой формы.
	СброситьРазмерыИПоложениеОкна();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Запуск обратного отсчета.
	Если ОжиданиеСчетчик >= 1 Тогда
		ОжиданиеСчетчик = ОжиданиеСчетчик + 1;
		ПродолжитьОбратныйОтсчет();
	КонецЕсли;
	
КонецПроцедуры

// Обработка оповещения события см. ОблачныйАрхив20Клиент.ИмяСобытияОповещенияОбновитьДиалогВопросОЗавершенииРаботы
//
// Параметры:
//  ИмяСобытия - Строка
//  Параметр - Структура:
//    * ТекстСообщения - Строка
//    * ПредставлениеКнопкиОтмена - Строка
//    * Таймаут - Число
//  Источник - Неопределено
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = ОблачныйАрхив20Клиент.ИмяСобытияОповещенияОбновитьДиалогВопросОЗавершенииРаботы() Тогда
		
		// Размещение текста.
		Если ТекстСообщения <> Параметр.ТекстСообщения Тогда
			ТекстСообщения = Параметр.ТекстСообщения;
		КонецЕсли;
		
		// Установка таймера обратного отсчета.
		Если ОжиданиеСчетчик <> Параметр.Таймаут Тогда
			
			ОтключитьОбработчикОжидания("ПродолжитьОбратныйОтсчет");
			ОжиданиеСчетчик = Параметр.Таймаут;
			
			Если ОжиданиеСчетчик >= 1 Тогда
				ОжиданиеСчетчик = ОжиданиеСчетчик + 1;
				ПродолжитьОбратныйОтсчет();
			КонецЕсли;
			
		КонецЕсли;
		
		// Обновление заголовок команд.
		Если Элементы.Отмена.Заголовок <> Параметр.ПредставлениеКнопкиОтмена Тогда
			Элементы.Отмена.Заголовок = Параметр.ПредставлениеКнопкиОтмена;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПродолжитьОбратныйОтсчет()
	
	ОжиданиеСчетчик = ОжиданиеСчетчик - 1;
	Если ОжиданиеСчетчик <= 0 Тогда
		Закрыть(КодВозвратаДиалога.ОК);
	Иначе
		
		Элементы.ЗавершитьРаботу.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Завершить работу (%1)'"),
			Строка(ОжиданиеСчетчик));
		
		ПодключитьОбработчикОжидания("ПродолжитьОбратныйОтсчет", 1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СброситьРазмерыИПоложениеОкна()
	
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		ИмяПользователя = ИмяПользователя();
		ХранилищеСистемныхНастроек.Удалить(ИмяФормы, "", ИмяПользователя);
	КонецЕсли;
	
	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры

// Определяет примерное число строк с учетом переносов.
&НаСервереБезКонтекста
Функция ЧислоСтрок(Текст, ОтсечкаПоШирине, ПриводитьКРазмерамЭлементовФормы = Истина)
	
	ЧислоСтрок     = СтрЧислоСтрок(Текст);
	ЧислоПереносов = 0;
	
	Для НомерСтроки = 1 По ЧислоСтрок Цикл
		Строка = СтрПолучитьСтроку(Текст, НомерСтроки);
		ЧислоПереносов = ЧислоПереносов + Цел(СтрДлина(Строка) / ОтсечкаПоШирине);
	КонецЦикла;
	
	ПримерноеЧислоСтрок = ЧислоСтрок + ЧислоПереносов;
	
	Если ПриводитьКРазмерамЭлементовФормы Тогда
		Коэффициент = 2 / 3; // В такси в высоту 2 вмещается примерно 3 строки текста.
		ПримерноеЧислоСтрок = Цел((ПримерноеЧислоСтрок + 1) * Коэффициент);
	КонецЕсли;
	
	Если ПримерноеЧислоСтрок = 2 Тогда
		ПримерноеЧислоСтрок = 3;
	КонецЕсли;
	
	Возврат ПримерноеЧислоСтрок;
	
КонецФункции

#КонецОбласти
