///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкаПодключения = Параметры.НастройкаПодключения;
	Если Не ЗначениеЗаполнено(НастройкаПодключения) Тогда
		ВызватьИсключение НСтр("ru = 'Не корректные параметры формы.'");
	КонецЕсли;
	
	ИнтеграцияПодсистемБИП.ПриСозданииНаСервереФормыПодключенияСсылки(
		ЭтотОбъект,
		Отказ);
	ПереводыСБПc2bПереопределяемый.ПриСозданииНаСервереФормыПодключенияСсылки(
		ЭтотОбъект,
		Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИнтеграцияПодсистемБИПКлиент.ПриОткрытииФормыПодключенияСсылки(
		ЭтотОбъект,
		Отказ);
	ПереводыСБПc2bКлиентПереопределяемый.ПриОткрытииФормыПодключенияСсылки(
		ЭтотОбъект,
		Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ИнтеграцияПодсистемБИПКлиент.ПриЗакрытииФормыПодключенияСсылки(
		ЭтотОбъект,
		ЗавершениеРаботы);
	ПереводыСБПc2bКлиентПереопределяемый.ПриЗакрытииФормыПодключенияСсылки(
		ЭтотОбъект,
		ЗавершениеРаботы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Подключить = Ложь;
	ИнтеграцияПодсистемБИПКлиент.ОбработкаОповещенияФормыПодключенияСсылки(
		ЭтотОбъект,
		ИмяСобытия,
		Параметр,
		Источник,
		Подключить);
	ПереводыСБПc2bКлиентПереопределяемый.ОбработкаОповещенияФормыПодключенияСсылки(
		ЭтотОбъект,
		ИмяСобытия,
		Параметр,
		Источник,
		Подключить);
	
	Если Подключить Тогда
		СлужебныйПодключить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключить(Команда)
	
	СлужебныйПодключить();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СлужебныйПодключить()
	
	Если Не СтрНачинаетсяС(КассоваяСсылка, "https")
		И СтрДлина(КассоваяСсылка) = 32
		И СтроковыеФункцииКлиентСервер.ТолькоЛатиницаВСтроке(КассоваяСсылка, Истина, "1234567890") Тогда
		
		ИдентификаторСсылки = КассоваяСсылка;
		
	Иначе
		РезультатПроверки = СистемаБыстрыхПлатежейКлиент.СтруктураURLФункциональнойСсылки(
			КассоваяСсылка,
			Истина);
		
		Если Не РезультатПроверки.URLВалиден Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Неверный формат кассовой ссылки.'"),
				,
				,
				"КассоваяСсылка");
			Возврат;
		КонецЕсли;
		
		ИдентификаторСсылки = Лев(
			РезультатПроверки.СтруктураURI.ПутьНаСервере,
			32);
		
	КонецЕсли;
	
	РезультатПодключения = КассоваяСсылка(
		НастройкаПодключения,
		ИдентификаторСсылки);
	
	Если ЗначениеЗаполнено(РезультатПодключения.КодОшибки) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			РезультатПодключения.СообщениеОбОшибке);
		Возврат;
	КонецЕсли;
	
	ДанныеСсылки = Новый Структура;
	ДанныеСсылки.Вставить("ИдентификаторОплаты", РезультатПодключения.ИдентификаторОплаты);
	ДанныеСсылки.Вставить("КассоваяСсылка", РезультатПодключения.КассоваяСсылка);
	
	Закрыть(ДанныеСсылки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КассоваяСсылка(Знач НастройкаПодключения, Знач ИдентификаторОплаты)
	
	Возврат ПереводыСБПc2bСлужебный.СлужебныйКассоваяСсылка(
		НастройкаПодключения,
		ИдентификаторОплаты);
	
КонецФункции

#КонецОбласти
