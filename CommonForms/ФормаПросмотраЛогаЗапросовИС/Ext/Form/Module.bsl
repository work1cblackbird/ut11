
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Текст                    = Параметры.Текст;
	Подсистема               = Параметры.Подсистема;
	ЭтоИнформацияОбОкружении = Параметры.ЭтоИнформацияОбОкружении;
	
	МодульЛогированияЗапросов = ОбщегоНазначения.ОбщийМодуль("ЛогированиеЗапросов" + Подсистема);
	
	Если ЭтоИнформацияОбОкружении Тогда
		Заголовок = НСтр("ru = 'Информация об окружении'");
		АвтоЗаголовок = Ложь;
		Текст = МодульЛогированияЗапросов.ИнформацияОбОкружении();
		Элементы.ФормаФорматировать.Видимость = Ложь;
	Иначе
		Если ЭтоАдресВременногоХранилища(Параметры.АдресДанных) Тогда
			Текст = СтрШаблон(
				НСтр("ru = 'Полный лог запросов:
				|%1
				|
				|%2'"),
				СокрЛП(ПолучитьИзВременногоХранилища(Параметры.АдресДанных)),
				МодульЛогированияЗапросов.ИнформацияОбОкружении());
		КонецЕсли;
		Элементы.ФормаФорматировать.Видимость = (Подсистема = "ИСМП" Или ((Подсистема = "ЗЕРНО" Или Подсистема = "САТУРН") И ОбщегоНазначенияИС.ЭтоРасширеннаяВерсияГосИС(Подсистема)));
	КонецЕсли;
	
	ИсходныйТекст = Текст;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	Если ЭтоИнформацияОбОкружении Тогда
		ИмяФайла = СтрШаблон(
			НСтр("ru = 'Информация об окружении от %1.txt'"),
			Формат(ТекущаяДата(), "ДФ=dd_MM_yyyy_HH_mm_ss"));
	Иначе
		ИмяФайла = СтрШаблон(
			НСтр("ru = 'Полный лог запросов от %1.txt'"),
			Формат(ТекущаяДата(), "ДФ=dd_MM_yyyy_HH_mm_ss"));
	КонецЕсли;
	ДиалогВыбораФайла                = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбораФайла.Фильтр         = "*.txt|*.txt";
	ДиалогВыбораФайла.ПолноеИмяФайла = ИмяФайла;
	
	ОповещениеВыбораФайла = Новый ОписаниеОповещения("ВыборФайлаЗавершение", ЭтотОбъект);
	
	ДиалогВыбораФайла.Показать(ОповещениеВыбораФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура Форматировать(Команда)
	
	Элементы.ФормаФорматировать.Пометка = Не Элементы.ФормаФорматировать.Пометка;
	ФорматироватьНаСервере(Не Элементы.ФормаФорматировать.Пометка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОписанияОповещений

&НаКлиенте
Процедура ВыборФайлаЗавершение(ПутиКФайлам, ДополнительныеПараметры) Экспорт
	
	Если ПутиКФайлам = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДвоичныеДанные = ПолучитьДвоичныеДанныеИзСтроки(Текст);
	
	Попытка
		ДвоичныеДанные.Записать(ПутиКФайлам[0]);
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ПоказатьПредупреждение(,
			СтрШаблон(
				НСтр("ru = 'Ошибка сохранения файла:
					       |%1'"),
				КраткоеПредставлениеОшибки(ИнформацияОбОшибке)));
		Возврат;
		
	КонецПопытки;
	
	ПоказатьОповещениеПользователя(
		СтрШаблон(
			НСтр("ru = 'Файл сохранен по пути: %1'"),
			ПутиКФайлам[0]));
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ФорматироватьНаСервере(ВосстановитьИзначальный, ДополнительныеПараметры = Неопределено)
	
	Если ВосстановитьИзначальный Тогда
		Текст = ИсходныйТекст;
	Иначе
		Если Подсистема = "ИСМП" Тогда
			ИмяМодуляФорматированияЛога = СтрШаблон("ОбщегоНазначения%1", Подсистема);
		ИначеЕсли Не ОбщегоНазначенияИС.ЭтоРасширеннаяВерсияГосИС(Подсистема) Тогда
			Возврат;
		Иначе
			ИмяМодуляФорматированияЛога = СтрШаблон("Интеграция%1", Подсистема);
		КонецЕсли;
		МодульФорматированияЛога    = ОбщегоНазначения.ОбщийМодуль(ИмяМодуляФорматированияЛога);
		Текст                       = МодульФорматированияЛога.ФорматироватьЛогЗапросов(ИсходныйТекст, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти