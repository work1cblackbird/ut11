
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.АварийныйРежимВключен Тогда
		
		Элементы.СтраницыТекущееСостояниеАварийногоРежима.ТекущаяСтраница = Элементы.СтраницаАварийныйРежимВключен;
		
		Элементы.ВключитьАварийныйРежим.Видимость = Ложь;
		Элементы.Отмена.Заголовок   = НСтр("ru = 'Закрыть'");
		
		ШаблонСтроки = НСтр("ru = 'Аварийный режим включен до %1.'");
		Элементы.СтатусВключенияВАварийногоРежима.Заголовок = СтрШаблон(ШаблонСтроки, Параметры.АварийныйРежимДо);
		
	Иначе
		
		Элементы.СтраницыТекущееСостояниеАварийногоРежима.ТекущаяСтраница = Элементы.СтраницаАварийныйРежимВыключен;
		
		Элементы.ВключитьАварийныйРежим.Видимость = Истина;
		Элементы.Отмена.Заголовок   = НСтр("ru = 'Отмена'");
		
	КонецЕсли;
	
	ПериодВключения = 24;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВключитьАварийныйРежим(Команда)
	
	СтруктураВозврата = Новый Структура("АктивностьРежима, СрокДействияВЧасах", Истина, ПериодВключения);
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьАварийныйРежим(Команда)
	
	СтруктураВозврата = Новый Структура("АктивностьРежима", Ложь);
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ПродлитьАварийныйРежим(Команда)
	
	СписокПродолжительностьАварийногоРежима = Новый СписокЗначений();
	СписокПродолжительностьАварийногоРежима.Добавить(1,  НСтр("ru='продлить на 1 час'"));
	СписокПродолжительностьАварийногоРежима.Добавить(8, НСтр("ru='продлить на 8 часов'"));
	СписокПродолжительностьАварийногоРежима.Добавить(24, НСтр("ru='продлить на 24 часа'"));
	
	ОповещениеОВыборе = Новый ОписаниеОповещения("ВыборПродолжительностьАварийногоРежимаЗавершение", ЭтотОбъект);
	ПоказатьВыборИзСписка(ОповещениеОВыборе, СписокПродолжительностьАварийногоРежима, Элементы.ПродлитьАварийныйРежим);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыборПродолжительностьАварийногоРежимаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВозврата = Новый Структура("АктивностьРежима, СрокДействияВЧасах", Истина, Результат.Значение);
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

#КонецОбласти
