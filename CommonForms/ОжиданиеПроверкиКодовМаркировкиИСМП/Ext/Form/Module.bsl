#Область ОписаниеПеременных

&НаКлиенте
Перем ПараметрыПроверкиСредствамиККТ; // см. ШтрихкодированиеОбщегоНазначенияИСМПКлиент.НовыеПараметрыПроверкиНаККТ -
&НаКлиенте
Перем ВыполняемыеОперации; // см. ВыполняемыеОперации
&НаКлиенте
Перем ИмяСобытияПроверкаКМ;
&НаКлиенте
Перем МодульМенеджерОборудованияМаркировкаКлиент;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Прогресс.Видимость           = Ложь;
	Элементы.ПояснениеПрогресса.Видимость = Ложь;
	
	ДанныеСтрокиСообщения = Новый Массив();
	ДанныеСтрокиСообщения.Добавить(НСтр("ru = 'Подготовка к проверке средствами ККТ пожалуйста, подождите...'"));
	
	Элементы.ДекорацияПоясняющийТекстДлительнойОперации.Заголовок = Новый ФорматированнаяСтрока(ДанныеСтрокиСообщения);
	
	СтандартныеПодсистемыСервер.СброситьРазмерыИПоложениеОкна(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	ШтрихкодированиеОбщегоНазначенияИСМПКлиент.ОбработкаОповещенияПриПроверкеСредствамиККТ(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
	Если ПараметрыПроверкиСредствамиККТ <> Неопределено Тогда
		ИдентификаторОсновногоОбъекта = ШтрихкодированиеОбщегоНазначенияИСМПКлиент.ФормаБлокировкиПоПараметрамПроверки(
			ПараметрыПроверкиСредствамиККТ).УникальныйИдентификатор;
	КонецЕсли;

	Если Источник = Неопределено
		Или Не (ВладелецФормы = Источник
			Или ИдентификаторОсновногоОбъекта = Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяСобытия = ШтрихкодированиеОбщегоНазначенияИСМПКлиент.ИмяСобытияОповещенияИзменилосьСостояниеПроверкиСредствамиККТ() Тогда
		
		Если ПроверкаНачата Тогда
			Возврат;
		Иначе
			ПроверкаНачата = Истина;
		КонецЕсли;
		
		Если ПараметрыПроверкиСредствамиККТ = Неопределено Тогда
			Элементы.БуферОбмена.Высота = 2;
		КонецЕсли;
		
		ПараметрыПроверкиСредствамиККТ = Параметр;
		НастроитьПредставление();

	ИначеЕсли ИмяСобытия = ШтрихкодированиеОбщегоНазначенияИСМПКлиент.ИмяСобытияОповещенияСледующегоШагаПроверкиСредствамиККТ() Тогда

		ПодключитьОбработчикОжидания("Подключаемый_ВыполнениеШагаПроверки", Параметр, Истина);

	ИначеЕсли ИмяСобытия = ШтрихкодированиеОбщегоНазначенияИСМПКлиент.ИмяСобытияОповещенияЕстьОшибкиПроверкиСредствамиККТ() Тогда
		
		ШтрихкодированиеИСМПКлиент.ОткрытьФормуОтображенияОшибок(Параметр);
		ЗакрытьОкно();

	ИначеЕсли ИмяСобытия = ШтрихкодированиеОбщегоНазначенияИСМПКлиент.ИмяСобытияОповещенияПрогрессПолученияИдентификаторовЗапросаГИСМТ() Тогда

		ПараметрыПроверкиСредствамиККТ = Параметр.ПараметрыПроверки;
		ОтобразитьПрогрессПолученияИдентификаторовЗапросаГИСМТ(Параметр);

	ИначеЕсли ИмяСобытия = "ЗакрытьОкноОжиданияПроверкиСредствамиККТ" Тогда

		ЗакрытьОкно();

	ИначеЕсли ИмяСобытия = МенеджерОборудованияМаркировкаКлиент.СобытиеПроверкаКМ() Тогда

		ДополнительныеПараметры                      = Параметр.ДополнительныеПараметры;
		ВыполняемыеОперации                          = ШтрихкодированиеОбщегоНазначенияИСМПКлиентСервер.ВыполняемыеОперацииПроверкиСредствамиККТ();
		ОперацииПроверкиКМ                           = МенеджерОборудованияМаркировкаКлиент.СписокОперацийПроверкиКМ();
		ПараметрыПроверкиСредствамиККТ.ТекущийИндекс = ДополнительныеПараметры.ИндексЭлемента;

		Если Параметр.Операция = ОперацииПроверкиКМ.ЗапросККТ Тогда
			ПараметрыПроверкиСредствамиККТ.ТекущийЭтапПроверки = ВыполняемыеОперации.ЛокальнаяПроверка;
		ИначеЕсли Параметр.Операция = ОперацииПроверкиКМ.ЗапросОИСМ Тогда
			ПараметрыПроверкиСредствамиККТ.ТекущийЭтапПроверки = ВыполняемыеОперации.УдаленнаяПроверка;
		ИначеЕсли Параметр.Операция = ОперацииПроверкиКМ.ПодтверждениеККТ Тогда
			ПараметрыПроверкиСредствамиККТ.ТекущийЭтапПроверки = ВыполняемыеОперации.Подтверждение;
		Иначе
			Возврат;
		КонецЕсли;

		НастроитьПредставление();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ВыполняемыеОперации      = ШтрихкодированиеОбщегоНазначенияИСМПКлиентСервер.ВыполняемыеОперацииПроверкиСредствамиККТ();
	ПроверкаЕдинымМетодомБПО = ШтрихкодированиеОбщегоНазначенияИСМПКлиент.ИспользуетсяВерсияБПОСЕдинымМетодомПроверкиКМСредствамиККТ();

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы
		Или ЗакрытиеОкнаРазрешено Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияПоясняющийТекстДлительнойОперацииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "СкопироватьШтриховойКодВБуферОбмена" Тогда
		
		СтандартнаяОбработка = Ложь;
		ИнтеграцияИСКлиент.СкопироватьШтрихКодВБуферОбмена(Элементы.БуферОбмена, КодМаркировки);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрерватьОперацию(Команда)
	
	ПараметрыПроверкиСредствамиККТ.ПерерватьОперацию = Истина;
	Элементы.ПрерватьОперацию.Доступность            = Ложь;
	Элементы.ПрерватьОперацию.Заголовок              = НСтр("ru = 'Отменено'");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОписаниеОповещений

&НаКлиенте
Процедура ОтобразитьПрогрессПолученияИдентификаторовЗапросаГИСМТ(ДанныеПрогресса) Экспорт
	
	// процент выполнения - количество обработанных объектов
	Если ДанныеПрогресса.ПроцентВыполнения > 0 Тогда
		ПараметрыПроверкиСредствамиККТ.ТекущийИндекс = ДанныеПрогресса.ПроцентВыполнения - 1;
	Иначе
		ПараметрыПроверкиСредствамиККТ.ТекущийИндекс = ПараметрыПроверкиСредствамиККТ.ЭлементыПроверки.Количество() - 1;
	КонецЕсли;
		
	НастроитьПредставление();
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Подключаемый_ВыполнениеШагаПроверки()
	
	ШтрихкодированиеОбщегоНазначенияИСМПКлиент.ВыполнениеШагаПроверкиСредствамиККТ(ЭтотОбъект, ПараметрыПроверкиСредствамиККТ);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьОкно(РезультаЗакрытия = Ложь)
	
	ЗакрытиеОкнаРазрешено = Истина;
	Закрыть(РезультаЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьПредставление()
	
	ПараметрыПроверки     = ПараметрыПроверкиСредствамиККТ;
	ЭлементПроверки       = ШтрихкодированиеОбщегоНазначенияИСМПКлиент.ТекущийЭлементПроверкиСредствамиККТ(ПараметрыПроверки);
	КодМаркировки         = ЭлементПроверки.КодМаркировки;
	КоличествоЭлементов   = ПараметрыПроверки.ЭлементыПроверки.Количество();
	НомерТекущегоЭлемента = ПараметрыПроверки.ТекущийИндекс + 1;
	
	Для Каждого ЭлементПроверки Из ПараметрыПроверки.ЭлементыПроверки Цикл
		Если Не ШтрихкодированиеОбщегоНазначенияИСМПКлиент.ЭлементПроверкиСредствамиКТТПроверяетсяНаОборудовании(ЭлементПроверки) Тогда
			КоличествоЭлементов = КоличествоЭлементов - 1;
		КонецЕсли;
	КонецЦикла;
	
	ДанныеСтрокиСообщения = Новый Массив();
	Если ПараметрыПроверки.ТекущаяОперация = ВыполняемыеОперации.ЛокальнаяПроверка Тогда
		ДанныеСтрокиСообщения.Добавить(НСтр("ru = 'Выполняется локальная проверка средствами ККТ кода маркировки:'"));
	ИначеЕсли ПараметрыПроверки.ТекущаяОперация = ВыполняемыеОперации.УдаленнаяПроверка Тогда
		ДанныеСтрокиСообщения.Добавить(НСтр("ru = 'Выполняется проверка статуса ОИСМ кода маркировки средствами ККТ:'"));
	ИначеЕсли ПараметрыПроверки.ТекущаяОперация = ВыполняемыеОперации.Подтверждение Тогда
		ДанныеСтрокиСообщения.Добавить(НСтр("ru = 'Выполняется подтверждение кода маркировки на ККТ:'"));
	ИначеЕсли ПараметрыПроверки.ТекущаяОперация = ВыполняемыеОперации.ПроверкаИдентификатораГИСМТ
		Или ПараметрыПроверки.ТекущаяОперация = ВыполняемыеОперации.ПолучениеТокенаГИСМТ Тогда
		ДанныеСтрокиСообщения.Добавить(НСтр("ru = 'Выполняется разрешительный запрос ГИС МТ'"));
	ИначеЕсли ПараметрыПроверки.ТекущаяОперация = ВыполняемыеОперации.ОбновлениеCDNПлощадок Тогда
		ДанныеСтрокиСообщения.Добавить(НСтр("ru = 'Выполняется обновление CDN-площадок ГИС МТ'"));
	ИначеЕсли ПараметрыПроверки.ТекущаяОперация = ВыполняемыеОперации.ПроверкаЕдинымМетодомБПО Тогда
		Если ПараметрыПроверки.ТекущийЭтапПроверки = ВыполняемыеОперации.ЛокальнаяПроверка Тогда
			ДанныеСтрокиСообщения.Добавить(НСтр("ru = 'Выполняется локальная проверка средствами ККТ кода маркировки:'"));
		ИначеЕсли ПараметрыПроверки.ТекущийЭтапПроверки = ВыполняемыеОперации.УдаленнаяПроверка Тогда
			ДанныеСтрокиСообщения.Добавить(НСтр("ru = 'Выполняется проверка статуса ОИСМ кода маркировки средствами ККТ:'"));
		ИначеЕсли ПараметрыПроверки.ТекущийЭтапПроверки = ВыполняемыеОперации.Подтверждение Тогда
			ДанныеСтрокиСообщения.Добавить(НСтр("ru = 'Выполняется подтверждение кода маркировки на ККТ:'"));
		КонецЕсли;
	КонецЕсли;
	
	ДанныеСтрокиСообщения.Добавить(" ");
	ДанныеСтрокиСообщения.Добавить(Новый ФорматированнаяСтрока(КодМаркировки,,,, "СкопироватьШтриховойКодВБуферОбмена"));
	
	Если КоличествоЭлементов > 1 Тогда
		
		ПояснениеПрогресса = СтрШаблон(
			НСтр("ru = 'Код маркировки %1 из %2'"),
			НомерТекущегоЭлемента,
			КоличествоЭлементов);
		Если Не Элементы.Прогресс.Видимость Тогда
			Элементы.Прогресс.Видимость           = Истина;
			Элементы.ПояснениеПрогресса.Видимость = Истина;
		КонецЕсли;
		
		Если КоличествоЭлементов <> 0 Тогда
			Прогресс = Окр(НомерТекущегоЭлемента / КоличествоЭлементов * 100);
		КонецЕсли;
		
	КонецЕсли;
	
	ДанныеСтрокиСообщения.Добавить(Символы.ПС);
	ДанныеСтрокиСообщения.Добавить(НСтр("ru = 'Пожалуйста, подождите...'"));
	
	Элементы.ДекорацияПоясняющийТекстДлительнойОперации.Заголовок = Новый ФорматированнаяСтрока(ДанныеСтрокиСообщения);
	Элементы.ПрерватьОперацию.Видимость                           = (Не ПараметрыПроверки.ЭтоСканирование И Не ПроверкаЕдинымМетодомБПО);
	
КонецПроцедуры

#КонецОбласти
