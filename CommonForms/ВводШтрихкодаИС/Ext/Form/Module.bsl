
#Область ОписаниеПеременных

&НаКлиенте
Перем ШтрихкодДляПроверки;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	СтандартныеПодсистемыСервер.СброситьРазмерыИПоложениеОкна(ЭтотОбъект);
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ШтрихкодИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	ШтрихкодДляПроверки = Текст;
	
	ПодключитьОбработчикОжидания("ПроверитьФорматШтрихкода", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Отказ = Ложь;
	ПроверитьЗаполнениеРеквизитов(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ФорматBase64", ФорматBase64);
	Результат.Вставить("Штрихкод",     Штрихкод);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроверитьЗаполнениеРеквизитов(Отказ)
	
	ОчиститьСообщения();
	
	ШаблонСообщения = Нстр("ru='Поле ""%1"" не заполнено'");
	
	Если Не ЗначениеЗаполнено(Штрихкод) Тогда
		ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Штрихкод'"));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "Штрихкод",, Отказ);
	КонецЕсли;
	
	Если ФорматBase64 Тогда
		ИдентификаторыОшибокРазбораКодаМаркировки = РазборКодаМаркировкиИССлужебныйКлиентСервер.ИдентификаторыОшибокРазбораКодаМаркировки();
		РезультатРаспаковки = ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.Base64ВШтрихкод(Штрихкод);
		
		ВидыПродукции = Новый Массив;
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			ВидыПродукции,
			ОбщегоНазначенияИСКлиентСервер.ВидыПродукцииИСМП(Истина), Истина);
		
		ПримечаниеКРезультатуРазбора = Неопределено;
		РезультатРазбора = РазборКодаМаркировкиИССлужебныйКлиент.РазобратьКодМаркировки(РезультатРаспаковки, ВидыПродукции, ПримечаниеКРезультатуРазбора);
		Если РезультатРазбора = Неопределено
			И ПримечаниеКРезультатуРазбора <> Неопределено
			И ЗначениеЗаполнено(ПримечаниеКРезультатуРазбора.ТекстОшибки)
			И ПримечаниеКРезультатуРазбора.ИдентификаторОшибки <> ИдентификаторыОшибокРазбораКодаМаркировки.КодМаркировкиСоответствуетНесколькимШаблонам Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ПримечаниеКРезультатуРазбора.ТекстОшибки,, "Штрихкод",, Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьФорматШтрихкода()
	
	ФорматИзменен = ШтрихкодированиеИСКлиент.ПроверитьФорматШтрихкода(ШтрихкодДляПроверки, ФорматBase64);
	
	Если ФорматИзменен Тогда
		Штрихкод = ШтрихкодДляПроверки;
		Элементы.Штрихкод.ОбновитьТекстРедактирования();
		ТекущийЭлемент = Элементы.ФорматBase64;
		ТекущийЭлемент = Элементы.Штрихкод;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти