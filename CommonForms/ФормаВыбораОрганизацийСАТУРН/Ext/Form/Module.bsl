#Область ОписаниеПеременных

&НаКлиенте
Перем ФормаОткрыта;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокОрганизаций();
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	Если Параметры.Свойство("ОрганизацииСАТУРН") Тогда
		Настройки.Удалить("СтруктураОтбораОрганизаций");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	СтруктураОтбораОрганизаций = Настройки.Получить("СтруктураОтбораОрганизаций");
	
	Если СтруктураОтбораОрганизаций <> Неопределено Тогда
		Для Каждого СтрокаТЧ Из ТаблицаОрганизации  Цикл
			НайденнаяСтрока = СтруктураОтбораОрганизаций.Организации.Найти(СтрокаТЧ.ОрганизацияСАТУРН);
			Если НайденнаяСтрока = Неопределено Тогда
				СтрокаТЧ.Выбрана = Ложь;
			Иначе
				СтрокаТЧ.Выбрана = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Выбрана", Истина);
	Организациии = ТаблицаОрганизации.Выгрузить(ПараметрыОтбора).ВыгрузитьКолонку("ОрганизацияСАТУРН");
	СопоставленныеОрганизациии = ТаблицаОрганизации.Выгрузить(ПараметрыОтбора).ВыгрузитьКолонку("Организация");
	
	Настройки.Вставить("СтруктураОтбораОрганизаций",
		ИнтеграцияСАТУРНКлиентСервер.СтруктураОтбораОрганизаций(Организациии, СопоставленныеОрганизациии));
		
	СтруктураОтбораОрганизаций = ИнтеграцияСАТУРНКлиентСервер.СтруктураОтбораОрганизаций(Организациии, СопоставленныеОрганизациии, Истина);
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"ОбщаяФорма.ФормаВыбораОрганизацийСАТУРН", "СтруктураОтбораОрганизаций", СтруктураОтбораОрганизаций);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаОрганизации

&НаКлиенте
Процедура ТаблицаОрганизацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ТекущиеДанные = Элементы.ТаблицаОрганизации.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ПоказатьЗначение(, ТекущиеДанные.ОрганизацияСАТУРН);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	
	ИзменитьПометку(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьВсе(Команда)
	
	ИзменитьПометку(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	Организациии               = Новый Массив;
	СопоставленныеОрганизации = Новый Массив;
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("Организации",               Организациии);
	РезультатВыбора.Вставить("СопоставленныеОрганизации", СопоставленныеОрганизации);
	РезультатВыбора.Вставить("СохраненыНастройки",        СохраненыНастройки);
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Выбрана", Истина);
	НайденныеСтроки = ТаблицаОрганизации.НайтиСтроки(ПараметрыОтбора);
	
	Для Каждого СтрокаТЧ Из НайденныеСтроки Цикл
		РезультатВыбора.Организации.Добавить(СтрокаТЧ.ОрганизацияСАТУРН);
		РезультатВыбора.СопоставленныеОрганизации.Добавить(СтрокаТЧ.Организация);
	КонецЦикла;
	
	Закрыть(РезультатВыбора);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокОрганизаций()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КлассификаторОрганизацийСАТУРН.Ссылка КАК ОрганизацияСАТУРН
	|ИЗ
	|	Справочник.КлассификаторОрганизацийСАТУРН КАК КлассификаторОрганизацийСАТУРН
	|ГДЕ
	|	НЕ КлассификаторОрганизацийСАТУРН.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОрганизацияСАТУРН
	|";
	
	МассивОрганизаций = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ОрганизацияСАТУРН");
	
	СопоставленныеОрганизации = Справочники.КлассификаторОрганизацийСАТУРН.ОрганизацииКонтрагентыПоКлассификаторамСАТУРН(МассивОрганизаций);
	
	ОрганизацииСАТУРН = Параметры.ОрганизацииСАТУРН.ВыгрузитьЗначения();
	
	Для Каждого ОрганизацияСАТУРН Из МассивОрганизаций Цикл
		
		СопоставленнаяОрганизация = СопоставленныеОрганизации[ОрганизацияСАТУРН];
		Если СопоставленнаяОрганизация = Неопределено
			Или Не ЗначениеЗаполнено(СопоставленнаяОрганизация.Организация) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаОрганизации.Добавить();
		НоваяСтрока.ОрганизацияСАТУРН = ОрганизацияСАТУРН;
		НоваяСтрока.Выбрана = ОрганизацииСАТУРН.Найти(ОрганизацияСАТУРН) <> Неопределено;
		НоваяСтрока.Организация = СопоставленнаяОрганизация.Организация;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьПометку(Пометка)
	
	Для Каждого СтрокаТЧ Из ТаблицаОрганизации Цикл
		СтрокаТЧ.Выбрана = Пометка;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти