#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ

&НаКлиенте
Перем ОбработкаЗакрытия; //используется отработки закрытия формы

&НаКлиенте
Перем ТекущиеДанныеИдентификатор; //используется для передачи текущей строки в обработчик ожидания

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияУТ.НастроитьПодключаемоеОборудование(ЭтаФорма);
	НеИспользоватьУпаковки = Параметры.НеИспользоватьУпаковки;
	
	Если Параметры.Свойство("ВыводитьВесОбъем") Тогда
		ВыводитьВесОбъем = Параметры.ВыводитьВесОбъем;
	Иначе
		ВыводитьВесОбъем = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("ВыгруженаТолькоНеМаркируемаяПродукция", ВыгруженаТолькоНеМаркируемаяПродукция) Тогда
		Если ВыгруженаТолькоНеМаркируемаяПродукция Тогда
			Заголовок = НСтр("ru = 'Проверка количества товаров в документе (немаркируемая продукция)'");
		КонецЕсли;
	КонецЕсли;
	
	ПревышениеКоличестваТоваровРазрешено = Параметры.ПревышениеКоличестваТоваровРазрешено;
	ОтклонениеЗапрещено                  = Параметры.ОтклонениеЗапрещено;
	Склад                                = Параметры.Склад;
	СкладГруппа                          = ЭтоГруппаСкладовИСкладыИспользуютсяВТЧДокументовПродажи(Склад);
	ПеремещениеПоЗаказам                 = Параметры.ПеремещениеПоЗаказам;
	Статус                               = Параметры.Статус;
	Ссылка                               = Параметры.Ссылка;
	ЗаказНаСборку                        = Параметры.ЗаказНаСборку;
	Соглашение                           = Параметры.Соглашение;
	ПоступлениеПоЗаказам                 = Параметры.ПоступлениеПоЗаказам;
	
	Если ЗначениеЗаполнено(Параметры.ПараметрыУказанияСерий) Тогда
		ПараметрыУказанияСерийВрем = Новый Структура(Параметры.ПараметрыУказанияСерий);
		ПараметрыУказанияСерийВрем.ИмяИсточникаЗначенийВФормеОбъекта = "ЭтаФорма";
		ПараметрыУказанияСерий = Новый ФиксированнаяСтруктура(ПараметрыУказанияСерийВрем);
		
		НоменклатураСервер.ДобавитьПоляСтатусовУказанияСерий(ЭтотОбъект, ПараметрыУказанияСерий);
	КонецЕсли;
	
	ПараметрыВыбораНоменклатурыВТабличнойЧасти = Ссылка.Метаданные().ТабличныеЧасти[Параметры.ИмяТабличнойЧасти].Реквизиты.Номенклатура.ПараметрыВыбора;
	Для Каждого СтрокаТЧ Из ПараметрыВыбораНоменклатурыВТабличнойЧасти Цикл
		Если СтрНайти(СтрокаТЧ.Имя, "Отбор.ТипНоменклатуры") Тогда
			Если ТипЗнч(СтрокаТЧ.Значение) = Тип("ПеречислениеСсылка.ТипыНоменклатуры") Тогда
				ТипыНоменклатуры.Добавить(СтрокаТЧ.Значение);
			Иначе
				Для Каждого ЗначениеПеречисления Из СтрокаТЧ.Значение Цикл
					ТипыНоменклатуры.Добавить(ЗначениеПеречисления);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Если Не ЗначениеЗаполнено(ТипыНоменклатуры) Тогда
		Для Каждого ЗначениеПеречисления Из Перечисления.ТипыНоменклатуры Цикл
			ТипыНоменклатуры.Добавить(ЗначениеПеречисления);
		КонецЦикла;
	КонецЕсли;
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ТоварыСерия",
		"Видимость",
		ЗначениеЗаполнено(Параметры.ПараметрыУказанияСерий) И ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры);
		
	Товары.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресВоВременномХранилище));
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		СтрокаТЧ.ЕстьРасхождение = 1;
	КонецЦикла;
	
	НаборыСервер.ЗаполнитьСлужебныеРеквизиты(ЭтаФорма, "Товары", Истина);
	
	Если ВыводитьВесОбъем Тогда
		ЗаполнитьСлужебныеРеквизитыПоНоменклатуре();
	Иначе
		Элементы.ТоварыГруппаВес.Видимость = Ложь;
		Элементы.ТоварыГруппаОбъем.Видимость = Ложь;
	КонецЕсли;
	
	Если НеИспользоватьУпаковки Тогда
		Элементы.Упаковка.Видимость = Ложь;
	КонецЕсли;
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	РаботаСТабличнымиЧастями.ИнициализироватьКэшСтрок(Элементы.Товары);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтаФорма, "СканерШтрихкода");
	
	ОбработкаЗакрытия = Истина;
	ТекущийЭлемент = Элементы.Номенклатура;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Не ОбработкаЗакрытия Тогда
		
		Если ОтклонениеЗапрещено Тогда
			Отмена(Неопределено);
		Иначе
			Отказ = Истина;
			ПоказатьВопрос(
				Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект),
				НСтр("ru = 'Завершить проверку без переноса расхождений в документ?'"),
				РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если НоменклатураКлиент.ЭтоУказаниеСерий(ИсточникВыбора) Тогда
		
		НоменклатураКлиент.ОбработатьУказаниеСерии(ЭтаФорма, ПараметрыУказанияСерий, ВыбранноеЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаОповещенияНаСервере(ИмяСобытия, Параметр, Источник)
	
	ДополнительныеПараметры = Новый Структура("ИмяСобытия, Параметр, Источник", ИмяСобытия, Параметр, Источник);
	СобытияФорм.ПриИзмененииЭлемента(ЭтотОбъект, "Событие", ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("КэшированныеЗначения",    КэшированныеЗначения);
	ДополнительныеПараметры.Вставить("СтандартнаяОбработка",    Истина);
	ДополнительныеПараметры.Вставить("ТребуетсяСерверныйВызов", Ложь);
	СобытияФормКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник, ДополнительныеПараметры);
	КэшированныеЗначения = ДополнительныеПараметры.КэшированныеЗначения;
	Если ДополнительныеПараметры.ТребуетсяСерверныйВызов Тогда
		ОбработкаОповещенияНаСервере(ИмяСобытия, Параметр, Источник);
	КонецЕсли;
	Если Не ДополнительныеПараметры.СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" И МенеджерОборудованияУТКлиент.ЕстьНеобработанноеСобытие() Тогда
			
			ДанныеШтрихкодов = МенеджерОборудованияУТКлиент.ПреобразоватьДанныеСоСканераВМассив(Параметр);
			ОбработатьШтрихкоды(ДанныеШтрихкодов);
			
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
	// Неизвестные штрихкоды
	Если Источник = "ПодключаемоеОборудование"
		И ИмяСобытия = "НеизвестныеШтрихкоды"
		И Параметр.ФормаВладелец = УникальныйИдентификатор Тогда
		
		КэшированныеЗначения.Штрихкоды.Очистить();
		ДанныеШтрихкодов = Новый Массив;
		Для Каждого ПолученныйШтрихкод Из Параметр.ПолученыНовыеШтрихкоды Цикл
			ДанныеШтрихкодов.Добавить(ПолученныйШтрихкод);
		КонецЦикла;
		Для Каждого ПолученныйШтрихкод Из Параметр.ЗарегистрированныеШтрихкоды Цикл
			ДанныеШтрихкодов.Добавить(ПолученныйШтрихкод);
		КонецЦикла;
		
		ОбработатьШтрихкоды(ДанныеШтрихкодов);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ИспользоватьХарактеристикиНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		НомерСтрокиДляПользователя = Товары.Индекс(СтрокаТЧ) + 1;
		Если Не ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не заполнено поле ""Номенклатура"" в строке %1 списка ""Товары""'"), НомерСтрокиДляПользователя);
			ПутьКСтрокеТабличнойЧасти = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", НомерСтрокиДляПользователя, "Номенклатура");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,ПутьКСтрокеТабличнойЧасти,,Отказ);
		КонецЕсли;
		Если ИспользоватьХарактеристикиНоменклатуры И Не ЗначениеЗаполнено(СтрокаТЧ.Характеристика) И СтрокаТЧ.ХарактеристикиИспользуются Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не заполнено поле ""Характеристика"" в строке %1 списка ""Товары""'"), НомерСтрокиДляПользователя);
			ПутьКСтрокеТабличнойЧасти = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", НомерСтрокиДляПользователя, "Характеристика");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,ПутьКСтрокеТабличнойЧасти,,Отказ);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ТабличнаяЧасть
	|ИЗ
	|	&ТабличнаяЧасть КАК ТабличнаяЧасть
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.Номенклатура.ТипНоменклатуры В (&ТипыНоменклатуры)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОшибкаПоТипуНоменклатуры,
	|	&УсловиеПоМаркируемойПродукции КАК ОшибкаПоМаркировке
	|ИЗ
	|	ТабличнаяЧасть КАК ТабличнаяЧасть");

	Если ВыгруженаТолькоНеМаркируемаяПродукция Тогда
		ОсобенностьУчета = НоменклатураЛокализация.ОсобенностьУчетаПоМаркируемойПродукции();
		
		Если ОсобенностьУчета.Количество() > 0 Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоМаркируемойПродукции", 
			"ВЫБОР
			|		КОГДА ТабличнаяЧасть.Номенклатура.ОсобенностьУчета В (&ОсобенностьУчета)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ");
	
			
			Запрос.УстановитьПараметр("ОсобенностьУчета", ОсобенностьУчета);
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоМаркируемойПродукции", "Ложь");
		КонецЕсли;
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоМаркируемойПродукции", "Ложь");
	КонецЕсли;
	
	ТабличнаяЧасть = Товары.Выгрузить(,"Номенклатура");
	ОбщегоНазначенияУТ.ПронумероватьТаблицуЗначений(ТабличнаяЧасть, "НомерСтроки"); //нумерация начинается с 0
	
	Запрос.УстановитьПараметр("ТипыНоменклатуры", ТипыНоменклатуры.ВыгрузитьЗначения());
	Запрос.УстановитьПараметр("ТабличнаяЧасть", ТабличнаяЧасть);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ПутьКСтрокеТабличнойЧасти = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", НомерСтрокиДляПользователя, "Номенклатура");
		
		Если Выборка.ОшибкаПоТипуНоменклатуры Тогда
			НомерСтрокиДляПользователя = Выборка.НомерСтроки + 1;
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Тип номенклатуры %1 в строке %2 списка ""Товары"" отличается от допустимых типов: %3'"),
				Выборка.ТипНоменклатуры, 
				НомерСтрокиДляПользователя,
				СтрСоединить(ТипыНоменклатуры.ВыгрузитьЗначения(),","));
				
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,ПутьКСтрокеТабличнойЧасти,,Отказ);
		КонецЕсли;
	
		Если Выборка.ОшибкаПоМаркировке Тогда
			НомерСтрокиДляПользователя = Выборка.НомерСтроки + 1;
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Номенклатура %1 в строке %2 списка ""Товары"" не может быть маркируемой.'"),
				Выборка.Номенклатура, 
				НомерСтрокиДляПользователя);
				
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,ПутьКСтрокеТабличнойЧасти,,Отказ);
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.Характеристика);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьУпаковкуПоВладельцу", ТекущаяСтрока.Упаковка);
	СтруктураДействий.Вставить("ЗаполнитьПризнакТипНоменклатуры", Новый Структура("Номенклатура", "ТипНоменклатуры"));
	ОбработкаТабличнойЧастиКлиентСерверЛокализация.ДополнитьСтруктуруДействийПриИзмененииЭлемента(ЭтотОбъект, "Номенклатура", СтруктураДействий);
	
	Если Не ЗначениеЗаполнено(ТекущаяСтрока.Склад)
		И НЕ СкладГруппа Тогда
		ТекущаяСтрока.Склад = Склад;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущаяСтрока.Склад)
		И ПараметрыУказанияСерий <> Неопределено Тогда
		СтруктураДействий.Вставить("ПроверитьСериюРассчитатьСтатус",
			Новый Структура("Склад, ПараметрыУказанияСерий", ТекущаяСтрока.Склад, ПараметрыУказанияСерий));
	Иначе
		ТекущаяСтрока.СтатусУказанияСерий = 0;
	КонецЕсли;
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

	ОчиститьСообщения();
	ТекущаяСтрокаДляПользователя = Элементы.Товары.ТекущаяСтрока + 1;
	НоменклатураПриИзмененииСервер(ТекущаяСтрокаДляПользователя, ТекущаяСтрока.Номенклатура);
	
КонецПроцедуры
	
&НаСервере
Процедура НоменклатураПриИзмененииСервер(ТекущийНомерСтроки, Номенклатура)
	Если ВыгруженаТолькоНеМаркируемаяПродукция Тогда
		Если НоменклатураЛокализация.ЭтоМаркируемаяПродукция(Номенклатура) Тогда
			
			СтандартнаяОбработка = Ложь;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Номенклатура %1 маркируемая. Можно выбрать только немаркируемую продукцию'"),
			Номенклатура);
			ПутьКСтрокеТабличнойЧасти = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", ТекущийНомерСтроки, "Номенклатура");
				
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,ПутьКСтрокеТабличнойЧасти);
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	//++ Локализация
	
	Если Не ВыгруженаТолькоНеМаркируемаяПродукция Тогда
		Возврат;
	КонецЕсли;

	СтандартнаяОбработка = Ложь;

	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("МаркируемаяПродукция", Ложь);

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВыборГруппИЭлементов",    ИспользованиеГруппИЭлементов.Элементы);
	ПараметрыФормы.Вставить("Отбор",                   ПараметрыОтбора);
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", ПараметрыФормы, Элемент);
	//-- Локализация
	Возврат;
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоРасхожденияПриИзменении(Элемент)
	УстановитьОтборПоРасхождениям();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	ЕстьРасхождение = ТекущиеДанные.ЕстьРасхождение;
	ТекущиеДанные.ЕстьРасхождение = ?(ТекущиеДанные.КоличествоУпаковок = ТекущиеДанные.КоличествоУпаковокФакт, 0, 1);
	
	Если ЕстьРасхождение <> ТекущиеДанные.ЕстьРасхождение И ТолькоРасхождения Тогда
		УстановитьОтборПоРасхождениям();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыСерияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьПодборСерий(Элемент.ТекстРедактирования);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСерияПриИзменении(Элемент)
	
	ВыбранноеЗначение = НоменклатураКлиентСервер.ВыбраннаяСерия();
	
	ВыбранноеЗначение.Значение            		 = Элементы.Товары.ТекущиеДанные.Серия;
	ВыбранноеЗначение.ИдентификаторТекущейСтроки = Элементы.Товары.ТекущиеДанные.ПолучитьИдентификатор();
	
	НоменклатураКлиент.ОбработатьУказаниеСерии(ЭтаФорма, ПараметрыУказанияСерий, ВыбранноеЗначение);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Отказ = Истина;
	ИдентификаторТекущейСтроки = ТекущиеДанные.ПолучитьИдентификатор();
	
	ОчиститьСообщения();
	
	УдаляемыеСтроки = Новый Массив;
	НеудаляемыеСтроки = Новый Массив;
	Для Каждого Идентификатор Из Элементы.Товары.ВыделенныеСтроки Цикл
		СтрокаТаблицы = Элементы.Товары.ДанныеСтроки(Идентификатор);
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.КоличествоУпаковок) Тогда
			УдаляемыеСтроки.Добавить(СтрокаТаблицы);
			Если Идентификатор = ИдентификаторТекущейСтроки Тогда
				
				// Текущая строка удаляется - нужно перепозиционироваться.
				ИдентификаторПредыдущейСтроки = Неопределено;
				НайденаТекущаяСтрока = Ложь;
				НайденаНоваяТекущаяСтрока = Ложь;
				Для Каждого СтрокаТЧ Из Товары Цикл
					Если СтрокаТЧ.ПолучитьИдентификатор() = ИдентификаторТекущейСтроки Тогда
						НайденаТекущаяСтрока = Истина;
					Иначе
						Если НайденаТекущаяСтрока Тогда
							Если УдаляемыеСтроки.Найти(СтрокаТЧ) = Неопределено Тогда
								Элементы.Товары.ТекущаяСтрока = СтрокаТЧ.ПолучитьИдентификатор();
								НайденаНоваяТекущаяСтрока = Истина;
								Прервать;
							КонецЕсли;
						Иначе
							Если УдаляемыеСтроки.Найти(СтрокаТЧ) = Неопределено Тогда
								ИдентификаторПредыдущейСтроки = СтрокаТЧ.ПолучитьИдентификатор();
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
				Если Не НайденаНоваяТекущаяСтрока Тогда
					Элементы.Товары.ТекущаяСтрока = ИдентификаторПредыдущейСтроки;
				КонецЕсли;
				
			КонецЕсли;
		Иначе
			НеудаляемыеСтроки.Добавить(СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из УдаляемыеСтроки Цикл
		Товары.Удалить(СтрокаТЧ);
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из НеудаляемыеСтроки Цикл
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Строку %1 удалить нельзя, т.к. она присутствует в исходном документе.'"), Товары.Индекс(СтрокаТЧ) + 1);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"Товары",,Отказ);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Копирование Тогда
		
		Отказ = Истина;
		
		ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
		
		НоваяСтрока = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные);
		НоваяСтрока.КоличествоУпаковок     = 0;
		НоваяСтрока.КоличествоУпаковокФакт = 0;
		НоваяСтрока.КоличествоУпаковокФактИтог = 0;
		НоваяСтрока.Расхождение                = 0;
		
		Элементы.Товары.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоУпаковокФактПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;

	СтруктураДействий = Новый Структура;
	ДобавитьВСтруктуруДействияПриИзмененииКоличестваУпаковок(СтруктураДействий);
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока,СтруктураДействий,КэшированныеЗначения);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("КэшированныеЗначения", КэшированныеЗначения);
	СобытияФормКлиент.ПриИзмененииЭлемента(ЭтотОбъект, Элемент, ДополнительныеПараметры);
	КэшированныеЗначения = ДополнительныеПараметры.КэшированныеЗначения;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыВесПриИзменении(Элемент)
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковокПоВесу", "Факт");
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыОбъемПриИзменении(Элемент)
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковокПоОбъему", "Факт");
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьПроверку(Команда)
	
	ОбработкаЗакрытия = Ложь;
	
	ТоварыОтвязаноОтНабора.Очистить();
	
	АнализНаборов();
	
	ОчиститьСообщения();
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	МассивСтрокНовыеТовары = Новый Массив;
	МассивСтрокНедостачи   = Новый Массив;
	МассивСтрокИзлишки     = Новый Массив;
	Для Каждого СтрокаТЧ Из Товары Цикл
		Если СтрокаТЧ.КоличествоУпаковок = СтрокаТЧ.КоличествоУпаковокФактИтог Тогда
			// Нет расхождений
		ИначеЕсли СтрокаТЧ.КоличествоУпаковок = 0 И СтрокаТЧ.КоличествоУпаковокФактИтог > 0 Тогда
			// Новый товар
			МассивСтрокНовыеТовары.Добавить(СтрокаТЧ);
			
		ИначеЕсли СтрокаТЧ.КоличествоУпаковок > СтрокаТЧ.КоличествоУпаковокФактИтог Тогда
			// Недостача
			МассивСтрокНедостачи.Добавить(СтрокаТЧ);
			
		ИначеЕсли СтрокаТЧ.КоличествоУпаковок < СтрокаТЧ.КоличествоУпаковокФактИтог Тогда
			// Излишек
			МассивСтрокИзлишки.Добавить(СтрокаТЧ);
			
		КонецЕсли;
	КонецЦикла;
	
	РазрешеноЗавершитьПроверку = Истина;
	Если    МассивСтрокНовыеТовары.Количество() > 0
		ИЛИ МассивСтрокНедостачи.Количество()   > 0
		ИЛИ МассивСтрокИзлишки.Количество()     > 0
		ИЛИ ТоварыОтвязаноОтНабора.Количество() > 0 Тогда
		
		Если ОтклонениеЗапрещено Тогда
			РазрешеноЗавершитьПроверку = Ложь;
		КонецЕсли;
		
		СписокВопросов = Новый СписокЗначений();
		СписокВопросов.Добавить("Перенести", НСтр("ru = 'Перенести'"));
		СписокВопросов.Добавить("НеПереносить", НСтр("ru = 'Не переносить'"));
		СписокВопросов.Добавить("ВернутьсяКПроверке", НСтр("ru = 'Вернуться к проверке'"));
		
		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения("ОбработкаОтветаНаВопрос",
		                                                    ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Обнаружены расхождения между количеством товаров в документе и результатом проверки. 
									|Перенести фактические данные в документ?'");
		ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, СписокВопросов);

	Иначе
		
		ОбработкаЗакрытия = Истина;
		
		ДополнительныеПараметры = Новый Структура("ТребуетсяСерверныйВызов, РезультатЗакрытия", Ложь, Неопределено);
		СобытияФормКлиент.ПриИзмененииЭлемента(ЭтотОбъект, "РезультатЗакрытия", ДополнительныеПараметры);
		Если ДополнительныеПараметры.ТребуетсяСерверныйВызов Тогда
			ПолучитьРезультатЗакрытияНаСервере(ДополнительныеПараметры);
		КонецЕсли;
		
		Закрыть(ДополнительныеПараметры.РезультатЗакрытия);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьРезультатЗакрытияНаСервере(ДополнительныеПараметры)
	
	СобытияФорм.ПриИзмененииЭлемента(ЭтотОбъект, "РезультатЗакрытия", ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВес(Команда)
	
	ТекущаяСтрока = МенеджерОборудованияУТКлиент.ТекущаяСтрока(ЭтаФорма);
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерОборудованияКлиент.НачатьПолученияВесаСЭлектронныхВесов(
		Новый ОписаниеОповещения("ПолучитьВесЗавершение", ЭтотОбъект, ТекущаяСтрока),
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВесЗавершение(РезультатВыполнения, ТекущаяСтрока) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		ТекущаяСтрока.КоличествоУпаковокФакт = РезультатВыполнения.Вес;
	Иначе
		МенеджерОборудованияУТКлиент.СообщитьОбОшибке(РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеИзТСД(Команда)
	
	ОчиститьСообщения();
	
	МенеджерОборудованияКлиент.НачатьЗагрузкуДанныеИзТСД(
		Новый ОписаниеОповещения("ЗагрузитьИзТСДЗавершение", ЭтотОбъект),
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзТСДЗавершение(Результат, Параметры) Экспорт
	
	Если Результат.Результат Тогда
		ОбработатьШтрихкоды(Результат.ТаблицаТоваров);
	Иначе
		МенеджерОборудованияУТКлиент.СообщитьОбОшибке(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ОбработкаЗакрытия = Истина;
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкодуВыполнить(Команда)
	
	ОчиститьСообщения();
	Оповещение = Новый ОписаниеОповещения("ПоискПоШтрихкодуЗавершение", ЭтотОбъект);
	ШтрихкодированиеНоменклатурыКлиент.ПоказатьВводШтрихкода(Оповещение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	ПараметрыУсловногоОформления = НоменклатураСервер.НовыеПараметрыУсловногоОформленияЕдиницИзмерения();
	ПараметрыУсловногоОформления.ИмяПоляЕдиницаИзмерения = "ЕдиницаИзмерения";
	ПараметрыУсловногоОформления.ПутьКПолюУпаковка = "Товары.Упаковка";
	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтаФорма, ПараметрыУсловногоОформления);

	//

	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма, 
																			 "Характеристика",
																		     "Товары.ХарактеристикиИспользуются");

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыКоличествоУпаковокФакт.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Товары.КоличествоУпаковок");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("Товары.КоличествоУпаковокФакт");

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Green);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыКоличествоУпаковокФакт.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Товары.КоличествоУпаковок");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("Товары.КоличествоУпаковокФакт");

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Red);

	//

	НоменклатураСервер.УстановитьУсловноеОформлениеСерийНоменклатуры(ЭтаФорма, Ложь,, 
																     "Товары.СтатусУказанияСерий", 
																     "Товары.ТипНоменклатуры");

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Номенклатура.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Характеристика.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Упаковка.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЕдиницаИзмерения.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыКоличествоУпаковокВДокументе.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыСерия.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Товары.КоличествоУпаковок");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше;
	ОтборЭлемента.ПравоеЗначение = 0;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыВес.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Товары.ВесУпаковки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не используется>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыОбъем.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Товары.ОбъемУпаковки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не используется>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыЕдиницаИзмеренияВеса.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Товары.ВесУпаковки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыЕдиницаИзмеренияОбъема.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Товары.ОбъемУпаковки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);

КонецПроцедуры

#Область Серии

&НаКлиенте
Процедура ОткрытьПодборСерий(Текст = "", ТекущиеДанные = Неопределено)

	ТекущиеДанныеСтруктурой = Новый Структура;
	
	Если ТекущиеДанные = Неопределено Тогда
		ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	КонецЕсли;
	
	Для Каждого КлючЗначение Из РаботаСТабличнымиЧастямиКлиентСервер.КэшСтроки(Элементы.Товары, ЭтотОбъект) Цикл
		Если КлючЗначение.Ключ = "КоличествоУпаковокФакт" Тогда
			ТекущиеДанныеСтруктурой.Вставить(ПараметрыУказанияСерий.ИмяПоляКоличество, ТекущиеДанные[КлючЗначение.Ключ]);
		Иначе	
			ТекущиеДанныеСтруктурой.Вставить(КлючЗначение.Ключ, ТекущиеДанные[КлючЗначение.Ключ]);
		КонецЕсли;
	КонецЦикла;
	ТекущиеДанныеСтруктурой.Вставить("ИдентификаторТекущейСтроки", ТекущиеДанные.ПолучитьИдентификатор());
	
	Если НоменклатураКлиент.ДляУказанияСерийНуженСерверныйВызов(ЭтаФорма,ПараметрыУказанияСерий,Текст,ТекущиеДанныеСтруктурой, Ложь) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Штрихкоды

&НаКлиенте
Процедура ОбработатьШтрихкоды(ДанныеШтрихкодов)
	
	СтруктураДействийСДобавленнымиСтроками = Новый Структура;
	СтруктураДействийСДобавленнымиСтроками.Вставить("ЗаполнитьПризнакТипНоменклатуры",
		Новый Структура("Номенклатура", "ТипНоменклатуры"));
	СтруктураДействийСДобавленнымиСтроками.Вставить("ПроверитьЗаполнитьСклад",
			Новый Структура("СкладПоУмолчанию, СкладГруппа", Склад, СкладГруппа));
	ОбработкаТабличнойЧастиКлиентСерверЛокализация.ДополнитьСтруктуруДействийПриИзмененииЭлемента(ЭтотОбъект, "Номенклатура", СтруктураДействийСДобавленнымиСтроками);
	
	СтруктураДействийСИзмененнымиСтроками = Новый Структура;
	Если ПараметрыУказанияСерий <> Неопределено Тогда
		СтруктураДляРасчетаСерий = Новый Структура("Склад,ПараметрыУказанияСерий",
				?(СкладГруппа,ПредопределенноеЗначение("Справочник.Склады.ПустаяСсылка"),Склад),
				ПараметрыУказанияСерий);
		СтруктураДействийСДобавленнымиСтроками.Вставить("ПроверитьСериюРассчитатьСтатус",СтруктураДляРасчетаСерий);
		СтруктураДействийСИзмененнымиСтроками.Вставить("ПроверитьСериюРассчитатьСтатус",СтруктураДляРасчетаСерий);
	КонецЕсли;
	ОбработкаТабличнойЧастиКлиентСерверЛокализация.ДополнитьСтруктуруДействийПриИзмененииЭлемента(ЭтотОбъект, "НоменклатураИзменение", СтруктураДействийСИзмененнымиСтроками);
	
	СтруктураДействий = ШтрихкодированиеНоменклатурыКлиент.ПараметрыОбработкиШтрихкодов();
	СтруктураДействий.Штрихкоды                              = ДанныеШтрихкодов;
	СтруктураДействий.СтруктураДействийСДобавленнымиСтроками = СтруктураДействийСДобавленнымиСтроками;
	СтруктураДействий.СтруктураДействийСИзмененнымиСтроками  = СтруктураДействийСИзмененнымиСтроками;
	СтруктураДействий.ПараметрыУказанияСерий                 = ПараметрыУказанияСерий;
	СтруктураДействий.ИмяКолонкиКоличество                   = "КоличествоУпаковокФакт";
	СтруктураДействий.НеИспользоватьУпаковки                 = НеИспользоватьУпаковки;
	СтруктураДействий.БлокироватьДанныеФормы                 = Ложь;
	ОбработкаТабличнойЧастиКлиентСерверЛокализация.ДополнитьСтруктуруДействийПриИзмененииЭлемента(ЭтотОбъект, "ОбработатьШтрихкоды", СтруктураДействий);
	СтруктураДействий.ШтрихкодыВТЧ                           = Истина;
	
	ОбработатьШтрихкодыСервер(СтруктураДействий,КэшированныеЗначения);
	
	ДанныеДляОбработки = СтруктураДействий;
	Подключаемый_ПослеОбработкиШтрихкодов();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьШтрихкодыСервер(СтруктураПараметровДействия,КэшированныеЗначения)
	
	ШтрихкодированиеНоменклатурыСервер.ОбработатьШтрихкоды(ЭтаФорма,ЭтаФорма,СтруктураПараметровДействия,КэшированныеЗначения);
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		СтрокаТЧ.ЕстьРасхождение = ?(СтрокаТЧ.КоличествоУпаковок = СтрокаТЧ.КоличествоУпаковокФакт, 0, 1);
	КонецЦикла;
	
	УстановитьОтборПоРасхождениям();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкодуЗавершение(ДанныеШтрихкода, ДополнительныеПараметры) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СтандартнаяОбработка",    Истина);
	ДополнительныеПараметры.Вставить("ДанныеШтрихкода",         ДанныеШтрихкода);
	ДополнительныеПараметры.Вставить("КэшированныеЗначения",    КэшированныеЗначения);
	ДополнительныеПараметры.Вставить("ТребуетсяСерверныйВызов", Ложь);
	
	СобытияФормКлиент.ПриИзмененииЭлемента(ЭтотОбъект, "ТоварыПоискПоШтрихкоду", ДополнительныеПараметры);
	
	Если ДополнительныеПараметры.ТребуетсяСерверныйВызов Тогда
		ПоискПоШтрихкодуЗавершениеНаСервере(ДополнительныеПараметры);
		СобытияФормКлиент.ПриИзмененииЭлемента(ЭтотОбъект, "ЗавершитьОбработкуШтрихкода", ДополнительныеПараметры);
	КонецЕсли;
	
	Если Не ДополнительныеПараметры.СтандартнаяОбработка Тогда
		Возврат;
	
	КонецЕсли;
	
	ДанныеШтрихкодов = Новый Массив;
	ДанныеШтрихкодов.Добавить(ДанныеШтрихкода);
	ОбработатьШтрихкоды(ДанныеШтрихкодов);
	
КонецПроцедуры

&НаСервере
Процедура ПоискПоШтрихкодуЗавершениеНаСервере(ДополнительныеПараметры)
	
	СобытияФорм.ПриИзмененииЭлемента(ЭтотОбъект, "ОбработатьДанныеШтрихкода", ДополнительныеПараметры);
	ПослеОбработкиШтрихкодов(ДополнительныеПараметры);
	ДополнительныеПараметры.РезультатОбработкиШтрихкода.ДобавленныеСтроки = Новый Массив;
	ДополнительныеПараметры.РезультатОбработкиШтрихкода.ИзмененныеСтроки  = Новый Массив;
	
КонецПроцедуры

&НаСервере
Функция ОбработкаКодаМаркировкиВыполнитьДействиеСервер(Действие, РезультатВыбора, РезультатОбработкиШтрихкода, ПараметрыСканирования, КэшированныеЗначения)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Действие",                    Действие);
	ДополнительныеПараметры.Вставить("РезультатВыбора",             РезультатВыбора);
	ДополнительныеПараметры.Вставить("РезультатОбработкиШтрихкода", РезультатОбработкиШтрихкода);
	ДополнительныеПараметры.Вставить("ПараметрыСканирования",       ПараметрыСканирования);
	ДополнительныеПараметры.Вставить("КэшированныеЗначения",        КэшированныеЗначения);
	
	СобытияФорм.ПриИзмененииЭлемента(ЭтотОбъект, "ОбработкаКодаМаркировкиВыполнитьДействиеСервер", ДополнительныеПараметры);
	
	ПослеОбработкиШтрихкодов(ДополнительныеПараметры);
	
	ДополнительныеПараметры.РезультатОбработкиШтрихкода.ИзмененныеСтроки  = Новый Массив;
	ДополнительныеПараметры.РезультатОбработкиШтрихкода.ДобавленныеСтроки = Новый Массив;
	
	Возврат ДополнительныеПараметры.РезультатОбработкиШтрихкода;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ОткрытьФормуУточненияДанных()
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДанныеДляОбработки", ДанныеДляОбработки());
	
	СобытияФормКлиент.ПриИзмененииЭлемента(ЭтотОбъект, "Подключаемый_ОткрытьФормуУточненияДанных", ДополнительныеПараметры);
	
КонецПроцедуры

//++ Локализация

// Параметры:
// 	ДанныеДляВыполненияДействия - см. ШтрихкодированиеИСКлиент.ИнициализацияРезультатаУточненияДанныхДляВыполненияДальнейшихДействий
// 	ДополнительныеПараметры - см. СобытияФормИСКлиентПереопределяемый.ПриИзмененииЭлемента.ДополнительныеПараметры
//
&НаКлиенте
Процедура ОбработкаКодаМаркировкиВыполнитьДействие(ДанныеДляВыполненияДействия, ДополнительныеПараметры) Экспорт
	
	РезультатВыбора             = ДанныеДляВыполненияДействия.РезультатВыбора;
	РезультатОбработкиШтрихкода = ДанныеДляВыполненияДействия.РезультатОбработкиШтрихкода;
	КэшированныеЗначения        = ДанныеДляВыполненияДействия.КэшированныеЗначения;
	ПараметрыСканирования       = ДанныеДляВыполненияДействия.ПараметрыСканирования;
	Действие                    = ДанныеДляВыполненияДействия.Действие;
	
	ДанныеДляВыполненияДействия.РезультатОбработкиШтрихкода = ОбработкаКодаМаркировкиВыполнитьДействиеСервер(
		Действие, РезультатВыбора, РезультатОбработкиШтрихкода, ПараметрыСканирования, КэшированныеЗначения);
	
	СобытияФормКлиент.ПриИзмененииЭлемента(ЭтотОбъект, "ЗавершитьОбработкуШтрихкода", ДанныеДляВыполненияДействия);
	
КонецПроцедуры

//-- Локализация
&НаКлиенте
Процедура Подключаемый_ПослеОбработкиШтрихкодов()
	
	ШтрихкодированиеНоменклатурыКлиент.ОбработатьНеизвестныеШтрихкоды(ДанныеДляОбработки, КэшированныеЗначения, ЭтотОбъект);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("КэшированныеЗначения", КэшированныеЗначения);
	ДополнительныеПараметры.Вставить("ДанныеДляОбработки", ДанныеДляОбработки);
	ДополнительныеПараметры.Вставить("СтандартнаяОбработка", Истина);
	СобытияФормКлиент.ПриИзмененииЭлемента(ЭтотОбъект, "ПослеОбработкиШтрихкодов", ДополнительныеПараметры);
	КэшированныеЗначения = ДополнительныеПараметры.КэшированныеЗначения;
	
	Если Не ДополнительныеПараметры.СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	НужноОткрытьФормуУказанияСерийПослеОбработкиШтрихкодов = ШтрихкодированиеНоменклатурыКлиент.НужноОткрытьФормуУказанияСерийПослеОбработкиШтрихкодов(ДанныеДляОбработки);
	Если НужноОткрытьФормуУказанияСерийПослеОбработкиШтрихкодов Тогда
		
		Если ТекущиеДанныеИдентификатор <> Неопределено Тогда
			
			ТекущиеДанные = Товары.НайтиПоИдентификатору(ТекущиеДанныеИдентификатор);
			ОткрытьПодборСерий(,
				ТекущиеДанные);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДанныеДляОбработкиПер = ДанныеДляОбработки;// см. ШтрихкодированиеНоменклатурыКлиентСервер.ПараметрыОбработкиШтрихкодов
	Если ДанныеДляОбработкиПер.ТекущаяСтрока <> Неопределено Тогда
		Элементы.Товары.ТекущаяСтрока = ДанныеДляОбработкиПер.ТекущаяСтрока;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ОбработатьСтрокиТЧ(ДобавленныеСтроки, ИзмененныеСтроки, КэшированныеЗначения = Неопределено) Экспорт
	
	СтруктураДействийСДобавленнымиСтроками = Новый Структура;
	СтруктураДействийСДобавленнымиСтроками.Вставить("ЗаполнитьПризнакТипНоменклатуры", Новый Структура("Номенклатура", "ТипНоменклатуры"));
	СтруктураДействийСДобавленнымиСтроками.Вставить("ПересчитатьКоличествоУпаковокСуффикс", "Факт");
	ОбработкаТабличнойЧастиКлиентСерверЛокализация.ДополнитьСтруктуруДействийПриИзмененииЭлемента(ЭтотОбъект, "Номенклатура", СтруктураДействийСДобавленнымиСтроками);
	
	Для Каждого СтрокаТЧ Из ДобавленныеСтроки Цикл
		
		СтруктураДействийСДобавленнымиСтроками.Вставить("ПроверитьХарактеристикуПоВладельцу", СтрокаТЧ.Характеристика);
		СтруктураДействийСДобавленнымиСтроками.Вставить("ПроверитьЗаполнитьУпаковкуПоВладельцу", СтрокаТЧ.Упаковка);
		
		Если ЗначениеЗаполнено(СтрокаТЧ.Склад)
			И ПараметрыУказанияСерий <> Неопределено Тогда
			СтруктураДействийСДобавленнымиСтроками.Вставить("ПроверитьСериюРассчитатьСтатус",
				Новый Структура("Склад, ПараметрыУказанияСерий", СтрокаТЧ.Склад, ПараметрыУказанияСерий));
		КонецЕсли;
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТЧ, СтруктураДействийСДобавленнымиСтроками, КэшированныеЗначения);
		
		СтрокаТЧ.ЕстьРасхождение = 1;
		
	КонецЦикла;
	
	СтруктураДействийСИзмененнымиСтроками = Новый Структура;
	СтруктураДействийСИзмененнымиСтроками.Вставить("ПересчитатьКоличествоУпаковокСуффикс", "Факт");
	
	Для Каждого СтрокаТЧ Из ИзмененныеСтроки Цикл
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТЧ, СтруктураДействийСИзмененнымиСтроками, КэшированныеЗначения);
		
		СтрокаТЧ.ЕстьРасхождение = ?(СтрокаТЧ.КоличествоУпаковок = СтрокаТЧ.КоличествоУпаковокФакт, 0, 1);
		
	КонецЦикла;
	
	Если ДобавленныеСтроки.Количество() > 0 Тогда
		Элементы.Товары.ТекущаяСтрока = ДобавленныеСтроки[ДобавленныеСтроки.Количество() - 1].ПолучитьИдентификатор();
	ИначеЕсли ИзмененныеСтроки.Количество() > 0 Тогда
		Элементы.Товары.ТекущаяСтрока = ИзмененныеСтроки[ИзмененныеСтроки.Количество() - 1].ПолучитьИдентификатор();
	КонецЕсли;
	
	УстановитьОтборПоРасхождениям();
	
КонецПроцедуры

&НаСервере
Процедура ПослеОбработкиШтрихкодов(ДополнительныеПараметры)
	
	РезультатОбработкиШтрихкода = ДополнительныеПараметры.РезультатОбработкиШтрихкода;
	КэшированныеЗначения        = ДополнительныеПараметры.КэшированныеЗначения;
	
	Если РезультатОбработкиШтрихкода.ТребуетсяОбработкаШтрихкода Тогда
		
		Если ТипЗнч(РезультатОбработкиШтрихкода.ИсходныеДанные) = Тип("Структура") Тогда
			ДанныеШтрихкодов = Новый Массив;
			ДанныеШтрихкодов.Добавить(РезультатОбработкиШтрихкода.ИсходныеДанные);
		Иначе
			ДанныеШтрихкодов = РезультатОбработкиШтрихкода.ИсходныеДанные;
		КонецЕсли;
		
		СтруктураДействийСДобавленнымиСтроками = Новый Структура;
		СтруктураДействийСДобавленнымиСтроками.Вставить("ЗаполнитьПризнакТипНоменклатуры",
			Новый Структура("Номенклатура", "ТипНоменклатуры"));
		СтруктураДействийСДобавленнымиСтроками.Вставить("ПроверитьЗаполнитьСклад",
				Новый Структура("СкладПоУмолчанию, СкладГруппа", Склад, СкладГруппа));
		ОбработкаТабличнойЧастиКлиентСерверЛокализация.ДополнитьСтруктуруДействийПриИзмененииЭлемента(ЭтотОбъект, "Номенклатура", СтруктураДействийСДобавленнымиСтроками);
		
		СтруктураДействийСИзмененнымиСтроками = Новый Структура;
		Если ПараметрыУказанияСерий <> Неопределено Тогда
			СтруктураДляРасчетаСерий = Новый Структура("Склад,ПараметрыУказанияСерий",
					?(СкладГруппа,ПредопределенноеЗначение("Справочник.Склады.ПустаяСсылка"),Склад),
					ПараметрыУказанияСерий);
			СтруктураДействийСДобавленнымиСтроками.Вставить("ПроверитьСериюРассчитатьСтатус",СтруктураДляРасчетаСерий);
			СтруктураДействийСИзмененнымиСтроками.Вставить("ПроверитьСериюРассчитатьСтатус",СтруктураДляРасчетаСерий);
		КонецЕсли;
		ОбработкаТабличнойЧастиКлиентСерверЛокализация.ДополнитьСтруктуруДействийПриИзмененииЭлемента(ЭтотОбъект, "НоменклатураИзменение", СтруктураДействийСИзмененнымиСтроками);
		
		ДанныеДляОбработки = ШтрихкодированиеНоменклатурыКлиентСервер.ПараметрыОбработкиШтрихкодов();
		ДанныеДляОбработки.Штрихкоды                              = ДанныеШтрихкодов;
		ДанныеДляОбработки.СтруктураДействийСДобавленнымиСтроками = СтруктураДействийСДобавленнымиСтроками;
		ДанныеДляОбработки.СтруктураДействийСИзмененнымиСтроками  = СтруктураДействийСИзмененнымиСтроками;
		ДанныеДляОбработки.ПараметрыУказанияСерий                 = ПараметрыУказанияСерий;
		ДанныеДляОбработки.ИмяКолонкиКоличество                   = "КоличествоУпаковокФакт";
		ДанныеДляОбработки.НеИспользоватьУпаковки                 = НеИспользоватьУпаковки;
		ДанныеДляОбработки.БлокироватьДанныеФормы                 = Ложь;
		ДанныеДляОбработки.ШтрихкодыВТЧ                           = Истина;
		ОбработкаТабличнойЧастиКлиентСерверЛокализация.ДополнитьСтруктуруДействийПриИзмененииЭлемента(ЭтотОбъект, "ОбработатьШтрихкоды", ДанныеДляОбработки);
		
		ОбработатьШтрихкодыСервер(ДанныеДляОбработки,КэшированныеЗначения);
			
	Иначе
		
		Подключаемый_ОбработатьСтрокиТЧ(
			РезультатОбработкиШтрихкода.ДобавленныеСтроки,
			РезультатОбработкиШтрихкода.ИзмененныеСтроки,
			КэшированныеЗначения);
		
	КонецЕсли;
	
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре();
	
КонецПроцедуры

// Возвращаемое значение:
// 	см. ШтрихкодированиеНоменклатурыКлиентСервер.ПараметрыОбработкиШтрихкодов
&НаКлиенте
Функция ДанныеДляОбработки()
	
	Возврат ДанныеДляОбработки;
	
КонецФункции

#КонецОбласти

#Область Прочее

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	ОтветНаВопрос = РезультатВопроса;
	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		Отмена(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция Анализ()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.КодСтроки              КАК КодСтроки,
	|	Таблица.ИдентификаторСтроки    КАК ИдентификаторСтроки,
	|	Таблица.ЗаказКлиента           КАК ЗаказКлиента,
	|	Таблица.НоменклатураНабора     КАК НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора   КАК ХарактеристикаНабора,
	|	Таблица.Номенклатура           КАК Номенклатура,
	|	Таблица.Характеристика         КАК Характеристика,
	|	Таблица.Упаковка               КАК Упаковка,
	|	Таблица.КоличествоУпаковокФакт КАК КоличествоУпаковокФакт,
	|	Таблица.КоличествоУпаковок     КАК КоличествоУпаковок
	|ПОМЕСТИТЬ _СоставНабора
	|ИЗ
	|	&Таблица КАК Таблица
	|ГДЕ Таблица.КодСтроки = 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВариантыКомплектацииНоменклатуры.Ссылка КАК ВариантКомплектацииНоменклатуры,
	|	ВариантыКомплектацииНоменклатуры.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	Таблица.КодСтроки            КАК КодСтроки,
	|	Таблица.ИдентификаторСтроки  КАК ИдентификаторСтроки,
	|	Таблица.ЗаказКлиента         КАК ЗаказКлиента,
	|	Таблица.НоменклатураНабора   КАК НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	Таблица.Номенклатура         КАК Номенклатура,
	|	Таблица.Характеристика       КАК Характеристика,
	|	Таблица.Упаковка             КАК Упаковка,
	|	Таблица.КоличествоУпаковокФакт   КАК КоличествоУпаковокФакт,
	|	Таблица.КоличествоУпаковок       КАК КоличествоУпаковок
	|ПОМЕСТИТЬ СоставНабора
	|ИЗ
	|	_СоставНабора КАК Таблица
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
	|		ПО ВариантыКомплектацииНоменклатуры.Владелец = Таблица.НоменклатураНабора
	|		И ВариантыКомплектацииНоменклатуры.Характеристика = Таблица.ХарактеристикаНабора
	|		И ВариантыКомплектацииНоменклатуры.Основной
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка                          КАК ВариантКомплектацииНоменклатуры,
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	0                                                            КАК КодСтроки,
	|	""""                                                         КАК ИдентификаторСтроки,
	|	Неопределено                                                 КАК ЗаказКлиента,
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка.Владелец       КАК НоменклатураНабора,
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка.Характеристика КАК ХарактеристикаНабора,
	|	ВариантыКомплектацииНоменклатурыТовары.Номенклатура          КАК Номенклатура,
	|	ВариантыКомплектацииНоменклатурыТовары.Характеристика        КАК Характеристика,
	|	ВариантыКомплектацииНоменклатурыТовары.Упаковка                  КАК Упаковка,
	|	СУММА(ВариантыКомплектацииНоменклатурыТовары.КоличествоУпаковок) КАК КоличествоУпаковокПоУмолчанию,
	|	СУММА(0) КАК КоличествоУпаковокФакт,
	|	СУММА(0) КАК КоличествоУпаковок
	|ПОМЕСТИТЬ Таблица
	|ИЗ
	|	Справочник.ВариантыКомплектацииНоменклатуры.Товары КАК ВариантыКомплектацииНоменклатурыТовары
	|ГДЕ
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка В (ВЫБРАТЬ РАЗЛИЧНЫЕ Т.ВариантКомплектацииНоменклатуры ИЗ СоставНабора КАК Т)
	|	И (ВариантыКомплектацииНоменклатурыТовары.Ссылка.ВариантРасчетаЦеныНабора В (ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ЦенаЗадаетсяЗаНаборРаспределяетсяПоЦенам),
	|                                                                               ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ЦенаЗадаетсяЗаНаборРаспределяетсяПоДолям))
	|	ИЛИ (ВариантыКомплектацииНоменклатурыТовары.Ссылка.НоменклатураОсновногоКомпонента = ВариантыКомплектацииНоменклатурыТовары.Номенклатура И ВариантыКомплектацииНоменклатурыТовары.Ссылка.ХарактеристикаОсновногоКомпонента = ВариантыКомплектацииНоменклатурыТовары.Характеристика))
	|
	|СГРУППИРОВАТЬ ПО
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка,
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка.ВариантРасчетаЦеныНабора,
	|	0,
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка.Владелец,
	|	ВариантыКомплектацииНоменклатурыТовары.Ссылка.Характеристика,
	|	ВариантыКомплектацииНоменклатурыТовары.Номенклатура,
	|	ВариантыКомплектацииНоменклатурыТовары.Характеристика,
	|	ВариантыКомплектацииНоменклатурыТовары.Упаковка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.ВариантКомплектацииНоменклатуры,
	|	Таблица.ВариантРасчетаЦеныНабора,
	|	Таблица.КодСтроки,
	|	Таблица.ИдентификаторСтроки,
	|	Таблица.ЗаказКлиента,
	|	Таблица.НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	Таблица.Упаковка,
	|	0,
	|	Таблица.КоличествоУпаковокФакт,
	|	Таблица.КоличествоУпаковок
	|ИЗ
	|	СоставНабора КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.ВариантРасчетаЦеныНабора,
	|	Таблица.КодСтроки,
	|	Таблица.ИдентификаторСтроки,
	|	Таблица.ЗаказКлиента,
	|	Таблица.НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	Таблица.Упаковка,
	|	СУММА(Таблица.КоличествоУпаковокПоУмолчанию) КАК КоличествоУпаковокПоУмолчанию,
	|	СУММА(Таблица.КоличествоУпаковокФакт) КАК КоличествоУпаковокФакт,
	|	СУММА(Таблица.КоличествоУпаковок) КАК КоличествоУпаковок
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	Таблица КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.ВариантРасчетаЦеныНабора,
	|	Таблица.КодСтроки,
	|	Таблица.ИдентификаторСтроки,
	|	Таблица.ЗаказКлиента,
	|	Таблица.НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	Таблица.Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Результат.КодСтроки,
	|	Результат.ИдентификаторСтроки,
	|	Результат.ЗаказКлиента,
	|	Результат.ВариантРасчетаЦеныНабора,
	|	Результат.НоменклатураНабора,
	|	Результат.ХарактеристикаНабора,
	|	ВЫРАЗИТЬ(МИНИМУМ(ВЫБОР
	|			КОГДА Результат.КоличествоУпаковокПоУмолчанию <> 0
	|				ТОГДА Результат.КоличествоУпаковок / Результат.КоличествоУпаковокПоУмолчанию
	|			ИНАЧЕ null
	|		КОНЕЦ) + 0.5 КАК Число(10,0)) - 1 КАК КоличествоМинимум,
	|	ВЫРАЗИТЬ(Минимум(ВЫБОР
	|			КОГДА Результат.КоличествоУпаковокПоУмолчанию <> 0
	|				ТОГДА Результат.КоличествоУпаковокФакт / Результат.КоличествоУпаковокПоУмолчанию
	|			ИНАЧЕ null
	|		КОНЕЦ) + 0.5 КАК Число(10,0)) - 1 КАК КоличествоМинимумФакт
	|ПОМЕСТИТЬ Коэффициенты
	|ИЗ
	|	Результат КАК Результат
	|СГРУППИРОВАТЬ ПО
	|	Результат.КодСтроки,
	|	Результат.ИдентификаторСтроки,
	|	Результат.ЗаказКлиента,
	|	Результат.ВариантРасчетаЦеныНабора,
	|	Результат.НоменклатураНабора,
	|	Результат.ХарактеристикаНабора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.ВариантРасчетаЦеныНабора,
	|	Таблица.КодСтроки,
	|	Таблица.ИдентификаторСтроки,
	|	Таблица.ЗаказКлиента,
	|	Таблица.НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	Таблица.Упаковка,
	|	Таблица.КоличествоУпаковокПоУмолчанию,
	|	Таблица.КоличествоУпаковок,
	|	ЕСТЬNULL(Коэффициенты.КоличествоМинимум, 1) * Таблица.КоличествоУпаковокПоУмолчанию КАК КоличествоУпаковокПолныйКомплект,
	|	ЕСТЬNULL(Коэффициенты.КоличествоМинимумФакт, 1) * Таблица.КоличествоУпаковокПоУмолчанию КАК КоличествоУпаковокПолныйКомплектФакт,
	|	Таблица.КоличествоУпаковокФакт - ЕСТЬNULL(Коэффициенты.КоличествоМинимумФакт, 1) * Таблица.КоличествоУпаковокПоУмолчанию КАК Разница
	|ИЗ
	|	Результат КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Коэффициенты КАК Коэффициенты
	|		ПО Коэффициенты.НоменклатураНабора = Таблица.НоменклатураНабора
	|		 И Коэффициенты.ХарактеристикаНабора = Таблица.ХарактеристикаНабора
	|		 И Коэффициенты.КодСтроки = Таблица.КодСтроки
	|		 И Коэффициенты.ИдентификаторСтроки = Таблица.ИдентификаторСтроки
	|		 И Коэффициенты.ЗаказКлиента = Таблица.ЗаказКлиента
	|ГДЕ
	|	Таблица.ВариантРасчетаЦеныНабора В (ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ЦенаЗадаетсяЗаНаборРаспределяетсяПоЦенам),
	|                                       ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ЦенаЗадаетсяЗаНаборРаспределяетсяПоДолям))
	|	ИЛИ Коэффициенты.КоличествоМинимумФакт = 0 
	|;
	|ВЫБРАТЬ
	|	Коэффициенты.КодСтроки,
	|	Коэффициенты.ИдентификаторСтроки,
	|	Коэффициенты.ЗаказКлиента,
	|	Коэффициенты.ВариантРасчетаЦеныНабора,
	|	Коэффициенты.НоменклатураНабора,
	|	Коэффициенты.ХарактеристикаНабора,
	|	Коэффициенты.КоличествоМинимум,
	|	Коэффициенты.КоличествоМинимумФакт
	|ИЗ
	|	Коэффициенты КАК Коэффициенты
	|ГДЕ
	|	Коэффициенты.ВариантРасчетаЦеныНабора В (ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ЦенаЗадаетсяЗаНаборРаспределяетсяПоЦенам),
	|                                            ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ЦенаЗадаетсяЗаНаборРаспределяетсяПоДолям))
	|	ИЛИ Коэффициенты.КоличествоМинимумФакт = 0 
	|");
	
	Запрос.УстановитьПараметр("Таблица", Товары.Выгрузить());
	РезультатЗапросаПакет = Запрос.ВыполнитьПакет();
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("КоэффициентыНаборов", РезультатЗапросаПакет[РезультатЗапросаПакет.Количество() - 1].Выгрузить());
	ВозвращаемоеЗначение.Вставить("ДанныеПоНаборам",     РезультатЗапросаПакет[РезультатЗапросаПакет.Количество() - 2].Выгрузить());
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

&НаСервере
Процедура АнализНаборов()
	
	РезультатАнализа = Анализ();
	
	ДанныеПоНаборам = РезультатАнализа.ДанныеПоНаборам;
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		
		СтрокаТЧ.КоличествоУпаковокФактИтог = СтрокаТЧ.КоличествоУпаковокФакт;
		
		ОтборНаборов = Новый Структура;
		ОтборНаборов.Вставить("НоменклатураНабора", СтрокаТЧ.НоменклатураНабора);
		ОтборНаборов.Вставить("ХарактеристикаНабора", СтрокаТЧ.ХарактеристикаНабора);
		НайденоВАнализе = ДанныеПоНаборам.НайтиСтроки(ОтборНаборов).Количество() > 0;
		
		Если ЗначениеЗаполнено(СтрокаТЧ.НоменклатураНабора) И НайденоВАнализе Тогда
			
			Отбор = Новый Структура;
			Отбор.Вставить("ИдентификаторСтроки");
			Отбор.Вставить("КодСтроки");
			Отбор.Вставить("ЗаказКлиента");
			Отбор.Вставить("НоменклатураНабора");
			Отбор.Вставить("ХарактеристикаНабора");
			Отбор.Вставить("Номенклатура");
			Отбор.Вставить("Характеристика");
			Отбор.Вставить("Упаковка");
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаТЧ);
			НайденныеСтроки = ДанныеПоНаборам.НайтиСтроки(Отбор);
			Если НайденныеСтроки.Количество() > 0 Тогда
				
				КоличествоУпаковокФакт           = 0;
				КоличествоУпаковокФактОтклонение = 0;
				Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
					КоличествоУпаковокФактОтклонение = КоличествоУпаковокФактОтклонение + НайденнаяСтрока.Разница;
					КоличествоУпаковокФакт = КоличествоУпаковокФакт + НайденнаяСтрока.КоличествоУпаковокПолныйКомплектФакт;
				КонецЦикла;
				СтрокаТЧ.КоличествоУпаковокФактИтог = КоличествоУпаковокФакт;
				
				Если КоличествоУпаковокФактОтклонение <> 0 Тогда
					НоваяСтрока = ТоварыОтвязаноОтНабора.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
					НоваяСтрока.КоличествоУпаковок = КоличествоУпаковокФактОтклонение;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЗакончитьПроверкуНаСервере()
	
	// Создаем пустую таблицу Товаров
	ТаблицаТовары = ТоварыОтвязаноОтНабора.Выгрузить(Новый Структура("Номенклатура", Справочники.Номенклатура.ПустаяСсылка())).СкопироватьКолонки();
	
	МассивСтрокНовыеТовары = Новый Массив;
	МассивСтрокНедостачи   = Новый Массив;
	МассивСтрокИзлишки     = Новый Массив;
	
	Для Каждого СтрокаТЧ Из ТоварыОтвязаноОтНабора Цикл
		НоваяСтрока = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ,,"КоличествоУпаковок,НоменклатураНабора, ХарактеристикаНабора, ИндексНабора");
		НоваяСтрока.КоличествоУпаковокФакт = СтрокаТЧ.КоличествоУпаковок;
		НоваяСтрока.КоличествоУпаковокФактИтог = СтрокаТЧ.КоличествоУпаковок;
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		
		Если СтрокаТЧ.КоличествоУпаковок = СтрокаТЧ.КоличествоУпаковокФактИтог Тогда
			// Нет расхождений
		ИначеЕсли СтрокаТЧ.КоличествоУпаковок = 0 И СтрокаТЧ.КоличествоУпаковокФактИтог > 0 Тогда
			// Новый товар
			МассивСтрокНовыеТовары.Добавить(СтрокаТЧ);
		ИначеЕсли СтрокаТЧ.КоличествоУпаковок > СтрокаТЧ.КоличествоУпаковокФактИтог Тогда
			// Недостача
			МассивСтрокНедостачи.Добавить(СтрокаТЧ);
		ИначеЕсли СтрокаТЧ.КоличествоУпаковок < СтрокаТЧ.КоличествоУпаковокФактИтог Тогда
			// Излишек
			МассивСтрокИзлишки.Добавить(СтрокаТЧ);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПревышениеКоличестваТоваровРазрешено Тогда
		Для Каждого СтрокаТЧ Из МассивСтрокНовыеТовары Цикл
			НоваяСтрока = ТаблицаТовары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ, , "КоличествоУпаковок");
			НоваяСтрока.КоличествоУпаковок = СтрокаТЧ.КоличествоУпаковокФактИтог - СтрокаТЧ.КоличествоУпаковок;
		КонецЦикла;
		Для Каждого СтрокаТЧ Из МассивСтрокИзлишки Цикл
			НоваяСтрока = ТаблицаТовары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ, , "КоличествоУпаковок");
			НоваяСтрока.КоличествоУпаковок = СтрокаТЧ.КоличествоУпаковокФактИтог - СтрокаТЧ.КоличествоУпаковок;
		КонецЦикла;
	КонецЕсли;
	Для Каждого СтрокаТЧ Из МассивСтрокНедостачи Цикл
		НоваяСтрока = ТаблицаТовары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ, , "КоличествоУпаковок");
		НоваяСтрока.КоличествоУпаковок = СтрокаТЧ.КоличествоУпаковокФактИтог - СтрокаТЧ.КоличествоУпаковок;
	КонецЦикла;
	
	АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ТаблицаТовары, АдресВоВременномХранилище);
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("Товары",        АдресВоВременномХранилище);
	
	СобытияФорм.ПриИзмененииЭлемента(ЭтотОбъект, "ЗавершитьПроверку", ПараметрыЗакрытия);
	
	Возврат ПараметрыЗакрытия;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоГруппаСкладовИСкладыИспользуютсяВТЧДокументовПродажи(Склад)
	
	Возврат Справочники.Склады.ЭтоГруппаИСкладыИспользуютсяВТЧДокументовПродажи(Склад);
	
КонецФункции

&НаСервере
Процедура УстановитьОтборПоРасхождениям()
	Если ТолькоРасхождения Тогда
		Элементы.Товары.ОтборСтрок = Новый ФиксированнаяСтруктура("ЕстьРасхождение", 1);
	Иначе
		Элементы.Товары.ОтборСтрок = Неопределено;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуре()
	
	ТаблицаТоваров = Товары.Выгрузить();
	ТаблицаТоваров.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	
	Для Каждого СтрокаТаблицы Из ТаблицаТоваров Цикл
		СтрокаТаблицы.НомерСтроки = ТаблицаТоваров.Индекс(СтрокаТаблицы) + 1;
	КонецЦикла;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются",      Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));
	СтруктураДействий.Вставить("ЗаполнитьПризнакТипНоменклатуры",                 Новый Структура("Номенклатура", "ТипНоменклатуры"));
	СтруктураДействий.Вставить("ЗаполнитьВесУпаковки",Новый Структура("Номенклатура, Упаковка", "ВесУпаковки"));
	СтруктураДействий.Вставить("ЗаполнитьОбъемУпаковки",Новый Структура("Номенклатура, Упаковка", "ОбъемУпаковки"));
	СтруктураДействий.Вставить("ЗаполнитьЕдиницуИзмеренияВеса",Новый Структура("Номенклатура, Упаковка", "ЕдиницаИзмеренияВеса"));
	СтруктураДействий.Вставить("ЗаполнитьЕдиницуИзмеренияОбъема",Новый Структура("Номенклатура, Упаковка", "ЕдиницаИзмеренияОбъема"));
	ОбработкаТабличнойЧастиКлиентСерверЛокализация.ДополнитьСтруктуруДействийПриИзмененииЭлемента(ЭтотОбъект, "НоменклатураПризнаки", СтруктураДействий);
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(ТаблицаТоваров, СтруктураДействий);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьВесОбъем", "Факт");
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТЧ, СтруктураДействий, Неопределено);
	КонецЦикла;
	
	НаборыСервер.ЗаполнитьСлужебныеРеквизиты(ЭтотОбъект, "Товары", Истина);
	
	ОбработкаТабличнойЧастиСерверЛокализация.ЗаполнитьСлужебныеРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаНаВопрос(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = "Перенести" Тогда
		ОбработкаЗакрытия = Истина;
		Закрыть(ЗакончитьПроверкуНаСервере());
	ИначеЕсли РезультатВопроса = "НеПереносить" Тогда 
		ОбработкаЗакрытия = Истина;
		Закрыть(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьВСтруктуруДействияПриИзмененииКоличестваУпаковок(СтруктураДействий)
	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьВесОбъем", "Факт");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Инициализация

ОбработкаЗакрытия = Истина;

#КонецОбласти
