#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок               = Параметры.Заголовок;
	ИспользоватьКодУпаковки = Параметры.ИспользоватьКодУпаковки;
	
	ТабличныйДокумент.Область(1,1).Текст = ЗаголовокКодМаркировки();
	ТабличныйДокумент.Область(, 1,, 1).ШиринаКолонки = 50;
	
	Если ИспользоватьКодУпаковки Тогда
		ТабличныйДокумент.Область(1,2).Текст = ЗаголовокКодУпаковки();
		ТабличныйДокумент.Область(, 2,, 2).ШиринаКолонки = 50;
	КонецЕсли;
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Если ЕстьКодыМаркировки() Тогда
		ОповеститьОВыборе(КодыМаркировкиИзТабличногоДокумента());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокКодУпаковки()
	
	Возврат НСтр("ru = 'Код упаковки'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокКодМаркировки()
	
	Возврат НСтр("ru = 'Код маркировки'");
	
КонецФункции

// Заполняет коды маркировки из табличного документа в массив структур для возврата вызвавшей форме
//
// Возвращаемое значение:
//   Массив Из Структура - коды маркировки из табличного документа с полями:
//    * Штрихкод                          - Строка - код маркировки (обязательный)
//    * ШтрихкодУпаковки                  - Строка - код упаковки (необязательный)
//    * Количество                        - Число  - 1 (для совместимости с получением данных из ТСД БПО)
//    * ШтрихкодМаркиАлкогольнойПродукции - Строка - пустой (для совместимости с получением данных из ТСД БПО)
//
&НаКлиенте
Функция КодыМаркировкиИзТабличногоДокумента()
	
	Результат = Новый Массив;
	
	НомерКолонкиКодМаркировки = Неопределено;
	НомерКолонкиКодУпаковки   = Неопределено;
	Для Индекс = 1 По ТабличныйДокумент.ШиринаТаблицы Цикл
		Если ТабличныйДокумент.Область(1,Индекс).Текст = ЗаголовокКодМаркировки() Тогда
			НомерКолонкиКодМаркировки = Индекс;
		КонецЕсли;
		Если ТабличныйДокумент.Область(1,Индекс).Текст = ЗаголовокКодУпаковки() Тогда
			НомерКолонкиКодУпаковки = Индекс;
		КонецЕсли;
	КонецЦикла;
	
	КолонкаКодУпаковкиИспользуется = ИспользоватьКодУпаковки И НомерКолонкиКодУпаковки <> Неопределено;
	
	Для Индекс = 2 По ТабличныйДокумент.ВысотаТаблицы Цикл
		Штрихкод = ТабличныйДокумент.Область(Индекс, НомерКолонкиКодМаркировки).Текст;
		Если ЗначениеЗаполнено(Штрихкод) Тогда
			
			СодержитНедопустимыеСимволы = Ложь;
			
			Если РазборКодаМаркировкиИССлужебныйКлиентСервер.НайденНедопустимыйСимволXML(Штрихкод) Тогда
				СодержитНедопустимыеСимволы = Истина;
			КонецЕсли;
			
			Если КолонкаКодУпаковкиИспользуется Тогда
				ШтрихкодУпаковки = СокрЛП(ТабличныйДокумент.Область(Индекс, НомерКолонкиКодУпаковки).Текст);
				Если ЗначениеЗаполнено(ШтрихкодУпаковки)
					И РазборКодаМаркировкиИССлужебныйКлиентСервер.НайденНедопустимыйСимволXML(ШтрихкодУпаковки) Тогда
					СодержитНедопустимыеСимволы = Истина;
				КонецЕсли;
			Иначе
				ШтрихкодУпаковки = "";
			КонецЕсли;
			
			Если СодержитНедопустимыеСимволы Тогда
				Штрихкод = ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.ШтрихкодВBase64(Штрихкод);
				ШтрихкодУпаковки = ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.ШтрихкодВBase64(ШтрихкодУпаковки);
			КонецЕсли;
			
			ПоляСтроки = Новый Структура;
			ПоляСтроки.Вставить("Штрихкод",                          СокрЛП(Штрихкод));
			ПоляСтроки.Вставить("Количество",                        1);
			ПоляСтроки.Вставить("ШтрихкодМаркиАлкогольнойПродукции", "");
			ПоляСтроки.Вставить("ШтрихкодУпаковки",                  ШтрихкодУпаковки);
			ПоляСтроки.Вставить("ФорматBase64",                      ФорматBase64 Или СодержитНедопустимыеСимволы);
			
			Результат.Добавить(ПоляСтроки);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Проверяет наличие колонки "Код маркировки" и хотя бы 1 заполненного значения в ней
// 
// Возвращаемое значение:
//   Булево - коды маркировки в таблице есть
//
&НаКлиенте
Функция ЕстьКодыМаркировки()
	
	НомерКолонкиКодМаркировки = Неопределено;
	Для Индекс = 1 По ТабличныйДокумент.ШиринаТаблицы Цикл
		Если ТабличныйДокумент.Область(1,Индекс).Текст = ЗаголовокКодМаркировки() Тогда
			НомерКолонкиКодМаркировки = Индекс;
		КонецЕсли;
	КонецЦикла;
	
	Если НомерКолонкиКодМаркировки = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			СтрШаблон(НСтр("ru = 'Не найдена колонка с заголовком: %1'"), ЗаголовокКодМаркировки()), , "ТабличныйДокумент");
		Возврат Ложь;
	КонецЕсли;
	
	Для Индекс = 2 По ТабличныйДокумент.ВысотаТаблицы Цикл
		Штрихкод = ТабличныйДокумент.Область(Индекс, НомерКолонкиКодМаркировки).Текст;
		Если ЗначениеЗаполнено(Штрихкод) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	ОбщегоНазначенияКлиент.СообщитьПользователю(
		СтрШаблон(НСтр("ru = 'Не заполнена колонка с заголовком: %1'"), ЗаголовокКодМаркировки()), , "ТабличныйДокумент");
	Возврат Ложь;
	
КонецФункции

#КонецОбласти
