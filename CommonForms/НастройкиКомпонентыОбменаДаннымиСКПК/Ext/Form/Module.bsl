
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьУсловноеОформление();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьСтатусы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПодключениеКомпонентыОбменаДаннымиСМобильнымиПриложениями" Тогда
		ОбновитьСтатусы();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьКомпоненту(Команда)
	
	ОчиститьСообщения();
	
	Оповещение = Новый ОписаниеОповещения("УстановитьКомпонентуЗавершение", ЭтотОбъект);

	ПараметрыУстановки = ОбщегоНазначенияКлиент.ПараметрыУстановкиКомпоненты();

	ОбщегоНазначенияКлиент.УстановитьКомпонентуИзМакета(Оповещение,
        "ОбщийМакет.КомпонентаОбменДаннымиСМобильнымиПриложениями",
        ПараметрыУстановки);

КонецПроцедуры

&НаКлиенте
Процедура ПодключитьКомпоненту(Команда)
	
	ОчиститьСообщения();
	
	Если СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиентаПриЗапуске().СлужебныеПодсистемы_ОбъектыУТКАУП_КомпонентаОбменаСМП <> Неопределено Тогда
		ТекстСообщения = НСтр("ru='Компонента обмена данными с мобильными приложениями уже подключена'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	Иначе
		МобильныеПриложенияКлиент.ПодключитьВнешнююКомпонентуДляОбменаДаннымиСМобильнымиПриложениями();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьКомпоненту(Команда)
	
	ОчиститьСообщения();
	
	Если СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиентаПриЗапуске().СлужебныеПодсистемы_ОбъектыУТКАУП_КомпонентаОбменаСМП = Неопределено Тогда
		ТекстСообщения = НСтр("ru='Компонента обмена данными не подключена'");
		ПоказатьПредупреждение(Новый ОписаниеОповещения("ОтключитьКомпонентуЗавершение", ЭтотОбъект, Новый Структура("ТекстСообщения", ТекстСообщения)), ТекстСообщения);
        Возврат;
	Иначе
		СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиентаПриЗапуске().СлужебныеПодсистемы_ОбъектыУТКАУП_КомпонентаОбменаСМП.Отключить(0);
		СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиентаПриЗапуске().СлужебныеПодсистемы_ОбъектыУТКАУП_КомпонентаОбменаСМП = Неопределено;
		ТекстСообщения = НСтр("ru='Отключение компоненты'");
		ТекстПояснения = НСтр("ru='Компонента обмена данными отключена'");
		ПоказатьОповещениеПользователя(ТекстСообщения,,ТекстПояснения, БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
	ОтключитьКомпонентуФрагмент();
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьКомпонентуЗавершение(ДополнительныеПараметры) Экспорт
    
    ТекстСообщения = ДополнительныеПараметры.ТекстСообщения;
    
    
    ОтключитьКомпонентуФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ОтключитьКомпонентуФрагмент()
    
    ОбновитьСтатусы();

КонецПроцедуры

&НаКлиенте
Процедура Настройки(Команда)
	
	ОткрытьФорму("ОбщаяФорма.ПерсональныеНастройкиРаботыСКомпонентойОбменаДанными",,,,,, Новый ОписаниеОповещения("НастройкиЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    ОбновитьСтатусы();

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнформациюОСтатусе(Команда)
	
	ОбновитьСтатусы();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтатусПодключения.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КомпонентаПодключена");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.SpecialTextColor);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтатусПодключения.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КомпонентаПодключена");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", Новый Цвет(51, 153, 102));

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтатусы()
	
	КомпонентаОбменаСМобильнымиПриложениями = СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиентаПриЗапуске().СлужебныеПодсистемы_ОбъектыУТКАУП_КомпонентаОбменаСМП;
	
	Если КомпонентаОбменаСМобильнымиПриложениями <> Неопределено Тогда
		
		КомпонентаПодключена = Истина;
		СтатусПодключения = НСтр("ru='Компонента подключена'");
		
		TCPIPПодключен = КомпонентаОбменаСМобильнымиПриложениями.TCPIPПодключен;
		COMПортПодключен = КомпонентаОбменаСМобильнымиПриложениями.COMПортПодключен;
		IRDAПодключен = КомпонентаОбменаСМобильнымиПриложениями.IRDAПодключен;
		
		ПортTCPIP = КомпонентаОбменаСМобильнымиПриложениями.ПортTCPIP;
		COMПорт = КомпонентаОбменаСМобильнымиПриложениями.COMПорт;
		ИмяСервисаIRDA = КомпонентаОбменаСМобильнымиПриложениями.ИмяСервисаIRDA;
		
		ВестиЖурналСобытий = КомпонентаОбменаСМобильнымиПриложениями.ВестиЖурналСобытий;
		КаталогЖурналаСобытий = КомпонентаОбменаСМобильнымиПриложениями.КаталогЖурналаСобытий;
		
	Иначе
		
		КомпонентаПодключена = Ложь;
		СтатусПодключения = НСтр("ru='Компонента не подключена'");
		
		TCPIPПодключен = Ложь;
		COMПортПодключен = Ложь;
		IRDAПодключен = Ложь;
		
		ПортTCPIP = 0;
		COMПорт=0;
		ИмяСервисаIRDA = "";
		
		ВестиЖурналСобытий = Ложь;
		КаталогЖурналаСобытий = "";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКомпонентуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Установлено И Не ПустаяСтрока(Результат.ОписаниеОшибки) Тогда 
		ПоказатьПредупреждение(, Результат.ОписаниеОшибки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
