
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ТипЗапроса = Параметры.ТипЗапроса;
	Если ТипЗапроса = "Партии" Тогда
		Заголовок                      = НСтр("ru = 'Загрузка номеров партий из внешних файлов'");
		Элементы.НадписьСДИЗ.Видимость = Ложь;
	Иначе
		Заголовок                        = НСтр("ru = 'Загрузка номеров СДИЗ из внешних файлов'");
		Элементы.НадписьПартии.Видимость = Ложь;
	КонецЕсли;
	ИнициализироватьТабличныйДокумент();
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	
	Закрыть(Идентификаторы());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализироватьТабличныйДокумент()
	
	Если ТипЗапроса = "Партии" Тогда
		Макет = Справочники.РеестрПартийЗЕРНО.ПолучитьМакет("ЗагрузкаИдентификаторовПартийИзВнешнихФайлов");
	Иначе
		Макет = Справочники.СДИЗЗЕРНО.ПолучитьМакет("ЗагрузкаИдентификаторовСДИЗИзВнешнихФайлов");
	КонецЕсли;
	
	ТабличныйДокумент.Очистить();
	
	ОбластьТовар = Макет.ПолучитьОбласть("НомерСДИЗ");
	ТабличныйДокумент.Присоединить(ОбластьТовар);
	
	ТабличныйДокумент.ФиксацияСверху = 1;
	
КонецПроцедуры

&НаСервере
Функция Идентификаторы()
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("НомерСДИЗ", Метаданные.Справочники.СДИЗЗЕРНО.Реквизиты.Идентификатор.Тип);
	
	КоличествоСтрок = ТабличныйДокумент.ВысотаТаблицы;
	
	Для НомерСтроки = 2 По КоличествоСтрок Цикл
		
		НомерСДИЗ = НРег(СтрЗаменить(СокрЛП(ТабличныйДокумент.Область("R" + НомерСтроки + "C2").Текст), "-", ""));
		
		Если ЗначениеЗаполнено(НомерСДИЗ) Тогда
			НоваяСтрока = Таблица.Добавить();
			НоваяСтрока.НомерСДИЗ = НомерСДИЗ;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Таблица.ВыгрузитьКолонку("НомерСДИЗ");
	
КонецФункции

#КонецОбласти
