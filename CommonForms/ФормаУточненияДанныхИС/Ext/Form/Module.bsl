#Область ОписаниеПеременных

&НаКлиенте
Перем ВопросОВыбытииБутылкиЗадан;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
	ЗаполнитьДанныеФормыПриСозданииНаСервере();
	СтандартныеПодсистемыСервер.СброситьРазмерыИПоложениеОкна(ЭтотОбъект);
	ВывестиПоясняющийТекст();
	
	СобытияФормИСКлиентСерверПереопределяемый.УстановитьПараметрыВыбораНоменклатуры(
		ЭтотОбъект, ПараметрыСканирования.ДопустимыеВидыПродукции, "Номенклатура");
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСНоменклатурой(
		ЭтотОбъект, "Характеристика", "Номенклатура");
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСНоменклатурой(
		ЭтотОбъект, "Серия", "Номенклатура");
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСХарактеристикой(
		ЭтотОбъект, "Серия", "Характеристика");
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ГосИС.ИСМП") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("СобытияФормИСМППереопределяемый");
		Модуль.УстановитьПараметрыВыбораШаблонаЭтикетки(ЭтотОбъект, Элементы.ШаблонЭтикетки.Имя);
	КонецЕсли;

	НастроитьRFID();
	ПоддерживаемыеТипыПодключаемогоОборудования = "СканерШтрихкода";
	Если ПодключатьСчитывательRFID Тогда
		ПоддерживаемыеТипыПодключаемогоОборудования = ПоддерживаемыеТипыПодключаемогоОборудования + ", СчитывательRFID";
	КонецЕсли;
	ИнтеграцияИС.НастроитьПодключаемоеОборудование(ЭтотОбъект, ПоддерживаемыеТипыПодключаемогоОборудования, "");

	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	УстановитьЗаголовокЭлементаИдентификаторВЕТИС();
	УстановитьФорматДатыГоденДо(ЭтотОбъект);
	УстановитьВидимостьДоступностьЭлементовПриСозданииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ЭтоМаркировкаОстатков
		И ПараметрыСканирования.РазрешенаОбработкаКодовСПустойНоменклатурой
		И ВидПродукции <> Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Номенклатура");
	КонецЕсли;
	
	Если Не УказатьИдентификаторВЕТИС Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ИдентификаторПроисхожденияВЕТИС");
	КонецЕсли;
	
	Если Не УказатьСрокГодности Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ГоденДо");
	ИначеЕсли ИнтеграцияИСМПСлужебныйКлиентСервер.ДоступенСрокГодностиПриПриемкеОтгрузке(ПараметрыСканирования.ВидОперацииИСМП, ВидПродукции) Тогда
		Если Не ИнтеграцияИСМПСлужебныйКлиентСервер.ТребуетсяУказаниеСрокаГодностиПриПриемкеОтгрузке(ПараметрыСканирования.ВидОперацииИСМП, ВидПродукции) Тогда
			МассивНепроверяемыхРеквизитов.Добавить("ГоденДо");
		КонецЕсли;
	ИначеЕсли ИнтеграцияИСМПКлиентСервер.ЭтоВидПродукцииСоСрокамиГодностиБезОбязательногоЗаполнения(ВидПродукции) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ГоденДо");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Номенклатура)
		Или Не ПараметрыСканирования.ЗапрашиватьКоличествоМерногоТовара
		Или РазборКодаМаркировкиИССлужебныйКлиентСервер.ЭтоEANИлиGTIN(КодМаркировки)
		Или ЭтоЧастичноеВыбытие(ЭтотОбъект) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Количество");
	КонецЕсли;
	
	Если Не РежимПечатиЭтикеток Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ШаблонЭтикетки");
		МассивНепроверяемыхРеквизитов.Добавить("ШаблонКодаМаркировки");
	ИначеЕсли Не ВидимостьШаблонаЭтикетки Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ШаблонЭтикетки");
	КонецЕсли;
	
	Если ЭтоЧастичноеВыбытие(ЭтотОбъект) Тогда
		
		ЕстьОшибкаКоличества = Ложь;
		Если ЧастичноеВыбытиеКоличество > ЕмкостьПотребительскойУпаковки Тогда
			ЕстьОшибкаКоличества = Истина;
		ИначеЕсли ЧастичноеВыбытиеКоличество = ЕмкостьПотребительскойУпаковки
			И Не (ВидПродукции = Перечисления.ВидыПродукцииИС.Пиво
				Или ВидПродукции = Перечисления.ВидыПродукцииИС.ПивоВПотребительскихУпаковках) Тогда
			ЕстьОшибкаКоличества = Истина;
		КонецЕсли;

		Если ЕстьОшибкаКоличества Тогда
			Если ВидПродукции = Перечисления.ВидыПродукцииИС.Духи Тогда
				ТекстСообщения = НСтр("ru = 'При продаже на разлив количество должно быть меньше емкости флакона.'");
			ИначеЕсли ВидПродукции = Перечисления.ВидыПродукцииИС.АльтернативныйТабак Тогда
				ТекстСообщения = НСтр("ru = 'При поштучной продаже количество должно быть меньше емкости пачки.'");
			ИначеЕсли ВидПродукции = Перечисления.ВидыПродукцииИС.Пиво Тогда
				ТекстСообщения = НСтр("ru = 'При продаже на разлив количество должно быть меньше емкости кега.'");
			Иначе
				ТекстСообщения = НСтр("ru = 'Количество должно быть меньше емкости упаковки.'");
			КонецЕсли;
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "ЧастичноеВыбытиеКоличество",, Отказ);
		КонецЕсли;
		
	Иначе
		
		МассивНепроверяемыхРеквизитов.Добавить("ЕмкостьПотребительскойУпаковки");
		МассивНепроверяемыхРеквизитов.Добавить("ЧастичноеВыбытиеКоличество");
		МассивНепроверяемыхРеквизитов.Добавить("ЧастичноеВыбытиеВариантУчета");
		
	КонецЕсли;
	
	Если ПараметрыСканирования.Свойство("ИндивидуализироватьКИЗ")
		И ПараметрыСканирования.ИндивидуализироватьКИЗ Тогда
		
		ТекстСообщения = "";
		
		СтатусRFID = ШтрихкодированиеИСКлиентСервер.СтатусRFID(ДанныеRFID, GTIN);
		Если СтатусRFID = 0 Тогда
			ТекстСообщения = НСтр("ru = 'Считайте RFID-метку'");
		ИначеЕсли СтатусRFID = 1 Тогда
			ТекстСообщения = НСтр("ru = 'Запишите RFID-метку'");
		КонецЕсли;
		Если ЗначениеЗаполнено(ТекстСообщения) Тогда
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,,
				"СтатусRFIDПредставление",,
				Отказ);
		КонецЕсли;
		
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("GTIN");
	КонецЕсли;
	
	// Используется для добавления кодов ОСУ
	Если Не УказатьКоличествоПотребительскихУпаковок Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КоличествоПотребительскихУпаковок");
	КонецЕсли;
	
	Если Не ЭтоВыборКодаМаркировкиВскрытойПотребительскойУпаковки(ЭтотОбъект) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КодМаркировки");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	СобытияФормИСПереопределяемый.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИнтеграцияИСКлиент.ЭтоФормаУказанияСерий(ИсточникВыбора, ВыбранноеЗначение) Тогда
		Серия = ВыбранноеЗначение.Значение;
	КонецЕсли;
	
	Если ТипыНоменклатуры.НайтиПоЗначению(ТипЗнч(ВыбранноеЗначение)) <> Неопределено Тогда
		
		Если ВыбранноеЗначение <> Номенклатура
			И ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
			
			Номенклатура = ВыбранноеЗначение;
			НоменклатураПриИзмененииСервер();
			
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ДекорацияУдалосьПодобрать.Видимость = ЗначениеЗаполнено(Серия);
	
	ОтключитьОтметкуНезаполненного();
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Если Не (Элементы.ПоискПоШтрихкоду.Видимость
		Или Элементы.СчитатьRFID.Видимость
		Или Элементы.ЗаписатьRFID.Видимость) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеОборудования = СобытияФормИСКлиент.ВнешнееСобытиеПреобразоватьДанныеСоСканераВСтруктуру(
		ЭтотОбъект, Источник, Событие, Данные);
	
	Если ДанныеОборудования = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеОборудования.Свойство("TID") Тогда
		
		ДанныеRFID = ДанныеОборудования;
		
		ОбновитьСтатусRFID(ЭтотОбъект);
		
	Иначе
		ВводШтрихкодаЗавершение(ДанныеОборудования);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ОповещениеПриОтключении = Новый ОписаниеОповещения("ОтключитьОборудованиеЗавершение", ЭтотОбъект);
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(ОповещениеПриОтключении, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОповещениеПриПодключении = Новый ОписаниеОповещения("ПодключитьОборудованиеЗавершение", ЭтотОбъект);
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(
		ОповещениеПриПодключении,
		ЭтотОбъект,
		ПоддерживаемыеТипыПодключаемогоОборудования);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	НоменклатураПриИзмененииСервер();
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВидПродукции) Тогда
		ВидыПродукции = Новый Массив;
		ВидыПродукции.Добавить(ВидПродукции);
	Иначе
		ВидыПродукции = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(ПараметрыСканирования.ДопустимыеВидыПродукции, Ложь);
	КонецЕсли;
	
	Если РазборКодаМаркировкиИССлужебныйКлиентСервер.ЭтоШтрихкодВводаОстатков(КодМаркировки) И ВидыПродукции.Количество() > 1 Тогда
		ВидыПродукцииПоКодуМаркировкиОстатков(ВидыПродукции, КодМаркировки, ПараметрыСканирования.Организация);
	КонецЕсли;
	
	СобытияФормИСКлиентПереопределяемый.ПриНачалеВыбораНоменклатуры(Элемент, ВидыПродукции, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ХарактеристикаПриИзменении(Элемент)
	
	ОбновитьДанныеGTIN();
	
КонецПроцедуры

&НаКлиенте
Процедура СерияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ПараметрыУказанияСерий <> Неопределено Тогда
		
		ИнтеграцияИСКлиент.ОткрытьПодборСерий(ЭтотОбъект, ПараметрыУказанияСерий, Элемент.ТекстРедактирования, СтандартнаяОбработка, ЭтотОбъект);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура GTINОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = "НастроитьГрупповыеУпаковкиИНаборы"
		И ОбщегоНазначенияКлиент.ПодсистемаСуществует("ГосИС.ИСМП") Тогда
		
		МодульИнтеграцияИСМПКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтеграцияИСМПКлиент");
		
		СтандартнаяОбработка = Ложь;
		
		СтрокаДанных = Новый Структура;
		СтрокаДанных.Вставить("Номенклатура",   Номенклатура);
		СтрокаДанных.Вставить("Характеристика", Характеристика);
		
		ДанныеПоискаGTIN = Новый Массив;
		ДанныеПоискаGTIN.Добавить(СтрокаДанных);
		
		ПараметрыОткрытияФормы = МодульИнтеграцияИСМПКлиент.ПараметрыОткрытияФормыУточненияУпаковокПоGTIN();
		ПараметрыОткрытияФормы.ВидПродукции          = ВидПродукции;
		ПараметрыОткрытияФормы.УточнениеВидаУпаковки = Истина;
		ПараметрыОткрытияФормы.ДанныеПоискаGTIN      = ДанныеПоискаGTIN;
		
		МодульИнтеграцияИСМПКлиент.ОткрытьФормуНастройкиВидовУпаковокПоGTIN(ПараметрыОткрытияФормы, ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторПроисхожденияВЕТИСПриИзменении(Элемент)
	УстановитьФорматДатыГоденДо(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторПроисхожденияВЕТИСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Модуль = ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтеграцияИСМПВЕТИСКлиент");
	ПараметрыОткрытия = Модуль.ПараметрыВыбораИдентификатораПросхождения(ПараметрыСканирования.ВидОперацииИСМП);
	ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, ЭтотОбъект);
	ПараметрыОткрытия.ОповещениеВыбора = Новый ОписаниеОповещения("ВыборИдентификатораПроисхожденияВЕТИСЗавершение", ЭтотОбъект);
	ПараметрыОткрытия.Организация = ПараметрыСканирования.Организация;
	Модуль.ОткрытьФормуВыбораИдентификатораПроисхожденияВЕТИС(ПараметрыОткрытия, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоВУпаковкеПриИзменении(Элемент)
	РасчетОграниченийПолейЧастичногоВыбытия(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура КоличествоИзУпаковкиПриИзменении(Элемент)
	ПриИзмененииКоличестваЧастичногоВыбытия();
	РасчетОграниченийПолейЧастичногоВыбытия(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура РежимВыбытияПриИзменении(Элемент)
	
	ПриИзмененииРежимаВыбытия();
	НастроитьЭлементыФормыЧастичногоВыбытия(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоПотребительскихУпаковокПриИзменении(Элемент)
	
	Если Не Элементы.ГруппаКоличество.Видимость Тогда
		Количество = КоличествоПотребительскихУпаковок * КоличествоВПотребительскойУпаковке;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблонКодаМаркировкиПриИзменении(Элемент)
	
	ОбновитьДанныеGTIN();
	
КонецПроцедуры

&НаКлиенте
Процедура КодМаркировкиПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(КодМаркировки) Тогда
		СтруктураПоиска = Новый Структура("КодМаркировкиСтрокой", КодМаркировки);
		Для Каждого СтрокаТаблицы Из КодыМаркировкиДляВыбора.НайтиСтроки(СтруктураПоиска) Цикл
			ЧастичноеВыбытиеОстаток      = СтрокаТаблицы.ЧастичноеВыбытиеОстаток;
			ЧастичноеВыбытиеОстатокГИСМТ = СтрокаТаблицы.ЧастичноеВыбытиеОстатокГИСМТ;
		КонецЦикла;
	Иначе
		ЧастичноеВыбытиеОстаток      = Неопределено;
		ЧастичноеВыбытиеОстатокГИСМТ = Неопределено;
	КонецЕсли;
	
	ПриИзмененииРеквизитаСвязанногоСОстаткомВскрытойУпаковки(Ложь);
	НастроитьЭлементыФормыЧастичногоВыбытия(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоясняющийТекстОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "СкопироватьШтрихкодБуферОбмена" Тогда
		ИнтеграцияИСКлиент.СкопироватьШтрихКодВБуферОбмена(Элементы.БуферОбмена, ШтрихкодEAN);
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "СкопироватьКодМаркировкиВБуферОбмена" Тогда 
		ИнтеграцияИСКлиент.СкопироватьШтрихКодВБуферОбмена(Элементы.БуферОбмена, КодМаркировки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДанныеДокумента

&НаКлиенте
Процедура ДанныеДокументаВыбратьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДанныеДокумента.ТекущиеДанные;
	Если ТекущиеДанные.Выбрать Тогда
		ИзменениеНоменклатуры = ТекущиеДанные.Номенклатура <> Номенклатура;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ТекущиеДанные,,"Количество");
		ГоденДо = ТекущиеДанные.СрокГодности;
		Для Каждого СтрокаТЧ Из ДанныеДокумента Цикл
			Если СтрокаТЧ.Выбрать И СтрокаТЧ.ПолучитьИдентификатор() <> ТекущиеДанные.ПолучитьИдентификатор() Тогда
				СтрокаТЧ.Выбрать = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ИзменениеНоменклатуры Тогда
			НоменклатураПриИзмененииСервер();
		ИначеЕсли ЗначениеЗаполнено(ИдентификаторПроисхожденияВЕТИС) Тогда
			СкоропортящаясяПродукция = ПризнакСкоропортящаясяПродукция(ИдентификаторПроисхожденияВЕТИС);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеДокументаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ДанныеДокумента.ТекущиеДанные;
	ТекущиеДанные.Выбрать  = Истина;
	ИзменениеНоменклатуры = ТекущиеДанные.Номенклатура <> Номенклатура;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ТекущиеДанные,,"Количество");
	ГоденДо = ТекущиеДанные.СрокГодности;
	Для Каждого СтрокаТЧ Из ДанныеДокумента Цикл
		Если СтрокаТЧ.Выбрать И СтрокаТЧ.ПолучитьИдентификатор() <> ТекущиеДанные.ПолучитьИдентификатор() Тогда
			СтрокаТЧ.Выбрать = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ИзменениеНоменклатуры Тогда
		НоменклатураПриИзмененииСервер();
	ИначеЕсли ЗначениеЗаполнено(ИдентификаторПроисхожденияВЕТИС) Тогда
		СкоропортящаясяПродукция = ПризнакСкоропортящаясяПродукция(ИдентификаторПроисхожденияВЕТИС);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	ВопросОВыбытииБутылкиЗадан = Ложь;

	ЗакрытьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	ЗакрытьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкоду(Команда)
	
	ОчиститьСообщения();
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВводШтрихкодаЗавершение", ЭтотОбъект);
	ШтрихкодированиеИСКлиент.ПоказатьВводШтрихкода(ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВес(Команда)
	
	МенеджерОборудованияИСКлиент.НачатьПолученияВесаСЭлектронныхВесов(
		Новый ОписаниеОповещения("ПолучитьВесЗавершение", ЭтотОбъект),
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура СчитатьRFID(Команда)
	
	ОчиститьСообщения();
	
	СобытияФормИСКлиент.ОткрытьСессиюСчитывателяRFID(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("ОтработатьТаймаутОжиданияСчитыванияRFID", 5, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьRFID(Команда)
	
	ОчиститьСообщения();
	
	ИдетЗаписьRFID = Истина;
	
	ДанныеRFIDДляЗаписи = Новый Структура;
	ДанныеRFIDДляЗаписи.Вставить("GTIN",          GTIN);
	ДанныеRFIDДляЗаписи.Вставить("TID",           ДанныеRFID.TID);
	ДанныеRFIDДляЗаписи.Вставить("EPC",           ДанныеRFID.EPC);
	ДанныеRFIDДляЗаписи.Вставить("ЗаписанныйEPC", "");
	
	ПараметрыЗаписиRFID = Новый Структура;
	ПараметрыЗаписиRFID.Вставить("ДанныеRFIDДляЗаписи",     ДанныеRFIDДляЗаписи);
	ПараметрыЗаписиRFID.Вставить("Форма",                   ЭтотОбъект);
	ПараметрыЗаписиRFID.Вставить("ОповещениеПриЗавершении", Новый ОписаниеОповещения("ПослеЗаписиRFID", ЭтотОбъект, ДанныеRFIDДляЗаписи));
	
	СобытияФормИСКлиент.ЗаписатьДанныеRFID(Неопределено, ПараметрыЗаписиRFID);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	СобытияФормИСПереопределяемый.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(
		ЭтотОбъект, "Характеристика", "ХарактеристикиИспользуются");
	СобытияФормИСПереопределяемый.УстановитьУсловноеОформлениеСерийНоменклатуры(
		ЭтотОбъект, "Серия", "СтатусУказанияСерий", "ТипНоменклатуры");
	
КонецПроцедуры

&НаСервере
Процедура НоменклатураПриИзмененииСервер()
	
	СобытияФормИСПереопределяемый.ПриИзмененииНоменклатуры(ЭтотОбъект, ЭтотОбъект,, ПараметрыУказанияСерий);
	
	УказатьСрокГодности = ПараметрыСканирования.Свойство("ЗаполнятьСрокГодности")
		И ПараметрыСканирования.ЗаполнятьСрокГодности;
	
	Если УказатьИдентификаторВЕТИС И ЗначениеЗаполнено(ИдентификаторПроисхожденияВЕТИС) Тогда
		
		ДанныеСопоставления = Новый Структура;
		ДанныеСопоставления.Вставить("Номенклатура", Номенклатура);
		ДанныеСопоставления.Вставить("Характеристика", Характеристика);
		ДанныеСопоставления.Вставить("Серия", Серия);
		Модуль = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияИСМПВЕТИС");
		Если Не Модуль.НоменклатураСоответствуетСопоставленнойПродукцииВЕТИСПоИдентификаторуПроисхождения(
				ИдентификаторПроисхожденияВЕТИС, ДанныеСопоставления) Тогда
			ИдентификаторПроисхожденияВЕТИС = Неопределено;
		ИначеЕсли УказатьСрокГодности Тогда
			ДанныеВЕТИС = ДанныеИдентификаторовПроисхожденияВЕТИС(ИдентификаторПроисхожденияВЕТИС);
			ГоденДо         = ДанныеВЕТИС[ИдентификаторПроисхожденияВЕТИС].СрокГодности;
			СкоропортящаясяПродукция = ДанныеВЕТИС[ИдентификаторПроисхожденияВЕТИС].СкоропортящаясяПродукция;
		КонецЕсли;
		
	КонецЕсли;

	ЗаполнитьОписаниеНоменклатуры();
	ОбновитьПараметрыЧастичногоВыбытия(Истина);
	
	ОбновитьОтображениеПоляНоменклатура();
	ОбновитьОтображениеПоляКоличество();
	ОбновитьДанныеGTIN();
	
	СобытияФормИС.ПрименитьУсловноеОформлениеКПолю(ЭтотОбъект, "Характеристика");
	СобытияФормИС.ПрименитьУсловноеОформлениеКПолю(ЭтотОбъект, "Серия");
	ОбновитьВидимостьДоступностьЭлементов();
	ОбновитьСтатусRFID(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеПоляКоличество()
	
	ЭтоEANИлиGTIN = РазборКодаМаркировкиИССлужебныйКлиентСервер.ЭтоEANИлиGTIN(КодМаркировки);
	
	Если ТребуетВзвешивания И (РежимПечатиЭтикеток Или Не ЭтоEANИлиGTIN) Тогда
		Элементы.ГруппаКоличество.Видимость = ПараметрыСканирования.ЗапрашиватьКоличествоМерногоТовара;
		Элементы.ПолучитьВес.Видимость = ОбщегоНазначенияИСКлиентСервер.ЭтоМолочнаяПродукцияИСМП(ВидПродукции);
	Иначе
		Элементы.ГруппаКоличество.Видимость = Ложь;
		Если УказатьКоличествоПотребительскихУпаковок Тогда
			Количество = КоличествоВПотребительскойУпаковке * КоличествоПотребительскихУпаковок;
		ИначеЕсли Количество = 0 Тогда
			Количество = 1;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиПоясняющийТекст()
	
	МассивСтрок = Новый Массив;
	
	Элементы.ПоясняющийТекст.Высота = 1;
	Если Не РежимПечатиЭтикеток И Не ПустаяСтрока(КодМаркировки) Тогда
		
		Элементы.ПоясняющийТекст.Высота = 3;
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Обработка кода маркировки:'"),, ЦветаСтиля.ПоясняющийТекст));
		МассивСтрок.Добавить(" ");
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(КодМаркировки, Новый Шрифт(,,,,Истина), ЦветаСтиля.ЦветГиперссылкиГосИС,, "СкопироватьКодМаркировкиВБуферОбмена"));
		МассивСтрок.Добавить(Символы.ПС);
	
	ИначеЕсли ЭтоВыборКодаМаркировкиВскрытойПотребительскойУпаковки(ЭтотОбъект) Тогда
		
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Выберите код маркировки'"),, ЦветаСтиля.ПоясняющийТекст));
		
	КонецЕсли;
	
	Если ТипЗнч(Параметры.Номенклатура) = Тип("Массив") Тогда
		
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Штрихкоду'"),, ЦветаСтиля.ПоясняющийТекст));
		МассивСтрок.Добавить(" ");
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(ШтрихкодEAN, Новый Шрифт(,,,,Истина), ЦветаСтиля.ЦветГиперссылкиГосИС,, "СкопироватьШтрихкодБуферОбмена"));
		МассивСтрок.Добавить(" ");
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'сопоставлено несколько номенклатурных позиций. Укажите номенклатуру.'"),, ЦветаСтиля.ПоясняющийТекст));
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.Номенклатура) Тогда
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Укажите номенклатуру.'"),, ЦветаСтиля.ПоясняющийТекст));
	КонецЕсли;
	
	Если УказатьИдентификаторВЕТИС Тогда
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Укажите специфику ВетИС.'"),, ЦветаСтиля.ПоясняющийТекст));
	КонецЕсли;
	
	Если Элементы.Серия.СписокВыбора.Количество() > 1 Тогда
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Укажите серию номенклатуры.'"),, ЦветаСтиля.ПоясняющийТекст));
	КонецЕсли;
	
	Если РежимПечатиЭтикеток Тогда
		Если МассивСтрок.Количество() Тогда
			Элементы.ПоясняющийТекст.Высота = Элементы.ПоясняющийТекст.Высота + 1;
			МассивСтрок.Добавить(Символы.ПС);
		КонецЕсли;
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Укажите шаблон этикетки.'"),, ЦветаСтиля.ПоясняющийТекст));
	КонецЕсли;
	
	ПоясняющийТекст = Новый ФорматированнаяСтрока(МассивСтрок);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеФормыПриСозданииНаСервере()
	
	Заголовок = НСтр("ru = 'Уточнение данных'");
	
	Количество = Параметры.Количество;
	
	ОбработатьРежимВыбораИзДокумента();
	
	ТипыНоменклатуры.ЗагрузитьЗначения(Метаданные.ОпределяемыеТипы.Номенклатура.Тип.Типы());
	
	ПараметрыСканирования = ОбщегоНазначения.СкопироватьРекурсивно(Параметры.ПараметрыСканирования);
	Склад                 = ПараметрыСканирования.Склад;
	ЦветТекстаПроблема    = ЦветаСтиля.ЦветТекстаПроблемаГосИС;
	
	ПараметрыУказанияСерий = ПараметрыСканирования.ПараметрыУказанияСерий;
	ИнтеграцияИСПереопределяемый.ЗаполнитьПараметрыУказанияСерий(
		ПараметрыУказанияСерий, Метаданные.ОбщиеФормы.ФормаУточненияДанныхИС, ЭтотОбъект);
	
	Если Параметры.ВидУпаковки = Перечисления.ВидыУпаковокИС.ОбъемноСортовойУчет
		И Параметры.КоличествоПотребительскихУпаковок = 0 Тогда
		УказатьКоличествоПотребительскихУпаковок = Истина;
		УказатьGTIN = ПараметрыСканирования.ВидОперацииИСМП <> ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.Приемка");
		КодМаркировки = СтрШаблон("(02)%1(37)0", Параметры.GTIN);
	Иначе
		КодМаркировки = Параметры.КодМаркировки;
	КонецЕсли;
	
	Если ПараметрыСканирования.Свойство("ИндивидуализироватьКИЗ")
			И ПараметрыСканирования.ИндивидуализироватьКИЗ Тогда
		УказатьGTIN = Истина;
	КонецЕсли;
	
	МаркируемаяПродукция    = Параметры.МаркируемаяПродукция;
	ОбработатьБезМаркировки = Параметры.ОбработатьБезМаркировки;
	ЭтоШтрихкодНоменклатуры = Параметры.ЭтоШтрихкодНоменклатуры;
	ШтрихкодEAN             = Параметры.ШтрихкодEAN;
	GTIN                    = Параметры.GTIN;
	ВидПродукции            = Параметры.ВидПродукции;
	ВидУпаковки             = Параметры.ВидУпаковки;
	ЭтоМаркировкаОстатков   = РазборКодаМаркировкиИССлужебныйКлиентСервер.ЭтоШтрихкодВводаОстатков(GTIN)
		Или РазборКодаМаркировкиИССлужебныйКлиентСервер.ЭтоШтрихкодВводаОстатков(ШтрихкодEAN);
	РежимПечатиЭтикеток     = Параметры.РежимПечатиЭтикеток;
	
	Если ВидПродукции = Перечисления.ВидыПродукцииИС.Алкогольная Тогда
		Параметры.Свойство("АлкогольнаяПродукция", АлкогольнаяПродукция);
	КонецЕсли;

	Элементы.АлкогольнаяПродукция.Видимость = ЗначениеЗаполнено(АлкогольнаяПродукция);

	Если ТипЗнч(Параметры.Номенклатура) = Тип("Массив") Тогда
		
		Элементы.Номенклатура.РежимВыбораИзСписка = Истина;
		Элементы.Номенклатура.СписокВыбора.ЗагрузитьЗначения(Параметры.Номенклатура);
		
	Иначе
		
		Номенклатура   = Параметры.Номенклатура;
		Характеристика = Параметры.Характеристика;
		Серия          = Параметры.Серия;
		НоменклатураПриИзмененииСервер();
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Номенклатура) И Не Параметры.ПроизвольноеРедактированиеРеквизитов Тогда
		Элементы.Номенклатура.ТолькоПросмотр = Истина;
	Иначе
		ОбновитьОтображениеПоляНоменклатура();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Характеристика) И Не Параметры.ПроизвольноеРедактированиеРеквизитов
		Или ЭтоЧастичноеВыбытие(ЭтотОбъект) Тогда
		Элементы.Характеристика.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ПредставлениеНоменклатуры) Тогда
		Элементы.Номенклатура.ПодсказкаВвода = Параметры.ПредставлениеНоменклатуры;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ГосИС.ИСМП") Тогда
		ВидимостьШаблонаЭтикетки  = РегистрыСведений["ПулКодовМаркировкиСУЗ"].ВидимостьШаблонаЭтикетки();
	КонецЕсли;
	
	Если Параметры.СоставКодаМаркировки = Неопределено Тогда
		ДанныеРазбора = РазборКодаМаркировкиИССлужебный.РазобратьКодМаркировки(КодМаркировки, ПараметрыСканирования.ДопустимыеВидыПродукции);
		Если ДанныеРазбора <> Неопределено Тогда
			СоставКодаМаркировки = Новый ФиксированнаяСтруктура(ДанныеРазбора.СоставКодаМаркировки);
		КонецЕсли;
	Иначе
		СоставКодаМаркировки = ОбщегоНазначения.СкопироватьРекурсивно(Параметры.СоставКодаМаркировки, Истина);
	КонецЕсли;
	ЭтоКодМаркировкиСоСрокомГодности = СоставКодаМаркировки <> Неопределено
		И СоставКодаМаркировки.Свойство("ГоденДо");
	
	Если Не ПустаяСтрока(ШтрихкодEAN) И Не ЗначениеЗаполнено(GTIN) Тогда
		GTIN = ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.GTINПоШтрихкодуEAN(ШтрихкодEAN);
	КонецЕсли;
	
	ЗаполнитьСерииПоДокументу();
	ЗаполнитьИдентификаторыПроисхожденияВЕТИС();
	
	// Заполнение значений по-умолчанию: в источнике вызова есть ровно одна подходящая строка с незаполненными кодами маркировки
	Если ДанныеДокумента.Количество() = 1 Тогда
		ДанныеДокумента[0].Выбрать = Истина;
		ИзменениеНоменклатуры = (ДанныеДокумента[0].Номенклатура <> Номенклатура);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеДокумента[0],,"Количество");
		ГоденДо = ДанныеДокумента[0].СрокГодности;
		Если ИзменениеНоменклатуры Тогда
			НоменклатураПриИзмененииСервер();
		ИначеЕсли ЗначениеЗаполнено(ИдентификаторПроисхожденияВЕТИС) Тогда
			СкоропортящаясяПродукция = ПризнакСкоропортящаясяПродукция(ИдентификаторПроисхожденияВЕТИС);
		КонецЕсли;
	КонецЕсли;
	
	Элементы.КодМаркировки.Видимость = Ложь;
	
	ЗаполнитьСрокГодности();
	ЗаполнитьДанныеДляПечатиЭтикеток();
	ПервоначальноеЗаполнениеПоЧастичномуВыбытию();
	
	Если ПараметрыСканирования.Свойство("ИндивидуализироватьКИЗ")
		И ПараметрыСканирования.ИндивидуализироватьКИЗ Тогда
		
		ДанныеRFID = Параметры.ДанныеRFID;
		
		ОбновитьСтатусRFID(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРежимВыбораИзДокумента()
	
	Колонки = Новый Массив;
	
	Элементы.СтраницаУказаниеДанных.Видимость = Параметры.РежимПроизвольногоВвода;
	Элементы.СтраницаВыборИзСписка.Видимость  = Параметры.РежимПодбораИзДокумента;
	Элементы.ДанныеДокумента.Видимость        = Параметры.РежимПодбораИзДокумента;
	Если Параметры.РежимПодбораИзДокумента Тогда
		Если Параметры.ДанныеДокумента.Количество() Тогда
			КолонкиКЗаполнению = Новый Массив;
			Элементы.РежимыВыбора.ТекущаяСтраница = Элементы.СтраницаВыборИзСписка;
			ПредставлениеИзвестныхДанныхМассивом = Новый Массив;
			Элемент = Параметры.ДанныеДокумента[0];
			// Настроим колонки таблицы выбора
			Для Каждого Колонка Из ПолучитьРеквизиты("ДанныеДокумента") Цикл
				Если Колонка.Имя = "Выбрать"
					Или Колонка.Имя = "ПредставлениеПустого" Тогда
				ИначеЕсли Не Элемент.Свойство(Колонка.Имя) Тогда
					Элементы["ДанныеДокумента"+Колонка.Имя].Видимость = Ложь;
					Если (Колонка.Имя = "Номенклатура"
						Или Колонка.Имя = "Характеристика"
						Или Колонка.Имя = "Серия")
						И ЗначениеЗаполнено(Параметры[Колонка.Имя]) Тогда
							Если ПредставлениеИзвестныхДанныхМассивом.Количество() Тогда
								ПредставлениеИзвестныхДанныхМассивом.Добавить(" / ");
							КонецЕсли;
							ПредставлениеИзвестныхДанныхМассивом.Добавить(Новый ФорматированнаяСтрока(
								""+Параметры[Колонка.Имя], , ЦветаСтиля.ЦветГиперссылкиГосИС, , ПолучитьНавигационнуюСсылку(Параметры[Колонка.Имя])));
					КонецЕсли;
				Иначе
					
					Колонки.Добавить(Колонка.Имя);
					КолонкиКЗаполнению.Добавить(Колонка.Заголовок);
				КонецЕсли;
			КонецЦикла;
			
			Элементы.ДанныеДокументаВыбрать.Заголовок = СтрСоединить(КолонкиКЗаполнению, ",");
			
			Если ПредставлениеИзвестныхДанныхМассивом.Количество() Тогда
				ПредставлениеИзвестныхДанных = Новый ФорматированнаяСтрока(ПредставлениеИзвестныхДанныхМассивом);
			Иначе
				Элементы.ПредставлениеИзвестныхДанных.Видимость = Ложь;
			КонецЕсли;
			// Загрузим данные документа для выбора
			Для Каждого ВариантВыбора Из Параметры.ДанныеДокумента Цикл
				НоваяСтрока = ДанныеДокумента.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Параметры);
				НоваяСтрока.СрокГодности = Параметры.ГоденДо;
				Если ЗначениеЗаполнено(Параметры.ИдентификаторыПроисхожденияВЕТИС) Тогда
					НоваяСтрока.ИдентификаторПроисхожденияВЕТИС = Параметры.ИдентификаторыПроисхожденияВЕТИС;
				КонецЕсли;
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВариантВыбора);
			КонецЦикла;
			ТаблицаДанныеДокумента = ДанныеДокумента.Выгрузить();
			ТаблицаДанныеДокумента.Свернуть("Номенклатура,Характеристика,Серия,ИдентификаторПроисхожденияВЕТИС,СрокГодности","Количество");
			ДанныеДокумента.Загрузить(ТаблицаДанныеДокумента);
			
			Для Каждого СтрокаДанныеДокумента Из ДанныеДокумента Цикл
				ЭтоПустаяСтрока = Истина;
				Для Каждого ВидимаяКолонка Из Колонки Цикл
					ЭтоПустаяСтрока = ЭтоПустаяСтрока И Не ЗначениеЗаполнено(СтрокаДанныеДокумента[ВидимаяКолонка]);
				КонецЦикла;
				Если ЭтоПустаяСтрока Тогда
					СтрокаДанныеДокумента.ПредставлениеПустого = НСтр("ru = '<без уточнения>'");
				КонецЕсли;
			КонецЦикла;
			Если ДанныеДокумента.Количество() = 1 Тогда
				Элементы.РежимыВыбора.ТекущаяСтраница = Элементы.СтраницаУказаниеДанных;
			КонецЕсли;
		Иначе
			Элементы.СтраницаУказаниеДанных.Видимость = Истина;
			Элементы.СтраницаВыборИзСписка.Видимость = Ложь;
			Элементы.ДанныеДокумента.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если Не(Параметры.РежимПроизвольногоВвода И Параметры.РежимПодбораИзДокумента) Тогда
		Элементы.РежимыВыбора.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИдентификаторыПроисхожденияВЕТИС()
	
	УказатьИдентификаторВЕТИС = ПараметрыСканирования.Свойство("ЗаполнятьДанныеВЕТИС")
		И ПараметрыСканирования.ЗаполнятьДанныеВЕТИС;
	
	Если Не УказатьИдентификаторВЕТИС Тогда
		Возврат;
	Иначе
		Элементы.ИдентификаторПроисхожденияВЕТИС.АвтоОтметкаНезаполненного = ПараметрыСканирования.ЗаполнятьДанныеВЕТИС;
	КонецЕсли;
	
	ИдентификаторыВЕТИС = Новый Массив;
	
	Если ТипЗнч(Параметры.ИдентификаторыПроисхожденияВЕТИС) = Тип("Массив") Тогда
		ИдентификаторыВЕТИС = Параметры.ИдентификаторыПроисхожденияВЕТИС;
	ИначеЕсли ЗначениеЗаполнено(Параметры.ИдентификаторыПроисхожденияВЕТИС) Тогда
		ИдентификаторПроисхожденияВЕТИС = Параметры.ИдентификаторыПроисхожденияВЕТИС;
		ИдентификаторыВЕТИС.Добавить(ИдентификаторПроисхожденияВЕТИС);
	ИначеЕсли ЗначениеЗаполнено(Параметры.ДанныеДокумента) Тогда
		Для Каждого СтрокаДокумента Из Параметры.ДанныеДокумента Цикл
			Если СтрокаДокумента.Свойство("ИдентификаторПроисхожденияВЕТИС")
				И ИдентификаторыВЕТИС.Найти(СтрокаДокумента.ИдентификаторПроисхожденияВЕТИС) = Неопределено Тогда
				ИдентификаторыВЕТИС.Добавить(СтрокаДокумента.ИдентификаторПроисхожденияВЕТИС);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ДобавитьИдентификаторыВЕТИСВСписокВыбора(ИдентификаторыВЕТИС, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСрокГодности()
	
	УказатьСрокГодности = ПараметрыСканирования.Свойство("ЗаполнятьСрокГодности")
		И ПараметрыСканирования.ЗаполнятьСрокГодности;
	
	Если Не УказатьСрокГодности Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ГоденДо) Тогда
		ГоденДо         = Параметры.ГоденДо;
		СкоропортящаясяПродукция = Параметры.СкоропортящаясяПродукция;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьИдентификаторыВЕТИСВСписокВыбора(Идентификаторы, РежимОткрытияСпискаВыбора = Ложь)
	
	Если Идентификаторы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Модуль = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияИСМПВЕТИС");
	ДанныеИдентификаторовПроисхождения = Модуль.ДанныеИдентификаторовПроисхождения(Идентификаторы);
	
	Для Каждого КлючИЗначение Из ДанныеИдентификаторовПроисхождения Цикл
		
		ПредставлениеСсылки = СтрШаблон(НСтр("ru = '%1'"), КлючИЗначение.Значение.Представление);
		Элементы.ИдентификаторПроисхожденияВЕТИС.СписокВыбора.Вставить(0, КлючИЗначение.Ключ, ПредставлениеСсылки);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПризнакСкоропортящаясяПродукция(ИдентификаторПроисхожденияВЕТИС)
	ДанныеВЕТИС = ДанныеИдентификаторовПроисхожденияВЕТИС(ИдентификаторПроисхожденияВЕТИС);
	Возврат ДанныеВЕТИС[ИдентификаторПроисхожденияВЕТИС].СкоропортящаясяПродукция;
КонецФункции

&НаСервере
Процедура ЗаполнитьДанныеДляПечатиЭтикеток()
	
	Если Не РежимПечатиЭтикеток
		И Параметры.Свойство("ПолныйКодМаркировки")
		И ПараметрыСканирования.Свойство("ВидОперацииИСМП")
		И (ПараметрыСканирования.ВидОперацииИСМП = Перечисления.ВидыОперацийИСМП.ОтчетОПеревзвешивании
			Или ПараметрыСканирования.ВидОперацииИСМП = Перечисления.ВидыОперацийИСМП.КорректировкаСведенийКМФактическийВес)
		И ЗначениеЗаполнено(КодМаркировки)
		И РегистрыСведений.ПулКодовМаркировкиСУЗ.КодМаркировкиСодержитВес(Параметры.ПолныйКодМаркировки) Тогда
		РежимПечатиЭтикеток = Истина;
	КонецЕсли;
	
	СразуНаПринтер = Параметры.СразуНаПринтер;
	ШаблонЭтикетки = Параметры.ШаблонЭтикетки;
	ШаблонКодаМаркировки = Параметры.ШаблонКодаМаркировки;
	
	Если Не РежимПечатиЭтикеток Тогда
		Если ЗначениеЗаполнено(ШаблонЭтикетки)
				И ПараметрыСканирования.ИспользуютсяДанныеВыбораПоМаркируемойПродукции Тогда
			ЗапомнитьВыбор = Истина;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидПродукции) Тогда
		ВидПродукцииНоменклатуры = ВидПродукции;
	ИначеЕсли ЗначениеЗаполнено(Номенклатура) Тогда
		ВидПродукцииНоменклатуры = ИнтеграцияИС.ВидПродукцииПоНоменклатуре(Номенклатура);
	КонецЕсли;
	
	Элементы.ШаблонКодаМаркировки.СписокВыбора.Очистить();
	
	Если ЗначениеЗаполнено(ВидПродукцииНоменклатуры) Тогда
		Если ЗначениеЗаполнено(Параметры.Шаблоны) Тогда
			
			ДоступныеШаблоны = ИнтеграцияИСМПКлиентСервер.ШаблоныКодовПоВидуПродукции(ВидПродукцииНоменклатуры, 3);
			
			Для Каждого ЗначениеШаблона Из Параметры.Шаблоны Цикл
				ЭлементСпискаЗначений = ДоступныеШаблоны.НайтиПоЗначению(ЗначениеШаблона);
				Если ЭлементСпискаЗначений <> Неопределено Тогда
					Элементы.ШаблонКодаМаркировки.СписокВыбора.Добавить(
						ЭлементСпискаЗначений.Значение, ЭлементСпискаЗначений.Представление);
				КонецЕсли;
			КонецЦикла;
			
			Элементы.ШаблонКодаМаркировки.РежимВыбораИзСписка = Истина;
			
			Если ЗначениеЗаполнено(Параметры.ШаблонКодаМаркировки)
				И Элементы.ШаблонКодаМаркировки.СписокВыбора.НайтиПоЗначению(Параметры.ШаблонКодаМаркировки) <> Неопределено Тогда
				ШаблонКодаМаркировки = Параметры.ШаблонКодаМаркировки;
			ИначеЕсли Элементы.ШаблонКодаМаркировки.СписокВыбора.Количество() > 0 Тогда
				ШаблонКодаМаркировки = Элементы.ШаблонКодаМаркировки.СписокВыбора.Получить(0).Значение;
			КонецЕсли;
			
		ИначеЕсли ОбщегоНазначенияИСПовтИсп.ЭтоПродукцияИСМП(ВидПродукцииНоменклатуры, Истина) Тогда
			Модуль = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияИСМП");
			Модуль.НастроитьШаблоныФормыУточненияДанных(ЭтотОбъект, ВидПродукцииНоменклатуры);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьДоступностьЭлементов()
	
	Элементы.ДекорацияУдалосьПодобрать.Видимость = Ложь;
	
	Если УказатьСрокГодности Тогда
		Элементы.ГруппаСрокГодности.Видимость = Истина;
		Если ИнтеграцияИСМПСлужебныйКлиентСервер.ДоступенСрокГодностиПриПриемкеОтгрузке(ПараметрыСканирования.ВидОперацииИСМП, ВидПродукции) Тогда
			Элементы.ГоденДо.АвтоОтметкаНезаполненного =
				ИнтеграцияИСМПСлужебныйКлиентСервер.ТребуетсяУказаниеСрокаГодностиПриПриемкеОтгрузке(ПараметрыСканирования.ВидОперацииИСМП, ВидПродукции);
		ИначеЕсли ИнтеграцияИСМПКлиентСервер.ЭтоВидПродукцииСоСрокамиГодностиБезОбязательногоЗаполнения(ВидПродукции) Тогда
			Элементы.ГоденДо.АвтоОтметкаНезаполненного = Ложь;
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(ГоденДо) Тогда
		Элементы.ГруппаСрокГодности.Видимость = Истина;
		Элементы.ГруппаСрокГодности.ТолькоПросмотр = Истина;
	Иначе
		Элементы.ГруппаСрокГодности.Видимость = Ложь;
	КонецЕсли;
	
	НастроитьЭлементыФормыЧастичногоВыбытия(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеПоляНоменклатура()
	
	Если ВидПродукции = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха Тогда
		
		Элементы.Номенклатура.ПодсказкаВвода            = "";
		Элементы.Номенклатура.АвтоОтметкаНезаполненного = Истина;
		
	ИначеЕсли ПараметрыСканирования.РазрешенаОбработкаКодовСПустойНоменклатурой
		И Не ПолученоПредставлениеGTIN Тогда
		
		ДанныеДляПолученияПредставленияGTINОстатков = Новый ТаблицаЗначений;
		ДанныеДляПолученияПредставленияGTINОстатков.Колонки.Добавить("Номенклатура",             Метаданные.ОпределяемыеТипы.Номенклатура.Тип);
		ДанныеДляПолученияПредставленияGTINОстатков.Колонки.Добавить("GTIN",                     Метаданные.ОпределяемыеТипы.GTIN.Тип);
		ДанныеДляПолученияПредставленияGTINОстатков.Колонки.Добавить("ПредставлениеGTINОстатки", Новый ОписаниеТипов("Строка"));
		ДанныеДляПолученияПредставленияGTINОстатков.Колонки.Добавить("ВидПродукции",             Новый ОписаниеТипов("ПеречислениеСсылка.ВидыПродукцииИС"));
		
		СтрокаТаблицы = ДанныеДляПолученияПредставленияGTINОстатков.Добавить();
		СтрокаТаблицы.GTIN = GTIN;
		
		ВидПродукцииДляПолученияПредставления = Неопределено;
		Если ПараметрыСканирования.ДопустимыеВидыПродукции.Количество() = 1 Тогда
			ВидПродукцииДляПолученияПредставления = ПараметрыСканирования.ДопустимыеВидыПродукции[0];
		КонецЕсли;
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ГосИС.ИСМП") Тогда
			РегистрыСведений["КэшОписанияОстатковИСМП"].ЗаполнитьТаблицуПредставленийGTINОстатки(
				ДанныеДляПолученияПредставленияGTINОстатков,, ВидПродукцииДляПолученияПредставления);
		КонецЕсли;
			
		Если ДанныеДляПолученияПредставленияGTINОстатков.Количество() > 0 Тогда
			ПредставлениеGTIN = ДанныеДляПолученияПредставленияGTINОстатков[0].ПредставлениеGTINОстатки;
		Иначе
			ПредставлениеGTIN = GTIN;
		КонецЕсли;
		
		Элементы.Номенклатура.ПодсказкаВвода            = ПредставлениеGTIN;
		Элементы.Номенклатура.АвтоОтметкаНезаполненного = Ложь;
		
		ПолученоПредставлениеGTIN = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементовПриСозданииНаСервере()
	
	ОбновитьВидимостьДоступностьЭлементов();
	
	Элементы.ГруппаПечатьЭтикеток.Видимость = РежимПечатиЭтикеток;
	Если РежимПечатиЭтикеток Тогда
		Элементы.ЗавершитьРедактирование.Видимость = Ложь;
		Элементы.Печать.Видимость                  = Истина;
		Элементы.Печать.КнопкаПоУмолчанию          = Истина;
	КонецЕсли;
	Элементы.ШаблонЭтикетки.Видимость = ВидимостьШаблонаЭтикетки;
	
	Элементы.ШаблонКодаМаркировки.Видимость = Элементы.ШаблонКодаМаркировки.СписокВыбора.Количество() > 1
		Или Не ЗначениеЗаполнено(ШаблонКодаМаркировки);
	
	Если УказатьИдентификаторВЕТИС Тогда
		Элементы.ГруппаДанныеВЕТИС.Видимость = Истина;
	ИначеЕсли ЗначениеЗаполнено(ИдентификаторПроисхожденияВЕТИС) Тогда
		Элементы.ГруппаДанныеВЕТИС.Видимость = Истина;
		Элементы.ГруппаДанныеВЕТИС.ТолькоПросмотр = Истина;
	Иначе
		Элементы.ГруппаДанныеВЕТИС.Видимость = Ложь;
	КонецЕсли;
	
	Если УказатьСрокГодности И ЭтоКодМаркировкиСоСрокомГодности Тогда
		
		Элементы.ГоденДо.ТолькоПросмотр = Истина;
		
		Если УказатьИдентификаторВЕТИС Тогда
			Элементы.ИдентификаторПроисхожденияВЕТИС.ПараметрыВыбора = Новый ФиксированныйМассив(
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
					Новый ПараметрВыбора("Отбор.СкоропортящаясяПродукция", СкоропортящаясяПродукция)));
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ПараметрыСканирования.ИспользуютсяДанныеВыбораПоМаркируемойПродукции
		Или МожетВыбыватьЧастично Тогда
		Элементы.ГруппаЗапомнитьВыбор.Видимость = Ложь;
	КонецЕсли;
	
	Если Элементы.Номенклатура.ТолькоПросмотр
			И Не ТребуетВзвешивания Тогда
		Элементы.ПоискПоШтрихкоду.Видимость = Ложь;
	КонецЕсли;
	
	// Используется для добавления кодов ОСУ
	Элементы.КоличествоПотребительскихУпаковок.Видимость = УказатьКоличествоПотребительскихУпаковок;
	
	// Используется для добавления кодов ОСУ и индивидуализации КИЗ меха
	Элементы.GTIN.Видимость       = УказатьGTIN;
	
	Если ПараметрыСканирования.Свойство("ИндивидуализироватьКИЗ") Тогда
		Элементы.ГруппаRFID.Видимость = ПараметрыСканирования.ИндивидуализироватьКИЗ;
	Иначе
		Элементы.ГруппаRFID.Видимость = Ложь;
	КонецЕслИ;
	
	Элементы.ЗаводскойСерийныйНомер.Видимость = РежимПечатиЭтикеток
		И (ВидПродукции = Перечисления.ВидыПродукцииИС.КреслаКоляски
			Или ВидПродукции = Перечисления.ВидыПродукцииИС.ТехническиеСредстваРеабилитации
			Или ВидПродукции = Перечисления.ВидыПродукцииИС.МедицинскиеИзделия);
	
	Элементы.КодМаркировки.Видимость = (КоличествоВскрытыхПотребительскихУпаковок > 1);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСерииПоДокументу()
	
	Если Не Элементы.Серия.Видимость Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеТаблицыТовары = Неопределено;
	Если ЭтоАдресВременногоХранилища(ПараметрыСканирования.АдресДанныхДокументаОснования) Тогда
		ДанныеТаблицыТовары = ПолучитьИзВременногоХранилища(ПараметрыСканирования.АдресДанныхДокументаОснования);
	ИначеЕсли ЭтоАдресВременногоХранилища(ПараметрыСканирования.ДанныеТаблицыТовары) Тогда
		ДанныеТаблицыТовары = ПолучитьИзВременногоХранилища(ПараметрыСканирования.ДанныеТаблицыТовары);
	Иначе
		Возврат;
	КонецЕсли;
	
	Элементы.Серия.КнопкаВыпадающегоСписка = Истина;
	ДоступныеСерии = ДоступныеСерииПоДаннымТаблицыТовары(ДанныеТаблицыТовары);
	
	Для Каждого Серия Из ДоступныеСерии Цикл
		Если ЗначениеЗаполнено(Серия) Тогда
			Элементы.Серия.СписокВыбора.Добавить(Серия);
		Иначе
			Элементы.Серия.СписокВыбора.Добавить(Серия, НСтр("ru='<Неопределена>'"));
		КонецЕсли;
	КонецЦикла;
	
	Если ДоступныеСерии.Количество() = 1 Тогда
		Серия = ДоступныеСерии[0];
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДоступныеСерииПоДаннымТаблицыТовары(ДанныеТаблицыТовары)
	
	МассивДоступныхСерий = Новый Массив;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Номенклатура", Номенклатура);
	Если ИнтеграцияИС.ХарактеристикиИспользуются() Тогда
		Отбор.Вставить("Характеристика", Характеристика);
	КонецЕсли;
	
	НайденныеСтроки = ДанныеТаблицыТовары.НайтиСтроки(Отбор);
	
	Для Каждого Строка Из НайденныеСтроки Цикл
		МассивДоступныхСерий.Добавить(Строка.Серия);
	КонецЦикла;
	
	МассивДоступныхСерий = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивДоступныхСерий);
	
	Возврат МассивДоступныхСерий;
	
КонецФункции

&НаКлиенте
Процедура ВводШтрихкодаЗавершение(ДанныеШтрихкода, ДополнительныеПараметры = Неопределено) Экспорт
	
	ТекстОшибки = "";
	ДанныеШтрихкода.Штрихкод = ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.ШтрихкодВBase64(ДанныеШтрихкода.Штрихкод);
	ДанныеШтрихкода.Вставить("ШтрихкодBase64", Истина);
	
	ШтрихкодНайден = Ложь;
	ОбработатьШтрихкод(ДанныеШтрихкода, ШтрихкодНайден, ТекстОшибки);
	
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		
		ПараметрыОткрытия = ШтрихкодированиеИСКлиент.ПараметрыОткрытияФормыНевозможностиДобавленияОтсканированного();
		
		Если ТипЗнч(ТекстОшибки) = Тип("ФорматированнаяСтрока") Тогда
			ПараметрыОткрытия.ТекстОшибкиФорматированнаяСтрока = ТекстОшибки;
		Иначе
			ПараметрыОткрытия.ТекстОшибки = ТекстОшибки;
		КонецЕсли;
		
		ШтрихкодированиеИСКлиент.ОткрытьФормуНевозможностиДобавленияОтсканированного(ЭтотОбъект, ПараметрыОткрытия);
		
		Возврат;
		
	КонецЕсли;
	
	Если Не ШтрихкодНайден Тогда
		
		Штрихкоды = Новый Массив;
		Штрихкоды.Добавить(ДанныеШтрихкода);
		
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПослеСопоставленияНеизвестныхШтрихкодов", ЭтотОбъект);
		ШтрихкодированиеИСКлиентПереопределяемый.ОткрытьФормуПодбораНоменклатурыПоШтрихкодам(
			Штрихкоды, ЭтотОбъект, ОповещениеОЗакрытии);
		
		Возврат;
		
	КонецЕсли;
	
	Если ЗакрытьПослеСканированияШтрихкодаНоменклатуры() Тогда
		ПодключитьОбработчикОжидания("ЗакрытьФорму", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьШтрихкод(ДанныеШтрихкода, ШтрихкодНайден, ТекстОшибки = "")
	
	Если Не ДопустимыйФорматШтрихкода(ДанныеШтрихкода) Тогда
		ТекстОшибки = НСтр("ru='Недопустимый формат штрихкода'");
		Возврат;
	КонецЕсли;
	
	EAN = ДанныеШтрихкода.Штрихкод;
	Если МенеджерОборудованияКлиентСервер.ПроверитьКорректностьGTIN(ДанныеШтрихкода.Штрихкод) Тогда
		EAN = РазборКодаМаркировкиИССлужебныйКлиентСервер.ШтрихкодEANИзGTIN(ДанныеШтрихкода.Штрихкод);
	КонецЕсли;
	
	Штрихкоды = Новый Массив;
	Штрихкоды.Добавить(EAN);
	ДанныеПоШтрихкодамEAN = ШтрихкодированиеОбщегоНазначенияИС.ДанныеПоШтрихкодамEAN(Штрихкоды);
	
	ДопустимыеДанныеПоШтрихкодамEAN = ДанныеПоШтрихкодамEAN.СкопироватьКолонки();
	// Номенклатура уже выбрана. Можно предупреждать о несоответствии.
	Если Элементы.Номенклатура.ТолькоПросмотр Тогда
		Для Каждого СтрокаТЧ Из ДанныеПоШтрихкодамEAN Цикл
			Если СтрокаТЧ.Номенклатура = Номенклатура
				И СтрокаТЧ.Характеристика = Характеристика Тогда
				ЗаполнитьЗначенияСвойств(ДопустимыеДанныеПоШтрихкодамEAN.Добавить(), СтрокаТЧ);
			КонецЕсли;
		КонецЦикла;
	// Проверим известный вид продукции или хотя бы признак маркируемой продукции
	Иначе
		Для Каждого СтрокаТЧ Из ДанныеПоШтрихкодамEAN Цикл
			Подходит = СтрокаТЧ.МаркируемаяПродукция;
			Если ПараметрыСканирования.ДопустимыеВидыПродукции.Количество() Тогда
				Подходит = Подходит И ПараметрыСканирования.ДопустимыеВидыПродукции.Найти(СтрокаТЧ.ВидПродукции) <> Неопределено;
			КонецЕсли;
			Если Подходит Тогда
				ЗаполнитьЗначенияСвойств(ДопустимыеДанныеПоШтрихкодамEAN.Добавить(), СтрокаТЧ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ДанныеПоШтрихкодамEAN.Количество() = 0
		Или Не ЗначениеЗаполнено(ДанныеПоШтрихкодамEAN[0].Номенклатура) Тогда
		ШтрихкодНайден = Ложь;
		Возврат;
	ИначеЕсли ДопустимыеДанныеПоШтрихкодамEAN.Количество() = 0 Тогда
		ТекстОшибки = НСтр("ru='Отсканированный штрихкод не соответствует уточняемой номенклатуре'");
		Возврат;
	КонецЕсли;
	
	ШтрихкодНайден = Истина;
	
	Если ДопустимыеДанныеПоШтрихкодамEAN.Количество() = 1 Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДопустимыеДанныеПоШтрихкодамEAN[0],,"Количество");
		Если ТребуетВзвешивания Тогда
			Количество = ДопустимыеДанныеПоШтрихкодамEAN[0].Количество;
		КонецЕсли;
		
	Иначе
		
		НоменклатураСписок = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ДопустимыеДанныеПоШтрихкодамEAN.ВыгрузитьКолонку("Номенклатура"));
		Элементы.Номенклатура.СписокВыбора.ЗагрузитьЗначения(НоменклатураСписок);
		Номенклатура = НоменклатураСписок[0];
		
	КонецЕсли;
	
	НоменклатураПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСопоставленияНеизвестныхШтрихкодов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.ЗарегистрированныеШтрихкоды.Количество() > 0 Тогда
		ОбработатьШтрихкод(Результат.ЗарегистрированныеШтрихкоды[0], Неопределено);
		
		Если ЗакрытьПослеСканированияШтрихкодаНоменклатуры() Тогда
			ПодключитьОбработчикОжидания("ЗакрытьФорму",0.1,Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДопустимыйФорматШтрихкода(ДанныеШтрихкода)
	
	Если Не ДанныеШтрихкода.Свойство("ШтрихкодBase64") Или Не ДанныеШтрихкода.ШтрихкодBase64 Тогда
		Возврат Истина;
	КонецЕсли;
	
	ЗначениеШтрихкода = ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.Base64ВШтрихкод(ДанныеШтрихкода.Штрихкод);
	Если НайтиНедопустимыеСимволыXML(ЗначениеШтрихкода) > 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДанныеШтрихкода.Штрихкод = ЗначениеШтрихкода;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура УстановитьЗаголовокЭлементаИдентификаторВЕТИС()
	
	Элементы.ИдентификаторПроисхожденияВЕТИС.Заголовок = ИнтеграцияИСМПВЕТИСКлиентСервер.ИмяИдентификатораПроисхожденияВЕТИС();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВидыПродукцииПоКодуМаркировкиОстатков(ВидыПродукции, КодМаркировки, Организация)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ГосИС.ИСМП") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ШтрихкодированиеИСМП");
		ВидыПродукцииКодаМаркировки = Модуль.ВидыПродукцииПоКодуМаркировкиОстатков(КодМаркировки, Организация);
		Если ВидыПродукцииКодаМаркировки <> Неопределено Тогда
			ВидыПродукции = ОбщегоНазначенияИСКлиентСервер.ПересечениеМассивов(ВидыПродукции, ВидыПродукцииКодаМаркировки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#Область Оборудование

&НаКлиенте
Процедура ОтключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если Не РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр( "ru = 'При отключении оборудования произошла ошибка: ""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если Не РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр( "ru = 'При подключении оборудования произошла ошибка:""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЧастичноеВыбытие

&НаСервере
Процедура ПервоначальноеЗаполнениеПоЧастичномуВыбытию()
	
	Если Не ШтрихкодированиеОбщегоНазначенияИС.ТребуетсяЧастичноеВыбытие(ПараметрыСканирования) Тогда
		Возврат;
	КонецЕсли;
	
	ЧастичноеВыбытие                        = Параметры.ЧастичноеВыбытие;
	ЕмкостьПотребительскойУпаковкиНачальная = Параметры.ЕмкостьПотребительскойУпаковки;
	ЧастичноеВыбытиеКоличество              = Параметры.Количество;
	ЧастичноеВыбытиеОстаток                 = Параметры.ЧастичноеВыбытиеОстаток;
	ЧастичноеВыбытиеОстатокГИСМТ            = Параметры.ЧастичноеВыбытиеОстатокГИСМТ;
	ЧастичноеВыбытиеВыведено                = Параметры.ЧастичноеВыбытиеВыведено;
	ЧастичноеВыбытиеНеКорректныйОстатокГИС  = Параметры.ЧастичноеВыбытиеНеКорректныйОстаток;
	ЧастичноеВыбытиеВариантУчета            = Параметры.ЧастичноеВыбытиеВариантУчета;
	ЧастичноеВыбытиеНоменклатура            = Параметры.ЧастичноеВыбытиеНоменклатура;
	ЧастичноеВыбытиеХарактеристика          = Параметры.ЧастичноеВыбытиеХарактеристика;
	Статус                                  = Параметры.Статус;
	ЧастичноеВыбытиеНеКорректныйОстаток     = ЧастичноеВыбытиеНеКорректныйОстатокГИС;
	
	Если ОбщегоНазначенияИСПовтИсп.ЭтоПродукцияИСМП(ВидПродукции, Истина) Тогда

		Если Не ОбщегоНазначения.ПодсистемаСуществует("ГосИС.ИСМП") Тогда
			Возврат;
		КонецЕсли;

		Если МожетВыбыватьЧастично И ЧастичноеВыбытие Тогда

			Штрихкоды = Новый Массив();

			Если ЭтоШтрихкодНоменклатуры Тогда

				МенеджерВскрытыеПотребительскиеУпаковкиИС = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени("РегистрСведений.ВскрытыеПотребительскиеУпаковкиИС");

				ТаблицаТовары = МенеджерВскрытыеПотребительскиеУпаковкиИС.НоваяТаблицаПоискаВскрытыхПотребительскихУпаковок();

				Если ЧастичноеВыбытиеВариантУчета = Перечисления.ВариантыУчетаЧастичногоВыбытияИС.НастроеннаяНоменклатура Тогда
					НоваяСтрока = ТаблицаТовары.Добавить();
					НоваяСтрока.Номенклатура   = ЧастичноеВыбытиеИсходнаяНоменклатура;
					НоваяСтрока.Характеристика = ЧастичноеВыбытиеИсходнаяХарактеристика;
					НоваяСтрока.Серия          = Серия;
					КодыМаркировки = МенеджерВскрытыеПотребительскиеУпаковкиИС.ВскрытыеПотребительскиеУпаковкиПоТаблицеТоваров(
						ТаблицаТовары,
						ПараметрыСканирования,
						Истина);
				Иначе
					НоваяСтрока = ТаблицаТовары.Добавить();
					НоваяСтрока.Номенклатура   = Номенклатура;
					НоваяСтрока.Характеристика = Характеристика;
					НоваяСтрока.Серия          = Серия;
					КодыМаркировки = МенеджерВскрытыеПотребительскиеУпаковкиИС.ВскрытыеПотребительскиеУпаковкиПоТаблицеТоваров(
						ТаблицаТовары,
						ПараметрыСканирования,
						Истина);
				КонецЕсли;

				КодыМаркировкиДляВыбора.Загрузить(КодыМаркировки);

				КоличествоВскрытыхПотребительскихУпаковок = КодыМаркировки.Количество();

				Если КоличествоВскрытыхПотребительскихУпаковок > 1 Тогда

					Элементы.КодМаркировки.СписокВыбора.Очистить();

					Для Каждого СтрокаТаблицы Из КодыМаркировки Цикл

						Если ЗначениеЗаполнено(СтрокаТаблицы.Комментарий) Тогда
							ПредставлениеКодаМаркировки = СтрШаблон(
								"%1 %2",
								СтрокаТаблицы.Комментарий,
								СтрокаТаблицы.КодМаркировкиСтрокой);
						Иначе
							ПредставлениеКодаМаркировки = СтрокаТаблицы.КодМаркировкиСтрокой;
						КонецЕсли;

						Элементы.КодМаркировки.СписокВыбора.Добавить(
							СтрокаТаблицы.КодМаркировкиСтрокой,
							ПредставлениеКодаМаркировки);

					КонецЦикла;

					КодМаркировки = "";

				КонецЕсли;

			КонецЕсли;

			Если ЭтоШтрихкодНоменклатуры Тогда
				Для Каждого СтрокаТаблицы Из КодыМаркировкиДляВыбора Цикл
					ДанныеШтрихкода = Новый Структура();
					ДанныеШтрихкода.Вставить("Штрихкод",   СтрокаТаблицы.КодМаркировкиСтрокой);
					ДанныеШтрихкода.Вставить("Количество", 1);
					Штрихкоды.Добавить(ДанныеШтрихкода);
				КонецЦикла;
			Иначе
				ДанныеШтрихкода = Новый Структура();
				ДанныеШтрихкода.Вставить("Штрихкод",   КодМаркировки);
				ДанныеШтрихкода.Вставить("Количество", 1);
				Штрихкоды.Добавить(ДанныеШтрихкода);
			КонецЕсли;

			ДанныеОстаткаБылиПолучены = (Параметры.ЧастичноеВыбытиеОстаток <> Неопределено);

			Если ОбщегоНазначенияИСМПКлиентСервер.ЗначениеПараметраСканированияЗапрашиватьДанныеСервисаИСМП(ПараметрыСканирования)
				И ДанныеОстаткаБылиПолучены Тогда
				ПараметрыСканированияДанныхШтрихкода = ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыСканирования);
				ОбщегоНазначенияИСМПКлиентСервер.ЗначениеПараметраСканированияЗапрашиватьДанныеСервисаИСМП(ПараметрыСканирования, Ложь);
			Иначе
				ПараметрыСканированияДанныхШтрихкода = ПараметрыСканирования;
			КонецЕсли;

			ДанныеПоШтрихкодам = ШтрихкодированиеОбщегоНазначенияИС.ДанныеПоШтрихкодам(Штрихкоды, ПараметрыСканированияДанныхШтрихкода);

			Для Каждого СтрокаДанных Из ДанныеПоШтрихкодам.ДанныеКодовМаркировки Цикл

				ЕмкостьПотребительскойУпаковки = СтрокаДанных.ЕмкостьПотребительскойУпаковки;
				Номенклатура                   = СтрокаДанных.Номенклатура;
				Характеристика                 = СтрокаДанных.Характеристика;
				ЧастичноеВыбытиеНоменклатура   = СтрокаДанных.ЧастичноеВыбытиеНоменклатура;
				ЧастичноеВыбытиеХарактеристика = СтрокаДанных.ЧастичноеВыбытиеХарактеристика;

				Если ОбщегоНазначенияИСМПКлиентСервер.ЗначениеПараметраСканированияЗапрашиватьДанныеСервисаИСМП(ПараметрыСканированияДанныхШтрихкода)
					И Не ДанныеОстаткаБылиПолучены Тогда

					Если ЭтоШтрихкодНоменклатуры Тогда

						СтруктураПоиска = Новый Структура("КодМаркировки", СтрокаДанных.ШтрихкодУпаковки);
						Для Каждого СтрокаТаблицы Из КодыМаркировкиДляВыбора.НайтиСтроки(СтруктураПоиска) Цикл
							СтрокаТаблицы.ЧастичноеВыбытиеОстаток      = СтрокаДанных.ЧастичноеВыбытиеОстаток;
							СтрокаТаблицы.ЧастичноеВыбытиеОстатокГИСМТ = СтрокаДанных.ЧастичноеВыбытиеОстатокГИСМТ;
							СтрокаТаблицы.Владелец                     = СтрокаДанных.Владелец;
							СтрокаТаблицы.ИННВладельца                 = СтрокаДанных.ИННВладельца;
							СтрокаТаблицы.Статус                       = СтрокаДанных.Статус;
							СтрокаТаблицы.ОсобоеСостояние              = СтрокаДанных.ОсобоеСостояние;
						КонецЦикла;
						
					Иначе
						
						ЧастичноеВыбытиеОстаток                = СтрокаДанных.ЧастичноеВыбытиеОстаток;
						ЧастичноеВыбытиеОстатокГИСМТ           = СтрокаДанных.ЧастичноеВыбытиеОстатокГИСМТ;
						ЧастичноеВыбытиеВыведено               = СтрокаДанных.ЧастичноеВыбытиеВыведено;
						ЧастичноеВыбытиеНеКорректныйОстаток    = СтрокаДанных.ЧастичноеВыбытиеНеКорректныйОстаток;
						ЧастичноеВыбытиеНеКорректныйОстатокГИС = СтрокаДанных.ЧастичноеВыбытиеНеКорректныйОстаток;

					КонецЕсли;

				КонецЕсли;

			КонецЦикла;

		КонецЕсли;
		
	КонецЕсли;
	
	ОбновитьПараметрыЧастичногоВыбытия(Истина);
	
	РасчетОграниченийПолейЧастичногоВыбытия(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПараметрыЧастичногоВыбытия(Инициализация = Ложь)
	
	Если ВидПродукции <> Перечисления.ВидыПродукцииИС.Алкогольная
		И Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ПараметрыСканирования, "ВидОперацииИСМП") Тогда
		Возврат;
	КонецЕсли;

	ДанныеЧастичногоВыбытияПолученыИзГИС = ОбщегоНазначенияИСКлиентСерверПовтИсп.ЗапрашиватьДанныеЧастичногоВыбытияИзСервиса(
		ВидПродукции,
		ПараметрыСканирования.РасширеннаяВерсияГосИС);

	ОперацияИС = ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.ОперацияИС(ПараметрыСканирования, ВидПродукции);
	МожетВыбыватьЧастично = ЗначениеЗаполнено(ЧастичноеВыбытиеВариантУчета)
		И ОбщегоНазначенияИСКлиентСерверПовтИсп.ПоддерживаетсяЧастичноеВыбытие(ВидПродукции, ОперацияИС);

	ВыбытиеБутылкиДоступно = МожетВыбыватьЧастично
		И ВидПродукции = Перечисления.ВидыПродукцииИС.Алкогольная
		И (Статус = Перечисления["СтатусыАкцизныхМарок"].ВскрытаяБутылка
			Или ЧастичноеВыбытиеВыведено > 0);

	Если Не МожетВыбыватьЧастично Тогда
		Возврат;
	КонецЕсли;
	
	Если Инициализация Тогда
		
		Если ДанныеЧастичногоВыбытияПолученыИзГИС Тогда
			ЕмкостьПотребительскойУпаковкиГИСМТ = ЕмкостьПотребительскойУпаковкиНачальная;
		КонецЕсли;
		РежимВыбытия = Элементы.РежимВыбытия.СписокВыбора[0].Значение;
	
		ЗаголовокРежимаВыбытия = ОбщегоНазначенияИСКлиентСерверПовтИсп.ЗаголовокРежимаВыбытияПоВидуОперации(ОперацияИС);
		
		Если ЗначениеЗаполнено(ЗаголовокРежимаВыбытия) Тогда
			Элементы.РежимВыбытия.Заголовок = ЗаголовокРежимаВыбытия;
		КонецЕсли;
		
		Для Каждого ЭлементСписка Из Элементы.РежимВыбытия.СписокВыбора Цикл
			
			ПредставлениеРежима = ОбщегоНазначенияИСКлиентСерверПовтИсп.ПредставлениеРежимаВыбытияПоВидуПродукции(
				ЭлементСписка.Значение,
				ВидПродукции);
			
			Если ЗначениеЗаполнено(ПредставлениеРежима) Тогда
				ЭлементСписка.Представление = ПредставлениеРежима;
			КонецЕсли;
			
		КонецЦикла;
		
		Элементы.Характеристика.ТолькоПросмотр = Истина;
		
		Если ЧастичноеВыбытиеВариантУчета = Перечисления.ВариантыУчетаЧастичногоВыбытияИС.ТекущаяНоменклатура Тогда
		
			УпаковкиНоменклатуры = ИнтеграцияИС.УпаковкиНоменклатуры(Номенклатура);
			Элементы.ЧастичноеВыбытиеКоличество.СписокВыбора.Очистить();
			
			Для Каждого СтрокаТаблицы Из УпаковкиНоменклатуры Цикл
				Если Не ЗначениеЗаполнено(СтрокаТаблицы.Упаковка)
					Или СтрокаТаблицы.ЯвляетсяПотребительской
					Или СтрокаТаблицы.ЯвляетсяУпаковкойЧастичногоВыбытия Тогда
					Продолжить;
				КонецЕсли;
				Элементы.ЧастичноеВыбытиеКоличество.СписокВыбора.Добавить(
					СтрокаТаблицы.Коэффициент,
					СтрокаТаблицы.ПредставлениеУпаковки);
			КонецЦикла;
			
			Элементы.ЧастичноеВыбытиеКоличество.КнопкаВыпадающегоСписка = Булево(Элементы.ЧастичноеВыбытиеКоличество.СписокВыбора.Количество());
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПриИзмененииРежимаВыбытия();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОписаниеНоменклатуры()
	
	ОписаниеНоменклатуры = ОбщегоНазначенияИС.ОписаниеНоменклатуры(Номенклатура)[Номенклатура];
	ЕмкостьПотребительскойУпаковкиИБ   = ОписаниеНоменклатуры.ЕмкостьПотребительскойУпаковки;
	ЕмкостьПотребительскойУпаковки     = ОписаниеНоменклатуры.ЕмкостьПотребительскойУпаковки;
	КоличествоВПотребительскойУпаковке = ОписаниеНоменклатуры.КоличествоВПотребительскойУпаковке;
	ЧастичноеВыбытиеВариантУчета       = ОписаниеНоменклатуры.ВариантЧастичногоВыбытия;
	Если Не ДанныеЧастичногоВыбытияПолученыИзГИС Тогда
		ЕмкостьПотребительскойУпаковкиНачальная = ЕмкостьПотребительскойУпаковки;
	КонецЕсли;
	ЧастичноеВыбытиеИсходнаяНоменклатура   = Номенклатура;
	ЧастичноеВыбытиеИсходнаяХарактеристика = Характеристика;
	
	Если ЧастичноеВыбытиеВариантУчета = Перечисления.ВариантыУчетаЧастичногоВыбытияИС.НастроеннаяНоменклатура Тогда
		
		НастройкиЧастичногоВыбытия = РегистрыСведений.НастройкиЧастичногоВыбытияПродукцииИС.ПолучитьОписание(Номенклатура, Характеристика);
		Если НастройкиЧастичногоВыбытия.НоменклатураНастроена Тогда
			ЧастичноеВыбытиеНоменклатура   = НастройкиЧастичногоВыбытия.НоменклатураЧастичногоВыбытия;
			ЧастичноеВыбытиеХарактеристика = НастройкиЧастичногоВыбытия.ХарактеристикаЧастичногоВыбытия;
		КонецЕсли;
		
		Если Элементы.Номенклатура.РежимВыбораИзСписка
			И Элементы.Номенклатура.СписокВыбора.НайтиПоЗначению(ЧастичноеВыбытиеНоменклатура) = Неопределено Тогда
			Элементы.Номенклатура.СписокВыбора.Добавить(ЧастичноеВыбытиеНоменклатура);
		КонецЕсли;
		
	КонецЕсли;

	Если ВидПродукции = Перечисления.ВидыПродукцииИС.Алкогольная
		И ЗначениеЗаполнено(Номенклатура)
		И ЧастичноеВыбытиеОстаток <> Неопределено
		И ЕмкостьПотребительскойУпаковки > 0 Тогда
		ЧастичноеВыбытиеОстаток = ЕмкостьПотребительскойУпаковки;
	КонецЕсли;

	Если ЗначениеЗаполнено(ОписаниеНоменклатуры.УпаковкаЧастичногоВыбытия) Тогда
		ЕдиницаИзмерения = ОписаниеНоменклатуры.УпаковкаЧастичногоВыбытия;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииРежимаВыбытия()
	
	Если ЧастичноеВыбытиеВариантУчета = Перечисления.ВариантыУчетаЧастичногоВыбытияИС.НастроеннаяНоменклатура Тогда
		
		ТаблицаНоменклатуры = Новый ТаблицаЗначений();
		ТаблицаНоменклатуры.Колонки.Добавить("НомерСтроки",                ОбщегоНазначения.ОписаниеТипаЧисло(10));
		ТаблицаНоменклатуры.Колонки.Добавить("Номенклатура",               Метаданные.ОпределяемыеТипы.Номенклатура.Тип);
		ТаблицаНоменклатуры.Колонки.Добавить("Характеристика",             Метаданные.ОпределяемыеТипы.ХарактеристикаНоменклатуры.Тип);
		ТаблицаНоменклатуры.Колонки.Добавить("ЕдиницаИзмерения",           Метаданные.ОпределяемыеТипы.Упаковка.Тип);
		ТаблицаНоменклатуры.Колонки.Добавить("ХарактеристикиИспользуются", Новый ОписаниеТипов("Булево"));
		
		НоваяСтрока                = ТаблицаНоменклатуры.Добавить();
		НоваяСтрока.НомерСтроки    = 1;
		ЧастичноеВыбытиеКоличество = 1;
		
		Если ЭтоЧастичноеВыбытие(ЭтотОбъект) Тогда
			
			Номенклатура   = ЧастичноеВыбытиеНоменклатура;
			Характеристика = ЧастичноеВыбытиеХарактеристика;
			
		Иначе
			
			Номенклатура   = ЧастичноеВыбытиеИсходнаяНоменклатура;
			Характеристика = ЧастичноеВыбытиеИсходнаяХарактеристика;
			Количество     = 1;
			ВыбытиеБутылки = Ложь;

		КонецЕсли;
		
		НоваяСтрока.Номенклатура   = Номенклатура;
		НоваяСтрока.Характеристика = Характеристика;
			
		ИнтеграцияИСПереопределяемый.ЗаполнитьСлужебныеРеквизитыВКоллекции(ЭтотОбъект, ТаблицаНоменклатуры);
		
		ХарактеристикиИспользуются = НоваяСтрока.ХарактеристикиИспользуются;
		ЕдиницаИзмерения           = НоваяСтрока.ЕдиницаИзмерения;
		
		СобытияФормИС.ПрименитьУсловноеОформлениеКПолю(ЭтотОбъект, "Характеристика");
		
	ИначеЕсли ЧастичноеВыбытиеВариантУчета = Перечисления.ВариантыУчетаЧастичногоВыбытияИС.ТекущаяНоменклатура Тогда
		
		Если ЭтоЧастичноеВыбытие(ЭтотОбъект) Тогда
			ЧастичноеВыбытиеКоличество = 1;
		Иначе
			Количество     = ЕмкостьПотребительскойУпаковки;
			ВыбытиеБутылки = Ложь;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииКоличестваЧастичногоВыбытия()

	Если ВыбытиеБутылкиДоступно
		И ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная") Тогда
		ВыбытиеБутылки = ЧастичноеВыбытиеКоличество >= ЧастичноеВыбытиеОстаток;
	КонецЕсли;
	
	ПриИзмененииРеквизитаСвязанногоСОстаткомВскрытойУпаковки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииРеквизитаСвязанногоСОстаткомВскрытойУпаковки(НастраиватьЭлементыФормы = Истина)

	Если ЧастичноеВыбытиеОстаток <> Неопределено
		И (ЧастичноеВыбытиеОстаток - ЧастичноеВыбытиеКоличество) < 0 Тогда
		ЧастичноеВыбытиеНеКорректныйОстаток = Истина;
		Если НастраиватьЭлементыФормы Тогда
			НастроитьЭлементыФормыЧастичногоВыбытия(ЭтотОбъект);
		КонецЕсли;
	Иначе
		ЧастичноеВыбытиеНеКорректныйОстаток = ЧастичноеВыбытиеНеКорректныйОстатокГИС;
		Если НастраиватьЭлементыФормы Тогда
			НастроитьЭлементыФормыЧастичногоВыбытия(ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЭлементыФормыЧастичногоВыбытия(Форма)
	
	Элементы = Форма.Элементы;
	
	ЭтоЧастичноеВыбытие = ЭтоЧастичноеВыбытие(Форма);
	
	Элементы.РежимВыбытия.Видимость = Форма.МожетВыбыватьЧастично;
	Элементы.ВыбытиеБутылки.Видимость = ЭтоЧастичноеВыбытие И Форма.ВыбытиеБутылкиДоступно;
	Элементы.ЕмкостьПотребительскойУпаковки.ТолькоПросмотр = (Форма.ДанныеЧастичногоВыбытияПолученыИзГИС Или ЗначениеЗаполнено(Форма.ЕмкостьПотребительскойУпаковкиИБ));
	Элементы.РежимВыбытия.ТолькоПросмотр = Форма.ЧастичноеВыбытиеВыведено > 0
		Или Форма.КоличествоВскрытыхПотребительскихУпаковок > 0
		Или ЭтоВыборКодаМаркировкиВскрытойПотребительскойУпаковки(Форма)
		Или (ЗначениеЗаполнено(Форма.ЕмкостьПотребительскойУпаковки)
			И ЗначениеЗаполнено(Форма.ЧастичноеВыбытиеОстаток)
			И Форма.ЧастичноеВыбытиеОстаток <> Форма.ЕмкостьПотребительскойУпаковки);
	
	Элементы.ЕмкостьПотребительскойУпаковки.Видимость = ЭтоЧастичноеВыбытие;
	Элементы.ЧастичноеВыбытиеКоличество.Видимость     = ЭтоЧастичноеВыбытие;
	ДанныеПодсказкиЕмкостьЧастичногоВыбытия           = Новый Массив();
	
	Если ЭтоЧастичноеВыбытие Тогда
		
		Если Форма.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво")
			Или Форма.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПивоВПотребительскихУпаковках") Тогда
			Элементы.ЧастичноеВыбытиеКоличество.ОграничениеТипа     = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 3, ДопустимыйЗнак.Неотрицательный));
			Элементы.ЕмкостьПотребительскойУпаковки.ОграничениеТипа = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 3, ДопустимыйЗнак.Неотрицательный));
		Иначе
			Элементы.ЧастичноеВыбытиеКоличество.ОграничениеТипа     = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Неотрицательный));
			Элементы.ЕмкостьПотребительскойУпаковки.ОграничениеТипа = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Неотрицательный));
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЭтоЧастичноеВыбытие
		И Форма.ЧастичноеВыбытиеНеКорректныйОстаток Тогда
		Элементы.ГруппаЧастичноеВыбытиеКоличество.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
		Если Форма.ЧастичноеВыбытиеНеКорректныйОстатокГИС Тогда
			Элементы.ГруппаЧастичноеВыбытиеКоличество.РасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(
				НСтр("ru = 'По данным ГИС МТ остаток не корректный'"),,
				Форма.ЦветТекстаПроблема);
		Иначе
			Элементы.ГруппаЧастичноеВыбытиеКоличество.РасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(
				НСтр("ru = 'Превышение остатка вскрытой потребительской упаковки'"),,
				Форма.ЦветТекстаПроблема);
		КонецЕсли;
	Иначе
		Элементы.ГруппаЧастичноеВыбытиеКоличество.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли;
	
	Если ЭтоЧастичноеВыбытие
		И ЗначениеЗаполнено(Форма.ЕмкостьПотребительскойУпаковкиГИСМТ)
		И ЗначениеЗаполнено(Форма.ЕмкостьПотребительскойУпаковкиИБ)
		И Форма.ЕмкостьПотребительскойУпаковкиГИСМТ <> Форма.ЕмкостьПотребительскойУпаковкиИБ Тогда
		
		ДанныеПодсказкиЕмкостьЧастичногоВыбытия.Добавить(
			СтрШаблон(
				НСтр("ru = 'Емкость упаковки по данным информационной базы: %1 %2, по данным ГИС МТ: %3 %4'"),
				Форма.ЕмкостьПотребительскойУпаковкиИБ,
				Форма.ЕдиницаИзмерения,
				Форма.ЕмкостьПотребительскойУпаковкиГИСМТ,
				Форма.ЕдиницаИзмерения));
		
	КонецЕсли;
	
	Если ДанныеПодсказкиЕмкостьЧастичногоВыбытия.Количество() Тогда
		Элементы.ГруппаКоличествоЕмкость.ОтображениеПодсказки           = ОтображениеПодсказки.ОтображатьСнизу;
		Элементы.ГруппаКоличествоЕмкость.РасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(
			ДанныеПодсказкиЕмкостьЧастичногоВыбытия,,
			Форма.ЦветТекстаПроблема);
	Иначе
		Элементы.ГруппаКоличествоЕмкость.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли;
	
	ДанныеПодсказкиКоличества = Новый Массив();
	Если ЗначениеЗаполнено(Форма.ЕдиницаИзмерения) Тогда
		ДанныеПодсказкиКоличества.Добавить(СокрЛП(Форма.ЕдиницаИзмерения));
	КонецЕсли;
	
	Если Форма.ЧастичноеВыбытиеОстаток <> Неопределено Тогда
		ДанныеПодсказкиКоличества.Добавить(", ");
		ДанныеПодсказкиКоличества.Добавить(
			СтрШаблон(
				НСтр("ru = 'Остаток: %1 %2'"),
				Формат(Форма.ЧастичноеВыбытиеОстаток, "ЧН=0;"),
				Форма.ЕдиницаИзмерения));
	КонецЕсли;
	
	Элементы.ЕмкостьПотребительскойУпаковки.РасширеннаяПодсказка.Заголовок    = СокрЛП(Форма.ЕдиницаИзмерения);
	Элементы.ЧастичноеВыбытиеКоличество.РасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(
		ДанныеПодсказкиКоличества);
		
	Если ЗначениеЗаполнено(Форма.ЧастичноеВыбытиеВариантУчета)
		Или Форма.РежимВыбытия <> "Частично" Тогда
		Элементы.ГруппаРежимВыбытия.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	Иначе
		Элементы.ГруппаРежимВыбытия.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
	КонецЕсли;
	
	РасчетОграниченийПолейЧастичногоВыбытия(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РасчетОграниченийПолейЧастичногоВыбытия(Форма)
	
	Если Не Форма.МожетВыбыватьЧастично Тогда
		Возврат;
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	
	Если ЗначениеЗаполнено(Форма.ЧастичноеВыбытиеОстаток)
		И Форма.ВидПродукции <> ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво")
		И Форма.ВидПродукции <> ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПивоВПотребительскихУпаковках")
		И Форма.ВидПродукции <> ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная") Тогда
		
		Элементы.ЧастичноеВыбытиеКоличество.МаксимальноеЗначение = Форма.ЧастичноеВыбытиеОстаток;
		
	ИначеЕсли ЗначениеЗаполнено(Форма.ЧастичноеВыбытиеОстатокГИСМТ)
		И Форма.ВидПродукции <> ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво")
		И Форма.ВидПродукции <> ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПивоВПотребительскихУпаковках")
		И Форма.ВидПродукции <> ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная") Тогда
		
		Элементы.ЧастичноеВыбытиеКоличество.МаксимальноеЗначение = Форма.ЧастичноеВыбытиеОстатокГИСМТ;
		
	ИначеЕсли ЗначениеЗаполнено(Форма.ЕмкостьПотребительскойУпаковки) Тогда
		
		Если Форма.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво")
			Или Форма.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПивоВПотребительскихУпаковках")
			Или Форма.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная")
			Или Форма.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.БезалкогольноеПиво") Тогда
			Элементы.ЧастичноеВыбытиеКоличество.МаксимальноеЗначение = Форма.ЕмкостьПотребительскойУпаковки;
		Иначе
			Элементы.ЧастичноеВыбытиеКоличество.МаксимальноеЗначение = Форма.ЕмкостьПотребительскойУпаковки - 1;
		КонецЕсли;
		
	Иначе
		
		Элементы.ЧастичноеВыбытиеКоличество.МаксимальноеЗначение = Неопределено;
		
	КонецЕсли;
	
	Элементы.ЕмкостьПотребительскойУпаковки.МинимальноеЗначение = 1;
	Если Форма.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво")
		Или Форма.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПивоВПотребительскихУпаковках") Тогда
		Элементы.ЧастичноеВыбытиеКоличество.МинимальноеЗначение = 0.001;
	Иначе
		Элементы.ЧастичноеВыбытиеКоличество.МинимальноеЗначение = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоЧастичноеВыбытие(Форма)
	
	Возврат (Форма.МожетВыбыватьЧастично
		И Форма.РежимВыбытия = "Частично");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоВыборКодаМаркировкиВскрытойПотребительскойУпаковки(Форма)
	Возврат (Форма.КоличествоВскрытыхПотребительскихУпаковок > 1
		И Форма.ЭтоШтрихкодНоменклатуры
		И Форма.ЧастичноеВыбытие);
КонецФункции

&НаКлиенте
Функция ТребуетсяУточнениеФиксироватьВыбытиеБутылки()

	Результат = Ложь;

	Если ЭтоЧастичноеВыбытие(ЭтотОбъект)
		И ВыбытиеБутылкиДоступно
		И ВопросОВыбытииБутылкиЗадан <> Истина Тогда

		Если ЧастичноеВыбытиеКоличество >= ЧастичноеВыбытиеОстаток Тогда
			Если Не ВыбытиеБутылки Тогда

				Результат = Истина;

				ОписаниеОповещенияВопрос = Новый ОписаниеОповещения(
					"ОтветНаВопросФиксироватьВыбытиеБутылки",
					ЭтотОбъект);

				ТекстВопроса = НСтр("ru = 'По данным системы, бутылка пуста.
				|При выбытии последней порции продукции в бутылке, необходимо фиксировать выбытие бутылки.
				|При фиксации выбытия, акцизная марка выводится из оборота и списывается из регистра 1 по справке 2.
				|Вы уверены, что в бутылке есть остаток?'");

				СписокКнопок = Новый СписокЗначений;
				СписокКнопок.Добавить("ЕстьОстаток",  НСтр("ru = 'Есть остаток'"));
				СписокКнопок.Добавить("БутылкаПуста", НСтр("ru = 'Бутылка пуста'"));
				СписокКнопок.Добавить("Отмена",       НСтр("ru = 'Отмена'"));

				ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, СписокКнопок,, СписокКнопок[0].Значение);

			КонецЕсли;
		Иначе
			Если ВыбытиеБутылки Тогда

				Результат = Истина;

				ОписаниеОповещенияВопрос = Новый ОписаниеОповещения(
					"ОтветНаВопросФиксироватьВыбытиеБутылки",
					ЭтотОбъект);

				ТекстВопроса = НСтр("ru = 'По данным системы, в бутылке еще имеется остаток.
				|Фиксировать выбытие бутылки, необходимо при выбытии последней порции продукции в бутылке.
				|При фиксации выбытия, акцизная марка выводится из оборота и списывается из регистра 1 по справке 2.
				|Вы уверены, что бутылка пуста? Фиксировать выбытие бутылки?'");

				СписокКнопок = Новый СписокЗначений;
				СписокКнопок.Добавить("БутылкаПуста", НСтр("ru = 'Фиксировать выбытие'"));
				СписокКнопок.Добавить("ЕстьОстаток",  НСтр("ru = 'Есть остаток'"));
				СписокКнопок.Добавить("Отмена",       НСтр("ru = 'Отмена'"));

				ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, СписокКнопок,, СписокКнопок[0].Значение);

			КонецЕсли;
		КонецЕсли;

	КонецЕсли;

	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ОтветНаВопросФиксироватьВыбытиеБутылки(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = "Отмена" Тогда
		Возврат;
	КонецЕсли;

	Если РезультатВопроса = "БутылкаПуста" Тогда
		ВыбытиеБутылки = Истина;
	ИначеЕсли РезультатВопроса = "ЕстьОстаток" Тогда
		ВыбытиеБутылки = Ложь;
	КонецЕсли;

	ВопросОВыбытииБутылкиЗадан = Истина;

	ЗакрытьФорму();

КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ЗакрытьФорму()
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если ТребуетсяУточнениеФиксироватьВыбытиеБутылки() Тогда
		Возврат;
	КонецЕсли;

	СтруктурыДанных = ШтрихкодированиеИСКлиент.ИнициализацияСтруктурыДанныхСохраненногоВыбора();
	
	СтруктурыДанных.ДанныеВыбора.Номенклатура                    = Номенклатура;
	СтруктурыДанных.ДанныеВыбора.Характеристика                  = Характеристика;
	СтруктурыДанных.ДанныеВыбора.Серия                           = Серия;
	
	СтруктурыДанных.ДанныеВыбора.ВидПродукции                    = ВидПродукции;
	СтруктурыДанных.ДанныеВыбора.ВидУпаковки                     = ВидУпаковки;
	СтруктурыДанных.ДанныеВыбора.ЭтоКодВводаОстатков             = ЭтоМаркировкаОстатков;
	СтруктурыДанных.ДанныеВыбора.ПредставлениеНоменклатуры       = ПредставлениеНоменклатуры();
	СтруктурыДанных.ДанныеВыбора.ИдентификаторПроисхожденияВЕТИС = ИдентификаторПроисхожденияВЕТИС;
	СтруктурыДанных.ДанныеВыбора.ГоденДо                         = ГоденДо;
	СтруктурыДанных.ДанныеВыбора.МаркируемаяПродукция            = МаркируемаяПродукция;
	СтруктурыДанных.ДанныеВыбора.СкоропортящаясяПродукция        = СкоропортящаясяПродукция;

	СтруктурыДанных.ДанныеВыбора.ПроизвольнаяЕдиницаУчета        = ПроизвольнаяЕдиницаУчета;
	СтруктурыДанных.ДанныеВыбора.ТребуетВзвешивания              = ТребуетВзвешивания;
	
	Если ТребуетВзвешивания
		И Не ПараметрыСканирования.ЗапрашиватьКоличествоМерногоТовара Тогда
		СтруктурыДанных.ДанныеВыбора.Количество = 0;
	Иначе
		СтруктурыДанных.ДанныеВыбора.Количество = Количество;
	КонецЕсли;
	
	Если ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.ОбъемноСортовойУчет") Тогда
		СтруктурыДанных.ДанныеВыбора.ОбработатьБезМаркировки           = Ложь;
		СтруктурыДанных.ДанныеВыбора.ЭтоШтрихкодНоменклатуры           = Ложь;
		СтруктурыДанных.ДанныеВыбора.КоличествоПотребительскихУпаковок = КоличествоПотребительскихУпаковок;
		СтруктурыДанных.ДанныеВыбора.КодМаркировки = СтрШаблон(
			"(02)%1(37)%2", GTIN, Формат(СтруктурыДанных.ДанныеВыбора.КоличествоПотребительскихУпаковок, "ЧН=0; ЧГ=0"));
	ИначеЕсли ЭтоВыборКодаМаркировкиВскрытойПотребительскойУпаковки(ЭтотОбъект) Тогда
		СтруктурыДанных.ДанныеВыбора.ОбработатьБезМаркировки           = Ложь;
		СтруктурыДанных.ДанныеВыбора.ЭтоШтрихкодНоменклатуры           = Ложь;
		СтруктурыДанных.ДанныеВыбора.МаркируемаяПродукция              = Истина;
		СтруктурыДанных.ДанныеВыбора.КодМаркировки                     = КодМаркировки;
		СтруктураПоиска = Новый Структура("КодМаркировкиСтрокой", КодМаркировки);
		НайденныеСтроки = КодыМаркировкиДляВыбора.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() Тогда
			СтрокаКодаМаркировки                                           = НайденныеСтроки[0];
			СтруктурыДанных.ДанныеВыбора.ПолныйКодМаркировки               = СтрокаКодаМаркировки.ПолныйКодМаркировки;
			СтруктурыДанных.ДанныеВыбора.Серия                             = СтрокаКодаМаркировки.Серия;
			СтруктурыДанных.ДанныеВыбора.ЧастичноеВыбытиеШтрихкодУпаковки  = СтрокаКодаМаркировки.КодМаркировки;
			СтруктурыДанных.ДанныеВыбора.ДанныеСтатуса = Новый Структура;
			СтруктурыДанных.ДанныеВыбора.ДанныеСтатуса.Вставить("Статус",          СтрокаКодаМаркировки.Статус);
			СтруктурыДанных.ДанныеВыбора.ДанныеСтатуса.Вставить("Владелец",        СтрокаКодаМаркировки.Владелец);
			СтруктурыДанных.ДанныеВыбора.ДанныеСтатуса.Вставить("ИННВладельца",    СтрокаКодаМаркировки.ИННВладельца);
			СтруктурыДанных.ДанныеВыбора.ДанныеСтатуса.Вставить("ОсобоеСостояние", СтрокаКодаМаркировки.ОсобоеСостояние);
		КонецЕсли;
	Иначе
		СтруктурыДанных.ДанныеВыбора.ОбработатьБезМаркировки = ОбработатьБезМаркировки;
		СтруктурыДанных.ДанныеВыбора.ЭтоШтрихкодНоменклатуры = ЭтоШтрихкодНоменклатуры;
		СтруктурыДанных.ДанныеВыбора.КодМаркировки           = КодМаркировки;
	КонецЕсли;
	
	СтруктурыДанных.ДанныеВыбора.СоставКодаМаркировки = СоставКодаМаркировки;
	
	Если ЗначениеЗаполнено(GTIN) Тогда
		СтруктурыДанных.ДанныеВыбора.GTIN = GTIN;
	ИначеЕсли ЗначениеЗаполнено(ШтрихкодEAN) Тогда
		СтруктурыДанных.ДанныеВыбора.GTIN = ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.GTINПоШтрихкодуEAN(ШтрихкодEAN);
	КонецЕсли;
	
	СтруктурыДанных.ДанныеВыбора.ШаблонЭтикетки   = ШаблонЭтикетки;
	СтруктурыДанных.ДанныеВыбора.СразуНаПринтер   = СразуНаПринтер;
	СтруктурыДанных.ДанныеВыбора.ШаблонМаркировки = ШаблонКодаМаркировки;
	
	Если ЭтоЧастичноеВыбытие(ЭтотОбъект) Тогда
		
		СтруктурыДанных.ДанныеВыбора.ЧастичноеВыбытие               = Истина;
		СтруктурыДанных.ДанныеВыбора.Количество                     = ЧастичноеВыбытиеКоличество;
		СтруктурыДанных.ДанныеВыбора.ЕмкостьПотребительскойУпаковки = ЕмкостьПотребительскойУпаковки;
		СтруктурыДанных.ДанныеВыбора.ЧастичноеВыбытиеВариантУчета   = ЧастичноеВыбытиеВариантУчета;
		СтруктурыДанных.ДанныеВыбора.ЧастичноеВыбытиеНоменклатура   = ЧастичноеВыбытиеИсходнаяНоменклатура;
		СтруктурыДанных.ДанныеВыбора.ЧастичноеВыбытиеХарактеристика = ЧастичноеВыбытиеИсходнаяХарактеристика;
		СтруктурыДанных.ДанныеВыбора.ВыбытиеБутылки                 = ВыбытиеБутылки;

		Если ЕмкостьПотребительскойУпаковкиНачальная <> ЕмкостьПотребительскойУпаковки
			И (ЗначениеЗаполнено(ЕмкостьПотребительскойУпаковкиНачальная)
				Или Не ДанныеЧастичногоВыбытияПолученыИзГИС) Тогда
			СтруктурыДанных.ДанныеВыбора.ЧастичноеВыбытиеСохранитьНовуюЕмкость = Истина;
		КонецЕсли;
		
	ИначеЕсли МожетВыбыватьЧастично Тогда
		
		СтруктурыДанных.ДанныеВыбора.ЧастичноеВыбытиеНоменклатура   = ЧастичноеВыбытиеНоменклатура;
		СтруктурыДанных.ДанныеВыбора.ЧастичноеВыбытиеХарактеристика = ЧастичноеВыбытиеХарактеристика;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЕмкостьПотребительскойУпаковки)
		И ЗначениеЗаполнено(КоличествоВПотребительскойУпаковке)
		И ЧастичноеВыбытиеВариантУчета <> ПредопределенноеЗначение("Перечисление.ВариантыУчетаЧастичногоВыбытияИС.НастроеннаяНоменклатура") Тогда
		Коэффициент = КоличествоВПотребительскойУпаковке / ЕмкостьПотребительскойУпаковки;
		СтруктурыДанных.ДанныеВыбора.Количество = Окр(СтруктурыДанных.ДанныеВыбора.Количество * Коэффициент, 3);
	КонецЕсли;
	
	Если ЗапомнитьВыбор
		И УказатьСрокГодности
		И Не ЗначениеЗаполнено(ГоденДо) Тогда
		СтруктурыДанных.ДанныеВыбора.СохраненВыборПустогоСрокаГодности = ЗапомнитьВыбор И Не ЗначениеЗаполнено(ГоденДо);
	КонецЕсли;
	
	СтруктурыДанных.ЗапомнитьВыбор = ЗапомнитьВыбор;
	
	Если РежимПечатиЭтикеток Тогда
		Если ТребуетВзвешивания
			И (ОбщегоНазначенияИСКлиентСервер.ЭтоМолочнаяПродукцияИСМП(ВидПродукции)
				Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МорепродуктыПодконтрольныеВЕТИС"))
			И ПараметрыСканирования.ЗапрашиватьКоличествоМерногоТовара Тогда
			СтруктурыДанных.ДополнитьПолныйКодМаркировки = "3103";
		ИначеЕсли ТребуетВзвешивания
			И (ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво")
				Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПивоВПотребительскихУпаковках")
				Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.БезалкогольноеПиво"))
			И ПараметрыСканирования.ЗапрашиватьКоличествоМерногоТовара Тогда
			СтруктурыДанных.ДополнитьПолныйКодМаркировки = "3353";
		ИначеЕсли (ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КреслаКоляски")
				Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ТехническиеСредстваРеабилитации")
				Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МедицинскиеИзделия"))
			И ЗначениеЗаполнено(ЗаводскойСерийныйНомер) Тогда
			СтруктурыДанных.ДополнитьПолныйКодМаркировки = СтрШаблон("97%1",
				ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.ШтрихкодВBase64(ЗаводскойСерийныйНомер));
		КонецЕсли;
	КонецЕсли;
	
	Закрыть(СтруктурыДанных);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборИдентификатораПроисхожденияВЕТИСЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторыВЕТИС = Новый Массив;
	ИдентификаторыВЕТИС.Добавить(Результат);
	ДобавитьИдентификаторыВЕТИСВСписокВыбора(ИдентификаторыВЕТИС);
	
	ИдентификаторПроисхожденияВЕТИС = Результат;
	УстановитьГоденДоИСкоропортящаясяПродукцияПоИдентификаторПроисхожденияВЕТИС();
	
	УстановитьФорматДатыГоденДо(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьФорматДатыГоденДо(Форма)
	
	Если Форма.СкоропортящаясяПродукция Тогда
		ФорматДаты = "ДФ='dd.MM.yyyy HH:mm';";
	Иначе
		ФорматДаты = "ДФ=dd.MM.yyyy;";
	КонецЕсли;
	
	Форма.Элементы.ГоденДо.Формат               = ФорматДаты;
	Форма.Элементы.ГоденДо.ФорматРедактирования = ФорматДаты;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьГоденДоИСкоропортящаясяПродукцияПоИдентификаторПроисхожденияВЕТИС()
	
	Если Не ЗначениеЗаполнено(ИдентификаторПроисхожденияВЕТИС) 
			Или ЭтоКодМаркировкиСоСрокомГодности
			Или Не УказатьСрокГодности Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВЕТИС = ДанныеИдентификаторовПроисхожденияВЕТИС(ИдентификаторПроисхожденияВЕТИС);
	
	ГоденДо         = ДанныеВЕТИС[ИдентификаторПроисхожденияВЕТИС].СрокГодности;
	СкоропортящаясяПродукция = ДанныеВЕТИС[ИдентификаторПроисхожденияВЕТИС].СкоропортящаясяПродукция;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеИдентификаторовПроисхожденияВЕТИС(ИдентификаторыПроисхождения)
	
	Модуль = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияИСМПВЕТИС");
	Возврат Модуль.ДанныеИдентификаторовПроисхождения(ИдентификаторыПроисхождения);
	
КонецФункции

&НаКлиенте
Функция ПредставлениеНоменклатуры()
	
	Если ПараметрыСканирования.РазрешенаОбработкаКодовСПустойНоменклатурой
		И Не ЗначениеЗаполнено(Номенклатура) Тогда
		
		Если Не ПустаяСтрока(Элементы.Номенклатура.ПодсказкаВвода) Тогда
			Возврат Элементы.Номенклатура.ПодсказкаВвода;
		КонецЕсли;
		
	Иначе
		
		Возврат ИнтеграцияИСКлиентСервер.ПредставлениеНаименования(Строка(Номенклатура), Ложь);	
		
	КонецЕсли;
	
КонецФункции

// Проверяет возможность закрытия формы уточнения данных после сканирования штрихкода номенклатуры
// 
// Возвращаемое значение:
//  Булево - форму можно закрыть
&НаКлиенте
Функция ЗакрытьПослеСканированияШтрихкодаНоменклатуры()
	
	// Специальные режимы уточнения: сканирование не закрывает форму
	Если РежимПечатиЭтикеток Тогда
		Возврат Ложь;
	ИначеЕсли УказатьИдентификаторВЕТИС
		И Не ЗначениеЗаполнено(ИдентификаторПроисхожденияВЕТИС) Тогда
		Возврат Ложь;
	ИначеЕсли УказатьСрокГодности
		И Не ЗначениеЗаполнено(ГоденДо) Тогда
		Возврат Ложь;
	// Штрихкод соответствует одному элементу
	ИначеЕсли Элементы.Номенклатура.СписокВыбора.Количество() Тогда
		Возврат Ложь;
	// Данные заполнены
	ИначеЕсли Не ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат Ложь;
	// Введено количество (это не выбор номенклатуры)
	ИначеЕсли Элементы.Номенклатура.ТолькоПросмотр
			И ТребуетВзвешивания
			И Не ЗначениеЗаполнено(Количество) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ПараметрыСканирования.Свойство("ИндивидуализироватьКИЗ")
		И ПараметрыСканирования.ИндивидуализироватьКИЗ Тогда
		СтатусRFID = ШтрихкодированиеИСКлиентСервер.СтатусRFID(ДанныеRFID, GTIN);
		Если СтатусRFID = 0 Тогда
			Возврат Ложь;
		ИначеЕсли СтатусRFID = 1 Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Отказ = Ложь;
	СобытияФормИСКлиентПереопределяемый.ПроверитьЗаполнение(ЭтотОбъект, Отказ);
	Возврат Не Отказ;
	
КонецФункции

&НаКлиенте
Процедура ПолучитьВесЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		
		Количество = РезультатВыполнения.Вес;
		
	Иначе
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			СтрШаблон(НСтр("ru = 'При выполнении операции произошла ошибка: %1'"),
				РезультатВыполнения.ОписаниеОшибки));
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеGTIN()
	
	Если Не УказатьGTIN Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.GTIN.СписокВыбора.ЗагрузитьЗначения(МассивЗначенийGTINДляВыбора());
	
	Если Элементы.GTIN.СписокВыбора.НайтиПоЗначению(GTIN) = Неопределено Тогда
		GTIN = Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(GTIN)
		И Элементы.GTIN.СписокВыбора.Количество() = 1 Тогда
		GTIN = Элементы.GTIN.СписокВыбора[0].Значение;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидПродукции)
		И (ОбщегоНазначенияИСКлиентСервер.ВидПродукцииИспользуетНаборы(ВидПродукции)
		Или ОбщегоНазначенияИСКлиентСервер.ВидПродукцииИспользуетГрупповыеУпаковки(ВидПродукции)) Тогда
		Элементы.GTIN.СписокВыбора.Добавить("НастроитьГрупповыеУпаковкиИНаборы",
			Новый ФорматированнаяСтрока(
				НСтр("ru = 'Настроить'"),,,,
				"НастроитьГрупповыеУпаковкиИНаборы"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция МассивЗначенийGTINДляВыбора()
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("Номенклатура",               Номенклатура);
	ПараметрыВыбора.Вставить("Характеристика",             Характеристика);
	ПараметрыВыбора.Вставить("ХарактеристикиИспользуются", ХарактеристикиИспользуются);
	Если ЗначениеЗаполнено(ШаблонКодаМаркировки) Тогда
		ПараметрыВыбора.Вставить("Шаблон",                 ШаблонКодаМаркировки);
		ДопустимыеВидыУпаковок = Неопределено;
	Иначе
		ДопустимыеВидыУпаковок = Новый Массив;
		ДопустимыеВидыУпаковок.Добавить(Перечисления.ВидыУпаковокИС.Потребительская);
		ДопустимыеВидыУпаковок.Добавить(Перечисления.ВидыУпаковокИС.Набор);
	КонецЕсли;
	
	Возврат ИнтеграцияИСМП.МассивЗначенийGTINДляВыбора(ПараметрыВыбора, ЭтотОбъект, , ДопустимыеВидыУпаковок);
	
КонецФункции

#Область СлужебныеПроцедурыИФункцииRFID

&НаКлиенте
Процедура ОтработатьТаймаутОжиданияСчитыванияRFID()
	
	Если ОткрытаСессияСчитывателяRFID Тогда
		СобытияФормИСКлиент.ОтработатьТаймаутОжиданияСчитыванияRFID(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьRFID()
	
	Если ПараметрыСканирования.Свойство("ИндивидуализироватьКИЗ")
		И ПараметрыСканирования.ИндивидуализироватьКИЗ Тогда
		
		Считыватели = МенеджерОборудованияВызовСервера.ОборудованиеПоПараметрам("СчитывательRFID");
		Если Считыватели.Количество() = 0 Тогда
			ПодключатьСчитывательRFID = Ложь;
		ИначеЕсли Считыватели.Количество() = 1 Тогда
			ПодключатьСчитывательRFID = Истина;
			СчитывательRFID = Считыватели[0].Ссылка;
		Иначе
			ТекстСообщения = НСтр("ru = 'К рабочему месту подключено несколько считывателей RFID.
			                            |Одновременная работа с несколькими считывателями не поддерживается.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			ПодключатьСчитывательRFID = Ложь;
		КонецЕсли;
		
	Иначе
		
		ПодключатьСчитывательRFID = Ложь;
		
	КонецЕсли;
	
	Элементы.ГруппаRFID.Видимость = ПодключатьСчитывательRFID;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьСтатусRFID(Форма)
	
	Элементы = Форма.Элементы;
	
	СтатусRFID = ШтрихкодированиеИСКлиентСервер.СтатусRFID(Форма.ДанныеRFID, Форма.GTIN);
	Если СтатусRFID = 0 Тогда
		Форма.СтатусRFIDПредставление = Новый ФорматированнаяСтрока(НСтр("ru = 'Считайте RFID-метку'"));
		Элементы.СчитатьRFID.Доступность  = Истина;
		Элементы.ЗаписатьRFID.Доступность = Ложь;
	ИначеЕсли СтатусRFID = 1 Тогда
		Форма.СтатусRFIDПредставление = Новый ФорматированнаяСтрока(НСтр("ru = 'Запишите RFID-метку'"));
		Элементы.СчитатьRFID.Доступность  = Ложь;
		Элементы.ЗаписатьRFID.Доступность = Истина;
	ИначеЕсли СтатусRFID = 2 Тогда
		Форма.СтатусRFIDПредставление = Новый ФорматированнаяСтрока(НСтр("ru = 'RFID-метка индивидуализирована'"));
		Элементы.СчитатьRFID.Доступность  = Ложь;
		Элементы.ЗаписатьRFID.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиRFID(РезультатОперации, ДополнительныеПараметры) Экспорт
	
	Если РезультатОперации.Результат Тогда
		
		ЗаписанныйEPC = ДополнительныеПараметры.ЗаписанныйEPC;
		
		ДекодированныеДанные = МенеджерОборудованияКлиентСервер.ДекодированиеДанныхSGTIN(ЗаписанныйEPC);
		Если ДекодированныеДанные.GTIN = GTIN Тогда
			
			РезультатОперации = ЗаписатьДанныеRFIDВИнформационнуюБазу(ДанныеRFID.TID, КодМаркировки, ДекодированныеДанные);
			Если РезультатОперации.ДанныеУспешноЗаписаны Тогда
				
				ДанныеRFID.EPC = ЗаписанныйEPC;
				
				ОбновитьСтатусRFID(ЭтотОбъект);
				
			Иначе
				ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатОперации.ТекстОшибки);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаписатьДанныеRFIDВИнформационнуюБазу(TID, КодМаркировки, ДекодированныеДанные)
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ДанныеУспешноЗаписаны", Ложь);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",           "");
	
	Попытка
		
		НаборЗаписей = РегистрыСведений.ДанныеRFIDИСМП.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.RFIDTID.Установить(TID, Истина);
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Количество() Тогда
			ЗаписьНабора = НаборЗаписей[0];
		Иначе
			ЗаписьНабора = НаборЗаписей.Добавить();
			
		КонецЕсли;
		
		ЗаписьНабора.RFIDTID           = TID;
		ЗаписьНабора.ЗначениеШтрихкода = КодМаркировки;
		ЗаписьНабора.RFIDEPC           = ДекодированныеДанные.EPC;
		ЗаписьНабора.EPCGTIN           = ДекодированныеДанные.GTIN;
		
		НаборЗаписей.Записать();
		
		ВозвращаемоеЗначение.ДанныеУспешноЗаписаны = Истина;
		
		Возврат ВозвращаемоеЗначение;
		
	Исключение
		
		ВозвращаемоеЗначение.ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Не удалось записать данные RFID в информационную базу:
			           |Текст ошибки: %1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Возврат ВозвращаемоеЗначение;
		
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#КонецОбласти
