
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	НоваяДата = Параметры.Дата;
	НачалоПериода = Параметры.НачалоПериода;
	КонецПериода = Параметры.КонецПериода;
	
	Если Параметры.ЧастьДаты = ЧастиДаты.Дата Тогда
		ФорматДат = "ДЛФ=D;";
	Иначе
		ФорматДат = "ДЛФ=DT;";
	КонецЕсли;

	Элементы.НоваяДата.Формат = ФорматДат;
	Элементы.НоваяДата.ФорматРедактирования = ФорматДат;
	
	Если ЗначениеЗаполнено(Параметры.Подсказка) Тогда
		Заголовок = Параметры.Подсказка;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если НачалоПериода <> '000101010000'
		И КонецПериода <> '000101010000' Тогда
			
		Если НоваяДата < НачалоПериода
			ИЛИ НоваяДата > КонецПериода Тогда
				
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Дата должна быть в интервале с %1 по %2'"), 
				Формат(НачалоПериода, ФорматДат),
				Формат(КонецПериода, ФорматДат));
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			
		КонецЕсли;
		
	ИначеЕсли НачалоПериода <> '000101010000' 
		И НоваяДата < НачалоПериода Тогда
			
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Дата должна быть не раньше %1'"), 
			Формат(НачалоПериода, ФорматДат));
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			
	ИначеЕсли КонецПериода <> '000101010000' 
		И НоваяДата > КонецПериода Тогда
			
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Дата должна быть не позже %1'"), 
			Формат(КонецПериода, ФорматДат));
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьВвод(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		Закрыть(НоваяДата);
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти
