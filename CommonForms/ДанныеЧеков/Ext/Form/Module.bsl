///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Документ = Параметры.Документ;
	
	РезультатОперации = СервисИнтеграцииСОФД.ДанныеЧеков(Документ);
	
	Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
		ОбщегоНазначения.СообщитьПользователю(РезультатОперации.СообщениеОбОшибке);
		Возврат;
	КонецЕсли;
	
	Для Каждого ДанныеЧека Из РезультатОперации.ДанныеЧеков Цикл
		СтрокаЧека = ЭтотОбъект.ЧекиПоДокументу.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЧека, ДанныеЧека);
		СтрокаЧека.ПредставлениеЧека = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Номер чека в смене %1 от %2 (%3)'"),
			ДанныеЧека.НомерЧекаВСмене,
			ДанныеЧека.ДатаЧека,
			ДанныеЧека.ПризнакРасчета);
		СтрокаЧека.Просмотр   = "Просмотреть";
		СтрокаЧека.Сохранение = "Сохранить";
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДанныеЧеков

&НаКлиенте
Процедура ДанныеЧековПриАктивизацииЯчейки(Элемент)
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущийЭлемент.Имя = "ДанныеЧековПросмотр"
		И ЗначениеЗаполнено(Элемент.ТекущиеДанные.UrlPdfЧека) Тогда
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(
			Элемент.ТекущиеДанные.UrlПросмотраЧека);
	ИначеЕсли Элемент.ТекущийЭлемент.Имя = "ДанныеЧековСохранение"
		И ЗначениеЗаполнено(Элемент.ТекущиеДанные.UrlPdfЧека) Тогда
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(
			Элемент.ТекущиеДанные.UrlPdfЧека);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти