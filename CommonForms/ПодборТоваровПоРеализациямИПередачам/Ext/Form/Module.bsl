
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИспользоватьСоглашенияСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами");
	ИспользоватьДоговорыСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами");
	ИспользоватьНаправленияДеятельности = ПолучитьФункциональнуюОпцию("ИспользоватьУчетДоходовПоНаправлениямДеятельности");
	
	Элементы.ТаблицаТоваровОткрытьКарточкуТовара.Видимость = ОбщегоНазначенияУТКлиентСервер.АвторизованВнешнийПользователь();
	
	ДокументВозврата = Параметры.ДокументВозврата;
	Валюта = Параметры.Валюта;
	Дата = Параметры.Дата;
	ЦенаВключаетНДС = Параметры.ЦенаВключаетНДС;
	Партнер = Параметры.Партнер;
	Соглашение = Параметры.Соглашение;
	Договор = Параметры.Договор;
	Склад = Параметры.Склад;
	Организация = Параметры.Организация;
	Контрагент = Параметры.Контрагент;
	НалогообложениеНДС = Параметры.НалогообложениеНДС;
	ВозвратОтРозничногоПокупателя = Параметры.ВозвратОтРозничногоПокупателя;
	ВозвратМеждуОрганизациями = Параметры.ВозвратМеждуОрганизациями;
	ЧекККМ = Параметры.ЧекККМ;
	НаправлениеДеятельности = Параметры.НаправлениеДеятельности;
	ПредусмотренЗалогЗаТару = Параметры.ПредусмотренЗалогЗаТару;
	ХозяйственнаяОперация = Параметры.ХозяйственнаяОперация;
	КлиентКонтрагент = Параметры.КлиентКонтрагент;
	КлиентПартнер = Параметры.КлиентПартнер;
	КлиентДоговор = Параметры.КлиентДоговор;
	
	Если ВозвратМеждуОрганизациями Тогда
		Заголовок = НСтр("ru = 'Подбор товаров по документам передачи в'");
	Иначе
		Заголовок = НСтр("ru = 'Подбор товаров по документам продажи в'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументВозврата) Тогда
		ТекстДокумент = ДокументВозврата;
	Иначе
		ТекстДокумент = НСтр("ru='%1 (новый)'");
		ТекстДокумент = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстДокумент,
																				ДокументВозврата.Метаданные().Синоним);
	КонецЕсли;
	
	Заголовок = Заголовок + Символы.НПП + ТекстДокумент;
	
	Период = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ПодборТоваровПоРеализациямИПередачам",
																"ПериодПодбораТоваровПоРеализациям");
	
	ЗаполнитьТаблицуТоваров();
	РассчитатьИтогиПоТаблицеТоваров();
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТоварыИнформационнаяНадпись,
		ТоварыВыбраноКоличество,
		ТоварыВыбраноСумма,
		Валюта);
	
	ИспользоватьМногооборотнуюТару = ПолучитьФункциональнуюОпцию("ИспользоватьМногооборотнуюТару");
	ЕстьПравоНаТару = ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ПереданнаяВозвратнаяТара);
	Если ИспользоватьМногооборотнуюТару И Параметры.ПоказыватьТару И ЕстьПравоНаТару Тогда
		
		ЗаполнитьТаблицуТары();
		РассчитатьИтогиПоТаблицеТары();
		ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
			ТараИнформационнаяНадпись,
			ТараВыбраноКоличество,
			ТараВыбраноСумма,
			Валюта);
		
		Если Не Параметры.ПоказыватьТовары Тогда
			Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаТара;
		Иначе
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаТовары;
		КонецЕсли;
		
	Иначе
		
		Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаТовары;
		
	КонецЕсли;
	
	МассивЭлементов = Новый Массив();
	МассивЭлементов.Добавить("ТаблицаТары");
	МассивЭлементов.Добавить("ТараИнформационнаяНадпись");
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы,
																	МассивЭлементов,
																	"Видимость",
																	ИспользоватьМногооборотнуюТару
																		И Параметры.ПоказыватьТару);
	
	МассивЭлементов = Новый Массив();
	МассивЭлементов.Добавить("ПериодВариант");
	МассивЭлементов.Добавить("ПериодДатаНачала");
	МассивЭлементов.Добавить("ПериодДатаОкончания");
	МассивЭлементов.Добавить("Обновить");
	МассивЭлементов.Добавить("ТаблицаТоваров");
	МассивЭлементов.Добавить("ТоварыИнформационнаяНадпись");
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "Видимость",
		Параметры.ПоказыватьТовары);
		
	МассивЭлементов = Новый Массив();
	МассивЭлементов.Добавить("ТаблицаТоваровСклад");
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы,
																	МассивЭлементов,
																	"Видимость",
																	?(Параметры.НеПоказыватьСклад = Истина, Ложь, Истина));
	
	УчетПрослеживаемыхТоваровЛокализация.УстановитьЗаголовокНомерГТД(Элементы, "ТаблицаТоваровНомерГТД");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗакрытьФормуПринудительно 
		ИЛИ ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	Если Не ПеренестиВДокумент И ТоварыВыбраноКоличество > 0 Тогда
		
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ВопросПодобранныеТоварыНеПеренесеныВДокументЗавершение", ЭтотОбъект),
			НСтр("ru = 'Подобранные товары не перенесены в документ. Перенести?'"),
			РежимДиалогаВопрос.ДаНетОтмена);
		
	Иначе
		
		ПередЗакрытиемФрагмент();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПодобранныеТоварыНеПеренесеныВДокументЗавершение(ОтветНаВопрос, ДополнительныеПараметры) Экспорт
	
	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		
		ПеренестиВДокумент = Истина;
		ПередЗакрытиемФрагмент();
		
	ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
		
		ЗакрытьФормуПринудительно = Истина;
		ПеренестиВДокумент = Ложь;
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемФрагмент()
	
	ПереноситьЧек = ПроверитьВозможностьПереносаЧекаККМ();
	Если ПеренестиВДокумент И НЕ ПереноситьЧек Тогда
		
		ВариантыОтветов = Новый СписокЗначений;
		ВариантыОтветов.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Перенести без чека ККМ'"));
		ВариантыОтветов.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Вернуться'"));
		
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ПередЗакрытиемФрагментВопросЗавершение", ЭтотОбъект),
			НСтр("ru = 'Выбраны товары из разных чеков ККМ. Чек ККМ не будет заполнен. Перенести товары без чека ККМ?'"),
			ВариантыОтветов);
		
	Иначе
		
		ЗакрытьФормуПринудительно = Истина;
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемФрагментВопросЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		ПеренестиВДокумент = Ложь;
	Иначе
		ЗакрытьФормуПринудительно = Истина;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Если ПеренестиВДокумент Тогда
		
		АдресТоваровВХранилище = ПоместитьТоварыВХранилище();
		Структура = Новый Структура("АдресТоваровВХранилище", АдресТоваровВХранилище);
		ОповеститьОВыборе(Структура);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ПодборТоваровПоРеализациямИПередачам",
		"ПериодПодбораТоваровПоРеализациям", Период);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаСервере
Процедура ТаблицаТоваровВыбранПриИзмененииНаСервере()
	
	ТекущиеДанные = ТаблицаТоваров.НайтиПоИдентификатору(Элементы.ТаблицаТоваров.ТекущаяСтрока);
	
	ЗнакСложения = ?(ТекущиеДанные.Выбран, 1, -1);
	ТоварыВыбраноКоличество = ТоварыВыбраноКоличество + ЗнакСложения;
	ТоварыВыбраноСумма = ТоварыВыбраноСумма + ТекущиеДанные.СуммаСНДС*ЗнакСложения;
	
	НоменклатураНабора         = ТекущиеДанные.НоменклатураНабора;
	ХарактеристикаНабора       = ТекущиеДанные.ХарактеристикаНабора;
	ВариантРасчетаЦеныНабора   = ТекущиеДанные.ВариантРасчетаЦеныНабора;
	ДокументРеализацииПередачи = ТекущиеДанные.ДокументРеализацииПередачи;
	
	Если ЗначениеЗаполнено(НоменклатураНабора) И (ВариантРасчетаЦеныНабора = Перечисления.ВариантыРасчетаЦенНаборов.ЦенаЗадаетсяЗаНаборРаспределяетсяПоДолям
		ИЛИ ВариантРасчетаЦеныНабора = Перечисления.ВариантыРасчетаЦенНаборов.ЦенаЗадаетсяЗаНаборРаспределяетсяПоЦенам) Тогда
		
		ТоварыВыбраноСумма = 0;
		ТоварыВыбраноКоличество = 0;
		
		Для Каждого СтрокаТЧ Из ТаблицаТоваров Цикл
			
			Если  СтрокаТЧ.НоменклатураНабора = НоменклатураНабора
				И СтрокаТЧ.ХарактеристикаНабора = ХарактеристикаНабора
				И СтрокаТЧ.ДокументРеализацииПередачи = ДокументРеализацииПередачи Тогда
				СтрокаТЧ.Выбран = ТекущиеДанные.Выбран;
			КонецЕсли;
			
			Если СтрокаТЧ.Выбран Тогда
				ТоварыВыбраноСумма = ТоварыВыбраноСумма + СтрокаТЧ.СуммаСНДС;
				ТоварыВыбраноКоличество = ТоварыВыбраноКоличество + СтрокаТЧ.КоличествоУпаковок;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(ТоварыИнформационнаяНадпись, ТоварыВыбраноКоличество, ТоварыВыбраноСумма, Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаТоваровВыбранПриИзменении(Элемент)
	
	ТаблицаТоваровВыбранПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаТарыВыбранПриИзменении(Элемент)
	
	Данные = Элементы.ТаблицаТары.ТекущиеДанные;
	
	ЗнакСложения = ?(Данные.Выбран, 1, -1);
	ТараВыбраноКоличество = ТараВыбраноКоличество + ЗнакСложения;
	ТараВыбраноСумма = ТараВыбраноСумма + Данные.Сумма*ЗнакСложения;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТараИнформационнаяНадпись,
		ТараВыбраноКоличество,
		ТараВыбраноСумма,
		Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаТоваровВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияУТКлиентСервер.АвторизованВнешнийПользователь()
		И Поле.Имя = "ТаблицаТоваровНоменклатура"
		ИЛИ Поле.Имя = "ТаблицаТоваровХарактеристика"
		ИЛИ Поле.Имя = "ТаблицаТоваровСерия" Тогда
		
		ОткрытьКарточкуНоменклатуры();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокументВыполнить()
	
	ПеренестиВДокумент = Истина;
	ЗакрытьФормуПринудительно = Истина;
	Закрыть(КодВозвратаДиалога.OK);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьТовары()
	
	ВыбратьВсеТоварыНаСервере();
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(ТоварыИнформационнаяНадпись, ТоварыВыбраноКоличество, ТоварыВыбраноСумма, Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьТовары()
	
	ИсключитьВсеТоварыНаСервере();
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(ТоварыИнформационнаяНадпись, ТоварыВыбраноКоличество, ТоварыВыбраноСумма, Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВыделенныеТовары(Команда)
	
	МассивСтрок = Элементы.ТаблицаТоваров.ВыделенныеСтроки;
	Для Каждого ИдентификаторСтроки Из МассивСтрок Цикл
		Строка = ТаблицаТоваров.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если Не Строка.Выбран Тогда
			Строка.Выбран = Истина;
			ТоварыВыбраноКоличество = ТоварыВыбраноКоличество + 1;
			ТоварыВыбраноСумма = ТоварыВыбраноСумма + Строка.СуммаСНДС;
		КонецЕсли;
	КонецЦикла;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(ТоварыИнформационнаяНадпись, ТоварыВыбраноКоличество, ТоварыВыбраноСумма, Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьВыделенныеТовары(Команда)
	
	МассивСтрок = Элементы.ТаблицаТоваров.ВыделенныеСтроки;
	Для Каждого ИдентификаторСтроки Из МассивСтрок Цикл
		Строка = ТаблицаТоваров.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если Строка.Выбран Тогда
			Строка.Выбран = Ложь;
			ТоварыВыбраноКоличество = ТоварыВыбраноКоличество - 1;
			ТоварыВыбраноСумма = ТоварыВыбраноСумма - Строка.СуммаСНДС;
		КонецЕсли;
	КонецЦикла;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(ТоварыИнформационнаяНадпись, ТоварыВыбраноКоличество, ТоварыВыбраноСумма, Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьТару()
	
	ВыбратьВсюТаруНаСервере();
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТараИнформационнаяНадпись,
		ТараВыбраноКоличество,
		ТараВыбраноСумма,
		Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьТару()
	
	ИсключитьВсюТаруНаСервере();
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТараИнформационнаяНадпись,
		ТараВыбраноКоличество,
		ТараВыбраноСумма,
		Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВыделеннуюТару(Команда)
	
	МассивСтрок = Элементы.ТаблицаТары.ВыделенныеСтроки;
	Для Каждого ИдентификаторСтроки Из МассивСтрок Цикл
		Строка = ТаблицаТары.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если Не Строка.Выбран Тогда
			Строка.Выбран = Истина;
			ТараВыбраноКоличество = ТараВыбраноКоличество + 1;
			ТараВыбраноСумма = ТараВыбраноСумма + Строка.Сумма;
		КонецЕсли;
	КонецЦикла;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТараИнформационнаяНадпись,
		ТараВыбраноКоличество,
		ТараВыбраноСумма,
		Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьВыделеннуюТару(Команда)
	
	МассивСтрок = Элементы.ТаблицаТары.ВыделенныеСтроки;
	Для Каждого ИдентификаторСтроки Из МассивСтрок Цикл
		Строка = ТаблицаТары.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если Строка.Выбран Тогда
			Строка.Выбран = Ложь;
			ТараВыбраноКоличество = ТараВыбраноКоличество - 1;
			ТараВыбраноСумма = ТараВыбраноСумма - Строка.Сумма;
		КонецЕсли;
	КонецЦикла;
	
	ПодборТоваровКлиентСервер.СформироватьИнформационнуюНадписьПодобранныхТоваров(
		ТараИнформационнаяНадпись,
		ТараВыбраноКоличество,
		ТараВыбраноСумма,
		Валюта);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТовары(Команда)
	
	ЗаполнитьТаблицуТоваров();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТару(Команда)
	
	ЗаполнитьТаблицуТары();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодВариантПриИзменении(Элемент)
	
	ЗаполнитьТаблицуТоваров();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодДатаНачалаПриИзменении(Элемент)
	
	ЗаполнитьТаблицуТоваров();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодДатаОкончанияПриИзменении(Элемент)
	
	ЗаполнитьТаблицуТоваров();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточкуТовара(Команда)
	
	ОткрытьКарточкуНоменклатуры();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаТоваровСтавкаНДС.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаТоваровСуммаНДС.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НалогообложениеНДС");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС;

	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаТоваровСуммаРучнойСкидки.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаТоваровПроцентРучнойСкидки.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СуммаРучнойСкидки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;

	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаТоваровСуммаАвтоматическойСкидки.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаТоваровПроцентАвтоматическойСкидки.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СуммаАвтоматическойСкидки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;

	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

	//

	ПараметрыУсловногоОформления = НоменклатураСервер.НовыеПараметрыУсловногоОформленияЕдиницИзмерения();
	ПараметрыУсловногоОформления.ИмяПоляЕдиницаИзмерения = "ТаблицаТоваровНоменклатураЕдиницаИзмерения";
	ПараметрыУсловногоОформления.ПутьКПолюУпаковка = "ТаблицаТоваров.Упаковка";
	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтаФорма, ПараметрыУсловногоОформления);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаТарыДатаВозврата.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаТары.ДатаВозврата");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ОтборЭлемента.ПравоеЗначение = Новый СтандартнаяДатаНачала(ВариантСтандартнойДатыНачала.НачалоЭтогоДня);

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПросроченныеДанныеЦвет);

КонецПроцедуры

#Область Прочее

&НаСервере
Функция ПоместитьТоварыВХранилище()
	
	Товары = ТаблицаТоваров.Выгрузить(Новый Структура("Выбран", Истина));
	СтруктураШапки = Новый Структура("ЧекККМ,Партнер",
										Документы.ЧекККМ.ПустаяСсылка(),
										Партнер);
	
	Если ВозвратОтРозничногоПокупателя Тогда
		
		ТаблицаОснований = ТаблицаТоваров.Выгрузить(Новый Структура("Выбран", Истина));
		ТаблицаОснований.Свернуть("ДокументРеализацииПередачи,Партнер");
		
		Если ТаблицаОснований.Количество() = 1 Тогда
			СтрокаТаблицы = ТаблицаОснований.Получить(0);
			СтруктураШапки.ЧекККМ = СтрокаТаблицы.ДокументРеализацииПередачи;
			СтруктураШапки.Партнер = СтрокаТаблицы.Партнер;
		КонецЕсли;
		
	Иначе
		
		Для Каждого ТекСтрока Из ТаблицаТары Цикл
			Если ТекСтрока.Выбран Тогда
				НоваяСтрока = Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
				НоваяСтрока.КоличествоУпаковок = ТекСтрока.Количество;
				НоваяСтрока.СуммаСНДС = ТекСтрока.Сумма;
				НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
			КонецЕсли;
		КонецЦикла;
	
	КонецЕсли; 
	
	АдресТоваровВХранилище = ПоместитьВоВременноеХранилище(Новый Структура("Товары,СтруктураШапки", Товары, СтруктураШапки), УникальныйИдентификатор);
	
	Возврат АдресТоваровВХранилище;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьТаблицуТоваров()
	
	ТаблицаТоваров.Очистить();
	
	Запрос = Новый Запрос;
	Если Не ВозвратОтРозничногоПокупателя
		И Не ВозвратМеждуОрганизациями Тогда
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ДокументыРеализации
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрСведений.РеестрДокументов КАК РеестрДокументовСторноИсправление
		|	ПО
		|		РеализацияТоваровУслуг.Ссылка = РеестрДокументовСторноИсправление.СторнируемыйДокумент
		|		И РеестрДокументовСторноИсправление.Проведен
		|		И НЕ РеестрДокументовСторноИсправление.ДополнительнаяЗапись
		|ГДЕ
		|	РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И РеализацияТоваровУслуг.Проведен
		|	И РеализацияТоваровУслуг.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.Отгружено)
		|	И РеализацияТоваровУслуг.Контрагент = &Контрагент
		|	И РеализацияТоваровУслуг.КлиентКонтрагент = &КлиентКонтрагент
		|	И РеализацияТоваровУслуг.Организация = &Организация
		|	И ВЫБОР
		|			КОГДА НЕ &ИспользоватьСоглашенияСКлиентами
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ РеализацияТоваровУслуг.Соглашение = &Соглашение
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА НЕ &ИспользоватьНалогообложениеНДС 
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ РеализацияТоваровУслуг.НалогообложениеНДС = &НалогообложениеНДС
		|		КОНЕЦ
		|	И РеализацияТоваровУслуг.Партнер = &Партнер
		|	И РеализацияТоваровУслуг.КлиентПартнер = &КлиентПартнер
		|	И ВЫБОР
		|			КОГДА НЕ &ИспользоватьДоговорыСКлиентами
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ РеализацияТоваровУслуг.Договор = &Договор
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА НЕ &ИспользоватьНаправленияДеятельности
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ РеализацияТоваровУслуг.НаправлениеДеятельности = &НаправлениеДеятельности
		|		КОНЕЦ
		|	И РеестрДокументовСторноИсправление.Ссылка ЕСТЬ NULL
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	МАКСИМУМ(КорректировкаРеализации.Дата) КАК Дата,
		|	ДокументыРеализации.Ссылка КАК СсылкаРеализации
		|ПОМЕСТИТЬ ДанныеКорректировки
		|ИЗ
		|	ДокументыРеализации КАК ДокументыРеализации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК КорректировкаРеализации
		|		ПО (КорректировкаРеализации.ДокументОснование = ДокументыРеализации.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументовСторноИсправление
		|		ПО КорректировкаРеализации.Ссылка = РеестрДокументовСторноИсправление.СторнируемыйДокумент
		|			И РеестрДокументовСторноИсправление.Проведен
		|			И НЕ РеестрДокументовСторноИсправление.ДополнительнаяЗапись
		|ГДЕ
		|	КорректировкаРеализации.Проведен
		|	И НЕ КорректировкаРеализации.ВидКорректировки В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПерепоставленногоТовара), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара))
		|	И РеестрДокументовСторноИсправление.Ссылка ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	ДокументыРеализации.Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СсылкаРеализации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	МАКСИМУМ(КорректировкаРеализации.Ссылка) КАК Ссылка,
		|	МАКСИМУМ(КорректировкаРеализации.Ссылка.Дата) КАК Дата,
		|	КорректировкаРеализации.Ссылка.ДокументОснование КАК СсылкаРеализации
		|ПОМЕСТИТЬ ДанныеПоследнейКорректировки
		|ИЗ
		|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
		|ГДЕ
		|	(КорректировкаРеализации.Ссылка.ДокументОснование, КорректировкаРеализации.Ссылка.Дата) В
		|			(ВЫБРАТЬ
		|				ДанныеКорректировки.СсылкаРеализации КАК СсылкаРеализации,
		|				ДанныеКорректировки.Дата КАК Дата
		|			ИЗ
		|				ДанныеКорректировки)
		|
		|СГРУППИРОВАТЬ ПО
		|	КорректировкаРеализации.Ссылка.ДокументОснование
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка,
		|	СсылкаРеализации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВЫБОР
		|		КОГДА ДанныеПоследнейКорректировки.Ссылка ЕСТЬ NULL
		|			ТОГДА ДокументыРеализации.Ссылка
		|		ИНАЧЕ ДанныеПоследнейКорректировки.Ссылка
		|	КОНЕЦ КАК Ссылка
		|ПОМЕСТИТЬ СсылкиНаРеализации
		|ИЗ
		|	ДокументыРеализации КАК ДокументыРеализации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
		|		ПО ДокументыРеализации.Ссылка = ДанныеПоследнейКорректировки.СсылкаРеализации
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КорректировкаРеализацииТовары.НоменклатураНабора КАК НоменклатураНабора,
		|	КорректировкаРеализацииТовары.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|	КорректировкаРеализацииТовары.Номенклатура КАК Номенклатура,
		|	КорректировкаРеализацииТовары.Характеристика КАК Характеристика,
		|	КорректировкаРеализацииТовары.Серия КАК Серия,
		|	КорректировкаРеализацииТовары.Назначение КАК Назначение,
		|	СУММА(КорректировкаРеализацииТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(КорректировкаРеализацииТовары.Количество) КАК Количество,
		|	КорректировкаРеализацииТовары.Цена КАК Цена,
		|	КорректировкаРеализацииТовары.Ссылка.ДокументОснование КАК ДокументРеализацииПередачи,
		|	КорректировкаРеализацииТовары.Упаковка КАК Упаковка,
		|	КорректировкаРеализацииТовары.ВидЦены КАК ВидЦены,
		|	СУММА(КорректировкаРеализацииТовары.Сумма) КАК Сумма,
		|	КорректировкаРеализацииТовары.СтавкаНДС КАК СтавкаНДС,
		|	СУММА(КорректировкаРеализацииТовары.СуммаНДС) КАК СуммаНДС,
		|	СУММА(КорректировкаРеализацииТовары.СуммаСНДС) КАК СуммаСНДС,
		|	КорректировкаРеализацииТовары.Склад КАК Склад,
		|	NULL КАК СуммаРучнойСкидки,
		|	NULL КАК СуммаАвтоматическойСкидки,
		|	NULL КАК ПроцентРучнойСкидки,
		|	NULL КАК ПроцентАвтоматическойСкидки,
		|	КорректировкаРеализацииТовары.Ссылка.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
		|	КорректировкаРеализацииТовары.Ссылка.Валюта КАК Валюта,
		|	КорректировкаРеализацииТовары.Ссылка.ДокументОснование.Дата КАК ДатаРеализацииПередачи,
		|	КорректировкаРеализацииТовары.Ссылка.ДокументОснование.Номер КАК НомерРеализацииПередачи,
		|	КорректировкаРеализацииТовары.Ссылка.ДокументОснование.ВернутьМногооборотнуюТару КАК ВернутьМногооборотнуюТару
		|ПОМЕСТИТЬ ДанныеДокументовРеализации
		|ИЗ
		|	СсылкиНаРеализации КАК СсылкиНаРеализации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализацииТовары
		|		ПО (КорректировкаРеализацииТовары.Ссылка = СсылкиНаРеализации.Ссылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	КорректировкаРеализацииТовары.НоменклатураНабора,
		|	КорректировкаРеализацииТовары.ХарактеристикаНабора,
		|	КорректировкаРеализацииТовары.Номенклатура,
		|	КорректировкаРеализацииТовары.Характеристика,
		|	КорректировкаРеализацииТовары.Серия,
		|	КорректировкаРеализацииТовары.Цена,
		|	КорректировкаРеализацииТовары.Ссылка.ДокументОснование,
		|	КорректировкаРеализацииТовары.Упаковка,
		|	КорректировкаРеализацииТовары.ВидЦены,
		|	КорректировкаРеализацииТовары.СтавкаНДС,
		|	КорректировкаРеализацииТовары.Склад,
		|	КорректировкаРеализацииТовары.Ссылка.ЦенаВключаетНДС,
		|	КорректировкаРеализацииТовары.Ссылка.Валюта,
		|	КорректировкаРеализацииТовары.Ссылка.ДокументОснование.Дата,
		|	КорректировкаРеализацииТовары.Ссылка.ДокументОснование.Номер,
		|	КорректировкаРеализацииТовары.Упаковка,
		|	КорректировкаРеализацииТовары.Ссылка.ДокументОснование.ВернутьМногооборотнуюТару,
		|	КорректировкаРеализацииТовары.Назначение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РеализацияТоваровУслугТовары.НоменклатураНабора,
		|	РеализацияТоваровУслугТовары.ХарактеристикаНабора,
		|	РеализацияТоваровУслугТовары.Номенклатура,
		|	РеализацияТоваровУслугТовары.Характеристика,
		|	РеализацияТоваровУслугТовары.Серия,
		|	РеализацияТоваровУслугТовары.Назначение,
		|	СУММА(РеализацияТоваровУслугТовары.КоличествоУпаковок),
		|	СУММА(РеализацияТоваровУслугТовары.Количество),
		|	РеализацияТоваровУслугТовары.Цена,
		|	РеализацияТоваровУслугТовары.Ссылка,
		|	РеализацияТоваровУслугТовары.Упаковка,
		|	РеализацияТоваровУслугТовары.ВидЦены,
		|	СУММА(РеализацияТоваровУслугТовары.Сумма),
		|	РеализацияТоваровУслугТовары.СтавкаНДС,
		|	СУММА(РеализацияТоваровУслугТовары.СуммаНДС),
		|	СУММА(РеализацияТоваровУслугТовары.СуммаСНДС),
		|	РеализацияТоваровУслугТовары.Склад,
		|	РеализацияТоваровУслугТовары.СуммаРучнойСкидки,
		|	РеализацияТоваровУслугТовары.СуммаАвтоматическойСкидки,
		|	РеализацияТоваровУслугТовары.ПроцентРучнойСкидки,
		|	РеализацияТоваровУслугТовары.ПроцентАвтоматическойСкидки,
		|	РеализацияТоваровУслугТовары.Ссылка.ЦенаВключаетНДС,
		|	РеализацияТоваровУслугТовары.Ссылка.Валюта,
		|	РеализацияТоваровУслугТовары.Ссылка.Дата,
		|	РеализацияТоваровУслугТовары.Ссылка.Номер,
		|	РеализацияТоваровУслугТовары.Ссылка.ВернутьМногооборотнуюТару
		|ИЗ
		|	СсылкиНаРеализации КАК СсылкиНаРеализации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
		|		ПО (РеализацияТоваровУслугТовары.Ссылка = СсылкиНаРеализации.Ссылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТоваровУслугТовары.НоменклатураНабора,
		|	РеализацияТоваровУслугТовары.ХарактеристикаНабора,
		|	РеализацияТоваровУслугТовары.Номенклатура,
		|	РеализацияТоваровУслугТовары.Характеристика,
		|	РеализацияТоваровУслугТовары.Серия,
		|	РеализацияТоваровУслугТовары.Цена,
		|	РеализацияТоваровУслугТовары.Ссылка,
		|	РеализацияТоваровУслугТовары.Упаковка,
		|	РеализацияТоваровУслугТовары.ВидЦены,
		|	РеализацияТоваровУслугТовары.СтавкаНДС,
		|	РеализацияТоваровУслугТовары.СуммаРучнойСкидки,
		|	РеализацияТоваровУслугТовары.СуммаАвтоматическойСкидки,
		|	РеализацияТоваровУслугТовары.ПроцентРучнойСкидки,
		|	РеализацияТоваровУслугТовары.ПроцентАвтоматическойСкидки,
		|	РеализацияТоваровУслугТовары.Склад,
		|	РеализацияТоваровУслугТовары.Ссылка.ЦенаВключаетНДС,
		|	РеализацияТоваровУслугТовары.Ссылка.Валюта,
		|	РеализацияТоваровУслугТовары.Ссылка.Дата,
		|	РеализацияТоваровУслугТовары.Ссылка.Номер,
		|	РеализацияТоваровУслугТовары.Назначение,
		|	РеализацияТоваровУслугТовары.Упаковка,
		|	РеализацияТоваровУслугТовары.Ссылка.ВернутьМногооборотнуюТару
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаПроверяемыеТовары.НоменклатураНабора КАК НоменклатураНабора,
		|	ТаблицаПроверяемыеТовары.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|	ТаблицаПроверяемыеТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаПроверяемыеТовары.Характеристика КАК Характеристика,
		|	ТаблицаПроверяемыеТовары.Серия КАК Серия,
		|	ТаблицаПроверяемыеТовары.Назначение КАК Назначение,
		|	ТаблицаПроверяемыеТовары.Упаковка КАК Упаковка,
		|	ТаблицаПроверяемыеТовары.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаПроверяемыеТовары.Ссылка.Склад КАК Склад,
		|	СУММА(ТаблицаПроверяемыеТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(ТаблицаПроверяемыеТовары.Количество) КАК Количество,
		|	ТаблицаПроверяемыеТовары.ДокументРеализации КАК ДокументРеализации,
		|	СУММА(ТаблицаПроверяемыеТовары.Сумма) КАК Сумма,
		|	СУММА(ТаблицаПроверяемыеТовары.СуммаНДС) КАК СуммаНДС,
		|	СУММА(ТаблицаПроверяемыеТовары.СуммаСНДС) КАК СуммаСНДС
		|ПОМЕСТИТЬ ДанныеДокументовВозврата
		|ИЗ
		|	ДокументыРеализации КАК ДокументыРеализации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента.Товары КАК ТаблицаПроверяемыеТовары
		|		ПО (ТаблицаПроверяемыеТовары.ДокументРеализации = ДокументыРеализации.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументовСторноИсправление
		|		ПО ТаблицаПроверяемыеТовары.Ссылка = РеестрДокументовСторноИсправление.СторнируемыйДокумент
		|			И РеестрДокументовСторноИсправление.Проведен
		|			И НЕ РеестрДокументовСторноИсправление.ДополнительнаяЗапись
		|ГДЕ
		|	ТаблицаПроверяемыеТовары.Ссылка.Проведен
		|	И ТаблицаПроверяемыеТовары.Ссылка <> &ДокументВозврата
		|	И РеестрДокументовСторноИсправление.Ссылка ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаПроверяемыеТовары.НоменклатураНабора,
		|	ТаблицаПроверяемыеТовары.ХарактеристикаНабора,
		|	ТаблицаПроверяемыеТовары.Номенклатура,
		|	ТаблицаПроверяемыеТовары.Характеристика,
		|	ТаблицаПроверяемыеТовары.Серия,
		|	ТаблицаПроверяемыеТовары.Ссылка.Склад,
		|	ТаблицаПроверяемыеТовары.Назначение,
		|	ТаблицаПроверяемыеТовары.Упаковка,
		|	ТаблицаПроверяемыеТовары.СтавкаНДС,
		|	ТаблицаПроверяемыеТовары.ДокументРеализации
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	Серия,
		|	Назначение,
		|	ДокументРеализации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСТЬNULL(ДанныеДокументовРеализации.НоменклатураНабора, ДанныеДокументовВозврата.НоменклатураНабора) КАК НоменклатураНабора,
		|	ЕСТЬNULL(ДанныеДокументовРеализации.ХарактеристикаНабора, ДанныеДокументовВозврата.ХарактеристикаНабора) КАК ХарактеристикаНабора,
		|	ЕСТЬNULL(ДанныеДокументовРеализации.Номенклатура, ДанныеДокументовВозврата.Номенклатура) КАК Номенклатура,
		|	ЕСТЬNULL(ДанныеДокументовРеализации.Характеристика, ДанныеДокументовВозврата.Характеристика) КАК Характеристика,
		|	ЕСТЬNULL(ДанныеДокументовРеализации.Серия, ДанныеДокументовВозврата.Серия) КАК Серия,
		|	ЕСТЬNULL(ДанныеДокументовРеализации.Склад, ДанныеДокументовВозврата.Склад) КАК Склад,
		|	ЕСТЬNULL(ДанныеДокументовРеализации.Назначение, ДанныеДокументовВозврата.Назначение) КАК Назначение,
		|	ЕСТЬNULL(ДанныеДокументовРеализации.Упаковка, ДанныеДокументовВозврата.Упаковка) КАК Упаковка,
		|	0 КАК КоличествоУпаковок,
		|	ЕСТЬNULL(ДанныеДокументовРеализации.Количество, 0) - ЕСТЬNULL(ДанныеДокументовВозврата.Количество, 0) КАК Количество,
		|	ЕСТЬNULL(ДанныеДокументовРеализации.Сумма, 0) - ЕСТЬNULL(ДанныеДокументовВозврата.Сумма, 0) КАК Сумма,
		|	ЕСТЬNULL(ДанныеДокументовРеализации.СуммаНДС, 0) - ЕСТЬNULL(ДанныеДокументовВозврата.СуммаНДС, 0) КАК СуммаНДС,
		|	ЕСТЬNULL(ДанныеДокументовРеализации.СуммаСНДС, 0) - ЕСТЬNULL(ДанныеДокументовВозврата.СуммаСНДС, 0) КАК СуммаСНДС,
		|	ЕСТЬNULL(ДанныеДокументовРеализации.СтавкаНДС, ДанныеДокументовВозврата.СтавкаНДС) КАК СтавкаНДС,
		|	ЕСТЬNULL(ДанныеДокументовРеализации.ДокументРеализацииПередачи, ДанныеДокументовВозврата.ДокументРеализации) КАК ДокументРеализацииПередачи,
		|	ЕСТЬNULL(ДанныеДокументовРеализации.ВернутьМногооборотнуюТару, ЛОЖЬ) КАК ВернутьМногооборотнуюТару
		|ПОМЕСТИТЬ ДанныеДокументовРеализацииВозврата
		|ИЗ
		|	ДанныеДокументовРеализации КАК ДанныеДокументовРеализации
		|		ПОЛНОЕ СОЕДИНЕНИЕ ДанныеДокументовВозврата КАК ДанныеДокументовВозврата
		|		ПО ДанныеДокументовРеализации.Номенклатура = ДанныеДокументовВозврата.Номенклатура
		|			И ДанныеДокументовРеализации.Характеристика = ДанныеДокументовВозврата.Характеристика
		|			И ДанныеДокументовРеализации.НоменклатураНабора = ДанныеДокументовВозврата.НоменклатураНабора
		|			И ДанныеДокументовРеализации.ХарактеристикаНабора = ДанныеДокументовВозврата.ХарактеристикаНабора
		|			И ДанныеДокументовРеализации.Серия = ДанныеДокументовВозврата.Серия
		|			И ДанныеДокументовРеализации.Склад = ДанныеДокументовВозврата.Склад
		|			И ДанныеДокументовРеализации.Назначение = ДанныеДокументовВозврата.Назначение
		|			И ДанныеДокументовРеализации.ДокументРеализацииПередачи = ДанныеДокументовВозврата.ДокументРеализации
		|			И ДанныеДокументовРеализации.Упаковка = ДанныеДокументовВозврата.Упаковка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеДокументовРеализацииВозврата.Количество КАК Количество,
		|	ВариантыКомплектацииНоменклатуры.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
		|	ВЫБОР
		|		КОГДА ДанныеДокументовРеализацииВозврата.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ИндексНабора,
		|	ДанныеДокументовРеализацииВозврата.НоменклатураНабора КАК НоменклатураНабора,
		|	ДанныеДокументовРеализацииВозврата.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|	ДанныеДокументовРеализацииВозврата.Номенклатура КАК Номенклатура,
		|	ДанныеДокументовРеализацииВозврата.Характеристика КАК Характеристика,
		|	ДанныеДокументовРеализацииВозврата.Серия КАК Серия,
		|	ДанныеДокументовРеализацииВозврата.Назначение КАК Назначение,
		|	ДанныеДокументовРеализацииВозврата.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ДанныеДокументовРеализацииВозврата.Упаковка КАК Упаковка,
		|	ДанныеДокументовРеализацииВозврата.Сумма КАК Сумма,
		|	ДанныеДокументовРеализацииВозврата.СуммаНДС КАК СуммаНДС,
		|	ДанныеДокументовРеализацииВозврата.СуммаСНДС КАК СуммаСНДС,
		|	ДанныеДокументовРеализацииВозврата.Склад КАК Склад,
		|	ДанныеДокументовРеализацииВозврата.ДокументРеализацииПередачи.Дата КАК ДатаРеализацииПередачи,
		|	ДанныеДокументовРеализацииВозврата.ДокументРеализацииПередачи.Номер КАК НомерРеализацииПередачи,
		|	ДанныеДокументовРеализацииВозврата.СтавкаНДС КАК СтавкаНДС,
		|	ДанныеДокументовРеализацииВозврата.ДокументРеализацииПередачи КАК ДокументРеализацииПередачи,
		|	ДанныеДокументовРеализацииВозврата.ДокументРеализацииПередачи.Валюта КАК ВалютаРеализации,
		|	ДанныеДокументовРеализацииВозврата.ДокументРеализацииПередачи.ЦенаВключаетНДС КАК ЦенаВключаетНДС
		|
		|ИЗ ДанныеДокументовРеализацииВозврата КАК ДанныеДокументовРеализацииВозврата
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
		|		ПО (ВариантыКомплектацииНоменклатуры.Владелец = ДанныеДокументовРеализацииВозврата.НоменклатураНабора)
		|			И (ВариантыКомплектацииНоменклатуры.Характеристика = ДанныеДокументовРеализацииВозврата.ХарактеристикаНабора)
		|			И (ВариантыКомплектацииНоменклатуры.Основной)
		|ГДЕ
		|	ВЫБОР
		|		КОГДА НЕ ДанныеДокументовРеализацииВозврата.ВернутьМногооборотнуюТару
		|			ТОГДА ДанныеДокументовРеализацииВозврата.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|		ИНАЧЕ ДанныеДокументовРеализацииВозврата.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар))
		|	КОНЕЦ
		|	И ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(&ДокументВозврата) В (ТИП(Документ.ВозвратТоваровОтКлиента), ТИП(Документ.ЗаявкаНаВозвратТоваровОтКлиента))
		|			ТОГДА НЕ ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.СодержитУслуги, ЛОЖЬ)
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаРеализацииПередачи УБЫВ";
		
		Запрос.УстановитьПараметр("Партнер",                          Партнер);
		Запрос.УстановитьПараметр("КлиентПартнер",                    КлиентПартнер);
		Запрос.УстановитьПараметр("Контрагент",                       Контрагент);
		Запрос.УстановитьПараметр("КлиентКонтрагент",                 КлиентКонтрагент);
		Запрос.УстановитьПараметр("Организация",                      Организация);
		Запрос.УстановитьПараметр("ДатаНачала",                       Период.ДатаНачала);
		Запрос.УстановитьПараметр("ДатаОкончания",                    Период.ДатаОкончания);
		Запрос.УстановитьПараметр("Соглашение",                       Соглашение);
		Запрос.УстановитьПараметр("Договор",                          Договор);
		Запрос.УстановитьПараметр("ИспользоватьНалогообложениеНДС",   Не НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПустаяСсылка());
		Запрос.УстановитьПараметр("НалогообложениеНДС",               НалогообложениеНДС);
		Запрос.УстановитьПараметр("ДокументВозврата",                 ДокументВозврата);
		Запрос.УстановитьПараметр("ИспользоватьСоглашенияСКлиентами", ИспользоватьСоглашенияСКлиентами);
		Запрос.УстановитьПараметр("ИспользоватьДоговорыСКлиентами",   ИспользоватьДоговорыСКлиентами);
		Запрос.УстановитьПараметр("ИспользоватьНаправленияДеятельности", ИспользоватьНаправленияДеятельности);
		Запрос.УстановитьПараметр("НаправлениеДеятельности",          НаправлениеДеятельности);
		
	ИначеЕсли ВозвратОтРозничногоПокупателя Тогда
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЧекККМТовары.Ссылка КАК ДокументРеализацииПередачи,
		|	ЧекККМТовары.НоменклатураНабора КАК НоменклатураНабора,
		|	ЧекККМТовары.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|	ЧекККМТовары.Номенклатура КАК Номенклатура,
		|	ЧекККМТовары.Характеристика КАК Характеристика,
		|	ЧекККМТовары.Серия КАК Серия,
		|	ЧекККМТовары.Упаковка КАК Упаковка,
		|	СУММА(ЧекККМТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(ЧекККМТовары.Количество) КАК Количество,
		|	ВЫБОР
		|		КОГДА СУММА(ЧекККМТовары.СуммаРучнойСкидки) + СУММА(ЧекККМТовары.СуммаАвтоматическойСкидки) = 0
		|				ИЛИ СУММА(ЧекККМТовары.КоличествоУпаковок) = 0
		|			ТОГДА ЧекККМТовары.Цена
		|		ИНАЧЕ СУММА(ЧекККМТовары.Сумма) / СУММА(ЧекККМТовары.КоличествоУпаковок)
		|	КОНЕЦ КАК Цена,
		|	СУММА(ЧекККМТовары.Сумма) КАК Сумма,
		|	ЧекККМТовары.СтавкаНДС КАК СтавкаНДС,
		|	СУММА(ЧекККМТовары.СуммаНДС) КАК СуммаНДС,
		|	ЧекККМТовары.ПроцентАвтоматическойСкидки КАК ПроцентАвтоматическойСкидки,
		|	СУММА(ЧекККМТовары.СуммаАвтоматическойСкидки) КАК СуммаАвтоматическойСкидки,
		|	ЧекККМТовары.ПроцентРучнойСкидки КАК ПроцентРучнойСкидки,
		|	СУММА(ЧекККМТовары.СуммаРучнойСкидки) КАК СуммаРучнойСкидки,
		|	ЧекККМТовары.Ссылка.Дата КАК ДатаРеализацииПередачи,
		|	ЧекККМТовары.Ссылка.Номер КАК НомерРеализацииПередачи,
		|	ЧекККМТовары.Ссылка.Валюта КАК ВалютаРеализации,
		|	ЧекККМТовары.Ссылка.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
		|	ЧекККМТовары.Ссылка.Партнер КАК Партнер
		|ПОМЕСТИТЬ ЧекиККМ
		|ИЗ
		|	Документ.ЧекККМ.Товары КАК ЧекККМТовары
		|ГДЕ
		|	ЧекККМТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ЧекККМТовары.Ссылка.НалогообложениеНДС = &НалогообложениеНДС
		|	И ЧекККМТовары.Ссылка.Проведен
		|	И ЧекККМТовары.Ссылка.Организация = &Организация
		|	И ЧекККМТовары.Ссылка.Партнер = &Партнер
		|	И (ЧекККМТовары.Ссылка = &ЧекККМ ИЛИ &ЧекККМ = ЗНАЧЕНИЕ(Документ.ЧекККМ.ПустаяСсылка))
		|
		|СГРУППИРОВАТЬ ПО
		|	ЧекККМТовары.Ссылка,
		|	ЧекККМТовары.НоменклатураНабора,
		|	ЧекККМТовары.ХарактеристикаНабора,
		|	ЧекККМТовары.Номенклатура,
		|	ЧекККМТовары.Характеристика,
		|	ЧекККМТовары.Серия,
		|	ЧекККМТовары.Упаковка,
		|	ЧекККМТовары.Цена,
		|	ЧекККМТовары.СтавкаНДС,
		|	ЧекККМТовары.ПроцентАвтоматическойСкидки,
		|	ЧекККМТовары.ПроцентРучнойСкидки,
		|	ЧекККМТовары.Ссылка.Дата,
		|	ЧекККМТовары.Ссылка.Номер,
		|	ЧекККМТовары.Ссылка.Валюта,
		|	ЧекККМТовары.Ссылка.ЦенаВключаетНДС,
		|	ЧекККМТовары.Ссылка.Партнер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОбъединеннаяТаблица.ДокументРеализацииПередачи КАК ДокументРеализацииПередачи,
		|	ОбъединеннаяТаблица.НоменклатураНабора КАК НоменклатураНабора,
		|	ОбъединеннаяТаблица.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|	ОбъединеннаяТаблица.Номенклатура КАК Номенклатура,
		|	ОбъединеннаяТаблица.Характеристика КАК Характеристика,
		|	ОбъединеннаяТаблица.Упаковка КАК Упаковка,
		|	ОбъединеннаяТаблица.Серия КАК Серия,
		|	ОбъединеннаяТаблица.СтавкаНДС КАК СтавкаНДС,
		|	СУММА(ОбъединеннаяТаблица.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(ОбъединеннаяТаблица.Количество) КАК Количество,
		|	СУММА(ОбъединеннаяТаблица.Сумма) КАК Сумма,
		|	СУММА(ОбъединеннаяТаблица.СуммаНДС) КАК СуммаНДС
		|ПОМЕСТИТЬ ДанныеВозврата
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЧекККМВозвратТовары.Ссылка.ЧекККМ КАК ДокументРеализацииПередачи,
		|		ЧекККМВозвратТовары.НоменклатураНабора КАК НоменклатураНабора,
		|		ЧекККМВозвратТовары.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|		ЧекККМВозвратТовары.Номенклатура КАК Номенклатура,
		|		ЧекККМВозвратТовары.Характеристика КАК Характеристика,
		|		ЧекККМВозвратТовары.Упаковка КАК Упаковка,
		|		ЧекККМВозвратТовары.Серия КАК Серия,
		|		ЧекККМВозвратТовары.СтавкаНДС КАК СтавкаНДС,
		|		ЧекККМВозвратТовары.КоличествоУпаковок КАК КоличествоУпаковок,
		|		ЧекККМВозвратТовары.Количество КАК Количество,
		|		ЧекККМВозвратТовары.Сумма КАК Сумма,
		|		ЧекККМВозвратТовары.СуммаНДС КАК СуммаНДС
		|	ИЗ
		|		Документ.ЧекККМВозврат.Товары КАК ЧекККМВозвратТовары
		|	ГДЕ
		|		ЧекККМВозвратТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|		И ЧекККМВозвратТовары.Ссылка.НалогообложениеНДС = &НалогообложениеНДС
		|		И ЧекККМВозвратТовары.Ссылка.Проведен
		|		И ЧекККМВозвратТовары.Ссылка.Организация = &Организация
		|		И ЧекККМВозвратТовары.КоличествоУпаковок <> 0
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ТаблицаПроверяемыеТовары.Ссылка.ЧекККМ,
		|		ТаблицаПроверяемыеТовары.НоменклатураНабора,
		|		ТаблицаПроверяемыеТовары.ХарактеристикаНабора,
		|		ТаблицаПроверяемыеТовары.Номенклатура,
		|		ТаблицаПроверяемыеТовары.Характеристика,
		|		ТаблицаПроверяемыеТовары.Упаковка,
		|		ТаблицаПроверяемыеТовары.Серия,
		|		ТаблицаПроверяемыеТовары.СтавкаНДС,
		|		ТаблицаПроверяемыеТовары.КоличествоУпаковок КАК КоличествоУпаковок,
		|		ТаблицаПроверяемыеТовары.Количество КАК Количество,
		|		ТаблицаПроверяемыеТовары.Сумма КАК Сумма,
		|		ТаблицаПроверяемыеТовары.СуммаНДС КАК СуммаНДС
		|	ИЗ
		|		ЧекиККМ КАК ЧекиККМ
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента.Товары КАК ТаблицаПроверяемыеТовары
		|			ПО ЧекиККМ.ДокументРеализацииПередачи = ТаблицаПроверяемыеТовары.Ссылка.ЧекККМ
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументовСторноИсправление
		|			ПО ТаблицаПроверяемыеТовары.Ссылка = РеестрДокументовСторноИсправление.СторнируемыйДокумент
		|				И РеестрДокументовСторноИсправление.Проведен
		|				И НЕ РеестрДокументовСторноИсправление.ДополнительнаяЗапись
		|	ГДЕ
		|		ТаблицаПроверяемыеТовары.Ссылка.Проведен
		|		И ТаблицаПроверяемыеТовары.Ссылка <> &ДокументВозврата
		|		И РеестрДокументовСторноИсправление.Ссылка ЕСТЬ NULL) КАК ОбъединеннаяТаблица
		|
		|
		|СГРУППИРОВАТЬ ПО
		|	ОбъединеннаяТаблица.ДокументРеализацииПередачи,
		|	ОбъединеннаяТаблица.НоменклатураНабора,
		|	ОбъединеннаяТаблица.ХарактеристикаНабора,
		|	ОбъединеннаяТаблица.Номенклатура,
		|	ОбъединеннаяТаблица.Характеристика,
		|	ОбъединеннаяТаблица.Упаковка,
		|	ОбъединеннаяТаблица.Серия,
		|	ОбъединеннаяТаблица.СтавкаНДС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСТЬNULL(ЧекиККМ.ДокументРеализацииПередачи, ДанныеВозврата.ДокументРеализацииПередачи) КАК ДокументРеализацииПередачи,
		|	ЕСТЬNULL(ЧекиККМ.Номенклатура, ДанныеВозврата.Номенклатура) КАК Номенклатура,
		|	ЕСТЬNULL(ЧекиККМ.Характеристика, ДанныеВозврата.Характеристика) КАК Характеристика,
		|	ЕСТЬNULL(ЧекиККМ.НоменклатураНабора, ДанныеВозврата.НоменклатураНабора) КАК НоменклатураНабора,
		|	ЕСТЬNULL(ЧекиККМ.ХарактеристикаНабора, ДанныеВозврата.ХарактеристикаНабора) КАК ХарактеристикаНабора,
		|	ЕСТЬNULL(ЧекиККМ.Серия, ДанныеВозврата.Серия) КАК Серия,
		|	ЕСТЬNULL(ЧекиККМ.Упаковка, ДанныеВозврата.Упаковка) КАК Упаковка,
		|	ЕСТЬNULL(ЧекиККМ.СтавкаНДС, ДанныеВозврата.СтавкаНДС) КАК СтавкаНДС,
		|	0 КАК КоличествоУпаковок,
		|	ЕСТЬNULL(ЧекиККМ.Количество, 0) - ЕСТЬNULL(ДанныеВозврата.Количество, 0) КАК Количество,
		|	ЕСТЬNULL(ЧекиККМ.Сумма, 0) - ЕСТЬNULL(ДанныеВозврата.Сумма, 0) КАК Сумма,
		|	ЕСТЬNULL(ЧекиККМ.СуммаНДС, 0) - ЕСТЬNULL(ДанныеВозврата.СуммаНДС, 0) КАК СуммаНДС
		|ПОМЕСТИТЬ ДанныеДокументовРеализацииВозврата
		|ИЗ
		|	ЧекиККМ КАК ЧекиККМ
		|		ПОЛНОЕ СОЕДИНЕНИЕ ДанныеВозврата КАК ДанныеВозврата
		|		ПО ЧекиККМ.ДокументРеализацииПередачи = ДанныеВозврата.ДокументРеализацииПередачи
		|			И ЧекиККМ.Номенклатура = ДанныеВозврата.Номенклатура
		|			И ЧекиККМ.Характеристика = ДанныеВозврата.Характеристика
		|			И ЧекиККМ.НоменклатураНабора = ДанныеВозврата.НоменклатураНабора
		|			И ЧекиККМ.ХарактеристикаНабора = ДанныеВозврата.ХарактеристикаНабора
		|			И ЧекиККМ.Серия = ДанныеВозврата.Серия
		|			И ЧекиККМ.Упаковка = ДанныеВозврата.Упаковка
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеДокументовРеализацииВозврата.Количество КАК Количество,
		|	ВариантыКомплектацииНоменклатуры.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
		|	ВЫБОР
		|		КОГДА ДанныеДокументовРеализацииВозврата.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ИндексНабора,
		|	ДанныеДокументовРеализацииВозврата.НоменклатураНабора КАК НоменклатураНабора,
		|	ДанныеДокументовРеализацииВозврата.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|	ДанныеДокументовРеализацииВозврата.Номенклатура КАК Номенклатура,
		|	ДанныеДокументовРеализацииВозврата.Характеристика КАК Характеристика,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ДанныеДокументовРеализацииВозврата.Серия КАК Серия,
		|	ДанныеДокументовРеализацииВозврата.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ДанныеДокументовРеализацииВозврата.Упаковка КАК Упаковка,
		|	ДанныеДокументовРеализацииВозврата.Сумма КАК Сумма,
		|	ДанныеДокументовРеализацииВозврата.СуммаНДС КАК СуммаНДС,
		|	ДанныеДокументовРеализацииВозврата.ДокументРеализацииПередачи.Склад КАК Склад,
		|	ДанныеДокументовРеализацииВозврата.ДокументРеализацииПередачи.Дата КАК ДатаРеализацииПередачи,
		|	ДанныеДокументовРеализацииВозврата.ДокументРеализацииПередачи.Номер КАК НомерРеализацииПередачи,
		|	ДанныеДокументовРеализацииВозврата.СтавкаНДС КАК СтавкаНДС,
		|	ДанныеДокументовРеализацииВозврата.ДокументРеализацииПередачи КАК ДокументРеализацииПередачи,
		|	ДанныеДокументовРеализацииВозврата.ДокументРеализацииПередачи.Валюта КАК ВалютаРеализации,
		|	ДанныеДокументовРеализацииВозврата.ДокументРеализацииПередачи.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
		|	ДанныеДокументовРеализацииВозврата.ДокументРеализацииПередачи.Партнер КАК Партнер,
		|	0 КАК СуммаСНДС
		|ИЗ
		|	ДанныеДокументовРеализацииВозврата КАК ДанныеДокументовРеализацииВозврата
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
		|			ПО ВариантыКомплектацииНоменклатуры.Владелец = ДанныеДокументовРеализацииВозврата.НоменклатураНабора
		|				И ВариантыКомплектацииНоменклатуры.Характеристика = ДанныеДокументовРеализацииВозврата.ХарактеристикаНабора
		|				И ВариантыКомплектацииНоменклатуры.Основной
		|ГДЕ
		|	ДанныеДокументовРеализацииВозврата.Номенклатура.ТипНоменклатуры В
		|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаРеализацииПередачи УБЫВ";
		
		Запрос.УстановитьПараметр("Партнер", Партнер);
		Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
		Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
		Запрос.УстановитьПараметр("НалогообложениеНДС", НалогообложениеНДС);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("ЧекККМ", ЧекККМ);
		Запрос.УстановитьПараметр("ДокументВозврата", ДокументВозврата);
		
	ИначеЕсли ВозвратМеждуОрганизациями Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаДокумента.Ссылка            КАК ДокументРеализацииПередачи,
		|	ТаблицаДокумента.Ссылка.Склад      КАК Склад,
		|	ТаблицаДокумента.Ссылка.Дата       КАК ДатаРеализацииПередачи,
		|	ТаблицаДокумента.Ссылка.Номер      КАК НомерРеализацииПередачи,
		|	ТаблицаДокумента.Ссылка.Валюта     КАК ВалютаРеализации,
		|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
		|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Серия          КАК Серия,
		|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Назначение     КАК Назначение,
		|	ТаблицаДокумента.Упаковка          КАК Упаковка,
		|	СУММА(ТаблицаДокумента.Количество) КАК Количество,
		|	СУММА(ТаблицаДокумента.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	ТаблицаДокумента.Ссылка.ЦенаВключаетНДС    КАК ЦенаВключаетНДС,
		|	МАКСИМУМ(ТаблицаДокумента.СуммаСНДС
		|		/ ТаблицаДокумента.Количество) КАК Цена,
		|	СУММА(ТаблицаДокумента.СуммаСНДС)  КАК Сумма,
		|	ТаблицаДокумента.СтавкаНДС         КАК СтавкаНДС,
		|	СУММА(ТаблицаДокумента.СуммаНДС)   КАК СуммаНДС,
		|	СУММА(ТаблицаДокумента.СуммаСНДС)  КАК СуммаСНДС
		|ПОМЕСТИТЬ ВТТоварыПередач
		|ИЗ
		|	Документ.ПередачаТоваровМеждуОрганизациями.ВидыЗапасов КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка.Проведен
		|	И ТаблицаДокумента.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ТаблицаДокумента.Ссылка.Склад = &Склад
		|	И ТаблицаДокумента.Ссылка.Договор = &Договор
		|	И ТаблицаДокумента.Ссылка.Организация = &Организация
		|	И ТаблицаДокумента.Ссылка.ОрганизацияПолучатель = &Партнер
		|	И ВЫБОР
		|			КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссиюВДругуюОрганизацию)
		|				ТОГДА ТаблицаДокумента.Ссылка.НалогообложениеНДС = &НалогообложениеНДС
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И ТаблицаДокумента.Ссылка.ХозяйственнаяОперация = &ХозяйственнаяОперация
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаДокумента.Ссылка,
		|	ТаблицаДокумента.Ссылка.Склад,
		|	ТаблицаДокумента.Ссылка.Дата,
		|	ТаблицаДокумента.Ссылка.Номер,
		|	ТаблицаДокумента.Ссылка.Валюта,
		|	ТаблицаДокумента.АналитикаУчетаНоменклатуры,
		|	ТаблицаДокумента.Упаковка,
		|	ТаблицаДокумента.Ссылка.ЦенаВключаетНДС,
		|	ТаблицаДокумента.СтавкаНДС
		|
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаДокумента.ДокументПередачи                  КАК ДокументРеализацииПередачи,
		|	ТаблицаДокумента.Номенклатура                      КАК Номенклатура,
		|	ТаблицаДокумента.Характеристика                    КАК Характеристика,
		|	ТаблицаДокумента.Серия                             КАК Серия,
		|	ТаблицаДокумента.Назначение                        КАК Назначение,
		|	ТаблицаДокумента.Упаковка                          КАК Упаковка,
		|	ТаблицаДокумента.СтавкаНДС                         КАК СтавкаНДС,
		|	ТаблицаДокумента.ДокументПередачи.Склад            КАК Склад,
		|	ТаблицаДокумента.ДокументПередачи.Дата             КАК ДатаРеализацииПередачи,
		|	ТаблицаДокумента.ДокументПередачи.Номер            КАК НомерРеализацииПередачи,
		|	ТаблицаДокумента.ДокументПередачи.Валюта           КАК ВалютаРеализации,
		|	ТаблицаДокумента.ДокументПередачи.ЦенаВключаетНДС  КАК ЦенаВключаетНДС,
		|	СУММА(ТаблицаДокумента.Количество)                 КАК Количество,
		|	СУММА(ТаблицаДокумента.СуммаСНДС)                  КАК Сумма,
		|	СУММА(ТаблицаДокумента.СуммаНДС)                   КАК СуммаНДС,
		|	СУММА(ТаблицаДокумента.СуммаСНДС)                  КАК СуммаСНДС
		|ПОМЕСТИТЬ ВТВозвраты
		|ИЗ
		|	Документ.ВозвратТоваровМеждуОрганизациями.Товары КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка.Проведен
		|	И ТаблицаДокумента.Ссылка <> &ДокументВозврата
		|	И ТаблицаДокумента.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ТаблицаДокумента.Ссылка.Склад = &Склад
		|	И ТаблицаДокумента.Ссылка.Договор = &Договор
		|	И ТаблицаДокумента.Ссылка.Организация = &Партнер
		|	И ТаблицаДокумента.Ссылка.ОрганизацияПолучатель = &Организация
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаДокумента.ДокументПередачи,
		|	ТаблицаДокумента.Номенклатура,
		|	ТаблицаДокумента.Характеристика,
		|	ТаблицаДокумента.Серия,
		|	ТаблицаДокумента.Назначение,
		|	ТаблицаДокумента.СтавкаНДС,
		|	ТаблицаДокумента.Упаковка
		|
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ТаблицаДокумента.ДокументРеализацииПередачи, ТаблицаВозвратов.ДокументРеализацииПередачи) КАК ДокументРеализацииПередачи,
		|	ЕСТЬNULL(ТаблицаДокумента.Склад, ТаблицаВозвратов.Склад) КАК Склад,
		|	ЕСТЬNULL(ТаблицаДокумента.ДатаРеализацииПередачи, ТаблицаВозвратов.ДатаРеализацииПередачи) КАК ДатаРеализацииПередачи,
		|	ЕСТЬNULL(ТаблицаДокумента.НомерРеализацииПередачи, ТаблицаВозвратов.НомерРеализацииПередачи) КАК НомерРеализацииПередачи,
		|	ЕСТЬNULL(ТаблицаДокумента.ВалютаРеализации, ТаблицаВозвратов.ВалютаРеализации) КАК ВалютаРеализации,
		|	ЕСТЬNULL(ТаблицаДокумента.Номенклатура, ТаблицаВозвратов.Номенклатура) КАК Номенклатура,
		|	ЕСТЬNULL(ТаблицаДокумента.Характеристика, ТаблицаВозвратов.Характеристика) КАК Характеристика,
		|	ЕСТЬNULL(ТаблицаДокумента.Серия, ТаблицаВозвратов.Серия) КАК Серия,
		|	ЕСТЬNULL(ТаблицаДокумента.Назначение, ТаблицаВозвратов.Назначение) КАК Назначение,
		|	ЕСТЬNULL(ТаблицаДокумента.Упаковка, ТаблицаВозвратов.Упаковка) КАК Упаковка,
		|	ЕСТЬNULL(ТаблицаДокумента.ЦенаВключаетНДС, ТаблицаВозвратов.ЦенаВключаетНДС) КАК ЦенаВключаетНДС,
		|	ЕСТЬNULL(ТаблицаДокумента.СтавкаНДС, ТаблицаВозвратов.СтавкаНДС) КАК СтавкаНДС,
		|	0 КоличествоУпаковок,
		|	ЕСТЬNULL(ТаблицаДокумента.Количество, 0) - ЕСТЬNULL(ТаблицаВозвратов.Количество, 0) КАК Количество,
		|	ЕСТЬNULL(ТаблицаДокумента.Сумма, 0) - ЕСТЬNULL(ТаблицаВозвратов.Сумма, 0) КАК Сумма,
		|	ЕСТЬNULL(ТаблицаДокумента.СуммаНДС, 0) - ЕСТЬNULL(ТаблицаВозвратов.СуммаНДС, 0) КАК СуммаНДС,
		|	ЕСТЬNULL(ТаблицаДокумента.СуммаСНДС, 0) - ЕСТЬNULL(ТаблицаВозвратов.СуммаСНДС, 0) КАК СуммаСНДС
		|ИЗ
		|	ВТТоварыПередач КАК ТаблицаДокумента
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТВозвраты КАК ТаблицаВозвратов
		|		ПО ТаблицаДокумента.ДокументРеализацииПередачи = ТаблицаВозвратов.ДокументРеализацииПередачи
		|			И ТаблицаДокумента.Номенклатура            = ТаблицаВозвратов.Номенклатура
		|			И ТаблицаДокумента.Характеристика          = ТаблицаВозвратов.Характеристика
		|			И ТаблицаДокумента.Серия                   = ТаблицаВозвратов.Серия
		|			И ТаблицаДокумента.Назначение              = ТаблицаВозвратов.Назначение
		|			И ТаблицаДокумента.Упаковка                = ТаблицаВозвратов.Упаковка
		|ГДЕ
		|	ТаблицаДокумента.Номенклатура.ТипНоменклатуры В
		|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|УПОРЯДОЧИТЬ ПО
		|	ДатаРеализацииПередачи УБЫВ";
		
		Запрос.УстановитьПараметр("ДокументВозврата", ДокументВозврата);
		Запрос.УстановитьПараметр("Партнер", Партнер);
		Запрос.УстановитьПараметр("Договор", Договор);
		Запрос.УстановитьПараметр("Склад", Склад);
		Запрос.УстановитьПараметр("Контрагент", Контрагент);
		Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
		Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
		Запрос.УстановитьПараметр("НалогообложениеНДС", НалогообложениеНДС);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("ХозяйственнаяОперация", ХозяйственнаяОперация);
		
	КонецЕсли;
	
	ТаблицаТоваровВыгрузка = Запрос.Выполнить().Выгрузить();
	СтруктураПоиска = Новый Структура("Номенклатура, Характеристика, 
			|Серия, Назначение, СтавкаНДС, Склад, ДокументРеализацииПередачи");
	
	Для Каждого Строка Из ТаблицаТоваровВыгрузка Цикл
		
		Если Строка.Количество < 0 Тогда
		
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, Строка);
			СтрокиПродажи = ТаблицаТоваровВыгрузка.НайтиСтроки(СтруктураПоиска);
			
			Для Каждого СтрокаПродажи Из СтрокиПродажи Цикл
			
				Если СтрокаПродажи.Количество < 0 Тогда
					Продолжить;
				КонецЕсли; 
				
				Строка.Количество 		= -Строка.Количество;
				Строка.Сумма 			= -Строка.Сумма;
				Строка.СуммаНДС 		= -Строка.СуммаНДС;
				Строка.СуммаСНДС 		= -Строка.СуммаСНДС;
				
				Если Строка.Количество > СтрокаПродажи.Количество Тогда
					КоличествоНаВычет 	= СтрокаПродажи.Количество;
					СуммаНаВычет 		= СтрокаПродажи.Сумма;
					СуммаНДСНаВычет 	= СтрокаПродажи.СуммаНДС;
					СуммаСНДСНаВычет 	= СтрокаПродажи.СуммаСНДС;
				Иначе
					КоличествоНаВычет 	= Строка.Количество;
					СуммаНаВычет 		= Строка.Сумма;
					СуммаНДСНаВычет 	= Строка.СуммаНДС;
					СуммаСНДСНаВычет 	= Строка.СуммаСНДС;
				КонецЕсли;
				
				СтрокаПродажи.Количество 	= СтрокаПродажи.Количество - КоличествоНаВычет;
				Строка.Количество 			= Строка.Количество - КоличествоНаВычет;
				
				СтрокаПродажи.Сумма 		= СтрокаПродажи.Сумма - СуммаНаВычет;
				Строка.Сумма 				= Строка.Сумма - СуммаНаВычет;
				
				СтрокаПродажи.СуммаНДС 		= СтрокаПродажи.СуммаНДС - СуммаНДСНаВычет;
				Строка.СуммаНДС 			= Строка.СуммаНДС - СуммаНДСНаВычет;
				
				СтрокаПродажи.СуммаСНДС 	= СтрокаПродажи.СуммаСНДС - СуммаСНДСНаВычет;
				Строка.СуммаСНДС 			= Строка.СуммаСНДС - СуммаСНДСНаВычет;
			
			КонецЦикла; 
		
		КонецЕсли; 
	
	КонецЦикла; 
	
	МассивУдаляемыхСтрок = Новый Массив();
	Для Каждого Строка Из ТаблицаТоваровВыгрузка Цикл 
		Если Строка.Количество <= 0 Тогда
			МассивУдаляемыхСтрок.Добавить(Строка);
		КонецЕсли;
	КонецЦикла;
	Для Каждого Строка Из МассивУдаляемыхСтрок Цикл
		ТаблицаТоваровВыгрузка.Удалить(Строка);
	КонецЦикла;
	
	БазоваяВалюта = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
	
	КурсНовойВалюты      = РаботаСКурсамиВалютУТ.ПолучитьКурсВалюты(Валюта, Дата, БазоваяВалюта);
	ПараметрыНовогоКурса = Новый Структура("Валюта, КурсЧислитель, КурсЗнаменатель",
											Валюта, КурсНовойВалюты.КурсЧислитель, КурсНовойВалюты.КурсЗнаменатель);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
	
	Для каждого Строка Из ТаблицаТоваровВыгрузка Цикл
	
		НоваяСтрока = ТаблицаТоваров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействий, Неопределено);
		
		Если ВозвратОтРозничногоПокупателя Тогда
			НоваяСтрока.СуммаСНДС = НоваяСтрока.Сумма + ?(ЦенаВключаетНДС, 0, НоваяСтрока.СуммаНДС);
		КонецЕсли;
		
		Если Валюта <> Строка.ВалютаРеализации Тогда
			КурсСтаройВалюты       = РаботаСКурсамиВалютУТ.ПолучитьКурсВалюты(Строка.ВалютаРеализации, Дата, БазоваяВалюта);
			ПараметрыТекущегоКурса = Новый Структура("Валюта, КурсЧислитель, КурсЗнаменатель",
													Строка.ВалютаРеализации, КурсСтаройВалюты.КурсЧислитель, КурсСтаройВалюты.КурсЗнаменатель);
			
			НоваяСтрока.СуммаСНДС = РаботаСКурсамиВалютУТКлиентСервер.ПересчитатьПоКурсу(НоваяСтрока.СуммаСНДС,
																						ПараметрыТекущегоКурса,
																						ПараметрыНовогоКурса);
		КонецЕсли;
		
		Ценообразование.ПересчитатьСуммыВСтрокеПоСуммеСНДС(НоваяСтрока, ЦенаВключаетНДС, Ложь, Ложь, Истина);
	
	КонецЦикла; 
	
	ЗаполнитьНомераГТДПоРаспоряжениям();
	
	ТаблицаТоваров.Сортировать("ДатаРеализацииПередачи,НомерРеализацииПередачи,Номенклатура,Характеристика");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуТары()
	
	ТаблицаТары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПереданнаяВозвратнаяТараОстатки.Номенклатура      КАК Номенклатура,
	|	ПереданнаяВозвратнаяТараОстатки.Характеристика    КАК Характеристика,
	|	ПереданнаяВозвратнаяТараОстатки.Партнер           КАК Партнер,
	|	ПереданнаяВозвратнаяТараОстатки.ДокументПередачи  КАК ДокументПередачи,
	|	ПереданнаяВозвратнаяТараОстатки.КоличествоОстаток КАК Количество,
	|	ПереданнаяВозвратнаяТараОстатки.СуммаОстаток      КАК Сумма
	|ПОМЕСТИТЬ
	|	втПереданнаяВозвратнаяТара
	|ИЗ
	|	РегистрНакопления.ПереданнаяВозвратнаяТара.Остатки(
	|			,
	|			Партнер = &Партнер
	|			И ДокументПередачи.Организация = &Организация
	|			И ДокументПередачи.Контрагент = &Контрагент
	|			И ДокументПередачи.КлиентКонтрагент = &КлиентКонтрагент
	|			И ДокументПередачи.КлиентПартнер = &КлиентПартнер
	|			И ДокументПередачи.Организация = &Организация
	|			И ВЫБОР
	|				КОГДА НЕ &ИспользоватьСоглашенияСКлиентами
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ДокументПередачи.Соглашение = &Соглашение
	|			КОНЕЦ
	|			И ВЫБОР
	|				КОГДА НЕ &ИспользоватьДоговорыСКлиентами
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ДокументПередачи.Договор = &Договор
	|			КОНЕЦ
	|			И ДокументПередачи.Валюта = &Валюта
	|			И ДокументПередачи.НалогообложениеНДС = &НалогообложениеНДС
	|			И ДокументПередачи.ТребуетсяЗалогЗаТару = &ПредусмотренЗалогЗаТару
	|		) КАК ПереданнаяВозвратнаяТараОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПереданнаяВозвратнаяТараДвижения.Номенклатура     КАК Номенклатура,
	|	ПереданнаяВозвратнаяТараДвижения.Характеристика   КАК Характеристика,
	|	ПереданнаяВозвратнаяТараДвижения.Партнер          КАК Партнер,
	|	ПереданнаяВозвратнаяТараДвижения.ДокументПередачи КАК ДокументПередачи,
	|	ВЫБОР КОГДА ПереданнаяВозвратнаяТараДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			-ПереданнаяВозвратнаяТараДвижения.Количество
	|		ИНАЧЕ
	|			ПереданнаяВозвратнаяТараДвижения.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР КОГДА ПереданнаяВозвратнаяТараДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			-ПереданнаяВозвратнаяТараДвижения.Сумма
	|		ИНАЧЕ
	|			ПереданнаяВозвратнаяТараДвижения.Сумма
	|	КОНЕЦ КАК Сумма
	|ИЗ
	|	РегистрНакопления.ПереданнаяВозвратнаяТара КАК ПереданнаяВозвратнаяТараДвижения
	|ГДЕ
	|	ПереданнаяВозвратнаяТараДвижения.Регистратор = &ДокументВозврата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втПереданнаяВозвратнаяТара.Номенклатура           КАК Номенклатура,
	|	втПереданнаяВозвратнаяТара.Характеристика         КАК Характеристика,
	|	втПереданнаяВозвратнаяТара.Партнер                КАК Партнер,
	|	втПереданнаяВозвратнаяТара.ДокументПередачи       КАК ДокументРеализацииПередачи,
	|	втПереданнаяВозвратнаяТара.ДокументПередачи.Номер КАК НомерРеализацииПередачи,
	|	втПереданнаяВозвратнаяТара.ДокументПередачи.Дата  КАК ДатаРеализацииПередачи,
	|	втПереданнаяВозвратнаяТара.ДокументПередачи.ДатаВозвратаМногооборотнойТары КАК ДатаВозврата,
	|	СУММА(втПереданнаяВозвратнаяТара.Количество)      КАК Количество,
	|	СУММА(втПереданнаяВозвратнаяТара.Сумма)           КАК Сумма,
	|	ВЫБОР
	|		КОГДА
	|			СУММА(втПереданнаяВозвратнаяТара.Количество) = 0
	|		ТОГДА
	|			СУММА(втПереданнаяВозвратнаяТара.Сумма)
	|		ИНАЧЕ
	|			СУММА(втПереданнаяВозвратнаяТара.Сумма) / СУММА(втПереданнаяВозвратнаяТара.Количество)
	|	КОНЕЦ КАК Цена
	|ИЗ
	|	втПереданнаяВозвратнаяТара
	|СГРУППИРОВАТЬ ПО
	|	втПереданнаяВозвратнаяТара.Номенклатура,
	|	втПереданнаяВозвратнаяТара.Характеристика,
	|	втПереданнаяВозвратнаяТара.Партнер,
	|	втПереданнаяВозвратнаяТара.ДокументПередачи
	|ИМЕЮЩИЕ
	|	СУММА(втПереданнаяВозвратнаяТара.Количество) > 0
	|УПОРЯДОЧИТЬ ПО
	|	ДатаВозврата ВОЗР,
	|	Номенклатура.Наименование ВОЗР,
	|	Характеристика.Наименование ВОЗР
	|";

	Запрос.УстановитьПараметр("Партнер",                          Партнер);
	Запрос.УстановитьПараметр("КлиентПартнер",                    КлиентПартнер);
	Запрос.УстановитьПараметр("Контрагент",                       Контрагент);
	Запрос.УстановитьПараметр("КлиентКонтрагент",                 КлиентКонтрагент);
	Запрос.УстановитьПараметр("Организация",                      Организация);
	Запрос.УстановитьПараметр("Соглашение",                       Соглашение);
	Запрос.УстановитьПараметр("Договор",                          Договор);
	Запрос.УстановитьПараметр("Валюта",                           Валюта);
	Запрос.УстановитьПараметр("НалогообложениеНДС",               НалогообложениеНДС);
	Запрос.УстановитьПараметр("ДокументВозврата",                 ДокументВозврата);
	Запрос.УстановитьПараметр("ИспользоватьСоглашенияСКлиентами", ИспользоватьСоглашенияСКлиентами);
	Запрос.УстановитьПараметр("ИспользоватьДоговорыСКлиентами",   ИспользоватьДоговорыСКлиентами);
	Запрос.УстановитьПараметр("ПредусмотренЗалогЗаТару",          ПредусмотренЗалогЗаТару);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаТары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтогиПоТаблицеТоваров()
	
	ТоварыВыбраноКоличество = 0;
	ТоварыВыбраноСумма = 0;
	
	Для НомерСтроки=0 По ТаблицаТоваров.Количество()-1 Цикл
		
		Если ТаблицаТоваров[НомерСтроки].Выбран Тогда
			ТоварыВыбраноКоличество = ТоварыВыбраноКоличество + 1;
			ТоварыВыбраноСумма = ТоварыВыбраноСумма + ТаблицаТоваров[НомерСтроки].СуммаСНДС;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтогиПоТаблицеТары()
	
	ТараВыбраноКоличество = 0;
	ТараВыбраноСумма = 0;
	
	Для НомерСтроки=0 По ТаблицаТары.Количество()-1 Цикл
		
		Если ТаблицаТары[НомерСтроки].Выбран Тогда
			ТараВыбраноКоличество = ТараВыбраноКоличество + 1;
			ТараВыбраноСумма = ТараВыбраноСумма + ТаблицаТары[НомерСтроки].Сумма;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьВсеТоварыНаСервере()
	
	Для Каждого СтрокаТаблицы Из ТаблицаТоваров Цикл
		СтрокаТаблицы.Выбран = Истина;
	КонецЦикла;
	
	ТоварыВыбраноКоличество = ТаблицаТоваров.Количество();
	ТоварыВыбраноСумма = ТаблицаТоваров.Итог("СуммаСНДС");
	
КонецПроцедуры

&НаСервере
Процедура ИсключитьВсеТоварыНаСервере()
	
	Для Каждого СтрокаТаблицы Из ТаблицаТоваров Цикл
		СтрокаТаблицы.Выбран = Ложь;
	КонецЦикла;
	
	ТоварыВыбраноКоличество = 0;
	ТоварыВыбраноСумма = 0;
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьВсюТаруНаСервере()
	
	Для Каждого СтрокаТаблицы Из ТаблицаТары Цикл
		СтрокаТаблицы.Выбран = Истина;
	КонецЦикла;
	
	ТараВыбраноКоличество = ТаблицаТары.Количество();
	ТараВыбраноСумма = ТаблицаТары.Итог("Сумма");
	
КонецПроцедуры

&НаСервере
Процедура ИсключитьВсюТаруНаСервере()
	
	Для Каждого СтрокаТаблицы Из ТаблицаТары Цикл
		СтрокаТаблицы.Выбран = Ложь;
	КонецЦикла;
	
	ТараВыбраноКоличество = 0;
	ТараВыбраноСумма = 0;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьВозможностьПереносаЧекаККМ()
	
	Если ВозвратОтРозничногоПокупателя Тогда
		ТаблицаОснований = ТаблицаТоваров.Выгрузить(ТаблицаТоваров.НайтиСтроки(Новый Структура("Выбран", Истина)));
		ТаблицаОснований.Свернуть("ДокументРеализацииПередачи");
		Если ТаблицаОснований.Количество() <= 1 Тогда
			Возврат Истина
		Иначе
			Возврат Ложь
		КонецЕсли;
	Иначе
		Возврат Истина
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьКарточкуНоменклатуры()

	ТекущиеДанные = Элементы.ТаблицаТоваров.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		ПараметрыФормы = Новый Структура("Ключ", ТекущиеДанные.Номенклатура);
		ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаЭлемента", ПараметрыФормы);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНомераГТДПоРаспоряжениям()
	
	ПараметрыЗаполнения = ЗакупкиСервер.ПараметрыЗаполненияНомеровГТДПоУчетнымДанным();
	ПараметрыЗапроса = ПараметрыЗаполнения.ПараметрыЗапроса;
	ПараметрыЗапроса.Ссылка = ДокументВозврата;
	
	ПараметрыПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(ЭтотОбъект);
	
	ДействияСоСтрокой = Новый Структура;
	ДействияСоСтрокой.Вставить("ПересчитатьСумму");
	ДействияСоСтрокой.Вставить("ПересчитатьСуммуНДС",	ПараметрыПересчетаСуммы);
	ДействияСоСтрокой.Вставить("ПересчитатьСуммуСНДС",	ПараметрыПересчетаСуммы);
	
	ПараметрыЗаполнения.ДействияСоСтрокой	= ДействияСоСтрокой;
	ПараметрыЗаполнения.ИмяПоляРаспоряжение	= "ДокументРеализацииПередачи";
	
	ЗакупкиСервер.ЗаполнитьНомераГТДПоУчетнымДанным(ТаблицаТоваров, "ПоРаспоряжениям", ПараметрыЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
