
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("СписокОбъектов") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	СписокОбъектов = Параметры.СписокОбъектов;
	ОтобразитьЭлементыСписка();
	СброситьРазмерыИПоложениеОкна();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьНовый(Команда)
	
	Закрыть(КодВозвратаДиалога.Да);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтобразитьЭлементыСписка()
	
	Для Каждого ДобавляемаяСсылка Из СписокОбъектов Цикл
		
		Идентификатор = ДобавляемаяСсылка.ПолучитьИдентификатор();
		
		Элемент = Элементы.Добавить("Ссылка_" + Идентификатор, Тип("ПолеФормы"), Элементы.ГруппаСписокДокументов);
		Элемент.ПутьКДанным = "СписокОбъектов[" + Идентификатор + "].Значение";
		Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элемент.Гиперссылка = Истина;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СброситьРазмерыИПоложениеОкна()
	
	ИмяПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		ПолноеИмяФормы = Метаданные.ОбщиеФормы.ВопросПриНаличииИмеющихсяДокументовБРД.ПолноеИмя();
		ХранилищеСистемныхНастроек.Удалить(ПолноеИмяФормы, "", ИмяПользователя);
	КонецЕсли;
	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти